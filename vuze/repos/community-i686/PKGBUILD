# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=vuze
pkgver=4.6.0.2
pkgrel=2
pkgdesc='BitTorrent client with GUI, written in Java.'
arch=('i686' 'x86_64')
url='http://www.vuze.com/'
license=('GPL')
depends=('java-runtime')
optdepends=('xulrunner: needed for browsing the vuze content')
source=("http://downloads.sourceforge.net/project/azureus/vuze/Vuze_${pkgver//./}/Vuze_${pkgver//./}_linux.tar.bz2"
        'vuze.desktop')
md5sums=('af80c418f0bcc718941762485c916914'
         '345ad45b972009dcd672c4eaf52fb336')
[[ $CARCH == 'x86_64' ]] && source[0]="http://downloads.sourceforge.net/project/azureus/vuze/Vuze_${pkgver//./}/Vuze_${pkgver//./}_linux-x86_64.tar.bz2" && md5sums[0]='22a9c2077cb686fbc00b09dc281fe5a1'

package() {
  install -d ${pkgdir}/usr/share
  cp -r vuze ${pkgdir}/usr/share

  install -Dm755 vuze/vuze ${pkgdir}/usr/bin/vuze
  sed -i 's%#PROGRAM_DIR="/home/username/apps/azureus"%PROGRAM_DIR="/usr/share/vuze"%' ${pkgdir}/usr/bin/vuze

  install -Dm644 vuze/vuze.png ${pkgdir}/usr/share/pixmaps/vuze.png
  install -Dm644 vuze.desktop  ${pkgdir}/usr/share/applications/vuze.desktop
}
