# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vuze
pkgver=4.5.0.4
pkgrel=1
pkgdesc="BitTorrent client with GUI, written in Java, formerly known as Azureus"
arch=('i686' 'x86_64')
url="http://azureus.sourceforge.net/"
license=('GPL')
depends=('java-runtime')
optdepends=('xulrunner: needed for browsing the vuze content')
conflicts=('java-gcj-compat')
source=("http://downloads.sourceforge.net/azureus/Vuze_${pkgver//./}_linux.tar.bz2"
        'vuze.desktop')
md5sums=('951a51e8d64fc65d822cc7328387cb77'
         '345ad45b972009dcd672c4eaf52fb336')
[ "${CARCH}" = "x86_64" ] && source[0]="http://downloads.sourceforge.net/azureus/Vuze_${pkgver//./}_linux-${CARCH}.tar.bz2"
[ "${CARCH}" = "x86_64" ] && md5sums[0]='8a8dac9f9defc6df4ab9f563b7e1a2a1'

package() {
	install -d ${pkgdir}/usr/share
	cp -r vuze ${pkgdir}/usr/share

	install -Dm755 vuze/vuze ${pkgdir}/usr/bin/vuze
	sed -i 's%#PROGRAM_DIR="/home/username/apps/azureus"%PROGRAM_DIR="/usr/share/vuze"%' ${pkgdir}/usr/bin/vuze

	install -Dm644 vuze/vuze.png ${pkgdir}/usr/share/pixmaps/vuze.png
	install -Dm644 vuze.desktop  ${pkgdir}/usr/share/applications/vuze.desktop
}
