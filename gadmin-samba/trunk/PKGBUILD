# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Marcin Kaczorek <kaczus@karek.pl>

pkgname=gadmin-samba
pkgver=0.2.9
pkgrel=1
pkgdesc="An easy to use GTK+ frontend for the SAMBA file and print server"
url="http://gadmintools.flippedweb.com/index.php?option=com_content&task=view&id=16&Itemid=30"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtk2' 'samba')
replaces=('gsambad')
install=gadmin-samba.install
source=("http://mange.dynalias.org/linux/${pkgname}/${pkgname}-${pkgver}.tar.gz" "makefile.patch")
options=(!emptydirs)

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p0 < "${srcdir}/makefile.patch"
  ./configure --prefix=/usr --sysconfdir=/etc \
  --localstatedir=/var --sbindir=/usr/sbin
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 desktop/${pkgname}.desktop "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
md5sums=('9902f6aff533367e5f62caa9fc383d9b'
         '23582cbe1716d3683b96987547fa7724')
