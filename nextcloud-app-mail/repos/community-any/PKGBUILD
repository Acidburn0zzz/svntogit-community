# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-mail
pkgver=0.7.5
pkgrel=1
pkgdesc="An email app for NextCloud"
arch=('any')
url="https://github.com/nextcloud/mail"
license=('AGPL')
depends=('nextcloud')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/mail/releases/download/v${pkgver}/mail.tar.gz")
sha512sums=('4fd3f40303d4efd3bb800660a37bcc324fcb96043b830254151730957587da46b2a5ed77903d127b67cc07879212d81ca4042fa9beea97ad258580f47fa1fedb')

prepare() {
  sed -i 's|max-version="7.0"|max-version="7.1"|' "${srcdir}/mail/appinfo/info.xml"
}

package() {
  install -d "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -a "${srcdir}/mail" "${pkgdir}/usr/share/webapps/nextcloud/apps/mail"
}
