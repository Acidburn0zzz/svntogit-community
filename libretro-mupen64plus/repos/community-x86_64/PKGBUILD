# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Thomas Schneider <maxmusterm@gmail.com>

pkgname=libretro-mupen64plus
pkgver=680
pkgrel=1
epoch=1
pkgdesc='Nintendo 64 core'
arch=(x86_64)
url='https://github.com/libretro/mupen64plus-libretro'
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  mesa
)
_commit='a4fbedf5c30db1761812627cbb4923cef8bb92b4'
source=(libretro-mupen64plus::git+https://github.com/libretro/mupen64plus-libretro.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-mupen64plus

  git rev-list --count HEAD
}

build() {
  cd libretro-mupen64plus

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-mupen64plus

  install -Dm 644 mupen64plus_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
