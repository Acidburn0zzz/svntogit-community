# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgname=python-zope-component
pkgver=4.6.1
pkgrel=1
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
depends=('python-setuptools' 'python-zope-deferredimport' 'python-zope-deprecation'
         'python-zope-event' 'python-zope-hookable' 'python-zope-interface')
checkdepends=('python-zope-testrunner' 'python-persistent' 'python-zope-location'
              'python-zope-proxy' 'python-zope-security' 'python-zope-configuration' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.component/archive/$pkgver.tar.gz")
sha512sums=('3aa77b11e49a0a08a6b38fce5c26761bfa3b417f12fb27583efa22bc8c1e055993ada50c28271c0deace93fe6f8078f0596158884de6576e8b8ec947524b583b')

build() {
  cd "$srcdir"/zope.component-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.component-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.component-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
