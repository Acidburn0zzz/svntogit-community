# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=hub
pkgver=2.2.5
pkgrel=1

pkgdesc="cli interface for Github"
url="https://hub.github.com"
arch=('x86_64' 'i686')
license=('MIT')

depends=('git')
makedepends=('go')

_source_dir=github-hub-7988c4b
source=("$pkgname-$pkgver.tar.gz::https://github.com/github/hub/tarball/v$pkgver")
sha256sums=('daa3aa13db40342365e28a43fc4642eda8001736f7c43d4c2ac8f544a6ca20d0')

build() {
  cd $_source_dir

  ./script/build
  gzip --best -c man/hub.1> hub.1.gz

}

package() {
  cd $_source_dir

  install -Dm755 bin/hub "$pkgdir"/usr/bin/hub

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 etc/hub.bash_completion.sh "$pkgdir"/usr/share/bash-completion/completions/hub
  install -Dm644 etc/hub.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_hub
  install -Dm644 man/hub.1 "$pkgdir"/usr/share/man/man1/hub.1
}
