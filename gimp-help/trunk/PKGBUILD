# $Id$
# Maintainer:
# Contributor: Piotr Rogo≈ºa <rogoza dot piotr at gmail dot com>

pkgbase=gimp-help
pkgver=2.8.1
pkgrel=3
arch=('any')
url='http://docs.gimp.org/'
license=('GPL' 'LGPL')
makedepends=('python2' 'docbook-xsl')
options=(!strip !zipman)
source=(ftp://ftp.gimp.org/pub/gimp/help/gimp-help-$pkgver.tar.bz2)
md5sums=('7f48c64bcadab9611ce8d3d89d990bb4')

declare -A _languages
_languages=(
  [ca]='Catalan'
  [da]='Danish'
  [de]='German'
  [el]='Greek'
  [en]='English'
  [en_GB]='English (United Kingdom)'
  [es]='Spanish'
  [fr]='French'
  [it]='Italian'
  [ja]='Japanese'
  [ko]='Korean'
  [nl]='Dutch'
  [nn]='Norwegian'
  [pt_BR]='Brazilian Portuguese'
  [ru]='Russian'
  [sl]='Slovenian'
  [sv]='Swedish'
  [zh_CN]='Chinese (simplified)'
)

_package() {
  pkgdesc="${_languages[$1]} help files for Gimp"
  install -dm755 "$pkgdir"/usr/share/gimp/2.0/help/$1
  cp -rL "$srcdir"/$pkgbase-$pkgver/html/$1 \
    "$pkgdir"/usr/share/gimp/2.0/help
}

prepare() {
  cd $pkgbase-$pkgver
  sed -e '1s/python$/python2/' -i tools/xml2po.py
}

build(){
  cd $pkgbase-$pkgver
  ./configure --prefix=/usr --without-gimp
  make
}

for lang in ${!_languages[@]}; do
  _pkgname=gimp-help-$lang

  pkgname+=($_pkgname)
  eval "package_$_pkgname() {
    _package $lang
  }"
done
