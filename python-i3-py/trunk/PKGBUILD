# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: Anntoin Wilkinson <anntoin gmail com>

pkgname=python-i3-py
pkgver=0.6.5
# https://github.com/ziberna/i3-py/issues/22
_commit=27f88a616e9ecc340e7d041d3d00782f8a1964c1
pkgrel=6
pkgdesc="tools for i3 users and developers"
arch=('any')
url='https://github.com/ziberna/i3-py'
license=('GPL3' 'custom:WTFPL2')
depends=('python' 'i3-wm')
makedepends=('git' 'python')
source=("$pkgbase::git+https://github.com/ziberna/i3-py#commit=$_commit"
        'LICENSE::http://www.wtfpl.net/txt/copying/')
sha256sums=('SKIP'
            '0356258391e190dc1d44ea01565cfe627fe44e27dad693a0a54c2483a7b223e5')

build() {
    cd "$pkgbase"
    python setup.py build
}

package() {
    cd "$pkgbase"
    python setup.py install --root="$pkgdir/" --optimize=1

    install -Dm644 "$srcdir"/LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"
    install -Dm644 examples/*.py -t "$pkgdir"/usr/share/"$pkgname"/examples
}

# vim: set et sw=4 sts=4 ft=sh:
