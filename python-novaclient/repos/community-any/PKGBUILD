# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-novaclient
pkgver=15.0.0
pkgrel=1
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-novaclient/archive/$pkgver.tar.gz")
sha512sums=('2cd1d68587288d909e1593a5f9a33fe92ab445325bc9195f382f18f8f1a69a2c68bd4c874db487ec8dfd4a2bc30f2cc8c4d96f543d9104b67b5df42b50f98c2f')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-novaclient-$pkgver/requirements.txt
}

build() {
  cd python-novaclient-$pkgver
  python setup.py build
}

check() {
  cd python-novaclient-$pkgver
  stestr run || warning "Tests failed"
}

package() {
  cd "$srcdir"/python-novaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
