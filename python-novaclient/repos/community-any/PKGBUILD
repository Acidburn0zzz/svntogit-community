# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-novaclient
pkgname=(python-novaclient python2-novaclient)
pkgver=14.1.0
pkgrel=1
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-prettytable' 'python2-prettytable' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-requests' 'python2-requests' 'python-iso8601'
             'python2-iso8601' 'python-oslo-utils' 'python2-oslo-utils' 'python-oslo-i18n'
             'python2-oslo-i18n' 'python-oslo-serialization' 'python2-oslo-serialization')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock'
              'python-ddt' 'python2-ddt')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-novaclient/archive/$pkgver.tar.gz")
sha512sums=('724c363288d70e84e944d03a9f8007ce8dfd4fdc114d64fe021704552320d6f6bfc7ad6fffebda93a08f03d572d994ca16430ff80a81972e367ce86a6c345faf')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-novaclient-$pkgver/requirements.txt
  cp -a python-novaclient-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-novaclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-novaclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-novaclient-$pkgver
  stestr run || warning "Tests failed"

  cd "$srcdir"/python-novaclient-$pkgver-py2
  PYTHON=python2 stestr2 run || warning "Tests failed"
}

package_python-novaclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
           'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
           'python-oslo-serialization')

  cd "$srcdir"/python-novaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-novaclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneauth1'
           'python2-requests' 'python2-iso8601' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-oslo-serialization')

  cd "$srcdir"/python-novaclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/nova{,2}
}

# vim:set ts=2 sw=2 et:
