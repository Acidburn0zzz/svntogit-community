# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.5.1
pkgrel=1
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('libxss'
         'openal'
         'gtk2'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('qt5-tools')
source=("https://github.com/tux3/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('85fd2feb2e2f9af3c7712ea13bffff55e4f7b8f3419016465528b0dde1eabbd710e66dba67b79bb230262fb0ec1bebac9c80d4cf204d4c79d2d398e7633ddbe0')

build() {
  cd $_pkgname-$pkgver
  install -d build
  cd build
  qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO ..
  make
}

package() {
  # executable
  cd $_pkgname-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
