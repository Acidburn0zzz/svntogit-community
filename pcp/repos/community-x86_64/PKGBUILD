# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Iwan Timmer <irtimmer@gmail.com>

pkgname=pcp
pkgver=5.0.3
pkgrel=2
pkgdesc="System performance analysis toolkit"
arch=('x86_64')
url="https://pcp.io"
license=('LGPL')
depends=('python' 'avahi' 'procps-ng')
makedepends=('libmicrohttpd' 'cairo' 'qt5-svg')
optdepends=('libmicrohttpd: support for pmwebd'
            'cairo: support for pmwebd'
            'qt5-svg: support for PCP-GUI and pmchart'
            'perl-xml-tokeparser: support for sar2pcp'
            'perl-date-parse: support for sar2pcp')
source=("https://dl.bintray.com/pcp/source/pcp-$pkgver.src.tar.gz"
        "sysuser.conf"
        "tmpfile.conf")
sha256sums=('23f4022d3a3a2660a7f5051a19bc57fc6b001f8dfb8df8d108bd322a7d6ea1c2'
            '3e21ef7925296f73c91fe85c8dba341ce00366163845dd8993e2813b0ad5e4a4'
            '0166ffea180527de5a48a8e8f145cc80860b93e8f44bc5f96836c3d1cb4b01a2')

build() {
  cd "$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --with-rundir=/run/pcp \
    --without-dstat-symlink
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DIST_ROOT="$pkgdir" PCP_USER=root PCP_GROUP=root install

  install -D -m644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/pcp.conf
  install -D -m644 "$srcdir"/tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/pcp.conf

  rm -vrf "$pkgdir"/var/lib/pcp/testsuite "$pkgdir"/run "$pkgdir"/var/tmp
}
