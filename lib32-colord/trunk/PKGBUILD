# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-colord
pkgver=1.3.5
pkgrel=1
pkgdesc='System daemon for managing color devices'
arch=('x86_64')
url='https://www.freedesktop.org/software/colord'
license=('GPL2')
depends=('colord' 'lib32-dconf' 'lib32-libgudev' 'lib32-libgusb' 'lib32-lcms2'
         'lib32-polkit' 'lib32-sqlite')
makedepends=('argyllcms' 'gcc-multilib' 'gobject-introspection' 'gnome-common'
             'intltool' 'python2' 'sane' 'vala')
source=("https://www.freedesktop.org/software/colord/releases/colord-${pkgver}.tar.xz")
sha256sums=('2daa8ffd2a532d7094927cd1a4af595b8310cea66f7707edcf6ab743460feed2')

build() {
  cd colord-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/colord' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --enable-libcolordcompat \
    --enable-vala \
    --disable-bash-completion \
    --disable-sane \
    --disable-static \
    --with-daemon-user='colord' \
    --with-systemdsystemunitdir='/usr/lib/systemd/system'
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  PATH="${srcdir}/path:$PATH" make
}

package() {
  cd colord-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,var,usr/{bin,include,lib,share}}
}

# vim: ts=2 sw=2 et:
