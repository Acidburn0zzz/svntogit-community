# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: unexist <unexist@dorfelite.net>
# Contributor: Abakus <java5@arcor.de>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=subtle
pkgver=0.11.3224
pkgrel=4
pkgdesc='Grid-based manual tiling window manager'
arch=('x86_64' 'i686')
url='http://subtle.subforge.org/'
license=('GPL')
depends=('ruby' 'libxft' 'libxpm' 'libxrandr' 'libxft' 'libxpm' 'libxinerama'
         'libxtst')
makedepends=('pkg-config' 'libxinerama' 'libxrandr' 'libxft' 'libxpm')
backup=('etc/xdg/subtle/subtle.rb')
install=subtle.install
source=("http://subforge.org/attachments/download/81/$pkgname-$pkgver-xi.tbz2")
sha256sums=('a22ee94a70c1105a018e1c0a754597b4e3a87d1b915301a0b22888920fa0f8a2')

prepare() {
  cd "$pkgname-$pkgver-xi"

  # Disable the check for Ruby 1.9 by comparing with 7.7 instead
  sed -i 's/ 1 != / 7 == /' Rakefile
  sed -i 's/ 9 != / 7 == /' Rakefile

  # Use vendor_ruby instead of site_ruby
  sed -e \
    's/RbConfig::CONFIG\["sitelibdir"\]/RbConfig::CONFIG\["vendorlibdir"\]/' \
    -i Rakefile

  # Configure the project
  rake config

  #setconf config.yml cflags _ '"'
  #setconf config.yml cflags "\"-M -Wall -Werror -Wpointer-arith -Wstrict-prototypes -Wunused -Wshadow -std=gnu99 -DNDEBUG $CFLAGS \"" '"'
  #setconf config.yml configdir "\"$pkgdir/etc/xdg/subtle\""
  #sed -i 's/gcc/gcc -c/g' config.yml
}

build() {
  cd "$pkgname-$pkgver-xi"

  rake build --trace
}

package() {
  cd "$pkgname-$pkgver-xi"

  rake destdir="$pkgdir" install --trace
}

# vim:set ts=2 sw=2 et:
