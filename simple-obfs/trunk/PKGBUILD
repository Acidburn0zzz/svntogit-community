# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=simple-obfs
pkgver=0.0.3
pkgrel=1
pkgdesc='A simple obfusacting tool designed as plugin server of shadowsocks'
arch=('i686' 'x86_64')
url='https://github.com/shadowsocks/simple-obfs'
license=('GPL')
depends=('libsodium' 'udns' 'libev' 'libcork')
makedepends=('git' 'asciidoc' 'xmlto')
source=("git+https://github.com/shadowsocks/simple-obfs.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cd simple-obfs
  patch -p1 -i debian/patches/0001-Use-libcork-dev-in-system.patch
}

build() {
  cd simple-obfs
  ./autogen.sh
  ./configure --prefix=/usr --enable-system-shared-lib
  make
}

package() {
  cd simple-obfs
  make DESTDIR="$pkgdir/" install
}
