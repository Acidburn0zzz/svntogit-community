# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nextcloud
# NOTE: do not package x.0.0 versions as they are considered beta
pkgver=21.0.0
pkgrel=9
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="https://nextcloud.com"
license=('AGPL3')
depends=('php>=8.0.0' 'php<8.1.0' 'php-gd')
optdepends=(
  'curl: for cron jobs from within uwsgi'
  'ffmpeg: for preview generation'
  'mariadb: to use a local MariaDB server'
  'memcached: to use a local memcached server'
  'php-apcu: for caching with APCU'
  'php-apache: to use the Apache web server'
  'php-fpm: to run in fastCGI process manager'
  'php-imagick: for preview generation'
  'php-imap: for external user authentication'
  'php-intl: to increase language translation performance and fix sorting'
  'php-memcached: for caching with memcached'
  'php-pgsql: to use the PostgreSQL database backend'
  'php-redis: for caching with redis'
  'php-sqlite: to use the SQLite database backend'
  'postgresql: to use a local PostgreSQL server'
  'redis: to use a local Redis server'
  'smbclient: for SMB/CIFS integration'
  'sudo: for occ command'
  'libreoffice: for preview generation'
  'uwsgi-plugin-php: run as application container'
)
backup=(
  "etc/webapps/${pkgname}/config/config.php"
  "etc/uwsgi/${pkgname}.ini"
)
# there are no elf files, no need to strip anything
options=(!strip)
install="${pkgname}.install"
source=(
  "https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2"{,.asc}
  'apache.example.conf'
  "${pkgname}.config.php"
  "${pkgname}.hook"
  "${pkgname}.occ.sh"
  "${pkgname}.sysusers"
  "${pkgname}.tmpfiles"
  "${pkgname}.uwsgi"
)
sha512sums=('a1ae3400f0fb5997e54b0cee821c50d13e4ba0e27c9d11731c8b5233cba564666121a58aed848ccd2593a9783e27db1ed98ff559588a76a1acaeaf083ef4404a'
            'SKIP'
            '6f9f284d78d414a3bb7c159a812b105e31e8cd2393afee44465701e6f18169709f6d492d790e403e1e66f308c51b6b6496d29ddce29e4fac2c73a3c84a10c7fe'
            '92592f9a76ee534d0ae6d3aedf2257c147337debe7e2d6dbec1daff66a682e3abc74611e08662058de314bf3ea6ff1c85efd678674b782d71251c38786e446f9'
            '7b18b73ee369607b8ee3d00d2a054262a7b8f3033fc4873eb333c9b9333d7338870c035910ae123b564a65d7d245730178137311048f0713d5bc524de3654923'
            '1340a9f3479ac83248242547ce948552334ed81097f18fb20c4e9cc3aaafe1575790024bf73399babec8c91858ea0ed4898def4111c4c4585a48e28d8b63e2f3'
            '1d06f339c43d57e5f5406c2698b815241fd9e39039a9e55f777face510c9a5ddae5ccd4e051393e8f16f391301a6aa03fff2462837a2d3441d969ea7195e7d84'
            'e9cc3e29358720ff6514dff4670c7e61462fc01b6150bf668e6b3084ffaf57f618c68ab718f2277edf484aa6357ce5bbd4eb78c83f3f4561d99e9243b337f6c8'
            '234dfd58717a59c7df78c0f7db61e441fd9a56529f147c19d062b30f18218d230882f5a059339d13f7610e5a5f4213d9d66d9d7ddd3d0ac00cacf4fa4d85101c')
b2sums=('7c583c150f6b315830191329310993582439e7aef57f306b93681d654b8fa7d4d5dafee082befd493aafd2bf6efd940d52ca0ca05b8e42b79a1599ff3e809550'
        'SKIP'
        'a7e1aa1d8cb2b0ff7832bb7d6d857987733253317c0f3727e744502aeaa3b2008351b47ecc24e348fe7b252cefaffe2418797523ee288dfd55e128917e8fbe2c'
        '4ad061efbc20b4d0e9f8cfd01c5e8e612fa5af29bce754b77f90c1835b98ee445ef4de3d8c83da91461053622620af14d880eb9014b51f2383c75f1860d479f4'
        '772a9571fe7d88dbcbeb46351a188354e237dfe8e020131f2752b3a36cff0ff177423b4cfd6a1a5f28946f86a8581f3e24ee06ebda7260c5f862ca4071908e43'
        '7644165e2218cde22e44ed5f2e65362b847aa37866b00406761a859506a6d5c97ecf8b59c437f16b82927158a7a74f77ff4f87e7c6f96e4d86b5af91dfd9736c'
        '1a61a89531636b65dade03cd7edca8747b3e1e880f9bbd4d5a53cffa452d70fe17e345cfed739ae92e99e336d4c1f2633c84a5c84a4ee6da518762bf1396b1db'
        '913ce3c91de88c991ec79fe237eb40c13a7e9babd4463694b5f6c25ff47f6a133d48f5cd25a38d4fcb425a822ff5541db9cd4c0f545b3f4726272599278fa95f'
        '21b4ca4e7d4191eef136f3f805533f0ec6ad16db0b9cd0c92d0a800e5bf7043d82ce4f9c8c563b5c952c366989e5dd56e138bebc8cc02db13b8d713c7d9d30a3')
validpgpkeys=('28806A878AE423A28372792ED75899B9A724937A') # Nextcloud Security <security@nextcloud.com>

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
}

package() {
  cd "${pkgname}-${pkgver}"
  # sysusers.d integration
  install -vDm 644 ../${pkgname}.sysusers \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d integration
  install -vDm 644 ../${pkgname}.tmpfiles \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # state dir for data and writable apps
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/"
  install -vdm 770 "${pkgdir}/var/lib/${pkgname}/data"
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/apps"
  # log dir
  install -vdm 755 "${pkgdir}/var/log/${pkgname}/"
  # uwsgi integration
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # config dir
  install -vDm 640 "../${pkgname}.config.php" "${pkgdir}/etc/webapps/${pkgname}/config/config.php"
  find config -type f -exec install -vDm 644 {} "${pkgdir}/etc/webapps/${pkgname}/"{} \;
  # remove config dir from source so that it is not installed to default location
  rm -rf config

  # webapp
  find . -type f -exec install -vDm 644 {} "${pkgdir}/usr/share/webapps/${pkgname}/"{} \;

  # symlink config directory into place
  ln -sv "/etc/webapps/${pkgname}/config" "${pkgdir}/usr/share/webapps/${pkgname}/"
  # add symlink to default writable apps directory
  ln -sv "/var/lib/${pkgname}/apps" "${pkgdir}/usr/share/webapps/${pkgname}/wapps"
  # install occ command
  install -vDm 755 occ -t "${pkgdir}/usr/share/webapps/${pkgname}"
  # occ script
  install -vDm 755 "../${pkgname}.occ.sh" "${pkgdir}/usr/bin/occ"
  # optional pacman hook
  install -vDm 644 "../${pkgname}.hook" -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # apache example conf
  install -vDm 644 ../apache.example.conf -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
