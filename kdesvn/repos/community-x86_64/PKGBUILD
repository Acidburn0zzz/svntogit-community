# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=kdesvn
pkgver=1.5.5
pkgrel=2
pkgdesc="A pure C++ SVN-client for KDE"
arch=('i686' 'x86_64')
url="http://kdesvn.alwins-world.de/"
license=('GPL')
depends=('kdebase-runtime' 'subversion')
makedepends=('automoc4' 'cmake')
conflicts=('qsvn')
install=kdesvn.install
source=("http://kdesvn.alwins-world.de/downloads/${pkgname}-${pkgver}.tar.bz2"
        'kdesvn-1.5.5-qt48.patch'
        'kdesvn-1.5.5-doctype-version.patch')
md5sums=('8f11d31cc55fba408b7971541ee261d0'
         'a7440581e373e06e4290e1f0d905c6f2'
         '64368d7cb40ce9fe7656f886337628b8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/kdesvn-1.5.5-qt48.patch"
  patch -Np1 -i "${srcdir}/kdesvn-1.5.5-doctype-version.patch"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  #removing conflicting files - kdesdk
  rm -f ${pkgdir}/usr/share/kde4/services/svn*

  #removing $srcdir reference
  rm -rf ${pkgdir}/usr/share/doc
}
