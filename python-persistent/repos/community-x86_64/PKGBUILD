# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.6.0
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-cffi')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('f962af7d1f8c3c76591d817b597ab41ad6a96e7d1be9a4d1dbc67b1e004ce7cbf0a2e84a400f98c319872a483191631f9799ff5323e022f109bd5426300a4f31')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=.
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
