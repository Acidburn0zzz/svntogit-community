# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer-vaapi
pkgver=34180
_sha1=f6ac548a
pkgrel=1
pkgdesc="A movie player, compiled with vaapi support"
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/mplayer-vaapi/"
license=('GPL')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl'
         'lame' 'fontconfig' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'mesa'
         'smbclient' 'aalib' 'jack' 'libcaca' 'x264' 'faac' 'faad2'
         'lirc-utils' 'ttf-dejavu' 'libxvmc' 'enca' 'opencore-amr' 'libdca'
         'a52dec' 'schroedinger' 'mpg123' 'libvpx' 'libpulse' 'fribidi' 'faad2'
         'libva' 'libass' 'desktop-file-utils')
makedepends=('unzip' 'live-media' 'yasm' 'ladspa' 'git')
provides=("mplayer=$pkgver")
conflicts=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
#source=(http://pkgbuild.com/~foutrelis/mplayer-$pkgver.tar.xz
#        http://www.splitted-desktop.com/static/libva/mplayer-vaapi/mplayer-vaapi-$_vaapi_version.tar.bz2
source=(mplayer-vaapi-$_sha1.tar.gz::http://gitorious.org/vaapi/mplayer/archive-tarball/$_sha1
        mplayer.desktop
        mplayer.png)
options=('!buildflags' '!emptydirs')
install=mplayer-vaapi.install
sha1sums=('17f95da56302ae6e2fa052ba6f33470d5387e77b'
          '289952d4d75b1ad4a647e75a02b174f2874a7398'
          '123bb5acf8163b5aaed1ec29bb0bf0bc05eb366c')

build() {
  cd "$srcdir/vaapi-mplayer"

  [[ -d ffmpeg ]] && rm -rf ffmpeg
  git clone --depth 1 git://git.videolan.org/ffmpeg.git ffmpeg

  ./configure --prefix=/usr \
    --enable-runtime-cpudetection \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --disable-ass-internal \
    --enable-xvmc \
    --disable-vdpau \
    --enable-vaapi \
    --language=all \
    --confdir=/etc/mplayer

  [[ $CARCH == i686 ]] && sed -i 's|-march=i486|-march=i686|g' config.mak

  make
}

package() {
  cd "$srcdir/vaapi-mplayer"

  make -j1 DESTDIR="$pkgdir" install-mplayer install-mplayer-man
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
  install -dm755 "$pkgdir/usr/share/mplayer/"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
  rm -rf "$pkgdir/usr/share/mplayer/font"
  # Desktop file (FS#14770)
  install -Dm644 "$srcdir/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
  install -Dm644 "$srcdir/mplayer.png" "$pkgdir/usr/share/pixmaps/mplayer.png"
}

# vim:set ts=2 sw=2 et:
