# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer-vaapi
pkgver=33161
_vaapi_version=20110127
pkgrel=5
pkgdesc="A movie player, compiled with vaapi support"
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/mplayer-vaapi/"
license=('GPL')
depends=('libxxf86dga' 'libxxf86vm' 'libmad' 'cdparanoia' 'libxinerama' 'sdl'
         'lame' 'libtheora' 'xvidcore' 'libmng' 'libxss' 'mesa' 'smbclient'
         'aalib' 'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'ttf-dejavu'
         'libxvmc' 'enca' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger'
         'libvpx' 'libpulse' 'fribidi' 'faad2' 'libva' 'libass'
         'desktop-file-utils')
makedepends=('unzip' 'live-media' 'yasm' 'ladspa')
provides=("mplayer=$pkgver")
conflicts=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(http://pkgbuild.com/~foutrelis/mplayer-$pkgver.tar.xz
        http://www.splitted-desktop.com/static/libva/mplayer-vaapi/mplayer-vaapi-$_vaapi_version.tar.bz2
        mplayer.desktop
        mplayer.png
        patch-fixes.patch
        gcc-4.6-mp3lib-sse-code-miscompilation.patch)
options=('!buildflags' '!emptydirs')
install=mplayer-vaapi.install
sha1sums=('ba3481bbc832abd2e55a8de6aff844f769c3ff3a'
          '682e360778dc43f69e3a3f77fc58e31edd650691'
          '289952d4d75b1ad4a647e75a02b174f2874a7398'
          '123bb5acf8163b5aaed1ec29bb0bf0bc05eb366c'
          'cbedd78fd67f273ac6e9482e30a8820648966d0c'
          'f1bc4ceda8715ff9de5cea370b7f19cb6aef2e7b')

build() {
  cd "$srcdir/mplayer-$pkgver"

  # Update vaapi patches for changes introduced in mplayer/ffmpeg
  patch -d "$srcdir/mplayer-vaapi-$_vaapi_version" -p1 -i \
    "$srcdir/patch-fixes.patch"

  # Workaround mp3lib SSE code miscompilation with gcc 4.6 (r33469 upstream)
  # Patch by Jindrich Makovicka <makovick <at> gmail>
  patch -Np0 -i "$srcdir/gcc-4.6-mp3lib-sse-code-miscompilation.patch"

  for patch in mplayer-{vaapi{,-{gma500-workaround,0.29}},vdpau}; do
    patch -Np1 -i "$srcdir/mplayer-vaapi-$_vaapi_version/patches/$patch.patch"
  done

  ./configure --prefix=/usr \
    --enable-runtime-cpudetection \
    --disable-gui \
    --disable-arts \
    --disable-liblzo \
    --disable-speex \
    --disable-openal \
    --disable-libdv \
    --disable-musepack \
    --disable-esd \
    --disable-mga \
    --enable-xvmc \
    --disable-vdpau \
    --enable-vaapi \
    --language=all \
    --confdir=/etc/mplayer

  [[ $CARCH == i686 ]] && sed -i 's|-march=i486|-march=i686|g' config.mak

  make
}

package() {
  cd "$srcdir/mplayer-$pkgver"

  make -j1 DESTDIR="$pkgdir" install-mplayer install-mplayer-man
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
  install -dm755 "$pkgdir/usr/share/mplayer/"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
  rm -rf "$pkgdir/usr/share/mplayer/font"
  # Desktop file (FS#14770)
  install -Dm644 "$srcdir/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
  install -Dm644 "$srcdir/mplayer.png" "$pkgdir/usr/share/pixmaps/mplayer.png"
}

# vim:set ts=2 sw=2 et:
