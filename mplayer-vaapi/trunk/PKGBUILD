# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=mplayer-vaapi
pkgver=32025
_vaapi_version=20100713
pkgrel=1
pkgdesc="A movie player, compiled with vaapi support"
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/mplayer-vaapi/"
license=('GPL')
depends=('libxxf86dga' 'libmad' 'cdparanoia' 'libxinerama' 'sdl' 'lame'
         'libtheora' 'xvidcore' 'libmng' 'libxss' 'libgl' 'smbclient' 'aalib'
         'jack' 'libcaca' 'x264' 'faac' 'lirc-utils' 'ttf-dejavu' 'libxvmc'
         'enca' 'opencore-amr' 'libdca' 'a52dec' 'schroedinger' 'libvpx'
         'libva')
makedepends=('unzip' 'mesa' 'live-media>=2010.01.13' 'yasm')
provides=("mplayer=$pkgver")
conflicts=('mplayer')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
source=(ftp://ftp.archlinux.org/other/mplayer/mplayer-$pkgver.tar.xz
        http://www.splitted-desktop.com/~gbeauchesne/mplayer-vaapi/mplayer-vaapi-$_vaapi_version.tar.bz2
        mplayer.desktop
        mplayer.png)
md5sums=('0b66e1f7d26e5f5ffb2b15d0313da71b'
         '48ddba37ac197a1b2b3aa02ea34ff076'
         'c0d6ef795cf6de48e3b87ff7c23f0319'
         'd00874ccc644b7f43d6ef1c942fcef28')

build() {
  cd "$srcdir/mplayer"

  # Custom CFLAGS break the mplayer build
  unset CFLAGS LDFLAGS

  # Update mplayer-vaapi.patch for changes introduced in mplayer r31869
  # (r31869: Rename libass types to match upstream libass >= 0.9.7)
  sed -i -e 's/mp_eosd_images_t/EOSD_ImageList/g' \
         -e 's/ass_image_t/ASS_Image/g' \
    "$srcdir/mplayer-vaapi-$_vaapi_version/patches/mplayer-vaapi.patch"

  for patch in mplayer-{vaapi{,-{gma500-workaround,0.29}},vdpau}; do
    patch -Np1 -i "$srcdir/mplayer-vaapi-$_vaapi_version/patches/$patch.patch"
  done

  ./configure --prefix=/usr \
      --enable-runtime-cpudetection \
      --disable-gui \
      --disable-arts \
      --disable-liblzo \
      --disable-speex \
      --disable-openal \
      --disable-fribidi \
      --disable-libdv \
      --disable-musepack \
      --disable-esd \
      --disable-mga \
      --enable-xvmc \
      --disable-vdpau \
      --enable-vaapi \
      --language=all \
      --confdir=/etc/mplayer

  [ "$CARCH" = "i686" ] &&  sed 's|-march=i486|-march=i686|g' -i config.mak

  make
  make -j1 DESTDIR=$pkgdir install
  install -Dm644 etc/{codecs.conf,input.conf,example.conf} "$pkgdir/etc/mplayer/"
  install -dm755 "$pkgdir/usr/share/mplayer/"
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf "$pkgdir/usr/share/mplayer/subfont.ttf"
  rm -rf "$pkgdir/usr/share/mplayer/font"
  # Desktop file (FS#14770)
  install -Dm644 "$srcdir/mplayer.desktop" "$pkgdir/usr/share/applications/mplayer.desktop"
  install -Dm644 "$srcdir/mplayer.png" "$pkgdir/usr/share/pixmaps/mplayer.png"
}

# vim:set ts=2 sw=2 et:
