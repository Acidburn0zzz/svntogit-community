# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk2-greeter
pkgver=1.5.1
pkgrel=3
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk2' 'lightdm')
makedepends=('exo' 'gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/2/}")
conflicts=("${pkgname/2/3}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/${pkgname/2/}/1.6/${pkgver}/+download/${pkgname/2/}-${pkgver}.tar.gz"
        "onscreen-keyboard.patch")
sha256sums=('ecce7e917a79fa8f2126c3fafb6337f81f2198892159a4ef695016afecd2d621'
            '381c4da15ed2482800aaf3258217cd48c2313aee5da4f7ade77f7ef55659d0b9')

build() {
  cd "${srcdir}"/${pkgname/2/}-${pkgver}

# Patch
  patch -Np1 -i ../onscreen-keyboard.patch

# Build
  AUTOMAKE=automake ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm --disable-static --with-gtk2
  make
}

package() {
  cd "${srcdir}"/${pkgname/2/}-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
