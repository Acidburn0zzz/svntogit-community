# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.9.6
pkgrel=1
pkgdesc="A popular and easy to use graphical IRC (chat) client"
arch=('i686' 'x86_64')
url="http://www.hexchat.org/"
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'hicolor-icon-theme' 'libcanberra' 'libnotify' 'libproxy' 'libsexy' 'pciutils')
makedepends=('perl' 'python')
optdepends=('enchant: Spell check'
            'perl: Perl plugin'
            'python: Python plugin')
options=('!libtool')
install="${pkgname}.install"
source=("http://dl.hexchat.org/hexchat/hexchat-${pkgver}.tar.xz")
sha256sums=('eaefc07867a6e546cd9e19a4ccc976b4736c759ba3f946020eeae3cd6f4684b0')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --enable-{python='python3',shm,spell='libsexy',textfe,xft}
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
