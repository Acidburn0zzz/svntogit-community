# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: TingPing <tingping@tingping.se>

pkgname=hexchat
pkgver=2.12.2
pkgrel=1
pkgdesc='A popular and easy to use graphical IRC (chat) client'
arch=('i686' 'x86_64')
url='http://hexchat.github.io/'
license=('GPL')
depends=('dbus-glib' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'libcanberra' 'libnotify' 'libproxy' 'openssl' 'pango' 'pciutils')
makedepends=('intltool' 'iso-codes' 'lua' 'perl' 'python')
optdepends=('enchant: Spell check'
            'lua: Lua plugin'
            'perl: Perl plugin'
            'python: Python plugin')
source=("http://dl.hexchat.net/hexchat/hexchat-${pkgver}.tar.xz"{,.asc})
sha256sums=('6dad783f37aac32a8301a12d498af7a74900f011aec4e77bb378ae9797a3ccf6'
            'SKIP')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis

build() {
  cd hexchat-${pkgver}

  ./configure \
    --prefix='/usr' \
    --enable-python='python3' \
    --enable-textfe
  make
}

package() {
  cd hexchat-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
