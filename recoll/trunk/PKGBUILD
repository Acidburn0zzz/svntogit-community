# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Vladimir Chizhov <jagoterr@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=recoll
pkgver=1.16.2
pkgrel=1
pkgdesc="Full text search tool based on Xapian backend"
arch=('i686' 'x86_64')
url="http://www.lesbonscomptes.com/recoll/"
license=('GPL')
depends=('xapian-core>=1.0.15-1' 'qt' 'openssl' 'hicolor-icon-theme')
optdepends=('libxslt: for XML based formats (fb2,etc)'
	    'unzip: for the OpenOffice.org documents'
	    'xpdf: for pdf'
	    'pstotext: for postscipt'
	    'antiword: for msword'
	    'catdoc: for ms excel and powerpoint'
	    'unrtf: for RTF'
	    'untex: for dvi support with dvips'
	    'djvulibre: for djvu'
	    'id3lib: for mp3 tags support with id3info'
	    'python2: for using some filters'
	    'mutagen: Audio metadata'
	    'python-pychm: CHM files'
	    'perl-image-exiftool: EXIF data from raw files'
	    'aspell-en: English stemming support')
install=recoll.install
source=(http://www.lesbonscomptes.com/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('1bbe35a1c0fb248d1c5b3a4c3ed20d1c')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in filters/{rclchm,rclexecm.py,rclics,rclpython,rclzip,rclaudio,rclinfo,rclkar,rcllatinclass.py} doc/user/usermanual.html; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
  sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' desktop/hotrecoll.py

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
