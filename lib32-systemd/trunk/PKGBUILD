# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=199
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-dbus-core' 'lib32-libcap' 'lib32-acl' 
             'lib32-libgcrypt' 'lib32-glib2' 'lib32-xz' 'intltool' 'gperf')
options=('!libtool')
source=("http://www.freedesktop.org/software/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz"
        use-split-usr-path.patch)
md5sums=('4bb13f84ce211e93f0141774a90a2322')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/systemd-${pkgver}"

  # hang onto this until we do the /{,s}bin merge
  patch -Np1 <"$srcdir/use-split-usr-path.patch"

  ./configure \
    --enable-static \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-audit \
    --disable-ima \
    --disable-pam \
    --disable-kmod \
    --disable-blkid \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path=

  make
}

package() {
  depends=('lib32-glib2' 'lib32-libgcrypt' 'lib32-xz' 'systemd')
  cd "${srcdir}/systemd-${pkgver}"
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}
  rm -rf "${pkgdir}"/usr/lib32/libnss*
}
md5sums=('4bb13f84ce211e93f0141774a90a2322'
         '76bf83fe34c5b40533abc5dc940576a6')
