# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=restinio
pkgver=0.6.4
pkgrel=1
pkgdesc="Header-only C++14 library that gives you an embedded HTTP/Websocket server"
arch=(any)
url="https://stiffstream.com/en/products/restinio.html"
license=(BSD)
depends=(asio http-parser fmt)
makedepends=(cmake gcc openssl zlib pcre pcre2 boost)
source=(https://github.com/Stiffstream/restinio/releases/download/v.${pkgver}/${pkgname}-${pkgver}.tar.bz2
        FindHTTP_Parser.cmake)
sha256sums=('e585426eedb8e5e6e7d75e02a49f9c2fc644433826ffda2eb7361aa110a17e05'
            '3b8f066d44679fcbfc8220c4c4b0ce69c2d49adbff8a05a7e59f9f2f71012b12')

prepare() {
  cp FindHTTP_Parser.cmake ${pkgname}-${pkgver}/dev/cmake/modules/
  sed -i 's|unofficial-http-parser CONFIG|HTTP_Parser|g' ${pkgname}-${pkgver}/dev/CMakeLists.txt
}

build() {
  cmake -B build -S ${pkgname}-${pkgver}/dev/ \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DRESTINIO_FIND_DEPS=ON \
      -DRESTINIO_TEST=OFF \
      -DRESTINIO_SAMPLE=OFF \
      -DRESTINIO_INSTALL_SAMPLES=OFF \
      -DRESTINIO_BENCH=OFF \
      -DRESTINIO_INSTALL_BENCHES=OFF \
      -DRESTINIO_ALLOW_SOBJECTIZER=OFF
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
