# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9166
pkgrel=1
pkgdesc='Ameliorated interface to the CPAN'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-log-message' 'perl-archive-extract' 'perl-term-ui' 'perl-log-message-simple'
         'perl-module-pluggable' 'perl-object-accessor' 'perl-package-constants')
url=https://metacpan.org/release/CPANPLUS
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
sha512sums=('381db2862be5ffd3da9a4c9a68b2beee7b1c83bdf35d7e34342a1e9b94feee48bafa8f1232c6913d72f7f2df00c6ac262c6d9b85504bbc6092e3c743d8cebe6e')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
