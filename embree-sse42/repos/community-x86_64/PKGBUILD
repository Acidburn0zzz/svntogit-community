# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
_pkgname=embree
pkgname=embree-sse42
pkgver=2.17.1
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels â€“ with SSE4.2 support"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'openexr')
provides=('embree' 'embree-isa')
conflicts=('embree')
source=(${_pkgname}-${pkgver}.tar.gz::"https://github.com/${_pkgname}/${_pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('ab4af2f86690f82228337bb4e54213ca3316fd38726770f720fe2c9b4cf0f066')

build() {
    cd ${_pkgname}-${pkgver}

    cmake . \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DEMBREE_TUTORIALS=OFF \
        -DEMBREE_MAX_ISA="SSE4.2"
    make
}

package() {
    cd ${_pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
}
