# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=coin-or-cgl
pkgver=0.60.4
pkgrel=2
pkgdesc='COIN-OR Cut Generation Library'
arch=(x86_64)
url='https://github.com/coin-or/Cgl'
license=(EPL)
groups=(coin-or)
depends=(coin-or-clp)
source=($pkgname-$pkgver.tar.gz::https://github.com/coin-or/Cgl/archive/refs/tags/releases/$pkgver.tar.gz)
sha256sums=('57db498a0b7e1f3614ca061a93b23dc7e65017f092457c7366fa7d78397b5657')

build() {
  cd Cgl-releases-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd Cgl-releases-$pkgver
  make test
}

package() {
  cd Cgl-releases-$pkgver
  make DESTDIR="$pkgdir" install
}
