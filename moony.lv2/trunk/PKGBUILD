# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=moony.lv2
pkgver=0.32.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11')
makedepends=('glu' 'lv2' 'meson' 'sord')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('ce7571284be0cf2eb0f25e0450f67d7398db2192557188d2854a8df78e307936af4ed1adac6cb9d34c314055250b9fb638b924271de6f570c1267615a375c7f4')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('a1xa1' 'a2xa2' 'a4xa4' 'c1a1xc1a1' 'c1xc1' 'c2a1xc2a1'
  'c2xc2' 'c4a1xc4a1' 'c4xc4')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/moony#${_plugin}"
  done
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
