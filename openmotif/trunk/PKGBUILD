# $Id: PKGBUILD,v 1.12 2009/07/14 16:18:01 spupykin Exp $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

_automakever=`pacman -Q automake | cut -f 2 -d \  | cut -f 1 -d -`

pkgname=openmotif
pkgver=2.3.1
pkgrel=5
pkgdesc="Open Motif"
arch=('i686' 'x86_64')
url="http://www.motifzone.org/"
license=('GPL')
depends=(libxext libxp libxft libxt)
makedepends=(xbitmaps xproto printproto xextproto automake)
conflicts=(lesstif)
options=('!libtool')
source=(ftp://ftp.ics.com/openmotif/2.3/$pkgver/openmotif-$pkgver-1.tar.gz \
	openmotif-etched-in-menus.diff \
	openmotif-insensitive-label.diff)
md5sums=('1372108f50f3554ee2f28367ff76bd32'
	 '15d9230dbf832a8c595bb93b556d96d5'
         '455c274ec5529c3c1e4b488c2d030b6e')

build() {
  cd $startdir/src/openmotif-$pkgver

  [ $NOEXTRACT -eq 1 ] || patch -p0 Makefile.in <<EOF
--- Makefile.in.org	2007-06-11 09:32:01.000000000 +0400
+++ Makefile.in	2007-06-19 19:13:04.000000000 +0400
@@ -249,8 +249,7 @@
              include \\
              tools \\
              clients \\
-             doc \\
-             demos
+             doc
 
 AUTOMAKE_OPTIONS = 1.4
 all: all-recursive
EOF

#  patch -p1 <$srcdir/openmotif-etched-in-menus.diff
#  patch -p1 <$srcdir/openmotif-insensitive-label.diff

  ln -s -f /usr/share/automake-${_automakever}/compile compile
  ln -s -f /usr/share/libtool/config/config.guess config.guess
  ln -s -f /usr/share/libtool/config/config.sub config.sub
  ln -s -f /usr/share/automake-${_automakever}/depcomp depcomp
  ln -s -f /usr/share/automake-${_automakever}/install-sh install-sh
  ln -s -f /usr/share/automake-${_automakever}/missing missing
  ln -s -f /usr/share/libtool/config/ltmain.sh ltmain.sh

  [ $NOEXTRACT -eq 1 ] || autoreconf

  [ $CARCH == "x86_64" ] && export CFLAGS="$CFLAGS -fPIC"

  [ $NOEXTRACT -eq 1 ] || ./configure --with-x --prefix=/usr
  make -j1 || return 1

  make DESTDIR=$startdir/pkg install || return 1
  # cleanup
  (cd $startdir/pkg/usr/share/man/man3/ && mv Shell.3 OpenMotif-Shell.3)
}
