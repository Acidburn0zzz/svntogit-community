# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SanskritFritz (gmail)

pkgname=netdata
pkgver=1.19.0
pkgrel=1
pkgdesc="Real-time performance monitoring, in the greatest possible detail, over the web"
url="https://github.com/netdata/netdata"
arch=('x86_64')
license=('GPL')
backup=('etc/netdata/netdata.conf')
depends=('libmnl' 'libnetfilter_acct' 'zlib' 'judy' 'libuv' 'json-c' 'libcap' 'lz4')
optdepends=('nodejs: for monitoring named and SNMP devices'
            'lm_sensors: for monitoring hardware sensors'
            'iproute2: for monitoring Linux QoS'
            'python: for most of the external plugins'
            'python-psycopg2: for monitoring PostgreSQL databases'
            'python-mysqlclient: for monitoring MySQL/MariaDB databases'
            'python-requests: for monitoring elasticsearch'
            'hddtemp: for monitoring hhd temperature'
            'apcupsd: for monitoring APC UPS'
            'iw: for monitoring Linux as access point')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/netdata/netdata/archive/v${pkgver}.tar.gz"
        "$pkgname.tmpfiles"
        "$pkgname.sysusers")
sha512sums=('026ae159d8ed52a30e406c110c2732e54e50f070865ba3e430df73e4df40387105053e750ca6029cfbc7f40a2c05b7fb6ba4e3e4a7070822c7eec4c1a4712aea'
            '3f934ddd1f5248f9e11c29050c023d60b76e4098ec9c8d413bb362d43e9242f767fd58310d966076e8400779af8bda2459afcc314b267fcb9f1c84173e14e313'
            'a910809a823ba58ca7bdaa72e8d68427b48f452c1fbb2343fa182ecb0a091a7640e73af24b8ba2fdd90e00aed8ef53b7fccd25cb8f04ca9b9fa6c8e52223ca66')

build() {
  cd "$pkgname-$pkgver"

  autoreconf -i
  ./configure \
  	--prefix="/usr" \
  	--sbindir="/usr/bin" \
  	--sysconfdir="/etc" \
  	--libexecdir="/usr/lib" \
  	--localstatedir="/var" \
  	--with-zlib --with-math --with-user=netdata
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Remove /var/*, pacman creates it via tmpfiles hook
  rm -vrf "${pkgdir}/var"
  touch "$pkgdir/etc/netdata/netdata.conf"
  chown -R 134:134 "$pkgdir"/etc/netdata
  chown -R 134:134 "$pkgdir"/usr/share/netdata/web

  install -Dm0644 "system/netdata.service" "$pkgdir/usr/lib/systemd/system/netdata.service"
  install -Dm0644 "system/netdata.logrotate" "$pkgdir/etc/logrotate.d/netdata"
  install -Dm0644 "${srcdir}/${pkgname}.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  install -Dm0644 "${srcdir}/${pkgname}.sysusers" "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
