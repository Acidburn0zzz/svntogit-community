# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.11.0
pkgrel=2
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-yaml python-mock python-parameterized)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/awslabs/serverless-application-model/archive/v$pkgver.tar.gz")
sha256sums=('2bf620d1d22ee28781eefe0cb47e121c3f40eab4b04635889936b0ec1847e4f2')

prepare() {
  cd serverless-application-model-$pkgver

  # remove coverage.py options - running coverage as a non-developer does not make much sense
  sed -i 's#addopts = .*##' pytest.ini

  # backport of https://github.com/awslabs/serverless-application-model/pull/927
  sed -i 's#jsonschema~=2.6#jsonschema~=3.0#' requirements/base.txt
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver

  pytest -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
