# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.7.9
pkgrel=3
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='http://christophe.varoqui.free.fr/'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'ceph-libs' 'libsystemd')
makedepends=('git')
source=("multipath-tools::git+https://git.opensvc.com/multipath-tools/.git#tag=${pkgver}"
        'usr_lib_initcpio_hooks_kpartx'
        'usr_lib_initcpio_install_kpartx')
sha256sums=('SKIP'
            '46c80c89093c3ca76aa35e1c07b0b5f08474189f0f84c2411d0a58fdbeb61a6e'
            'db06ab0c18574e4e3e7c9f54af9860a60cb7c1b20e3a9114558eef7a22e95199')

prepare() {
  cd "${pkgname}"

  # Fix bindir in Makefile
  sed -i 's|$(exec_prefix)/sbin|$(exec_prefix)/bin|g' Makefile.inc
}

build() {
  cd "${pkgname}"

  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" \
       exec_prefix="/usr" \
       LIB="/usr/lib" \
       install

  install -Dm 644 "${srcdir}/usr_lib_initcpio_hooks_kpartx" "${pkgdir}/usr/lib/initcpio/hooks/kpartx"
  install -Dm 644 "${srcdir}/usr_lib_initcpio_install_kpartx" "${pkgdir}/usr/lib/initcpio/install/kpartx"
}
