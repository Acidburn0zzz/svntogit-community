# $Id$
# Maintainer:
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.4.9
pkgrel=6
pkgdesc="Multipath Tools For Linux"
arch=('i686' 'x86_64')
url="http://christophe.varoqui.free.fr/"
license=('GPL')
depends=('libaio' 'device-mapper')
backup=('etc/multipath.conf' 'etc/multipath.conf.annotated')
install=multipath-tools.install
options=('!emptydirs')
source=("http://christophe.varoqui.free.fr/multipath-tools/$pkgname-$pkgver.tar.bz2"
        "multipath.conf" "multipath.conf.annotated" "multipathd.rc" 'fix-build.patch')
md5sums=('a6d4b48afc28f1f50f5ee4b1b06d2765'
         '9324ff0ba8330dcb21b2fcf64988026f'
         'be11462922eeeb9fcd2ba5f3f137b7d9'
         '7c04ef96441363b0d43a2ebb13a87659'
         '2576fc535d7fa767837b1c7827013e52')

build() {
  patch -p1 -i fix-build.patch

  sed -i 's|etc/udev|usr/lib/udev|g' multipath/Makefile kpartx/Makefile
  sed -i 's|/sbin/|/usr/bin/|g' kpartx/kpartx.rules
  
  # Needs to be fixed upstream. Refer to
  # https://bbs.archlinux.org/viewtopic.php?pid=793814#p793814
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed}
  
  make
}

package() {
  make LIB="usr/lib" DESTDIR="$pkgdir" bindir="/usr/bin" libudevdir="/usr/lib/udev" install
  install -D -m 644 multipath.conf "$pkgdir"/etc/multipath.conf
  install -D -m 644 multipath.conf.annotated "$pkgdir"/etc/multipath.conf.annotated
  install -D -m 755 multipathd.rc "$pkgdir"/etc/rc.d/multipathd
}
