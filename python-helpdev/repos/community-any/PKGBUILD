# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=helpdev
pkgname=python-${_pkg}
pkgver=0.6.10
pkgrel=1
pkgdesc="Helping users and developers to get information about the environment to report bugs"
arch=(any)
url="https://gitlab.com/dpizetta/helpdev"
license=(MIT CCPL)
depends=(python-importlib-metadata python-psutil)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pip)
#PyPi does not have tests nor LICENSE
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(${url}/-/archive/v${pkgver}/${_pkg}-v${pkgver}.tar.bz2)
sha256sums=('ca478a30dbe0742f7a513b4017e83672e946f5c3c5b6c394d4386e54c0c63a19')

build() {
  cd ${_pkg}-v${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-v${pkgver}
  PYTHONPATH="${PWD}/build/lib/" pytest tests
}

package() {
  cd ${_pkg}-v${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
