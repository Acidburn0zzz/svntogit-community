# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=3.2.0.1
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('1b0fefb6b093018040829c943d95ade7f19d0078bacb26b56fa56bcf7e200c4008216fd40a9789136cf5bfb3c4c925499d870e16854d8f8a64e1f2e036c78963')

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-movie-reborn-$pkgver
  make DESTDIR="$pkgdir" install
}
