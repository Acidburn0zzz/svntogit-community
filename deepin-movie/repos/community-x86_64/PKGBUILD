# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=3.2.13
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('576d18e9d38a73beff42fc6891ab765f2119278905efd1378cfe6120417b62a35e887f780a3477231af1e437b87728fe8a8ae357d26b8bad348480a080738c7b')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i 's/libavresample//' src/CMakeLists.txt
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-movie-reborn-$pkgver
  make DESTDIR="$pkgdir" install
}
