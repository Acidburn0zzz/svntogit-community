# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=3.2.11
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('90645667687d650e32ef60f1bbd02725d5394ac7f90fbef1423b2dcc545aa6d71ad52ab44d16fb252bc7b87f2b5cef32d18ac9137e3a1ef7e1db4d3e7c60f4c9')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i 's/libavresample//' src/CMakeLists.txt
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-movie-reborn-$pkgver
  make DESTDIR="$pkgdir" install
}
