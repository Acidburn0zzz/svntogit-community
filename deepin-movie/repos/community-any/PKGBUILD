# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=2.2.14
pkgrel=2
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
depends=('python2-ass' 'python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-magic' 'deepin-qml-widgets' 'python2-xpybutil' 'python2-pysrt'
         'python2-opengl' 'deepin-manual' 'deepin-qt5integration')
makedepends=('deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie/archive/$pkgver.tar.gz")
sha512sums=('e63cb7b2158a378aa5fc77d399c7ae4f642997d6097f3f75fff16b2b12311e6a3ddd84c1bf7cda0517cf83f88c36880c926d57d2b8d86d25fd8ade97dcd93159')

prepare() {
  cd deepin-movie-$pkgver

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-movie-$pkgver
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie-$pkgver
  make DESTDIR="$pkgdir" PREFIX="/usr" install

  chmod 0755 "$pkgdir"/usr/share/deepin-movie/main.py
}
