# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.6.20
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('89df0244c2e6112f5fe86e1d8c88f6aa8d227987cf9129877d26396dce09ed13099344e4f2abbfab1d1368324502c6d8925468d3dd93b67e6a750b96a83b4a34')

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-movie-reborn-$pkgver
  make DESTDIR="$pkgdir" install
}
