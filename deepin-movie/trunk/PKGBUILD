# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=2.2.13
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
depends=('python2-ass' 'python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-magic' 'deepin-qml-widgets' 'python2-xpybutil' 'python2-pysrt'
         'python2-opengl' 'deepin-manual')
makedepends=('deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie/archive/$pkgver.tar.gz")
sha512sums=('630bbc83ef30e6ae002d0432620c770e33adc7c047aac81d388168a7bdfff2f17df7f2d2d892ffe9da4eb545471d235f80692e78ef3e1c68df7c623d1aff309c')

prepare() {
  cd deepin-movie-$pkgver

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-movie-$pkgver
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie-$pkgver
  make DESTDIR="$pkgdir" PREFIX="/usr" install

  chmod 0755 "$pkgdir"/usr/share/deepin-movie/main.py
}
