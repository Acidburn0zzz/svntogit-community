# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=2.2.10
pkgrel=3
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://github.com/linuxdeepin/deepin-movie"
license=('GPL3')
depends=('python2-ass' 'python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-magic' 'deepin-qml-widgets' 'python2-xpybutil' 'python2-pysrt'
         'python2-opengl')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
source=("git+https://github.com/linuxdeepin/deepin-movie.git#tag=$pkgver"
        ctypes.patch xcffib.patch)
sha256sums=('SKIP'
            '0427345ba7da2460795b992e566daa0f5be234f4c66dac6e73b64ade17ed1eb7'
            '612699576290d30e3687995e76aa75b8559afd697d3bf8b8d8c858633a38eac5')

prepare() {
  cd deepin-movie

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

  patch -p1 -i ../ctypes.patch
  patch -p1 -i ../xcffib.patch
}

build() {
  cd deepin-movie
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie
  make DESTDIR="${pkgdir}" PREFIX="/usr" install

  chmod 0755 "$pkgdir"/usr/share/deepin-movie/main.py
}
