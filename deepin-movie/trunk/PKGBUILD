# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
pkgver=2.3.0.20151020
_commit=640ea9471b4d911728a9742bdae78a785f323b15
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('any')
url="https://gitcafe.com/Deepin/deepin-movie"
license=('GPL3')
depends=('python2-pyqt5' 'qtav' 'mediainfo' 'deepin-menu' 'python2-dbus'
         'python2-peewee' 'python2-pillow' 'python2-peewee' 'python2-magic'
         'deepin-qml-widgets' 'python2-xpybutil' 'python2-deepin-utils'
         'python2-requests' 'python2-bottle' 'dleyna-renderer')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-movie.git#commit=$_commit")
sha256sums=('SKIP')

prepare() {
  cd deepin-movie

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-movie
  python2 configure.py
  deepin-generate-mo locale/locale_config.ini
}

package() {
  cd deepin-movie
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
