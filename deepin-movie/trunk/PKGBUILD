# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.6.104
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('d550f28dfde5180a4c7f5cc9c5c7e1ce5e7477e0ba5013b6220e8553ef5156fc0d8394286bc60bbe6ffa6f866685950eca98b85b05ba93b37d856524b2186b46')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i '/#include <DPalette>/a #include <QPainterPath>' src/widgets/{tip,toolbutton}.h

  # https://github.com/linuxdeepin/deepin-movie-reborn/issues/59
  sed -i 's/ && LIBAVFORMAT_VERSION_MINOR <= 25//' src/libdmr/playlist_model.cpp
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
