# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

pkgname=haveged
pkgver=1.4
pkgrel=2
pkgdesc="A simple entropy daemon."
arch=('i686' 'x86_64')
url="http://www.issihosts.com/haveged"
license=('GPL')
depends=('glibc')
source=(${url}/${pkgname}-${pkgver}.tar.gz
        haveged haveged.service)
md5sums=('39a03d4b8a4225706f78dbef3f623b0e'
         '264b725be5271288b40f363d7cfc6b68'
         '3cf7fb96dc6efe303f292d241076195c')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver/a/}
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver/a/}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc/init.d

  cd "${srcdir}"
  install -D -m755 haveged "${pkgdir}"/etc/rc.d/haveged
  install -D -m644 haveged.service "${pkgdir}"/usr/lib/systemd/system/haveged.service
}
