# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: kfgz <kfgz at interia dot pl>
# Contributor: pootzko <pootzko at gmail dot com>

pkgname=haveged
pkgver=1.7.a
_pkgver=${pkgver/.a/a}
pkgrel=2
pkgdesc='Entropy harvesting daemon using CPU timings'
arch=('i686' 'x86_64')
url='http://www.issihosts.com/haveged'
license=('GPL')
depends=('glibc')
options=('!libtool')
source=($url/$pkgname-$_pkgver.tar.gz
        service)
sha256sums=('8e7b0779bd8a4b9b73769ddaf626918355e2a542d3ac35e5ec31b74a58b9b051'
            'badf8ceb5484d45888a089fabdfba5f44f6ec2930bca203c50200051a4d83e12')

build() {
  cd $pkgname-$_pkgver
  ./configure --prefix=/usr --bindir=/usr/bin --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$_pkgver
  make DESTDIR="$pkgdir" install

  rm -rf $pkgdir/etc/init.d
  install -D -m644 $srcdir/service $pkgdir/usr/lib/systemd/system/haveged.service
}
