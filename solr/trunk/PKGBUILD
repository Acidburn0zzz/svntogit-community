# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=solr
pkgver=8.4.1
pkgrel=1
pkgdesc="Open source enterprise search platform built on Apache Lucene"
arch=('any')
url="https://lucene.apache.org/solr/"
license=('Apache')
depends=('bash' 'java-runtime>=8')
makedepends=('ant' 'ivy' 'java-environment>=8')
source=("https://archive.apache.org/dist/lucene/${pkgname}/${pkgver}/${pkgname}-${pkgver}-src.tgz"{,.asc}
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha512sums=('29df94ce5ee571b3cdb9738b1d05ed3e1abe4bc171b4407789100f338671a539d5751f27c12c3317f064354d5cb96df7cd0cf7d03de6882d06b5db4501e278ce'
            'SKIP'
            '4ddd652315d6b640e1ba84e3ad5dba97bf9d93548c16812d2a219cf0d62cce79603050329eae632c22a7c7528227a93c01dd4671864de1a55f8e54269bb4b44d'
            '06e5e40b96d2b6668790e4b166fc2867b9e694a2c72fd57eec702526e009b8b0495acbe16a5a27e259827477f4783ce87742f1f806254d8a2baec23b0b317058'
            '9cc97763a50c11c305b06ce07f0b2936b8fcb0b1d43f8b469fe1399850cb009fc1eba297d295a386a556e5c042e189dba1b1fc96a54aa46964ed4db8e17d40a8')
validpgpkeys=('2085660D9C1FCCACC4A479A3BF160FF14992A24C') # Ishan Chattopadhyaya <ishan@apache.org>

prepare() {
  cd "$pkgname-$pkgver"
  ant ivy-bootstrap
  rm -rvf "${pkgname}/bin/init.d"
}

build() {
  cd "$pkgname-$pkgver"
  cd "${pkgname}"
  ant compile
  ant server
  ant dist
  # removing unneeded sources
  rm -rvf contrib/*/src
  find . -type f \( -iname "*build.xml" -o -iname "*ivy.xml" \) -delete
}

check() {
  cd "$pkgname-$pkgver"
  cd "${pkgname}"
  ant test
}

package() {
  cd "$pkgname-$pkgver"
  # script
  install -vDm 755 "${pkgname}/bin/${pkgname}" -t "${pkgdir}/usr/bin"
  # configuration
  install -vDm 644 "${pkgname}/bin/${pkgname}.in.sh" \
    -t "${pkgdir}/etc/${pkgname}"
  install -vDm 644 "${pkgname}/server/etc/"*.xml \
    -t "${pkgdir}/etc/${pkgname}/server"
  install -vDm 644 "${pkgname}/server/${pkgname}/"*.xml \
    -t "${pkgdir}/etc/${pkgname}/"
  install -vdm 755 "${pkgdir}/usr/share/${pkgname}"
  cp -rvL "${pkgname}/"{bin,contrib,dist,docs,example,licenses,server} \
    "${pkgdir}/usr/share/${pkgname}"
  # logs directory
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  ln -svf "/var/log/${pkgname}" "${pkgdir}/usr/share/${pkgname}/server/logs"
  # symlink configuration into place
  ln -svf "/etc/${pkgname}/${pkgname}.in.sh" \
    "${pkgdir}/usr/share/${pkgname}/"
  ln -svf "/etc/${pkgname}/${pkgname}.xml" \
    "${pkgdir}/usr/share/${pkgname}/server/${pkgname}"
  for config in {jetty,jetty-{http,https,https8,ssl},webdefault}.xml; do
    ln -svf "/etc/${pkgname}/server/${config}" \
      "${pkgdir}/usr/share/${pkgname}/server/etc/${config}"
  done
  # docs
  install -vDm 644 "${pkgname}/"{CHANGES,LUCENE_CHANGES,NOTICE,README}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "../${pkgname}.service" -t "${pkgdir}/usr/lib/systemd/system/"
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
}
