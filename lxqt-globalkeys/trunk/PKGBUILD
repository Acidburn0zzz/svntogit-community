# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=lxqt-globalkeys
pkgver=0.14.3
pkgrel=1
pkgdesc="LXQt daemon and library for global keyboard shortcuts registration."
arch=("x86_64")
groups=("lxqt")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	$pkgname-qt5.14.patch::https://github.com/lxqt/lxqt-globalkeys/commit/c9ec69850428e599fba282c32e51868b815e3019.patch
)
sha256sums=('e6b92c5d9b61ad95ab30d70ecf0b30fd20ef1713910cce366e97e0dd7101f455'
            'SKIP'
            '368744b010e494e018ddfca8d44b3917b7e851b683821c2faebc5ecf63ccda72')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i ../$pkgname-qt5.14.patch
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
