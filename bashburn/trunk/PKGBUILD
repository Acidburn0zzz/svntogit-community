# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Andreas Wagner <A.Wagner@stud.uni-frankfurt.de>

pkgname=bashburn
pkgver=3.0.1
pkgrel=3
pkgdesc="A cd burning shell script"
arch=('any')
url="http://bashburn.dose.se/"
license=('GPL2')
changelog=$pkgname.changelog
depends=('bash' 'cdrdao' 'cdrkit' 'eject')
optdepends=('dvd+rw-tools: support for burning DVDs'
            'cdparanoia: Ripping audio CDs'
            'vorbis-tools: Vorbis-Support'
            'flac: Flac-Support'
            'normalize: Normalize audio'
            'mpg123: MP3-Support'
            'sudo: For missing permissions')
source=(http://ftp.de.debian.org/debian/pool/main/b/${pkgname}/${pkgname}_${pkgver}.orig.tar.gz)
sha256sums=('68ff1f5cd868bd98e3ed725d929e21099cc99ad01aa00d590f3838acba358a04')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

# let it install in the correct location
  sed -i 's|bblib=${lib}/lib|bblib=${lib}|' Install.sh

# install
  ./Install.sh --prefix=${pkgdir}/usr

# fix executable
  sed -i 's|'$pkgdir'||' ${pkgdir}/usr/bin/$pkgname

# remove useless docs (changelog and such)
  rm -rf ${pkgdir}/usr/lib/Bashburn/docs
}
