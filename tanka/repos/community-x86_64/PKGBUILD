# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=tanka
pkgver=0.17.1
pkgrel=1
pkgdesc="Flexible, reusable and concise configuration for Kubernetes"
arch=('x86_64')
url="https://github.com/grafana/tanka"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/grafana/tanka/archive/v${pkgver}.tar.gz")
sha512sums=('b00c36e1d695c413e81976a5a2e95d1af5a2350b4110819a32c1efffe9cd3b973ed233562ed34a80dfb4f1c3034a1835774b62819b39fa469eeb9b01d808e56e')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/grafana "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/grafana/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/grafana/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-s -w -linkmode=external -X github.com/grafana/tanka/pkg/tanka.CURRENT_VERSION=${pkgver}" -v .
  chmod +x ./tk
}

check() {
  cd "${PACKAGE_ROOT}"
  go test -v ./...
}

package() {
  cd "${PACKAGE_ROOT}/cmd/tk/"
  install -Dsm755 ./tk "${pkgdir}/usr/bin/tk"
}
