# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.18
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python2: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('19768f1dffb036012ac992acf8690b0875815f1abc91c82150b423096b2f796dbe0fa84ecafe991639b3bb96daa9b332dcac2330866a8d84a1f127f4188fc269')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
