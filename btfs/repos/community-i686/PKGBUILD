# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.15
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('i686' 'x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python2: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('c95ce2161d5493a3145b5381af83f4c1990f6036a554c5571efdb490d6bac217163b52369cce0c50885eaad0a0a8139e798d25eb55722438535c0241546e50fc')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
