# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: Torin Daniel <torindan@gmail.com>
# Contributor: Diep Pham Van <imeo@favadi.com>

pkgname=fbpanel
pkgver=6.1
pkgrel=5
pkgdesc="NetWM compliant desktop panel"
arch=('i686' 'x86_64')
url="http://fbpanel.sourceforge.net/"
depends=('gtk2')
license=('GPL')
source=("http://downloads.sourceforge.net/fbpanel/fbpanel-${pkgver}.tbz2"
        "battery_fix.patch")
md5sums=('80ca0c64195b30587cfcb8c2cd9887a0'
         'dc1db8eee1be899725f350eb36793ea4')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/battery_fix.patch
  ./configure --prefix=/usr --libexecdir=/usr/lib
  echo "LDFLAGSX += -lX11 -lm" >>config.mk
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
