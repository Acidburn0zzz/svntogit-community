# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.3
pkgrel=1
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment java-runtime)
makedepends=(git)
optdepends=('rlwrap: for readline support'
            'leiningen-completions: completion scripts')
source=("$url/releases/download/$pkgver/leiningen-$pkgver-standalone.zip"{,.asc}
        "git+$url#commit=d4aec407c677a9672e2b0150c31e913bc05c429a") # tag: 2.9.3
noextract=("leiningen-$pkgver-standalone.zip")
validpgpkeys=('20242BACBBE95ADA22D0AFD7808A33D379C806C3') # Phil Hagelberg <phil@hagelb.org>
sha256sums=('23e1df18bc97226d570f47335a8d543e1b759ea303544ea57d5309be3dedcbbb'
            'SKIP'
            'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein-pkg "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.zip" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
# vim: ts=2 sw=2 et:
