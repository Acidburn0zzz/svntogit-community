# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=fflas-ffpack
pkgver=2.4.2
pkgrel=1
pkgdesc="A library for dense linear algebra over word-size finite fields"
arch=(x86_64) # pkgconfig file is arch-specific
url="https://linbox-team.github.io/fflas-ffpack/"
license=(LGPL)
depends=(givaro cblas lapack)
source=("https://github.com/linbox-team/fflas-ffpack/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('0b470fc986ee3041bd757e1b2e8808732cc547ba37196a97ab32b746af618daa')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --enable-precompilation --with-blas-libs="-lblas -lcblas -llapack" \
    --disable-sse3 --disable-ssse3 --disable-sse41 --disable-sse42 --disable-avx --disable-avx2 --disable-fma
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
