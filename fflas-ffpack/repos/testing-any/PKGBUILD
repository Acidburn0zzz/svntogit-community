# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=fflas-ffpack
pkgver=2.2.1
pkgrel=5
pkgdesc="A library for dense linear algebra over word-size finite fields"
arch=(any)
url="http://linbox-team.github.io/fflas-ffpack/"
license=(LGPL)
makedepends=(givaro cblas lapack)
source=("https://github.com/linbox-team/fflas-ffpack/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('4aa8d77f233774ed29c4be84e43190dd')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-blas-libs="-lcblas -llapack -lgomp" --disable-avx
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
