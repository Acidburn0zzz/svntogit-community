# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=fbreader
pkgver=0.99.4
pkgrel=1
pkgdesc="An e-book reader for Linux"
arch=('i686' 'x86_64')
url="http://www.fbreader.org/"
license=('GPL')
depends=('fribidi' 'bzip2' 'curl' 'qt' 'liblinebreak' 'sqlite')
makedepends=('ccache')
source=(http://www.fbreader.org/files/desktop/fbreader-sources-$pkgver.tgz)
md5sums=('ed13a9c3aeb68aaa98a1dd6f7947a31d')

build() {
  export TARGET_ARCH=desktop
  export UI_TYPE=qt4
  export TARGET_STATUS=release
  cd $srcdir/fbreader-$pkgver
  sed -i 's|-lunibreak|-llinebreak|g' makefiles/config.mk zlibrary/text/Makefile
  make ZLSHARED=no MOC=/usr/bin/moc
}

package() {
  cd $srcdir/fbreader-$pkgver
  make ZLSHARED=no DESTDIR=$pkgdir install
}
