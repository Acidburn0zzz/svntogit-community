# $Id$
# Contributor: Aaron Schaefer <aaron@elasticdog.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=dar
pkgver=2.3.10
pkgrel=2
pkgdesc='A full featured command-line backup tool, short for Disk ARchive'
arch=('i686' 'x86_64')
url='http://dar.linux.free.fr/'
license=('GPL')
depends=('attr' 'bzip2' 'gcc-libs' 'openssl' 'zlib')
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('f134276bb9dc761dbb318c5511e65833')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --prefix=/usr --sysconfdir=/etc 
  make 
}
package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -d $pkgdir/usr/share/doc/$pkgname
  make DESTDIR="$pkgdir" install 
  cd $pkgdir/usr/share/$pkgname
  mv *.html  $pkgdir/usr/share/doc/$pkgname
  rm -r man
  for _i in html mini-howto samples 
  do
    mv ${_i} $pkgdir/usr/share/doc/$pkgname
  done
}
