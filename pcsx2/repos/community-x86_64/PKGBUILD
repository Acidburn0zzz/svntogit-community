# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.7.2243
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)
depends=(
  gcc-libs
  glibc
  libaio
  libcap
  libfmt.so
  libgdk-3.so
  libgdk_pixbuf-2.0.so
  libgl
  libglib-2.0.so
  libgobject-2.0.so
  libgtk-3.so
  libpcap.so
  libpng
  libpulse.so
  libryml.so
  libsamplerate.so
  libudev.so
  libx11
  libxcb
  libxml2
  libxrandr
  sdl2
  soundtouch
  wxgtk3
  wxgtk-common
  xz
  zlib
)
makedepends=(
  cmake
  git
  glslang
  ninja
  png++
  vulkan-headers
)
_tag=8db0b52821fafabcc556b3669e217bc9c91e5805
source=(
  git+https://github.com/PCSX2/pcsx2.git#tag=${_tag}
  git+https://github.com/mozilla/cubeb.git
  git+https://github.com/KhronosGroup/glslang.git
  git+https://github.com/ocornut/imgui.git
  git+https://github.com/rtissera/libchdr.git
  pcsx2-system-libs.patch
)
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        '29c8f279f96c67b3a35eb39440c6dfa134ef1cda36026f69f7b9931c59700e61431d7b38002bd811f6e38089296128a16c737e1c0eb2120b342790089c1e4017')
install=pcsx2.install

prepare() {
  cd pcsx2
  patch -Np1 -i ../pcsx2-system-libs.patch
  cd 3rdparty
  for submodule in cubeb/cubeb glslang/glslang imgui/imgui libchdr/libchdr; do
    git submodule init ${submodule}
    git config submodule.${submodule}.url ../../${submodule#*/}
    git submodule update ${submodule}
  done
}

pkgver() {
  cd pcsx2
  git describe --tags | sed 's/^v//'
}

build() {
  cmake -S pcsx2 -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
    -DCUBEB_API=ON \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DDISABLE_BUILD_DATE=ON \
    -DDISABLE_PCSX2_WRAPPER=ON \
    -DDISABLE_SETCAP=ON \
    -DENABLE_TESTS=OFF \
    -DLTO_PCSX2_CORE=ON \
    -DPACKAGE_MODE=ON \
    -DUSE_SYSTEM_YAML=ON \
    -DUSE_VTUNE=OFF \
    -DUSE_VULKAN=ON \
    -DXDG_STD=ON \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
    -Wno-dev
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}

# vim: ts=2 sw=2 et:
