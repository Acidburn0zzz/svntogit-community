# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.4.0
pkgrel=10
pkgdesc='A Sony PlayStation 2 emulator'
arch=(x86_64)
url=https://www.pcsx2.net
license=(
  GPL2
  GPL3
  LGPL2.1
  LGPL3
)
depends=(
  lib32-glew
  lib32-libaio
  lib32-libcanberra
  lib32-libjpeg-turbo
  lib32-libpulse
  lib32-nvidia-cg-toolkit
  lib32-portaudio
  lib32-sdl2
  lib32-soundtouch
  lib32-wxgtk2
)
makedepends=(
  cmake
  git
  png++
)
source=(
  git+https://github.com/PCSX2/pcsx2.git#tag=8f74b640f01d8d00030254b25742bdd002ea18c8
  pcsx2-gcc6.patch
)
sha256sums=(
  SKIP
  68364f4e571836c96674061a989934d0f551f6a142e33816c791de980d140e00
)

pkgver() {
  cd pcsx2

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd pcsx2

  patch -Np1 -i ../pcsx2-gcc6.patch
}

build() {
  cmake -S pcsx2 -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_TOOLCHAIN_FILE=cmake/linux-compiler-i386-multilib.cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_LIBRARY_PATH=/usr/lib32 \
    -DGAMEINDEX_DIR=/usr/share/pcsx2 \
    -DPLUGIN_DIR=/usr/lib32/pcsx2 \
    -DDISABLE_ADVANCE_SIMD=ON \
    -DEXTRA_PLUGINS=ON \
    -DGLSL_API=ON \
    -DPACKAGE_MODE=ON \
    -DREBUILD_SHADER=ON \
    -DXDG_STD=ON \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config32
  make -C build
}

package() {
  make DESTDIR="${pkgdir}" -C build install
}

# vim: ts=2 sw=2 et:
