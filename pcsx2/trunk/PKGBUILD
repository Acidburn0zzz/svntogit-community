# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: vEX <vex@niechift.com>

pkgname=pcsx2
pkgver=1.1.0
_rev=5674
pkgrel=1
pkgdesc='A Sony PlayStation 2 emulator'
arch=('i686' 'x86_64')
url='http://www.pcsx2.net'
license=('GPL')
makedepends=('cmake' 'sparsehash')
if [[ $CARCH == "i686" ]]; then
  depends=('glew' 'libaio' 'libcanberra' 'libjpeg-turbo' 'nvidia-cg-toolkit' 'portaudio' 'sdl' 'soundtouch' 'wxgtk2.8')
elif [[ $CARCH == "x86_64" ]]; then
  makedepends+=('gcc-multilib' 'lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo' 'lib32-mesa-libgl' 'lib32-portaudio' 'lib32-sdl' 'lib32-soundtouch' 'lib32-wxgtk2.8')
fi
options=('!emptydirs')
source=("http://code.google.com/p/pcsx2/downloads/detail?name=${pkgname}-${pkgver}-r${_rev}-lnx-sources.tar.bz2"
        'gcc48.patch')
sha256sums=('3f6067c3ea23b2afa4d4c411f3121d86612c310b1c7a44413577cf5727e498bc'
            '57204299c89d98f900af2e6c98fd3af0460d9095d1046f4d67772b9b75dc894b')

build() {
  cd ${pkgname}.snapshot-${_rev}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  if [[ $CARCH == "i686" ]]; then
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -D{GLSL_API,PACKAGE_MODE,REBUILD_SHADER,XDG_STD}=TRUE -DPLUGIN_DIR='/usr/lib/pcsx2' -D{GAMEINDEX,GLSL_SHADER}_DIR='/usr/share/pcsx2' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc-2.8'
  elif [[ $CARCH == "x86_64" ]]; then
    export CC='gcc -m32'
    export CXX='g++ -m32'
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
    cmake .. -DCMAKE_INSTALL_PREFIX='/usr' -DCMAKE_BUILD_TYPE='Release' -D{GLSL_API,PACKAGE_MODE,REBUILD_SHADER,XDG_STD}=TRUE -DPLUGIN_DIR='/usr/lib32/pcsx2' -D{GAMEINDEX,GLSL_SHADER}_DIR='/usr/share/pcsx2' -DwxWidgets_CONFIG_EXECUTABLE='/usr/bin/wx-config32-2.8' -DwxWidgets_wxrc_EXECUTABLE='/usr/bin/wxrc32-2.8' -DCMAKE_LIBRARY_PATH='/usr/lib32'
  fi

  make
}

package() {
  cd ${pkgname}.snapshot-${_rev}/build

  if [[ $CARCH == "x86_64" ]]; then
    depends=('lib32-glew' 'lib32-libaio' 'lib32-libcanberra' 'lib32-libjpeg-turbo' 'lib32-nvidia-cg-toolkit' 'lib32-portaudio' 'lib32-soundtouch' 'lib32-wxgtk2.8')
  fi

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
