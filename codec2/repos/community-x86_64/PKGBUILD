# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=codec2
pkgver=0.8
pkgrel=1
epoch=1
pkgdesc='Low bit rate open source speech codec, and FDMDV HF modem'
arch=('x86_64')
license=('GPL2')
url='https://rowetel.com/codec2.html'
depends=('portaudio' 'hamlib' 'wxgtk3' 'speex')
makedepends=('cmake')
source=("https://hobbes1069.fedorapeople.org/freetel/$pkgname/$pkgname-$pkgver.tar.xz")
sha512sums=('dd10ca2d5cb18bef2b1e363d005b63ed3d0ed4b829ed172ec61f2f678460356f8cadb578d9fe4adfff49c18991e0396b0bc892c9e88b53acf98c2412e731d4f9')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DCMAKE_INSTALL_LIBDIR:PATH=lib \
  	-DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}
