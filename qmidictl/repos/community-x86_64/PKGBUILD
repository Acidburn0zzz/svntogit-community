# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.2
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=('x86_64')
url="https://qmidictl.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('cmake' 'qt6-tools')
optdepends=(
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('402c56856dc7a46a2b55c526370370a570259ba94374f697c79edac6edf5b7c28a422a1501b49c50865e72189508560f9c9a79eba4181648c903cd27f78f796b')
b2sums=('b63ec3bcbbe34bd91c3f9a77f225040453b35ef85e7b3e98bd23ab70ce7a1a461319c1f1614bfbf49492ae61da0799d9ba5a12630ce1950e5a51dad3789bdec5')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
