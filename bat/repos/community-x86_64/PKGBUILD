# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.13.0
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=(x86_64)
url='https://github.com/sharkdp/bat'
license=(APACHE MIT)
makedepends=(clang cmake git rust)
depends=(libssh2)
source=("git+$url#commit=5edd8260e8fef887bd4c7e39c411dfe843d03894") # tag: v0.13.0
sha256sums=('SKIP')

build() {
  cd $pkgname
  cargo build --release
}

package() {
  cd $pkgname

  # Package the executable
  install -Dm755 target/release/$pkgname "$pkgdir/usr/bin/$pkgname"

  # Package the man page
  find "$srcdir" -name bat.1 -exec install -Dm644 {} \
    "$pkgdir/usr/share/man/man1/bat.1" \;

  # Package the license files
  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  # Package the fish completion file (already included in fish)
  #find "$srcdir" -name bat.fish -exec install -Dm644 {} \
  #  "$pkgdir/usr/share/fish/completions/bat.fish" \;
}

# getver: github.com/sharkdp/bat/releases/latest
# vim: ts=2 sw=2 et:
