# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.4.1
pkgrel=2
pkgdesc='cat clone with syntax highlighting and Git integration'
arch=('x86_64')
url='https://github.com/sharkdp/bat'
license=('MIT' 'APACHE')
makedepends=('cmake' 'rust')
depends=('curl' 'libssh2' 'oniguruma')
conflicts=('bacula-bat')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('3ae66854da59d691b8740672708a2e7f2f240c76e8a00283f59a6e39127e4583')

build() {
  cd "$pkgname-$pkgver"

  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 target/release/$pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim: ts=2 sw=2 et:
