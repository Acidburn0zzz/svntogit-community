# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagetex
pkgver=3.0
pkgrel=1
pkgdesc='Allows to embed code, results of computations, and plots from SageMath into LaTeX documents'
arch=('any')
url='https://github.com/dandrake/sagetex'
license=('GPL2')
depends=('sagemath' 'texlive-core')
makedepends=('python2')
source=("https://github.com/dandrake/sagetex/archive/v$pkgver.tar.gz")
install=$pkgname.install
md5sums=('5bdc069b0b55f89756b59cdb5d8a0f20')

prepare() {
  cd $pkgname-$pkgver

# use python2
  find -name '*.py' | xargs sed -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' -i
}

package() {
  cd $pkgname-$pkgver

  python2 setup.py install --root "$pkgdir" --optimize=1
}
