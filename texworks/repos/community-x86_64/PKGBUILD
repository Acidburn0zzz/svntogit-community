# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=texworks
pkgver=0.6.6
pkgrel=1
pkgdesc='Unicode-based, TeX-aware editor with integrated PDF viewer'
arch=('x86_64')
url='https://www.tug.org/texworks/'
license=('GPL')
depends=('hunspell' 'libsynctex' 'poppler-qt6' 'qt6-5compat' 'qt6-base'
         'qt6-declarative' 'qt6-tools' 'libpoppler-qt6.so')
makedepends=('cmake' 'lua' 'python')
checkdepends=('gsfonts' 'poppler-data' 'xorg-server-xvfb')
optdepends=('gsfonts: correctly display PDF files without embedded fonts'
            'lua: Lua scripts support'
            'poppler-data: correctly display certain CJK documents'
            'python: Python scripts support'
            'texlive-core: typeset TeX documents')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TeXworks/texworks/archive/release-$pkgver.tar.gz")
sha512sums=('65300dfbe4e0e2f3b387c868e343f6caeff4602b47afee5a02cce82e0e23b74fe559b4b54fb65d778748de56f38581305a9b3a53d743c957b7dc14ecce4546d1')

build() {
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DCMAKE_BUILD_TYPE=None \
	      -DCMAKE_CXX_FLAGS="$CXXFLAGS $CPPFLAGS" \
	      -DQT_DEFAULT_MAJOR_VERSION=6 \
	      -DTW_BUILD_ID='Arch Linux' \
	      -DWITH_PYTHON=ON \
	      -B build \
	      -S "texworks-release-$pkgver"
	make -C build
}

check() {
	cd build
	xvfb-run ctest --output-on-failure
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
