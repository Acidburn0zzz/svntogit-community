# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.10.13
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'flake8' 'python-pytest-localserver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('22c9d133927fa2a28525e718f73ca36157ec82a639f47f315ff46e879db5394593ebfe992c416c4be2956315570078c0ef774e91038198e9368c6549488e7d48')

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
