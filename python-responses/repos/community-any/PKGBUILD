# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-responses
pkgname=('python-responses' 'python2-responses')
pkgver=0.7.0
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-cookies' 'python2-cookies' 'python-six' 'python2-six' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'flake8' 'python2-flake8')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('cd9886614e4335a2217c5c0fcf98dbe882f0e5ea8b34173d4fb5498af4519f7dfba6701e8a2df83240bab4a58d876e320f8c2f0cc6ca304dc7167b92d4275acf')

prepare() {
  cp -a responses-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py pytest
}

package_python-responses() {
  depends=('python-requests' 'python-cookies' 'python-six')

  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-responses() {
  depends=('python2-requests' 'python2-cookies' 'python2-six' 'python2-mock')

  cd responses-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
