# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-responses
pkgname=('python-responses' 'python2-responses')
pkgver=0.10.6
pkgrel=2
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-biscuits' 'python2-cookies' 'python-six' 'python2-six' 'python2-mock')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-cov'
              'python2-pytest-cov' 'flake8' 'python2-flake8' 'python-pytest-localserver'
              'python2-pytest-localserver')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('7fee4150de9b477ca2fb01e71ee9dffae0e3e2138502af54c39bb9291d950d5feb6a6f8db3e37a8a58b6561d625ae8e42a90921463ecb0c5a0f9213466047334'
            '828e675cc27cf9c58b8e453053c8a4a12e560f07c77fa80aacd57decb2a8abb0ff9e91afdee11ff132bf15be1ae45c90b0ca09fdcdf97617532f835909f1ba2d')

prepare() {
  patch -Np1 -d responses-$pkgver <pytest5.patch
  cp -a responses-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest

  cd "$srcdir"/responses-$pkgver-py2
  python2 setup.py pytest
}

package_python-responses() {
  depends=('python-requests' 'python-biscuits' 'python-six')

  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-responses() {
  depends=('python2-requests' 'python2-cookies' 'python2-six' 'python2-mock')

  cd responses-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
