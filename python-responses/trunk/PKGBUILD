# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.10.10
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'flake8' 'python-pytest-localserver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz")
sha512sums=('7a33529741e211ddf204543cd3490b8502fe8eb3d26f0b5956ce02d6a0e0273697d65d5eacf85ef7fc6470239daeb66a949e1c5200446792d8b8d24a31c88025')

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
