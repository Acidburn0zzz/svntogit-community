# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=1.0.2
pkgrel=1
pkgdesc="Molecule Docker Driver"
arch=('any')
url="https://github.com/ansible-community/molecule-docker"
license=('MIT')
depends=('molecule' 'python-ansible-compat' 'python-docker' 'python-selinux')
makedepends=('python-setuptools-scm' 'python-setuptools-scm-git-archive')
checkdepends=('python-pytest' 'python-pytest-helpers-namespace')
optdepends=('docker: for using a local docker setup')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6e21ded1aba056e8cbe3696a598b6cf627bb62559a8d2d6595cf9822988c9a0f0861b9f6ea2955e65a41cc7e254813cacce1f44d4c4ce8d2d3828d7ef3e55c03')
b2sums=('f04ddd69e0097aebd7f2c1a52c73d7d708cf602621d78d37bfe809268246f2a05d69cb4b4ec5e2d19244a1b35c1edbfb82549c98e02f12f469745003888809cb')

prepare() {
  cd "$pkgname-$pkgver"
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -e '/error/d' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable tests that require the driver to be installed for the system
  # version of molecule that we are testing with
  pytest -v --ignore "src/molecule_docker/test/test_func.py"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
