# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule-docker
pkgver=0.2.4
pkgrel=1
pkgdesc="Molecule Docker Driver"
arch=('any')
url="https://github.com/ansible-community/molecule-docker"
license=('MIT')
depends=('molecule' 'python-docker' 'python-selinux')
makedepends=('python-setuptools_scm')
checkdepends=('python-pytest' 'python-pytest-helpers-namespace')
optdepends=('docker: for using a local docker setup')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c53072439b935077dc116b57026f0d84bd58cc5c6ea9b77400e6da93a0cd68b585791e4bcd4b354145ac97d157b639220d43dad9e86db324b4dd2956f845ea16')
b2sums=('b33dd0fd940464e0cec7cd334d7b8fc9d4e545cf286761008d4f9997c05c4ab6d459a2e556a8e3a4e0733724606cdcc8543450038f632c97002f3cbe77e5d5e6')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # NOTE: we can only run the detection test, as the other tests require running docker
  pytest -v "${pkgname//-/_}/test/test_driver.py"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
