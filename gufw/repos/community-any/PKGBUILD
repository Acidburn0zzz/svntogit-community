# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>

pkgname=gufw
pkgver=11.04.2
pkgrel=1
pkgdesc="An easy, intuitive way to manage your Linux firewall"
arch=('any')
url="http://gufw.tuxfamily.org/index.html"
license=('GPL3')
depends=('dbus' 'gksu' 'hicolor-icon-theme' 'polkit' 'pygtk' 'sudo' 'ufw')
makedepends=('patch')
optdepends=('gnome-python')
changelog=$pkgname.changelog
source=(http://archive.ubuntu.com/ubuntu/pool/universe/g/gui-ufw/gui-ufw_${pkgver}.orig.tar.gz
        $pkgname-$pkgver-execute.patch)
install=$pkgname.install
sha256sums=('7ccd686efd95ecd2a84064026fc067f71f913a195bd83456024d5d6effd3021e'
            'f2b1e65cff0444c7dfee86bc646b0a3282d3e4f27f13cb9d5d4f7aae0b32a02b')

package() {
  cd ${srcdir}/gui-ufw-${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print); do
      sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
      sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py build install --root=${pkgdir}

  cd ${pkgdir}
  patch -Np0 -i ${srcdir}/$pkgname-$pkgver-execute.patch
}
