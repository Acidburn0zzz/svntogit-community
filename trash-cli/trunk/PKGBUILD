# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Renato Garcia <fgarcia.renato@gmail.com>

pkgname=trash-cli
pkgver=0.21.5.25
pkgrel=1
pkgdesc="Command line trashcan (recycle bin) interface"
arch=('any')
url="https://github.com/andreafrancia/${pkgname}"
license=('GPL')
depends=('python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('1c26cbaa8daac275768c5f46c52d693af76050a6ee6d30b10a169059b61ee270')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # don't depend on thirdparty copies of the stdlib
    find tests -type f -name "*.py" -exec \
        sed -i 's/^import mock$/from unittest import mock/;s/from mock /from unittest.mock /' {} +

    # don't install dev tooling? https://github.com/andreafrancia/trash-cli/issues/205
    rm trashcli/bump.py tests/test_bump.py
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python -m pytest
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
