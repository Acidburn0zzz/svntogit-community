# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
pkgver=2.6.0
_commit=e0b32c19e4dd641f4edf74fc3fbdba3d86ca9784
pkgrel=2
pkgdesc='Nextcloud desktop client'
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
depends=(openssl sqlite qtkeychain qt5-svg qt5-webengine xdg-utils)
makedepends=(doxygen extra-cmake-modules kio python-sphinx qt5-tools git cmocka)
optdepends=(
  'kio: integration with Dolphin'
  'nemo-python: integration with Nemo'
  'python-nautilus: integration with Nautilus'
  'python2-caja: integration with Caja'
)
source=("$pkgname::git+https://github.com/nextcloud/desktop.git?signed#commit=$_commit"
        issue1458.patch::https://github.com/nextcloud/desktop/commit/481d8d3a0bcd26a3fd2b0d4fc6fb009260092bb1.patch)
validpgpkeys=(
  A26B951528EA1BA1678C7AE5D406C75CEE1A36D6  # one of keys controlled by github.com/camilasan
)
sha256sums=('SKIP'
            '8e3129fbbda282d5b0fbc1f3d495338c18fd19f639301647eb5f853b713c369c')
backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  # tmpdir for check()
  mkdir tmpdir $pkgname/build

  # Use system GNUInstallDirs.cmake so that we can benefit from
  # https://gitlab.kitware.com/cmake/cmake/merge_requests/3735
  rm -v $pkgname/cmake/modules/GNUInstallDirs.cmake

  cd $pkgname
  patch -Np1 -i ../issue1458.patch
}

build() {
  cd $pkgname/build

  # bundled breakpad in libcrashreporter-qt submodule is too old and does not build with glibc >= 2.26
  # Upstream fix: https://github.com/google/breakpad/commit/bddcc58860f522a0d4cbaa7e9d04058caee0db9d
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_SHIBBOLETH=1 \
    -DWITH_CRASHREPORTER=OFF \
    -DUNIT_TESTING=ON \
    ..

  make
  # TODO: fix installation of PDF and HTML documents
  # WIP at https://github.com/yan12125/desktop/tree/doc-install-path
  make doc-man
}

check() {
  cd $pkgname/build

  # Tests fail if $TMPDIR is too small; specify an alternative for machines
  # with a small /tmp partition.
  TMPDIR="$srcdir/tmpdir" make test ARGS="-V"
}

package() {
  cd $pkgname/build

  make DESTDIR="$pkgdir" install
}
