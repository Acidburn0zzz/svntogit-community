# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
pkgver=2.5.0
pkgrel=4
pkgdesc='Nextcloud desktop client'
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
depends=(sqlite qtkeychain qt5-svg qt5-webengine qt5-webkit xdg-utils)
makedepends=(doxygen extra-cmake-modules kio python-sphinx qt5-tools)
optdepends=(
  'kio: integration with Dolphin'
  'nemo-python: integration with Nemo'
  'python-nautilus: integration with Nautilus'
  'python2-caja: integration with Caja'
)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/nextcloud/desktop/archive/v$pkgver.tar.gz"
        fix-man-pages.patch::https://github.com/nextcloud/desktop/commit/d6feb984c3e0d6d3c17868d36da8380bcc05e55b.patch)
sha256sums=('4d639f43e49fd4367cd1b99bf21aecb2eac3bd89a7b0d96c7d2a0975baad6528'
            '1203cdb5480ef52bfe0ae6f1b9dc13f8e53ed3272fd691400c50897310dd28c3')
backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  cd desktop-$pkgver

  mkdir -p build

  # https://github.com/nextcloud/desktop/issues/699
  patch -Np1 -i ../fix-man-pages.patch
}

build() {
  cd desktop-$pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..

  make
  make doc-man
}

package() {
  cd desktop-$pkgver/build

  make DESTDIR="$pkgdir" install
}
