# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
pkgver=2.5.3
_commit=477614b8c954836327eb136be1c035627f09760b
pkgrel=2
pkgdesc='Nextcloud desktop client'
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
depends=(openssl sqlite qtkeychain qt5-svg qt5-webengine xdg-utils)
makedepends=(doxygen extra-cmake-modules kio python-sphinx qt5-tools git)
optdepends=(
  'kio: integration with Dolphin'
  'nemo-python: integration with Nemo'
  'python-nautilus: integration with Nautilus'
  'python2-caja: integration with Caja'
)
source=("$pkgname::git+https://github.com/nextcloud/desktop.git?signed#commit=$_commit")
# one of keys controlled by @camilasan; see https://api.github.com/users/camilasan/gpg_keys
validpgpkeys=(A26B951528EA1BA1678C7AE5D406C75CEE1A36D6)
sha256sums=('SKIP')
backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  cd $pkgname

  mkdir -p build
}

build() {
  cd $pkgname/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DNO_SHIBBOLETH=1 \
    ..

  make
  make doc-man
}

package() {
  cd $pkgname/build

  make DESTDIR="$pkgdir" install
}
