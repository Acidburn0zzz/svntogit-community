# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=nzbget
pkgver=0.7.0
pkgrel=4
pkgdesc="Download from Usenet using .nzb files"
arch=('x86_64' 'i686')
url="http://nzbget.sourceforge.net/"
license=('GPL')
depends=('gnutls' 'libpar2' 'libxml2' 'ncurses')
makedepends=('autoconf' 'automake')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('b798bbcc9f6816b4e19a95f74df067440f7ad49e1c16a3f33f99bddf7916771e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-parcheck
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/usr/share/$pkgname"
  install -m644 -t "$pkgdir/usr/share/$pkgname" \
    AUTHORS postprocess-example.sh postprocess-example.conf \
    ChangeLog nzbget.conf.example README
}

# vim:set ts=2 sw=2 et:
