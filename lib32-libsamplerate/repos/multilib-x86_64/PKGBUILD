# $Id: PKGBUILD 79156 2010-05-01 02:26:45Z eric $
# Maintainer: Eric Belanger <eric@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

_pkgbasename=libsamplerate
pkgname=lib32-$_pkgbasename
pkgver=0.1.7
pkgrel=1
pkgdesc="Secret Rabbit Code - aka Sample Rate Converter for audio (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/SRC/index.html"
license=('GPL')
depends=('lib32-libsndfile' $_pkgbasename)
makedepedns=('gcc-multilib')
options=('!libtool')
source=(http://www.mega-nerd.com/SRC/libsamplerate-${pkgver}.tar.gz)
md5sums=('ad093e60ec44f0a60de8e29983ddbc0f')
sha1sums=('98a52392eb97f9ba724ca024b3af29a8a0cc0206')

build() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}
  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
