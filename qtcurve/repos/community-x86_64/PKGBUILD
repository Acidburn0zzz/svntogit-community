# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=qtcurve
pkgname=('qtcurve-utils' 'qtcurve-gtk2' 'qtcurve-qt4' 'qtcurve-qt5')
pkgver=1.9.0
pkgrel=1
pkgdesc='A configurable set of widget styles for KDE and Gtk'
arch=('i686' 'x86_64')
url='https://github.com/KDE/qtcurve/'
license=('LGPL')
groups=('qtcurve')
makedepends=('extra-cmake-modules' 'gtk2' 'qt4' 'kdelibs4support' 'kdoctools' 'frameworkintegration' 'python' 'kdesignerplugin')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/KDE/qtcurve/archive/$pkgver.tar.gz")
sha256sums=('1024503be314596f8047f0ab0efde99a01959bacfaffd0d1305212be53d34b85')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgbase-$pkgver \
           -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false
  make
}

package_qtcurve-utils() {
  depends=('libx11' 'gcc-libs')
  cd build/lib/utils
  make DESTDIR="$pkgdir" install
}

package_qtcurve-gtk2() {
  depends=('qtcurve-utils' 'gtk2')
  cd build/gtk2
  make DESTDIR="$pkgdir" install
  cd ../lib/cairo
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt4() {
  depends=('qtcurve-utils' 'qt4')
  cd build/qt4
  make DESTDIR="$pkgdir" install
}

package_qtcurve-qt5() {
  depends=('qtcurve-utils' 'kdelibs4support' 'frameworkintegration')
  cd build/qt5
  make DESTDIR="$pkgdir" install
}
