# $Id$ 
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Soup <soup a.t soultrap d.o.t n.e.t>
# Contributor: Vladimir Koshelenko <koshelenko@rndavia.ru>

pkgname=start-stop-daemon
pkgver=1.15.8.12
pkgrel=1
pkgdesc='Start and stop system daemon programs'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://packages.qa.debian.org/d/dpkg.html'
depends=('glibc')
source=("http://ftp.debian.org/debian/pool/main/d/dpkg/dpkg_$pkgver.tar.bz2")
md5sums=('0cd6f20a574d0df31298e70fc3b26173')

build() {
  cd dpkg-$pkgver
  ./configure --without-dselect --without-update-alternatives --without-install-info
  (cd lib && make)
  cd utils && make
}

package() {
  cd dpkg-$pkgver
  install -D -m 755 utils/start-stop-daemon "$pkgdir/usr/bin/start-stop-daemon"
  install -D -m 644 man/start-stop-daemon.8 "$pkgdir/usr/share/man/man8/start-stop-daemon.8"
}

# vim:set ts=2 sw=2 ft=sh et:
