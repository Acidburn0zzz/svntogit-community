# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=shairport-sync
pkgver=2.2.2
pkgrel=2
pkgdesc='Emulates an AirPort Express for the purpose of streaming music from iTunes and compatible iPods and iPhones'
url='https://github.com/mikebrady/shairport-sync'
arch=(i686 x86_64)
license=(GPL)
depends=(openssl avahi libsoxr popt alsa-lib)
source=(https://github.com/mikebrady/shairport-sync/archive/$pkgver.zip
        shairport-sync.service
        shairport-sync.conf
        remove_rc.patch)
sha1sums=('5360f4bd6a6b9678a8d7d3c2006db2146cda2d69'
          '5f3f8d8c64d90603d9efc2faed4c80f9221a2f25'
          'f16217da0fe559616db36e37214853c42e7f24ae'
          '48174838de1ef4672bd71511753f05fd845b7137')

prepare() {
  cd shairport-sync-$pkgver
  patch -p1 < ../remove_rc.patch
}

build() {
  cd shairport-sync-$pkgver

  autoreconf -i -f
  ./configure --prefix=/usr --with-alsa --with-avahi --with-ssl=openssl --with-soxr
  make
}

package() {
  cd shairport-sync-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/shairport-sync.service "$pkgdir"/usr/lib/systemd/system/shairport-sync.service
  install -D -m644 "$srcdir"/shairport-sync.conf "$pkgdir"/etc/conf.d/shairport-sync
}
