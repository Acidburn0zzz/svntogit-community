# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-nginx
pkgver=0.38.0
pkgrel=1
pkgdesc="Nginx plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-mock' 'python-pyopenssl' 'python-pyparsing'
         'python-setuptools' 'python-zope-interface')
checkdepends=('python-pytest')
replaces=("letsencrypt-nginx")
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('4fe7fa1ad131cb9971b0cbc4934d4d4167920b16b35da310cf339ae64d46a72bd33ee0b5fc5b3177a564be627061cb1c2394a180ba42cc61c9180539372637c6'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
