# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.6.6
pkgrel=2
pkgdesc="Middleware to access a smart card using the SCard API (PC/SC)"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('hal')
makedepends=('pkgconfig')
options=('!libtool')
source=(https://alioth.debian.org/frs/download.php/3479/pcsc-lite-${pkgver}.tar.bz2
        pcscd)
md5sums=('f80d3ecd9569b71d557f283f16295c74'
         '760ed7db7300f89ea20a994f0aae9074')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --enable-usbdropdir=/usr/lib/pcsc/drivers || return 1
  make || return 1
  make DESTDIR=${pkgdir} install || return 1
  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING || return 1
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd || return 1
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
