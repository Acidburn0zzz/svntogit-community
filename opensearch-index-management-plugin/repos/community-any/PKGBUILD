# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-index-management-plugin
pkgver=1.3.0.0
pkgrel=1
_opensearchver=1.3.0
_jdkver=11
pkgdesc="OpenSearch Index Management Plugin"
arch=('any')
url="https://opensearch.org/docs/latest/im-plugin"
license=('Apache')
depends=("opensearch=${_opensearchver}")
makedepends=("java-environment=${_jdkver}" 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/index-management/archive/${pkgver}.tar.gz"
)
sha256sums=('ef8ff38efbb1f89a9d928911a0da2e7c632e2c29ce77f09360636f96bce47c9c')

build() {
  cd "index-management-${pkgver}"
  export JAVA_HOME="/usr/lib/jvm/java-${_jdkver}-openjdk"
  export PATH="/usr/lib/jvm/java-${_jdkver}-openjdk/bin:$PATH"
  export GRADLE_OPTS="-Dbuild.snapshot=false -Dopensearch.version=${_opensearchver}"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  unzip "${srcdir}/index-management-${pkgver}/build/distributions/opensearch-index-management-${pkgver}.zip"

  install -Dm644 "${srcdir}/index-management-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
