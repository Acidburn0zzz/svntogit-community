# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-index-management-plugin
pkgver=1.2.0.0
pkgrel=1
pkgdesc="OpenSearch Index Management Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/im-plugin"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/index-management/archive/${pkgver}.tar.gz"
)
sha256sums=('fd503fd1ff16cf05d6b76b21082f3174a94240d3b1d257d6fe25b857b7ac0e70')

build() {
  cd "index-management-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-index-management"
  unzip "${srcdir}/index-management-${pkgver}/build/distributions/opensearch-index-management-${pkgver}.zip"

  install -Dm644 "${srcdir}/index-management-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
