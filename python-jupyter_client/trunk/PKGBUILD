# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-jupyter_client
pkgver=7.2.0
pkgrel=1
pkgdesc="Jupyter protocol implementation and client libraries"
arch=('any')
url="https://pypi.python.org/pypi/jupyter_client"
license=('BSD')
depends=('python-ipykernel' 'python-traitlets' 'python-pyzmq'
         'python-jupyter_core' 'python-dateutil' 'python-entrypoints' 'python-nest-asyncio')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-async_generator')
#source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/jupyter_client/archive/$pkgver.tar.gz")
source=("https://files.pythonhosted.org/packages/source/j/jupyter_client/jupyter_client-$pkgver.tar.gz")
sha256sums=('00e284dd1a5ac605ead8a42ada2a97041b642c1ef6cefb30c3c415b4eb94bead')

# dep cycle with python-ipykernel

build() {
  cd jupyter_client-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd jupyter_client-$pkgver
  pytest -vv
}

package() {
  cd jupyter_client-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
