# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.2
_pkgver=0.18.3-rc1
pkgrel=2
pkgdesc='OpenStreetMap editor'
arch=('i686' 'x86_64')
url='http://merkaartor.be/'
license=('GPL2')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt5-svg' 'qt5-webkit')
makedepends=('qt5-tools')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/openstreetmap/$pkgname/archive/$_pkgver.tar.gz")
sha256sums=('57492ed2459d4d2dd25282562daf550d480be6351b35887d56e9b51909ce2014')

build() {
  cd $pkgname-$_pkgver
  lrelease src/src.pro
  qmake Merkaartor.pro PREFIX=/usr GPSDLIB=1 GEOIMAGE=1
  make
}

package() {
  cd $pkgname-$_pkgver
  make INSTALL_ROOT="$pkgdir" install
}
