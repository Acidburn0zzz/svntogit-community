# Maintainer: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>
# Contributor: niQo
pkgname=virtualbox-ose
pkgver=3.0.2
pkgrel=2
pkgdesc="Powerful x86 virtualization for enterprise as well as home use (Open Source Edition)"
url="http://www.virtualbox.org"
license=('GPL' 'custom')
arch=('i686')
provides=('virtualbox')
conflicts=('virtualbox')
depends=('virtualbox-modules' 'libxcursor' 'libidl2' 'libxslt' 'sdl' 'libxmu' 'curl')
makedepends=('libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2'
	'libxcursor' 'qt' 'libidl2' 'sdl_ttf' 'alsa-lib' 'pulseaudio'
	'hal' 'libxtst' 'xalan-c')
optdepends=('qt: For VirtualBox GUI' 
	    'libgl: For Shared OpenGL'
	    'libxt: For Shared Clipboard'
	    'pulseaudio: For PulseAudio Support'
	    'python: For Python Support')
install=virtualbox.install
source=(http://download.virtualbox.org/virtualbox/$pkgver/VirtualBox-$pkgver-OSE.tar.bz2
        '60-virtualbox.rules')
build() {
  cd "$srcdir/VirtualBox-${pkgver}_OSE"

  sed -i 's/python2.5/python2.6/g' configure
  ./configure 
  source ./env.sh
  kmk all || return 1
  cd out/linux.$BUILD_PLATFORM_ARCH/release/bin
  rm -rf sdk src tst* testcase SUPInstall SUPUninstall
  mkdir -p $pkgdir/usr/{bin,lib/virtualbox/components,share/virtualbox/nls}

  #Binaries and Wrapper with Launchers
  install -m 0755 VBox.sh "$pkgdir/usr/bin/VBox"
  ln -sf VBox "$pkgdir/usr/bin/VBoxHeadless"
  ln -sf VBox "$pkgdir/usr/bin/VBoxManage"
  ln -sf VBox "$pkgdir/usr/bin/VBoxSDL"
  ln -sf VBox "$pkgdir/usr/bin/VirtualBox"

  install -m 0755 VBoxTunctl "$pkgdir/usr/bin"

  #components
  install -m 0755 components/* -t "$pkgdir/usr/lib/virtualbox/components"

  #lib
  install -m 0644 *.so "$pkgdir/usr/lib/virtualbox"
  install -m 0644 *.gc *.r0 "$pkgdir/usr/lib/virtualbox"

  #setuid root binaries
  install -m 4755 VBoxHeadless VBoxSDL VBoxNetDHCP VBoxNetAdpCtl VirtualBox VBoxBFE \
  	-t "$pkgdir/usr/lib/virtualbox"

  #other binaries
  install -m 0755 VBoxManage VBoxSVC VBoxXPCOMIPCD VBoxSysInfo.sh xpidl VBoxTestOGL \
  	-t "$pkgdir/usr/lib/virtualbox"

  #language
  install -m 0755 nls/* -t "$pkgdir/usr/share/virtualbox/nls"

  #icon
  install -D -m 0644 VBox.png "$pkgdir/usr/share/pixmaps/VBox.png"

  #desktop
  install -D -m 0644 VirtualBox.desktop "$pkgdir/usr/share/applications/virtualbox.desktop"

  #install configuration
  mkdir -p "$pkgdir/etc/vbox"
  echo 'INSTALL_DIR=/usr/lib/virtualbox' > "$pkgdir/etc/vbox/vbox.cfg"

  #udev and licence
  install -D -m 0644 "$srcdir/VirtualBox-${pkgver}_OSE/COPYING" \
                   "$pkgdir/usr/share/licenses/virtualbox-ose/LICENSE"

  install -D -m 0644 "$srcdir/60-virtualbox.rules" \
                   "$pkgdir/etc/udev/rules.d/60-virtualbox.rules"

}
md5sums=('68e6658b8b11a509d7dec57835cba267'
         '519d32d8c2408e0ed9d643f412117644')
