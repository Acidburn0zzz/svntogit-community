# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Mort Yao <soi@mort.ninja>
# Contributor: Alex Merry <dev@randomguy3.me.uk>

pkgname=polyml
pkgver=5.8
pkgrel=2
pkgdesc='Standard ML compiler originally written in the experimental language Poly'
arch=(x86_64)
url='https://polyml.org/'
license=(LGPL)
conflicts=('mesa-demos') # /usr/bin/poly
options=(!libtool)
depends=(gmp libffi)
install=polyml.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz"
        remove-FFI_SYSV-from-abi-table.patch)
sha256sums=('6bcc2c5af91f361ef9e0bb28f39ce20171b0beae73b4db3674df6fc793cec8bf'
            'cea28038a37c646ce7f6601443726411587bfb4ace2d6179b99ef247f6ac795b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../remove-FFI_SYSV-from-abi-table.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --with-system-libffi \
    --with-gmp
  make
}

check() {
  make -C $pkgname-$pkgver -k check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# getver: polyml.org
# vim: ts=2 sw=2 et:
