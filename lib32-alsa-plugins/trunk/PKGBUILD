# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.1.8
pkgrel=1
pkgdesc="Extra alsa plugins (32-bit)"
arch=(x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib $_pkgbasename)
makedepends=(lib32-libpulse lib32-jack lib32-speex lib32-libsamplerate gcc-multilib)
optdepends=('lib32-libpulse: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/$_pkgbasename-$pkgver.tar.bz2")
sha256sums=('7f77df171685ccec918268477623a39db4d9f32d5dc5e76874ef2467a2405994')

prepare() {
  cd $_pkgbasename-$pkgver
  autoreconf -fvi
}

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-avcodec
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/share}
}
