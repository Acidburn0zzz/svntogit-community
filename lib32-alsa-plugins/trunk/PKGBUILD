# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

_pkgbasename=alsa-plugins
pkgname=lib32-$_pkgbasename
pkgver=1.0.23
pkgrel=3
pkgdesc="Extra alsa plugins (32-bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
license=('GPL')
depends=('lib32-alsa-lib' $_pkgbasename)
makedepends=('lib32-pulseaudio' 'lib32-jack' gcc-multilib 'lib32-speex')
optdepends=('lib32-pulseaudio' 'lib32-jack' 'lib32-speex')
options=('!libtool')
source=(ftp://ftp.alsa-project.org/pub/plugins/alsa-plugins-$pkgver.tar.bz2)
md5sums=('a671f8102366c5b388133e948e1c85cb')

build() {
  cd $srcdir/alsa-plugins-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-samplerate --disable-avcodec
  make
}

package() {
  cd $srcdir/alsa-plugins-$pkgver
  make DESTDIR=$pkgdir install
}
