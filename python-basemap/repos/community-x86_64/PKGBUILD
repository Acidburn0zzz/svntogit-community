# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Matthias Ellmer <ellmer.matthias@gmail.com>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgbase=python-basemap
pkgname=('python-basemap' 'python2-basemap' 'python-basemap-common')
pkgver=1.2.0
pkgrel=1
arch=('x86_64')
url="https://matplotlib.org/basemap/"
license=('MIT')
makedepends=('cython' 'python2-numpy' 'python-numpy' 'geos' 'shapelib'
             'python2-pyproj' 'python-pyproj' 'python2-setuptools' 'python-setuptools')
source=($pkgname-$pkgver.tar.gz::https://github.com/matplotlib/basemap/archive/v${pkgver}rel.tar.gz
        LICENSE)
sha256sums=('bd5bf305918a2eb675939873b735238f9e3dfe6b5c290e37c41e5b082ff3639a'
            '0deac85afb0740df7626b6d8d7aa4b8814bbe9a412fb1a0e5962895d17e63add')

_py2basever=2.7
_py3basever=3.7

prepare() {
  pushd "${srcdir}"/basemap-${pkgver}rel
# regenerate cythonized sources for python 3.7
  cythonize --force src/_geoslib.pyx
# Remove the bundled libraries and common data
  rm -r geos-*
  sed -i "s/lib64/lib/g" setup.py
  popd

  cp -a "${srcdir}"/basemap-${pkgver}rel{,-python2}
}

build() {
# Python2 Version
  cd "${srcdir}"/basemap-${pkgver}rel-python2
  python2 setup.py config
  python2 setup.py build

# Python3 Version
  cd "${srcdir}"/basemap-${pkgver}rel
  python setup.py config
  python setup.py build
}

package_python2-basemap() {
  pkgdesc="Toolkit for plotting data on map projections"
  depends=('python-basemap-common' 'python2-matplotlib' 'geos' 'shapelib' 'python2-numpy' 'python2-httplib2' 'python2-pyproj')
  optdepends=('python2-pillow: Python Imaging Library is only needed for bluemarble and warpimage methods.')

  cd "${srcdir}"/basemap-${pkgver}rel-python2

  python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  rm -fr "${pkgdir}"/usr/lib/python${_py2basever}/site-packages/mpl_toolkits/basemap/data
  ln -sf /usr/share/basemap/data "${pkgdir}"/usr/lib/python${_py2basever}/site-packages/mpl_toolkits/basemap/data
}

package_python-basemap() {
  pkgdesc="Toolkit for plotting data on map projections"
  depends=('python-basemap-common' 'python-matplotlib' 'geos' 'shapelib' 'python-numpy' 'python-httplib2' 'python-pyproj')
  optdepends=('python-pillow: Python Imaging Library is only needed for bluemarble and warpimage methods.')

  cd "${srcdir}"/basemap-${pkgver}rel

  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  rm -fr "${pkgdir}"/usr/lib/python${_py3basever}/site-packages/mpl_toolkits/basemap/data
  ln -sf /usr/share/basemap/data "${pkgdir}"/usr/lib/python${_py3basever}/site-packages/mpl_toolkits/basemap/data
}

package_python-basemap-common() {
  pkgdesc="Data files for python-basemap"
  license=('GPL2')

  cd "${srcdir}"/basemap-${pkgver}rel

  install -dm755 "${pkgdir}"/usr/share/basemap
  cp -a lib/mpl_toolkits/basemap/data/ "${pkgdir}"/usr/share/basemap/
}
