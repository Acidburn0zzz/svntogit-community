# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whelk
pkgver=2.7.1
pkgrel=3
pkgdesc="Easy access to shell commands from python"
arch=('any')
license=('custom:zlib')
url="https://pypi.python.org/pypi/whelk"
depends=('python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/seveas/whelk/archive/$pkgver.tar.gz"
        python-3.8.patch)
sha512sums=('39b73ed09a0d2442cdde5b8bae72805571a172c6acfa3f652c8cb125d3a400f2538fe6534179f95383d321e8975b6127115616372c60c736e8cd8ceba00ef093'
            'cb1f1cd86c78fce28e1515966547d66eaec9d474352b03710a83b5b4977cee650ee32501aea135c78a4065c40711ab9e8d32182ea770116c2b472fa94473ada9')

prepare() {
  cd whelk-$pkgver
  patch -Np1 -i ../python-3.8.patch
}

build() {
  cd whelk-$pkgver
  python setup.py build
}

check() {
  cd whelk-$pkgver
  python -munittest discover
}

package() {
  cd whelk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
