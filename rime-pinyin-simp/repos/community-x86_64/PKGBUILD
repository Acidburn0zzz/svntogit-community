# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-pinyin-simp
pkgver=0.0.0.20200624.1
_commit=3afc10b2a9bfef63c0010a3afab06876ea28c9de
pkgrel=1
pkgdesc="Simple pinyin input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-pinyin-simp"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-pinyin-simp/archive/$_commit/rime-pinyin-simp-$_commit.tar.gz")
sha512sums=('4effcbfd716adbef58cafdb7a5eb890f987e1236c4fec15127427361827fc9f47ccdf57e560be8b85e45bcc27fdac1998e8ddbb6c99c77f6a1d1a8e5638c4daf')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
