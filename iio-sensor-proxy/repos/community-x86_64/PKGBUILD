# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eric Lehmann <katyl@katyl.info>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Pablo Lezeta <prflr88@gmail.com>

pkgname=iio-sensor-proxy
pkgver=2.8
pkgrel=1
pkgdesc='IIO accelerometer sensor to input device proxy'
arch=('x86_64')
url='https://gitlab.freedesktop.org/hadess/iio-sensor-proxy/'
license=('GPL2')
depends=('systemd' 'libgudev' 'gtk3')
makedepends=('gtk-doc' 'gnome-common')
source=("$url/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bd0e8c0352ea8ddf116ec11bdd62c78a444443f0ab909e4e68a7d82434d45c9d4142403d204f2a60dd11996a8ba9b8f8808a6ff814df41c78a77ad189a491241')

build() {
  cd $pkgname-$pkgver

  aclocal --verbose --install --force
  gtkdocize --copy --flavour no-tmpl
  autoreconf -vif

  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/usr/share \
  	--bindir=/usr/bin \
  	--sbindir=/usr/bin \
  	--libdir=/usr/lib \
  	--libexecdir=/usr/lib \
  	--with-dbuspolicydir

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

