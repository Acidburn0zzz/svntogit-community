# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.4.0
pkgrel=1
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-packagekit' "$_pkgname")
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs' '!libtool')
install=$_pkgname.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"
        'systemd-fallback.patch'
        'arch.patch')
sha256sums=('5639cbba28caa7b3e33990f49aeed11ed9325e78e76af2175d9ccd5ff298401d'
            '82b721d0e274af15ed2770052d550d3162d2d37e5e9ee5e6fcebac1ed6cb8199'
            '727ab3e8181464af32ba604b2931b77cd6d4fbf34e10bbba99bb213eb992466e')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/arch.patch"
  patch -Np1 -i "$srcdir/systemd-fallback.patch"

  gnome-autogen.sh --prefix=/usr \
              --sysconfdir=/etc \
	      --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-settings-daemon \
	      --disable-static \
	      --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install

  _schema='org.gnome.settings-daemon.plugins.updates.gschema.xml'
  install -D -m644 "data/$_schema" "$pkgdir/usr/share/glib-2.0/schemas/$_schema"
}
