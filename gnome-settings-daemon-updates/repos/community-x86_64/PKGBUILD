# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.4.1
pkgrel=1
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gnome-packagekit' "$_pkgname")
makedepends=('intltool' 'gtk-doc' 'gnome-desktop' 'gnome-common' 'xf86-input-wacom')
options=('!emptydirs' '!libtool')
install=$_pkgname.install
url="http://www.gnome.org"
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"
        'systemd-fallback.patch'
        'evironment.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=9d7ea1d0b8b90b55f916751858a210a2e02a06b6'
        'arguments.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=00681573ad6ebeb127ec72ac61d30d69be6459f8'
        'mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=053f48b3b1b9a3b2ab0714d470da4354735d0c4b'
        'glist-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=214a832820f6448f07d438f6686267eaf8301dd1'
        'mouse-mem-leak.patch::http://git.gnome.org/browse/gnome-settings-daemon/patch/?id=09c215b42312ca5bb63b215c2f95231613b3103d'
        'arch.patch')
sha256sums=('d5d4a270ec5692cdc56a58cfb57bc7f8fa93374566912a65e78c1c3db7faf4ed'
            '82b721d0e274af15ed2770052d550d3162d2d37e5e9ee5e6fcebac1ed6cb8199'
	    '1ba33f6eadac4c54f3f08f4bce075d2bef8124f857f5af22eaee010c8d38bc3b'
	    '884a959b334f864f75dcb4eec8c137785f587c7c2ae57906f8b4e60e04c29246'
	    '3a3f8b2964406b9b3c85c05f25e677e8c9eaa221779b95826ffbc0474740decc'
	    'af3bda801befdbd3d318723ae6de3aaf7df1e16bccd84e13f15b57f2dceaefcd'
	    '43b7397e622a2c19091b3665822e9461067fb6a497b9507418a3bbb100a69e1b'
            '727ab3e8181464af32ba604b2931b77cd6d4fbf34e10bbba99bb213eb992466e')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/arch.patch"
  patch -Np1 -i "$srcdir/systemd-fallback.patch"
  patch -Np1 -i ../evironment.patch
  patch -Np1 -i ../arguments.patch
  patch -Np1 -i ../mem-leak.patch
  patch -Np1 -i ../glist-leak.patch
  patch -Np1 -i ../mouse-mem-leak.patch

  gnome-autogen.sh --prefix=/usr \
              --sysconfdir=/etc \
	      --localstatedir=/var \
	      --libexecdir=/usr/lib/gnome-settings-daemon \
	      --disable-static \
	      --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install

  _schema='org.gnome.settings-daemon.plugins.updates.gschema.xml'
  install -D -m644 "data/$_schema" "$pkgdir/usr/share/glib-2.0/schemas/$_schema"
}
