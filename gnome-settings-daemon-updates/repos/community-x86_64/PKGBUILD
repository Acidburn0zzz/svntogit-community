# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

_pkgname=gnome-settings-daemon
pkgname=$_pkgname-updates
pkgver=3.0.3
pkgrel=1
pkgdesc="Updates plugin for the GNOME Settings daemon"
arch=('i686' 'x86_64')
url="http://www.gnome.org"
license=('GPL')
depends=('gnome-packagekit' "$_pkgname" 'upower')
makedepends=('intltool' 'gtk-doc')
options=('!emptydirs' '!libtool')
install=$_pkgname.install
source=("http://ftp.gnome.org/pub/gnome/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.xz"
        'arch.patch')
sha256sums=('9c6e3bb5c4b3a98ab83a917b7be15578d11831330f7f2deeecfbb625568e3e03'
            '91700d5c7c52cb6e15ed46ec61a150af58e073de7332351a1f7863a41042d134')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/arch.patch"
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
	      --localstatedir=/var \
	      --libexecdir=/usr/bin \
	      --disable-static \
	      --enable-pulse
  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -s
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  make -C 'plugins/updates' DESTDIR="${pkgdir}" install

  _schema='org.gnome.settings-daemon.plugins.updates.gschema.xml'
  install -D -m644 "data/$_schema" "$pkgdir/usr/share/glib-2.0/schemas/$_schema"
}
