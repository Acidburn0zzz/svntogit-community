# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gramps
pkgver=4.0.1
pkgrel=2
epoch=2
pkgdesc="A genealogy program, which helps you track your family tree"
arch=('any')
url="http://gramps-project.org/"
license=('GPL')
depends=('pygtk' 'librsvg' 'desktop-file-utils' 'shared-mime-info'
	 'gnome-icon-theme' 'xdg-utils' 'python2-bsddb'
	 'python2-gobject')
makedepends=('intltool' 'python2-distribute')
optdepends=('python2-gnomevfs: mime functions support'
            'python2-gtkspell: spellchecking support'
            'pywebkitgtk: html rendering support'
            'graphviz: graph exporting support'
            'ttf-freefont: more font support')
#            'python-osmgpsmap: openstreetmap support'
#            'pyexiv2: exif support'
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/gramps/Stable/$pkgver/gramps-$pkgver.tar.gz
	block-changed.patch)
md5sums=('078ae6f9138570447081b64f8ae8a701'
         'b2443587e08c4951f00e9401d47e1ccc')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 <$srcdir/block-changed.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root=$pkgdir
  echo -n '/usr/share' >$pkgdir/usr/lib/python2.7/site-packages/gramps/gen/utils/resource-path
}
