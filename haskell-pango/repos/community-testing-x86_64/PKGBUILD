# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-pango
pkgver=0.13.3.1
pkgrel=3
pkgdesc="Binding to the pango library for Gtk2Hs."
url="https://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'pango' 'haskell-glib' 'haskell-cairo')
makedepends=("haskell-gtk2hs-buildtools" "mesa")
provides=('gtk2hs-pango')
replaces=('gtk2hs-pango')
conflicts=('gtk2hs-pango')
source=(https://hackage.haskell.org/packages/archive/pango/${pkgver}/pango-${pkgver}.tar.gz)
sha256sums=('306a4f17d2fe4053b2ddd841a48720513fe391df49080ce61a31b8a0f0633fbb')

build() {
    cd "${srcdir}/pango-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    sed -i 's|import Foreign.C.Types.*(CULong, CUInt)|import Foreign.C.Types (CULong(..), CUInt(..))|' Graphics/Rendering/Pango/Types.chs
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/pango-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/pango"
    runhaskell Setup copy --destdir="${pkgdir}"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
