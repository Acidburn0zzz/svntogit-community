# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=cargo-watch
pkgver=7.7.1
pkgrel=1
pkgdesc="Utility for Cargo to compile projects when sources change"
url='https://github.com/passcod/cargo-watch'
arch=('x86_64')
license=('CC0-1.0')
depends=('cargo')
makedepends=('ruby-ronn')
source=("https://github.com/passcod/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('59084ac4d2cf6a37f781981ab004524b41d1170e06c95d4887e503fe2c32a91f8ee9cc415420d8ffda5577a48a4c70ccb8371432783fe3805fa14e0f0ed63101')
b2sums=('7980ccb5792562c249ad9cadf34af25225590ed66e2b95a5a5154acaf464f18e5cba0e0a0d34f4eafa22800463e66388765771750e1dfaa976595ab3163f650e')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
  ronn --roff cargo-watch.1.ronn
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm 644 completions/zsh "${pkgdir}/usr/share/zsh/site-functions/_cargo-watch"
  install -Dm 644 cargo-watch.1 -t "${pkgdir}/usr/share/man/man1"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
