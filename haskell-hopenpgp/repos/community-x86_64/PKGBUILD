# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hOpenPGP
pkgname=haskell-hopenpgp
pkgver=2.5.5
pkgrel=46
pkgdesc="Native Haskell implementation of OpenPGP (RFC4880)"
url="http://floss.scru.org/hOpenPGP/"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc=8.0.1' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base16-bytestring'
         'haskell-base64-bytestring' 'haskell-bifunctors' 'haskell-byteable' 'haskell-bzlib'
         'haskell-binary-conduit' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite'
         'haskell-crypto-cipher-types' 'haskell-data-default-class' 'haskell-errors'
         'haskell-hashable' 'haskell-incremental-parser' 'haskell-ixset-typed' 'haskell-lens'
         'haskell-memory' 'haskell-monad-loops' 'haskell-nettle' 'haskell-network'
         'haskell-network-uri' 'haskell-newtype' 'haskell-openpgp-asciiarmor' 'haskell-resourcet'
         'haskell-securemem' 'haskell-semigroups' 'haskell-split' 'haskell-text'
         'haskell-time-locale-compat' 'haskell-unordered-containers' 'haskell-wl-pprint-extras'
         'haskell-zlib')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
md5sums=('f480201a10f14150adc290c81d38e7b6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
