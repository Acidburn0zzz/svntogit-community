# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hOpenPGP
pkgname=haskell-hopenpgp
pkgver=2.9
pkgrel=17
pkgdesc="Native Haskell implementation of OpenPGP (RFC4880)"
url="http://floss.scru.org/hOpenPGP/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-asn1-encoding' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-bifunctors' 'haskell-bzlib' 'haskell-binary-conduit'
         'haskell-conduit' 'haskell-conduit-extra' 'haskell-cryptonite'
         'haskell-crypto-cipher-types' 'haskell-errors' 'haskell-hashable'
         'haskell-incremental-parser' 'haskell-ixset-typed' 'haskell-lens' 'haskell-memory'
         'haskell-monad-loops' 'haskell-nettle' 'haskell-network-uri' 'haskell-newtype'
         'haskell-openpgp-asciiarmor' 'haskell-prettyprinter' 'haskell-resourcet' 'haskell-split'
         'haskell-time-locale-compat' 'haskell-unliftio-core' 'haskell-unordered-containers'
         'haskell-wl-pprint-extras' 'haskell-zlib')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        hopenpgp-ghc-8.8.patch::https://salsa.debian.org/clint/hOpenPGP/commit/2c26a2e061178c08684b06f839066df05473da8c.patch)
sha512sums=('751c325eb7c33ad64a5cd20d13c414d1bd59fccd4f703871d029237431b4966998056360aa236e0bf3c3379245a9a610aa3ad9f3f3b83961918212bb1d7e315c'
            '0751b3a893335df5a267f05672dbac1f5bb48daeded9f3e2278568b7a5df30077c0cd5ccff4714b5743389f8db1e64b366d37595952b1f5f6a922d59c8a7a4a8')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../hopenpgp-ghc-8.8.patch
    sed -i -e 's/ fail "partial body length support needed"/ error "partial body length support needed"/' \
           -e 's/ fail ("too/ error ("too/' Codec/Encryption/OpenPGP/Serialize.hs
    sed -i -e 's/ fail$/ error/' -e 's/fail "Non-V4/error "Non-V4/' -e 's/fail "This should/error "This should/' Codec/Encryption/OpenPGP/SerializeForSigs.hs
    sed -i -e 's/fail "I think/error "I think/' -e 's/fail "MDC with/error "MDC with/' -e 's/fail \$/error $/' Data/Conduit/OpenPGP/Decrypt.hs
    sed -i '/semigroups/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
