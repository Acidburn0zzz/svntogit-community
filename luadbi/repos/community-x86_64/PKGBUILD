# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Robert Knauer <robert@privatdemail.net>

pkgname=lua51-dbi
pkgver=0.7.1
pkgrel=1
pkgdesc="Database interface library for Lua (5.1)"
arch=('x86_64')
url="https://github.com/mwild1/luadbi"
license=('MIT')
makedepends=('sqlite3' 'libmariadbclient' 'postgresql')
depends=('lua51')
optdepends=('sqlite3: sqlite3 Support'
            'postgresql-libs: postgresql Support'
            'libmariadbclient: mysql Support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mwild1/luadbi/archive/v$pkgver.tar.gz")
sha1sums=('992c322b09e8a109c45aae938c14303a6b28720c')

build() {
  cd "$srcdir"/luadbi-$pkgver
  make free
}

package() {
  cd "$srcdir"/luadbi-$pkgver
  make install_free DESTDIR="$pkgdir"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
