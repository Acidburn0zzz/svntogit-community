# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: juergen <juergen@archlinux.org>
# Contributor: Manolis Tzanidakis, Petter Gundersen

pkgname=scummvm
pkgver=2.1.2
pkgrel=1
pkgdesc="A 'virtual machine' for several classic graphical point-and-click adventure games."
arch=('x86_64')
license=('GPL')
url="http://www.scummvm.org/"
depends=('libpng' 'libtheora' 'sdl2' 'sdl2_net' 'fluidsynth' 'flac' 'faad2' 'libvorbis' 'libmad' 'freetype2'
         'libgl' 'glu' 'libjpeg-turbo' 'libmpeg2' 'curl' 'a52dec')
makedepends=('mesa')
source=("https://scummvm.org/frs/scummvm/${pkgver}/scummvm-${pkgver}.tar.xz"
        68758a879e0c8ecc0d40962516d4e808aa4e15e5.patch
        8593a9e1e4e8dd1f5dfac9b1304a417df9a742e6.patch)
sha512sums=('a6462bc55d90a229e32389380504d6ecf0c836c8a6733775c993f8db34918ef1f9d0559acc7c110e98719b32cf84408a01021dcfa3fe5a821fe4265c1bc49ecc'
            '5693442762d8a1bdeae2e7c9d63cb6809a90aef4e508aeb04a1c0dea0dcc573a0c62f415ec1b15d97e061aae561761220eae4d39a12362cd586667d6c9072fef'
            'caebf9901b63bce5c98218c63c84da6d2ed60c384464fa2a996d057e295fde6efe96fafbe61bef9e19c0170bc04e82d707eb855f5cc82254308458e25c95132f')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/8593a9e1e4e8dd1f5dfac9b1304a417df9a742e6.patch
  patch -Np1 -i "${srcdir}"/68758a879e0c8ecc0d40962516d4e808aa4e15e5.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure \
    --enable-c++11 \
    --enable-release \
    --prefix=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 dists/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
}
