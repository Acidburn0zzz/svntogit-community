# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Diego Principe <cdprincipe@gmail.com>
# Contributor: paterbrown <paterbrown@silberhelme.de>
# Contributor: Josip <josipponjavic@gmail.com>

pkgname=elementary-icon-theme
pkgver=5.0
pkgrel=1
pkgdesc='Named, vector icons for elementary OS'
arch=('any')
url='https://github.com/elementary/icons'
license=('GPL3')
groups=('pantheon')
depends=('hicolor-icon-theme')
makedepends=('git' 'meson')
source=("elementary-icon-theme::git+https://github.com/elementary/icons.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  arch-meson elementary-icon-theme build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/.VolumeIcon.*
}

# vim: ts=2 sw=2 et:
