# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=(python-cysignals python2-cysignals)
pkgver=1.3.2
pkgrel=2
pkgdesc="Interrupt and signal handling for Cython"
arch=(i686 x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(python2-sphinx cython2 python-sphinx cython pari-sage)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgbase-$pkgver.tar.bz2")
md5sums=('abc98cb7b07c3a40979ee24dead06a38')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
  sed -e 's|sphinx-build|sphinx-build2|g' -i $pkgbase-$pkgver-py2/docs/Makefile
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cysignals() {
  depends=(python pari-sage)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"

# Fix include dir
  sed -e "s|include_dirs.*/lib/|include_dirs = /usr/lib/|" -i "$pkgdir"/usr/lib/*/site-packages/cysignals/__init__.pxd

# Keep scripts in python2 package
  rm -r "$pkgdir"/usr/bin
}

package_python2-cysignals() {
  depends=(python2 pari-sage)
  conflicts=(cysignals)
  provides=(cysignals)
  replaces=(cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"

# Fix include dir
  sed -e "s|include_dirs.*/lib/|include_dirs = /usr/lib/|" -i "$pkgdir"/usr/lib/*/site-packages/cysignals/__init__.pxd
}
