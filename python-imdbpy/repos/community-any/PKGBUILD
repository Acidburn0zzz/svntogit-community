# Maintainer: David Runge <dave@sleepmap.de>

_name=IMDbPY
pkgname=python-imdbpy
pkgver=6.7
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('bcf9c26d4a1fc078e7f2a09a047329a0cc516d81e280578515f93a2c0201ae8f12baf7f710d962f29aa09a81ee426891a58840a6e279c5acc30dbc3b0911dee9')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc\/python-imdbpy\/'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH=${srcdir}/${pkgname}-${pkgver}/build:${PYTHONPATH}
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
