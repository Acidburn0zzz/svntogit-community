# Maintainer: David Runge <dave@sleepmap.de>

pkgname=python-imdbpy
pkgver=6.3
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alberanid/${pkgname/python-/}/archive/${pkgver}.tar.gz")
sha512sums=('7317d55a7dce4d0d94d6d7c8758b719685c596981d7ee07077eb9cb998d67d3f808c8659a0e920bde8d029deb0ec0ccd102ea577e98cd9cea4ba2ae8b0a27f6d')

prepare() {
  mv -v "${pkgname/python-/}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
  # removing broken test: https://github.com/alberanid/imdbpy/issues/135
  sed '366,369d' -i tests/test_http_movie_combined.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH=${srcdir}/${pkgname}-${pkgver}/build:${PYTHONPATH}
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
