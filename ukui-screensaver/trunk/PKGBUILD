# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-screensaver
pkgver=2.1.0
pkgrel=1
pkgdesc="Screensaver for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-screensaver"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'libxtst' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-screensaver/archive/v$pkgver.tar.gz"
        ukui-screensaver-autostart.patch::https://github.com/ukui/ukui-screensaver/pull/9.patch)
sha512sums=('a0881aa5165f93377f10157096b5fbebdd2a00c79d903420e6c11cd457863a45264fa382972a278e752281199c5c348a0d2dc6626f0575c9e009169b32f5eeb5'
            '13459b31ae919dd1ac4e46ea1ba37043003bdeadb6149b312194d5904de0f4c7a17e593b966d9f942b490aae07a967e41bd8a96db134234f81b32708c0ec4e51')

prepare() {
  mkdir -p build
  patch -d $pkgname-$pkgver -p1 -i ../ukui-screensaver-autostart.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
