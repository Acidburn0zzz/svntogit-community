# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.13.0
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='https://pari.math.u-bordeaux.fr/'
license=(GPL)
arch=(x86_64)
depends=(gmp readline libx11)
makedepends=(perl texlive-core)
optdepends=('perl: gphelp, tex2mail')
source=("https://pari.math.u-bordeaux.fr/pub/pari/unix/$pkgname-$pkgver.tar.gz"{,.asc})
sha256sums=('c811946de9d2c1ed0e97ff08e80d966f9a0b55848b7688406fab229e3948ba93'
            'SKIP')
validpgpkeys=('42028EA404A2E9D80AC453148F0E7C2B4522E387')

build() {
  cd $pkgname-$pkgver

  # Upstream Bill Allombert recommends linking gp statically against
  # libpari.so with -flto to recover speed losses incurred when enabling
  # pthread support. That's also what he now does for the Debian package.
  export CFLAGS+=' -flto'

  ./Configure \
    --prefix=/usr \
    --with-readline \
    --mt=pthread \
    --with-gmp
  make all
}

check() {
  cd $pkgname-$pkgver
  make bench
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  ln -sf gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1

  cd Olinux-x86_64
  make DESTDIR="$pkgdir" install-bin-sta
}
