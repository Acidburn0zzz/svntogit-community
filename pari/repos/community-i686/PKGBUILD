# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.5.3
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
source=("${url}pub/pari/unix/${pkgname}-${pkgver}.tar.gz")
sha1sums=('de79eee7ae017a495dc0c648b6f7d5a4f6c7a522')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	sed 's/\$addlib64/ /' -i config/get_libpth
	./Configure --prefix=/usr --with-gmp --with-readline
	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	# symlink pointing to a symlink that is going to be moved by zipman
	rm "${pkgdir}"/usr/share/man/man1/pari.1
	ln -s gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1.gz
}
