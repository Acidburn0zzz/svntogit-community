# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: brent s. <bts[at]square-r00t[dot]net>

_name=GitPython
pkgdesc="A python library used to interact with Git repositories"
pkgname=python-gitpython
pkgver=3.0.2
pkgrel=1
url="https://github.com/gitpython-developers/gitpython"
license=('BSD')
arch=('any')
depends=('git' 'python-gitdb')
makedepends=('python-setuptools')
checkdepends=('python-ddt' 'python-nose')
source=("${pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/G/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/G/${_name}/${_name}-${pkgver}.tar.gz.asc")
sha512sums=('3ee5f31a45284f62a7d93500f559b130a62e848ee62bdaa6d18e15982b23d3457fecaa9d82eb5d20473531e69c4e21b6fdceb3ad1a82d73a47bc2112dcf67187'
            'SKIP')
validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# # tests are broken (only in released pypi sources):
# # https://github.com/gitpython-developers/GitPython/issues/914
# check() {
#   cd "${pkgname}-${pkgver}"
#   git config --global user.email "something@something.tld"
#   git config --global user.name "Test User"
#   nosetests -vd
# }

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGES,{CONTRIBUTING,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
