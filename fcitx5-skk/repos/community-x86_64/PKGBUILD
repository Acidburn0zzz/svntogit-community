# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-skk
pkgver=5.0.7
pkgrel=1
pkgdesc="libskk wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-skk"
license=('GPL')
depends=('fcitx5-qt' 'libskk' 'skk-jisyo')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('d67f7003079c16ceebcde2a5e0dc03efce970596f54bd7220d330e906742858fc005551079d7a9ab65bb826edbd5107e64ac154f082200483d26ba6c85730dc5'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
