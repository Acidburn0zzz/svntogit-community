# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=icecast
pkgver=2.3.2
pkgrel=5
pkgdesc="Streaming audio over the Internet"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.icecast.org/"
depends=('libxslt' 'libvorbis' 'curl' 'speex' 'libtheora')
backup=('etc/icecast.xml'
        'etc/logrotate.d/icecast')
source=("http://downloads.us.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'icecastd' 'icecast.logrotate'
        'start-by-nobody.patch')
md5sums=('ff516b3ccd2bcc31e68f460cd316093f'
         'e823c1fdb080aae3d0c54ef8be95f7cb'
         '59c6552bcb1dd9fb542af8670dfabd3c'
         'd8e929d2214123a1954da4383bf16583')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/start-by-nobody.patch

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  # install man-page
  sed -i -e 's/icecast2/icecast/g' debian/icecast2.1
  install -Dm644 debian/icecast2.1 \
    ${pkgdir}/usr/share/man/man1/icecast.1

  # init file
  install -Dm755 ${srcdir}/icecastd \
    ${pkgdir}/etc/rc.d/icecast
  # rotate the logs (taken from Fedora)
  install -Dm644 ${srcdir}/icecast.logrotate \
    ${pkgdir}/etc/logrotate.d/icecast

  # install log dir
  install -d -g99 -o99 ${pkgdir}/var/log/icecast
}
