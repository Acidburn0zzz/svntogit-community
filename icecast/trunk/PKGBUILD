# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Jason Chu <jchu@xentac.net>

pkgname=icecast
pkgver=2.3.3
pkgrel=2
pkgdesc='Streaming audio over the Internet'
arch=('i686' 'x86_64')
license=('GPL')
url='http://www.icecast.org/'
depends=('libxslt' 'libvorbis' 'curl' 'speex' 'libtheora')
backup=('etc/icecast.xml'
        'etc/logrotate.d/icecast')
source=("http://downloads.us.xiph.org/releases/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'icecastd'
        'icecast.logrotate'
        'start-by-nobody.patch'
        'icecast.service')
md5sums=('2b5d1b40778922e5f6431b7758c359ad'
         'e823c1fdb080aae3d0c54ef8be95f7cb'
         '59c6552bcb1dd9fb542af8670dfabd3c'
         'd8e929d2214123a1954da4383bf16583'
         'ada6c389351af0487b5c62198adb4e2f')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i "${srcdir}/start-by-nobody.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # install man page
  sed -i -e 's/icecast2/icecast/g' debian/icecast2.1
  install -Dm644 debian/icecast2.1 "${pkgdir}/usr/share/man/man1/icecast.1"

  # install rc.d script and logrotate config (taken from Fedora)
  install -Dm755 "${srcdir}/icecastd" "${pkgdir}/etc/rc.d/icecast"
  install -Dm644 "${srcdir}/icecast.logrotate" "${pkgdir}/etc/logrotate.d/icecast"

  # create log directory
  install -d -g99 -o99 "${pkgdir}/var/log/icecast"

  # install systemd unit
  install -Dm0644 "${srcdir}/icecast.service" "${pkgdir}/usr/lib/systemd/system/icecast.service"
}
