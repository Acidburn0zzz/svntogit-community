# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: AndyRTR <andyrtr@archlinux.org>
# Contributor: kiefer <jorgelmadrid@gmail.com>

pkgname=lxdm
pkgver=0.4.1
pkgrel=21
pkgdesc='Lightweight X11 Display Manager'
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/lxdm/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'xorg-server')
makedepends=('intltool')
install=${pkgname}.install
backup=('etc/lxdm/lxdm.conf' 'etc/pam.d/lxdm' 'etc/lxdm/Xsession'
        'etc/lxdm/PreLogin' 'etc/lxdm/LoginReady' 'etc/lxdm/PostLogin'
        'etc/lxdm/PostLogout' 'etc/lxdm/PreReboot' 'etc/lxdm/PreShutdown')
source=(http://downloads.sourceforge.net/lxde/$pkgname-$pkgver.tar.gz
		lxdm.pam  lxdm.service
		lxdm-0.4.1-Xsession-source-profile.patch
		lxdm-0.4.1-conf.patch
		lxdm-0.4.1-event-check-bug.patch
		lxdm-0.4.1-industrial-greeter-ui.patch
		lxdm-0.4.1-modern-systems.patch
		lxdm-0.4.1-nolang-show-sessions.patch
		lxdm-0.4.1-pam-env-vars.patch)

prepare(){
    cd $srcdir/$pkgname-$pkgver
    patch -Np1 -i $srcdir/lxdm-0.4.1-event-check-bug.patch
    patch -Np1 -i $srcdir/lxdm-0.4.1-nolang-show-sessions.patch
    patch -Np1 -i $srcdir/lxdm-0.4.1-pam-env-vars.patch
}

build() {
    cd $srcdir/$pkgname-$pkgver


    ./configure --sysconfdir=/etc --prefix=/usr --libexecdir=/usr/lib/lxdm \
		--bindir=/usr/bin \
		--sbindir=/usr/bin
    make
  	
    patch -Np0 -i $srcdir/lxdm-0.4.1-modern-systems.patch
    patch -Np0 -i $srcdir/lxdm-0.4.1-conf.patch
    patch -Np0 -i $srcdir/lxdm-0.4.1-Xsession-source-profile.patch
    patch -Np0 -i $srcdir/lxdm-0.4.1-industrial-greeter-ui.patch
}


package() {
    cd $srcdir/$pkgname-$pkgver
    make DESTDIR=$pkgdir sbindir=/usr/bin install

    install -m644 $srcdir/lxdm.pam $pkgdir/etc/pam.d/lxdm
    install -Dm644 $srcdir/lxdm.service $pkgdir/usr/lib/systemd/system/lxdm.service
    install -d $pkgdir/var/{lib,run}/lxdm

    # fix the greeter location
    sed -i -e 's/local\/libexec/lib\/lxdm/' $pkgdir/etc/lxdm/lxdm.conf
	sed -i 's:sbin:bin:' $pkgdir/usr/bin/lxdm

    # avoid conflict with filesystem>=2012.06
    rm -r $pkgdir/var/run
}

md5sums=('8da1cfc2be6dc9217c85a7cf51e1e821'
         '9cc734228696a3c6f06d91bba61b03c1'
         'b86317143ae44d7ed38c150fe4f25502'
         'd2e4a4a22ee2aa1a986be154c647b6c6'
         'c50dd01b715b0a236407d48066191601'
         'a1e3c46a8bef691bc544028f5b6cfe22'
         '2c4afdbe3532be4f90d8f6240d352766'
         'baed9055e8825a5511712bc095197519'
         '28475239d0c8b4fd778ec49f5ec72962'
         '4c1d43e81e9a256e8d1ea7686c24b3d3')
