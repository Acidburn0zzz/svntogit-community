# $Id$
# Maintainer: Florian Pritz <flo@xssn.at>

_pkgbasename=libxss
pkgname=lib32-$_pkgbasename
pkgver=1.2.0
pkgrel=3
pkgdesc="X11 Screen Saver extension library (32-bit)"
arch=(x86_64)
license=('custom:XFREE86')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext>=1.0.5' 'scrnsaverproto>=1.2.0' $_pkgbasename)
makedepends=('pkgconfig' gcc-multilib)
options=('!libtool')
source=(${url}/releases/individual/lib/libXScrnSaver-${pkgver}.tar.bz2)
sha1sums=('ea2935eb67efa77fd90372337f2d782a8ef74cea')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXScrnSaver-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXScrnSaver-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
