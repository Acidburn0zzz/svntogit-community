# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgname=gimp-plugin-gmic4gimp
pkgver=1.4.7.0
pkgrel=1
pkgdesc="Gimp plugin for the GMIC image procession framework. Large number of predefined filters"
depends=('gimp' 'fftw' 'lapack')
arch=('i686' 'x86_64')
license=('custom:CeCILL')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz")
url="http://gmic.sourceforge.net"

build() {
  cd "$srcdir/gmic-$pkgver"

  # Enable lapack optimizations
  CFLAGS+=" -Dcimg_use_lapack"
  LDFLAGS+=" -llapack"

  cd src
  make gimp
}

package() {
  cd "$srcdir/gmic-$pkgver"
  install -Dm755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

md5sums=('357096dbafc79823acd450fe33c7c12d')
