# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgname=gimp-plugin-gmic4gimp
_pkgname=gmic
pkgver=1.3.5.7
pkgrel=1
pkgdesc="A large number of pre-defined filters. One can use it to apply various effects on images, directly from the Gimp interface."
depends=('fftw' 'gimp' 'graphicsmagick' 'ffmpeg')
arch=('i686' 'x86_64')
license=('custom:"CeCILL"')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_${pkgver}.tar.gz" \
	"build.patch")
url="http://gmic.sourceforge.net"
md5sums=('bea52fb418d9f3a669695544cf22e67c'
         'a2ef070aec76544ddbfdab1de821a1fc')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  patch -Np1 -i "$srcdir/build.patch"

  ./configure --prefix=/usr

  cd src
  make gimp || return 1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  install -D -m755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
