# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-meta
pkgname=haskell-hspec-meta
pkgver=2.4.4
pkgrel=1
pkgdesc="A version of Hspec which is used to test Hspec itself"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-quickcheck' 'haskell-hspec-expectations' 'haskell-hunit'
         'haskell-setenv' 'haskell-random' 'haskell-quickcheck-io' 'haskell-ansi-terminal'
         'haskell-async' 'haskell-call-stack')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('91f66cbeecb3cede014b264f18693467c981bb07c79f173fb0e32935057d2415bd3f47e1b59d74f56b098710f1767e9ba5b5d9d060728989eaf516efaa3329e6')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
