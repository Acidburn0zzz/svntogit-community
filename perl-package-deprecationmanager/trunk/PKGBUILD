# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-package-deprecationmanager
pkgver=0.13
pkgrel=1
pkgdesc='Manage deprecation warnings for your distribution'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl-list-moreutils perl-params-util perl-sub-install)
checkdepends=(perl-test-fatal perl-test-requires)
url=https://metacpan.org/release/Package-DeprecationManager
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Package-DeprecationManager-$pkgver.tar.gz")
md5sums=(f81ae3c0f9bcac048eb4bff348b79f67)
sha512sums=(393841bc8a6a96dfc3ce9e2787d532f0e5ba45ef07a8d95be4450ad399b0ae3c4929f8abf07892194fca85c7de75c118a5b611a816b565a7945e34ebca36c4a8)
_dir="$srcdir/Package-DeprecationManager-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
