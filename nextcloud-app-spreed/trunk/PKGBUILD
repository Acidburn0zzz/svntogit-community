# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=11.1.1
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('28ec451a9319bc8a9a208bcc4dbac49ff7921d2d53e2f46733966f44f91b573433ce1212733914595ba97a9f7406e9d35c9c75f68c8171d35d523eac3bd48934')
b2sums=('c2d5c2f8753419b0bbabec16ee91b6fba4863db89e4d2be4794b17f99b3f3643d60198b470bea9f0660a96bc1e6e9366cd89d65e189086da41bea2ca02abb812')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
