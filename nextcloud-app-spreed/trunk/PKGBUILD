# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=10.0.2
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('7b356f95e64b5f49b89d5a8e47b101b6bd343df2435142c866ad5266322e3ff29d87a41277485793c7e508a65cf977c061f2860477d4a6ca601c5d9e7f02457c')
b2sums=('8d9067210eb4e0d90e6336473f41f93977697f3269e322b8e6f48282043a922f6771988e8001f60a95a04109eee3d793affa640e6403b95f0b9e957c9fe69999')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
