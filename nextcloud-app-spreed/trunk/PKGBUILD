# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=9.0.4
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('fb5da8a5f01efb43a69567e6829a636c76befff030c7995f90c729ccc074c6dd9ca312109bc29a3274591e4ab6b567c101108c17e2edc63b6dcbbab76b8f6583')
b2sums=('cbc935a4be09384e9309c344d5d97df7c2abc285b9981258f119b16944d8e93c4b018ad51e1cc10d2a8136bc78547e5c22c9c28226e0e09059a34d6cfa0dcb4c')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
