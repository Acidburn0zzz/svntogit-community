# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=8.0.9
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('85c8f6bd72a73913db5f6de60f90eb7503a5d5fe604d0997b6556154153c04b5f967a66ad3c0a8b25f86d99cbf22febb28048b04ab66685379d50a2c583024ce')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
