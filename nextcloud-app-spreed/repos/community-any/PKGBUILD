# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=10.0.1
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('a9677f466f3d700cc104acb6a698a1de8dff4fb207de8f4ec91e81df8b60b163f4e2f4f63775ac015b962092a68138d891e15cf95f6945e132c0549611568c02')
b2sums=('1cfed8a6b462a71d37aeb860c0451aa65c176acd4a1a450327ee548a03b77f8648b0a31cc77f4cda7b505247dad06a98b780904b78c9e9840431b0d6b6d6f79a')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
