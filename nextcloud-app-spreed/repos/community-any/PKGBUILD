# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=12.2.0
pkgrel=1
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
groups=('nextcloud-apps')
makedepends=('nextcloud')
checkdepends=('ripgrep' 'yq')
source=("https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('0b97021e11bf8e9dad7e3d65d9626ef76e14b6edee8749f0eb6a000830f6a95b15470d60b25c9e5094a2aec52cf25cfe2a16ebe8238c9bd8f8af39d7e412ed8e')
b2sums=('76ef0bcb3406c61b63d69ec5f99946bcdd6ef7bc3b93bac04d0538ae9ef3574f32f96b0838a7a9128afc32821c1558d652aab2ac49e22641e27b22246687da85')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")"<0)) || (("$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions
  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
