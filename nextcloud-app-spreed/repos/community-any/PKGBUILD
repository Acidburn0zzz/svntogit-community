# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=11.1.0
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('da6d8dbebc6653e9506f2c581815dee1515bfb93dc048ab46b14a22b7de7e6431b4194c4611a533a52860c5e49c778e00f096dc87fabfd92d43e6a0226b59187')
b2sums=('f3ae4223a46b9cd8111fabef25719644da31d1e8f11fe7f8a73843697925ae72b748a2af1d4ff3d3333971f93e0b0458d68b7812ac6b4f5204f628f4131ae210')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
