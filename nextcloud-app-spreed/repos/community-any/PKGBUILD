# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=9.0.2
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('4c6ab0de0a8c54d8755b807c71b00f4f4c52951aa63eb00f17e9c52479171745d056fcbdb0c3c2adac9f7c14c026b49521e69936516c275f74315aa5d022fa52')
b2sums=('312ec434e7f6e5fe860dd4cd50ecc59a915d3f2fbf541058e1f70286495b93d1036251c2efa1f5700bc5c9bd62fb25cd98821c38724a3056d6ec0169a295aeb9')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
