# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgname=python2-nltk
pkgver=2.0.2
pkgrel=1
pkgdesc='Software for natural language processing'
arch=('any')
url='http://www.nltk.org/'
license=('APACHE')
depends=('python2-yaml')
optdepends=('nltk-data: test data'
            'python2-numpy: used for calculations'
            'python2-matplotlib: used for plotting')
replaces=('python-nltk')
provides=('python-nltk')
source=("http://pypi.python.org/packages/source/n/nltk/nltk-${pkgver}.tar.gz")
md5sums=('6e714ff74c3398e88be084748df4e657')

build() {
  cd nltk-${pkgver}

  find . -type f -exec sed -i 's~^#.*env python~#!/usr/bin/env python2~' {} +
  python2 setup.py build
}

package() {
  cd nltk-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
}
