# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stack
pkgver=1.6.0.20171202
_commit=59a9a84eddfd8743f18ada9a4f58562442cbc9e6
pkgrel=3
pkgdesc="The Haskell Tool Stack"
url="https://github.com/commercialhaskell/stack"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-annotated-wl-pprint' 'haskell-ansi-terminal'
         'haskell-async' 'haskell-attoparsec' 'haskell-base64-bytestring' 'haskell-bindings-uname'
         'haskell-blaze-builder' 'haskell-clock' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-cryptonite' 'haskell-cryptonite-conduit' 'haskell-echo' 'haskell-exceptions'
         'haskell-extra' 'haskell-fast-logger' 'haskell-file-embed' 'haskell-filelock'
         'haskell-fsnotify' 'haskell-generic-deriving' 'haskell-gitrev' 'haskell-hackage-security'
         'haskell-hashable' 'haskell-hastache' 'haskell-hpack' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-conduit' 'haskell-http-types' 'haskell-memory'
         'haskell-microlens' 'haskell-microlens-mtl' 'haskell-mintty' 'haskell-monad-logger'
         'haskell-mono-traversable' 'haskell-mtl' 'haskell-neat-interpolation' 'haskell-network-uri'
         'haskell-open-browser' 'haskell-optparse-applicative' 'haskell-optparse-simple'
         'haskell-path' 'haskell-path-io' 'haskell-persistent' 'haskell-persistent-sqlite'
         'haskell-persistent-template' 'haskell-pid1' 'haskell-primitive' 'haskell-project-template'
         'haskell-regex-applicative-text' 'haskell-resourcet' 'haskell-retry' 'haskell-semigroups'
         'haskell-split' 'haskell-stm' 'haskell-store' 'haskell-store-core'
         'haskell-streaming-commons' 'haskell-tar' 'haskell-temporary' 'haskell-text'
         'haskell-text-metrics' 'haskell-th-reify-many' 'haskell-tls' 'haskell-unicode-transforms'
         'haskell-unix-compat' 'haskell-unliftio' 'haskell-unordered-containers' 'haskell-vector'
         'haskell-yaml' 'haskell-zip-archive' 'haskell-zlib')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-hspec' 'haskell-smallcheck')
conflicts=('haskell-stack')
replaces=('haskell-stack')
install="stack.install"
source=("$pkgname-$_commit.tar.gz::https://github.com/commercialhaskell/stack/archive/$_commit.tar.gz"
        https://github.com/commercialhaskell/stack/commit/8f5e42e8687b0d2ccc749b8f450cbf71b0ebf5a7.patch)
sha512sums=('e90e2453a6864dae686558047c58d7e39d10dceb3a307ff8f304dd371a571f79ffd3a88603f17cd85485de8e4ec6f9fb734ff1fa3afad3a84ee293afa7dc4cf5'
            'c80009075bd10e742caa0d03161a36ffd4afb3bde17cc5785b8314209cdebfadbced1757e9863861bfc2c18e33d0696cedddb1667cf8a67d366f4659873b3be9')

prepare() {
  mv $pkgname-$_commit $pkgname-$pkgver

  cd $pkgname-$pkgver
  patch -p1 -i ../8f5e42e8687b0d2ccc749b8f450cbf71b0ebf5a7.patch

  hpack
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
      -f-disable-git-info -f-integration-tests -f-static -f-hide-dependency-versions -f-supported-build
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $pkgname-$pkgver
  PATH="$PWD/dist/build/stack:$PATH" runhaskell Setup test
  # TODO: need more work for integration tests
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

  LD_PRELOAD=$(ls "$pkgdir"/usr/lib/libHSstack-*-ghc*.so) "${pkgdir}"/usr/bin/stack --bash-completion-script /usr/bin/stack > stack_completion_script
  install -Dm644 stack_completion_script "${pkgdir}/usr/share/bash-completion/completions/stack"
}
