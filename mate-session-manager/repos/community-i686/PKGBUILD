# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
pkgname=mate-session-manager
pkgver=${_ver}.1
pkgrel=1
pkgdesc="The MATE Session Handler"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'gtk3' 'libsm' 'mate-desktop-schemas')
makedepends=('intltool' 'xtrans' 'systemd')
optdepends=('gnome-keyring: keyring support'
            'xdg-user-dirs-gtk: manage user directories')
groups=('mate' 'mate-gtk3')
conflicts=('mate-session-manager-gtk3')
replaces=('mate-session-manager-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('510d4ffd3b15fddfefeae3c126ca23b31166bcb1')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-upower
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
