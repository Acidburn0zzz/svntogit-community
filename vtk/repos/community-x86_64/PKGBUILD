# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: leepesjee <lpeschier at xs4all dot nl>
# Contributor: Olivier Medoc
# Contributor: ignotus
# Contributor: Fabian Moser
# Contributor: djscholl

pkgname=vtk
pkgver=5.6.0
pkgrel=3
pkgdesc='A software system for 3D computer graphics, image processing, and visualization which supports a wide variety of visualization algorithms and advanced modeling techniques.'
arch=('i686' 'x86_64')
url='http://www.vtk.org'
license=('BSD')
depends=('libpng' 'libtiff' 'freetype2' 'python' 'java-runtime' 'tk' 'boost' 'mysql' 'ffmpeg' 'qt' 'lesstif')
optdepends=('gnuplot: plotting tools'
            'graphviz: drawing tools')
makedepends=('cmake' 'java-environment' 'doxygen' 'gnuplot' 'mesa')
source=("http://www.vtk.org/files/release/${pkgver:0:3}/${pkgname}-${pkgver}.tar.gz"
        "http://www.vtk.org/files/release/${pkgver:0:3}/vtkdata-${pkgver}.tar.gz"
        'vtk-disable-egg.patch'
        'vtk-libpng-1.4.0.patch')
md5sums=('a56232baae6071f9f59f138d8f4620c2'
         '97006362d7d67dc01289daf3e4ec4e38'
         '7338d6a33e9735dff74f012176fef580'
         '8e6e4a0cd34c3469b0635267f07472fd')

build() {
  # The Python-egg-install script doesn't work properly. We don't need the egg anyway.
  patch -Np2 -i ./vtk-disable-egg.patch

  # We need a patch to compile with libpng-1.4.0
  patch -Np2 -i ./vtk-libpng-1.4.0.patch

  # Build in a separate directory
  rm -rf build
  mkdir build
  cd build

  cmake \
    -Wno-dev \
    -DBUILD_SHARED_LIBS:BOOL=ON \
    -DCMAKE_INSTALL_PREFIX:FILEPATH=/usr \
    -DVTK_DATA_ROOT:FILEPATH=${srcdir}/VTKData-release-${pkgver} \
    -DBUILD_DOCUMENTATION:BOOL=ON \
    -DDOCUMENTATION_HTML_HELP:BOOL=ON \
    -DDOCUMENTATION_HTML_TARZ:BOOL=ON \
    -DBUILD_EXAMPLES:BOOL=ON \
    -DVTK_USE_HYBRID:BOOL=ON \
    -DVTK_USE_PARALLEL:BOOL=ON \
    -DVTK_USE_PATENTED:BOOL=ON \
    -DVTK_USE_SYSTEM_EXPAT:BOOL=ON \
    -DVTK_USE_SYSTEM_FREETYPE:BOOL=ON \
    -DVTK_USE_SYSTEM_JPEG:BOOL=ON \
    -DVTK_USE_SYSTEM_PNG:BOOL=ON \
    -DVTK_USE_SYSTEM_TIFF:BOOL=ON \
    -DVTK_USE_SYSTEM_ZLIB:BOOL=ON \
    -DVTK_USE_SYSTEM_LIBXML2:BOOL=ON \
    -DVTK_USE_BOOST:BOOL=ON \
    -DVTK_USE_INFOVIS:BOOL=ON \
    -DVTK_USE_MYSQL:BOOL=ON \
    -DVTK_USE_GL2PS:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    -DVTK_WRAP_PYTHON:BOOL=ON \
    -DVTK_PYTHON_SETUP_ARGS:STRING="--prefix=${pkgdir}/usr" \
    -DVTK_WRAP_JAVA:BOOL=ON \
    -DVTK_USE_QT:BOOL=ON \
    -DDESIRED_QT_VERSION:STRING=4 \
    -DVTK_USE_GUISUPPORT:BOOL=ON \
    -DCMAKE_CXX_FLAGS="-D__STDC_CONSTANT_MACROS" \
    ${srcdir}/VTK

  # j1 is used because of a bug in building with multiple threads
  make -j1
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  # Move the vtk.jar to the arch-specific location
  install -dv ${pkgdir}/usr/share/java/${pkgname}
  mv -v ${pkgdir}/usr/lib/${pkgname}-${pkgver:0:3}/java/${pkgname}.jar \
    ${pkgdir}/usr/share/java/${pkgname}

  # Move the qt-designer plugin to the appropriate location
  install -dv ${pkgdir}/usr/lib/qt
  mv -v ${pkgdir}/usr/plugins \
    ${pkgdir}/usr/lib/qt

  # Install license
  install -dv ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ${srcdir}/VTK/Copyright.txt \
    ${pkgdir}/usr/share/licenses/${pkgname}

  # Put an entry in /etc/ld.so.conf.d
  install -dv ${pkgdir}/etc/ld.so.conf.d
  echo "/usr/lib/${pkgname}-${pkgver:0:3}" > ${pkgdir}/etc/ld.so.conf.d/${pkgname}.conf
}
