# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=kicad-symbols
pkgver=6.0.0
pkgrel=1
pkgdesc="KiCad schematic symbol libraries"
arch=('any')
url="https://www.kicad.org"
license=('custom:CC-BY-SA-4.0')
makedepends=('cmake')
conflicts=('kicad-library')
replaces=('kicad-library')
source=("https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/$pkgver/kicad-symbols-$pkgver.tar.bz2")
sha512sums=('5891606d47004cc2a530e1d327ae6bd7a89c571bb60dbc4e2f8c6659277de361b6ea337939414028c834392a2f69668a3d3c99962765cc76048b8e4dd153cca3')
b2sums=('e3113eda85f1d3054013bbc7c952f5ec3cf49c40708cb527c6bc944b1fa90aff901ae9c1bfb5c422c4df96c579ff71b4f51927763a4afec7a6bf68bbf43e9bec')

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE.md"
}

