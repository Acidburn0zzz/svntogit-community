# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=3.0.0
pkgrel=2
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.org/"
depends=('gconf' 'gst-plugins-base' 'gst-plugins-good' 'gtk3' 'libnotify' 'python-gobject')
makedepends=('intltool')
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
source=("https://launchpad.net/soundconverter/trunk/$pkgver/+download/soundconverter-$pkgver.tar.xz")
sha512sums=('6f5518711c76e7748447805dbf16ac8622c5041c3f3cac2de6f31968aab4eff43a2fde2da48c47d12093ce1732208923d83d57b6a97ea99fbf882dfef239a62d')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr 
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
