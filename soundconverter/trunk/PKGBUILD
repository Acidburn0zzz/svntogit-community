# Contributor: Andrea `BaSh` Scarpino <bash.lnx@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=soundconverter
pkgver=1.5.3
pkgrel=5
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.berlios.de"
depends=('gnome-python' 'gstreamer0.10-python' 
  'hicolor-icon-theme' 'gstreamer0.10-ugly-plugins' 
  'gstreamer0.10-good-plugins')
makedepends=('perlxml' 'intltool')
optdepends=('gstreamer0.10-base-plugins: for ogg support')
install=soundconverter.install
source=(http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz soundconverter-1.5.3-bz647336.patch)
md5sums=('e090893dcab72671d2e509d389b664d9'
         '2d2ed0752029a66cff566a24485e2688')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2  ./configure --prefix=/usr 
  patch -p1 < $srcdir/soundconverter-1.5.3-bz647336.patch
  make
  sed -i 's#/usr/bin/python#/usr/bin/python2#' src/soundconverter
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
