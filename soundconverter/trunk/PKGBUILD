# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=2.0
pkgrel=2
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.org/"
depends=('gnome-python' 'gstreamer0.10-python' 
  'hicolor-icon-theme' 'gstreamer0.10-ugly-plugins' 
  'gstreamer0.10-good-plugins')
makedepends=('perlxml' 'intltool')
optdepends=('gstreamer0.10-base-plugins: for ogg support')
install=soundconverter.install
source=("http://launchpad.net/${pkgname}/trunk/${pkgver}/+download/${pkgname}-${pkgver}.tar.xz"
        "0001-Fix-set-syntax-in-gstreamer.py.patch")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  PYTHON=python2  ./configure --prefix=/usr 

  # FS#28150, can be removed once it's fixed upstream
  patch -Np1 < "${srcdir}/0001-Fix-set-syntax-in-gstreamer.py.patch"

  make
  sed -i 's#/usr/bin/python#/usr/bin/python2#' bin/soundconverter
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

md5sums=('c99e2da8f7d8cf788852aa5ddfdb5f9e'
         'c9489cfa68a99545ab2a3240ddd45886')
