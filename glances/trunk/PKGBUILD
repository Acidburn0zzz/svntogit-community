# $Id$
# Maintainer: Sébastien Luttringer <seblu@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=glances
pkgver=2.6.1
pkgrel=1
pkgdesc='CLI curses-based monitoring tool'
arch=('any')
url='https://github.com/nicolargo/glances'
license=('LGPL')
depends=('python' 'python-psutil' 'python-setuptools')
optdepends=('hddtemp: HDD temperature monitoring support'
            'python-bottle: web server support'
            'python-docker-py: for the Docker monitoring support'
            'python-matplotlib: for graphical/chart support'
            'python-netifaces: for the IP plugin'
            'zeroconf: for the autodiscover mode')
source=("https://pypi.python.org/packages/source/G/Glances/Glances-$pkgver.tar.gz")
md5sums=('576e1cb937c1d1f384edaf20e4e33d98')

package() {
  cd Glances-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
