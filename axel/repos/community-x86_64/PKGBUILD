# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Roman G <drakosha@au.ru>

pkgname=axel
pkgver=2.12
pkgrel=2
pkgdesc="Light command line download accelerator"
arch=('i686' 'x86_64')
url="https://github.com/eribertomota/axel"
license=('GPL')
depends=('glibc' 'openssl')
source=($pkgname-$pkgver.tar.gz::https://github.com/eribertomota/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('28e7bb26b7be3f56a61b60ef07e15e05ea9a41850b0ed45a0c56d6d2202f4a8b')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
