# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Roman G <drakosha@au.ru>

pkgname=axel
pkgver=2.11
pkgrel=1
pkgdesc="Light command line download accelerator"
arch=('i686' 'x86_64')
url="https://github.com/eribertomota/axel"
license=('GPL')
depends=('glibc' 'openssl')
source=($pkgname-$pkgver.tar.gz::https://github.com/eribertomota/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('7e528df719bad8041346a371290efd8d67959e935f6763ac977ba278d39f5a15')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
