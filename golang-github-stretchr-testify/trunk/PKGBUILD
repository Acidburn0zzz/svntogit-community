# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-stretchr-testify
pkgver=1.3.0
pkgrel=1
pkgdesc='A toolkit with common assertions and mocks that plays nicely with the standard library'
arch=('any')
url="https://github.com/stretchr/testify"
license=('MIT')
depends=('go-pie' 'golang-github-davecgh-go-spew' 'golang-github-pmezard-go-difflib'
         'golang-github-stretchr-objx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stretchr/testify/archive/v$pkgver.tar.gz")
sha512sums=('29dc8389f0efba9caeb2d2d5704878c93f3b00c21e6ad2712bac52bbc09c460d9028aecbfd5430f200a15fd0e8c8d0157e084b72463d82ceecc5459d93d4d896')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$srcdir"/build/src/github.com/stretchr/testify
  cd "$srcdir"/build/src/github.com/stretchr/testify
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/stretchr/testify

  install -Dm644 testify-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
