# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Ben <ben@benmazer.net>

pkgname=fox
pkgver=1.6.40
pkgrel=1
pkgdesc="Free Objects for X: GUI Toolkit for C++"
arch=('i686' 'x86_64')
url="http://www.fox-toolkit.org/"
license=('LGPL' 'custom')
depends=('bzip2' 'libcups' 'libxcursor' 'libxft' 'libxrandr' 'mesa' 'libxi')
options=('!libtool')
source=(http://www.fox-toolkit.org/ftp/${pkgname}-${pkgver}.tar.gz)
md5sums=('1253617ca2ef5652b865e35dc2ddb65c')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  export CPPFLAGS="$CPPFLAGS -I/usr/include/freetype2"

  ./configure   --prefix=/usr \
                --enable-release \
                --with-xft=yes \
                --enable-cups \
                --with-opengl=yes \
                --with-xim
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd ${srcdir}/${pkgname}-${pkgver}/tests
  make ControlPanel
  install -m755 ${srcdir}/${pkgname}-${pkgver}/tests/.libs/ControlPanel ${pkgdir}/usr/bin/
  install -Dm644 ../LICENSE_ADDENDUM ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
