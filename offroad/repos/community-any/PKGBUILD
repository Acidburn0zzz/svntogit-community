# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=offroad
pkgver=0.2
pkgrel=1
pkgdesc="Offline vector map display ported from OsmAnd"
arch=('any')
url="https://sourceforge.net/projects/offroadosm/"
license=('GPL')
depends=('hicolor-icon-theme' 'imagemagick' 'java-runtime')
source=("https://downloads.sourceforge.net/offroadosm/OffRoad-bin-$pkgver.zip"
        "http://central.maven.org/maven2/javax/xml/bind/jaxb-api/2.3.1/jaxb-api-2.3.1.jar"
        "http://central.maven.org/maven2/com/sun/xml/bind/jaxb-core/2.3.0.1/jaxb-core-2.3.0.1.jar"
        "http://central.maven.org/maven2/com/sun/xml/bind/jaxb-impl/2.3.2/jaxb-impl-2.3.2.jar"
        "http://central.maven.org/maven2/com/sun/activation/javax.activation/1.2.0/javax.activation-1.2.0.jar"
        "$pkgname.desktop"
        "$pkgname.appdata.xml"
        "$pkgname.sh")
sha256sums=('048aba0c619c663ba1eb62c09ea20bca728a9bc3b50590562eea76b99032c6cf'
            '88b955a0df57880a26a74708bc34f74dcaf8ebf4e78843a28b50eae945732b06'
            'd2ecba63615f317a11fb55c6468f6a9480f6411c10951d9881bafd9a9a8d0467'
            'b3a3d3aeaa5503cadc24dca9539b87efcd7bc3bd8c48018887a1489f2a77bdc0'
            '993302b16cd7056f21e779cc577d175a810bb4900ef73cd8fbf2b50f928ba9ce'
            'a59f0a5ab10efe74cbf75d52fb78642c1106335dd3c192a083d185f0358faec2'
            '3e5836a968bca5589183e470a01f2af978a3fe6d16e4406928d971284e38e971'
            '533d09ac0b0b63522cfc96ac6b3eda7676221d3f51d5066587795f8f63a2562d')

prepare() {
  bsdtar -xf lib/Resources.jar offroad_icon.png
}

package() {
  install -Dm644 OffRoad.jar "$pkgdir/usr/share/java/$pkgname/$pkgname.jar"
  install -Dm644 -t "$pkgdir/usr/share/java/$pkgname/lib/" lib/*.jar jaxb-*.jar javax.activation-*.jar
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname.appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
  install -dm755 "$pkgdir"/usr/share/icons/hicolor/256x256/apps/
  convert offroad_icon.png +set date:create +set date:modify -background none -gravity center -extent 300x300 -resize 256x256 -alpha on "$pkgdir/usr/share/icons/hicolor/256x256/apps/$pkgname.png"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
