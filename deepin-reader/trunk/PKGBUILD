# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.6.4
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libspectre' 'poppler-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-reader/archive/$pkgver.tar.gz")
sha512sums=('78b7441b9cb31a914ac81881c5c49176ce110dd69c6f33c6a063a3ff6cf609dffb80b819ec6c4b29216cdda706c1945b6803f772a0f8d73b8d0d7992b4a430f5')

prepare() {
  cd deepin-reader-$pkgver
  sed -i '/include <QAbstractListModel>/a #include <QPointF>' application/pdfControl/imageviewmodel.h
  sed -i '/include <QMetaType>/a #include <QRectF>' application/pdfControl/docview/commonstruct.h
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
