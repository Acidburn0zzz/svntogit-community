# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.7.0.7
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libspectre' 'poppler-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-reader/archive/$pkgver.tar.gz")
sha512sums=('7a227ac250bf7a333ea0ce89b3bb11d753910033225ea2aeacc5471ee9bd8977032a499b976cedb7afb5a822d30198d70444789b221109c46ea185d434bdadee')

prepare() {
  cd deepin-reader-$pkgver/src
  sed -i '/include <QMetaType>/a #include <QRectF>' pdfControl/docview/commonstruct.h
  sed -i '/include <QPainter>/a #include <QPainterPath>' CustomControl/RoundColorWidget.cpp lpreviewControl/bookmark/bookmarkdelegate.cpp \
                                                         utils/utils.cpp lpreviewControl/thumbnail/ThumbnailDelegate.cpp lpreviewControl/note/notesdelegate.cpp \
                                                         lpreviewControl/search/searchresdelegate.cpp
  sed -i '/include <QRect>/a #include <QPainterPath>' document/model.h
  sed -i '/include <QMap>/a #include <QSet>' uiframe/DocSheet.h
  sed -i '/include <QBitmap>/a #include <QMouseEvent>' djvuControl/SheetBrowserDJVU.cpp
  sed -i '/include <QDesktopWidget>/a #include <QMouseEvent>' widgets/SlideWidget.cpp
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
