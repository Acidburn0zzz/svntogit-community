# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-invoke
pkgname=(python-invoke python2-invoke)
pkgver=1.0.0
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-pytest-relaxed' 'python2-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('471aa961914e9cfc5206f1d92156a888561205f082012589c1b58b7f383b5b0fa097f077cc9a42e89994dcda632a9e068d793ddd410b6272e54b197a627939bb')

prepare() {
  cp -a invoke-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest

  cd "$srcdir"/invoke-$pkgver-py2
  python2 setup.py pytest
}

package_python-invoke() {
  depends=('python')

  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-invoke() {
  depends=('python2')

  cd invoke-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  for _exe in "$pkgdir"/usr/bin/*; do
    mv "$_exe"{,2}
  done
}
