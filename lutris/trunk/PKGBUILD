# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
_commit=a31a0a4116d883c3d68fd355f281214de79a9afa
pkgver=0.5.3.r5660.a31a0a4
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unzip' 'p7zip' 'curl' 'xorg-xrandr'
         'python-pillow' 'python-requests' 'gnome-desktop' 'webkit2gtk' 'gvfs' 'mesa-demos')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-nose-cover3' 'wine' 'xterm')
optdepends=('wine: Windows support'
            'vulkan-icd-loader: Vulkan support'
            'lib32-vulkan-icd-loader: Vulkan support')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/$_commit.tar.gz"
        'revert-47ecc419498abf127ee657fbc83bc313c883cb44.patch')
sha512sums=('d989b8f3e1631c6b82908b50f1083f5a6597893dee4a25b1509c6ba923fd4b589d74113972bdcba7bc2340cad596812401d1fe04d7999b458a5e5cb3b83ffcae'
            'fb7210948a6409babe38572e3d82a6afcf7b424ecf8c404c922ff3ef3d2a458d4be730e9ee2779005f3b93fa298654749840e808087cf0524c29217985fafc58')

prepare() {
  cd $pkgname-$_commit

  patch -p1 < ../revert-47ecc419498abf127ee657fbc83bc313c883cb44.patch
}

build() {
  cd $pkgname-$_commit

  python setup.py build
}

check() {
  cd $pkgname-$_commit

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname-$_commit

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

