# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.5.2.2
pkgrel=1
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unzip' 'p7zip' 'curl' 'xorg-xrandr'
         'python-pillow' 'python-requests' 'gnome-desktop' 'webkit2gtk' 'gvfs' 'mesa-demos')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-nose-cover3' 'wine' 'xterm')
optdepends=('wine: Windows support'
            'vulkan-icd-loader: Vulkan support'
            'lib32-vulkan-icd-loader: Vulkan support')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$pkgver.tar.gz")
sha512sums=('26a9274908665a18de522b98c37f8c688932753d158a5b2cb35c2ca8553c775e6a34854a660e536a002348bcd018fdfa5e49f6f12faa05001ddcce8e15a1bfba')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() {
  cd $pkgname-$pkgver

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

