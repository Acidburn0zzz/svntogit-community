# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-libdrm-old
pkgname=(lib32-libdrm-old lib32-libdrm-nouveau1)
pkgver=2.4.33
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32 bits)"
arch=('x86_64')
license=('custom')
depends=('lib32-libpciaccess' 'lib32-glibc')
makedepends=('gcc-multilib')
options=('!libtool' '!emptydirs')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/libdrm/libdrm-$pkgver.tar.bz2
        no-pthread-stubs.patch
        COPYING
)
sha1sums=('4da2c635491724e44326871e6a49ccfec0b6b5a6'
          '825ff5e0c4238b31bdea52f104bfec8949270e25'
          'ba3dcd636997ee0d30df14b03dae05c24ae5d094')

build() {
  cd "libdrm-$pkgver"
 
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  patch -Np1 -i "$srcdir/no-pthread-stubs.patch"

  #libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --disable-libkms \
      --disable-intel \
      --disable-radeon \
      --enable-nouveau-experimental-api
  make
}

package_lib32-libdrm-old() {
  pkgdesc="Userspace interface to kernel DRM services - used as makedepends for nouveau-dri (32 bits)"
  conflicts=('lib32-libdrm')
  provides=("lib32-libdrm=$pkgver")

  cd "libdrm-$pkgver"

  make DESTDIR="$pkgdir" install
  rm "$pkgdir"/usr/lib32/libdrm_nouveau.so.1*
}

package_lib32-libdrm-nouveau1() {
  pkgdesc="Userspace interface to kernel DRM services for nouveau - used as depends for nouveau-dri (32 bits)"
  depends=('lib32-libdrm')

  cd "libdrm-$pkgver"

  make DESTDIR="$pkgdir" install-libdrm_laLTLIBRARIES
  make -C nouveau DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" uninstall-libdrm_laLTLIBRARIES
  rm -rf "$pkgdir"/usr/include/  "$pkgdir"/usr/lib32/pkgconfig/libdrm_nouveau.pc  "$pkgdir"/usr/lib32/libdrm_nouveau.so
}
