# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=pantheon-code
pkgver=3.1.1
pkgrel=1
pkgdesc='The Pantheon Text Editor'
arch=(x86_64)
url=https://github.com/elementary/code
license=(GPL3)
depends=(
  editorconfig-core-c
  glib2
  gtk3
  gtksourceview3
  libgee
  libgit2-glib
  libgranite.so
  libpeas
  pango
)
makedepends=(
  git
  gobject-introspection
  gtkspell3
  intltool
  libsoup
  meson
  vala
  vte3
  webkit2gtk
)
optdepends=(
  'gtkspell3: Spell Check plugin'
  'libsoup: Pastebin plugin'
  'vala: Outline plugin'
  'vte3: Terminal plugin'
  'webkit2gtk: Browser Preview plugin'
)
_commit=66f277327ba481bcc8e0bb761ee7297495a4c4ff  # tags/3.1.1
source=("pantheon-code::git+https://github.com/elementary/code.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

# vim: ts=2 sw=2 et:
