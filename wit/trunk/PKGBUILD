# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Gordin <gordin@koeln.ccc.de>

pkgname=wit
pkgver=3.02a
pkgrel=1
pkgdesc='Wiimms ISO Tools'
arch=(x86_64)
url=https://wit.wiimm.de
license=(GPL2)
depends=(
  bzip2
  fuse2
  glibc
  libncursesw.so
  zlib
)
makedepends=(clang)
optdepends=(
  'bash: Update titles lists'
  'wget: Update titles lists'
)
install=wit.install
source=(
  https://download.wiimm.de/source/wiimms-iso-tools/wiimms-iso-tools.source-${pkgver}.txz
  wit-makefile.patch
  wit-no-exec-stack.patch
  wit-titles.patch
)
sha256sums=('b7a91ac2cfc40cbf0f8c0627a7888fa9f41b3549b0276039fddd076df6dc5d40'
            '71de6228dd9f3d6c6978c98874415f73d97b09a008c7fb94813606c9b6834e7e'
            'b6028504dfef12b8a2f0073d462eb647531033151d0b89b0027a590c7cdc60ef'
            'ace3f1245430e9db8962ecb0d4f459ab73bb43f2d5d536a6fac82ad9b34186fa')

prepare() {
  cd wiimms-iso-tools.source-${pkgver}

  patch -Np1 -i ../wit-makefile.patch
  patch -Np1 -i ../wit-no-exec-stack.patch
  patch -Np1 -i ../wit-titles.patch
}

build() {
  make INSTALL_PATH="${pkgdir}/usr" CC=clang -C wiimms-iso-tools.source-${pkgver} tools
  make INSTALL_PATH="${pkgdir}/usr" CC=clang -C wiimms-iso-tools.source-${pkgver} doc
}

package () {
  make INSTALL_PATH="${pkgdir}/usr" CC=clang -C wiimms-iso-tools.source-${pkgver} install
  install -Dm 644 wiimms-iso-tools.source-${pkgver}/doc/*.txt -t "${pkgdir}"/usr/share/doc/wit/
}

# vim: ts=2 sw=2 et:
