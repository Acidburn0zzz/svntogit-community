# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-net
pkgver=0.0.20190512
_commit=3ec19112720433827bbce8be9342797f5a6aaaf9
pkgrel=1
pkgdesc='Supplementary Go networking libraries'
arch=('any')
url="https://github.com/golang/net"
license=('BSD')
depends=('go-pie' 'golang-golang-x-crypto' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/net/archive/$_commit.tar.gz")
sha512sums=('afa4f33b3f772a642dabb542fda2421ce5858ce9f0cef7cf2922df60b74d1ec7679b59bdd219878b1956ace0ff511366394efd463bd44b1cedd8338fc489c417')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$srcdir"/build/src/golang.org/x/net
  cd "$srcdir"/build/src/golang.org/x/net
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/net

  install -Dm644 net-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
