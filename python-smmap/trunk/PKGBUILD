# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

_name=smmap
pkgname=python-smmap
pkgver=3.0.2
pkgrel=1
pkgdesc="A pure git implementation of a sliding window memory map manager"
arch=('any')
url="https://github.com/gitpython-developers/smmap"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("${pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/s/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('52db538f16b9d0bdbbb932c7eb67510facd0638ea69dec21e9d503c3381195130ac0bc1f48db24ca76b8e31b07279799a767dd7e1bf1d755e7f7684f3285b8da'
            '52db538f16b9d0bdbbb932c7eb67510facd0638ea69dec21e9d503c3381195130ac0bc1f48db24ca76b8e31b07279799a767dd7e1bf1d755e7f7684f3285b8da')
validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
