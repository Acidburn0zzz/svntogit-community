# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cinderclient
pkgver=7.0.0
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="https://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
         'python-requests' 'python-oslo-utils')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-cinderclient/archive/$pkgver.tar.gz")
sha512sums=('7c05560c0d2318296f54607799aa3d6bbd2c12112993e7d9502a010a43a6d57fe3445984d0c94155152861c3122265c40bdb23c0581821175e7919609f31eeb0')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-cinderclient-$pkgver/requirements.txt
}

build() {
  cd python-cinderclient-$pkgver
  python setup.py build
}

check() {
  cd python-cinderclient-$pkgver
  stestr run
}

package() {
  cd python-cinderclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
