# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-2
_patchver=5
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=2
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen polymake)
optdepends=('polymake: Polymake module')
source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
         omfree-segfault.patch::"https://github.com/Singular/Sources/commit/014a3fc5.patch")
#source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('743593fa17e0f87ff2ab61e87653e95c6c00a615e3a2e6fb1f0e43461473b89f'
            '53ff434a06b5202abd5b6c4922e7c061867f607e6a268ff852462a8c6f42a742')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -Rp1 -i ../omfree-segfault.patch
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib --docdir=/usr/share/doc
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  install -Dm644 doc/singular.idx -t "$pkgdir"/usr/share/singular
  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/singular/singular.idx
}
