# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-2
_patchver=4
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=1
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen polymake)
optdepends=('polymake: Polymake module')
source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz")
#source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('7b066001eee7b3269c0b68aeb2ee5fff4477d79988528ada9ed5a2cde2daee7e')
options=(!zipman)

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib --docdir=/usr/share/doc
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  install -Dm644 doc/singular.idx -t "$pkgdir"/usr/share/singular
  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/singular/singular.idx
}
