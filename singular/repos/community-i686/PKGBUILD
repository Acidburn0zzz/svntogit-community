# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-0-3
_patchver=3
pkgver=${_majver//-/.}.p${_patchver}
pkgrel=2
pkgdesc="Computer Algebra System for polynomial computations"
arch=(i686 x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz" 
        singular-datadir.patch)
md5sums=('129a642d9dca9efe6015e38935b0c6f7'
         '489a368564957ce863941612fed038a3')

prepare() {
  cd singular-${_majver//-/.}
# Fix paths in .desktop files
  patch -p1 -i ../singular-datadir.patch
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

# fix permissions
  chown -R root:root "$pkgdir"/usr/share
# move singular.hlp to the path expected by SageMath
  cp "$pkgdir"/usr/share/{info,singular}/singular.hlp
}
