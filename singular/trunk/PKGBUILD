# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-0-3
_patchver=4
pkgver=${_majver//-/.}.p${_patchver}
pkgrel=1
pkgdesc="Computer Algebra System for polynomial computations"
arch=(i686 x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
        singular-ntl10.patch::"https://github.com/Singular/Sources/commit/861b7899.patch")
md5sums=('5b5c67b2ff2e8fa12d2fc5afc327e2d8'
         '3dce1e6533a1b4686f9730e0b534df65')

prepare() {
  cd singular-${_majver//-/.}
# Fix build with NTL 10
  patch -p1 -i ../singular-ntl10.patch
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

# fix permissions
  chown -R root:root "$pkgdir"/usr/share
# move singular.hlp to the path expected by SageMath
  cp "$pkgdir"/usr/share/{info,singular}/singular.hlp
}
