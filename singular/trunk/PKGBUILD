# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-0
#_patchver=5
#pkgver=${_majver//-/.}.p${_patchver}
pkgver=${_majver//-/.}
pkgrel=1
pkgdesc="Computer Algebra System for polynomial computations"
arch=(i686 x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz")
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
md5sums=('14ea37ee7ddea2a8828c228d6e1c49c7')

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

# fix permissions
  chown -R root:root "$pkgdir"/usr/share
  chmod 755 "$pkgdir"/usr/share/singular/html
  chmod 644 "$pkgdir"/usr/share/singular/html/*
  chmod 644 "$pkgdir"/usr/share/info/singular.hlp
# move singular.hlp to the path expected by SageMath
  install -Dm644 "$pkgdir"/usr/share/{info,singular}/singular.hlp
}
