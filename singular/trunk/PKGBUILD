# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-1
_patchver=4
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=3
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz")
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('79d6ed8489ef08eed4d4f4b8f8be4ab4da70f611e0b8889fdc6f825f25981a84')
options=(!zipman)

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/doc/singular.idx
  # Fix singular.idx location https://www.singular.uni-kl.de:8002/trac/ticket/842
  mv "$pkgdir"/usr/share/doc/singular.idx "$pkgdir"/usr/share/singular
  rm -r "$pkgdir"/usr/share/doc
}
