# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Moritz Bunkus <moritz@bunkus.org>

pkgname='perl-moo'
pkgver=2.002004
pkgrel=1
pkgdesc="Minimalist Object Orientation (with Moose compatiblity)"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-class-method-modifiers>=1.1' 'perl-devel-globaldestruction>=0.11' 'perl-import-into>=1.002' 'perl-module-runtime>=0.014' 'perl-role-tiny>=2')
makedepends=('perl-test-fatal>=-0.003')
url='https://metacpan.org/release/Moo'
source=("https://cpan.metacpan.org/authors/id/H/HA/HAARG/Moo-${pkgver}.tar.gz")
md5sums=('e94b38d429c7e8546983953ee296031f')

build() {
  cd $srcdir/Moo-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null
  perl Makefile.PL
  make
}

check() {
  cd $srcdir/Moo-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
#  make test
}

package() {
  cd $srcdir/Moo-$pkgver
  make install
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
