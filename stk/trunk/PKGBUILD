# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=stk
pkgver=4.6.0
pkgrel=2
pkgdesc='The Synthesis ToolKit in C++'
arch=('x86_64')
url='https://ccrma.stanford.edu/software/stk/'
license=('MIT')
depends=('jack')
optdepends=('tk: run provided demos/samples')
source=("https://ccrma.stanford.edu/software/stk/release/$pkgname-$pkgver.tar.gz")
md5sums=('7c0880985023d629036d221f464a070e')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure RAWWAVE_PATH=/usr/share/${pkgname}/rawwaves/ \
              --prefix=/usr \
              --enable-shared \
              --with-alsa \
              --with-jack
  make
  make -C projects/demo
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
  # demo
  install -vDm 755 projects/demo/stk-demo -t "${pkgdir}/usr/bin"
  install -vDm 644 projects/demo/tcl/*.tcl \
    -t "${pkgdir}/usr/share/${pkgname}/tcl"
  install -vDm 644 projects/demo/scores/{README,*.ski} \
    -t "${pkgdir}/usr/share/${pkgname}/scores"
  install -vDm 644 rawwaves/*.{raw,c} \
    -t "${pkgdir}/usr/share/${pkgname}/rawwaves"
  # docs
  install -vDm 644 doc/{README-Linux,ReleaseNotes,SKINI,hierarchy}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 doc/html/*.{html,png,js} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/${pkgname}"
}
