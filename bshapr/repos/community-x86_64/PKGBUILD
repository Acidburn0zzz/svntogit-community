# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BShapr
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BShapr"
pkgname=bshapr
pkgver=0.4
pkgrel=1
pkgdesc="Beat/ envelope shaper LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BShapr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint' 'sord')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('fbfa43d3141eb80d61c2f00dcffa09b881c1e5a811ca7c76cae66ac869e05c214f983e428fdb1bbf843b5a562579927c40393fad7fc612507c0a00b7259c35c8')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  rm -v .gitignore
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  sord_validate "${_name}.lv2/"*.ttl || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
  LV2_PATH="${PWD}" lv2lint "${_plugin_uri}" || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
