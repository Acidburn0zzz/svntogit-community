# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BShapr
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BShapr"
pkgname=bshapr
pkgver=0.11
pkgrel=1
pkgdesc="Beat/ envelope shaper LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BShapr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-0.11-build.patch::https://github.com/sjaehn/BShapr/pull/20/commits/db6b6821e9b704c7d1620fdc54b12077f9c71476.patch")
sha512sums=('a93a559da6e185f700b312bf09fa09f4bbd6ae9d5a8c14b01048bae348fbfe2394929c70dbb41092c4c3e712cc453183fcad46f5eeb8571b709ebf0a8de32a40'
            '6a6aad1e9ebd975a7ed1ef838642d28a8e9ff26377e162c1877c4dcac37fefeecfe838eb41dd006a5ae4d9401b65778bc95073767199d10c597de76c36df17c2')
b2sums=('1bcf03c911e273e013071bd53057e885620c5781717376308fd5bed7b3b56b5c95cb8aa43a9acff6f80d1f07fc9627336b4ea88cdc6b2e42624a09658690a7cb'
        'adbcbf51b843dffd108e10129cc5245f22898d20c33d3cdead498632b06fdce26838273e0aeddc2775ccb06795c0cc25b70ae803cdb44238a8ac2f1126eb680c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  # the makefile breaks on concurrency issues:
  # https://github.com/sjaehn/BShapr/issues/19
  patch -Np1 -i ../"${pkgname}-0.11-build.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "${_plugin_uri}" || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
