# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BShapr
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BShapr"
pkgname=bshapr
pkgver=0.10
pkgrel=1
pkgdesc="Beat/ envelope shaper LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BShapr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('36819fb8186e27a6228a0e4cafadead87242fe5fd25cbfc6926b94fcc5c54c33e4b89f88bb5af0997a5a4e013509c5d87bed51dff4ca403b74565f4f8a823ba1')
b2sums=('d1387081e2fe94647e42e4861ff90eb7f81aeb4f34a4c1d299c15830a89da436c07af7fedb593f6f8b5f8e0906eb1a13e90a2a5517bbc3539cfcbcbd26afef9a')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "${_plugin_uri}" || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
