# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=singletons
pkgname=haskell-singletons
pkgver=2.6
pkgrel=9
pkgdesc="A framework for generating singleton types"
url="https://github.com/goldfirere/singletons"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-th-desugar' 'haskell-syb')
makedepends=('ghc' 'haskell-tasty' 'haskell-tasty-golden' 'haskell-turtle')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('6019bcd5e60718055d7a1369902c27b27a3b58c86049eaac4411bd347670f6e9835cfb680ea2126b6b4614688cb1e9ea9025dc590a9df83e1e654bb115db600d')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
