# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Achilleas Koutsou <achilleas@koutsou.net>

pkgname=osbuild
pkgdesc='Build Pipelines for Operating System Artifacts'
pkgver=52
pkgrel=4
url="https://www.osbuild.org"
arch=(any)
license=(Apache)
depends=(bubblewrap curl e2fsprogs qemu python util-linux python-yaml python-jsonschema)
makedepends=(make python-docutils python-setuptools)
optdepends=('dnf: build rpm/dnf images'
            'pacman: build pacman images')
source=($pkgname-$pkgver.tar.gz::https://github.com/osbuild/osbuild/archive/refs/tags/v${pkgver}.tar.gz
        ./no-selinux-distro-fix.patch)
sha256sums=('f0cfc3ddf22833b334773dd8b1275b2e2ec3ff192797a362a8c030676ba4abb8'
            'f2b4cf3910673beb8d229ed436cdd6d5f9b35c8ceea28c6dbc4f022c96db0a26')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ${srcdir}/no-selinux-distro-fix.patch
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  make man
  python setup.py install --skip-build --root="$pkgdir"

  install -m 0755 -d "$pkgdir/usr/lib/osbuild/osbuild"

  for group in assemblers devices inputs runners schemas sources stages mounts ; do
    cp -vdr --no-preserve=ownership "$group" "$pkgdir/usr/lib/osbuild/"
  done

  # udev rules
  install -Dm644 data/10-osbuild-inhibitor.rules "$pkgdir"/usr/lib/udev/rules.d/10-osbuild-inhibitor.rules
}
