# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

_resolutions=( 16 32 48 64 96 128 256 512 1024 )
_name=pure-data
pkgname=pd
pkgver=0.49.1
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('BSD')
groups=('pro-audio')
depends=('fftw' 'hicolor-icon-theme' 'portaudio' 'tk')
makedepends=('gendesk' 'inkscape' 'portmidi')
optdepends=('portmidi: for alternative portmidi support')
provides=('puredata')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pure-data/pure-data/archive/${_ver}.tar.gz"
        "${pkgname}.svg::https://raw.githubusercontent.com/pure-data/pd-icon/master/masters/icon.svg")
sha512sums=('c2dd4eadc622b1a626095cad95ce7e34be569414671af5938b339665526545174cf7abb77bf49943f617a93b3fae54fc890df46f1b9f73cb93b13a36db490fd7'
            '3c88122e0bdbdda73d47a688de7264f4cf86537b981dfabcbc646fc621189612bd0ae4804ba962d533072df85faddb9a77daf3eb7c6ef8ea46c22e3216bf0d74')

prepare() {
  mv -v "${_name}-${_ver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  # XDG desktop files
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --exec ${pkgname} \
          --name "Pure Data" \
          --categories "AudioVideo;Audio"
  for _res in ${_resolutions[@]}; do
    inkscape -z -e "pd-${_res}.png" \
             -w "${_res}" \
             -h "${_res}" \
             "../${pkgname}.svg"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-fftw \
              --enable-jack \
              --enable-portaudio \
              --enable-portmidi \
              --without-local-portaudio \
              --without-local-portmidi
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # license
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # desktop file
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  # icons
  install -vDm 644 "${pkgname}-32.png" \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 "${srcdir}/${pkgname}.svg" \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps"
  for _res in ${_resolutions[@]}; do
    install -vDm 644 "${pkgname}-${_res}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_res}x${_res}/apps/${pkgname}.png"
  done
  # readme
  install -vDm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # fix broken symlink
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  rm -v "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ln -sv "/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
}
