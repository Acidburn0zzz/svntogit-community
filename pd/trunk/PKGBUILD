# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>

pkgname=pd
pkgver=0.43.3
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://crca.ucsd.edu/~msp/software.html"
license=('custom:BSD')
depends=('jack' 'fftw' 'tk')
provides=('puredata')
source=("http://crca.ucsd.edu/~msp/Software/pd-$_ver.src.tar.gz")
md5sums=('c0a565d3be92844780a86524598a5137')

build() {
  cd "$srcdir/pd-$_ver/src"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --enable-fftw
  make
}

package() {
  cd "$srcdir/pd-$_ver/src"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 ../LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
