# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Alessio 'mOLOk' Bolognino <themolok@gmail.com>
# Contributor: Pawel Rusinek <p.rusinek@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=griffith
pkgver=0.12.1
pkgrel=4
pkgdesc="Movie collection manager application"
arch=('any')
url="http://griffith.berlios.de"
license=('GPL2')
depends=('pygtk>=2.14.1' 'sqlite3' 'python-pysqlite' \
         'python-reportlab>=2.3' 'pil' 'pyxml' 'python2-sqlalchemy')
optdepends=('python2-psycopg2: postgreSQL support'
            'mysql-python: MySQL support'
            'python2-chardet: encoding detection of imported CSV files'
            'gnome-python-extras: spell checking support'
            'griffith-extra-artwork: additional icons')
source=(http://launchpad.net/griffith/trunk/${pkgver}/+download/griffith-${pkgver}.tar.gz sqlalchemy.patch)

build() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/sqlalchemy.patch

    # python2 fix
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' griffith
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  # The program creates a wrong symlink so make a new one
  rm ${pkgdir}/usr/bin/griffith
  ln -s /usr/share/griffith/lib/griffith ${pkgdir}/usr/bin/griffith 
}
md5sums=('fb77aac2b1956bcb0eb766380daaf77b'
         '84b3fa3cd65d655c4cdfe8dd1fe9b429')
