# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>

pkgbase=flatbuffers
pkgname=(flatbuffers python-flatbuffers)
pkgver=1.12.1
pkgrel=1
pkgdesc='An efficient cross platform serialization library for C++, with support for Java, C# and Go'
arch=(x86_64)
url='https://google.github.io/flatbuffers/'
license=(Apache)
depends=(gcc-libs)
makedepends=(cmake python-setuptools)
source=(https://github.com/google/$pkgbase/archive/v$pkgver/$pkgbase-$pkgver.tar.gz)
sha256sums=('90e6a65ff5b8e9fc225af3a9d7bf4d2e120cfe40c27dfd5527f67c0d4eb5b954')

prepare() {
  sed -i 's/-Werror=/-W/g;s/-Werror//g' $pkgname-$pkgver/CMakeLists.txt
}

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DFLATBUFFERS_BUILD_FLATLIB=OFF \
    -DFLATBUFFERS_BUILD_SHAREDLIB=ON
  cmake --build build

# Python bindings
  cd $pkgbase-$pkgver/python
  VERSION=$pkgver python setup.py build
}

check() {
  cmake --build build --target test
}

package_flatbuffers() {
  DESTDIR="$pkgdir" cmake --install build
}

package_python-flatbuffers() {
  pkgdesc='An efficient cross platform serialization library for Python'
  depends=(python)

  cd $pkgbase-$pkgver/python
  VERSION=$pkgver python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
