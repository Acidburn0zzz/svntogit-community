# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>

pkgname=gnome-games
pkgver=3.32.0
pkgrel=1
pkgdesc='Simple game launcher for GNOME'
arch=('x86_64')
license=('GPL3')
url='https://wiki.gnome.org/Apps/Games'
depends=('grilo' 'libhandy' 'libmanette' 'retro-gtk' 'tracker')
makedepends=('meson' 'vala')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        "Revert-gamepad-view-Stop-accessing-Rsvg.Handle-fields.patch")
sha256sums=('be500607e5695b37d6c7a36a70c71ea886be1428302ffe3364d9d1ed6432b863'
            '4a9bc1b9247470e509e489eb51f8f1600ee581281779878a5df9fbbc2b7e73aa')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../Revert-gamepad-view-Stop-accessing-Rsvg.Handle-fields.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
