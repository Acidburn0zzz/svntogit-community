# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tom Richards <tom@tomrichards.net>

pkgname=gnome-games
pkgver=40.0
pkgrel=1
pkgdesc='Simple game launcher for GNOME'
arch=('x86_64')
license=('GPL3')
url='https://wiki.gnome.org/Apps/Games'
depends=('grilo' 'libarchive' 'libhandy' 'libmanette' 'retro-gtk' 'tracker')
makedepends=('meson' 'vala' 'git')
_commit=836807b418618e112d4c566ca45bcb5d9d100a31  # tags/40.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-games.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
