# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-update-checker
pkgver=0.16
pkgrel=3
pkgdesc="A python module that will check for package updates"
arch=('any')
license=('BSD')
url='https://github.com/bboe/update_checker'
depends=('python-requests')
provides=('python-update_checker')
conflicts=('python-update_checker')
replaces=('python-update_checker')
makedepends=('python-setuptools' 'python-requests')
source=("https://pypi.io/packages/source/u/update_checker/update_checker-$pkgver.tar.gz")
md5sums=('dcd73326d7e4db4226575cca03d24e42')

build() {
  cd "$srcdir"/update_checker-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/update_checker-$pkgver
  python setup.py test
}

package() {
  cd update_checker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
