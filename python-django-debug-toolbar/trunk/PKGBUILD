# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Anthony25 <Anthony Ruhier>
# Contributor Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>

_name=django-debug-toolbar
pkgname=python-django-debug-toolbar
pkgver=3.2
pkgrel=1
pkgdesc='A configurable set of panels that display various debug information about the current request/response.'
arch=(any)
url='https://github.com/jazzband/django-debug-toolbar'
license=(BSD)
makedepends=(python-setuptools)
depends=(python-django python-sqlparse)
options=(!emptydirs)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jazzband/${_name}/archive/${pkgver}.tar.gz)
sha256sums=('ffa8975f09e9acc18567bd11738a237002a6179ed0b9397e32faed70c5bd50b2')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
