# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk3-greeter
pkgver=1.5.2
pkgrel=2
epoch=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/3/}")
conflicts=("${pkgname/3/2}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/${pkgname/3/}/1.6/${pkgver}/+download/${pkgname/3/}-${pkgver}.tar.gz")
sha256sums=('0de611783a9306130b0f0865c533fcaf2d21fcda9f85e7ee81b58cfa5bd781cc')

build() {
  cd "${srcdir}"/${pkgname/3/}-${pkgver}

  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --libexecdir=/usr/lib/lightdm --disable-static
  make
}

package() {
  cd "${srcdir}"/${pkgname/3/}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
