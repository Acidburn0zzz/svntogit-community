# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Padfoot <padfoot@exemail.com.au>

pkgname=lightdm-gtk3-greeter
pkgver=1.3.1
pkgrel=1
epoch=1
pkgdesc="GTK+ greeter for LightDM"
arch=('i686' 'x86_64')
url="https://launchpad.net/lightdm-gtk-greeter"
license=('GPL3' 'LGPL3')
depends=('gtk3' 'lightdm')
makedepends=('exo' 'gnome-doc-utils' 'gobject-introspection' 'intltool')
provides=("${pkgname/3/}")
backup=('etc/lightdm/lightdm-gtk-greeter.conf')
source=("https://launchpad.net/${pkgname/3/}/1.4/${pkgver}/+download/${pkgname/3/}-${pkgver}.tar.gz")
sha256sums=('a384b6413a52e6c6aeaf3242b2420e857ad7e2e4e5bec47da663e67076c0ee87')

build() {
  cd "${srcdir}"/${pkgname/3/}-${pkgver}

# Build
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/lightdm --disable-static
  make
}

package() {
  cd "${srcdir}"/${pkgname/3/}-${pkgver}

# Install
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
