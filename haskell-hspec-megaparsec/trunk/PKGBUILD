# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-megaparsec
pkgname=haskell-hspec-megaparsec
pkgver=2.0.1
pkgrel=21
pkgdesc="Utility functions for testing Megaparsec parsers with Hspec"
url="https://github.com/goldfirere/hspec-megaparsec"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-hspec-expectations' 'haskell-megaparsec')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        https://github.com/mrkkrp/hspec-megaparsec/commit/9603717ecfda0496d7a0a08d3b33a527bbd403cb.patch)
sha512sums=('758994fccc31b03bb0bec281aa522b83ef0f1c08ab9baeaefbdb2a3e9ab4be51adf5ec839935538379cdfb6b4bfdfc834f8b485cbbd073ea5aff4fbdbfbeed0f'
            'ac01228e1c9e25a01e33490e5e2195e6fb24769421e47bc41bfcce33a854417ec45f4bba14e29a227d5a94f331b2bff4b53fa852b7e764cd88a27fd3a50ba5ee')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../9603717ecfda0496d7a0a08d3b33a527bbd403cb.patch || :
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.md
}
