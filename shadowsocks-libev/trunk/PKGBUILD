# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=shadowsocks-libev
pkgver=3.3.3
pkgrel=2
pkgdesc='A lightweight secured socks5 proxy for embedded devices and low end boxes'
arch=('x86_64')
url='https://github.com/shadowsocks/shadowsocks-libev'
license=('GPL3')
depends=('libcap' 'mbedtls' 'libsodium' 'libev' 'c-ares' 'pcre' 'libcorkipset' 'libbloom')
makedepends=('asciidoc' 'xmlto')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/$pkgname/archive/v$pkgver.tar.gz"
        'shadowsocks-libev@.service'
        'shadowsocks-libev-server@.service'
        'shadowsocks-libev-redir@.service'
        'shadowsocks-libev-tunnel@.service')

sha512sums=('f02b43ee95b18b0a63fae1a1ad56b4d5e41cc79dc38cd9ad71c3830b7bb97a14110299f766b1e2cfbd6ce362f4c76c338693b1d8b558cf18b9c5e53a08fddd38'
            '434a05a265bdd895bfaadfb3d46a38782779955c2637bc923a6dc708f4bf780af898a6c90884a453c4920c7ee41e38855cfbf1a2fc03fb663c4ef262a24c62e6'
            'fd1742bab0e8b66ccc3dabdd2f0e78f8b0a7b478b22e66826ca8394644e81518fcb51cfe20019fe104a63269312db7db24cd3c7b570a92091c1252913761e201'
            '1edbaeecbdc3b94e9ffd6559569a096d5204c0086b59294c8ab04bd9a40dafd5f81cccf7590676920913d249117aa3bd4c5ea18be41af8b8011f4888ddb62f67'
            '370db87042d64e74226bf5514e104647ea94daf280407e83ee5efe21ff4007428fd4bcdc06fb09264e7da8037b79030c3f79fd7d69e67413ac4aa439c4dc01bb')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  sed -i 's|AC_CONFIG_FILES(\[libbloom/Makefile libcork/Makefile libipset/Makefile\])||' configure.ac
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --enable-system-shared-lib
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/shadowsocks-libev@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-server@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-redir@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-redir@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-tunnel@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-tunnel@.service"
}
