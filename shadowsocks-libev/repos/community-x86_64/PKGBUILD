# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=shadowsocks-libev
pkgver=3.1.3
pkgrel=3
pkgdesc='A lightweight secured socks5 proxy for embedded devices and low end boxes'
arch=('x86_64')
url='https://github.com/shadowsocks/shadowsocks-libev'
license=('GPL3')
depends=('libcap' 'mbedtls' 'libsodium' 'libev' 'c-ares' 'pcre' 'libcorkipset' 'libbloom')
makedepends=('asciidoc' 'xmlto')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/$pkgname/archive/v$pkgver.tar.gz"
        gcc8.patch::https://github.com/shadowsocks/shadowsocks-libev/commit/662b6db3f3c395b552fa16f7fb22558aa38fe6aa.patch
        'shadowsocks-libev@.service'
        'shadowsocks-libev-server@.service'
        'shadowsocks-libev-redir@.service'
        'shadowsocks-libev-tunnel@.service')

sha512sums=('a262315cf04c325b5b659f07fd751f2f7b651feed5e2ac7290c1b3ce91fe5bd7efa7ff834f82f3fb00d5551cba8277f8668cdefbfe35279d7835089cbcced55f'
            'e4584c56427a295008714be8b189dcaa99f59b49231f97fce402a3eb1a0efd9b136ed3d1a68ab1906953b24f77e781f8caff63aee63886ac97e9c175f4476c57'
            '57c92f20d26283c87b86a66269a8475117ba4db4d13e5341f1795edb38181edd972179141cd1ef15e9b898b8b42d5319fe1a64edef8b35cb62f98443b701a5dd'
            'b40db992b2e77846064d5b689835bcaf335da961684ca0d6aa0dc798ca9541ee0e0236e10e39f44a9ff3fbe260666a25e5440160121b10a655ac95a3f4ecba0a'
            '8646a373b58327cdb75ed933447b4cb833ecca215807661120909e21d8c2a1d354beff302192e24dc77bfda3db2f806ec2b01a288ddb4f4e4b9ae200d0beeadd'
            '851a17a53cd84b425316c476f423b03d763e0fdfe987543964df4c67567374b833bf776e93476593d81652eda9986beeca1d06c8f13219fe6336c6f3e6441e98')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i ../gcc8.patch

  sed -i 's|AC_CONFIG_FILES(\[libbloom/Makefile libcork/Makefile libipset/Makefile\])||' configure.ac
}

build() {
  cd "$srcdir"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared --enable-system-shared-lib
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  install -Dm644 "$srcdir/shadowsocks-libev@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-server@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-server@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-redir@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-redir@.service"
  install -Dm644 "$srcdir/shadowsocks-libev-tunnel@.service" "$pkgdir/usr/lib/systemd/system/shadowsocks-libev-tunnel@.service"
}
