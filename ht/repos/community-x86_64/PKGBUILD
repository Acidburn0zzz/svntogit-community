# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=ht
pkgver=0.6.0
pkgrel=3
pkgdesc="Yet another HTTPie clone"
arch=('x86_64')
url="https://github.com/ducaale/ht"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
replaces=('ht-rs')
install=ht.install
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('a990e2973cd62cc483dc875e5ee880bfdd759e73f8e6ccf63b188092f7d9f293')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"

  # install as "ht-rust" to avoid TeX conflict
  install -Dm 755 "target/release/$pkgname" "$pkgdir/usr/bin/ht-rust"

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
