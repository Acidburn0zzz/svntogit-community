# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=deepin-anything
pkgname=(deepin-anything deepin-anything-module deepin-anything-dkms)
pkgver=0.0.3
_extramodules=extramodules-ARCH
pkgrel=2
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('qt5-base' 'linux>=4.19.12' 'linux-headers>=4.19.12')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        deepin-anything-server.sysusers
        fix-include.patch)
sha512sums=('8a506ba57d6a2ac9c9089719d18013fbde6443cb4f63de79498269e728b7e8725b4e06e7029ae2d2242aaac955c63e56c680fdd18e3fb0d72e9953a9fc5e8402'
            '0ff6a6de1fbfb0c33eaac511b989da321a9e43ece92708af88aee34ad1a05e55572713b1290bc2705d70b91dc7bec4fb4abd3dc664a0abe01de27d88bd9e9c85'
            '55498f8b14e1b233b4052d516b880f4c27797eede46481135dcf76808e9f00f4ac4fdd0e2e2d7d478d335fb12b7137ca1bef0745e1c8b4195e025b81eec94d21')

prepare() {
  cd deepin-anything-$pkgver
  patch -p1 -i ../fix-include.patch
  sed -i 's|^systemd_service.path.*|systemd_service.path = /usr/lib/systemd/system|' server/src/src.pro
}

build() {
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"

  cd deepin-anything-$pkgver
  make VERSION=$pkgver
  cp -r kernelmod kernelmod-dkms
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package_deepin-anything-module() {
  depends=('linux>=4.19.12' 'linux<4.20')

  cd deepin-anything-$pkgver/kernelmod
  install -dm 755 "$pkgdir"/usr/lib/{modules/$_extramodules,modules-load.d}
  install -m 644 vfs_monitor.ko "$pkgdir"/usr/lib/modules/$_extramodules/
  gzip "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
}

package_deepin-anything-dkms() {
  depends=('dkms')
  provides=('deepin-anything-module')
  conflicts=('deepin-anything-module')

  cd deepin-anything-$pkgver
  install -dm 755 "$pkgdir"/usr/src
  cp -r kernelmod-dkms "$pkgdir"/usr/src/deepin-anything-$pkgver
  install -m644 debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
}

package_deepin-anything() {
  depends=('deepin-anything-module' 'qt5-base')

  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/src

  install -Dm644 ../deepin-anything-server.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-anything-server.conf"
}
