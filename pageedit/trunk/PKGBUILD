# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=pageedit
pkgver=0.9.6
pkgrel=1
pkgdesc="Simple Single Page XHTML Visual Editor, split from Sigil's BookView"
arch=('x86_64')
url="https://github.com/Sigil-Ebook/PageEdit"
license=('GPL3')
depends=('qt5-webengine')
makedepends=('cmake' 'qt5-tools')
source=("PageEdit-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/PageEdit-${pkgver}.tar.gz.sig")
sha256sums=('f9cbfabc486acecdeb4ffe0c7ad5e73eebbb38341e1951c5240262bfbf8bfb05'
            'SKIP')
b2sums=('fcc8aaf04dddbb72d17814825f1d81e010481de6be98946eec2e8218aaa485253a950e2c4ca9bbc6f6971d598f1fe451797664e367c6a2024be4d62fb4469952'
        'SKIP')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

build() {
    mkdir -p "${srcdir}"/PageEdit-${pkgver}/build
    cd "${srcdir}"/PageEdit-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_BUILD_TYPE=Release \
        ..
    make
}

package() {
    cd "${srcdir}"/PageEdit-${pkgver}/build

    make install DESTDIR="${pkgdir}"
}
