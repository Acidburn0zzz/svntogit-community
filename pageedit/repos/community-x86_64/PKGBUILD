# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=pageedit
pkgver=1.2.0
pkgrel=1
pkgdesc="Simple Single Page XHTML Visual Editor, split from Sigil's BookView"
arch=('x86_64')
url="https://github.com/Sigil-Ebook/PageEdit"
license=('GPL3')
depends=('qt5-webengine')
makedepends=('cmake' 'qt5-tools')
source=("PageEdit-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/PageEdit-${pkgver}.tar.gz.sig")
sha256sums=('9142741f2321e2b991fc053a4b8f1755266733adaf75705b1215e2fbf788bb17'
            'SKIP')
b2sums=('9e282f33ffcd24e50a151c48188f2ba1617352be7c658caebaeca145d0c4d1ce9f30f3e42d6ffe2234aa326427a00a1f3ba0f6e60c518ec978b6780c913b40e0'
        'SKIP')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

build() {
    mkdir -p "${srcdir}"/PageEdit-${pkgver}/build
    cd "${srcdir}"/PageEdit-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_BUILD_TYPE=Release \
        ..
    make
}

package() {
    cd "${srcdir}"/PageEdit-${pkgver}/build

    make install DESTDIR="${pkgdir}"
}
