# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Marco Bartholomew <marco@marcobartholomew.com>

pkgname=monit
pkgver=5.2.1
pkgrel=1
pkgdesc="Utility for managing and monitoring, processes, files, directories and devices on a *NIX system"
arch=('i686' 'x86_64')
url="http://mmonit.com/monit/"
license=('GPL3')
depends=('bison' 'flex' 'openssl')
backup=('etc/monitrc')
changelog=$pkgname.changelog
source=(http://mmonit.com/$pkgname/dist/$pkgname-$pkgver.tar.gz \
        monitd)
md5sums=('19ee6247b7528947d151720767bcf9d3'
         '612d2fd53c5e45abef8cb820e1b93f1d')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -D -m755 ${srcdir}/monitd  ${pkgdir}/etc/rc.d/monitd
  install -D -m700 monitrc ${pkgdir}/etc/monitrc
}
