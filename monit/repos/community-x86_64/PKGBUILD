# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Marco Bartholomew <marco@marcobartholomew.com>

pkgname=monit
pkgver=5.3.2
pkgrel=1
pkgdesc="Utility for managing and monitoring, processes, files, directories and devices on a *NIX system"
arch=('i686' 'x86_64')
url="http://mmonit.com/monit/"
license=('GPL3')
depends=('openssl')
backup=('etc/monitrc')
changelog=$pkgname.changelog
source=(http://mmonit.com/$pkgname/dist/$pkgname-$pkgver.tar.gz \
        monitd)
sha256sums=('406a06ac912525c8e76066d07235c848466b331532b2b3de931b61fe455ae915'
            'b235b2fdeec49015d6b74f028d161b289663692f9ee2d2d78cf52d8db87ca496')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr

  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -D -m755 ${srcdir}/monitd  ${pkgdir}/etc/rc.d/monitd
  install -D -m700 monitrc ${pkgdir}/etc/monitrc
}
