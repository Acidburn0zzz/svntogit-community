# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='Library to create Wayland desktop components using the Layer Shell protocol'
pkgname=gtk-layer-shell
pkgver=0.4.0
pkgrel=1
arch=(x86_64)
license=(LGPL3)
url="https://github.com/wmww/gtk-layer-shell"
depends=(
    "gtk3"
    "wayland"
)
makedepends=(
    "gtk-doc"
    "gobject-introspection"
    "meson"
    "ninja"
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wmww/gtk-layer-shell/archive/v$pkgver.tar.gz")
sha512sums=('60882fa1b79c81ececd7d5dd1b3722cde10f48ff1e25a533afc6c0474d75f8e83d596ec3585301917337e770766db4a14c1c45132c4fef09d49967e2fd531049')

build() {
    meson --prefix=/usr \
          --wrap-mode=nofallback \
          --buildtype=plain \
          -Ddocs=true \
          "$pkgname-$pkgver" \
          build
    ninja -C build
}

test() {
    ninja -C test
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
}
