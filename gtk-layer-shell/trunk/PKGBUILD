# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgdesc='Library to create Wayland desktop components using the Layer Shell protocol'
pkgname=gtk-layer-shell
pkgver=0.4.0
pkgrel=2
arch=(x86_64)
license=(LGPL3)
url="https://github.com/wmww/gtk-layer-shell"
depends=(
    "gtk3"
    "wayland"
)
makedepends=(
    "gtk-doc"
    "gobject-introspection"
    "meson"
    "ninja"
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/wmww/gtk-layer-shell/archive/v$pkgver.tar.gz"
        "disable_license_check.patch")
sha512sums=('60882fa1b79c81ececd7d5dd1b3722cde10f48ff1e25a533afc6c0474d75f8e83d596ec3585301917337e770766db4a14c1c45132c4fef09d49967e2fd531049'
            '84e1c890ac57bd516e3616da7bfe096d4aa4b5c495140871e004a02af7d789377689e3f54344a9335fa4a8ee1d9fb6a9b914ba45b188b0e17abf4a25de2031ac')

prepare() {
    # https://github.com/wmww/gtk-layer-shell/issues/73
    patch -p1 -d "$pkgname-$pkgver" < disable_license_check.patch
}

build() {
    meson --prefix=/usr \
          --wrap-mode=nofallback \
          --buildtype=plain \
          -Dtests=true \
          -Ddocs=true \
          "$pkgname-$pkgver" \
          build
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
}
