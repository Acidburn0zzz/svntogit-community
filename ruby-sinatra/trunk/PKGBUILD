# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_gemname=sinatra
pkgname=ruby-${_gemname}
pkgver=2.0.5
pkgrel=1
pkgdesc="Classy web-development dressed in a DSL"
arch=(any)
url="http://sinatrarb.com"
license=(MIT)
depends=('ruby' 'ruby-rack' 'ruby-tilt' 'ruby-rack-protection' 'ruby-mustermann')
options=(!emptydirs)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sinatra/sinatra/archive/v${pkgver}.tar.gz)
sha512sums=('da626d584f065f924fa5a42802b0e7a03e40e780cfd92e9cb072c86e9cb6d8ba4a699d7383a81b13ac0ffd0ce427900cdf059b77565ace5961dbd00c088ce0f9')

prepare() {
  cd ${_gemname}-${pkgver}
  sed 's|git ls-files|find|' -i ${_gemname}.gemspec
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  # https://github.com/sinatra/sinatra/issues/1082
  gem install --ignore-dependencies --no-user-install --no-ri --no-rdoc -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" $_gemname-$pkgver.gem
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/$_gemname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
