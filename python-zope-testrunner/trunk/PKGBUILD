# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=('python-zope-testrunner' 'python2-zope-testrunner')
pkgver=4.5.0
pkgrel=1
pkgdesc="Zope testrunner script"
arch=('any')
url="https://github.com/zopefoundation/zope.testrunner"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-exceptions' 'python2-zope-exceptions'
             'python-six' 'python2-six' 'git')
checkdepends=('python-nose' 'python2-nose' 'python-zope-testing' 'python2-zope-testing'
              'python2-subunit' 'python2-virtualenv')
source=("git+https://github.com/zopefoundation/zope.testrunner.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cp -a zope.testrunner{,-py2}
}

build() {
  cd "$srcdir"/zope.testrunner
  python setup.py build

  cd "$srcdir"/zope.testrunner-py2
  python2 setup.py build
}

check() {
  # Hack to process .pth files properly
  # TODO: --system-site-packages breaks the tests

  cd "$srcdir"/zope.testrunner
  pyvenv "$srcdir"/pyvenv
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py test
  )

  cd "$srcdir"/zope.testrunner-py2
  virtualenv2 "$srcdir"/pyvenv-py2
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py test
  )
}

package_python-zope-testrunner() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-exceptions' 'python-six')

  cd zope.testrunner
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-testrunner() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-exceptions' 'python2-six')

  cd zope.testrunner-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir/usr/bin/zope-testrunner"{,2}
}

# vim:set ts=2 sw=2 et:
