# Maintainer: David Runge <dvzrv@archlinux.org>
_name=doit
pkgname=python-doit
pkgver=0.32.0
pkgrel=1
pkgdesc="A task management and automation tool"
arch=('any')
url="https://pydoit.org/"
license=('MIT')
depends=('python-cloudpickle' 'python-pyinotify' 'python-setuptools')
checkdepends=('flake8'  'python-coverage' 'python-pytest')
optdepends=('bash-completion: autocomplete for bash'
            'zsh-completions: autocomplete for ZSH')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d5c28a0c6a38648aeef58027b2246fc26360e133e91933f2244bd484f70d6d26110acbfec19c74d53c796e0bbae07a4bf6d76542d45e8b1a6b503c186bf2a68a')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

# tests are broken:
# https://github.com/pydoit/doit/issues/342
check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build/lib:${PYTHONPATH}"
  py.test -k 'not test_execute and not test_not_picklable_raises_InvalidTask'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "bash_completion_${_name}" \
    "${pkgdir}/usr/share/bash-completion/completions/${_name}"
  install -vDm 644 "zsh_completion_${_name}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${_name}"
}
