# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-flexmock
pkgname=('python-flexmock' 'python2-flexmock')
pkgver=0.10.3
pkgrel=1
pkgdesc="Mock/Stub/Spy library for Python"
arch=('any')
license=('BSD')
url="http://has207.github.com/flexmock"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bkabrda/flexmock/archive/$pkgver.tar.gz")
sha512sums=('1cd1352202d3564e5a223a073b7b61ba0fa1fc8cdd018b660a0c442103df3a971ec511c4295d02923963518e37110ddb354942538918135e5d745c38bcd4a98e')

export LC_CTYPE=en_US.UTF-8

build() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir"/flexmock-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-flexmock() {
  depends=('python')

  cd flexmock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flexmock() {
  depends=('python2')

  cd flexmock-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
