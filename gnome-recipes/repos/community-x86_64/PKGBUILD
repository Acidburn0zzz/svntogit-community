# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-recipes
pkgver=2.0.2
pkgrel=4
pkgdesc="Recipe management application for GNOME"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Recipes"
license=(GPL3)
groups=(gnome-extra)
depends=(gnome-autoar gnome-online-accounts gspell libcanberra libsoup)
makedepends=(git itstool meson)
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('1be9d2fcb7404a97aa029d2409880643f15071c37039247a6a4320e7478cd5fb')

prepare() {
  mkdir build
  cd $pkgname-$pkgver

  # Don't use legacy path for AppStream metainfo file
  # https://gitlab.gnome.org/GNOME/recipes/issues/54
  sed -i "s/install_dir: join_paths(\[datadir,'appdata'\])/install_dir: join_paths([datadir,'metainfo'])/
          s|'../po'|join_paths (meson.source_root (), 'po')|" data/meson.build
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
