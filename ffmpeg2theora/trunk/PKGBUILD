# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=ffmpeg2theora
pkgver=0.28
pkgrel=3
pkgdesc="Simple converter to create Ogg Theora files"
arch=('x86_64' 'i686')
url="http://www.v2v.cc/~j/ffmpeg2theora/"
license=('GPL3')
depends=('ffmpeg')
makedepends=('pkgconfig' 'scons') # 'libkate')
source=("http://www.v2v.cc/~j/$pkgname/downloads/$pkgname-$pkgver.tar.bz2")
sha256sums=('6893c1444d730a1514275ba76ba487ca207205b916d6cb1285704225ee86fe1e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's:pp_mode_t:pp_mode:' src/ffmpeg2theora.c
  sed -i 's:pp_context_t:pp_context:' src/ffmpeg2theora.c
  scons
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  scons install destdir="$pkgdir" prefix="/usr" mandir="PREFIX/share/man"
}

# vim:set ts=2 sw=2 et:
