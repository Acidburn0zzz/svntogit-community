# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.4.5
pkgrel=1
pkgdesc='Unit conversion tool and library written in rust'
arch=('x86_64')
url=https://github.com/tiffany352/rink-rs
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'gmp' 'openssl')
makedepends=('rust')
source=("rink-$pkgver.tar.gz::https://static.crates.io/crates/rink/rink-$pkgver.crate")
sha512sums=('8f7da00609a124f043874f0266d96d86f22ce8ff2457e8107b7d5590702dec1c137b01bcb34b7c714ca4218b6bf7bcbbaf0e9a98c814c88630bb9f089c246b0e')

build() {
  cd rink-$pkgver
  cargo build --release --locked
}

check() {
  cd rink-$pkgver
  cargo test --release --locked
}

package() {
  cd rink-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rink
}

# vim:set ts=2 sw=2 et:
