# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=piep
pkgdesc='Bringing the power of python to stream editing'
pkgver=0.9.0
pkgrel=1
arch=("any")
url="https://github.com/gfxmonk/piep"
license=("GPL")
depends=('python2-pygments' 'python2-setuptools')
makedepends=('python2-nose')
source=("https://github.com/gfxmonk/$pkgname/archive/version-$pkgver.tar.gz")
sha512sums=('1339ece900c35f63de2825a8352216b74fa48b2c5654a79aadacc72aad7698d7aa2f3f48cd70020d975d6a6349e38b24fb1bbfc122a69677caa043ff9385c043')

prepare() {
  cd $pkgname-version-$pkgver
  sed -i "s/'python'/'python2'/" test/test_helper.py
}

check() {
  cd $pkgname-version-$pkgver
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" nosetests2
}

package() {
  cd $pkgname-version-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
