# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Contributor: Severen Redwood <severen@shrike.me>
# Contributor: Robert Welin <robert.welin@gmail.com>
# Contributorr: dsboger <https://github.com/dsboger>

pkgname='gtkd'
pkgver='3.8.0'
pkgrel=1
pkgdesc='D bindings for GTK+ and related libraries.'
arch=('x86_64')
url='http://gtkd.org/'
license=('LGPL')
depends=('liblphobos' 'gtk3')
makedepends=('ldc')
optdepends=('pango' 'atk' 'gdk-pixbuf2' 'gtksourceview3' 'gstreamer' 'vte3' 'libpeas')
source=("https://github.com/gtkd-developers/GtkD/archive/v${pkgver}.tar.gz")
sha512sums=('a7fa8b0dc642e7085275a4b63b40cee4f83197399b0cd78c21bdef4973cbcf89412a79970c13b59c9f048becc3f2c6deb25746b5a7106b46641ff27239fe93f0')

build() {
    cd ${srcdir}/GtkD-${pkgver}

    make \
    DC='ldc' \
    LDFLAGS="-L=\"$LDFLAGS\"" \
    libdir='lib/' \
    shared-{gtkd,gtkdgl,sv,gstreamer,vte,peas}
}

package() {
    cd ${srcdir}/GtkD-${pkgver}

    make \
    prefix='/usr' \
    libdir='lib/' \
    DESTDIR="${pkgdir}/" \
    install-{shared,headers}-{gtkd,gtkdgl,gtkdsv,gstreamer,vte,peas}
}
