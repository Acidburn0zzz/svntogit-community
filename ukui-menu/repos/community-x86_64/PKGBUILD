# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-menu
pkgver=2.0.2
pkgrel=2
pkgdesc="Start menu development library and advanced graphical user interface for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-menu"
groups=('ukui')
depends=('qt5-svg')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-menu/archive/v$pkgver.tar.gz"
        fix-installation.patch)
sha512sums=('9bf09b97f39119d45325fa5b296ab2e36a6304365f79189ff9fc71c9ee163cdce82242f65d079ad4effa733fc1d61f0b2d92c37a98cc6ae3240de7f7b02ee625'
            'eb22d4cb171ee9ca46f55a6802515e3ed0c8ba4160a3dc975b6ec43ccb4e7e00e7321d071013491accb8f7c3842c7aa4f0370af4c9e902d9f1a7405836309a90')

prepare() {
  cd ukui-menu-$pkgver
  patch -p1 -i ../fix-installation.patch
}

build() {
  cd ukui-menu-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-menu-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
