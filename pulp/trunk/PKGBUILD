# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker at gmail dot com>

pkgname=pulp
pkgver=11.0.2
pkgrel=1
pkgdesc="A build system for PureScript projects"
arch=('any')
url="https://github.com/bodil/pulp"
license=('LGPL')
depends=('nodejs' 'acorn' 'browserify' 'bower' 'purescript')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('aa600ac7b75a2cd64e836a2a0bf4e25c93fed528cdeccf833f601fbc231493fdf9a164112c1379644bd450910b026d2bb9d120a26e2dcde7b4fb7e9e477e69db')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn browserify; do
    rm -r $dep;
    npm link $dep;
  done
}
