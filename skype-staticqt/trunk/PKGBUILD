# $Id$
# Maintainer:
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>

pkgname=skype-staticqt
pkgver=2.2.0.35
pkgrel=1
arch=('i686')
pkgdesc='P2P software for high-quality voice communication (static Qt)'
url="http://www.skype.com/"
license=('custom')
depends=('alsa-lib' 'fontconfig' 'libxinerama' 'libxi' 'libxcursor' 'libsm' 'libxrandr' 'gcc-libs' 'libxv' 'libxss' 'xdg-utils')
provides=('skype')
conflicts=('skype')
options=('!strip')
install=${pkgname}.install
source=("http://download.skype.com/linux/skype_static-$pkgver.tar.bz2"
        'PERMISSION')
md5sums=('b11c778fea63d5b033c20c129c8f3931'
         '26e1772379d4d4df9471b6ed660a6d97')

package() {
  cd "${srcdir}"/skype_static-$pkgver
  install -d "${pkgdir}"/usr/share/skype/{avatars,lang,sounds}
  install -m 644 avatars/* "${pkgdir}"/usr/share/skype/avatars
  install -m 644 lang/* "${pkgdir}"/usr/share/skype/lang
  install -m 644 sounds/* "${pkgdir}"/usr/share/skype/sounds
  install -m 755 -D skype "${pkgdir}"/usr/bin/skype
  install -m 644 -D skype.conf "${pkgdir}"/etc/dbus-1/system.d/skype.conf
  install -m 644 -D icons/SkypeBlue_16x16.png "${pkgdir}"/usr/share/icons/hicolor/16x16/skype.png
  install -m 644 -D icons/SkypeBlue_32x32.png "${pkgdir}"/usr/share/icons/hicolor/32x32/skype.png
  install -m 644 -D icons/SkypeBlue_48x48.png "${pkgdir}"/usr/share/icons/hicolor/48x48/skype.png
  install -m 644 -D icons/SkypeBlue_48x48.png "${pkgdir}"/usr/share/pixmaps/skype.png
  install -m 644 -D skype.desktop "${pkgdir}"/usr/share/applications/skype.desktop
  install -m 644 -D LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -m 644 -D "${srcdir}"/PERMISSION "${pkgdir}"/usr/share/licenses/$pkgname/PERMISSION
}
