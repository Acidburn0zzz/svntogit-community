# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# ThomasV PGP key: gpg --recv-key 6694D8DE7BE8EE5631BED9502BD5824B7F9470E6
# http://bitcoin-otc.com/viewgpg.php?nick=ThomasV

pkgname=electrum
pkgver=2.6
pkgrel=1
pkgdesc="Lightweight Bitcoin wallet"
arch=('any')
depends=('python2-ecdsa' 'python2-pbkdf2' 'python2-requests' 'python2-pyasn1' 'python2-pyasn1-modules' 'python2-qrcode'
         'python2-protobuf' 'python2-tlslite' 'python2-dnspython' 'python2-pyqt4' 'python2-sip' 'python2-slowaes')
makedepends=('gettext' 'python2-pycurl' 'python2-setuptools' 'desktop-file-utils')
optdepends=('python2-btchip: BTChip hardware wallet support'
            'python2-matplotlib: plot transaction history in graphical mode'
            'zbar: QR code reading support')
url="https://electrum.org"
license=(MIT)
source=("https://download.electrum.org/${pkgver}/${pkgname^}-${pkgver}.tar.gz"{,.asc}
        60b595356b8c049adc296deb0ffe65b357500ab4.patch)
md5sums=('63039f6e307274829a9b8cd0662e40e9'
         'SKIP'
         '99509675fb508c1d10ded5cb8dd1acff')
sha256sums=('6adb213f042fc4cdefca6675948d3a7d4b5d7ec89178d3388626ee5e0e032d29'
            'SKIP'
            'f157421a54ffd001fb0957a557f0a3a91709424ae4b78b97f0eda9e053ac3c81')
validpgpkeys=('6694D8DE7BE8EE5631BED9502BD5824B7F9470E6')

provides=('electrum')
install=electrum.install

prepare() {
  cd ${pkgname^}-${pkgver}

  find . -type f -exec sed -i 's#/usr/bin/python$#/usr/bin/python2#g' {} +
  find . -type f -exec sed -i 's#/usr/bin/env python$#/usr/bin/env python2#g' {} +

  # Revert commit 60b595356b8c049adc296deb0ffe65b357500ab4
  patch -Rp1 -i "$srcdir/60b595356b8c049adc296deb0ffe65b357500ab4.patch"
}

build() {
  cd ${pkgname^}-${pkgver}

  pyrcc4 icons.qrc -o gui/qt/icons_rc.py
  python2 setup.py build
}

package() {
  cd ${pkgname^}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
