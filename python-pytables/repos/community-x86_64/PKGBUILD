# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-pytables
pkgname=(python2-pytables python-pytables)
pkgver=3.5.2
pkgrel=1
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
arch=(x86_64)
url="https://www.pytables.org"
license=("BSD")
makedepends=(lzo hdf5 python2-numexpr python-numexpr python-setuptools python2-setuptools cython cython2 blosc bzip2)
source=(${pkgbase}-${pkgver}.tar.gz::"https://github.com/PyTables/PyTables/archive/v${pkgver}.tar.gz")
sha256sums=('e4fc6f1194f02a8b10ff923e77364fb70710592f620d7de35f4d4e064dc70e91')

prepare() {
  cp -a PyTables-$pkgver PyTables-py2-$pkgver
}

build() {
  # "Building Python2"
  cd PyTables-py2-$pkgver
  python2 setup.py build --blosc=/usr --lzo=/usr --hdf5=/usr --bzip2=/usr

  # "Building Python3"
  cd ../PyTables-$pkgver
  python setup.py build --blosc=/usr --lzo=/usr --hdf5=/usr --bzip2=/usr
}

package_python2-pytables() {
  depends=(lzo hdf5 python2-numexpr blosc)

  cd PyTables-py2-$pkgver
  python2 setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/

  # see FS#36015 and FS#44971
  mv "$pkgdir"/usr/bin/pt2to3{,-2.7}
  mv "$pkgdir"/usr/bin/ptdump{,-2.7}
  mv "$pkgdir"/usr/bin/ptrepack{,-2.7}
  mv "$pkgdir"/usr/bin/pttree{,-2.7}

  # see FS#45975
  _site_packages=`python2 -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod a+rx "${pkgdir}${_site_packages}"/*.egg-info
  chmod a+r "${pkgdir}${_site_packages}"/*.egg-info/*
}

package_python-pytables() {
  depends=(lzo hdf5 python-numexpr blosc)

  cd PyTables-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/

  # see FS#45975
  _site_packages=`python -c "from distutils.sysconfig import get_python_lib; print(get_python_lib())"`
  chmod a+rx "${pkgdir}${_site_packages}"/*.egg-info
  chmod a+r "${pkgdir}${_site_packages}"/*.egg-info/*
}

