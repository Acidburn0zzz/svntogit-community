# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk2.9
pkgver=2.9.3.1
pkgrel=1
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base' 'libgl' 'libxxf86vm' 'libsm')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'mesa')
source=(http://downloads.sourceforge.net/wxpython/wxPython-src-${pkgver}.tar.bz2 wxGTK-2.9.3.1-collision.patch)
sha1sums=('0202f64e1e99fb69d22d7be0d38cf7dcf3d80d79'
          '69eace867fb20dba9271b57292756e144bb698ce')

build() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  patch -p1 -i ../wxGTK-2.9.3.1-collision.patch
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd "${srcdir}/wxPython-src-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/share/locale/it/LC_MESSAGES/wxmsw.mo"
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
