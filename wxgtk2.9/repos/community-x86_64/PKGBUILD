# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=wxgtk2.9
pkgver=2.9.4
pkgrel=4
pkgdesc="GTK+ implementation of wxWidgets API for GUI"
arch=('i686' 'x86_64')
url="http://wxwidgets.org"
license=('custom:wxWindows')
depends=('gtk2' 'gstreamer0.10-base')
makedepends=('gstreamer0.10-base-plugins' 'gconf' 'webkitgtk2')
optdepends=('webkitgtk2: for webview support')
source=(http://downloads.sourceforge.net/wxwindows/wxWidgets-${pkgver}.tar.bz2 wxGTK-2.9.4-collision.patch)
sha1sums=('5a34ddf19d37c741f74652ee847df9568a8b81e1'
          'a76ccca4c2fad246c9aafbcc5a9697ebf35f33a3')

prepare() {
  cd wxWidgets-${pkgver}
  patch -p1 -i ../wxGTK-2.9.4-collision.patch
}

build() {
  cd wxWidgets-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib --with-gtk=2 --with-opengl --enable-unicode \
    --enable-graphics_ctx --enable-mediactrl --enable-webview --with-regex=builtin \
    --with-libpng=sys --with-libxpm=sys --with-libjpeg=sys --with-libtiff=sys \
    --disable-precomp-headers
  make
  make -C locale allmo
}

package() {
  cd wxWidgets-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/share/locale/it/LC_MESSAGES/wxmsw.mo"
  install -D -m644 docs/licence.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
