# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=entr
pkgver=4.3
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('f65dfc3f106e25ec4ff39eaec94222d8a547eb35390c654f6a741f92541c1b86d7d303a795308fcbfced44ac8eca6ad67e70393802ac9d46153e037dc1eab902'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
