# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=entr
pkgver=4.4
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('x86_64')
url='http://eradman.com/entrproject'
license=('MIT')
validpgpkeys=(519151D83E83D40A232B4D615C418B8631BC7C26) # Eric Radman <ericshane@eradman.com>
source=("$url/code/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('355bc19cfee9a9b5a460b136d8db014a1d129b6e4c95d905c1c4557cf7d845a076d260798ae8b84221563c049a9bd6d81974f00eea681abaf2eac2f2f5e6d087'
            'SKIP')

build() {
  cd $pkgname-$pkgver

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd $pkgname-$pkgver

  make test
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
