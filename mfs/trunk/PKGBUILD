# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=mfs
pkgname=('mfs-chunkserver' 'mfs-client' 'mfs-master')
pkgver=1.6.26
pkgrel=1
pkgdesc="MooseFS, fault tolerant, network distributed file system"
license=("GPL3")
arch=('x86_64' 'i686')
options=('!libtool')
depends=('zlib' 'fuse' 'python2' 'bash' 'xfsprogs')
url="http://www.moosefs.com/"
install=mfs.install
backup=('etc/mfshdd.cfg'
        'etc/mfschunkserver.cfg')
source=("http://www.moosefs.org/tl_files/mfscode/mfs-$pkgver.tar.gz"
        "mfsmaster"
        "mfschunkserver.service"
        "mfschunkserver.cfg"
        "mfshdd.cfg"
        "mfsmetalogger"
        "mfscgiserv")
sha256sums=('f250f97c6f2a229277fdbf9fe96036873fd6c95a38f484f06e6b87a404c01885'
            '7c307f7be520644ac5d9c71e39ff7ec7e26800aa77c8a18c51a2d0087e5b2f48'
            'a5ce48c5bf38db94c7265038f35e4bc9d4912cfdc8e23fd2b28b9dcc6e567e9f'
            '7b493eb19c5577ff18eb3e35dd88d6ad9d97fcd126365cc532aef44b7a84b676'
            'ef8dca435b1d00bb4738f5d609de12d8642c5032f7c4e78ebbec47983f2bc70d'
            'd9c072cbb9d9645e5131d08b08628ccc2a2bef0a4e9c6fd8c3c33e50efce39f3'
            'a2e1951888887e928e68a3a010047fa3528182e93c086e4addb5abc14ea72c3b')

build() {
  cd "$srcdir"

  # Stop chown from failing if mfs:mfs does not exist yet when building
  find "$srcdir" -name 'Makefile*' -type f -print0 | xargs -0 sed -i 's/chown/echo/g'

  msg2 "Building client..."
  cp -r $pkgbase-$pkgver $pkgbase-client
  cd $pkgbase-client
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc/mfs \
      --localstatedir=/var/lib \
      --disable-mfschunkserver \
      --disable-mfsmaster \
      --disable-mfscgi \
      --disable-mfscgiserv \
      --with-default-user=mfs \
      --with-default-group=mfs
  make
  
  cd ..
  
  msg2 "Building chunkserver..."
  cp -r $pkgbase-$pkgver $pkgbase-chunk
  cd $pkgbase-chunk
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc/mfs \
      --localstatedir=/var/lib \
      --disable-mfsmaster \
      --disable-mfsmount \
      --disable-mfscgi \
      --disable-mfscgiserv \
      --with-default-user=mfs \
      --with-default-group=mfs
  make

  cd ..
  
  msg2 "Building master..."
  cp -r $pkgbase-$pkgver $pkgbase-master
  cd $pkgbase-master
  ./configure \
      --prefix=/usr \
      --sysconfdir=/etc/mfs \
      --localstatedir=/var/lib \
      --disable-mfschunkserver \
      --disable-mfsmount \
      --with-default-user=mfs \
      --with-default-group=mfs
  make
}

package_mfs-client() {
  depends=('zlib' 'fuse' 'bash')
  cd "$srcdir/$pkgbase-client"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/man/man7"
  rmdir "$pkgdir/usr/share/man/man5"
}

package_mfs-chunkserver() {
  depends=('zlib' 'bash')
  cd "$srcdir/$pkgbase-chunk"
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/man/man7"
  rmdir "$pkgdir/usr/share/man/man1"
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
  install -Dm644 "$srcdir/mfschunkserver.service" \
    "$pkgdir/usr/lib/systemd/system/mfschunkserver.service"
  install -Dm644 "$srcdir/mfschunkserver.cfg" \
    "$pkgdir/etc/mfs/mfschunkserver.cfg"
  install -Dm644 "$srcdir/mfshdd.cfg" \
    "$pkgdir/etc/mfs/mfshdd.cfg"
}

package_mfs-master() {
  depends=('zlib' 'python2')
  cd "$srcdir/$pkgbase-master"
  make DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgbase/README"
  install -Dm644 INSTALL "$pkgdir/usr/share/doc/$pkgbase/INSTALL"
  install -Dm644 NEWS "$pkgdir/usr/share/doc/$pkgbase/NEWS"
  install -Dm644 UPGRADE "$pkgdir/usr/share/doc/$pkgbase/UPGRADE"
  install -Dm755 "$srcdir/mfsmaster" "$pkgdir/etc/rc.d/mfsmaster"
  install -Dm755 "$srcdir/mfsmetalogger" "$pkgdir/etc/rc.d/mfsmetalogger"
  install -Dm755 "$srcdir/mfscgiserv" "$pkgdir/etc/rc.d/mfscgiserv"
  rmdir "$pkgdir/usr/share/man/man1"
  mv "$pkgdir/usr/sbin" "$pkgdir/usr/bin"
  sed -i '0,/on/s//on2/' "$pkgdir/usr/bin/mfscgiserv"
}

# vim:set ts=2 sw=2 et:
