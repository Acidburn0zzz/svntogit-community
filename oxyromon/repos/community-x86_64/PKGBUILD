# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=oxyromon
pkgver=0.6.0
pkgrel=1
pkgdesc='Rusty ROM OrgaNizer'
arch=(x86_64)
url=https://github.com/alucryd/oxyromon
license=(GPL3)
depends=(
  gcc-libs
  glibc
)
makedepends=(
  git
  mame
  maxcso
  p7zip
  rust
)
optdepends=(
  'mame: CHD support'
  'maxcso: CSO support'
  'p7zip: 7Z and ZIP support'
)
_tag=bde32c31c83e12b18baefaf4182664b431dae16d
source=(
  git+https://github.com/alucryd/oxyromon.git?signed#tag=${_tag}
  oxyromon-chdman-path.patch
)
validpgpkeys=('9437DD3815A7A9169E3D3946AFF5D95098BC6FF5') # Maxime Gauduin <alucryd@archlinux.org>
b2sums=('SKIP'
        '8cb8a37e11c521436c2e015367458219205ded84c404928cf74a5f75843b7903b420af6bc820ff809fd1fd8bd605185918393f140e1f66dd85c9a538698a9407')

prepare() {
  cargo fetch \
    --locked \
    --manifest-path oxyromon/Cargo.toml

  cd oxyromon
  patch -Np1 -i ../oxyromon-chdman-path.patch
}

pkgver() {
  cd oxyromon

  git describe --tags
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path oxyromon/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path oxyromon \
    --root "${pkgdir}"/usr
}

# vim: ts=2 sw=2 et:
