# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=vmaf
pkgver=1.5.1
pkgrel=1
pkgdesc='Perceptual video quality assessment algorithm based on multi-method fusion'
arch=('x86_64')
url='https://github.com/Netflix/vmaf/'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/Netflix/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('9267b40223ac8d67fb6b99726ce7ed3925b9843f18ad5aa8ffbe2fe873e45cbe')

prepare() {
    mkdir -p "${pkgname}-${pkgver}/libvmaf/build"
}

build() {
    arch-meson "${pkgname}-${pkgver}/libvmaf/build" "${pkgname}-${pkgver}/libvmaf"
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build"
}

check() {
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" test
}

package() {
    DESTDIR="$pkgdir" ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" install
    
    local _bin
    install -D -m755 "${pkgname}-${pkgver}/libvmaf/build/tools/vmaf" -t "${pkgdir}/usr/bin"
    for _bin in moment ms_ssim psnr ssim
    do
        install -D -m755 "${pkgname}-${pkgver}/libvmaf/build/tools/${_bin}" "${pkgdir}/usr/bin/vmaf-${_bin}"
    done
    
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
