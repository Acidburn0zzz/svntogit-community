# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
pkgname=usd
pkgver=21.05
pkgrel=3
pkgdesc="3D VFX pipeline interchange file format"
arch=('x86_64')
url="https://graphics.pixar.com/usd/docs/index.html"
license=('Apache')
depends=('boost-libs' 'glew' 'openexr' 'opensubdiv' 'python' 'pyside2' 'python-opengl')
makedepends=('boost' 'cmake' 'intel-tbb' 'ninja' 'qt5-base' 'gcc10')
provides=("usd=${pkgver}")
source=("$pkgname-$pkgver.tar.gz::https://github.com/PixarAnimationStudios/USD/archive/v${pkgver}.tar.gz"
        'pyside2-uic')
sha256sums=('165d0e1e45385076121268eba0da2a0ba4a7e5c2b8eab9ab26fb2fcd81a1a478'
            'bf2d902eddd24d4c3498f05a89f0d867df8a6cbd921214131c14a28152e4ffbe')

build() {
  export CC=gcc-10
  export CXX=g++-10
  cd USD-${pkgver}
  cmake \
    -B build \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPXR_USE_PYTHON_3=ON \
    -DPXR_BUILD_IMAGING=ON \
    -DPXR_BUILD_TESTS=OFF \
    -DPXR_BUILD_MONOLITHIC=ON \
    -DPYSIDE_BIN_DIR="$srcdir"
  ninja -C build
}

package() {
  cd USD-${pkgver}
  DESTDIR="$pkgdir" ninja -C build install

  mv "$pkgdir"/usr/lib/python "$pkgdir"/usr/lib/python3.9
}

# vim:set ts=2 sw=2 et:
