# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libaccounts-glib
pkgver=1.3
pkgrel=1
pkgdesc="Glib-based client library for accessing the online accounts database"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('dbus-glib' 'libxml2' 'sqlite3')
makedepends=('python2-gobject')
options=('!libtool')
source=(http://accounts-sso.googlecode.com/files/$pkgname-$pkgver.tar.gz
        fix-docdir.patch
        make-tests-optional.patch)
sha1sums=('a2bd07082e9e9235103051f6574b154182745aee'
          '94291a58dbbd36f524538adbf6d0c5a55024a34c'
          '35732dc134c32af6ad1684d6388bad8ffe3f4dcc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use the standard docdir location
  # http://code.google.com/p/accounts-sso/issues/detail?id=123
  patch -Np1 -i "$srcdir/fix-docdir.patch"

  # Build tests optionally
  # http://code.google.com/p/accounts-sso/issues/detail?id=124
  patch -Np1 -i "$srcdir/make-tests-optional.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Accounts.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Accounts.py"

  # We don't have Backup Framework on Arch Linux
  rm -r "$pkgdir/usr/share/backup-framework"
}
