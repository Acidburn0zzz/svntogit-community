# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-validators
pkgver=0.17.0
pkgrel=1
pkgdesc="Python Data Validation for Humans"
url="https://github.com/kvesteri/validators"
license=('MIT')
arch=('any')
depends=('python-decorator' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kvesteri/validators/archive/$pkgver.tar.gz")
sha512sums=('37bc9b3ff6e504b40839a3086866c05d1f5534434c3ca698f4e60e7363237c7f591c98980eda238bed02536895d7c4e36e55befb123d39670bebeb312add0671')

build() {
  cd validators-$pkgver
  python setup.py build
}

check() {
  cd validators-$pkgver
  python -m pytest
}

package() {
  cd validators-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
