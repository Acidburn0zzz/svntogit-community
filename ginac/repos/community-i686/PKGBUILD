# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=ginac
pkgver=1.7.0
pkgrel=1
pkgdesc="C++ library for symbolic calculations"
arch=('i686' 'x86_64')
url='http://www.ginac.de/'
license=('GPL')
depends=('cln')
makedepends=('transfig' 'doxygen' 'texlive-bin' 'python')
source=(http://www.ginac.de/$pkgname-$pkgver.tar.bz2)
sha256sums=('ac9aa202b8b9683175a40b7a4d55f28616225291a4e2cbfe71378e0d02c944bd')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --libexecdir=/usr/lib/ginac --disable-static
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
