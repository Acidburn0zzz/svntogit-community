# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: stefan-husmann@t-online.de
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
pkgname=librep
pkgver=0.92.1b
pkgrel=3
pkgdesc="Lisp system for Sawfish"
license=('GPL2')
arch=('x86_64' 'i686')
url="http://sawfish.wikia.com/wiki/Librep"
depends=('gmp' 'gdbm' 'libffi')
install=$pkgname.install
options=('!emptydirs' '!libtool')
source=("http://download.tuxfamily.org/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('aedca2716252a1ff2256aca321cce35646b795840b65a3c481708c04a6fd7e3e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  ./configure --disable-static \
    --prefix=/usr \
    --libexecdir="/usr/lib/$pkgname" \
    --with-readline
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
