# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.3.3
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('dtkwidget' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good' 'mpv')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('4c96b191fd65660361db18e5faede60b590395256daf2cb7214500b5b0defd76b6acdf2e71c19d0da6c0a9d37a1d4108049cfc78ab42a22666cbe54ddca96bd3')

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
