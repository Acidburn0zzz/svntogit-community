# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.1.5.20170607
_commit=d6ed53941fa0734eb3176f4016b1a5c1a8cc8e9f
pkgrel=2
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual'
         'deepin-qt-dbus-factory' 'treefrog-framework')
optdepends=('deepin-terminal: for "Open in terminal"'
            'file-roller: for "Compress" and "Extract"')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$_commit.tar.gz"
        qt5.9.patch)
sha512sums=('e59ef1b9ed730a0b2bb6ef3a7d0834db39d27a39d2224cdee6f0121b7014c485b0e8d651b59c0e0eeb51f4c6a49950ad658f84daeba618658c834f57fa5151ab'
            '1cc25323ffa057654cc64720dfa9bcd4a0532a49c2ab29bc2d5c80c1dc063fd17cd2ea6b7de5cdd55599baf1bcff3e56fb08be19240d217ff949a6e6e8e2cae4')

prepare() {
  mv dde-file-manager-* dde-file-manager-$pkgver
  cd dde-file-manager-$pkgver
  # patch -p1 -i ../qt5.9.patch
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
