# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.1.5
pkgrel=3
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual')
optdepends=('deepin-terminal: for "Open in terminal"'
            'file-roller: for "Compress" and "Extract"')
makedepends=('qt5-tools' 'deepin-dock')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
        qt5.9.patch)
sha512sums=('03f724e3e1392022b1df1f9f6af24071be9de53df447d02cc81f1c75e271807c31314140954297806c9a71bcf301d60f746eac9caa3af5145615f76a41e54b6a'
            '1cc25323ffa057654cc64720dfa9bcd4a0532a49c2ab29bc2d5c80c1dc063fd17cd2ea6b7de5cdd55599baf1bcff3e56fb08be19240d217ff949a6e6e8e2cae4')

prepare() {
  cd dde-file-manager-$pkgver
  patch -p1 -i ../qt5.9.patch
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
