# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.6.3
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs'
         'polkit-qt5' 'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer'
         'file-roller' 'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal'
         'gst-plugins-good' 'mpv' 'deepin-anything' 'deepin-movie' 'jemalloc' 'kcodecs')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
optdepends=('deepin-manual: view the help manual')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-file-manager/archive/$pkgver.tar.gz"
        fix-include.diff)
sha512sums=('4eda28ec46ab9942313b98669a4b84bad79676da1f38199348c76ea8b58ee851e4556748115818a4d9c8ac8ac6c2d3458aea841f237d23b351eb8522ac734b9e'
            '42268ae64dd803884aa5573283ac500ebbc4628fd2ac1b10cf56286b64321fc05fce7a67f1e8883115ac2e7887143fe89e17e9a83e11ee3f06efad94b076659d')

prepare() {
  patch -d dde-file-manager-$pkgver -p1 -i ../fix-include.diff
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
