# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.7.7
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs'
         'polkit-qt5' 'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'xdg-user-dirs'
         'file-roller' 'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal'
         'gst-plugins-good' 'mpv' 'deepin-anything' 'deepin-movie' 'jemalloc' 'kcodecs'
         'deepin-screensaver')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
optdepends=('deepin-manual: view the help manual')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('9e281edf21a7c2babe3f0562a54d4be7379dfb49559eb4f3129743c5823b3fcb7e0740425f5a866b5787867285f27eb6b6f4bc38788e802abcbbb6905989b987')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' dde-file-manager-daemon/dde-file-manager-daemon.pro
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
