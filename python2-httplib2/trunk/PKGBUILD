# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Giorgio Lando <patroclo7@gmail.com>
# Contributor: Nicolas Quienot <niQo@aur>
# Contributor: Jesse Young <jesse.young@gmail.com>

pkgbase=python2-httplib2
pkgname=(python-httplib2 python2-httplib2)
_pkgbasename=httplib2
pkgver=0.8
pkgrel=1
pkgdesc='Comprehensive HTTP client library, supporting many features'
url='http://code.google.com/p/httplib2/'
license=('MIT')
arch=('any')
makedepends=('python' 'python2')
provides=('httplib2')
replaces=('httplib2')
source=("http://httplib2.googlecode.com/files/$_pkgbasename-$pkgver.tar.gz"
        'COPYING')
sha256sums=('af689bc3cb10a95721eb0d0cf806202a699e7799dacd3497f63b48b2ba429c3f'
            '76b9c89dc4c620837d81f3880903142932f258814a0e2634355bc2ceac21f406')

prepare() {
  cd "$srcdir"

  cp -a "$_pkgbasename-$pkgver" "${pkgname[0]}-$pkgver"
  cp -a "$_pkgbasename-$pkgver" "${pkgname[1]}-$pkgver"
}

package_python-httplib2() {
  depends=('python')

  cd "$srcdir/$pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-httplib2() {
  depends=('python2')

  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 "$srcdir/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
