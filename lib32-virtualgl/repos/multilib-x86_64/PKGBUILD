# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=lib32-virtualgl
pkgver=2.6.2
pkgrel=1
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
makedepends=('lib32-libxv' 'lib32-glu' 'virtualgl' 'lib32-libjpeg-turbo' 'cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-mesa-libgl' 'lib32-libxtst')
depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-turbojpeg')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig})
sha512sums=('826707451a7c85de84f3e4b2cc629ee41ee48132c62d55dd2af87d7f1a45873a8d0a281936bae37ce838491948c462e9cebb08ce8b20fcba2122c94d8bc2f5e5'
            'SKIP')
validpgpkeys=('ECF01671D05E2A105FF84DC46BBEFA1972FEB9CE') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  mkdir "$srcdir/VirtualGL-$pkgver"/build
}

build() {
  cd "$srcdir/VirtualGL-$pkgver"/build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DX11_X11_LIB=/usr/lib32/libX11.so \
      -DX11_Xext_LIB=/usr/lib32/libXext.so \
      -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so
  make
}

package() {
  cd "$srcdir/VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres32
  rm -rf bin
  mkdir -p bin
  mv glxspheres32 bin/

  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/include"
}
