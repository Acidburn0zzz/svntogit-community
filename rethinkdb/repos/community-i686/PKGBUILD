# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sigmund Lahn <sigmund@lahn.no>

pkgname=rethinkdb
pkgver=2.3.6
_tag=rethinkdb-$pkgver
pkgrel=1
pkgdesc='Distributed powerful and scalable NoSQL database'
arch=(i686 x86_64)
url='http://www.rethinkdb.com/'
license=(AGPL)
depends=(protobuf ncurses curl openssl-1.0)
makedepends=(boost python2 wget)
backup=(etc/rethinkdb/instances.d/default.conf)
install=rethinkdb.install
options=(!emptydirs)
source=(
  https://download.rethinkdb.com/dist/$_tag.tgz
  rethinkdb-tmpfile.conf
  rethinkdb.service
  rethinkdb.sysusers
  mksnapshot_crash_fix.patch::https://github.com/rethinkdb/rethinkdb/commit/871bd3705a1f29c4ab07a096d562a4b06231a97c.patch
)
sha256sums=('c42159666910ad01be295a57caf8839ec3a89227d8919be5418e3aa1f0a3dc28'
            '656d3a42e75d087e723f71aa320fdd91cbbb82071ef72eb11fd3e4a619b429a4'
            'e56bffa2b9ebc3a00ef566ab2be0719a633c89d961a2461dfa2d9ffdb258c1a2'
            '0660e0c7c093b440718486e1d0138322cf393da43e1fc92fb712b8282db76b5c'
            'a14a52cb6a0b1873b1c48847a6d7bdfd82a377a114ba40c8e9035b4e7b7cca16')

prepare() {
  cd $_tag

  # fix for https://github.com/rethinkdb/rethinkdb/issues/5757
  patch -p1 < ../mksnapshot_crash_fix.patch

  sed \
    -e 's|#!/usr/bin/python|#!/usr/bin/python2|' \
    -e 's|#!/usr/bin/env python|#!/usr/bin/env python2|' \
    -i scripts/*.py external/v8_*/tools/*.py
  sed -e 's|exec python|exec python2|g' -i external/v8_*/build/gyp/gyp
  sed -e 's|\bpython\b|python2|g' -i external/v8_*/{Makefile,*/*.gyp,*/*.gypi,*/*/*.gyp}

}

build() {
  cd $_tag
  export PYTHON=/usr/bin/python2
  CXX=g++-5 ./configure --fetch v8 --fetch jemalloc --dynamic all --enable-precompiled-web --prefix=/usr --sysconfdir=/etc --localstatedir=/var CXXFLAGS="$CXXFLAGS -I/usr/include/openssl-1.0" LDFLAGS="$LDFLAGS -L/usr/lib/openssl-1.0"
  make ALLOW_WARNINGS=1
}

check() {
  cd $_tag

  # these tests are flaky and extremely slow in Arch chroot
  # make build/release/rethinkdb-unittest
  # ./build/release/rethinkdb-unittest --gtest_filter=-RDBBtree.*:RDBInterrupt.*
  # some tests might be flaky on btrfs filesystem
}

package() {
  cd $_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/rethinkdb.sysusers "$pkgdir"/usr/lib/sysusers.d/rethinkdb.conf
  install -Dm644 "$srcdir"/rethinkdb-tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/rethinkdb.conf
  install -Dm644 "$srcdir"/rethinkdb.service "$pkgdir"/usr/lib/systemd/system/rethinkdb@.service

  # create 'default' database instance
  mv "$pkgdir"/etc/rethinkdb/default.conf.sample "$pkgdir"/etc/rethinkdb/instances.d/default.conf
  sed -e 's|# directory=/var/lib/rethinkdb|directory=/var/lib/rethinkdb|' \
      -i "$pkgdir"/etc/rethinkdb/instances.d/default.conf

  # Arch uses systemd, no need for init.d scripts
  rm -r "$pkgdir"/etc/init.d
}
