# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=2.0.2
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
sha512sums=('ab14bdf664e6df08c8436eb24c4465d3080aacd4e6cb47a0113b0877f869cd91d4184ad9122a428b323e91316b686eeb167c4e932773cac1e7b2adbc9309ca36')

prepare() {
   cp -a cryptography_vectors-$pkgver{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-$pkgver
   python3 setup.py install --root="$pkgdir" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-$pkgver-python2
   python2 setup.py install --root="$pkgdir" --optimize=1
}
