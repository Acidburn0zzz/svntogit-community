# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libvips
pkgver=8.11.1
pkgrel=2
pkgdesc="A fast image processing library with low memory needs"
arch=('x86_64')
license=('LGPL')
url="https://libvips.github.io/libvips/"
depends=('cfitsio' 'fftw' 'imagemagick' 'libexif' 'libgsf' 'libheif' 'libimagequant' 'librsvg'
         'libwebp' 'libxml2' 'openexr' 'orc' 'pango' 'poppler-glib')
makedepends=('gobject-introspection')
source=("https://github.com/libvips/libvips/releases/download/v$pkgver/vips-$pkgver.tar.gz")
sha512sums=('3915cc5f4f966a58aef0bf3b1f3768143491b127880125dea578431dc06b06b1490462799add1fb378c605180753afa539020da4c318e5774c4ccb4c893f94d3')

build() {
  cd vips-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd vips-$pkgver
  make DESTDIR="$pkgdir" install
}
