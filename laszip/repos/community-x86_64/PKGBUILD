# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Maciej Sieczka <msieczka at sieczka dot org>
# Contributor: Michael <mbostwick89@gmail.com>

pkgname=laszip
pkgver=3.4.0
pkgrel=1
pkgdesc='ASPRS LAS lossless compression library'
arch=('x86_64')
url='https://laszip.org'
license=('LGPL')
makedepends=('cmake')
source=("https://github.com/LASzip/LASzip/releases/download/${pkgver}/laszip-src-${pkgver}.tar.gz")
sha512sums=('f7eff01ee31a15a28d21ad24c1d1203d1dba2d7b59cf26468736e867d64a1621a4177d64826a17a68b99225448c3fb179f0f3cad4442cfe393991d09a50ecc88')

build() {
  cd "${srcdir}/${pkgname}-src-${pkgver}"

  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd "${srcdir}/${pkgname}-src-${pkgver}/build"
  make DESTDIR="$pkgdir" install
}
