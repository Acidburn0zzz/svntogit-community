# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libdom
pkgver=0.4.0
pkgrel=1
pkgdesc='Implementation of the W3C DOM'
arch=(x86_64)
url='https://www.netsurf-browser.org/projects/libdom/'
license=(MIT)
depends=(expat 'libhubbub>=0.3.6' 'libwapcaplet>=0.4.2')
makedepends=(netsurf-buildsystem)
source=("https://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('9eb9cd308136d5078fd9a342f391ef65071e8660a0ea29d50cb41d37a8c6b3c7')

prepare() {
  sed -i 's:_BSD_SOURCE:_DEFAULT_SOURCE:' "$pkgname-$pkgver/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -shared -fPIC -w"
  export LDFLAGS="$LDFLAGS -shared -z,now"
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  make -C "$pkgname-$pkgver" \
    DESTDIR="$pkgdir" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared" \
    install

  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libdom.git/tree/Makefile
# vim: ts=2 sw=2 et:
