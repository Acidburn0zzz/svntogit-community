# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.6.7
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
depends=('ncurses')
makedepends=('rust')
url=https://github.com/lotabout/skim
license=('MIT')
source=("$url/archive/v$pkgver/skim-v$pkgver.tar.gz")
sha512sums=('05aee9fc9a59cd28c7ec713a6cd70fd03d349441572b33611975cc685964aad042ad1d32c500e0d30abd56fc7fcb3b2a06135122460b2b1c5992ea3608e3fb11')

prepare() {
  cd skim-$pkgver
  sed -i '369s/0.6.6/0.6.7/' Cargo.lock
}

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}
