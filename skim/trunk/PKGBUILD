# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.9.2
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
url=https://github.com/lotabout/skim
license=('MIT')
depends=('ncurses')
makedepends=('rust')
optdepends=('bash: for resp. completions and key bindings, and for sk-tmux script'
            'fish: for resp. key bindings'
            'tmux: for sk-tmux script'
            'vim-plugin-runtime: for N/Vim plugin'
            'zsh: for resp. completions and key bindings')
source=("skim-$pkgver.tar.gz::https://static.crates.io/crates/skim/skim-$pkgver.crate")
b2sums=('6761a9df46ab960d37a0032b7c638dbd0d4a2ae818124f37c54cdf86765cdc2a0918c8ff1c9877cb7fbf55b1ea299538be8633182d4df2513f183a9d68070329')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}

# vim:set ts=2 sw=2 et:
