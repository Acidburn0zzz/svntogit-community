# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.6.6
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
depends=('ncurses')
makedepends=('rust')
url=https://github.com/lotabout/skim
license=('MIT')
source=("$url/archive/v$pkgver/skim-v$pkgver.tar.gz")
sha512sums=('df4c8c9a3641321786e014f63d1f1bc4b15762d666792d3f8b86e73704c15643ca01e3f6242243eb903454cc7c8e983255583b1cddc39cfdd5e779c00f1a546e')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}
