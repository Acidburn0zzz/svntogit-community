# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=1.9.14
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('ffmpeg' 'file' 'fluidsynth' 'gtk2' 'gtk3' 'linuxsampler'
'python-pyqt5' 'python-pyliblo' 'python-rdflib' 'qt5-svg')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('675fdd0d624c3a95f18abbbc54a45c288c96bacbfebeebf2a0653c558996e6b8f2d8a5961769e6e0db3999e60eaac6e180783114793528fe92d9940a7086b8ee')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
