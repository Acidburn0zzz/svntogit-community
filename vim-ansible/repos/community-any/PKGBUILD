# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-vim
pkgname=vim-ansible
pkgver=2.1
pkgrel=2
pkgdesc="A vim plugin for syntax highlighting Ansible's common filetypes "
arch=('any')
url="https://github.com/pearofducks/ansible-vim"
license=('MIT')
groups=('vim-plugins')
depends=('python-jinja' 'vim')
makedepends=('ansible' 'python')
optdepends=('vim-ultisnips: for ansible snippets')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pearofducks/${_name}/archive/${pkgver}.tar.gz"
        "${pkgname}-2.1-fix_ultisnips_generator.patch::https://github.com/pearofducks/ansible-vim/pull/98/commits/82fdc7f56363663a9452cf729398aac04f1f919c.patch")
sha512sums=('630276ee0587bddea1ece4639e7bb744533dd212d3756f3071a188597e5091d4946536a9c6a4af013b6a3c0391a32d24809a0d10ab3d0b4e2fbaa7f4d89ac232'
            '4e979c12514797af38621031d5a43c1ebbb2fb9e9c18684d75c45d85ca998d61f288c37f246c1b6abca66202c149c2c303ac15a19b110a1ea4ec13068b9c8f5b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # replace vendored jinja2.vim with python-jinja's jinja.vim
  # https://github.com/pearofducks/ansible-vim/issues/95
  sed -e 's/jinja2/jinja/g' \
      -i ftdetect/ansible.vim syntax/ansible.vim
  # fixing ultisnips generator:
  # https://github.com/pearofducks/ansible-vim/issue/96
  # https://github.com/pearofducks/ansible-vim/issue/97
  patch -Np1 -i "../${pkgname}-2.1-fix_ultisnips_generator.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python UltiSnips/generate.py --style 'dictionary' --sort
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ftdetect/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect/"
  install -vDm 644 ftplugin/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftplugin/"
  install -vDm 644 indent/*.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent/"
  install -vDm 644 syntax/ansible*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  install -vDm 644 ansible.snippets \
    -t "${pkgdir}/usr/share/vim/vimfiles/snippets/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
