# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-vim
pkgname=vim-ansible
pkgver=2.2
pkgrel=1
pkgdesc="A vim plugin for syntax highlighting Ansible's common filetypes "
arch=('any')
url="https://github.com/pearofducks/ansible-vim"
license=('MIT')
groups=('vim-plugins')
depends=('python-jinja' 'vim')
makedepends=('ansible' 'python')
optdepends=('vim-ultisnips: for ansible snippets')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pearofducks/${_name}/archive/${pkgver}.tar.gz"
        "generate.py")
sha512sums=('73c9147c713b3eb2965f015c76c7c0495bf10518159ea69c16ee65cade2b670b59581b5a335f8e68bc91ff95e9238dd73228963aaa7a8f7d400904a973259f9c'
            'e24f8c4cebb0a5f1f9e9bc0b005af1a957d1a986be8746039ca8f73d8b7c4c93ae352822e709c96f71b7726d1a6ca91be14f5553ef0153db9ad23a01b7a285a8')
b2sums=('98b673265ee4274c285d49ea11d17d4055bbac15d8a0726123c1b75f473e55dc112a5a0bac5a08725a2fc2a6811e33bd8d2cfb2bcea9cd8d1088ef7eba87b6dd'
        '0938ca439158af536ca04544c2015214b2d33d73dc36c93c0e048704bf71d6b0abc11f91f2c487a828bffe12b9c293587a027ee8883b95a2e411422f33ca4f74')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # replace vendored jinja2.vim with python-jinja's jinja.vim
  # https://github.com/pearofducks/ansible-vim/issues/95
  sed -e 's/jinja2/jinja/g' \
      -i ftdetect/ansible.vim syntax/ansible.vim
}

build() {
  cd "$pkgname-$pkgver"
  # generating a reproducible UltiSnips snippet file:
  # https://github.com/pearofducks/ansible-vim/pull/105
  python ../generate.py --style=dictionary
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ftdetect/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect/"
  install -vDm 644 ftplugin/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftplugin/"
  install -vDm 644 indent/*.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent/"
  install -vDm 644 syntax/ansible*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  install -vDm 644 ansible.snippets \
    -t "${pkgdir}/usr/share/vim/vimfiles/UltiSnips/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
