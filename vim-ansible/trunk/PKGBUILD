# Maintainer: David Runge <dvzrv@archlinux.org>

_name=ansible-vim
pkgname=vim-ansible
pkgver=3.1
pkgrel=1
pkgdesc="A vim plugin for syntax highlighting Ansible's common filetypes "
arch=('any')
url="https://github.com/pearofducks/ansible-vim"
license=('BSD' 'MIT')
groups=('vim-plugins')
depends=('python-jinja' 'vim')
makedepends=('ansible' 'python')
optdepends=('vim-ultisnips: for ansible snippets')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pearofducks/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('a0c72474a07a909c69129f7ddf0b5c0cdfd9fc0ab4283790adb42f4d2edb4a5160eda5c94f150de706b81f6155bb3bfcb5b2d8a64b4f0c7addadad9c6d673792')
b2sums=('66bc35226bbb052326b8014ccf76fa63b113b8807304e5eb8bbb58937f2f06fba2c81b53c8a95e1fc787ad57d329d65ba9e8d8fea82b82bb732b7a1d44c06c90')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  # generating a reproducible UltiSnips snippet file:
  # https://github.com/pearofducks/ansible-vim/pull/105
  python UltiSnips/generate.py --style=dictionary
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ftdetect/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftdetect/"
  install -vDm 644 ftplugin/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/ftplugin/"
  install -vDm 644 indent/*.vim -t "${pkgdir}/usr/share/vim/vimfiles/indent/"
  install -vDm 644 syntax/*.vim \
    -t "${pkgdir}/usr/share/vim/vimfiles/syntax/"
  install -vDm 644 ansible.snippets \
    -t "${pkgdir}/usr/share/vim/vimfiles/UltiSnips/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 syntax/*.vim_LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
