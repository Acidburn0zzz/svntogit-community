# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

_pkgbasename=talloc
pkgname=lib32-$_pkgbasename
pkgver=2.0.5
pkgrel=1
pkgdesc="A hierarchical pool based memory allocator with destructors (32-bit)"
arch=(x86_64)
license=('GPL3')
url="http://talloc.samba.org/"
source=(http://samba.org/ftp/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.gz)
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib python2)
md5sums=('6e3fdfbc43dde8ccba27b6af894b8fb2')

build() {
  export CC="gcc -m32"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

   sed -i -e 's#python#python2#g' buildtools/bin/waf

  ./configure --prefix=/usr \
  --disable-python \
  --sysconfdir=/etc/samba \
  --localstatedir=/var \
  --enable-talloc-compat1 \
  --libdir=/usr/lib32

  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=$pkgdir/ install

  rm -rf $pkgdir/usr/include
}

