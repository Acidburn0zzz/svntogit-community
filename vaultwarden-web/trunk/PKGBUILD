# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2.23.0c
pkgver=2.23.0.c
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with vaultwarden'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-fermium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        '2721a96db72714c7db93b7d468e8a7aad27db07fe5e3ab9d6d9656ae261388f6ee304ae1b38531a10f2c4d9a4cb1e4c174eef4e0c6bd0500de950fd43aa4f425')

pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-web
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch

  # ref: https://github.com/dani-garcia/bw_web_builds/blob/v2.23.0c/Dockerfile#L29
  sed -i 's/"gulp-google-webfonts": "^4.0.0"/"gulp-google-webfonts": "^4.1.0"/' package.json
  npm ci --legacy-peer-deps
}

build() {
  cd bitwarden-web
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
