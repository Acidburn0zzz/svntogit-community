# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.9.0
pkgrel=1
pkgdesc='Golang bindings for dde-daemon'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill'
         'poppler-glib' 'xcur2png' 'blur-effect')
makedepends=('git' 'deepin-gettext-tools' 'deepin-dbus-factory' 'deepin-gir-generator'
             'deepin-go-lib' 'deepin-go-dbus-factory' 'bzr' 'go-pie')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-api/archive/$pkgver.tar.gz"
        deepin-api.sysusers)
sha512sums=('21d87928b353d562ea41a6815ba4ac7ea0fed9f4b1c6e1ef0691e2091310cdad4410a07c9d5504bcb6a246048f1ecf9f1eabb9aedea6d62bf62bee129d794ba8'
            '548b4c8d06a10f0103d84fe6b68532ca71aae53347a6ad85dad1d274844146c9861d1bf6ce8e5518ea1b043aa082d705c534f0a4c0feb16a031537cee6befb08')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p build/src/pkg.deepin.io/dde/api
  cp -a dde-api-$pkgver/* build/src/pkg.deepin.io/dde/api/

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  cd "$srcdir"/dde-api-$pkgver
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2 github.com/nfnt/resize github.com/cryptix/wav \
         golang.org/x/net/context github.com/fogleman/gg
}

build(){
  cd dde-api-$pkgver
  make
}

package() {
  cd dde-api-$pkgver
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install

  install -Dm644 ../deepin-api.sysusers "$pkgdir"/usr/lib/sysusers.d/deepin-api.conf
}
