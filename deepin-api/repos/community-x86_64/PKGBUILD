# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.1.1
pkgrel=1
pkgdesc='Go-lang bindings for dde-daemon'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill' 'poppler-glib' 'xcur2png' 'blur-effect') # 'gcc-libs>=5.3.0-3')
makedepends=('git' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib' 'bzr' 'go') # 'gcc-go')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-api.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p build/src/pkg.deepin.io/dde/api
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  cd "$srcdir/dde-api"
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2
}

build(){
  cd "$srcdir/dde-api"
  make # USE_GCCGO=1
}

package() {
  cd dde-api
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install
}
