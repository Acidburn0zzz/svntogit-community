# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>

pkgname=dub
pkgver=1.12.1
pkgrel=1
# Build date format is "mmm dd yyyy"
_builddate="Dec 12 2018"
pkgdesc="Developer package manager for D programming language"
arch=('x86_64')
url="https://github.com/D-Programming-Language/dub"
license=('MIT')
groups=('dlang')
makedepends=('ldc')
depends=('liblphobos' 'curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/dlang/dub/archive/v$pkgver.tar.gz")
sha512sums=('581b1cb6a5d4c2ff2bb02338e8a5d3aeee10761018f470923945e6a8dc0341352bc816dbb65b6b72202cc7398c2d9d9f076494030ac920e7b8280fff83ddcb5a')

build() {
	cd "$pkgname-$pkgver"

	sed -i "s/__DATE__/\"$_builddate\"/" source/dub/commandline.d

	echo Generating version file...
	echo "module dub.version_;" > source/dub/version_.d
	echo "enum dubVersion = \"$pkgver\";" >> source/dub/version_.d

	ldmd2 -ofbin/dub -w -O -g -version=DubUseCurl -Isource -L-lcurl -L-Wl,-O1,--sort-common,--as-needed,-z,relro,-z,now -release -flto=full -linker=gold -link-defaultlib-shared=false @build-files.txt
}

package() {
	# binaries
	install -Dm755 "$pkgname-$pkgver/bin/dub" "$pkgdir/usr/bin/dub"

	# license
	install -Dm644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

	# bash completion
	install -Dm644 "$pkgname-$pkgver/scripts/bash-completion/dub.bash" "$pkgdir/usr/share/bash-completion/completions/dub"
}
