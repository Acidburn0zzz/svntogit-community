# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=python-pony
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.7.11
pkgrel=0
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
source=("$url/archive/v$pkgver.tar.gz")
sha512sums=('88d0ec9b0c66a8e791a135b4eee5c3fcf1df5abca3dc89fd1614021867a51330b1ba5a1735a665c8c710248c3d6f6d2d7275b13df9b04ad86f4f861947c9edb9')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  echo "Running tests for python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py test

  echo "Running tests for python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py test
}

package_python-pony() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pony() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

