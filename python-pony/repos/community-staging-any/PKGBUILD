# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pony
pkgname=python-$_pkgname
pkgver=0.7.13
pkgrel=3
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
depends=('python')
makedepends=('python-setuptools')
license=('Apache')
source=("$pkgname-$pkgver::$url/archive/v$pkgver.tar.gz"
        "$pkgname-py39-decompiler.patch::https://github.com/ponyorm/pony/commit/91c8f6559dae.patch"
        "$pkgname-py39-setup.patch::https://github.com/ponyorm/pony/commit/ef461888c409.patch"
        "$pkgname-py39-query-expr.patch::https://github.com/ponyorm/pony/commit/6fb91bd8ea18.patch")
sha512sums=('431d19e3297ba1cdae5313948123d8b8cd71ee8ad5868b8cc2f5447eb435875fdbcdc3768834b9afe22ce8e4c1abaabeb597f3e6974b00aab33aa61e497d6390'
            '04aa810fe2433ef1a14da28ce08ed223f4c541af13d2cbb23a3c7985d4bc82625067ad6785ede406188b4430b7562c1ba4b411ad840a4cba05df2a8e670d6c4b'
            '4339f1de58183db141e8a62b6d7d37ec4fbd3e59765d87f130299579d7e09dd1e5334ffbd5f0cbdc0df4277e044f5a37cc98d810178d0ca0f7e21ad22c44131a'
            'e600d918bd5c826d03beca7efa15ce917b022d22c2a36fa79a2f32c461740b5f44a2e6a4301f94e90e6da4272e570b071f93e6f947ab3c186f1e2f24b75f3397')

prepare() {
  cd $_pkgname-$pkgver

  # https://github.com/ponyorm/pony/pull/565
  patch -Np1 -i ../$pkgname-py39-decompiler.patch
  patch -Np1 -i ../$pkgname-py39-setup.patch
  patch -Np1 -i ../$pkgname-py39-query-expr.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py test
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  rm -rf "$pkgdir"/usr/lib/python*/site-packages/pony/orm/tests
}
