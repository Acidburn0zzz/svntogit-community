# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=python-json5
pkgver=0.9.0
pkgrel=1
pkgdesc="A Python implementation of the JSON5 data format"
arch=(any)
url="https://pypi.org/project/json5/"
license=(MPL GPL LGPL)
depends=(python)
makedepends=(python-setuptools)
source=($pkgname-$pkgver::"https://github.com/dpranke/pyjson5/archive/v$pkgver.tar.gz")
sha256sums=('170a0cc78ebe35475397ec9d4f29beb6a304de581774506cc0562b557618e7c7')

build() {
  cd pyjson5-$pkgver
  
  python setup.py build
}

package() {
  cd pyjson5-$pkgver

  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
  rm -r "$pkgdir"/usr/lib/*/site-packages/tests # Don't ship tests
}
