# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.4.4
pkgrel=1
pkgdesc="Creates virtual Ethernet networks of almost unlimited size."
arch=('x86_64')
url="https://www.zerotier.com/index.html"
license=('GPL3')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('ruby-ronn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz")
sha512sums=('7757b665fc09ecf537812e6ae8880078ffbd2025510ddf5fa1ffbd1e577517d5c8288098e9f9770c639108a575f80eab3d6057a1ecbae3f8f6db38bcbdc8a761')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
