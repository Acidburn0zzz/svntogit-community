# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Harry Jeffery <harry|@|exec64|.|co|.|uk>
# Contributor: Alex Jordan <alexander3223098@gmail.com>

pkgname=zerotier-one
pkgver=1.4.6
pkgrel=2
pkgdesc="Creates virtual Ethernet networks of almost unlimited size."
arch=('x86_64')
url="https://www.zerotier.com/index.html"
license=('GPL3')
depends=('gcc-libs' 'libnatpmp' 'miniupnpc')
makedepends=('ruby-ronn')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zerotier/ZeroTierOne/archive/$pkgver.tar.gz"
        "zerotier-one-1.4.6-gcc10-fix.patch::https://github.com/zerotier/ZeroTierOne/commit/cce4fa719d447c55d93458b25fa92717a2d61a60.patch")
sha512sums=('72f00602f67190d03ac0c8caa6b79e7f346b10745bdf772c68f77a3a940cdd718d05ec642bc862a6235aab9bd3bfefb0c57571f358ae55f38761f45500f0f189'
            '0bfbaf6a2e924ca687ea95c27111fbce9c88893344c404b68ebb3f4498470b99c2f575af3ad93d312fc4444501da23518caaff3b4afefc07fa80293f1989aabe')

prepare() {
  cd ZeroTierOne-$pkgver
  sed -i 's/sbin/bin/' make-linux.mk debian/zerotier-one.service

  # Fix compilation with GCC 10 (https://github.com/zerotier/ZeroTierOne/pull/1232)
  patch -Np1 -i "$srcdir/zerotier-one-1.4.6-gcc10-fix.patch"
}

build() {
  cd ZeroTierOne-$pkgver
  make
}

check() {
  cd ZeroTierOne-$pkgver
  make selftest
  ./zerotier-selftest
}

package() {
  cd ZeroTierOne-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 debian/zerotier-one.service "$pkgdir"/usr/lib/systemd/system/zerotier-one.service
}
