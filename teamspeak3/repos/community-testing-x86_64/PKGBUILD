# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: J.W. Birdsong  <jwbirdsongATmailDOTcom>

pkgname=teamspeak3
pkgver=3.1.4
pkgrel=1
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com"
license=('custom')
optdepends=('libpulse')
makedepends=('chrpath')
arch=('i686' 'x86_64')
source_i686=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_x86-${pkgver}.run")
source_x86_64=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_amd64-${pkgver}.run")
source=('teamspeak3.desktop'
        'teamspeak3.png'
        'teamspeak3.launcher'
        'LICENSE')
sha512sums=('4cac0b7446dc3e728b232fdd6c6d75be1d2a5530635eab5918a4e7dd6a4a3fc21d6a75055da919b56e5cf849f4c5ba26ece56759a45bc4850eba3eb73853759d'
            'af18ed36a3c8de90b60a7205ebf91a35d4b517f22f97d541c82debdb12697882469d9771fdf3fb8b081aeb74a01237f3afd4732daccf4b374dc6d39e7df5d444'
            '2365bf65500a7455551ca656d27d8fed54dd7151e61e369296e4fa2b5451ee3dd0c4adc67ec1d0f221de0ed7e6efa18c303b63bf2d858060671491bee51c899d'
            'd6d03f1bda86c90390ce0032e4d30e889977f80ff76ba1e5c443109e93cbedbee92c6fc2759f58d23659c15dba4f2ac718861571239ba117dcfe11f136f556dc')
sha512sums_i686=('0df524138c61b7e828dd1c8f27dea2e1b7c4490518fb62282fda9b05d893194895f8bf77c77657e1aeb4a4ca2f1be9d80ccf647ef9acf7555eda52542e0767e7')
sha512sums_x86_64=('450374f3cd3b314205223d80ab040e0da33bed14ec7f3561f600b3c065f6b1e4c2d755b192d4a6e2f0f2b89bc55b86183859331c0d7b22d2418f88aadc9f41fb')

[[ "$CARCH" == "i686" ]] && _TSARCH='x86'
[[ "$CARCH" == "x86_64" ]] && _TSARCH='amd64'

prepare() {
  mkdir archive && cd archive
  sh ../TeamSpeak3-Client-linux_${_TSARCH}-${pkgver}.run --tar -xf 2>/dev/null

  # Fix FS#34190
  sed -i "/export QTDIR.*/d" ts3client_runscript.sh
  sed -i "/export QT_PLUGIN_PATH*/d" ts3client_runscript.sh

  # Fix FS#34189
  rm qt.conf

  # Fix permissions
  find -type d | xargs chmod 755
  find -type f | xargs chmod 644
  find -name *.so | xargs chmod 755
  chmod +x ts3client*
  chmod +x package_inst
}

package() {
  install -d "${pkgdir}"/{usr/bin/,opt/teamspeak3}

  cp -r archive/* ${pkgdir}/opt/teamspeak3/

  # Install Desktop File
  install -D -m644 "${srcdir}"/teamspeak3.desktop "${pkgdir}"/usr/share/applications/teamspeak3.desktop

  # Install Icon File
  install -D -m644 "${srcdir}"/teamspeak3.png "${pkgdir}"/usr/share/pixmaps/teamspeak3.png

  # Install Custom License
  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Install Client Launcher
  install -D -m755 "${srcdir}"/teamspeak3.launcher "${pkgdir}"/usr/bin/teamspeak3

  # Remove insecure RPATH
  chrpath -d "${pkgdir}"/opt/teamspeak3/ts3client_linux_${_TSARCH}
}
# vim:set ts=2 sw=2 et:
