# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=2.7.1
pkgrel=1
pkgdesc="Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=(x86_64)
url="https://www.ospray.org/"
license=(Apache)
depends=(intel-tbb embree ispc libgl glfw glu openmpi openimagedenoise openvkl rkcommon snappy)
optdepends=('benchmark: benchmarks'
            'gtest: tests suite')
makedepends=(cmake benchmark gtest)
source=(https://github.com/ospray/OSPRay/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('4e7bd8145e19541c04f5d949305f19a894d85a827f567d66ae2eb11a760a5ace')

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DOSPRAY_BUILD_ISA=ALL \
    -DOSPRAY_MODULE_DENOISER=ON \
    -DOSPRAY_MODULE_MPI=ON
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
