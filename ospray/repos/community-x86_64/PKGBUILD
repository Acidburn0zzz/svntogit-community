# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=1.8.2
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="https://www.ospray.org/"
license=('Apache')
depends=('intel-tbb' 'embree' 'ispc' 'libgl' 'glfw-x11' 'glu' 'openmpi')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ospray/OSPRay/archive/v${pkgver}.tar.gz")
sha256sums=('3686b4561e808a251bef4667a9bc47198b7a8e6453f67f89897d3b925a08472e')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    # Remove tests
    rm "${pkgdir}"/usr/bin/test_*
    rm -r "${pkgdir}"/usr/scripts
}
