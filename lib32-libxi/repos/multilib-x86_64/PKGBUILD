# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.7.1
pkgrel=3
pkgdesc="X11 Input extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext' 'lib32-libxfixes' 'inputproto' $_pkgbasename)
makedepends=('pkgconfig' 'xorg-util-macros' gcc-multilib automake)
options=(!libtool)
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2
        git-fixes.diff)
sha256sums=('e92adb6b69c53c51e05c1e65db97e23751b935a693000fb0606c11b88c0066c5'
            'faa2f76f68e65f537062829ad45977561079ac8747bd8e24a82055e70a5bf0cc')

prepare() {
  cd "${srcdir}/libXi-${pkgver}"

  # fix CVE-2013-1998 CVE-2013-1984 CVE-2013-1995 merged upstream
  patch -Np1 -i ${srcdir}/git-fixes.diff

  # a patch change configure.ac
  autoreconf -fiv
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXi-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32

  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
