# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>

pkgname=fd
pkgver=8.2.0
pkgrel=1
pkgdesc='Simple, fast and user-friendly alternative to find'
arch=('x86_64')
url=https://github.com/sharkdp/fd
license=('MIT' 'APACHE')
depends=('gcc-libs')
makedepends=('rust')
source=("fd-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('14dc7f5433ae069503c65f559b8af186b0b82fd2a5ba16f0ff1dfd837f7b88b9fa119cfb9aadd5ec4add72d0deecf13d9878d8af77d79bbdd6f4d4760f329687')

build() {
  cd fd-$pkgver
  cargo build --release --locked
}

check() {
  cd fd-$pkgver
  cargo test --release --locked
}

package() {
  cd fd-$pkgver
  install -Dm755 target/release/fd "$pkgdir"/usr/bin/fd
  install -Dm644 target/release/build/fd-find-*/out/fd.bash "$pkgdir"/usr/share/bash-completion/completions/fd
  install -Dm644 target/release/build/fd-find-*/out/fd.fish "$pkgdir"/usr/share/fish/vendor_completions.d/fd.fish
  install -Dm644 contrib/completion/_fd "$pkgdir"/usr/share/zsh/site-functions/_fd
  install -Dm644 doc/fd.1 "$pkgdir"/usr/share/man/man1/fd.1
  install -Dm644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/fd/LICENSE-APACHE
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/fd/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
