# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=1.0
pkgrel=1
pkgdesc="Qt5 Configuration Utility"
arch=(x86_64)
url="https://qt5ct.sourceforge.io/"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('a92c7bbdaf8182ce38c1a49e34d30f5b79ec75bd70a4bfabeb9b7cb0e0971a85')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 $pkgname.pro 
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

