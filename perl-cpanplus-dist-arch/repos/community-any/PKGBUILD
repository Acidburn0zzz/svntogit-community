# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-cpanplus-dist-arch
pkgver=1.27
pkgrel=1
pkgdesc='CPANPLUS backend for building Archlinux pacman packages'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/CPANPLUS-Dist-Arch
source=("http://search.cpan.org/CPAN/authors/id/J/JU/JUSTER/CPANPLUS-Dist-Arch-$pkgver.tar.gz")
md5sums=('45c9af9128f5968f24fc12fc420134d2')
sha512sums=('84d3452182f247173aafb49c71a4efec2d7f2e0fd35db45143c26356f06474aaa3f37ae45983ae5cbfd1bd9f545cee211fe771d43bed2893484ba4e0dd907d18')
_dir="CPANPLUS-Dist-Arch-$pkgver"

build()
(
  cd "$srcdir/$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  /usr/bin/perl Build.PL
  ./Build
)

check()
(
  cd "$srcdir/$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  ./Build test
)

package()
(
  cd "$srcdir/$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  ./Build install installdirs=vendor destdir="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
