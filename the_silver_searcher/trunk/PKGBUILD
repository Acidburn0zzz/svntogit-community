# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jeff Horelick <jdhore1@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=the_silver_searcher
pkgver=2.0.0
pkgrel=1
pkgdesc='Code searching tool similar to Ack, but faster'
arch=('x86_64' 'i686')
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
depends=('pcre' 'xz')
makedepends=('git')
source=("git+https://github.com/ggreer/$pkgname.git#commit=99cf1834ce2c038c184d64793aaa6686381c49c5")
md5sums=('SKIP')

prepare() {
  sed -i 's:configure:configure --prefix=/usr:' "$pkgname/build.sh"
}

build() {
  "./$pkgname/build.sh"
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim: ts=2 sw=2 et:
