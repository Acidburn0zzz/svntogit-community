# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jeff Horelick <jdhore1@gmail.com>
# Contributor: skydrome <skydrome@i2pmail.org>

pkgname=the_silver_searcher
pkgver=0.15
pkgrel=1
pkgdesc='Code searching tool similar to Ack, but faster'
url='https://github.com/ggreer/the_silver_searcher'
license=('Apache')
depends=('pcre' 'zlib' 'xz')
arch=('x86_64' 'i686')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ggreer/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('bb05e329148315a9791cfa5cc1f7db4681e44476729971b4b3b7c2845a635748')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's:configure:configure --prefix=/usr:' build.sh
  ./build.sh
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
