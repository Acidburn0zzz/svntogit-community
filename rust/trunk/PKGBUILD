# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=('rust' 'rust-docs')
epoch=1
pkgver=1.11.0
pkgrel=1
arch=('x86_64' 'i686')
pkgdesc='Systems programming language focused on safety, speed and concurrency'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
makedepends=('libffi' 'perl' 'python2' 'curl' 'jemalloc' 'cmake')
depends=('gcc')
source=("http://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc})
sha256sums=('3685034a78e70637bdfa3117619f759f2481002fd9abbc78cc0f737c9974de6a'
            'SKIP')
options=('staticlibs' '!strip' '!emptydirs')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>
#gpg --keyserver hkp://keys.gnupg.net --recv-keys 108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE

build() {
  cd "rustc-$pkgver"

  ./configure --prefix=/usr --release-channel=stable
  make CFLAGS="$CFLAGS -fPIC -w"
}

package_rust() {
  depends=('gcc')

  cd "rustc-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"

  cd "$pkgdir/usr/lib"
  rm rustlib/{components,manifest-rustc,rust-installer-version}
  ln -sf rustlib/$CARCH-unknown-linux-gnu/lib/*.so .
}

package_rust-docs() {
  cd "rustc-$pkgver"

  install -d "$pkgdir/usr/share/doc/rust"
  cp -r "doc/"* "$pkgdir/usr/share/doc/rust"
  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}

# vim:set ts=2 sw=2 et:
