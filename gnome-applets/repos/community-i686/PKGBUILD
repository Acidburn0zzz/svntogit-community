# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-applets
pkgver=3.18.1
pkgrel=3
pkgdesc="Small applications for the GNOME Panel"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeApplets"
license=('GPL')
depends=('gnome-panel' 'libgtop' 'libtracker-sparql' 'gucharmap' 'python-gobject' 'cpupower' 'upower')
makedepends=('intltool' 'itstool' 'gnome-settings-daemon')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-stickynotes-don-t-ignore-font-type.patch
        0001-Add-Command-applet.patch
        0002-Add-Timer-applet.patch)
sha256sums=('e09eb18f55be851da4c75c7d97df40130625ea548b2aa8d9db21e17bb06eefd5'
            '7b6120c5d8daebe71fceb1f6993bed1580c82ed28d75d6ca44f1f87add46325a'
            '20fda1b2093100e7e816f991a2cc145afa5c4dc69750e42c55d019043eb59a3d'
            '7cd9a8e1abb203a7f6f7a7b48af678f9dde748ee9ddf57a1d0b0cd742d28a5c8')

prepare() {
  cd $pkgname-$pkgver
  # stickynotes: don't ignore font type
  patch -Np1 -i ../0001-stickynotes-don-t-ignore-font-type.patch

  # Add more applets
  # https://bugzilla.gnome.org/show_bug.cgi?id=763888
  # https://bugzilla.gnome.org/show_bug.cgi?id=763889
  patch -Np1 -i ../0001-Add-Command-applet.patch
  patch -Np1 -i ../0002-Add-Timer-applet.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --with-cpufreq-lib=cpupower
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
