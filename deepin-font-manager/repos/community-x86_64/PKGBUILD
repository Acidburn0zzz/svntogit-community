# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=5.8.0.15
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-file-manager')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-font-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8f08ca7d5c1c5aeacfb1d916933e95fed05074c51ef588135224d67f00029d390961e5d7ade271a4d17dfb6238f0bb32532e3de581bd760f68edc904b1314048')

build() {
  cd deepin-font-manager-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-font-manager-$pkgver
  ninja test || :
}

package() {
  cd deepin-font-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
}
