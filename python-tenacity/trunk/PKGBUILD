# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=8.0.0
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-typeguard')
source=("https://github.com/jd/tenacity/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('994d056c5d0585f67d2fa74c7e3a9aa9c83a473c6aaa33e0282ed619523af6dc9a1e685e9d7621d7ad7855904b2ffab0e05fa4b084eb3f5d7bca9c97fbbdece2')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
