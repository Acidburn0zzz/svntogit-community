# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=5.0.4
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python-six')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-pbr')
checkdepends=('python-pytest-runner' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('576ce7be803de2b86325c38792cf23780fa0e805687e4fc76a73d1d0c9857b175c8eac1a179f7f001c3009109d3cfef8b34999c3a05c512fef4287c7abb4b656')

export PBR_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
