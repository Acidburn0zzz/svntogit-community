# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=6.3.0
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python-six')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-typeguard')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('59098f6c5bf2d1ae8bc0d1e7474666cfe1b8ce81f778677deb8d6c1eb25fa00f4d06ff6f0ede405dd94ed5fee67a7ad67a09ef8e8ca2f7e3bc89b9ab54f0b94b')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
