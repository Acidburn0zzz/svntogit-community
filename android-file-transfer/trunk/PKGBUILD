# Contributor: yaroslav <proninyaroslav@mail.ru>
# Contributor: Askhat Bakarov <sirocco{at}ngs{dot}ru>

pkgname=android-file-transfer
pkgver=3.3
pkgrel=1
pkgdesc='Android MTP client with minimalistic UI'
arch=(x86_64)
url='https://github.com/whoozle/android-file-transfer-linux'
license=(GPL3)
depends=(qt5-base fuse2 libxkbcommon-x11 hicolor-icon-theme file)
makedepends=(cmake)
source=(android-file-transfer-$pkgver.tar.gz::https://github.com/whoozle/android-file-transfer-linux/archive/v$pkgver.tar.gz
        fix_build1.patch::https://github.com/whoozle/android-file-transfer-linux/commit/8980ff8171961134334d388b5a0ed54e0186b31f.patch
        fix_build2.patch::https://github.com/whoozle/android-file-transfer-linux/commit/53230d606e44b7901e931a347b7f59ed3a963e2a.patch)
sha256sums=('fa33ddf79cb5e6e62248868534c7b54096e67613ae1788a243617348a6d839a0'
            '601a84f891a5722ea845e9683765afdcbcf59550a422fa801d940a72de8c5489'
            '893eca553853383f29e641f2524e9577633990409706728029162832064a0787')

prepare() {
  cd android-file-transfer-linux-$pkgver
  patch -p1 < ../fix_build1.patch
  patch -p1 < ../fix_build2.patch
}

build() {
  cd android-file-transfer-linux-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd android-file-transfer-linux-$pkgver
  make DESTDIR="$pkgdir/" install
}
