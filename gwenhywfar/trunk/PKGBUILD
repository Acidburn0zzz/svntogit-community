# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=4.20.1
pkgrel=1
pkgdesc="OS abstraction functions for various projects"
arch=(x86_64)
url="https://www.aquamaniac.de/"
license=(LGPL)
depends=(gnutls libgcrypt openssl)
makedepends=(git gtk3 qt5-base)
optdepends=('gtk3: for the GTK3 UI'
            'qt5-base: for the Qt5 UI')
_commit=f7db2a496044ab4ea5c6d5c52378cb6fc4e143c9  # tags/4.20.1
source=("git+https://git.aquamaniac.de/git/gwenhywfar#commit=$_commit")
sha256sums=('SKIP')
#validpgpkeys=(42400AF5EB2A17F0A69BB551E9899D784A977416) # AqBanking Package Key <packages@aqbanking.de>

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  autoreconf -fi
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --enable-system-certs --with-guis="gtk3 qt5"
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
