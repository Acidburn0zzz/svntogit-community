# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: William Rea <sillywilly@gmail.com>

pkgname=gwenhywfar
pkgver=4.18.0
_dnrel=206
pkgrel=1
pkgdesc="OS abstraction functions for various projects"
arch=(x86_64)
url="http://www.aquamaniac.de"
license=("LGPL")
depends=('openssl' 'gnutls' 'libgcrypt' 'libice' 'libsm')
makedepends=('gtk2' 'qt4')
optdepends=('gtk2' 'qt4')
source=("gwenhywfar-$pkgver.tar.gz::http://www2.aquamaniac.de/sites/download/download.php?package=01&release=${_dnrel}&file=01&dummy=gwenhywfar-$pkgver.tar.gz")
sha256sums=('6915bba42d8b7f0213cee186a944296e5e5e97cdbde5b539a924261af03086ca')

build() {
  pacman -Q gwenhywfar && return 1
  cd "$srcdir"/gwenhywfar-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --enable-ssl --enable-system-certs
  sed -i 's|gnutls_transport_set_lowat||g' src/sio/syncio_tls.c
  make
}

package() {
  cd "$srcdir"/gwenhywfar-$pkgver
  make DESTDIR="$pkgdir" install
}
