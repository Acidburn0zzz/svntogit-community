# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Nikita Volodin <volodin.n at gmail dot com>

pkgname=keycloak
pkgver=10.0.1
_postgresql_jdbc=42.2.12
pkgrel=2
pkgdesc="Open Source Identity and Access Management For Modern Applications and Services"
arch=('any')
url="https://www.keycloak.org/"
license=('Apache')
depends=('java-runtime>=8' 'grep' 'bash' 'coreutils' 'util-linux')
makedepends=('maven' 'git' 'java-environment=8')
backup=(
  opt/keycloak/standalone/configuration/application-roles.properties
  opt/keycloak/standalone/configuration/application-users.properties
  opt/keycloak/standalone/configuration/logging.properties
  opt/keycloak/standalone/configuration/mgmt-groups.properties
  opt/keycloak/standalone/configuration/mgmt-users.properties
  opt/keycloak/standalone/configuration/standalone-ha.xml
  opt/keycloak/standalone/configuration/standalone.xml
)
options=(emptydirs)
source=(https://github.com/keycloak/keycloak/archive/$pkgver/$pkgname-$pkgver.tar.gz
        keycloak.service
        keycloak.sysusers
        keycloak.tmpfiles
        layers.conf
        postgresql-module.xml
        https://jdbc.postgresql.org/download/postgresql-${_postgresql_jdbc}.jar
        https://github.com/keycloak/keycloak/pull/7053.patch)
sha512sums=('311ec1c3ef46cca2267b2b0517e581916a8f95b76154f947017604f8e73b76ac556a2bcbd9a5156331a496f7bdb3789ce8c2dd069280aee05ae934e3e5a36090'
            '34d3fda501e0616bf5e4631f8dbe3a7f4c2b7566ffedc63e4f6ea49acbc6c1f3f75aabec22bb4b42d2aad586468ef5c7fddf29ca94253fcf141b71d88806a71a'
            'c53b734598ccf675930754514bd2af628355025197e77541e5acb8718629a8ece763c814e8373278758a30475e6a716febeff10bdebf0847d914bc193e732c79'
            '2f51757761d0444820e5624cdd061062d125a1ea74faf51c224603cae11583f99ff9ffbb79bb1834ae45dd6439b5d2db0f36f3a84c625df85fa9549e95308fea'
            'd54a04c120edb933c4f7168460bd813ff92a4e3771cedcabfeeb76c5d3e057b3cd0d0486dc11ddbd91e8d87ca90193e4c74cc3414e45106dd556d455ac7e60b2'
            '5eece4f3eb57372969f0e5319804198b546455828e938ff7f37bbcfb58be08efde43a5b0376c98f359c4bc5eb069e0cd9410daefe19da1c03e9e9c57d3c1b75b'
            '3f9518bc93ee12913040c7728a79bebab97ea4d991fff40cf227dd7ed09c147cd8faba3a50604dcfdbead78ed0fb910334c19b1db923bfe0e73c0c013d2beb34'
            'd8783f209512857de68fc7232f2ac08690094e47b876ed24533ade4352327b62191019fa06aab424b363f3ba8f5613cf7f523faac93273e17ab4107ed326b241')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i "$srcdir"/7053.patch
}

build() {
  cd $pkgname-$pkgver

  export PATH="/usr/lib/jvm/java-8-openjdk/bin:$PATH"
  mvn -Pdistribution \
    -pl distribution/server-dist \
    -am \
    -Dmaven.test.skip \
    clean install
}

package() {
  cd $pkgname-$pkgver

  mkdir -p "$pkgdir/opt"
  cp -r "distribution/server-dist/target/keycloak-$pkgver" "$pkgdir/opt/keycloak"
  install -Dm 644 "$srcdir"/layers.conf -t "$pkgdir/opt/keycloak/modules"
  install -d "$pkgdir"/opt/keycloak/standalone/{data,log}

  install -Dm644 "$srcdir"/postgresql-${_postgresql_jdbc}.jar "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/postgresql-${_postgresql_jdbc}.jar
  sed "s/@JDBC_POSTGRESQL_JAR@/postgresql-${_postgresql_jdbc}.jar/g" "$srcdir"/postgresql-module.xml > "$pkgdir"/opt/keycloak/modules/system/layers/keycloak/org/postgresql/main/module.xml

  install -d "$pkgdir/"{etc,var/log}
  ln -s /opt/keycloak/standalone/configuration "$pkgdir/etc/keycloak"
  ln -s /opt/keycloak/standalone/log "$pkgdir/var/log/keycloak"

  install -Dm644 "$srcdir"/keycloak.service "$pkgdir"/usr/lib/systemd/system/keycloak.service
  install -Dm644 "$srcdir"/keycloak.sysusers "$pkgdir"/usr/lib/sysusers.d/keycloak.conf
  install -Dm644 "$srcdir"/keycloak.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/keycloak.conf

  mkdir -p "$pkgdir"/usr/bin
  ln -s /opt/keycloak/bin/kcadm.sh "$pkgdir"/usr/bin/kcadm
  ln -s /opt/keycloak/bin/kcreg.sh "$pkgdir"/usr/bin/kcreg
}

# vim: ts=2 sw=2 et:
