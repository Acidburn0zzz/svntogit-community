# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgbase=python-pygit2
pkgname=('python-pygit2' 'python2-pygit2')
pkgver=0.25.1
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('i686' 'x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.25.0')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'git' 'python-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz"
        "https://github.com/libgit2/pygit2/commit/b88dc868423af2f760f649960112efd0e37e5335.patch")
sha256sums=('4dae0cf99a0ed21b3e5c98dc8b170cbb20f1d8b6c73626caaf6eac2617d8acde'
            '4cf18766e45781dccaeb30c81b668e23796e065d2408b5df11847d02bdc00757')

prepare() {
  patch -d "pygit2-$pkgver" -p1 -i ../b88dc868423af2f760f649960112efd0e37e5335.patch
}

package_python-pygit2() {
  depends+=('python-cffi' 'python-six')
  replaces=('python-libgit2')
  conflicts=('python-libgit2')

  cd "pygit2-$pkgver"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  depends+=('python2-cffi' 'python2-six')
  replaces=('python2-libgit2')
  conflicts=('python2-libgit2')

  cd "pygit2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
