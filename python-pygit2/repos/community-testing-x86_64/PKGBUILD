# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.8.0
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:1.3.0' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz"::$url/archive/v$pkgver.tar.gz
        "$pkgname-1.4.x.patch"::$url/commit/935e2b5b4cd36bfbc8c15b5498a4f80df77c3176.patch)
sha256sums=('61da6f55643bc7d9c70344b1b3f9f80fedbbc5bf41e88f82d10670380ae704b5'
            '9e1fe49f58b833eda5821be5df811f75b8c20374645398840a608f554eb02ad3')

prepare() {
	cd "pygit2-$pkgver"
	# Disable tests that do stuff online
	sed -i -e '/has_network/s/True/False/' test/utils.py
	# Unblock libgit2 1.4.x
	patch -p1 < ../${source[1]%::*}
}

build() {
	cd "pygit2-$pkgver"
	python setup.py build
}

check() {
	cd "pygit2-$pkgver"

	# Skipped tests fail because upstream test suite hasn't 100% adapted to libgit2 1.4.x
	python setup.py pytest \
		--addopts "--deselect test/test_diff.py::test_diff_patch --deselect test/test_diff.py::test_diff_patchid"
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
