# Maintainer: David Runge <dvzrv@archlinux.org>
_name=natsort
pkgname=python-natsort
pkgver=7.0.0
pkgrel=1
pkgdesc="Simple yet flexible natural sorting in Python"
arch=('any')
url="https://github.com/SethMMorton/natsort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-mock' 'python-pyflakes'
'flake8' 'python-hypothesis' 'python-mock')
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a6dde0eca7a8f51859c43b5c928c354849be6256fdfa1fea371920458b076f609746316b640317b3be8eea441ca76e08a4c99475c0e468c46bee35ec5779d3c7')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
