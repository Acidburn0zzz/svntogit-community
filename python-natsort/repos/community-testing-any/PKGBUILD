# Maintainer: David Runge <dave@sleepmap.de>
_name=natsort
pkgname=python-natsort
pkgver=5.4.1
pkgrel=1
pkgdesc="Simple yet flexible natural sorting in Python"
arch=('any')
url="https://github.com/SethMMorton/natsort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-mock' 'python-pyflakes'
'flake8' 'python-hypothesis' 'python-mock')
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('355aa834bec655232774b18f142fb5b296e427080f67778db2ee5a52cd3f0255adaa695c8adcc5b03b05884943e400dd682e82ba3d84235d0881ac37b1f84d62')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
