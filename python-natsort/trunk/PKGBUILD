# Maintainer: David Runge <dave@sleepmap.de>
_name=natsort
pkgname=python-natsort
pkgver=6.0.0
pkgrel=2
pkgdesc="Simple yet flexible natural sorting in Python"
arch=('any')
url="https://github.com/SethMMorton/natsort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-mock' 'python-pyflakes'
'flake8' 'python-hypothesis' 'python-mock')
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
         natsort-python-3.8.patch)
sha512sums=('d051de6824a727ead814c0305beefaa61ee64038999187b95db5395c761f623077b086ef453c134668cb764f4fc5e72a767177442f6ac3d71ec9b491e8876d5b'
            '132b40e2dced414f6b1969b31e7d2b71a2b44778aff0cfb0106c8e83d8c62a178cef61d64e6a7583fa05e1f1a9d663c7c1e5c6af2aeb4863cc1bfe0d09957302')

prepare() {
  cd $_name-$pkgver
  patch -p1 -i ../natsort-python-3.8.patch # Update unicode_numeric_hex list for python 3.8
  cd ..

  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
