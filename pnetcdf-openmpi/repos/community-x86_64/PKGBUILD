# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pnetcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=1.11.2
pkgrel=1
pkgdesc="Parallel I/O Library for NetCDF File Access (${_mpi} version)"
arch=(x86_64)
url="https://parallel-netcdf.github.io/"
license=(custom)
depends=("${_mpi}" "netcdf-${_mpi}")
makedepends=(gcc-fortran)
checkdepends=(inetutils)
provides=(parallel-netcdf parallel-netcdf-openmpi)
conflicts=(parallel-netcdf parallel-netcdf-openmpi)
replaces=(parallel-netcdf parallel-netcdf-openmpi)
options=(staticlibs)
source=("${url}/Release/${_pkg}-${pkgver}.tar.gz")
sha1sums=(cf310a037846a5722c05525b0a77273bea8059a0)

build() {
    cd ${_pkg}-${pkgver}
    autoreconf -vfi
    ./configure --prefix=/usr --enable-shared --enable-netcdf4
    make LIBTOOLFLAGS=--verbose
}

check() {
    cd ${_pkg}-${pkgver}
    make check
    make ptest
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}
}
