# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pnetcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=1.11.0
pkgrel=1
pkgdesc="Parallel I/O Library for NetCDF File Access (${_mpi} version)"
arch=('x86_64')
url="https://parallel-netcdf.github.io/"
license=('custom')
depends=("${_mpi}" "netcdf-${_mpi}")
makedepends=('gcc-fortran')
checkdepends=('inetutils')
provides=('parallel-netcdf' 'parallel-netcdf-openmpi')
conflicts=('parallel-netcdf' 'parallel-netcdf-openmpi')
replaces=('parallel-netcdf' 'parallel-netcdf-openmpi')
options=('staticlibs')
source=("${url}/Release/${_pkg}-${pkgver}.tar.gz")
sha1sums=('a13fd24953abce1f3fd7aa94fd66f1b9895182de')

build() {
    cd ${_pkg}-${pkgver}

    export CPPFLAGS="${CPPFLAGS} -fPIC"
    ./configure --prefix=/usr --enable-shared --enable-netcdf4
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make check
    make ptest || warning "Parallel tests failed"
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}
}
