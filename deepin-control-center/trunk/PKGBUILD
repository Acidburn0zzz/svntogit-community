# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.3.7.20180307
_commit=c92333a4f83b49fd4dd3deec3501911b99493e4e
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
optdepends=('redshift: automatic color temperature support')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$_commit.tar.gz"
        remove-unused-slots.patch)
sha512sums=('bba94c7fef5b5aa33ae7f9fed1d435eb9dadec12ff24efac9bd269f16529f2381a46c423806a8cb3d69960ecf2eaa8870537836aa66c71a8741a9c1abee95f1e'
            '209f3ebd105fd98e48be63d9ce9c9d6852764a3aa4db3ebe676179c29b279c12c4cb41e4f563406527eec58dcd180b45fec17d19a2fb28efb00ffc41774c04e5')

prepare() {
  mv dde-control-center-{$_commit,$pkgver}

  cd dde-control-center-$pkgver
  patch -p1 -i ../remove-unused-slots.patch
}

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
