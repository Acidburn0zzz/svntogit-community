# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.0.15
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'deepin-tool-kit' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::http://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz")
sha512sums=('7138d188a6a2f0b7b21526e99deed3df664493fa14a7ff8d2de4f4c351953479483960c45d2b215267594553b0242fcd13ea34d1eea1f80d57fb1fa3a4e0698c')

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
