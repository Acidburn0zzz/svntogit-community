# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.3.7.20180302
_commit=686d915ad873388b5f1e6d885a3b4c1cf1498ae0
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
optdepends=('redshift: automatic color temperature support')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$_commit.tar.gz")
sha512sums=('8f9532ac41752bcd10d71729422e3a19bcb6e3ab7d8148d7ab5ca50c1d37a038fc82d922fd2949a4c72412582deb07f41912e388caaa5136e22b702978e3d09a')

prepare() {
  mv dde-control-center-{$_commit,$pkgver}
}

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
