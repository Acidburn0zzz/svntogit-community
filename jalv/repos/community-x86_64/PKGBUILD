# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=jalv
pkgver=1.6.2
pkgrel=2
pkgdesc="A simple but fully featured LV2 host for Jack"
arch=('x86_64')
url="https://drobilla.net/software/jalv"
license=('ISC')
groups=('pro-audio')
depends=('lilv' 'suil')
makedepends=('gtk3' 'gtkmm' 'qt5-tools' 'waf')
optdepends=('qt5-base: Qt 5.x frontend'
            'gtk3: Gtk+ 3.x frontend'
            'gtkmm: Gtk+ 2.x frontend')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('a39d1212b8dc1da2d8509887f7d0b1239ce052917787a12df0de4a21887f6c375b325dd787d0866794a2ae3ecf8cbffef9c483b25c02dae7dcd2e634baa326d5'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

prepare() {
  cd "${pkgname}-${pkgver}"
  # copying custom waf script to separate directory, removing outdated waflib
  # and fixing wscript to use the custom tools location instead.
  mkdir -pv tools
  mv -v waflib/extras/autowaf.py tools/
  rm -rfv waflib
  sed -e 's/waflib.extras/tools/g' \
      -e "s/load('autowaf'/load('autowaf', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr
  waf
}

package() {
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm 644 {AUTHORS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
