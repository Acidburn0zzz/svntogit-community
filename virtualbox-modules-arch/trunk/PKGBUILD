# Maintainer: SÃ©bastien Luttringer
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=virtualbox-modules-arch
pkgname=('virtualbox-host-modules-arch')
pkgver=6.1.4
pkgrel=14
arch=('x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-headers'
             "virtualbox-host-dkms=$pkgver")

build() {
    _kernver=$(</usr/src/linux/version)

    dkms build --dkmstree "$srcdir" -m vboxhost/${pkgver}_OSE -k $_kernver
}

package_virtualbox-host-modules-arch(){
  _kernver="$(</usr/src/linux/version)"
  pkgdesc='Virtualbox host kernel modules for Arch Kernel'
  depends=('linux')
  replaces=('virtualbox-modules' 'virtualbox-host-modules')
  conflicts=('virtualbox-modules' 'virtualbox-host-modules'
             'virtualbox-host-dkms')
  provides=('VIRTUALBOX-HOST-MODULES')

  install -Dt "$pkgdir/usr/lib/modules/$_kernver/extramodules" -m0644 \
    vboxhost/${pkgver}_OSE/$_kernver/$CARCH/module/*

  # compress each module individually
  find "$pkgdir" -name '*.ko' -exec xz -T1 {} +

  # systemd module loading
  printf '%s\n' vboxdrv vboxnetadp vboxnetflt |
    install -D -m0644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/$pkgname.conf"
}
