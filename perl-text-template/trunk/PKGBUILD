# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=perl-text-template
pkgver=1.45
pkgrel=1
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='http://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MJ/MJD/Text-Template-$pkgver.tar.gz")
md5sums=('cd22441c1ab0870c012225c9a585e262')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 ft=sh et:
