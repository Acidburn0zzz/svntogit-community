# $Id$
# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.52
pkgrel=1
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='http://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('9964c7161fa757cd47b7a9f0ffb17e16982ca30a1088b701cd5f36b55ee47e0bfa84fa1969478c5fad33c57dbf3181e4e49e7d68139d575506a0b39966ae42e6')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
