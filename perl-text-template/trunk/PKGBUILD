# Contributor: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=perl-text-template
pkgver=1.57
pkgrel=1
pkgdesc='Expand template text with embedded perl'
arch=('any')
url='https://search.cpan.org/dist/Text-Template'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-warnings' 'perl-test-more-utf8')
options=('!emptydirs')
source=("https://search.cpan.org/CPAN/authors/id/M/MS/MSCHOUT/Text-Template-$pkgver.tar.gz")
sha512sums=('5be99605693560a3d6121f68caf40887256490fe12fae838ab939fde0cb273fff2bb5d10904095d34de63985188b5a0ae4a253b6af7ddb2ce4c5de8b7f50b762')

build() {
  cd Text-Template-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Text-Template-$pkgver
  make test
}

package() {
  cd Text-Template-$pkgver
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 et:
