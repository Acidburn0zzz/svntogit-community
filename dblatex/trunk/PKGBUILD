# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

pkgname=dblatex
pkgver=0.3.9
pkgrel=2
pkgdesc='DocBook (XML and SGML) to DVI, PDF, PostScript converter using latex.'
arch=('any')
url='http://dblatex.sourceforge.net/'
license=('GPL')
depends=('docbook-xsl' 'docbook-xml' 'libxslt' 'python2' 'texlive-core' 'texlive-htmlxml'
         'texlive-latexextra' 'texlive-pictures')
source=("https://downloads.sourceforge.net/project/dblatex/dblatex/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        dblatex-0.3.9-drop-multirow-dependency.patch)
md5sums=('38e9fd93dd37d71e352010ad40e1dfb4'
         '2ccaf937d0de48c9c283ed0c0f7af5a7')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../dblatex-0.3.9-drop-multirow-dependency.patch
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python2 setup.py install --root "${pkgdir}" --catalogs=/etc/xml/docbook-xml
  sed -i -e "s,${pkgdir},," -e "s,#!/usr/bin/env python,#!/usr/bin/env python2," \
    "${pkgdir}/usr/bin/dblatex"
}
