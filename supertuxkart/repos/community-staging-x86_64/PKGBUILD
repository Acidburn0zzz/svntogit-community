# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=1.0
pkgrel=5
pkgdesc='Kart racing game featuring Tux and his friends'
arch=('x86_64')
url='http://supertuxkart.sourceforge.net/'
license=('GPL2')
depends=('openal' 'libvorbis' 'fribidi' 'curl' 'bluez-libs' 'libxrandr' 'glu' 'wayland')
makedepends=('cmake' 'subversion' 'mesa' 'imagemagick' 'setconf' 'mesa-libgl' 'libxkbcommon')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}-src.tar.xz")
sha512sums=('182754610abd09545630fa1f7fda858ba1731bbacb078bc71597e143f9cb2a1cfcedd3038720760d0471a89b79cc3bb1eb292d38fcd53be05aad7cb17c474ca9')

build() {
  cd ${srcdir}/supertuxkart-${pkgver}

  _fn="data/${pkgname}.desktop"
  setconf "$_fn" Exec "$pkgname"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_RECORDER=0

  make
}

package() {
  cd "$srcdir"/supertuxkart-${pkgver}

  cd build
  make DESTDIR="$pkgdir" install

  # Why is this even in here?
  rm -r "$pkgdir"/usr/lib
  rm -r "$pkgdir"/usr/include
}

# vim:set ts=2 sw=2 et:
