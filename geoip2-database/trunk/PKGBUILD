# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20170712
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(GeoLite2-City-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-City.tar.gz
        GeoLite2-Country-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country.tar.gz
        GeoLite2-ASN-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN.tar.gz)
noextract=(GeoLite2-City-$pkgver.tar.gz
           GeoLite2-Country-$pkgver.tar.gz
           GeoLite2-ASN-$pkgver.tar.gz)
sha256sums=('0dea68a91cfa64c2ac61d8c7febececda51f8dbbf88dfe0c5957da3631bd293e'
            '17a0c534b77e431a39e96874aa89b8922d3f41c4ec7ae71abf6b87f8f18e020f'
            '04938106bebc713a0cb3ba6ca8e2d17fc79d89416078096fa3aed78001019398')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf $_db-$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN-$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
