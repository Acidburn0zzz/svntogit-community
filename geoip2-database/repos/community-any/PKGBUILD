# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname='geoip2-database'
pkgver=20171004
pkgrel=1
pkgdesc="GeoLite2 IP geolocation databases compiled by MaxMind"
arch=('any')
url="https://dev.maxmind.com/geoip/geoip2/geolite2/"
license=('custom:OPEN DATA LICENSE')
source=(GeoLite2-City-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-City.tar.gz
        GeoLite2-Country-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country.tar.gz
        GeoLite2-ASN-$pkgver.tar.gz::https://geolite.maxmind.com/download/geoip/database/GeoLite2-ASN.tar.gz)
noextract=(GeoLite2-City-$pkgver.tar.gz
           GeoLite2-Country-$pkgver.tar.gz
           GeoLite2-ASN-$pkgver.tar.gz)
sha256sums=('06f63b7da6baceb07e2b7f67830478b41340afbbfaec823e2bcbed5d6f651762'
            '5b82a399d242f24a23f64b1b3fdec68ed14548f5656564486041c0bb8e2b1c73'
            'd17081082b3557be08ba615ac40df323627bd1f2a7026d879cd185d4aac805c3')

prepare() {
  cd "$srcdir"

  for _db in GeoLite2-{City,Country,ASN}; do
    tar --strip-components=1 -xf $_db-$pkgver.tar.gz --wildcards "*/$_db.mmdb"
  done

  tar --strip-components=1 -xf GeoLite2-ASN-$pkgver.tar.gz --wildcards "*/COPYRIGHT.txt" "*/LICENSE.txt"
}

package() {
  cd "$srcdir"

  install -d "$pkgdir/usr/share/GeoIP"
  install -m644 -t "$pkgdir/usr/share/GeoIP" GeoLite2-{City,Country,ASN}.mmdb

  install -Dm644 COPYRIGHT.txt "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT.txt"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
