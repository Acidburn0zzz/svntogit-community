# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: see .contrib

_realname=celt
pkgname=lib32-$_realname
pkgver=0.11.1
pkgrel=1
pkgdesc='Low-latency audio communication codec (32 bit)'
arch=('x86_64')
url='http://www.celt-codec.org'
license=('BSD')
depends=("$_realname" 'lib32-glibc')
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://downloads.xiph.org/releases/$_realname/$_realname-$pkgver.tar.gz")
md5sums=('94fe4acd241da8702c295bcd13a555e6')

build() {
  cd "$_realname-$pkgver"

  unset CFLAGS
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure --prefix=/usr \
              --libdir=/usr/lib32

  make
}

package() {
  cd "$_realname-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove unneeded stuff
  rm -rf "$pkgdir/usr/"{bin,include}
}

# vim:set ts=2 sw=2 et:
