# $Id: PKGBUILD 61589 2012-01-03 23:37:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=libwebp
pkgver=0.1.99
pkgrel=1
pkgdesc="WebP library and conversion tools"
arch=('x86_64' 'i686')
url="http://code.google.com/intl/en/speed/webp/"
license=('BSD')
depends=('libpng' 'libjpeg')
options=('!libtool')
source=("http://webp.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha256sums=('5c98c4ad2af3649fa856ae44207aeb0ee559271e38cedfd10b25976c55cbea24')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
