# $Id: PKGBUILD 61589 2012-01-03 23:37:49Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=libwebp
pkgver=0.1.3
pkgrel=2
pkgdesc="WebP library and conversion tools"
arch=('x86_64' 'i686')
url="http://code.google.com/intl/en/speed/webp/"
license=('BSD')
depends=('libpng' 'libjpeg')
options=('!libtool')
source=("http://webp.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha256sums=('40b18456e39b2822311b0ea161d6f09507cf5e8e937b02b9be3ee994ded67aba')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
