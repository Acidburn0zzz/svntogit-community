# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sybil
pkgver=2.0.0
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('c441619db2404175e04b22c754d449095c219e5b6e97ed1411941797b8ac554980eb2a77371a84bb005e9497d42e42ffecc2ee3c34f2098e5c2fd220c3398895')

build() {
  cd sybil-$pkgver
  python setup.py build
}

check() {
  cd sybil-$pkgver
  python -m pytest
}

package() {
  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
