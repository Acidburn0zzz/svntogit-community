# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sybil
pkgver=1.2.2
pkgrel=1
pkgdesc='Automated testing for the examples in your documentation.'
arch=('any')
license=('MIT')
url='https://github.com/cjw296/sybil'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cjw296/sybil/archive/$pkgver.tar.gz")
sha512sums=('82c068e6ea2d03639cab78bb5bf37e69a2c6f1a51dc6f56d5fdcd904ebf05aaec002343267750385c6d0f74668189bd15d73fdfd1dd335d24909738448b7203f')

build() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/sybil-$pkgver
  python setup.py pytest
}

package() {
  cd sybil-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
