# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: fana-m <geminin@gmx.net>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Manuel Gaul <inkaine@hotmail.com>

pkgname=gigi
pkgver=6141
pkgrel=1
pkgdesc='Small, efficient and feature rich GUI for OpenGL and C++ (freeorion fork)'
#url='http://gigi.sourceforge.net/'
url='http://freeorion.svn.sourceforge.net/viewvc/freeorion/trunk/FreeOrion/GG/'
arch=('x86_64' 'i686')
license=('LGPL')
depends=('boost' 'libpng' 'libtiff' 'ogre' 'sdl' 'python')
makedepends=('subversion' 'cmake' 'setconf' 'doxygen' 'mesa-libgl')
#https://gigi.svn.sourceforge.net/svnroot/gigi/trunk/GG/
source=("$pkgname::svn+https://freeorion.svn.sourceforge.net/svnroot/freeorion/trunk/FreeOrion/GG/#revision=6141") # 5780 # 5407
sha256sums=('SKIP')

pkgver() {
  cd "$pkgname"

  svnversion | tr -d [A-z]
}

build() {
  cd "$srcdir/$pkgname"

  msg2 'Configuring...'
  setconf cmake/GiGiOgre.pc.in prefix /usr
  setconf cmake/GiGi.pc.in prefix /usr
  setconf cmake/GiGiSDL.pc.in prefix /usr
  # It would be better if CMAKE_INSTALL_PREFIX or the resulting Makefiles
  # could be re-configured in the package() function, but this works too.
  cmake \
    -D CMAKE_INSTALL_PREFIX="$srcdir/pkg/usr" \
    -D BUILD_TUTORIALS=OFF \
    -D CPACK_GENERATOR=GiGiDevel \
    -D CMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
    .

  #msg2 'Building documentation...'
  #cd doc
  #doxygen
  #cd ..

  msg2 'Compiling...'
  make -j1
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Packaging files...'
  make install

  msg2 'Moving files to the right location...'
  mv -v "$srcdir/pkg/usr" "$pkgdir/"

  #msg2 'Fixing documentation...'
  #mkdir -p "$pkgdir/usr/share/doc"
  #mv "$pkgdir/usr/doc" "$pkgdir/usr/share/doc/$pkgname"

  #for f in `find "$pkgdir/usr/share/doc" -name "dir_*.html"`; do
  #  sed -i "s:$srcdir:/:g" "$f"
  #done

  msg2 'Python 3 fix...'
  for f in `find "$pkgdir/usr" -name "*.py"`; do
    sed -i "s:/env python:/python3:g" "$f"
  done

  msg2 'Packaging license...'
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
