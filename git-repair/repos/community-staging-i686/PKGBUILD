# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=git-repair
pkgver=1.20161118
pkgrel=4
pkgdesc="Repairs a damaged git repisitory"
url="http://git-repair.branchable.com/"
license=("GPL")
arch=('i686' 'x86_64')
depends=('git')
makedepends=("ghc=8.0.1" "haskell-async" "haskell-data-default" "haskell-exceptions"
             "haskell-hslogger" "haskell-ifelse" "haskell-missingh" "haskell-mtl" "haskell-network"
             "haskell-network-uri" "haskell-optparse-applicative" "haskell-quickcheck"
             "haskell-text" "haskell-unix-compat" "haskell-utf8-string")
source=("$pkgname::git://git-repair.branchable.com/#tag=$pkgver")
sha256sums=('SKIP')

build() {
  cd git-repair
  
  runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/$pkgname" \
    -fnetwork-uri
  runhaskell Setup build
}

package() {
  cd git-repair
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 git-repair.1 "$pkgdir"/usr/share/man/man1/git-repair.1

  rm "$pkgdir"/usr/share/doc/git-repair/GPL
  rmdir "$pkgdir"/usr/share/doc/git-repair "$pkgdir"/usr/share/doc
}
