# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Andrew Steinke <rkcf@rkcf.me>

pkgname=python-astunparse
_pkgname=astunparse
pkgrel=2
pkgver=1.6.2
pkgdesc="An AST unparser for Python"
depends=('python' 'python-six')
makedepends=('python-setuptools' 'python-wheel')
url="https://github.com/simonpercivall/astunparse"
license=('python')
arch=('any')
source=("$pkgname-$pkgver.tar.gz::https://github.com/simonpercivall/astunparse/archive/v$pkgver.tar.gz")
sha512sums=('b1913f9e9a11154c0f802c3b93fcfe456012e90e3371924f717c07430fc50336cff8b02d125c622e1696a2632d49ec9a8ff0e1ed94c609579a9c2eb941f4facb')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
