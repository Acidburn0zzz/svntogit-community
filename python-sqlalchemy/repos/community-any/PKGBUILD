# $Id$ 
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
pkgbase="python-sqlalchemy"
pkgname=('python-sqlalchemy' 'python2-sqlalchemy')
pkgver=0.6.6
pkgdesc="Python SQL toolkit and Object Relational Mapper"
pkgrel=1
arch=('any')
url="http://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python' 'python2' 'python-distribute' 'python2-distribute')
source=(http://downloads.sourceforge.net/sourceforge/sqlalchemy/SQLAlchemy-$pkgver.tar.gz)
md5sums=('359f02242c52e92aa881c36c8e3720d8')

package_python-sqlalchemy() {
  depends=('python')
  cd ${srcdir}/SQLAlchemy-${pkgver}
  python setup.py install --root=${pkgdir}
  install -D -m644 LICENSE \
	${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-sqlalchemy() {
  depends=('python2')
  rm -rf ${srcdir}/SQLAlchemy-${pkgver}
  cd ${srcdir}
  tar xvzf SQLAlchemy-${pkgver}.tar.gz
  cd ${srcdir}/SQLAlchemy-${pkgver}
  python2 setup.py install --root=${pkgdir}
  install -D -m644 LICENSE \
	${pkgdir}/usr/share/licenses/python2-sqlalchemy/LICENSE
}
