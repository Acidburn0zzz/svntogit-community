# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-sqlalchemy
pkgname=('python-sqlalchemy' 'python2-sqlalchemy')
pkgver=1.3.9
pkgrel=3
arch=('x86_64') # python2 package contain .so
url="https://www.sqlalchemy.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-xdist' 'python2-pytest-xdist' 'python-mock' 'python2-mock')
source=("https://pypi.io/packages/source/S/SQLAlchemy/SQLAlchemy-$pkgver.tar.gz"{,.asc}
        'sqlite-3.30.patch')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1')
sha512sums=('7c4b7fec7cb754c33ff693df6b1d429f9756e3446b56d7244a0e96232e9ca2557064055c40c65119db99b17579f178a169bb07ed83c0f353027bc18ac8247c2a'
            'SKIP'
            '8760bd3a603639d84a2664114a982ba5fd41c9fdb31157fae10e602d9fd9b83a55d67a292a137ea67aaeb90dd096c59dc8dd65f71aef5f2de04bed16c6d95b53')

prepare() {
  # https://github.com/sqlalchemy/sqlalchemy/pull/4921
  patch -Np1 -d SQLAlchemy-$pkgver <sqlite-3.30.patch

  cp -a SQLAlchemy-$pkgver SQLAlchemy2-$pkgver
}

build() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py build

  cd "$srcdir"/SQLAlchemy2-$pkgver
  python2 setup.py build
}

check() {
  cd "$srcdir"/SQLAlchemy-$pkgver
  python setup.py pytest
 
  cd "$srcdir"/SQLAlchemy2-$pkgver  
  python2 setup.py pytest
}

package_python-sqlalchemy() {
  pkgdesc='Python SQL toolkit and Object Relational Mapper'
  depends=('python')
  optdepends=('python-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy-$pkgver
  python setup.py install --root="${pkgdir}"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-sqlalchemy() {
  pkgdesc='Python 2 SQL toolkit and Object Relational Mapper'
  depends=('python2')
  optdepends=('python2-psycopg2: connect to PostgreSQL database')

  cd SQLAlchemy2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m644 LICENSE \
	  "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 ft=sh et:
