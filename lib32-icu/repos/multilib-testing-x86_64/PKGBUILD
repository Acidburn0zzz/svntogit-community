# Maintainer: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-icu
pkgver=61.1
pkgrel=1
pkgdesc="International Components for Unicode library (32 bit)"
arch=('x86_64')
url="http://www.icu-project.org/"
license=('custom:icu')
depends=('lib32-gcc-libs' 'icu')
makedepends=('gcc-multilib')
source=(https://ssl.icu-project.org/files/icu4c/${pkgver}/icu4c-${pkgver//./_}-src.tgz{,.asc})
sha256sums=('d007f89ae8a2543a53525c74359b65b36412fa84b3349f1400be6dcf409fafef'
            'SKIP')
validpgpkeys=('9731166CD8E23A83BEE7C6D3ACA5DBE1FD8FABF1') #  "Steven R. Loomis (ICU Project) <srl@icu-project.org>"
validpgpkeys+=('FFA9129A180D765B7A5BEA1C9B432B27D1BA20D7') # "Fredrik Roubert <fredrik@roubert.name>"

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd icu/source

  ./configure --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man --libdir=/usr/lib32

  make
}

check() {
  cd "$srcdir/icu/source"
  # https://bugs.archlinux.org/task/55157
  make -k check || true
}

package() {
  cd icu/source

  make -j1 DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr"/{include,sbin,share}

  # keep icu-config-32
  find "${pkgdir}/usr/bin" -type f -not -name icu-config -delete
  mv "${pkgdir}/usr/bin"/icu-config{,-32}

  install -d m644 "${pkgdir}/usr/share/licenses"
  ln -s icu "${pkgdir}/usr/share/licenses/${pkgname}"
}

