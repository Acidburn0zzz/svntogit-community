# Maintainer:  Anatol Pomozov <anatol@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Clément Démoulins <clement@archivel.fr>
# Contributor: David Rosenstrauch <darose@darose.net>
# Contributor: Erik Mank <erik@braindisorder.org>

pkgname=tt-rss
pkgver=r11216.56fd06d61
pkgrel=1
epoch=2
pkgdesc='Web-based news feed (RSS/Atom) aggregator'
arch=(any)
url='https://tt-rss.org'
license=(GPL)
backup=(etc/webapps/tt-rss/config.php)
depends=(php php-intl)
install=tt-rss.install
optdepends=('mysql'
            'postgresql'
            'php-gd: for coloured feed badges')
makedepends=('git')
_commit=56fd06d611d7bd4896f886f3e20ac778f47771cb
# periodic releases are over https://tt-rss.org/forum/viewtopic.php?f=10&t=3262
source=(git+https://git.tt-rss.org/fox/tt-rss.git#commit=$_commit
       php81.patch::https://git.tt-rss.org/fox/tt-rss/commit/a201e10ee024f03fb6aad0ec1069cc4da72a9ac5.patch
       tt-rss.service)
sha1sums=('SKIP'
          'cd44113bcb2efeccde3c2ae1e8272900f95bc96c'
          'd764e22065f8e0104b4f1d5dcf41cce01cd7bec5')

pkgver() {
  cd tt-rss
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd tt-rss
  patch -p1 -R < ../php81.patch # https://bugs.archlinux.org/task/73451
}

package() {
  cd tt-rss
  _instdir="$pkgdir"/usr/share/webapps/$pkgname

  install -d "$_instdir"
  cp -ra * "$_instdir/"

  echo $pkgver > "$_instdir/version_static.txt"

  install -Dm640 -g 33 config.php-dist "$pkgdir"/etc/webapps/tt-rss/config.php
  ln -s /etc/webapps/tt-rss/config.php "$_instdir"/config.php

  install -d "$pkgdir"/var/lib/tt-rss
  mv "$_instdir"/{lock,feed-icons,cache} "$pkgdir"/var/lib/tt-rss
  ln -s /var/lib/tt-rss/lock "$_instdir"/lock
  ln -s /var/lib/tt-rss/feed-icons "$_instdir"/feed-icons
  ln -s /var/lib/tt-rss/cache "$_instdir"/cache
  chown -R 33:33 "$pkgdir"/var/lib/tt-rss
  chmod -R 777 "$pkgdir"/var/lib/tt-rss/*

  install -Dm644 "$srcdir"/tt-rss.service "$pkgdir"/usr/lib/systemd/system/tt-rss.service
}
