# $Id$
# Maintainer: Biru Ionut <ionut@archlinux.ro
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=2.12.2
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
options=('!libtool')
# uses built-in libtasn1 for the moment
depends=(lib32-{gcc-libs,readline,libgcrypt,zlib})
makedepends=('gcc-multilib')
source=(ftp://ftp.gnu.org/gnu/gnutls/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('ca6903d99b2aca2a6a326bc8df805b61')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --with-libgcrypt \
    --disable-static\
    --disable-guile
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
