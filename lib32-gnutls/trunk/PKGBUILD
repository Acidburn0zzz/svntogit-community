# $Id$
# Maintainer: Biru Ionut <ionut@archlinux.ro
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=gnutls
pkgname=lib32-$_pkgbasename
pkgver=3.0.5
pkgrel=1
pkgdesc="A library which provides a secure layer over a reliable transport layer (32-bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/gnutls/"
options=('!libtool')
depends=(lib32-{gcc-libs,readline,libgcrypt,zlib,nettle})
makedepends=('gcc-multilib')
source=(ftp://ftp.gnu.org/gnu/gnutls/${_pkgbasename}-${pkgver}.tar.xz)
md5sums=('c2d4e6344974545c228a1df2147bf334')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-zlib \
    --disable-static \
    --disable-guile \
    --disable-valgrind-tests
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  find $pkgdir

  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
