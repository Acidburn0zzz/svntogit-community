# Maintainer: David Runge <dvzrv@archlinux.org>

_name=calmjs.parse
pkgname=python-calmjs.parse
pkgver=1.2.1
pkgrel=1
pkgdesc="Various parsers for ECMA standards"
arch=('any')
url="https://github.com/calmjs/calmjs.parse"
license=('MIT')
depends=('python-ply')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# LICENSE missing in pypi sdist:
# https://github.com/calmjs/calmjs.parse/issues/27
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip"
        "https://raw.githubusercontent.com/calmjs/calmjs.parse/d4b19c417b35dcb2c184aca0428e26ddabf4f7b2/LICENSE")
sha512sums=('353861a0d08e0eb4de768556730c83def9af246a2ba31842a77599899bd4138073aac32317c2e1fdb623121349e4600e28b64ca0000959089888b133a460ef06'
            '6009d8a9c57655f9c678b8614a284cd1f3a63a67c557aaed4d362ac4022974a4019ddf7bbf92e8fea8adf1b20374cd2916f00319fc0b895ca27e11d20276cfd7')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
