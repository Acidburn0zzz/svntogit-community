# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-tool-kit
pkgver=0.3.4
pkgrel=1
pkgdesc='Base development tool of all C++/Qt Developer work on Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-tool-kit"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-x11extras' 'startup-notification' 'dtksettings' 'gsettings-qt')
makedepends=('qt5-tools')
replaces=('libdui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-tool-kit/archive/$pkgver.tar.gz")
sha512sums=('4f62993fb6e26fa6a7181a48007e40a9b07f6dafd2b0548a95f035582a4d2ad345507c250eb0a01674f9abda82819020d194580b1b966fe7ebe5d9ca0a9d2fdf')

build() {
  cd deepin-tool-kit-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-tool-kit-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
