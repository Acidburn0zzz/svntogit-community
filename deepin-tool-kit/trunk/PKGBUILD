# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-tool-kit
pkgver=0.2.10.20170613
_commit=e94d50ae842ccb6f68a8e79e298a7976d7670d86
pkgrel=1
pkgdesc='Base development tool of all C++/Qt Developer work on Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-tool-kit"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-x11extras' 'startup-notification' 'dtksettings' 'gsettings-qt')
makedepends=('qt5-tools')
replaces=('libdui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-tool-kit/archive/$_commit.tar.gz")
sha512sums=('b7a4199118ddc192954c8cf81f8e4a8f767778e8a33fd19220ffe5ef547454603cbd338a3de6ccd74b9bf0df9e9f243e5b1b6b231090e1bac854e504c34a5273')

prepare() {
  mv deepin-tool-kit-{$_commit,$pkgver}
}

build() {
  cd deepin-tool-kit-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-tool-kit-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
