# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-tool-kit
pkgver=0.3.2
pkgrel=1
pkgdesc='Base development tool of all C++/Qt Developer work on Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-tool-kit"
license=('GPL3')
depends=('qt5-multimedia' 'qt5-x11extras' 'startup-notification' 'dtksettings' 'gsettings-qt')
makedepends=('qt5-tools')
replaces=('libdui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-tool-kit/archive/$pkgver.tar.gz")
sha512sums=('f3a81325a80c11d012f9d9f6018543b1b983688eb3315edb175269c16966147ca4aaec0afe3e7ea4f960ba178210986a952e423c5d7a7c44c7d6cd83a1cd77ac')

build() {
  cd deepin-tool-kit-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-tool-kit-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
