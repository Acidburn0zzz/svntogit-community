# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: said <atvordhosbn@gmail.com>
# Contributor: clinx <clinx.hacker@googlemail.com>

pkgname=rbutil
pkgver=1.3.1
pkgrel=3
pkgdesc="Rockbox Utility"
arch=('i686' 'x86_64')
url="http://www.rockbox.org/twiki/bin/view/Main/RockboxUtility"
license=("GPL")
depends=('qt4' 'libusb')
optdepends=('speex: voice files support')
source=("http://download.rockbox.org/${pkgname}/source/RockboxUtility-v${pkgver}-src.tar.bz2" "${pkgname}.desktop")
sha256sums=('6c04b5c7eaad1762577908dedb9e40f5b0cee675150ae5ba844ea2c9bea294ca'
            '539285237af16fbc60a7501963e8c2f584e58a31f2613481e6b42d21337b7a25')

build() {
  cd "${srcdir}"/RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

# Build
  /usr/lib/qt4/bin/lrelease rbutilqt.pro
  qmake-qt4
  make
}

package(){
  cd "${srcdir}"/RockboxUtility-v${pkgver}/${pkgname}/rbutilqt

# Install
  install -dm 755 "${pkgdir}"/usr/{bin,share/pixmaps}
  install -m 644 icons/rockbox-64.png "${pkgdir}"/usr/share/pixmaps/rbutil.png
  install -m 755 RockboxUtility "${pkgdir}"/usr/bin/rbutil

# Desktop file
  install -dm 755 "${pkgdir}"/usr/share/applications
  install -m 644 "${srcdir}"/rbutil.desktop "${pkgdir}"/usr/share/applications/rbutil.desktop
}

#vim: ts=2 sw=2 et:
