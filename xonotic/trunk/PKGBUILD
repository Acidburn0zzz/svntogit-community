# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Alexander Suhoverhov <cy at ngs dot ru>
pkgname=xonotic
pkgver=0.6.0
pkgrel=2
pkgdesc="Free, fast-paced crossplatform first-person shooter"
arch=('i686' 'x86_64')
url="http://xonotic.org"
license=('GPL')
depends=('alsa-lib' 'curl' 'libjpeg>=8' 'libmodplug' 'libvorbis' 'libxpm' 'libxxf86dga' 'libxxf86vm' 'sdl' 'libpng>=1.4.0' 'xonotic-data')
makedepends=('unzip' 'mesa' 'setconf' 'gendesk')
source=("http://dl.xonotic.org/xonotic-${pkgver}-source.zip")
md5sums=('47e8468054bf1346c57f3f8fdc97575e')
_genericname=('First person shooter')

build() {
  cd "$srcdir"

  # Generate xonotic-glx.desktop and xonotic-sdl.desktop
  gendesk -n
  cp $pkgname.desktop $pkgname-sdl.desktop
  cp $pkgname.desktop $pkgname-glx.desktop
  setconf $pkgname-sdl.desktop Name 'Xonotic (SDL)'
  setconf $pkgname-sdl.desktop Exec xonotic-sdl
  setconf $pkgname-glx.desktop Name 'Xonotic (GLX)'
  setconf $pkgname-glx.desktop Exec xonotic-glx

  # compile Xonotic-flavored fteqcc
  #make -C Xonotic/source/fteqcc

  # compile QuakeC game code
  #( cd Xonotic/source/qcsrc/server && ../../fteqcc/fteqcc.bin -O3 -Ono-c -Ono-cs )
  #( cd Xonotic/source/qcsrc/client && ../../fteqcc/fteqcc.bin -O3 -Ono-c -Ono-cs )
  #( cd Xonotic/source/qcsrc/menu && ../../fteqcc/fteqcc.bin -O3 -Ono-c -Ono-cs )

  # compile engine
	make -C Xonotic/source/darkplaces CPUOPTIMIZATIONS="${CFLAGS}" DP_FS_BASEDIR=/usr/share/xonotic/ DP_LINK_TO_LIBJPEG=1 cl-release
	make -C Xonotic/source/darkplaces CPUOPTIMIZATIONS="${CFLAGS}" DP_FS_BASEDIR=/usr/share/xonotic/ DP_LINK_TO_LIBJPEG=1 sdl-release
	make -C Xonotic/source/darkplaces CPUOPTIMIZATIONS="${CFLAGS}" DP_FS_BASEDIR=/usr/share/xonotic/ DP_LINK_TO_LIBJPEG=1 sv-release
}

package() {
	cd $srcdir/Xonotic

	# binaries
	install -Dm755 source/darkplaces/darkplaces-dedicated $pkgdir/usr/bin/xonotic-dedicated
	install -Dm755 source/darkplaces/darkplaces-glx $pkgdir/usr/bin/xonotic-glx
	install -Dm755 source/darkplaces/darkplaces-sdl $pkgdir/usr/bin/xonotic-sdl

	# convenience files
	mkdir -p $pkgdir/usr/share/applications
	install -Dm644 $srcdir/$pkgname-sdl.desktop -t $pkgdir/usr/share/applications
	install -Dm644 $srcdir/$pkgname-glx.desktop -t $pkgdir/usr/share/applications
	install -Dm644 $srcdir/Xonotic/misc/logos/icons_png/xonotic_512.png $pkgdir/usr/share/pixmaps/$pkgname.png
}

# vim: ts=2:sw=2 et:
