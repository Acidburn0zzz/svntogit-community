# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client
pkgver=4.0.0
pkgrel=4
pkgdesc="Ubuntu One helps you store, sync and share files between your computers"
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client"
license=('GPL')
depends=('dbus-glib' 'python2-configglue' 'python2-distribute' 'python2-gobject' 'libnotify' 'python2-gobject2' 'python2-pyinotify' 'python2-simplejson' 'ubuntu-sso-client' 'python2-ubuntuone-storageprotocol' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'imake')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client/stable-4-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        1339_1338.diff
        fix-notify-hint.patch
        lp1093111.patch)
md5sums=('738039703d4dcf54518a725f8ffbc4de'
         'e82e80c229de06692988c1f938a34fb9'
         '54dfeb2be011f0a12ea7cfa0f0c19bec'
         'ca79cb558231639e28be7276017ae91c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  # Improve TransfersMenu declaration to avoid problems
  patch -Np0 -i "$srcdir/1339_1338.diff"

  # Workaround for crash in notification-daemon
  # https://bugzilla.gnome.org/show_bug.cgi?id=665166
  patch -Np1 -i "$srcdir/fix-notify-hint.patch"

  # Fix compatibility with glibc 2.17
  # https://bugs.launchpad.net/ubuntuone-client/+bug/1093111
  patch -Np1 -i "$srcdir/lp1093111.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}
