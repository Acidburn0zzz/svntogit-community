# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-client
pkgver=4.0.0
pkgrel=2
pkgdesc='Ubuntu One helps you store, sync and share files between your computers'
arch=('i686' 'x86_64')
url="https://launchpad.net/ubuntuone-client"
license=('GPL')
depends=('dbus-glib' 'python2-configglue' 'python2-distribute' 'libnotify' 'python2-gobject2' 'python2-pyinotify' 'python2-simplejson' 'ubuntu-sso-client' 'python2-ubuntuone-storageprotocol' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'imake' 'gobject-introspection')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/ubuntuone-client/stable-4-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        1339_1338.diff
        fix-notify-hint.patch)
md5sums=('738039703d4dcf54518a725f8ffbc4de'
         'e82e80c229de06692988c1f938a34fb9'
         '54dfeb2be011f0a12ea7cfa0f0c19bec')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  # Improve TransfersMenu declaration to avoid problems
  patch -Np0 -i "$srcdir/1339_1338.diff"

  # Workaround for crash in notification-daemon
  # https://bugzilla.gnome.org/show_bug.cgi?id=665166
  patch -Np1 -i "$srcdir/fix-notify-hint.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # We don't have Apport in Arch Linux
  rm -r "$pkgdir"/{etc/apport,usr/share/apport}
}
