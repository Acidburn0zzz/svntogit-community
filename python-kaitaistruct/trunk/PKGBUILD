# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-kaitaistruct
pkgname=('python-kaitaistruct' 'python2-kaitaistruct')
pkgver=0.6
pkgrel=1
pkgdesc="Kaitai Struct declarative parser generator for binary data: runtime library for Python"
arch=('any')
license=('MIT')
url="http://kaitai.io"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
source=("git+https://github.com/kaitai-io/kaitai_struct_python_runtime.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a kaitai_struct_python_runtime{,-py2}
}

build() {
  cd "$srcdir"/kaitai_struct_python_runtime
  python setup.py build

  cd "$srcdir"/kaitai_struct_python_runtime-py2
  python2 setup.py build
}

package_python-kaitaistruct() {
  depends=('python')

  cd kaitai_struct_python_runtime
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-kaitaistruct() {
  depends=('python2')

  cd kaitai_struct_python_runtime-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
