# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=drawing
pkgver=0.2
pkgrel=1
pkgdesc="Drawing application for the GNOME desktop"
url="https://github.com/maoschanz/drawing"
arch=(any)
license=(GPL3)
depends=(gtk3 python-cairo python-gobject)
makedepends=(meson)
source=($pkgname-$pkgver.tar.gz::https://github.com/maoschanz/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('83873fcda8102f51c9e5ecd17e95086d0e9ac8196bcf9e202d6e30c0c926fa35')

prepare() {
  cd $pkgname-$pkgver
  sed -i "s/'datadir'), 'appdata')/'datadir'), 'metainfo')/" data/meson.build
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
