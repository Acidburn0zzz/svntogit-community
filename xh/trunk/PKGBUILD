# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=xh
pkgver=0.7.0
pkgrel=1
pkgdesc="Yet another HTTPie clone"
arch=('x86_64')
url="https://github.com/ducaale/xh"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
replaces=('ht-rs' 'ht')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('428edefbb58ffdb10a731ef0f1bb15d77bb90434f3ea72b41f8e4870ac74ec49')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
