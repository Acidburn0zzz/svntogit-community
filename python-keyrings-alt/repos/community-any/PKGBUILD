# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Johannes Dewender  arch at JonnyJD dot net
# Contributor: Ivan Sichmann Freitas <ivansichfreitas@gmail.com>
# Contributor: Brice Maron <brice@bmaron.net>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>
# Contributor: Steven Allen <steven {at} stebalien {dot} com>

pkgbase=python-keyrings-alt
_pkgname=keyrings.alt
pkgname=(python-keyrings-alt python2-keyrings-alt)
epoch=1
pkgver=3.1.1
pkgrel=1
pkgdesc="Alternate keyring implementations"
arch=('any')
url="https://github.com/jaraco/keyrings.alt"
license=('MIT')
makedepends=('python-setuptools_scm' 'python2-setuptools')
checkdepends=('libgnome-keyring' 'python2-backports.unittest_mock'
              'python-pycryptodome' 'python-gobject' 'python-pytest-flake8' 'python-pytest-runner' 'python-keyring'
              'python2-pycryptodome' 'python2-gobject' 'python2-pytest-flake8' 'python2-pytest-runner' 'python2-keyring')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('0ec460620d684c83477f5a5810f8d01058cf251bf9ef34af1afcca0c663fb9025a94ae7785ac4bc6982eb88f44e103f7821c9756a359b2af04800752d3e1fc0e')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  echo -e '\nflake8-ignore = W191 W503 W504' >> keyrings.alt-$pkgver/pytest.ini

  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  echo "Running tests on python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py pytest

  echo "Running tests on python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py pytest
}

package_python-keyrings-alt() {
  depends=('python-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python-gobject: GNOME Integration'
              'python-fs'
              'python-crypto: Encrypted file storage')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-keyrings-alt() {
  depends=('python2-keyring')
  optdepends=('libgnome-keyring: GNOME Integration'
              'python2-gobject: GNOME Integration'
              'python2-fs'
              'python2-crypto: Encrypted file storage')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
