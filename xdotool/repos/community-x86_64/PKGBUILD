# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Rttommy <rttommy@gmail.com>

pkgname=xdotool
pkgver=3.20210804.1
pkgrel=1
pkgdesc="Command-line X11 automation tool"
arch=('x86_64')
url="https://www.semicomplete.com/projects/xdotool/"
license=('BSD')
depends=('libxtst' 'libxinerama' 'libxkbcommon')
source=(https://github.com/jordansissel/xdotool/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        libxdo.pc-$pkgver)
sha256sums=('0dbdfc662c981c2cac31fa8c23d1aa680f1726c50511509ce98396b27a030201'
            'b79715999168c68abffa2cb554caf62821fe7d40cc4ef3ceca83cfeb56ce8776')

prepare() {
  cd $pkgname-$pkgver

  # https://github.com/jordansissel/xdotool/pull/345
  mv ../libxdo.pc-$pkgver libxdo.pc
}

build() {
  cd $pkgname-$pkgver
  make WITHOUT_RPATH_FIX=1
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" INSTALLMAN="$pkgdir/usr/share/man" install
  install -Dm644 COPYRIGHT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
