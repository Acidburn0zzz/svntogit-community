# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=fcitx-libpinyin
pkgver=0.4.1
pkgrel=1
pkgdesc="Fcitx Wrapper for libpinyin, Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-libpinyin"
license=('GPL')
depends=('fcitx' 'libpinyin' 'fcitx-qt4' 'qtwebkit')
makedepends=('cmake' 'intltool' 'wget')
source=("http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}_dict.tar.xz")
sha512sums=('61c11b099f81b76a78b114762a182b74eee44fa8e0a260b8b654cccd08d5f5f7a7ac9e8f61ebcfa99e3f216c6cb0b4586b7528ae4265ea196e00a2ab2d5953f0')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
