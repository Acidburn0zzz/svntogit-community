# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=fcitx-libpinyin
pkgver=0.4.1
pkgrel=2
pkgdesc="Fcitx Wrapper for libpinyin, Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-libpinyin"
license=('GPL')
depends=('fcitx' 'libpinyin' 'fcitx-qt5' 'qt5-webengine')
makedepends=('cmake' 'intltool' 'wget')
source=("http://download.fcitx-im.org/$pkgname/$pkgname-${pkgver}_dict.tar.xz"
        "qt5-port.patch")
sha512sums=('61c11b099f81b76a78b114762a182b74eee44fa8e0a260b8b654cccd08d5f5f7a7ac9e8f61ebcfa99e3f216c6cb0b4586b7528ae4265ea196e00a2ab2d5953f0'
            'c39a179aab4af71f8d773a5e4d02c8810239165a22cf0b5ff3e518c65a8eb9c3f3ddff7dcd23d19f4fcc3ed6cb545169952839cd44a892012f5fd1a1174d5b4b')

prepare() {
  mkdir build

  cd $pkgname-$pkgver

  # Port to Qt5
  patch -Np1 -i ../qt5-port.patch
}

build() {
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
