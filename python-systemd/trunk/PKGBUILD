# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com
pkgbase=python-systemd
pkgname=(python-systemd python2-systemd)
pkgver=230
pkgrel=1
pkgdesc="Python bindings for systemd"
arch=('x86_64' 'i686')
url="https://github.com/systemd/python-systemd"
license=('GPL')
makedepends=('python-setuptools' 'python2-setuptools' "systemd")
source=("$url/archive/v$pkgver.tar.gz")
md5sums=('6902e5588397c796c85febc8334d434b')

prepare() {
    sed -i 's:include/:include:' "$pkgname-$pkgver/Makefile"
    cp -a "$pkgname-$pkgver" "python2-systemd-$pkgver"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make systemd/id128-constants.h
    python3 setup.py build
    cd "$srcdir/python2-systemd-$pkgver"
    make systemd/id128-constants.h
    python2 setup.py build
}

package_python2-systemd(){
    depends+=("python2" "systemd")
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir"
}

package_python-systemd(){
    depends+=("python" "systemd")
    cd "$srcdir/python2-systemd-$pkgver"
    python3 setup.py install --root="$pkgdir"
}
