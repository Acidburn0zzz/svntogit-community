# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=python-plumbum
_name=plumbum
pkgver=1.7.0
pkgrel=2
pkgdesc='A small yet feature-rich Python library for shell script-like programs, and more'
url='https://github.com/tomerfiliba/plumbum'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(https://github.com/tomerfiliba/${_name}/archive/v${pkgver}/${_name}-${pkgver}.tar.gz)
sha512sums=('39b97147b9e4d5774e0bf876599e5fbaea904a003f2397a845b8b0623539d08d32fbe1469e99b3c373ebb65a5c083ef896e1088bfa69309106301605db4d9a31')
b2sums=('7c501937243e143e5aa9a473b09d904f032a977a50c63079fbb5447bcc4d2ef027bec4e5dbc5ce4edfe841d1e35007ff9cc8b0804a9f744fbef0d56888253823')



build() {
  cd "${_name}-${pkgver}"
  export PYTHONHASHSEED=0
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
