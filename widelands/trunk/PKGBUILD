# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <rabyte*gmail>

pkgname=widelands
pkgver=20
_realver=build${pkgver}
pkgrel=1
pkgdesc="A realtime strategy game with emphasis on economy and transport"
arch=('x86_64')
url="http://widelands.org/"
license=('GPL')
depends=('sdl2_mixer' 'sdl2_image' 'sdl2_net' 'sdl2_ttf' 'sdl2_gfx' 'lua' 'glew' 'python' 'boost-libs')
makedepends=('cmake' 'boost' 'mesa' 'ninja')
replaces=('widelands-data')
source=("https://launchpad.net/widelands/${_realver}/build${pkgver}/+download/$pkgname-$_realver.tar.bz2"
        $pkgname.desktop
        $pkgname.png)
sha512sums=('25280db6a57adeb37d13db35ce9eb41a5478534b04772240f6228c180f5a41a55fc414dab66e724945442985cbbba7a8a59a582291aca176e0bd8ae6c08760e5'
            'ef7e54ffba11a2fe54d2cafc0a12fcbe5028e861b9b980f549317b159c8532ee62423af8724b05cfd344017097e022215fa750f4ca9afffa57703d434017d727'
            'f7db9d7584ec8431f2f1857baddeb32cd3ae182343e7f0369e61fb444514bd9c2e6a3931ae34f24cc994840dffbbe4451f33845798f3d582f41b31f451b261ac')

build() {
    cd $srcdir/$pkgname-$_realver

    sed -i "s/CMP0072 NEW/CMP0072 OLD/g" CMakeLists.txt

    mkdir -p build/compile && cd build/compile

    cmake -G Ninja ../.. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWL_INSTALL_BASEDIR=/usr \
        -DWL_INSTALL_DATADIR=/usr/share/widelands
    ninja
}

package() {
    cd $srcdir/$pkgname-$_realver/build/compile

    DESTDIR="$pkgdir" ninja install

    mkdir -p $pkgdir/usr/bin
    mv $pkgdir/usr/{widelands,wl_map_object_info,wl_map_info} $pkgdir/usr/bin
    mv $pkgdir/usr/{VERSION,ChangeLog,CREDITS} $pkgdir/usr/share/widelands

    mkdir -p $pkgdir/usr/share/licenses/$pkgname
    mv $pkgdir/usr/COPYING $pkgdir/usr/share/licenses/$pkgname

    # Install bin, icon and desktop file
    install -Dm644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps/$pkgname.png
    install -Dm644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications/$pkgname.desktop
}
