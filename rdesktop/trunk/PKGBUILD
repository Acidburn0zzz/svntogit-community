# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.5
pkgrel=1
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('x86_64')
url="https://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite' 'libgssglue')
source=(https://github.com/rdesktop/rdesktop/archive/v${pkgver}.tar.gz rdesktop-send_physical_buttons.diff)
sha512sums=('303f61f47d258ae30a481f80dff3074c96b1eff48dbaf2dec22824822755e61b59459282aa22bf83e7f1933f5baa267894170d9956475b7a6248ec228e3b1349'
            'a51d3d8a048cc5b67d872e9bfee91bfecd055c4bbf202cca3e768eb51dfd12e30e55ccee5431cec6ba20de818e1f0b94d4fe953f43828ee8b84f1fb7e4ffccec')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
  
  ./bootstrap
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
