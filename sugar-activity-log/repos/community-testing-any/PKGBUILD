# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-log
_pkgname=Log
pkgver=41
pkgrel=2
pkgdesc="Sugar activity to view system log files"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Log"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        sugar-activity-log-gtk-3.24.patch::https://github.com/sugarlabs/log-activity/commit/8db4fd568565a2740bc822b65a485e5947c4dce0.patch)
sha256sums=('b401369fce622c284b6372de14dda735bdf0453584d6cd5b6cd0f991d11a9628'
            '83183087337200eb55f8f40f55c5a401bc90edc5aeb0c41d3f8b2c225c8d883c')

prepare() {
  cd $_pkgname-$pkgver
  # https://github.com/sugarlabs/log-activity/commit/8db4fd568565a2740bc822b65a485e5947c4dce0
  patch -Np1 -i ../sugar-activity-log-gtk-3.24.patch
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
