# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=4.2.0
pkgrel=1
pkgdesc="GTK+ client for Matrix written in Rust"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Fractal"
license=(GPL3)
depends=(gtk3 gst-plugins-base-libs gst-plugins-bad gst-editing-services gtksourceview3 gspell libhandy)
optdepends=('gnome-keyring: password storage (or use any other service implementing org.freedesktop.secrets)')
makedepends=(meson rust)
_md5=0dcc45d8a1f8f286238d7def68531a03
source=("https://gitlab.gnome.org/GNOME/fractal/uploads/${_md5}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('ca73a7b611f763fb05a12c80f6473cc69bf8e7c282a3d9a66216108f9ac40d7f')

build() {
    cd ${pkgname}-${pkgver}
    arch-meson . _build
    ninja -C _build
}

package() {
    cd ${pkgname}-${pkgver}
    DESTDIR="${pkgdir}" ninja -C _build install
}
