# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=fractal
pkgver=4.2.1
pkgrel=1
pkgdesc="GTK+ client for Matrix written in Rust"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Fractal"
license=(GPL3)
depends=(gtk3 gst-plugins-base-libs gst-plugins-bad gst-editing-services gtksourceview4 gspell libhandy)
optdepends=('gnome-keyring: password storage (or use any other service implementing org.freedesktop.secrets)')
makedepends=(meson rust)
_md5=b64b7523a811c152b2de11901cfe699e
source=("https://gitlab.gnome.org/GNOME/fractal/uploads/${_md5}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('57a15d5913f76016617ac4a01e5673b55dcd5abc315a656cf847b060a18b1f38')

build() {
    cd ${pkgname}-${pkgver}
    arch-meson . _build
    ninja -C _build
}

package() {
    cd ${pkgname}-${pkgver}
    DESTDIR="${pkgdir}" ninja -C _build install
}
