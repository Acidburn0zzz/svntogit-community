# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=ropper
_pkgname=Ropper
pkgver=1.8.7
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
url="https://github.com/sashs/Ropper"
arch=('any')
license=('GPL2')
depends=('python-capstone' 'python-filebytes')
optdepends=('sqlite: gadgets database support')
makedepends=('python-setuptools')
checkdepends=('sqlite')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('11b3de7b0cfb009034a49e0d4f97fd3ecfb5ad171d4f7896c6834be7433c6f440e5f570856cc28a00eb7936e42644b96073d75944ef88e3f1df95f3d189d7bfd')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python test.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
}

# vim: ts=2 sw=2 et:
