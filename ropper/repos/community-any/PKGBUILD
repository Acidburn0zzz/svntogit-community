# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=ropper
_pkgname=Ropper
pkgver=1.8.3
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
url="https://github.com/sashs/Ropper"
arch=('any')
license=('GPL2')
depends=('python-capstone' 'python-filebytes')
optdepends=('sqlite: gadgets database support')
makedepends=('python-setuptools')
checkdepends=('sqlite')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('1f116e6425305db3ea2c0312c63fcb3e360d51575c23c8029457477c71110a17c32b59a0a80c8131c169a0b1bee03386ffd1bcce96dbde35a010bf42b986e7cd')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python test.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
}

# vim: ts=2 sw=2 et:
