# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=pdns-recursor
pkgver=3.3
pkgrel=2
pkgdesc="PowerDNS recursor"
url="http://www.powerdns.com"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'lua')
makedepends=('boost')
backup=('etc/powerdns/recursor.conf')
source=("http://downloads.powerdns.com/releases/${pkgname}-${pkgver}.tar.bz2"
        'pdns-recursor.rc')
md5sums=('87daeeebb6f7af9e07814ff6c43300dd'
         '5b3aa3c8ed2edf55821af16cfb7ef252')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  LUA=1 LUA_CPPFLAGS_CONFIG= LUA_LIBS_CONFIG=-llua make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/etc/rc.d"
  mv "${pkgdir}/etc/powerdns/recursor.conf-dist" "${pkgdir}/etc/powerdns/recursor.conf"
  install -m755 "${srcdir}/pdns-recursor.rc" "${pkgdir}/etc/rc.d/pdns-recursor"
  rm -rf "${pkgdir}/etc/init.d"
}
