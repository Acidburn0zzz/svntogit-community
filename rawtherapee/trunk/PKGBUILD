# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky AT gmail.com>
# Contributor: Bogdan Szczurek <thebodzio(at)gmail.com>
# Contributor: Vaclav Kramar <vaclav.kramar@tiscali.cz>
# Contributor: Archie <mymaud@gmail.com>

pkgname=rawtherapee
pkgver=3.1m5
pkgrel=1
pkgdesc="RAW photo editor"
arch=('i686' 'x86_64')
url="http://www.rawtherapee.com/"
license=('GPL')
depends=('bzip2' 'libsigc++' 'gtkmm' 'libiptcdata' 'lcms2' 'desktop-file-utils')
makedepends=('mercurial' 'cmake')
install=rawtherapee.install
source=(rawtherapee.desktop rawtherapee.png)
md5sums=('83f14b57b27f066705b0aec7ad56c53d'
         '4e54e74684971a2f04d9396b9051570e')

_root="https://rawtherapee.googlecode.com/hg/"
_repo="rawtherapee"
#_branch="branch_3.0"
#_changeset="71045bfba97e" # changeset $pkgver

build() {
  cd "$srcdir"
  msg "Connecting to Mercurial server...."

  if [ -d $_repo ] ; then
    cd $_repo
    hg pull
    hg up "Dev-$pkgver"
    msg "The local files are updated."
  else
    hg clone -u "Dev-$pkgver" $_root $_repo
  fi

  msg "Mercurial checkout done or server timeout"
  msg "Starting make..."

  if [ -e "$srcdir/$_repo-build" ]; then
    rm -rf "$srcdir/$_repo-build"
  fi
  mkdir "$srcdir/$_repo-build"
  cd "$srcdir/$_repo-build"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DWITH_MYFILE_MMAP=OFF \
     ../$_repo
  make
}

package() {
  cd "$srcdir/$_repo-build"
  make DESTDIR="$pkgdir/" install

  # remove unneded rtstart script
  # see http://code.google.com/p/rawtherapee/issues/detail?id=184
  rm "$pkgdir"/usr/bin/rtstart

  install -D -m644 "$srcdir"/rawtherapee.desktop "$pkgdir"/usr/share/applications/rawtherapee.desktop
  install -D -m644 "$srcdir"/rawtherapee.png "$pkgdir"/usr/share/pixmaps/rawtherapee.png
}
