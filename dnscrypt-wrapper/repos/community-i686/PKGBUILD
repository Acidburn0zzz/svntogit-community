# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: William Grieshaber <me@zee.li>

pkgname=dnscrypt-wrapper
pkgver=0.2.2
_commit=cc1a16510a951043ae53c2cd3bcd91222c72b582
pkgrel=1
pkgdesc="A server-side dnscrypt proxy"
url="https://dnscrypt.org/"
arch=('x86_64' 'i686')
license=('BSD')
depends=('libsodium' 'libevent')
makedepends=('git')
backup=('etc/default/dnscrypt-wrapper')
install=dnscrypt-wrapper.install
source=("git+https://github.com/cofyc/dnscrypt-wrapper.git#commit=$_commit"
        dnscrypt-wrapper.sysusers)
sha512sums=('SKIP'
            '91dfdf0be4eb0d1f8d29bbfadfef451e09a5eed4464afdb531b67354a1c9357224d46340e4472eb37aaa6c2ba2f1dc685ccaa24dfa7666dd2cb5ac8c0a55b7db')

prepare() {
  cd $pkgname
  sed -i 's|/usr/sbin|/usr/bin|' debian/dnscrypt-wrapper.service
}

build() {
  cd $pkgname
  make configure
  ./configure
  make
}

package() {
  cd $pkgname
  make sbindir="$pkgdir"/usr/bin install

  install -Dm644 debian/dnscrypt-wrapper.service "$pkgdir"/usr/lib/systemd/system/dnscrypt-wrapper.service
  install -Dm644 debian/dnscrypt-wrapper.default "$pkgdir"/etc/default/dnscrypt-wrapper
  install -Dm644 ../dnscrypt-wrapper.sysusers "$pkgdir/usr/lib/sysusers.d/dnscrypt-wrapper.conf"

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
  install -Dm644 debian/dnscrypt-wrapper.8 "$pkgdir"/usr/share/man/man8/dnscrypt-wrapper.8
}

# vim:set ts=2 sw=2 et:
