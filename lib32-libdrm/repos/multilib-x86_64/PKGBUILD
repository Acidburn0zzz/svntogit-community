# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.25
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2
        no-pthread-stubs.patch)
sha1sums=('b950f29cd1c4bb9f1c98a926486a47256b0a4194'
          'af2d9871565ea4cf524873f897691a18b2bba944')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"

  # libtoolize --force
  autoreconf --force --install
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --enable-udev \
      --enable-intel \
      --enable-radeon \
      --enable-vmwgfx-experimental-api \
      --enable-nouveau-experimental-api
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
