# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-terra-pinyin
pkgver=0.0.0.20200207
_commit=492aaf914f9de37cc9d26b846dc693116de70ae8
pkgrel=2
pkgdesc="Terra pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-terra-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-terra-pinyin/archive/$_commit/rime-terra-pinyin-$_commit.tar.gz")
sha512sums=('d15b5a6de6b20c4f50392703c21dded69a985bf25f3d93ee213facfd59e1d5b30d32f0296c6a97ac1edbb5056877591fc1d7637f27f7d273bb7f69636c2b1353')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
