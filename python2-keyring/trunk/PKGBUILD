# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python2-keyring
_pkgname=${pkgname#python2-}
pkgver=13.2.1
pkgrel=2
pkgdesc="Provides an easy way to access the system keyring service from python."
arch=('any')
url="https://github.com/jaraco/keyring"
license=('MIT')
depends=('python2-entrypoints' 'python2-secretstorage')
makedepends=('python2-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/${_pkgname:0:1}/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha512sums=('3c15c3415ba4b657b041d2395536fc92ba656dc71f28669235f13a630946ad1b332d1f5b031a55776ce8a2120d3d2601462708f00ee105f46a27cf043c1bd906')

build() {
  cd $_pkgname-$pkgver

  python2 setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  # Fix the binary name
  mv "$pkgdir"/usr/bin/keyring "$pkgdir"/usr/bin/keyring2

  # Install the license
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
