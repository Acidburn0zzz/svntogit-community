# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgbase='python-ovirt-engine-sdk'
pkgname=('python-ovirt-engine-sdk' 'python2-ovirt-engine-sdk')
pkgver=4.3.0
pkgrel=1
pkgdesc='Python access to the oVirt Engine API'
arch=('x86_64')
url='http://ovirt.org/develop/release-management/features/infra/python-sdk'
makedepends=('python-setuptools' 'python2-setuptools' 'libxml2')
license=('custom')
source=("http://resources.ovirt.org/pub/ovirt-4.3/src/python-ovirt-engine-sdk4/ovirt-engine-sdk-python-${pkgver}.tar.gz")
sha512sums=('73921b7a2506926a708f6bda02c8b52d4da2145438e8e56e0e7718b9a425b703ed503250df94c6f9a22b5154ffaf5470ac289e6a23c5910bc92b2558dcfe433e')

package_python-ovirt-engine-sdk() {
  depends=('python-pycurl' 'python-six')

  cd "${srcdir}/ovirt-engine-sdk-python-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-ovirt-engine-sdk() {
  depends=('python2-pycurl' 'python2-six' 'python2-enum34')

  cd "${srcdir}/ovirt-engine-sdk-python-${pkgver}"
  python2 setup.py install -O1 --root="${pkgdir}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
