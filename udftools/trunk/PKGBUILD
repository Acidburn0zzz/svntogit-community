# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: lucck <lucck@ep.com.pl>
# Contributor: aksr <aksr at t-com dot me>

pkgname=udftools
pkgver=2.1
pkgrel=1
pkgdesc='Linux tools for UDF filesystems and DVD/CD-R(W) drives'
url='https://github.com/pali/udftools'
arch=('x86_64')
license=('GPL')
depends=('ncurses' 'readline')
validpgpkeys=('B856B21074A8AE9B692B80858BF0C93D03E44352') # Pali Roh√°r <pali.rohar@gmail.com>
source=("https://github.com/pali/$pkgname/releases/download/$pkgver/$pkgname-${pkgver}.tar.gz"{,.asc})
sha256sums=('783221bcc2518ea480cee01ceeb8825db7a089f4e986b557f6460f89636142b6'
            'SKIP')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|/lib/|$(libdir)/|' pktsetup/Makefile.am
  autoreconf -fi
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sbindir=/usr/bin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

