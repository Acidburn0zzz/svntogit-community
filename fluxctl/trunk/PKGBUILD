# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fluxctl
_pkgname=flux
pkgver=1.18.0
pkgrel=1
pkgdesc="Controller for the GitOps Kubernetes operator flux"
arch=('x86_64')
url="https://github.com/fluxcd/flux"
license=('Apache')
makedepends=('go-pie' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fluxcd/flux/archive/${pkgver}.tar.gz")
sha512sums=('9973f71b0afd8c410ad561988759c068e7cd969445db1c71c1e1d835bf385fe075e94a7537e7eca307b0f8dc316369344e932f369df5a724e611581ae005cbb4')
changelog=CHANGELOG.md

prepare() {
  export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/fluxcd/"
  mv "${_pkgname}-${pkgver}" "src/github.com/fluxcd/${_pkgname}"
}

build(){
  cd "src/github.com/fluxcd/${_pkgname}/cmd/fluxctl"
  go build  \
  -trimpath \
  -ldflags "-w -X main.version="${pkgver}" -extldflags ${LDFLAGS}" .
  chmod +x ./fluxctl
}

check(){
  cd "src/github.com/fluxcd/${_pkgname}/cli"
  go test ./...
}

package(){
  cd "src/github.com/fluxcd/${_pkgname}/cli"
  install -Dsm755 ./fluxctl "${pkgdir}/usr/bin/fluxctl"
}
