# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-params-util
pkgver=1.04
pkgrel=1
pkgdesc="Simple, compact and correct param-checking functions"
arch=(i686 x86_64)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.5.30' 'perl-scalar-list-utils')
makedepends=('perl-extutils-cbuilder>=0.27' 'perl-extutils-makemaker>=6.52'
             'perl-pathtools')
checkdepends=('perl-test-simple')
url=https://metacpan.org/release/Params-Util
source=("http://search.cpan.org/CPAN/authors/id/A/AD/ADAMK/Params-Util-${pkgver}.tar.gz")
md5sums=(84bfb0a16cff67f2077ece0e24408b0f)
sha512sums=(086b6e37ce24a423439e346c4fc7069cbb5bde6af5c259fa765616bf637597105ee379df5edca82fdeb3bb0e44d83c7704ea1fe09170acea389ffb54d77f3d3d)
_distdir="${srcdir}/Params-Util-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
