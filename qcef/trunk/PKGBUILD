# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qcef
pkgver=1.1.4.3
pkgrel=1
pkgdesc="Qt5 binding of CEF"
arch=('x86_64')
url="https://github.com/linuxdeepin/qcef"
license=('GPL')
depends=('gconf' 'libxss' 'nss' 'qt5-webchannel' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.bz2::https://github.com/linuxdeepin/qcef/archive/$pkgver.tar.gz")
sha512sums=('49fb3a364e40cdddf4e366e482f668ef62182f8f8ed95bed9b44eaf60e38f3c77338172412121db374d95015e8474b7a8e7d3fe7060f4f9cb86545fdab71d482')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../qcef-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  chmod +s "$pkgdir"/usr/lib/qcef/chrome-sandbox
  rm -rf "$pkgdir"/build
}
