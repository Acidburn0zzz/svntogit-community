# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-settings-daemon
pkgver=1.22.1
pkgrel=2
pkgdesc="The MATE Settings daemon"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'libcanberra' 'libmatekbd' 'libmatemixer' 'libnotify'
         'mate-desktop' 'nss' 'polkit')
makedepends=('intltool' 'python')
optdepends=('libcanberra-pulse: PulseAudio support'
            'pulseaudio-alsa: PulseAudio support')
groups=('mate')
conflicts=('mate-settings-daemon-gtk3')
replaces=('mate-settings-daemon-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        '0001-dbus-sys-dir.patch')
sha256sums=('37122a5aec96ebff25789be90e0a0f749eac096e8441babbbbe0c508be00bc45'
            '8d1d02ce3545fd3fcda6a919844722623d393bb2bd526bd827eefc694fb5ade2')

prepare() {
	cd "${pkgname}-${pkgver}"

	patch -Np1 < ../0001-dbus-sys-dir.patch

	autoreconf -fi
}

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--with-dbus-sys="/usr/share/dbus-1/system.d" \
        	--enable-polkit \
        	--enable-pulse

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
