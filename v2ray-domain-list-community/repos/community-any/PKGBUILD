# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=202003051416
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('9d2d44ad6b0cde366b711545b9b34c516beb6174b70e1104b736a8f2cc1281d74fed0a9a1ab303ba6e8b394896e1ca429339e9869a17e7f38aa52f7d80a95467')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/usr/lib/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
