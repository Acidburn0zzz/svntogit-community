# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20201229161613
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("https://github.com/v2fly/domain-list-community/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f02403df46594e384d9e19d7235f9ad9da67250b3808e5775c877c1a470287d83639fbf6a5ef3d56a4dab5560fea2d25a4cf7c8e8edad0a85476d1e22f721f7f')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2fly
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2fly/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2fly/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm644 dlc.dat "$pkgdir"/usr/share/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
