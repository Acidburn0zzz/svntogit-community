# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=202003240831
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('dd758729615f9d24961475d62291db55f39d79d553279e906652e8d1a2153f23998f6dbc0ae2618ff4d31be225ddc2b30756f2b1e028782681838e6381806c6f')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/usr/lib/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
