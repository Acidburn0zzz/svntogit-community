# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20201206133507
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("https://github.com/v2fly/domain-list-community/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8b2a3580b56237e2c99506d9c23dec96013ee79d1909901e8f7d78b1aa27db8ae999b48c3db92db9376b208e580199c12ce9083aa2b420069d60f1d8e00a7c94')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2fly
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2fly/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2fly/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm644 dlc.dat "$pkgdir"/usr/share/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
