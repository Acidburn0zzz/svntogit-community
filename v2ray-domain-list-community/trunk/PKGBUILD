# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=202005261114
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('5f2972e6b1a123954a6b5b495bdeaf8ac08d0f70336ba120b9cf62db08529ff69e7b0ff36fe9478dda491d4441699ff3883e600a8e2b1a05c5a4ea93a80f8b42')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/usr/lib/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
