# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20200718115842
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('0a0b5001cbf45a513ea09a198f5eec05323a5611f04a1f98299fd88274cb0a2add177b25325dfd8d74a7ae351cbd98b08fc38060d94ec6c9ad8ffd4abcd474e2')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/usr/lib/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
