# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20190822.1
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz"
        https://github.com/v2ray/domain-list-community/pull/90.patch)
sha512sums=('03057dfcb53682bd3ae60650a668cccf8567616617277ed15ee825dc9627fb0750b3d208e9b6eeafde2ec701870a19b9ef4d8a1f348a43380d38f505fb823f27'
            '3fea3062ef8813becab2a90ed7356d07320d001662741b21b5e631fabb8c63212a9f2c0d78a8762e9296428dd04588c21cd612f1f60f1205b0734050c7e0eae5')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  patch -d domain-list-community-$pkgver -p1 -i ../90.patch

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/etc/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
