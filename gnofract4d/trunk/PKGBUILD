# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=gnofract4d
pkgver=3.13
pkgrel=2
pkgdesc='A fractal browser with PyGTK gui'
arch=('i686' 'x86_64')
url='http://gnofract4d.sourceforge.net'
license=('custom')
depends=('pygtk' 'libjpeg' 'libpng' 'shared-mime-info')
options=('!strip')
install='gnofract4d.install'
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'imageIO.cpp.patch')
md5sums=('d739eb0a61bc518ada8f5b8952298166'
         'ff29a621a27a3836de211c577f6ab9a4')

build() {
  cd ${pkgname}-${pkgver}

  patch -p0 < ${srcdir}/imageIO.cpp.patch

  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install --root=${pkgdir} --optimize=1

  install -D -m644 COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/COPYING 
}
