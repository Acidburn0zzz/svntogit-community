# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=gnofract4d
pkgver=3.14
pkgrel=1
pkgdesc='A fractal browser with PyGTK gui'
arch=('i686' 'x86_64')
url='http://gnofract4d.sourceforge.net'
license=('custom')
depends=('pygtk' 'libjpeg' 'libpng' 'shared-mime-info')
options=('!strip')
install='gnofract4d.install'
source=("http://downloads.sourceforge.net/sourceforge/gnofract4d/gnofract4d-${pkgver}.tar.gz")
md5sums=('5c4956ff9aed37b5ad8b984e3673937d')

build() {
  cd gnofract4d-${pkgver}

  python2 setup.py build
}

package() {
  cd gnofract4d-${pkgver}

  python2 setup.py install --root=${pkgdir} --optimize=1

  install -D -m644 COPYING \
    ${pkgdir}/usr/share/licenses/gnofract4d/COPYING 
}
