# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
_name=setBfree
pkgname=setbfree
pkgver=0.8.9
pkgrel=2
pkgdesc="DSP tonewheel organ"
arch=('x86_64')
url="http://setbfree.org"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('ftgl' 'pango')
makedepends=('gendesk' 'jack' 'liblo' 'lv2' 'ttf-dejavu')
optdepends=('jack: for standalone applications'
            'liblo: for standalone applications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pantherb/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('4710589e239c55c3854b97e70e059c8e0940903135a1441baea90ed9ce07a94ba362e12fd9a30442ed04b433c1a230537076a7f8004fe07b8becb678b0cda51d')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  # XDG desktop files
  gendesk -n \
          --pkgname "${pkgname}" \
          --name "${_name}" \
          --exec "${_name}UI" \
          --icon "${_name}" \
          --genericname "${pkgdesc}" \
          --pkgdesc "${pkgdesc}" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname x42-whirl \
          --name x42-whirl \
          --genericname "Leslie speaker emulation" \
          --pkgdesc "Leslie speaker emulation as JACK client" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # xdg desktop files
  install -vDm 644 ../*.desktop -t "${pkgdir}/usr/share/applications"
  # icons
  install -vDm 644 doc/{${_name},x42-whirl}.png \
    -t "${pkgdir}/usr/share/pixmaps/"
  # man
  install -vDm 644 doc/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README.md} doc/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
