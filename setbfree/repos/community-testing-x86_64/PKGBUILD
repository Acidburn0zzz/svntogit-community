# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
_name=setBfree
pkgname=setbfree
pkgver=0.8.7
pkgrel=2
pkgdesc="DSP tonewheel organ "
arch=('x86_64')
url="http://setbfree.org"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('ftgl' 'jack' 'liblo' 'pango')
makedepends=('lv2' 'ttf-dejavu')
# xdg desktop files not properly packaged:
# https://github.com/pantherb/setBfree/issues/59
_desktop_file_commit='ff15bf2fdb85120c5125deb5debdcabb3969c6a4'
source=("$pkgname-$pkgver.tar.gz::https://github.com/pantherb/${_name}/archive/v${pkgver}.tar.gz"
        "https://github.com/pantherb/${_name}/raw/${_desktop_file_commit}/debian/setbfree.desktop"
        "https://github.com/pantherb/${_name}/raw/${_desktop_file_commit}/debian/x42-whirl.desktop")
sha512sums=('f4da1391fd84439b7affc4862f1d3ccf6b3df6b17d1de3bed9c657af43e56a3b7dedcac27f668a3e4f6ba1bafc1b98f6023199c14187ea432ee86666f2fe7b5a'
            'bba3b56efd207fcea055ece250745204d983e5222b7979aed54d4b022c28c33edaffd7d8fb906a13133efcd63f7a3d193257a5ec4a382d73177b6daf8a57a200'
            '1f8a1d4d4fe8458eb9c3750ff5af682b8e16d104d7f6799c86e9706a6298c59b126276aaf762a829b8661837e019d950e1680cb89da498fcb1c68db05102b51e')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # xdg desktop files
  install -vDm 644 "${srcdir}/"*.desktop \
    -t "${pkgdir}/usr/share/applications"
  # icons
  install -vDm 644 doc/{${_name},x42-whirl}.png \
    -t "${pkgdir}/usr/share/pixmaps/"
  # man
  install -vDm 644 doc/*.1 -t "${pkgdir}/usr/share/man/man1/" 
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README.md} doc/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
