# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=nomad
pkgver=1.1.1
pkgrel=2
pkgdesc="A simple and flexible workload orchestrator"
arch=('x86_64')
url="https://www.nomadproject.io"
license=('MPL2')
depends=('glibc' 'iptables')
makedepends=('go' 'git')
optdepends=(
  'docker: for docker driver'
  'java-runtime: for java driver'
  'qemu: for qemu driver'
  'nomad-driver-nspawn: for systemd-nspawn driver'
  'nomad-driver-podman: for podman driver'
  'nomad-driver-lxc: for lxc driver'
  'nomad-driver-containerd: for containerd driver'
)
backup=(etc/nomad.d/defaults.hcl)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/nomad/archive/v$pkgver.tar.gz"
  'systemd.service'
  'tmpfiles.conf'
  'defaults.hcl'
)
b2sums=('85384697dc3da8846804e1e116b9048a662b90c1942254959f564da806a9cab7f42140f4c6947a5ccb02550b0b6902bde15c0fc607b32894ee5efb0f1ca95062'
        '0f931868e9d7465f3be176ab789b7c7b5a4c35713a82e4d3bb94460254011779d814d4708f4f8cc76ba0dd1928ac7eeda42e01ed43be675a5d26d0b75ba986b9'
        '93abad92a38c9a398d4e9fda448dc058d6d65ca24e0ee93a556a71e789ea9bc0cefc00679959e6cb2b2ad75818bf7ad06aa383b3fc524215d060c5ac1f86c08e'
        'abbf4c69d70e03b318f73ef84ccf258e39b25ac956ec68c82bebe277662ddd1677719c3b693c04a8cc38554c562a127aa2ba4061e20e594440d432d729bab3b3')

prepare() {
	cd "$pkgname-$pkgver"
  mkdir build
  go mod vendor
}

# README: https://www.nomadproject.io/docs/install/production/requirements
# TODO find out what kind of 'upstream' configuration goes into /etc/nomad.d ?
# REFERENCE: https://salsa.debian.org/go-team/packages/nomad

build() {
  # nonvidia: https://github.com/hashicorp/nomad/issues/8330
  local GO_TAGS="ui release nonvidia"
	cd "$pkgname-$pkgver"
  CGO_ENABLED=1 go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=vendor \
    -modcacherw \
    -ldflags "-extldflags $LDFLAGS -X "main.GitCommit=${pkgver}"" \
    -tags "$GO_TAGS" \
    -o build .
}

package() {
  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"

  # configuration
  install -vDm644 -t "$pkgdir/etc/nomad.d" defaults.hcl

  # systemd integration
  install -vDm644 systemd.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -vDm644 tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}
