# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
pkgver=4.0.r1
pkgrel=4
_perlver=5.30.2
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol perl=$_perlver)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk permlib)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${pkgver/.r/r}-minimal.tar.bz2"
         polymake-gcc10.patch)
sha256sums=('b29de50dda6f657f2e82ef6acff62df1b51128a20c5d53bd97226ea22fdc3b52'
            '64095f8688d70d88003eb67fe2c9ddc056033205aaeed19b264859c85589578c')

prepare() {
  patch -d polymake-${pkgver%.*} -p0 -i ../polymake-gcc10.patch # Fix build with GCC 10 (Fedora)
}

build() {
  cd polymake-${pkgver%.*}
  ./configure \
    --prefix=/usr \
    --with-cdd=/usr \
    --with-lrs=/usr \
    --with-permlib=/usr \
    --with-sympol=/usr
  make
}

package() {
  cd polymake-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
