# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=tracker
pkgver=0.10.22
pkgrel=2
pkgdesc="Powerful object database, tag/metadata database, search tool and indexer"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/tracker/"
license=('GPL')
depends=('libgee' 'libgnome-keyring' 'upower' 'libexif' 'exempi' 'poppler-glib'
	 'libgsf' 'icu' 'enca' 'xdg-utils')
makedepends=('gobject-introspection' 'intltool' 'giflib' 'gstreamer0.10-base'
	     'totem-plparser' 'evolution' 'taglib' 'nautilus' 'gnome-panel')
optdepends=('giflib: extractor for GIF data'
	    'gstreamer0.10-base: video extractor'
	    'totem-plparser: playlist support'
	    'evolution: Evolution email data miner'
	    'taglib: writeback for audio files'
	    'nautilus: nautilus-extension'
	    'gnome-panel: tracker-search-bar')
options=('!emptydirs' '!libtool')
conflicts=('libtracker')
provides=('libtracker')
install=tracker.install
source=(http://ftp.gnome.org/pub/GNOME/sources/tracker/0.10/tracker-$pkgver.tar.bz2
	tracker-0.10-gnome3-nautilus-extension.patch)
md5sums=('1248742586be5c95871ea59718457657'
         '316f1647afe178a87400a476ef1b62a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Ni ../tracker-0.10-gnome3-nautilus-extension.patch
  ./configure --prefix=/usr --sysconfdir=/etc  --libexecdir=/usr/lib/$pkgname \
    --disable-unit-tests --enable-video-extractor=gstreamer --disable-network-manager
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
