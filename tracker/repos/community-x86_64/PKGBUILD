# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=tracker
pkgver=0.10.0
pkgrel=1
pkgdesc="Powerful object database, tag/metadata database, search tool and indexer"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/tracker/"
license=('GPL')
depends=('upower' 'poppler-glib' 'gstreamer0.10-base' 'libgee'  'gnome-desktop' 'libnotify' 'xdg-utils' 'gtkhtml'
	 'libgsf' 'gnome-panel-bonobo' 'exempi')
makedepends=('intltool' 'gnome-panel' 'nautilus' 'evolution' 'totem-plparser')
optdepends=('nautilus'
	    'enca'
	    'gtkhtml'
	    'gnome-panel'
	    'totem-plparser'
	    'icu'
	    'taglib'
	    'networkmanager'
	    'giflib')
options=('!libtool')
conflicts=('libtracker')
provides=("libtracker")
install=tracker.install
source=(http://ftp.gnome.org/pub/GNOME/sources/tracker/0.10/tracker-$pkgver.tar.bz2)
md5sums=('03084485bdc91af0924121950e3f20db')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  export PYTHON=`which python2`
  for file in tests/functional-tests/*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  ./configure --prefix=/usr --sysconfdir=/etc  --libexecdir=/usr/lib/$pkgname \
    --enable-video-extractor=gstreamer --disable-unac --disable-hal \
    --disable-devkit-power --disable-miner-evolution

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
