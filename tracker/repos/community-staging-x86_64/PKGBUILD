# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alexander Fehr <pizzapunk gmail com>

pkgname=tracker
pkgver=0.8.17
pkgrel=2
pkgdesc="Powerful object database, tag/metadata database, search tool and indexer"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/tracker/"
license=('GPL')
depends=('upower' 'poppler-glib' 'gstreamer0.10-base' 'libgee'  'gnome-desktop' 'libnotify' 'xdg-utils')
makedepends=('intltool' 'gnome-panel' 'nautilus' 'evolution' 'totem-plparser')
optdepends=('nautilus'
            'enca'
            'gtkhtml'
            'gnome-panel'
            'totem-plparser')
options=('!libtool')
conflicts=('libtracker')
provides=("libtracker")
install=tracker.install
source=(http://ftp.gnome.org/pub/GNOME/sources/tracker/0.8/tracker-$pkgver.tar.bz2)
md5sums=('503117377b228e4575105c6b4ce34eaa')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  for file in tests/functional-tests/*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done

  ./configure --prefix=/usr --sysconfdir=/etc  --libexecdir=/usr/lib/$pkgname \
    --enable-video-extractor=gstreamer --disable-unac --disable-hal

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
