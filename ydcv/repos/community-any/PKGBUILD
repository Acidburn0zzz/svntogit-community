# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor : Fernando "Firef0x" G.P. da Silva <firefgx { aT ) gmail [ d0t } com>

pkgname=ydcv
pkgdesc="YouDao Console Version - Simple wrapper for Youdao online translate (Chinese <-> English) service API, as an alternative to the StarDict Console Version (sdcv)"
pkgver=0.6
pkgrel=1
arch=("any")
url="https://github.com/felixonmars/ydcv"
license=("GPL")
depends=('python-setuptools')
makedepends=('python-setuptools-scm' 'python-setuptools-markdown')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('e8f2b60bd099befc7d0bbe03608259a41979019bfc14c9e8f98034d32fdc4742bc7fa5d284bf70fe714862d027cedc9bf2fc14551ecd43ecd938013b5281775d')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 contrib/zsh_completion "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}

# vim:set ts=2 sw=2 et:
