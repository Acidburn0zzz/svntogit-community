# $Id$
# Maintainer: Roman Kyrylych <roman@archlinux.org>
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Mefju <mefju@o2.pl>

pkgname=isomaster
pkgver=1.3.9
pkgrel=1
pkgdesc="CD image editor written in GTK2"
arch=('i686' 'x86_64')
url="http://littlesvr.ca/isomaster"
license=('GPL2')
depends=('gtk2' 'desktop-file-utils')
makedepends=('iniparser')
install=isomaster.install
source=("http://littlesvr.ca/isomaster/releases/$pkgname-$pkgver.tar.bz2"
        isomaster-1.3.9-asneeded.patch)
md5sums=('ba7b44303979fd193df04b8f5f7ce4f8'
         '79ac690bd7edf69dfb53e9916fc1182d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 -i ../isomaster-1.3.9-asneeded.patch
  make PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir" install
}
