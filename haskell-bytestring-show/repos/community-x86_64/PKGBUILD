# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-bytestring-show
pkgver=0.3.5
pkgrel=1
pkgdesc="Efficient conversion of values into readable byte strings."
url="http://code.haskell.org/~dolio/"
license=("BSD3")
arch=('i686' 'x86_64')
makedepends=()
depends=("ghc=7.0.3"
         "haskell-array=0.3.0.2"
         "haskell-binary=0.5.0.2"
         "haskell-bytestring=0.9.1.10"
         "haskell-containers=0.4.0.0")
options=('strip')
install="${pkgname}.install"
source=("http://hackage.haskell.org/packages/archive/bytestring-show/${pkgver}/bytestring-show-${pkgver}.tar.gz")
md5sums=('934edba94aeceef6e9fc1b174d4f94ea')

build() {
  cd ${srcdir}/bytestring-show-${pkgver}

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
	--prefix=/usr --docdir=/usr/share/doc/${pkgname} \
	--libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${srcdir}/bytestring-show-${pkgver}
  install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
  install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
  install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/bytestring-show
  runhaskell Setup copy --destdir=${pkgdir}
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  rm -f ${pkgdir}/usr/share/doc/${pkgname}/LICENSE
}
