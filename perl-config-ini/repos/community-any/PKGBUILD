# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Alessandro Sagratini <ale_sagra at hotmail dot com>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=perl-config-ini
pkgver=0.020
pkgrel=1
pkgdesc='Perl/CPAN Module Config::INI - simple .ini-file format'
arch=('any')
license=('GPL' 'PerlArtistic')
depends=('perl-io-string' 'perl-mixin-linewise')
options=('!emptydirs')
url='http://search.cpan.org/~rjbs/Config-INI/'
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Config-INI-$pkgver.tar.gz")
md5sums=('835febf958f5bcf4c184defc822fc8ff')

build() {
  cd Config-INI-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Config-INI-$pkgver
  make test
}

package() {
  cd Config-INI-$pkgver
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
