# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=kdeconnect
pkgver=1.3.0
pkgrel=1
pkgdesc='Adds communication between KDE and your smartphone'
url='https://community.kde.org/KDEConnect'
license=(GPL)
arch=(x86_64)
depends=(hicolor-icon-theme kcmutils kwayland libfakekey qca-qt5)
makedepends=(extra-cmake-modules kdoctools qt5-declarative)
optdepends=('sshfs: remote filesystem browser' 'kde-cli-tools: configuration UI'
            'qt5-declarative: QML bindings' 'python-nautilus: Nautilus integration')
source=("https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-kde-$pkgver.tar.xz") #{,.sig} unavailable key
sha256sums=('954d4e9f7de06a7409d9e3e124c959481a1c2c20148576dc3d431a3d6129fb3f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-kde-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
