# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=buildbot-worker
pkgdesc='Buildbot worker daemon'
pkgver=2.6.0
_bb_contrib_commit=ada3c8f30ca7e1b6bb260e2e5971053fbd254333
pkgrel=1
arch=(any)
url='https://buildbot.net'
license=(GPL2)
depends=(python-setuptools python-twisted python-future)
makedepends=(git)
checkdepends=(python-mock)
source=("https://github.com/buildbot/buildbot/releases/download/v$pkgver/buildbot-v$pkgver.gitarchive.tar.gz"{,.sig}
        "git+https://github.com/buildbot/buildbot-contrib.git#commit=$_bb_contrib_commit")
sha256sums=('0ac835b58db309bebcf00fa77687385551833b3dac1c66aa671d271776050c19'
            'SKIP'
            'SKIP')
validpgpkeys=(
  '390EB159056ED56F66AB1092AECD456B4D2531FC'  # Pierre Tardy <tardyp@gmail.com> (@tardyp on GitHub)
  'FD0004A26EADFE43A4C3F249C6F7AE200374452D'  # Povilas Kanapickas <povilas@radix.lt> (@p12tic on GitHub)
)

build() {
  cd buildbot-$pkgver/worker
  python setup.py build
}

check() {
  cd buildbot-$pkgver/worker
  PYTHONPATH=. trial3 buildbot_worker
}

package() {
  cd buildbot-$pkgver/worker
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 "$srcdir"/buildbot-contrib/worker/contrib/systemd/buildbot-worker@.service \
    -t "$pkgdir"/usr/lib/systemd/system/
}
