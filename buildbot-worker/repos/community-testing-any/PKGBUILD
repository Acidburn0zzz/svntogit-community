# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=buildbot-worker
pkgdesc='Buildbot worker daemon'
pkgver=1.8.0
# `git rev-parse v$pkgver`
_tag_rev=5b1106e9e2898cff174e7dd4f03dfd80a5c79f16
pkgrel=1
arch=(any)
url='https://buildbot.net'
license=(GPL2)
depends=(python-setuptools python-twisted python-future)
makedepends=(git)
checkdepends=(python-mock)
source=("git+https://github.com/buildbot/buildbot?signed#tag=$_tag_rev")
sha256sums=('SKIP')
validpgpkeys=(
  '390EB159056ED56F66AB1092AECD456B4D2531FC'  # Pierre Tardy <tardyp@gmail.com>
)

build() {
  cd buildbot/worker
  python setup.py build
}

check() {
  cd buildbot/worker
  PYTHONPATH=. trial3 buildbot_worker
}

package() {
  cd buildbot/worker
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
