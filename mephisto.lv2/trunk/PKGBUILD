# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.18.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=(x86_64)
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=(Artistic2.0)
groups=(lv2-plugins pro-audio)
depends=(glibc libglvnd libvterm libx11 lv2-host ttf-fira-code ttf-fira-sans)
makedepends=(faust fontconfig glew glu lv2 meson)
checkdepends=(lv2lint)
source=(https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('1ed3f2fd005069163d4180bdce1104236a66746d1572561c4e4dcfafede73b460601ee10b0f4d64c8bf1b25c0810d71103a0ec9cf02944519791f47c69e2dddc'
            'SKIP')
b2sums=('ee925a28644ae1144af73ae0dbf4ddf7cf062fddfe3b8422f812f573fd82f53a4aac93178219afacba37afad683f99280fea48aedb0dd4c751a30ffd9938a69e'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

prepare() {
  local _subproject
  # disable accidental -Werror in subprojects
  for _subproject in $pkgname-$pkgver/subprojects/*/meson.build; do
    sed -e 's/werror=true/werror=false/g' -i $_subproject
  done
}

build() {
  arch-meson build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test || echo "Known issue with lv2lint call, upstream has been notified."
}

package() {
  depends+=(libGLEW.so libfaust.so libfontconfig.so)

  DESTDIR="$pkgdir" ninja -C build install
  # devendor ttf-fira-code
  rm -vf "$pkgdir/usr/lib/lv2/$pkgname/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf /usr/share/fonts/TTF/FiraCode-$font_type.ttf "$pkgdir/usr/lib/lv2/$pkgname/"
  done
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
}
