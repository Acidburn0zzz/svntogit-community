# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

pkgbase=python-contextlib2
_pypiname=contextlib2
pkgname=(python-contextlib2 python2-contextlib2)
pkgver=0.5.1
pkgrel=1
pkgdesc="contextlib2 is a backport of the standard libraryâ€™s contextlib module to earlier Python versions"
arch=('any')
url="http://pypi.python.org/pypi/${_pypiname}"
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.python.org/packages/source/${_pypiname:0:1}/${_pypiname}/${_pypiname}-$pkgver.tar.gz")
md5sums=('b7f4c5a2b900c419b11b20f59838de1b')

prepare() {
  cp -a ${_pypiname}-$pkgver{,-py2}
}

build() {
  cd "${srcdir}/${_pypiname}-$pkgver"
  python setup.py build

  cd "${srcdir}/${_pypiname}-$pkgver-py2"
  python2 setup.py build
}

package_python-contextlib2() {
  depends=('python')

  cd ${_pypiname}-$pkgver
  python setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-contextlib2() {
  depends=('python2')

  cd ${_pypiname}-$pkgver-py2
  python2 setup.py install --root="${pkgdir}"
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
