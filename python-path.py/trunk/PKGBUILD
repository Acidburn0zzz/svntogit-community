# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path.py
pkgver=12.0.1
pkgrel=2
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
depends=('python-importlib-metadata')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-flake8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz"
         path.py-pytest5.patch::"https://github.com/jaraco/path.py/commit/02eb16f0.patch")
sha512sums=('5906b31db753374a75e8b1a8515ed3d6ab7b394906ce8d07aea13be77d5e6db164d79c4b1dc517c8c6692b4eef57505a5c739ba54e282dcbda01f37d21e04044'
            '357b7568865030efde348a3e8ed745285ce3644496dfab32f9e67a293f506125f95fecc3b1def67b87a4e24836a4b161f8676ae675cb6f1cc501196ba0ca44c1')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd path.py-$pkgver
  patch -p1 -i ../path.py-pytest5.patch # Fix tests with pytest 5
}

build() {
  cd path.py-$pkgver
  python setup.py build
}

check() {
  cd path.py-$pkgver
  python setup.py pytest
}

package() {
  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
