# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-path.py
pkgname=('python-path.py' 'python2-path.py')
pkgver=11.2.0
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
makedepends=('python-setuptools-scm' 'python2-setuptools-scm' 'python-importlib-metadata'
             'python2-importlib-metadata')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-flake8' 'python2-pytest-flake8')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('faf5f2fc12cb9be47c6645408e192a65b6aef2a6c942230f63697f6b4bac1d7f0d7c381368fb5b96c6be097e03de29d2f604fa6392a1283c488e297b1f6b5377')

prepare() {
  sed -i 's/W503/W503 W504/' path.py-$pkgver/.flake8
  cp -a path.py-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py build

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/path.py-$pkgver
  python setup.py pytest

  cd "$srcdir"/path.py-$pkgver-py2
  python2 setup.py pytest
}

package_python-path.py() {
  depends=('python-importlib-metadata')

  cd path.py-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-path.py() {
  depends=('python2-importlib-metadata')

  cd path.py-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
