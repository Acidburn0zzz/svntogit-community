# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=gtksourceview4
pkgver=4.0.2
pkgrel=1
pkgdesc="A text widget adding syntax highlighting and more to GNOME"
url="https://wiki.gnome.org/Projects/GtkSourceView"
arch=(x86_64)
license=(GPL)
depends=(gtk3 libxml2)
makedepends=(intltool gobject-introspection glade vala gtk-doc autoconf-archive git)
_commit=fb6c1941ec2e62ec8cfd3939a038f013663dfe3c  # tags/4.0.2^0
source=("git+https://gitlab.gnome.org/GNOME/gtksourceview.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd gtksourceview
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd gtksourceview
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd gtksourceview
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-gtk-doc --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gtksourceview
  make DESTDIR="$pkgdir" install
}
