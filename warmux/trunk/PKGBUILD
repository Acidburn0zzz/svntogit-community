# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: pukyxd 
# Maintainer: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=warmux
pkgver=11.04
pkgrel=1
pkgdesc="A clone of the worms game"
arch=('i686' 'x86_64')
url="http://www.wormux.org/"
license=('GPL')
depends=('libxml++' 'sdl_image' 'sdl_gfx>=2.0.19' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'curl>=7.16.2' 'gcc-libs' 'libxml2')
makedepends=('pkgconfig')
replaces=('wormux')
source=(http://download.gna.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('668cd4c22afde352c566454e65fc4144')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver}"

  #gcc 4.6.0 fix
  sed -i '27i#include <stddef.h>' lib/warmux/include/WARMUX_action.h

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
