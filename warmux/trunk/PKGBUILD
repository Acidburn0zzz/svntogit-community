# $Id$
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: pukyxd 
# Maintainer: Daenyth <Daenyth+Arch AT gmail DOT com>

pkgname=warmux
pkgver=11.04.1
pkgrel=2
pkgdesc="A clone of the worms game"
arch=('i686' 'x86_64')
url="http://www.wormux.org/"
license=('GPL')
makedepends=('pkgconfig' 'libxml++' 'sdl_image' 'sdl_gfx>=2.0.19' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'curl>=7.16.2' 'gcc-libs' 'libxml2')
replaces=('wormux')
source=(http://download.gna.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('26ff65c43a9bb61a3f0529c98b943e35')

build() { 
  cd "${srcdir}/${pkgname}-${pkgver/.1//}"

  #gcc 4.6.0 fix
  #sed -i '27i#include <stddef.h>' lib/warmux/include/WARMUX_action.h

  ./configure --prefix=/usr
  make
}

package() {
  depends=('libxml++' 'sdl_image' 'sdl_gfx>=2.0.19' 'sdl_ttf' 'sdl_mixer' 'sdl_net' 'curl>=7.16.2' 'gcc-libs' 'libxml2' "warmux-data>=${pkgver/.1//}")

  cd "${srcdir}/${pkgname}-${pkgver/.1//}"

  make DESTDIR="${pkgdir}" install
  rm -r ${pkgdir}/usr/share/warmux
}
