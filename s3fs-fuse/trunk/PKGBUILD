# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Roheim <david dot roheim at gmail dot com>
# Contributor: Chris Heien <chris.h.heien@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=s3fs-fuse
pkgver=1.88
pkgrel=1
pkgdesc="FUSE-based file system backed by Amazon S3"
arch=('x86_64')
url="https://github.com/s3fs-fuse/s3fs-fuse/wiki"
license=('GPL2')
depends=('fuse2' 'curl' 'libxml2')
conflicts=('s3fs')
provides=('s3fs')
replaces=('s3fs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/s3fs-fuse/s3fs-fuse/archive/v$pkgver.tar.gz")
sha512sums=('e4bee4aa7a49effe9c4087e5841a0e0787cb7553d7037e0096f681fd588e9b35dbf8f4687f71e3360e7cde12e6820787dfe7565f845790719150350e7c473604')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
