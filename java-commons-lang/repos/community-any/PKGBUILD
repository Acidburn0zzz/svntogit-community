# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=java-commons-lang
pkgver=3.9
pkgrel=4
pkgdesc='A host of helper utilities for the java.lang API'
arch=('any')
url='https://commons.apache.org/proper/commons-lang/'
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('maven' 'strip-nondeterminism')
source=("https://www.apache.org/dist/commons/lang/source/commons-lang3-$pkgver-src.tar.gz"{,.asc})
sha512sums=('bf69e747a2ed1a0a1b67e5c0e7bdb2344f7d8b0a88e0b535d8bdee627cbdc7be769593b58b76c84149acbb6f284f9b125095ba5a9d222413c033d1b72e07ac98'
            'SKIP')
validpgpkeys=('B6E73D84EA4FCC47166087253FAAD2CD5ECBB314') # Rob Tompkins <chtompki@apache.org>

build() {
	cd "commons-lang3-$pkgver-src"
	mvn -Dmaven.repo.local="$srcdir/commons-lang3-$pkgver-src/.m2/repository" \
	    -Dmaven.test.skip=true \
	    -Drat.skip=true \
	    install

	# Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
	# (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "target/commons-lang3-$pkgver.jar"
}

check() {
	cd "commons-lang3-$pkgver-src"
	mvn -Drat.skip=true test
}

package() {
	cd "commons-lang3-$pkgver-src/target"
	install -Dm644 "commons-lang3-$pkgver.jar" -t "$pkgdir/usr/share/java/commons-lang"
	ln -s "commons-lang3-$pkgver.jar" "$pkgdir/usr/share/java/commons-lang/commons-lang.jar"
}
