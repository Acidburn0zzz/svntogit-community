# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongodb-tools
pkgver=3.4.9
pkgrel=1
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl' 'libpcap' 'openssl-1.0')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-tools/archive/r$pkgver.tar.gz"
        "disable-sslv3.patch")
sha512sums=('546060a65124e722a1c6eb152fd4e086bc523656312888e16238b4a1df5313ab63ece41e638f012676af4be94ed156855b053afa62d778f72cd31f0ccd70f19d'
            '674af43ebf9e73f1eae4f81c160cec4c4ea657b7e38f97929a0845d8981f9af904eca79e2144e6150774799a58074cee5b67e4385d082630c471e438981d9ea5')

prepare() {
  cd mongo-tools-r$pkgver
  . ./set_gopath.sh
  mkdir bin

  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
}

build() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog mongoreplay; do
    go build -o bin/$i -tags "ssl sasl" $i/main/$i.go
  done
}

package() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongooplog mongoreplay; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
