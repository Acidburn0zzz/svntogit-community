# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongodb-tools
pkgver=4.0.4
pkgrel=1
pkgdesc="The MongoDB tools provide import, export, and diagnostic capabilities."
arch=('x86_64')
url="https://github.com/mongodb/mongo-tools"
license=('Apache')
depends=('libsasl' 'libpcap' 'openssl')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-tools/archive/r$pkgver.tar.gz"
        "disable-sslv3.patch")
sha512sums=('d7900dbbd1e1e63d5518494c85559a7eab34f53ffa38880628f3dda0a71ee1211f67cf7dfaac66498b0359eca32abb9c89410e5cc56415a1594acf38ebfa226b'
            '674af43ebf9e73f1eae4f81c160cec4c4ea657b7e38f97929a0845d8981f9af904eca79e2144e6150774799a58074cee5b67e4385d082630c471e438981d9ea5')

prepare() {
  cd mongo-tools-r$pkgver
  . ./set_gopath.sh
  mkdir bin
}

build() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongoreplay; do
    go build -o bin/$i -tags "ssl sasl" $i/main/$i.go
  done
}

package() {
  cd mongo-tools-r$pkgver
  for i in bsondump mongostat mongofiles mongoexport mongoimport mongorestore mongodump mongotop mongoreplay; do
    install -Dm755 bin/$i "$pkgdir/usr/bin/$i"
  done
}
