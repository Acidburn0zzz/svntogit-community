# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=buho
pkgver=2.0.0
pkgrel=2
pkgdesc='Task and note keeper'
url='https://mauikit.org/'
arch=(x86_64)
license=(GPL3)
depends=(mauikit-accounts mauikit-filebrowsing mauikit-texteditor hicolor-icon-theme)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        https://invent.kde.org/maui/buho/-/commit/b8350691.patch)
sha256sums=('400df4ab9f63c49190f16a8d3bcdb605f6d74700ff68dbd4c5daefc6dfc31760'
            'f990becd0c3dc5fab920918d4f8ab8aaf661e832d7cfa6a85b65fb1bb2a7f781')

prepare() {
  patch -d $pkgname-$pkgver -p1 < b8350691.patch # Fix build
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
