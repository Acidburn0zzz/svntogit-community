# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-augeas
pkgver=1.0.2
pkgrel=2
pkgdesc="Python bindings for Augeas"
arch=('any')
license=('LGPL')
url="http://augeas.net"
depends=('augeas' 'python-cffi')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/p/python-augeas/python-augeas-$pkgver.tar.gz"
        issue26.patch)
sha512sums=('c24c8f01420ca474be2903d3da35a246fa74f5ae848bb9267bf4a2166d3a4c64fa4d29a1c5aad6dac3c43251cda90673d6a5a72e6ebdd192e147baa03281e727'
            'b59554d38a63f6ad944827e8cacb2631ea8996a1e58620447635cdf9aadc33d04c40651680f1b0252265b4940ff15534c879e5ea2484ce394b0ba76c0bc5ba81')

prepare() {
  cd python-augeas-$pkgver
  # https://github.com/hercules-team/python-augeas/issues/26
  patch -p1 -i ../issue26.patch
}

build() {
  cd python-augeas-$pkgver
  python setup.py build
}

check() {
  cd python-augeas-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" make check
}

package() {
  cd python-augeas-$pkgver
  python setup.py install --skip-build -O1 --root="$pkgdir"
}
