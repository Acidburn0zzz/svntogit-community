# Maintainer: David Runge <dave@sleepmap.de>
pkgname=khard
pkgver=0.13.0
pkgrel=1
pkgdesc="Console CardDAV client"
arch=('any')
url="https://github.com/scheibler/khard"
license=('GPL3')
depends=('python-atomicwrites' 'python-configobj' 'python-ruamel-yaml'
'python-setuptools' 'python-unidecode' 'python-vobject')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
optdepends=('vdirsyncer: Synchronization of address books with a DAV server'
            'diffutils: Using sdiff_khard_wrapper.sh')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('353a51a06d6c7aa27b0f1cf35b3f7c113097ea4fa5e27bcb400f00de0cdd65ccaeadcc835b1cba541361a0395265cb967f731b23d875162f2be13381bc0979a6')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # additional wrapper script
  install -vDm 755 "misc/sdiff/sdiff_${pkgname}_wrapper.sh" \
    "${pkgdir}/usr/lib/${pkgname}/sdiff_${pkgname}_wrapper.sh"
  # twinkle integration
  install -t "${pkgdir}/usr/share/${pkgname}/twinkle/scripts/" \
    -vDm 644 misc/twinkle/scripts/*.py
  install -t "${pkgdir}/usr/share/${pkgname}/twinkle/sounds/" \
    -vDm 644 misc/twinkle/sounds/*.wav
  # zsh
  install -vDm 644 "misc/zsh/_${pkgname}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,CHANGES,README.md} "misc/${pkgname}/"*.{example,yaml}
}
