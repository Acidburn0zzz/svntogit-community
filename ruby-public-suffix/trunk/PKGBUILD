# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farawayer <farwayer@gmail.com>

pkgname=ruby-public-suffix
pkgver=2.0.5
pkgrel=1
pkgdesc='Domain name parser based on the Public Suffix List'
arch=('any')
url='https://simonecarletti.com/code/publicsuffix-ruby'
license=('MIT')
depends=('ruby')
options=('!emptydirs')
source=("https://rubygems.org/downloads/public_suffix-$pkgver.gem")
noextract=("public_suffix-$pkgver.gem")
sha1sums=('c241227a2f49942d07fceb4bc20a03d762f7e666')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" public_suffix-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/public_suffix-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/public_suffix-$pkgver/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
