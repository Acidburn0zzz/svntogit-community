# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=070
pkgrel=1
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$pkgver.tar.gz"
        lrs-system-cflags.patch)
sha256sums=('e9f12b29be89b3ad6984f3a9cf83f5776ac06edc57b0716649e63395e5ac4dfe'
            '23814d543e458f43b9871f5de6112f3ca331d8ded3449c7e2efa681ca5a6606f')

prepare() {
  cd lrslib-$pkgver
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
}

build() {
  cd lrslib-$pkgver
  make -j1
}

package() {
  cd lrslib-$pkgver
  make DESTDIR="$pkgdir" prefix=/usr install
}
