# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=071
_pkgver=${pkgver/./}
pkgrel=2
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$_pkgver.tar.gz"
        lrs-system-cflags.patch
        lrs-missing-include.patch)
sha256sums=('d3ea5636bfde3011d43c835773fabe131d9251197b6cc666a52d8caa3e1c7816'
            '8e43f9f6c583c653e28b75907f27f616f45f36d6fc677a4aae6a7004d60424c0'
            '6d5b97457353948c92a67d65288eb6c6492679ec80d3234abe31684d9225925c')

prepare() {
  cd lrslib-${pkgver%.*}
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
  patch -p1 -i ../lrs-missing-include.patch # Install missing header
}

build() {
  cd lrslib-${pkgver%.*}
  make
}

package() {
  cd lrslib-${pkgver%.*}
  make DESTDIR="$pkgdir" prefix=/usr install
}
