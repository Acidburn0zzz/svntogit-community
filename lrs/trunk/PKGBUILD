# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=lrs
pkgver=070.a
_pkgver=${pkgver/./}
pkgrel=1
pkgdesc="C implementation of the reverse search algorithm for vertex enumeration/convex hull problems"
arch=(x86_64)
url="http://cgm.cs.mcgill.ca/~avis/C/lrs.html"
license=(GPL)
depends=(gmp)
source=("http://cgm.cs.mcgill.ca/~avis/C/lrslib/archive/lrslib-$_pkgver.tar.gz"
        lrs-system-cflags.patch)
sha256sums=('601f4cef3d07676076256dd2687143047457217cbc658706b00a52e25f0701fd'
            '23814d543e458f43b9871f5de6112f3ca331d8ded3449c7e2efa681ca5a6606f')

prepare() {
  cd lrslib-$_pkgver
  patch -p1 -i ../lrs-system-cflags.patch # Use system CFLAGS/LDFLAGS
}

build() {
  cd lrslib-$_pkgver
  make -j1
}

package() {
  cd lrslib-$_pkgver
  make DESTDIR="$pkgdir" prefix=/usr install
}
