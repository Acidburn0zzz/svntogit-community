# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.0.0.3
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('803523cf9a4228074e25920165fc9cbc1aa24e22ddd056aa0757d3451a3a03691c031c8a5e1157f87816ea38a47946150eb0875cedac568627eba6be791514d1')

prepare() {
  cd dde-clipboard-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' dde-clipboard/pixmaplabel.cpp dde-clipboard/itemwidget.cpp
}

build() {
  cd dde-clipboard-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-clipboard-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
