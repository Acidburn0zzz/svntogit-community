# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.3.0.4
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('9ff8685536061dfb77099aed8b4e426d2bd743133b402e222353f821b5b52ee90f9d1c683d89c2c8572952f922d2878f7df89a9d7e807508b13bd5f44c3976be')

prepare() {
  cd dde-clipboard-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' dde-clipboard/pixmaplabel.cpp dde-clipboard/itemwidget.cpp
  sed -i '$aOnlyShowIn=Deepin' dde-clipboard/dde-clipboard.desktop
}

build() {
  cd dde-clipboard-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-clipboard-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
