# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clipboard
pkgver=5.0.0.5
pkgrel=1
pkgdesc='DDE clipboard manager component'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-clipboard"
license=('GPL3')
depends=('deepin-qt5integration' 'gio-qt')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-clipboard/archive/$pkgver.tar.gz")
sha512sums=('741635953286a95a1b526e093b5b8af3447c2462b38cfb295421b598bb232ca594a14664fa305bc9d393e57c1ac464a9582ffc6e5e30d7831ddec0a906714a6b')

prepare() {
  cd dde-clipboard-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' dde-clipboard/pixmaplabel.cpp dde-clipboard/itemwidget.cpp
  sed -i '$aOnlyShowIn=Deepin' dde-clipboard/dde-clipboard.desktop
}

build() {
  cd dde-clipboard-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-clipboard-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
