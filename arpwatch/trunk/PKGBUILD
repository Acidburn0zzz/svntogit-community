# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arpwatch
pkgver=2.1a15
pkgrel=8
pkgdesc="arpwatch and arpsnmp network monitoring tools"
arch=(i686 x86_64)
url="ftp://ftp.ee.lbl.gov/"
license=("GPL")
depends=(libpcap)
source=(ftp://ftp.ee.lbl.gov/$pkgname-$pkgver.tar.gz)
md5sums=('cebfeb99c4a7c2a6cee2564770415fe7')

build() {
  cd $srcdir/$pkgname-$pkgver

  sed -i 's|ARPDIR = $(prefix)/arpwatch|ARPDIR = /var/lib/arpwatch|' Makefile.in

  ./configure --prefix=/usr
  make SENDMAIL=/usr/sbin/sendmail
}

package() {
  cd $srcdir/$pkgname-$pkgver

  install -d -m 0755 $pkgdir/usr/sbin
  make DESTDIR=$pkgdir install
  install -D -m 0644 arpwatch.8 $pkgdir/usr/share/man/man8/arpwatch.8
  install -D -m 0644 arpsnmp.8 $pkgdir/usr/share/man/man8/arpsnmp.8
  install -d -m 0755 $pkgdir/var/lib/arpwatch
  touch $pkgdir/var/lib/arpwatch/arp.dat
}
