# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.20
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver.tar.gz"
        deepin-calculator-qt5.15.patch)
sha512sums=('3f3c68cb860d8f3912d53957f33dd17b84261d37969577de4b29205b54efa9c723f9e087279a9165b7568cc0dad7f71e1cbee886597aacf9c14da8be06bd5091'
            'd38fed51d16655c4662bdea3a7e19cbcb27b2d04bccaad90e0089652068b6041b73a8512e717ff18c47426046dcfe188ba1535ef19fc21b74fae7dd4e4933e03')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '/<QList>/i #include <QObject>' src/core/session.h

  patch -p1 -i ../deepin-calculator-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-calculator-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-calculator-$pkgver
  make DESTDIR="$pkgdir" install
}
