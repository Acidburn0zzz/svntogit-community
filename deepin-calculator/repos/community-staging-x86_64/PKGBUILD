# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.10
pkgrel=2
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('qt5-tools' 'python')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver.tar.gz"
         deepin-calculator-qt5.15.patch)
sha512sums=('aa9bfd63d204933d06fea414b06cac8f7fb1cf7a4f3e1bb0f936d1a0e49a93f89e9a30641e4188d7e7420ba9fd9b522d2e82b931eb2b37db204cbecbf9163172'
            '09af5aca1fe25657fddfdc825583b4fa7941ea4bf3883a8c87629459e89ba44e0e311a17c5c759f8b309af69e44c18580ecffeeae5c710ab33ddbea1c7e97157')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '/<QList>/i #include <QObject>' core/session.h

  patch -p1 -i ../deepin-calculator-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-calculator-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-calculator-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
