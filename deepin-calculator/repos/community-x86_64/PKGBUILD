# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.40
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f84137df97a3f31947d246f8395241900e3019a12d3993ebb0c6abfb5c5d783c07f20954a9ff521bfc4ea717f143db7a50cb8b793ff7c50537b442349e5a9904')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '1i#include <QPainterPath>' src/views/simplelistdelegate.cpp

  # debundle gmock/gtest
  sed -i '/third_party/d' tests/CMakeLists.txt
  rm -r tests/third_party
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
