# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.25
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('eda3f301e220542b821e8ae97c8fa0cfc68915c8506f62db470afa1e90e0297830a20e0d486c8021aa08c40e163e17932e2a0ac1c98130cc9a82ee93df407469')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '1i#include <QPainterPath>' src/modules/simplelistdelegate.cpp src/widgets/equalbutton.cpp
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
