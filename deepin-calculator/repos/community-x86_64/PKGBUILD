# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.18
pkgrel=2
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver.tar.gz"
        $pkgname-file-installation.patch::https://github.com/linuxdeepin/deepin-calculator/pull/38.patch
        deepin-calculator-qt5.15.patch)
sha512sums=('6afa9ed59cddcd0d4b1ec1b3b0a159366f49ec5468d6cbc3a73b3a347da844c3aeb4c03524329087a7a19e5b966ff9e96470f121bff9ab7cbe25c35c2449d3fd'
            '5fd4ceae86467bdc61c3ea5ef0788f920a0715ce0030e110d79126354fa8122e4bcfff9dc80edf233fc2f956a2f992a765c3c585d6f3eef61aa1c1d12ff68c3a'
            'd38fed51d16655c4662bdea3a7e19cbcb27b2d04bccaad90e0089652068b6041b73a8512e717ff18c47426046dcfe188ba1535ef19fc21b74fae7dd4e4933e03')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '/<QList>/i #include <QObject>' src/core/session.h

  patch -p1 -i ../$pkgname-file-installation.patch
  patch -p1 -i ../deepin-calculator-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-calculator-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-calculator-$pkgver
  make DESTDIR="$pkgdir" install
}
