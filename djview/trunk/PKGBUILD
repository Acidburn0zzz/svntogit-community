# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview
pkgver=4.12
pkgrel=1
pkgdesc='Portable DjVu viewer and browser plugin'
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
arch=('x86_64')
makedepends=('qt5-tools')
depends=('qt5-base' 'djvulibre'
         'libxkbcommon-x11' 'libsm')
source=("https://downloads.sourceforge.net/djvu/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5673c6a8b7e195b91a1720b24091915b8145de34879db1158bc936b100eaf3e3')

prepare() {
  cd ${pkgname}4-${pkgver}
  ./autogen.sh
}

build() {
  cd ${pkgname}4-${pkgver}
  export QMAKE=qmake-qt5
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}4-${pkgver}
  make DESTDIR="${pkgdir}" install
  ln -s djview "${pkgdir}"/usr/bin/djview4
}
