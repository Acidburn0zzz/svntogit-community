# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Carsten Feuls <archlinux@carstenfeuls.de>

pkgname=ruby-faraday-middleware
pkgver=0.13.1
pkgrel=1
pkgdesc='Various middleware for Faraday'
arch=('any')
url='https://github.com/lostisland/faraday_middleware'
license=('MIT')
depends=('ruby' 'ruby-faraday')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("https://rubygems.org/downloads/faraday_middleware-$pkgver.gem")
noextract=("faraday_middleware-$pkgver.gem")
sha512sums=('8f69cb800246bdd2311cd062a1fe1ab6d4a33c14f59e94fe517a7a62c5d69b4f52ef799a786b3979baf1ff720422d39d09f31b28e4020d4769053d6ca691dd8a')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir/usr/bin" faraday_middleware-$pkgver.gem
  rm "$pkgdir/$_gemdir/cache/faraday_middleware-$pkgver.gem"
  install -D -m644 "$pkgdir/$_gemdir/gems/faraday_middleware-$pkgver/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
