# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libspatialindex
pkgver=1.8.5
pkgrel=1
pkgdesc="C++ implementation of R*-tree, an MVR-tree and a TPR-tree with C API"
arch=('x86_64')
url="https://github.com/libspatialindex/libspatialindex"
license=('MIT')
depends=('gcc-libs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libspatialindex/libspatialindex/archive/$pkgver.tar.gz")
sha256sums=('93cce77269612f45287b521d5afdfb245be2b93b8b6438d92f8b9e0bdb37059d')

build() {
  cd libspatialindex-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd libspatialindex-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
