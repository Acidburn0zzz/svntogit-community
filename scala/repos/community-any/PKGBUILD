# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-docs scala-sources)
pkgver=2.12.2
pkgrel=1
_watch="https://github.com/scala/scala/releases"
_distdate=20161205-201300
_distsha=2787b47396
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('BSD')
depends=('java-runtime>=8' 'java-environment>=8')
makedepends=('sbt' 'git')
source=("scala-$pkgver.tar.gz::https://github.com/scala/scala/archive/v${pkgver}.tar.gz"
	"scala-docs-${pkgver}.tar.xz::http://www.scala-lang.org/files/archive/scala-docs-${pkgver}.txz")
sha256sums=('822ef9c8077765cf558c1bbc88e957ccae77402ca02f432053f4f3bf4f91a2b1'
            'b64ac34aac4d61c8925ec51fcedc13438aa2ad8d49afa25d46ba4a1d0bb87f6c')

prepare() {
  cd "$srcdir"/${pkgname}-${pkgver}
  chmod 0755 tools/*
  echo -e "#!/bin/bash\necho ${_distdate}" >tools/get-scala-commit-date
  echo -e "#!/bin/bash\necho ${_distsha}" >tools/get-scala-commit-sha
}

build() {
  cd "$srcdir"/${pkgname}-${pkgver}
  sbt dist/mkPack packageSrc packageDoc
}

package_scala() {
  depends=('java-runtime')
  optdepends=('scala-docs' 'scala-sources' 'graphviz: generate diagrams')

  cd "$srcdir"/${pkgname}-${pkgver}/build/pack

  install -d "$pkgdir"/usr/{bin,share} "$pkgdir"/usr/share/man/man1 "$pkgdir"/usr/share/scala/{bin,lib}
  cp -r lib "$pkgdir"/usr/share/scala/
#  cp -r target/scala-dist/resource_managed/main/man "$pkgdir"/usr/share/
  install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} "$pkgdir"/usr/share/scala/bin
  install -D -m0644 "$srcdir"/scala-${pkgver}/doc/LICENSE.md "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.md

  ln -s ../share/scala/bin/fsc "$pkgdir"/usr/bin/fsc
  ln -s ../share/scala/bin/scala "$pkgdir"/usr/bin/scala
  ln -s ../share/scala/bin/scalac "$pkgdir"/usr/bin/scalac
  ln -s ../share/scala/bin/scalap "$pkgdir"/usr/bin/scalap
  ln -s ../share/scala/bin/scaladoc "$pkgdir"/usr/bin/scaladoc
}

package_scala-docs() {
  replaces=('scala-doc' 'scala-devel-docs')
  pkgdesc="Scala documentation"
  depends=()

  cd "$srcdir"/scala-${pkgver}
  mkdir -p "$pkgdir"/usr/share/doc/
  cp -r api "$pkgdir"/usr/share/doc/scala
  install -D -m0644 "$srcdir"/scala-${pkgver}/doc/LICENSE.md "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.md
}

package_scala-sources() {
  replaces=('scala-src')
  pkgdesc="Scala sources"
  depends=()

  cd "$srcdir"/scala-${pkgver}
  mkdir -p "$pkgdir"/usr/share/scala/
  find . -type f -name '*-sources.jar' | while read f; do
    install -Dm0644 $f "$pkgdir"/usr/share/scala/src/`basename $f`
  done
  install -D -m0644 "$srcdir"/scala-${pkgver}/doc/LICENSE.md "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.md
}
