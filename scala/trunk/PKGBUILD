# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-doc scala-src)
pkgver=2.8.0
pkgrel=3
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('GPL')
depends=('java-runtime')
makedepends=('apache-ant')
optdepends=('sbaz: bazaar package management'
	    'scala-doc'
	    'scala-src')
source=(http://www.scala-lang.org/downloads/distrib/files/scala-$pkgver.final-sources.tgz
	http://www.scala-lang.org/downloads/distrib/files/scala-$pkgver.final-devel-docs.tgz)
md5sums=('09ce2577d1cead7f1797e5b4ac0aa118'
         '9ee2904242d4e5f6220f9f7f28f4c658')

build(){
    install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib} || return 1

    cd ${srcdir}/${pkgname}-${pkgver}.final-sources
    ant build || return 1
    ant docs || return 1
#    ant pack || return 1

}

package_scala()
{
    cd ${srcdir}/${pkgname}-${pkgver}.final-sources

    mkdir -p ${pkgdir}/usr/share/scala/bin ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/bin

    install -D -m0644 docs/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

    cd build/
    cp -r pack/lib ${pkgdir}/usr/share/scala/
    install -m 755 pack/bin/{fsc,scala,scalac,scaladoc} ${pkgdir}/usr/share/scala/bin
    install -m 644 scaladoc/manual/man/man1/{fsc,sbaz,scala,scalac,scaladoc}.1 ${pkgdir}/usr/share/man/man1

    ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
    ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
    ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
    ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}

package_scala-doc()
{
    pkgdesc="Scala documentation"
    depends=('scala')

    cd ${srcdir}
    mkdir -p $pkgdir/usr/share/doc/
    cp -r scala-$pkgver.final-devel-docs $pkgdir/usr/share/doc/scala
}

package_scala-src()
{
    pkgdesc="Scala sources"
    depends=('scala')

    cd ${srcdir}/${pkgname}-${pkgver}.final-sources
    mkdir -p $pkgdir/usr/share/scala/src/
    find . -type f -name \*src.jar -exec install -m0644 {} $pkgdir/usr/share/scala/src/ \;
}
