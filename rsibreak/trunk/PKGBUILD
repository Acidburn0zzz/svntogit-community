# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rsibreak
pkgver=0.12.11
pkgrel=1
pkgdesc="Takes care of your health and regularly breaks your work to avoid repetitive strain injury (RSI)"
url="https://userbase.kde.org/RSIBreak"
arch=(x86_64)
license=(GPL)
depends=(knotifyconfig kidletime hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('1bf2e7d837a0807542b6f505a7fa71aa48e576ef4bb68ad0d4dddeba9e795d25'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
