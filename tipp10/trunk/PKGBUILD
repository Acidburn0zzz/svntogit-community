# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: toliman <altergan@hs-albsig.de>

pkgname=tipp10
pkgver=2.0.3
pkgrel=3
pkgdesc="intelligent typing tutor (german only)"
arch=('i686' 'x86_64')
url="http://www.tipp10.de/"
license=('GPL2')
depends=('qt>=4.2.2')
makedepends=('unzip' 'make')
source=(tipp10.desktop tipp10.png defines.h.patch gcc-4.5.patch \
  "http://www.tipp10.de/download_hidden/tipp10_source_v2-0-3.zip")

build() {
  sed -i 's/tipp10v2.template/..\/share\/tipp10\/tipp10v2.template/' $srcdir/tipp10/def/defines.h

  cd $srcdir/tipp10
  patch -p0 < "$srcdir/defines.h.patch"
  patch -p0 < "$srcdir/gcc-4.5.patch"
  qmake
  make
}

package() {
  cd $srcdir/tipp10

  install -D tipp10 $pkgdir/usr/bin/tipp10
  install -D tipp10v2.template $pkgdir/usr/share/tipp10/tipp10v2.template
  install -D $srcdir/tipp10.png $pkgdir/usr/share/pixmaps/tipp10.png
  install -D $srcdir/tipp10.desktop $pkgdir/usr/share/applications/tipp10.desktop
}
md5sums=('e0d6c26220396551386f91ce893e040c'
         'ff788e726d357370df1e7c8b19d38fd4'
         'ce67b64cb3a1d25119acd1a43804304d'
         'a9d388ddb17ae93f13acb915bc8eba57'
         'bac29896ec992496f7e0aab992993004')
