# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: toliman <altergan@hs-albsig.de>

pkgname=tipp10
pkgver=3.1.0
pkgrel=1
pkgdesc='Intelligent touch typing tutor'
arch=('x86_64')
url=https://gitlab.com/tipp10/tipp10
license=('GPL2')
depends=('qt5-multimedia')
makedepends=('cmake' 'qt5-tools')
source=("$url/-/archive/v$pkgver/tipp10-v$pkgver.tar.gz")
sha512sums=('492abb821d3fb9bc3b3d2733c4b5525346973267b57a2ce9e517fbf431983d88bea4114c6018985f5d6ea3d92bcfa808979f80585e5f1e61dc66be6e5a025650')

build() {
  cd tipp10-v$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd tipp10-v$pkgver
  make DESTDIR="$pkgdir" install
}
