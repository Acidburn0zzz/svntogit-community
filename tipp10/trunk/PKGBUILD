# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: toliman <altergan@hs-albsig.de>

pkgname=tipp10
pkgver=2.1.0
pkgrel=2
pkgdesc="intelligent typing tutor"
arch=('i686' 'x86_64')
url="http://www.tipp10.com/"
license=('GPL2')
depends=('qt>=4.2.2')
source=(tipp10.desktop tipp10.png defines.h.patch gcc-4.5.patch \
  "http://www.tipp10.com/de/download/tipp10_source_v"${pkgver//./-}".zip")
md5sums=('e0d6c26220396551386f91ce893e040c'
         'ff788e726d357370df1e7c8b19d38fd4'
         '73a987123ca842026b7bde5e89370b86'
         'a9d388ddb17ae93f13acb915bc8eba57'
         '975409935621147d56898a839bff4b08')

_srcdir="$srcdir/tipp10_source_v${pkgver//./-}"

build() {
  cd "$_srcdir"

  sed -i 's#tipp10v2.template#../share/tipp10/tipp10v2.template#' def/defines.h
  patch -p0 < "$srcdir/defines.h.patch"
  patch -p0 < "$srcdir/gcc-4.5.patch"
  qmake
  make
}

package() {
  cd "$_srcdir"

	make DESTDIR="$pkgdir" install

  install -Dm755 tipp10 "$pkgdir/usr/bin/tipp10"
  install -Dm644 release/tipp10v2.template "$pkgdir/usr/share/tipp10/tipp10v2.template"
  install -Dm644 "$srcdir/tipp10.png" "$pkgdir/usr/share/pixmaps/tipp10.png"
  install -Dm644 "$srcdir/tipp10.desktop" "$pkgdir/usr/share/applications/tipp10.desktop"
}
