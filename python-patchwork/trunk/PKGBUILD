# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-patchwork
pkgver=1.0.0
pkgrel=1
pkgdesc="Common deployment/sysadmin operations, built on Fabric"
arch=(any)
url="https://github.com/fabric/patchwork"
license=('BSD')
depends=('fabric')
makedepends=('python-setuptools')
source=("https://github.com/fabric/patchwork/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "succeeded-fix.patch")
sha256sums=('9abab02a1a49fae4816a8aab7dbc95a46a8a1cd2762b576ca72667f646705ce4'
            'a6be3a868e6a030e50825983e949be38437cab9c88ca782d6e3d828261cf3422')

prepare() {
  cd "$srcdir"/patchwork-$pkgver
  patch -p1 -i "$srcdir"/succeeded-fix.patch
}

build() {
  cd "$srcdir"/patchwork-$pkgver
  python setup.py build
}

package() {
  cd "$srcdir"/patchwork-$pkgver
  python setup.py install -O1 --skip-build --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
