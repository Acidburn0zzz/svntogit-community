# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=sysbench
pkgver=1.0.18
pkgrel=1
pkgdesc='Scriptable multi-threaded benchmark tool for databases and systems'
url='https://github.com/akopytov/sysbench'
arch=('x86_64')
license=('GPL2')
depends=('luajit' 'mariadb-libs' 'postgresql-libs')
makedepends=('libxslt')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('4b6df75ffd64afba3292007266dc671f5b592f029ca3ed38b822fec933a343dfbc6f921666134b41690e4035506ee9f42924370b0f1d37298f743f1c0e43b554')

build() {
  cd $pkgname-$pkgver

  autoreconf -vi

  ./configure \
  	--prefix=/usr \
  	--with-pgsql \
  	--with-mysql \
  	--with-system-luajit

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

