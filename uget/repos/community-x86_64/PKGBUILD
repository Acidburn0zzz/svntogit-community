# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=uget
pkgver=1.8.0
pkgrel=3
pkgdesc="GTK+ download manager featuring download classification and HTML import"
arch=('i686' 'x86_64')
url="http://urlget.sourceforge.net/"
license=('GPL')
depends=('libnotify' 'gtk3' 'gstreamer0.10' 'curl' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
optdepends=('aria2: alternative backend')
install=$pkgname.install
source=(http://downloads.sourceforge.net/urlget/$pkgname-$pkgver.tar.gz)
md5sums=('2e0ad66ecdd0104b8fb27b29fdb44df1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix aria2 arguments
  sed -i 's/\"--enable-xml-rpc\"/"--enable-rpc"/' uget-gtk/UgSetting.c

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-appindicator --with-gtk3
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
