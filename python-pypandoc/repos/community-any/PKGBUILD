# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pypandoc
pkgver=1.6.1
pkgrel=1
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('python-setuptools' 'pandoc')
makedepends=('python-setuptools' 'pandoc')
checkdepends=('texlive-core' 'texlive-latexextra')
source=("https://github.com/bebraw/pypandoc/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('b5af221bb372dde2890a2b4aee5dd21d98e203d68f27e0ad4592ba6894e962eb0ed07b1166ffa11f26e015fc30b6b7c4834b85ca8e07e8c735bc5de1197df108')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd pypandoc-$pkgver
  python setup.py build
}

check() {
  cd pypandoc-$pkgver
  # Ignore test failures now as pandoc-citeproc is deprecated and gone
  python setup.py test || echo "Tests failed"
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
