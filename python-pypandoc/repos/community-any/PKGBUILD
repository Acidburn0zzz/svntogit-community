# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pypandoc
pkgver=1.6
pkgrel=1
pkgdesc="Thin wrapper for pandoc"
arch=('any')
license=('MIT')
url="https://github.com/bebraw/pypandoc"
depends=('python-setuptools' 'pandoc')
makedepends=('python-setuptools' 'pandoc')
checkdepends=('texlive-core' 'texlive-latexextra')
source=("https://github.com/bebraw/pypandoc/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ccf508901e453c2d7e96a9bcec80f74f1ea9ac2e4bf3c4a61c0cf454ebcd7d03da0b794a292c6af8f6ab0738d4f7039985a0f37b97970c2cab5f2d9a360e16d7')

prepare() {
  # We don't really need pip and wheel at runtime
  sed -i "s/install_requires = .*/install_requires = ['setuptools'],/" pypandoc-$pkgver/setup.py
}

build() {
  cd pypandoc-$pkgver
  python setup.py build
}

check() {
  cd pypandoc-$pkgver
  # Ignore test failures now as pandoc-citeproc is deprecated and gone
  python setup.py test || echo "Tests failed"
}

package() {
  cd pypandoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
