# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sebastien Leduc <sebastien@sleduc.fr>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_pkgname=prettytable
pkgname=python-prettytable
pkgver=2.1.0
pkgrel=1
pkgdesc="A simple Python library for easily displaying tabular data"
url="https://github.com/jazzband/prettytable"
arch=('any')
license=('BSD')
depends=('python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest')
conflicts=('python-ptable')
source=("https://files.pythonhosted.org/packages/source/p/prettyTable/prettytable-${pkgver}.tar.gz")
sha512sums=('8eaad22c66299ca3f66cc167e1a94aa41e3b2699da4ff48a0e81927ee8cc623d3147ae29a0bef92370d07d6a7fb6b1eac535fca7090562419aed219b9483edd5')

export SETUPTOOLSA_SCM_PRETEND_VERSION=$pkgver

build() {
  cd prettytable-$pkgver
  python setup.py build
}

check() {
  cd prettytable-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD/src" pytest
}

package() {
  cd prettytable-$pkgver
  python setup.py install --root="$pkgdir/"
}
