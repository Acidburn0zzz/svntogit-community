# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-swiftclient
pkgver=3.9.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
depends=('python-six' 'python-requests')
makedepends=('python-setuptools' 'python-pbr' 'python-requests' )
checkdepends=('python-stestr' 'python-mock' 'python-keystoneauth1')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('8255e6cd8a54bb792072c2c0947a0405a8f43fa40a75f5ea63ab538c005f1c0dd4100e68e6c5d37247d4f0cd6cedf0e8218ebada2595c32409647dde3503982c')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  stestr run
}

package() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
