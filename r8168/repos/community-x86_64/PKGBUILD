# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Bob Fanger < bfanger(at)gmail >
# Contributor: Filip <fila pruda com>, Det < nimetonmaili(at)gmail >

pkgname=r8168
pkgver=8.027.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux>=3.2' 'linux<3.3')
makedepends=('linux-headers')
source=(http://r8168.googlecode.com/files/$pkgname-$pkgver.tar.bz2 r8168-linux32.patch)
install=$pkgname.install

_extramodules=extramodules-3.2-ARCH
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 -i "$srcdir/r8168-linux32.patch"
	make clean SYSSRC=/usr/src/linux-$_kernver modules
}

package() {
	cd "$srcdir/$pkgname-$pkgver/src"
	install -D -m644 $pkgname.ko "$pkgdir/lib/modules/$_extramodules/$pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('98155ce1ad5c248eeb7a93c9954d8344'
         'd965f0a8f744cf859231b9e339052ffd')
sha256sums=('5bc3b44bd69a05e45ab13be727256a95adf18a658f726141283046b397dc22e2'
            '9575bd9b4eb21496308d8682d1fbc537515052d53122f657d3ade4b9dd72f3da')
