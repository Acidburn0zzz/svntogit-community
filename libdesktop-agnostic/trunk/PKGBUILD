# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libdesktop-agnostic
pkgver=0.3.91
pkgrel=0.20101213
_pkgver=0.3.90
pkgdesc="Provides an extensible config API, a unified virtual fs API, and a desktop item editor for GLib-based projects."
arch=('i686' 'x86_64')
url="https://launchpad.net/libdesktop-agnostic"
license=('GPL' 'LGPL')
depends=('glib2' 'gtk2' 'pygtk' 'pygobject' 'gconf')
makedepends=('gobject-introspection' 'vala>=0.8.1' 'intltool')
options=('!libtool')
source=(https://launchpad.net/${pkgname}/0.4/${_pkgver}/+download/${pkgname}-${_pkgver}.tar.gz
        various-fixes.patch)
backup=(etc/xdg/libdesktop-agnostic/desktop-agnostic.ini)
md5sums=('67141440b7fa77e2d704e7368c067212'
         '255ccc24f65a56cdc4346db440546dfb')

build() {
	cd ${srcdir}/${pkgname}-${_pkgver}

        # Diff from various-fixes branch (revision 425)
        # https://code.launchpad.net/~awn-core/libdesktop-agnostic/various-fixes
        patch -Np1 -i "$srcdir/various-fixes.patch"

        # Force source regeneration
        rm -r gen_src

	# fix for python 2.7
	sed -i 's:^#!.*bin/env python:#!/usr/bin/env python2:' waf
	export PYTHON=python2
	
	./waf configure --config-backends=gconf,keyfile --vfs-backends=gio \
	    --desktop-entry-backends=gio,glib --prefix=/usr --sysconfdir=/etc
	./waf
}

package() {
	cd ${srcdir}/${pkgname}-${_pkgver}

	./waf install --destdir=${pkgdir} --sysconfdir=${pkgdir}/etc
}
