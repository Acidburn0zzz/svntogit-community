# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=15.12.60
pkgrel=2
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        input-keyboard.patch)
sha512sums=('235346ff4bcb93766a10d6a4297f3ca3f697178bbb32bca8bebaf92975d5a108d5cb16595cdbe87b031f457d13f85d6c4bdebbd2a7e127db28d8de81a8387376'
            '17f880ea00ec40b1bd5de9eff98e89110f70320ec737ec220141c3d20ba38e4f780ba994bf185186c1e5c18944984fc666c31831926f899ad319debd2482b2d2')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../input-keyboard.patch
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
