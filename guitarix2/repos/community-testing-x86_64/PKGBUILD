# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=guitarix2
pkgver=0.35.3
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('i686' 'x86_64')
url="http://guitarix.sourceforge.net/"
license=('GPL')
depends=('gtkmm' 'liblrdf' 'lilv' 'bluez-libs' 'boost-libs' 'zita-convolver' 'zita-resampler')
makedepends=('python' 'python2' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
source=("http://download.sourceforge.net/project/guitarix/guitarix/$pkgname-$pkgver.tar.xz")
md5sums=('d073e551b2da7d339e738f932150d581')

build() {
  cd guitarix-$pkgver
  python2 waf configure --prefix=/usr \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd guitarix-$pkgver
  python2 waf install --destdir="$pkgdir"
}
