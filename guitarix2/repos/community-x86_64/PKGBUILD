# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_name=guitarix
pkgname=guitarix2
pkgver=0.37.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('bluez-libs' 'boost-libs' 'gtkmm' 'liblrdf' 'lilv' 'ttf-roboto' 'zita-convolver' 'zita-resampler')
makedepends=('python2' 'boost' 'eigen' 'gperf' 'intltool' 'lv2')
optdepends=('meterbridge: sound meters')
provides=('guitarix' 'gx_head')
conflicts=('guitarix' 'gx_head')
replaces=('guitarix' 'gx_head')
source=("https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}-${pkgver}.tar.xz")
sha512sums=('931ed5e614b41b0bf2d85e214a3badeb834f59757a7431bb07938ecc2da949b5c9ca8a1883bea9a217c3320de51baa8804036e5dd0f476253a486aa8995f81ba')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fixing all (hopefully) relevant scripts: https://sourceforge.net/p/guitarix/bugs/43/
  sed -e 's/python/&2/g' \
    -i src/{faust,LV2/*,plugins,ladspa,gx_head}/wscript \
    -i src/gx_head/builder/make \
    -i src/gx_head/engine/gen_tube_tables \
    -i tools/{check_rpc,gcov.py,convert-0.06-format,ampsim/DK/quik.py,undiag,make_jsonrpc_methods} \
    -i tools/{coverage,dsp2cc,dsp2insert}
}

build() {
  cd "${pkgname}-${pkgver}"
  # build without faust support: https://sourceforge.net/p/guitarix/bugs/44/
  python2 waf configure --prefix=/usr \
                        --enable-nls \
                        --ladspa \
                        --new-ladspa \
                        --no-desktop-update \
                        --no-ldconfig \
                        --no-faust \
                        --shared-lib \
                        --lib-dev
  python2 waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python2 waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
