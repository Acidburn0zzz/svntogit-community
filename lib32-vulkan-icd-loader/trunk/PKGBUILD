# Maintainer: Laurent Carlier <lordheavym@gmail.com>

pkgname=lib32-vulkan-icd-loader
pkgver=1.1.107
pkgrel=1
arch=(x86_64)
pkgdesc="Vulkan Installable Client Driver (ICD) Loader (32-bit)"
url="https://www.khronos.org/vulkan/"
license=('custom')
makedepends=(cmake python lib32-libx11 lib32-libxrandr lib32-wayland vulkan-headers git)
depends=(lib32-glibc)
optdepends=('lib32-vulkan-driver: packaged vulkan driver')
source=("https://github.com/KhronosGroup/Vulkan-Loader/archive/v${pkgver}.tar.gz")
#source=("git+https://github.com/KhronosGroup/Vulkan-Loader.git#commit=e1eafa18e17d00374253bcd37d015befa89fcc43")
sha256sums=('4a20b7887612999d0190c7c405cde84d1e5367984755b6605359addc38c28f1f')

#pkgver() {
#  cd "${srcdir}/Vulkan-Loader"
#
#  echo 1.1.99+$(git rev-list --count HEAD)+$(git rev-parse --short HEAD)
#}

build() {
  export ASFLAGS=--32
  export CC="gcc -m32"
  export CXX="g++ -m32"                                                                                                                                                                                                            
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig" 

  cd "${srcdir}"/Vulkan-Loader*

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_INSTALL_DATADIR=/share \
    -DCMAKE_SKIP_RPATH=True \
    -DBUILD_TESTS=Off \
    -DBUILD_WSI_XCB_SUPPORT=On \
    -DBUILD_WSI_XLIB_SUPPORT=On \
    -DBUILD_WSI_WAYLAND_SUPPORT=On \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
}

package() {
  cd "${srcdir}"/Vulkan-Loader*/build
  
  make DESTDIR="${pkgdir}" install

  install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
  install -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/
}
