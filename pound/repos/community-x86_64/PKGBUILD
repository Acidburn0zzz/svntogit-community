# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=pound
pkgver=2.6
pkgrel=2
pkgdesc="A reverse proxy, load balancer, and SSL wrapper"
arch=('i686' 'x86_64')
url="http://www.apsis.ch/pound/index_html"
license=('GPL')
depends=('pcre' 'openssl')
backup=(etc/pound/pound.cfg)
source=(http://www.apsis.ch/pound/Pound-$pkgver.tgz
	pound.init
	pound.runit
	pound.cfg)
md5sums=('8c913b527332694943c4c67c8f152071'
         'bd3bddb6dc17fdcab824815a4705b055'
         '4bd5e2a8d8e707eb900ae88929e00d4c'
         '8937808acd22c6391ebe4340af8df854')

build() {
    cd $srcdir/Pound-$pkgver
    ./configure --prefix=/usr --sysconfdir=/etc/pound
    make 
}
package() {
    cd $srcdir/Pound-$pkgver
    install -d $pkgdir/usr/sbin
    make DESTDIR=$pkgdir install 
    chown root.root -R $pkgdir/usr
    install -d $pkgdir/etc/rc.d
    install -m0755 $srcdir/pound.init $pkgdir/etc/rc.d/pound
    install -d $pkgdir/etc/sv
    install -d $pkgdir/etc/sv/pound
    install -m0755 $srcdir/pound.runit $pkgdir/etc/sv/pound/run
    install -D -m0644 $srcdir/pound.cfg $pkgdir/etc/pound/pound.cfg
}
