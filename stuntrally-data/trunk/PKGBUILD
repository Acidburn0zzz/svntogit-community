# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=stuntrally-data
_pkgname=stuntrally
pkgver=2.0
pkgrel=1
pkgdesc="Stunt Rally game with track editor, based on VDrift (data files)"
arch=('any')
license=('GPL3')
url="http://code.google.com/p/vdrift-ogre"
makedepends=('cmake' 'boost' 'libvorbis' 'mygui' 'sdl' 'enet' 'hicolor-icon-theme' 'libxcursor')
source=(src_${pkgver}.tar.gz::https://nodeload.github.com/${_pkgname}/stuntrally/tar.gz/${pkgver}
        tracks_${pkgver}.tar.gz::https://nodeload.github.com/${_pkgname}/tracks/tar.gz/${pkgver})
md5sums=('a92d850c100220246833b330d1aca9bd'
         '37439c50f9641610b5b7e767ec52eebb')

build() {
  # build the sources
  cd "${srcdir}/stuntrally-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr" \
      -DSHARE_INSTALL="share/stuntrally"
  make

  # build the tracks
  cd "${srcdir}/tracks-${pkgver}/"

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX="/usr"
  make
}

package() {
  #install the sources
  cd "${srcdir}/stuntrally-${pkgver}/build/"
  make DESTDIR="${pkgdir}" install

  # install the tracks
  cd "${srcdir}/tracks-${pkgver}/build"
  make DESTDIR="${pkgdir}/usr/share/stuntrally/" install

  # clean up
  rm -rf "${pkgdir}/usr/share/stuntrally/tracks/build"
  rm -rf "${pkgdir}/usr/share/icons"
  rm -rf "${pkgdir}/usr/share/applications"
  rm -rf "${pkgdir}/usr/bin"
}
