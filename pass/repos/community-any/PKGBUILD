# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Simon Zimmermann <simon@insmo.com>
# Contributor: Daniel M. Capella <polyzen@archlinux.org>

pkgname='pass'
pkgver=1.7.3
pkgrel=3
pkgdesc='Stores, retrieves, generates, and synchronizes passwords securely'
arch=('any')
url='https://www.passwordstore.org/'
license=('GPL2')
depends=('bash' 'gnupg' 'tree')
checkdepends=('git')
optdepends=('git: for Git support'
            'dmenu: for passmenu'
            'xdotool: to type passwords with passmenu'
            'qrencode: for QR code support'
            'vim-plugin-runtime: for redact_pass.vim'
            'wl-clipboard: for clipboard support on Wayland'
            'xclip: for clipboard support on X11')
replaces=('passmenu')
provides=('passmenu')
conflicts=('passmenu')
source=("https://git.zx2c4.com/password-store/snapshot/password-store-${pkgver}.tar.xz")
sha256sums=('2b6c65846ebace9a15a118503dcd31b6440949a30d3b5291dfb5b1615b99a3f4')

check() {
  cd "${srcdir}/password-store-$pkgver/"
  make test
}

package() {
  cd "${srcdir}/password-store-$pkgver/"
  make DESTDIR="${pkgdir}" WITH_ALLCOMP=yes install

  install -Dm0755 -t "${pkgdir}/usr/bin" contrib/dmenu/passmenu
  install -Dm0644 -t "${pkgdir}/usr/share/vim/vimfiles/plugin" \
    contrib/vim/redact_pass.vim
}
