# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=gitg
pkgver=0.0.6
pkgrel=1
pkgdesc='A GIT repository viewer based on GTK+'
arch=('i686' 'x86_64')
url='http://trac.novowork.com/gitg/'
license=('GPL')
depends=('gconf' 'gtksourceview2' 'git')
makedepends=('intltool' 'pkgconfig')
install="${pkgname}.install"
source=("http://trac.novowork.com/gitg/raw-attachment/wiki/Releases/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5aab2b6fc97d72f2063f91f12dc014d8')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --disable-schemas-install \
    --with-gconf-schema-file-dir=/usr/share/gconf/schemas

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
