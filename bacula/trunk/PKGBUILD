# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Calogero Lo Leggio <kalos@autistici.org>
# Contributor: Matias Hernandez <msdark@archlinux.cl>

pkgname=bacula
pkgver=5.2.6
pkgrel=2
pkgdesc="An advanced backup tool with network and tape changer support (MySQL backend)"
arch=("i686" "x86_64")
url="http://www.${pkgname}.org"
license=("GPL")
depends=('openssl' 'lzo2' 'zlib' 'python2')
makedepends=("qt" "wxgtk" "gtk2" "libmysqlclient" "postgresql-libs" "sqlite3")
optdepends=("qt: for bat"
            "wxgtk: for bwx console"
            "gtk2: for tray monitor"
            "libmysqlclient: mysql backend"
            "postgresql-libs: postgresql backend"
            "sqlite3: sqlite3 backend"
            "python2: python support")
options=(!buildflags !libtool)
conflicts=("${pkgname}-sqlite" "${pkgname}-postgresql" "${pkgname}-client")
backup=("etc/${pkgname}/bconsole.conf"
		"etc/${pkgname}/${pkgname}-dir.conf"
		"etc/${pkgname}/${pkgname}-fd.conf"
		"etc/${pkgname}/${pkgname}-sd.conf")
install="${pkgname}.install"
source=(http://downloads.sourceforge.net/project/bacula/bacula/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-sd.rc.d
        ${pkgname}-fd.rc.d
        ${pkgname}-dir.rc.d)
md5sums=('914483b92539598678542bd2ddb28043'
         '6311f10c58261c4ee6e26ae2de5580a3'
         '7c240ed89c6e42b379386d27e163d3bc'
         'bb31d783b3362961eac4746219e3e3ff')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

    sed -i "s/python-config/python2-config/g" configure

	# Build
	./configure --prefix=/usr \
		--enable-build-dird --enable-build-stored --enable-smartalloc \
		--enable-bat --enable-tray-monitor --enable-bwx-console \
		--with-mysql --with-postgresql --with-sqlite3 --with-openssl --with-python \
		--with-dir-user=${pkgname} --with-dir-group=${pkgname} \
		--with-sd-user=${pkgname} --with-sd-group=${pkgname} \
		--sysconfdir=/etc/${pkgname} --with-scriptdir=/etc/${pkgname}/scripts \
		--with-working-dir=/var/cache/${pkgname}/working \
		--with-subsys-dir=/var/cache/${pkgname}/working \
		--with-archivedir=/var/cache/${pkgname}/archive \
        --with-pid-dir=/var/run/bacula/

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install

	# Permissions
	chmod a+x ${pkgdir}/etc/${pkgname}/scripts/{update_${pkgname}_tables,delete_catalog_backup,update_mysql_tables,make_catalog_backup,bconsole}

	# Daemons
	mkdir -p ${pkgdir}/etc/rc.d/
    install -Dm755 ${srcdir}/bacula-dir.rc.d ${pkgdir}/etc/rc.d/bacula-dir
    install -Dm755 ${srcdir}/bacula-fd.rc.d ${pkgdir}/etc/rc.d/bacula-fd
    install -Dm755 ${srcdir}/bacula-sd.rc.d ${pkgdir}/etc/rc.d/bacula-sd

	# Logs
	install -D -m644 ${srcdir}/${pkgname}-${pkgver}/scripts/logrotate ${pkgdir}/etc/logrotate.d/${pkgname}
	sed -i "s|/var/cache/${pkgname}/working/log|/var/log/${pkgname}.log|g" ${pkgdir}/etc/{${pkgname}/${pkgname}-dir.conf,logrotate.d/${pkgname}}

    # Fix permissions
    chmod -R 755 ${pkgdir}/usr/sbin
}

