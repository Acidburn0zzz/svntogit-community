# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=monad-logger
pkgname=haskell-monad-logger
pkgver=0.3.25
pkgrel=4
pkgdesc="A class of monads which can log messages."
url="https://github.com/kazu-yamamoto/logger"
license=("MIT")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-blaze-builder" "haskell-conduit" "haskell-conduit-extra"
         "haskell-exceptions" "haskell-fast-logger" "haskell-lifted-base" "haskell-monad-control"
         "haskell-monad-loops" "haskell-mtl" "haskell-resourcet" "haskell-stm" "haskell-stm-chans"
         "haskell-text" "haskell-transformers-base" "haskell-transformers-compat")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('142805f90d3898b3adc6026673fda753fe0ae5d3b7b74f7422742a2e24d27fbeb99da2dab2c4b73e67d9ea072758dd1fb372fd3ff364a8a528fbefe89fad59a7')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -ftemplate_haskell
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
