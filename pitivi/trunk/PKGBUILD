# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Gabor Nyekhelyi (n0gabor) <n0gabor@vipmail.hu>

pkgname=pitivi
pkgver=0.97.1
pkgrel=3
pkgdesc="Editor for audio/video projects using the GStreamer framework"
arch=('i686' 'x86_64')
url="http://www.pitivi.org/"
license=('LGPL')
depends=('gtk3' 'gst-editing-services' 'gst-plugins-good' 'gst-python'
         'libnotify' 'python-gobject' 'python-numpy' 'python-cairo'
         'desktop-file-utils' 'python-matplotlib' 'gst-plugins-bad'
         'python-dbus' 'gst-transcoder')
makedepends=('intltool' 'itstool' 'git' 'python-nose' 'meson')
optdepends=('frei0r-plugins: additional video effects, clip transformation feature'
            'gst-libav: additional multimedia codecs'
            'gst-plugins-ugly: additional multimedia codecs'
            'gnome-desktop: file thumbnails provided by GNOME'\''s thumbnailers')
source=(https://ftp.gnome.org/pub/GNOME/sources/$pkgname/0.97/$pkgname-$pkgver.tar.xz)
sha256sums=('41b23cfd21353647e55a0b2e86bab368b61d6f1a76cc132e80ca0dd27f000e77')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  [ -d "$pkgdir"/usr/lib64 ] && mv "$pkgdir"/usr/lib64 "$pkgdir"/usr/lib || true
}
