# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=ptex
pkgver=2.1.33
pkgrel=1
pkgdesc="Per-Face Texture Mapping for Production Rendering"
arch=(x86_64)
url="http://ptex.us/"
license=('BSD')
depends=(zlib)
makedepends=(git doxygen python2 cmake)
options=(!makeflags)
source=("https://github.com/wdas/ptex/archive/v${pkgver}.tar.gz")
sha512sums=('8707056b5073d23d9d82c2633bb3dfea67b8fad58f60b235683b2e43fc786a97e1e9ae44944263b085210a84bf09d326076327533de572c04b10623a45c19df5')

prepare() {
	cd "$pkgname-$pkgver"/src

    sed -i "s/python/python2/g" tests/run_tests.py
}

build() {
	cd "$pkgname-$pkgver"

    mkdir -p build && cd build
	cmake .. \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_INSTALL_PREFIX=/usr

    make
}

package() {
	cd "$pkgname-$pkgver"/build

    # install -d ${pkgdir}/usr/share
    make DESTDIR="$pkgdir" install

    # cp -r install/{bin,include,lib} ${pkgdir}/usr
    # cp -r install/doc ${pkgdir}/usr/share/

    install -Dm644 ../src/doc/License.txt ${pkgdir}/usr/share/licenses/${pkgname}/License.txt
}
