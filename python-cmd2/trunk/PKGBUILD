# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.7.4
pkgrel=1
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'python-pyperclip' 'python2-pyperclip')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock' 'vi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('9d2d84a773a6ee534336b4044e32521def400b81a7e5ac22dbc9b18ac4088d2adff34139965b382fb35d278c0f1dce83f43219a79c5fb8a642aafbfebe6dea83')

prepare() {
  cp -a cmd2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py build

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/python-cmd2/cmd2/issues/171

  cd "$srcdir"/cmd2-$pkgver
  python setup.py pytest --addopts "-k 'not test_path_completion_user_expansion'"

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py pytest --addopts "-k 'not test_path_completion_user_expansion'"
}

package_python-cmd2() {
  depends=('python-pyparsing' 'python-pyperclip')

  cd "$srcdir"/cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-pyparsing' 'python2-pyperclip')

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
