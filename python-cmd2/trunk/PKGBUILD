# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.7.8
pkgrel=1
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'python-pyperclip' 'python2-pyperclip' 'python2-subprocess32')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock' 'vi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('257b1eb30edbddb87664c32767f310c853efb8ed45f55c624467f9be11473bd8b3a9603e103f4c9e4b7ace1dc88f6b33fbfa06a3658915206c4142aa0dacf2be')

prepare() {
  cp -a cmd2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py build

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py pytest

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py pytest
}

package_python-cmd2() {
  depends=('python-pyparsing' 'python-pyperclip')

  cd "$srcdir"/cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-pyparsing' 'python2-pyperclip' 'python2-subprocess32')

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
