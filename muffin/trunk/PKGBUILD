# $Id$
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=1.1.2
pkgrel=2
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('libx11' 'clutter' 'gconf' 'gobject-introspection' 'gsettings-desktop-schemas' 'libcanberra' 'libxxf86vm' 'startup-notification' 'zenity' 'dconf')
makedepends=('libltdl' 'intltool' 'gnome-doc-utils' 'pkg-config' 'gnome-common' 'libice' 'libsm')
options=('!libtool' '!emptydirs')
replaces=('muffin-wm')
conflicts=('muffin-wm')
provides=('muffin')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/tarball/$pkgver")

build() {
  cd linuxmint-muffin-*

  autoreconf -vfi
  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin --localstatedir=/var --disable-static --disable-schemas-compile
  make
}

package() {
  cd linuxmint-muffin-*
  make DESTDIR="$pkgdir/" install
}

md5sums=('0950fa5c00a165c7fed73b0898d072d5')

# vim:set ts=2 sw=2 et:
