# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=1.7.4
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/muffin"
license=('GPL')
depends=('clutter' 'gobject-introspection' 'gsettings-desktop-schemas' \
		 'libcanberra' 'startup-notification' 'zenity' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-common')
options=('!libtool' '!emptydirs')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/muffin/tarball/$pkgver"
        "switch-applications.patch")
md5sums=('d64bfe853e6fda5ec11a06581743f774'
         'a5d5d76dc281277a8afc83d7affdcc7b')

build() {
  cd linuxmint-muffin-*

  # Fix windows switcher keybinding with default settings in GNOME 3.8
  patch -Np1 -i ../switch-applications.patch

  PYTHON=python2 ./autogen.sh --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/muffin \
  				 			 --localstatedir=/var --disable-static --disable-schemas-compile
  make
}

package() {
  cd linuxmint-muffin-*
  make DESTDIR="$pkgdir/" install
}


# vim:set ts=2 sw=2 et:
