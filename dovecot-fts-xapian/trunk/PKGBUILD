# Maintainer: George Rawlinson <george@rawlinson.net.nz>
# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

# This must be built against the version of dovecot being used,
# otherwise it will fail to load and thus work.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.18
# Make sure to bump pkgrel if changing this.

pkgname=dovecot-fts-xapian
_pkgname="${pkgname#dovecot-}"
_pkgver=1.5.4b # pacman's vercmp does not like alphabet characters without a dot prefix
pkgver=1.5.4.b
pkgrel=1
pkgdesc="Dovecot FTS plugin based on Xapian"
arch=('x86_64')
url="https://github.com/grosjo/fts-xapian"
license=('LGPL2.1')
depends=("dovecot=${_dcpkgver}" 'icu' 'xapian-core')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_pkgver.tar.gz")
sha512sums=('a943d8bbfd027c5b6c249d9424e9ef61dd6928239733515f2583c7480318f18ef172d9910e9b55289e4c7bcaa6bcfc5255ba1bb871aad9de4c42bd089c2e02c9')
b2sums=('be9785db7cc107d2c1ad9d9bd1fb70a2e6fa28f8dfdf5badb466aa9874f22008ffa1e72e2d9bb63d82a6e350235499606eb91ea7b987fcdda93a0cff276749d8')

build() {
  cd "$_pkgname-$_pkgver"
  autoreconf -vi
  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot

  make

  # generate systemd unit files
  mkdir build
  sed "s:@@prefix@@:/usr/bin:" contrib/systemd/dovecot-fts-optimize.service.in \
    > build/dovecot-fts-optimize.service
  sed "s:@@index_frequency@@:daily:" contrib/systemd/dovecot-fts-optimize.timer.in \
    > build/dovecot-fts-optimize.timer
}

package() {
  cd "$_pkgname-$_pkgver"

  make DESTDIR="$pkgdir" install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # systemd integration
  install -vDm644 -t "$pkgdir/usr/lib/systemd/system" \
    build/dovecot-fts-optimize.{service,timer}
}
