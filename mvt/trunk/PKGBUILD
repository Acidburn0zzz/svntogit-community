# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joaquim Monteiro <joaquim.monteiro@protonmail.com>

pkgname=mvt
pkgver=1.4.5
pkgrel=2
pkgdesc="Mobile Verification Toolkit, a forensics tool looking for signs of device compromise"
arch=(any)
url="https://github.com/mvt-project/mvt"
license=(custom:MVT)
depends=('libusb' 'python' 'python-adb-shell' 'python-click' 'python-iosbackup' 'python-libusb1' 'python-packaging' 'python-rich' 'python-tld' 'python-tqdm' 'python-requests' 'python-simplejson')
makedepends=('python-setuptools')
source=("mvt-${pkgver}.tar.gz::https://github.com/mvt-project/mvt/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('b84b633abf45ec89f639fda388053508ef5bd4284001cf716d68cb428862b970fa018349db86705a0e8f691e89ee7b1e2ca5f065055fde3e09a03220f1204c58')
b2sums=('4e1102ef4c7440c501f9049b1a2ea6a8828459f543c7f0422782de32d5227afc37191e2d03bd186c799a81b951eb46494ab0677e197000bb5e8b370855a83d3f')

prepare() {
  cd "mvt-${pkgver}"
  sed -i 's/packaging>=21.0/packaging>=20.9/' setup.py
}

build() {
  cd "mvt-${pkgver}"
  python setup.py build
}

package() {
  cd "mvt-${pkgver}"
  python setup.py install -O1 --root="$pkgdir" --skip-build
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  local _py3_ver=$(python --version | cut -d " " -f2)
  rm -rf "$pkgdir/usr/lib/python${_py3_ver%"."*}/site-packages/tests"
}

# vim: ts=2 sw=2 et:
