# $Id$
# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# AUR Maintainer: Marcus Scheunemann

pkgbase=python-markdown-math
pkgname=('python-markdown-math' 'python2-markdown-math')
pkgver=0.5
pkgrel=2
pkgdesc="Math extension for Python-Markdown"
arch=('any')
url="https://github.com/mitya57/python-markdown-math"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz"{,.asc}
        # license not in PyPI https://github.com/mitya57/python-markdown-math/issues/20
        "${pkgbase}-${pkgver}.LICENSE::${url}/raw/${pkgver}/LICENSE")
sha256sums=('8f8803c92ac0847d18bd82a51fdb6acec90b2b54a4990650e627a0d4d3a78445'
            'SKIP'
            '790098f2d689d037aed29c5e11d185390bc1c122a6b97f91c0ccfa90f6fcdbae')
validpgpkeys=('F24299FF1BBC9018B906A4CB6026936D2F1C8AE0') # Dmitry Shachnev <mitya57@debian.org>

build() {
    cd "python-markdown-math-${pkgver}"

    python setup.py build
    python2 setup.py build
}

package_python-markdown-math() {
    depends=('python')

    cd "python-markdown-math-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 ../${pkgbase}-${pkgver}.LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-markdown-math() {
    depends=('python2')

    cd "python-markdown-math-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 ../${pkgbase}-${pkgver}.LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
