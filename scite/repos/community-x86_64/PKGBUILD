# $Id: PKGBUILD,v 1.7 2009/07/15 12:41:35 cprimier Exp $
# Maintainer: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: Jochem Kossen <j.kossen@home.nl>

pkgname=scite
pkgver=1.79
pkgrel=1
pkgdesc="A generally useful editor with facilities for building and running programs"
arch=('i686' 'x86_64')
url="http://www.scintilla.org/SciTE.html"
license=('custom:scite')
depends=('desktop-file-utils' 'gtk2')
makedepends=('unzip')
install=${pkgname}.install
source=(http://downloads.sourceforge.net/sourceforge/scintilla/${pkgname}${pkgver/\./}.zip
        SciTE.desktop makefile.patch)
md5sums=('690c3dcdae69124ce22f6cfdbf864e0a' '48526532ef677982a4b156ae90f25b6f'
         '8417a7310318423d34b6c3ae0090230e')

build() {
  cd ${srcdir}
  patch -Np0 -i ${srcdir}/makefile.patch || return 1

  cd ${srcdir}/scintilla/gtk
  sed -i "s/#ARCHCXXFLAGS#/${CXXFLAGS}/g" makefile
  make || return 1
  install -Dm644 ../License.txt ${pkgdir}/usr/share/licenses/scintilla/LICENSE

  cd ${srcdir}/${pkgname}/gtk
  sed -i "s/#ARCHCXXFLAGS#/${CXXFLAGS}/g" makefile
  make || return 1
  make DESTDIR=${pkgdir} install

  install -Dm644 ../License.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 ${srcdir}/SciTE.desktop ${pkgdir}/usr/share/applications/
  ln -sf SciTE ${pkgdir}/usr/bin/scite
}

# vim:set ts=2 sw=2 et:
