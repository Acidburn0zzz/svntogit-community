# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
pkgver=1.6
pkgrel=1
pkgdesc="Self-contained numeric library that provides an efficient and accurate implementation of complex error functions"
arch=(x86_64)
url="http://apps.jcns.fz-juelich.de/doku/sc/libcerf"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(http://apps.jcns.fz-juelich.de/src/libcerf/$pkgname-$pkgver.tgz)
sha256sums=('43b493f3b145ac38cc7b9318c301e826510c1048d02611c5798a062c33992391')

prepare() {
  mkdir -p build
  
  sed -e 's|/man/|/share/man/|g' -i $pkgname-$pkgver/man/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
