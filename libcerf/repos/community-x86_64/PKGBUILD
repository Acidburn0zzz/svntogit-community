# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
pkgver=1.10
pkgrel=1
pkgdesc="Self-contained numeric library that provides an efficient and accurate implementation of complex error functions"
arch=(x86_64)
url="http://apps.jcns.fz-juelich.de/doku/sc/libcerf"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(http://apps.jcns.fz-juelich.de/src/libcerf/$pkgname-$pkgver.tgz)
sha256sums=('6a412c13e404411d77105c731a756384970424d6f65d1c4c63758c28183d4b61')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
