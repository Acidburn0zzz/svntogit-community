# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libcerf
pkgver=1.9
pkgrel=1
pkgdesc="Self-contained numeric library that provides an efficient and accurate implementation of complex error functions"
arch=(x86_64)
url="http://apps.jcns.fz-juelich.de/doku/sc/libcerf"
license=(custom)
depends=(glibc)
makedepends=(cmake)
source=(http://apps.jcns.fz-juelich.de/src/libcerf/$pkgname-$pkgver.tgz)
sha256sums=('ee0f5a58764fa5445b916d8838086bfa2bf5bb368f8f665a35bd71dbdbedaea9')

prepare() {
  mkdir -p build
  
  sed -e 's|/man/|/share/man/|g' -i $pkgname-$pkgver/man/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
