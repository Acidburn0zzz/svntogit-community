# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
pkgname=mate-polkit
pkgver=${_ver}.0
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
groups=('mate' 'mate-gtk3')
depends=('gtk3')
makedepends=('accountsservice' 'gobject-introspection' 'intltool')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('d4add9b8ad5ffd249f961dbc7f69fd372013bd46')

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
