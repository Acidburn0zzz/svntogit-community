# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=3.4
_pluginver=3ff8f79305beabcdf0e2418868fe99f1abe26b7e
pkgrel=2
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="http://www.nomacs.org/"
license=('GPL3')
#depends=('qt4' 'exiv2' 'libraw' 'opencv')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver::https://github.com/nomacs/nomacs/archive/$pkgver.tar.gz"
        "https://github.com/nomacs/nomacs-plugins/archive/$_pluginver.zip")
md5sums=('5f1a6586f51cebd01cb2ab71b38de649'
         '0b3451952d30633de6f4abd17ad388b9')

prepare() {
  mv nomacs-plugins-$_pluginver $pkgname-$pkgver/ImageLounge/plugins
  cd $pkgname-$pkgver
  [ -d b ] || mkdir b
}

build() {
  cd $pkgname-$pkgver/b
  cmake ../ImageLounge -DCMAKE_INSTALL_PREFIX=/usr
  make -j1
}

package() {
  cd $pkgname-$pkgver/b
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
