# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.0.3
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0' 'python-cffi' 'python-six')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six')
source=("$pkgname-$pkgver.tar.gz"::https://github.com/libgit2/pygit2/archive/v$pkgver.tar.gz
        https://github.com/libgit2/pygit2/commit/0328605c855a1a52bde4bd41d507f161cf7b224f.patch)
sha256sums=('d6428fd4cd5263c76842675519b85874cfbb79db02bc32523eb784573c328a5b'
            '20ac8232a3aea678aaa9271304643fc1d767a545b7849ed9ef14a18cbeab5da7')

prepare() {
	cd "pygit2-$pkgver"
	patch -p1 -i ../0328605c855a1a52bde4bd41d507f161cf7b224f.patch
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
