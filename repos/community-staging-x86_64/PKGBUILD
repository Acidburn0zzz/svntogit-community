# Maintainer: David Runge <dave@sleepmap.de>
_name=fastnumbers
pkgname=python-fastnumbers
pkgver=2.2.1
pkgrel=2
pkgdesc="Super-fast and clean conversions to numbers"
arch=('x86_64')
url="https://github.com/SethMMorton/fastnumbers"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d6212bf3bc9a0ca60d3cf708d9a3ed3038ba82993d8e00a7d406f1e52afcbb9cd04ddb2522efb676be01db3cab89ccb2799f4c0e5c485ef71fc553873b87a045')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _py3_ver=$(python --version | cut -d " " -f2)
  export PYTHONPATH="build/lib.linux-$CARCH-${_py3_ver%"."*}:${PYTHONPATH}"
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
