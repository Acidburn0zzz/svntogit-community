# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=leptonica
pkgver=1.68
pkgrel=3
pkgdesc="Software that is broadly useful for image processing and image analysis applications"
arch=('i686' 'x86_64')
url="http://www.leptonica.com/"
license=('custom')
depends=('glibc')
makedepends=('giflib' 'gnuplot' 'libjpeg' 'libpng' 'libtiff' 'zlib')
# 'webp'
optdepends=('giflib:  for supporting gif files'
	    'gnuplot: gnuplot support'
	    'libjpeg: for supporting jpeg files'
	    'libpng:  for supporting png files'
	    'libtiff: for supporting tiff files'
	    'zlib:    for supporting compressed files')
#	    'webp:    for supporting webp files'
source=(http://www.leptonica.com/source/leptonica-${pkgver}.tar.gz)
md5sums=('5cd7092f9ff2ca7e3f3e73bfcd556403')

build() {
  cd ${srcdir}/leptonica-${pkgver}
  sed -i '1,1i#include <zlib.h>' src/pngio.c
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/leptonica-${pkgver}
  make DESTDIR=${pkgdir} install
  install -D leptonica-license.txt ${pkgdir}/usr/share/licenses/leptonica/leptonica-license.txt
}
