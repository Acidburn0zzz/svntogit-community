# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=symmetrica
pkgver=3.0.0
pkgrel=1
pkgdesc="Algorithms for symmetric groups and classical groups"
arch=(x86_64)
url="http://www.algorithm.uni-bayreuth.de/en/research/SYMMETRICA/"
depends=(glibc)
license=(custom)
source=("https://gitlab.com/sagemath/symmetrica/-/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('116ae25854a9356063197760f84e6234058becd4da2ca15151b9514bc8324405')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/symmetrica
}
