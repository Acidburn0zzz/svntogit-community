# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.1.0.1
pkgrel=3
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        $pkgname-qt-5.14-fix.patch::https://github.com/linuxdeepin/qt5integration/pull/26.patch
        deepin-qt5integration-qt5.15.patch)
sha512sums=('a302e9177eca680f13a266a2297247ac35817af7811830e4974dfa207adf73617ac16f24cdba1bfccf419ab72cc9dd1d4b5d5109f544063a4c55a59c2b4abbcf'
            '6de3090f1200d0833a5e8b567186687cee0c1f51aa6a732d0a226b903b15a1641f1da06f8ec124d36fdf46e24d28f55b1441ec81afc36c8d30f61983652b11c9'
            'e77cccce5de00881305d1c117d571ee33e18ca2d7a4b1fabb86d38786045079c7b92ae3ced4093688af23af415c978c9035498188fa63481253393eaef16bd05')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../$pkgname-qt-5.14-fix.patch
  patch -p1 -i ../deepin-qt5integration-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
