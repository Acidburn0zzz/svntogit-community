# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=3.1.1
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('x86_64')
url="https://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c')
optdepends=('perl: for contrib script postgis_restore.pl')
makedepends=('clang' 'llvm')
changelog=$pkgname.changelog
source=(https://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0e96afef586db6939d48fb22fbfbc9d0de5e6bc1722d6d553d63bb41441a2a7d')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i 's/pj_get_release/proj_info/' configure # fix for proj8
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
