# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.2.3
pkgrel=1
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url="https://www.mixxx.org"
license=('GPL2')
groups=('pro-audio')
depends=('chromaprint' 'gcc-libs' 'glibc' 'glib2' 'hidapi' 'libfaad.so'
'libFLAC.so' 'libglvnd' 'libid3tag' 'liblilv-0.so' 'libmad' 'libmodplug'
'libmp4v2' 'libogg' 'libportaudio.so' 'libportmidi.so' 'libprotobuf-lite.so'
'librubberband.so' 'libshout' 'libsndfile' 'libtcmalloc.so' 'libusb-1.0.so'
'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so' 'libx11' 'opusfile'
'qt5-base' 'qt5-script' 'qt5-svg' 'qt5-x11extras' 'qtkeychain' 'soundtouch'
'sqlite' 'taglib' 'upower' 'wavpack')
makedepends=('glu' 'lv2' 'qt5-tools' 'scons' 'vamp-plugin-sdk')
checkdepends=('xorg-server-xvfb')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-scons-python3.patch::https://github.com/mixxxdj/mixxx/commit/73d02e58bcd.patch"
        "${pkgname}-list-fix.patch::https://github.com/mixxxdj/mixxx/commit/03fad27e1f0.patch")
sha512sums=('dfa4f9ccb7d8aa486167a01e54ee5ebeb3655a4f63c7c163b66f0dd25a21f309eae3a213d253cc37ef501777961028932a9d4ac71f5befd54daca2bd2326b395'
            '3dc0e7de1dbc6ab45b42ac418c03e5dd122411b0504b122050c27fbea41e7c5a5f9414c4189978e76d58d0c04e41aebe83addf7c449f80ae0a3674b00d018539'
            'aaef4ff6a8f9384b97157dad5ad655ddd7efaecd277fc34c90e52fb67b1aba127bda4e9ba72152279210c77ca9a42d1beadcd04595f6f43a98bd7ef22dc2c880')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fixes for building with python3 based scons
  patch -Np1 -i ../${pkgname}-scons-python3.patch
  patch -Np1 -i ../${pkgname}-list-fix.patch
}

build() {
  cd "${pkgname}-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons prefix=/usr \
        build=release \
        faad=1 \
        ffmpeg=1 \
        modplug=1 \
        perftools=1 \
        perftools_profiler=1 \
        qt_sqlite_plugin=0 \
        qtkeychain=1 \
        test=1 \
        target=linux \
        virtualize=0 \
        wv=1
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run lin64_build/${pkgname}-test
}

package() {
  cd "${pkgname}-${pkgver}"
  scons prefix=/usr \
        install_root="${pkgdir}/usr" \
        install
  # removing the test executable
  rm -v "${pkgdir}/usr/bin/${pkgname}-test"
}
