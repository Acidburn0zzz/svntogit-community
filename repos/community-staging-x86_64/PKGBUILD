# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

pkgname=snd
pkgver=19.9
pkgrel=2
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'glibc' 'glib2' 'gmp' 'gsl' 'gtk3'
'libjack.so' 'libmpc' 'libsamplerate' 'pango' 'ruby')
makedepends=('flac' 'gendesk' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'pulseaudio: For pulseaudio support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('fa78c80f52b78d5b78662fe74579c63f7933383ccf114be20286405578cb929bc9845bb4b779a95ecbd09a76bb23a1d9ad4ec0bc638d73f44e6d20392b54dd74')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-jack \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
