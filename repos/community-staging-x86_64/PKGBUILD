# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-user-accounts
pkgver=2.2.1
pkgrel=2
pkgdesc='Switchboard User Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-useraccounts
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgranite.so
  libpwquality
  libswitchboard-2.0.so
  polkit
)
makedepends=(
  git
  gobject-introspection
  meson
  vala
)
source=(switchboard-plug-user-accounts::git+https://github.com/elementary/switchboard-plug-useraccounts.git#tag=4a0f2432b1d034b183b15d54474470d709238257)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-user-accounts

  git describe --tags
}

build() {
  arch-meson switchboard-plug-user-accounts build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
