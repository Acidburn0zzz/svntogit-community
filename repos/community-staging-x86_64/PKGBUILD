# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.3.7
pkgrel=2
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'qt5-styleplugins' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz")
sha512sums=('07241176609f3dcbd534505e2c564b1c5c1638ec350bad859fdeb68cbe516cc801a1ae31bc6a4eeed55abdfac488ef41ce11333080f000703f8b5b38e218b415')

prepare() {
  cd qt5integration-$pkgver
  sed -i 's|qt5xdgiconloader/2.0.0|qt5xdgiconloader/3.2.0|' platformthemeplugin/main.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
