# Maintainer: David Runge <dave@sleepmap.de>
pkgname=sc3-plugins
pkgver=3.10.0
pkgrel=3
pkgdesc="Extension plugins for the SuperCollider3 audio synthesis server."
arch=('x86_64')
url="https://github.com/supercollider/sc3-plugins"
license=('GPL2')
groups=('pro-audio')
depends=('stk' 'supercollider')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/supercollider/${pkgname}/releases/download/Version-${pkgver}/${pkgname}-${pkgver}-Source.tar.bz2"
        "fixing-supernova-check.patch")
sha512sums=('2105e049046ae595ed7e2e57be5f3d7ba20b940530ea448bde098cd2cdfdddc6ab17d7f16b27ef41f9e245f879d287ecb51f66bc0535814e0a2be4b0a806e377'
            '34a0753c116a763d4e3576c0207cf20ccc24043829d0b8a3ffa77e3928ffa98ccba9640854e4a728f698eeda23283c6f44f713c5bf155de3bf5f174f597f2805')

prepare() {
  mv -v "$pkgname-$pkgver-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fixing wrong check for SUPERNOVA
  # https://github.com/supercollider/sc3-plugins/issues/185
  patch -Np1 -i "${srcdir}/fixing-supernova-check.patch"
  mkdir build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DSC_PATH=/usr/include/SuperCollider/ \
        -DSYSTEM_STK=ON \
        -DSUPERNOVA=ON \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 ../{{DEVELOPING,README}.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
