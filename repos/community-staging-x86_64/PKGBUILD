# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contriutor: Eric Belanger <eric@archlinux.org>
# Contributor: Jo Christian Bergskås <jcberg@gmail.com>

pkgname=fceux
pkgver=2.2.0
pkgrel=3
url='http://www.fceux.com/'
pkgdesc='Fast and ultra-compatible NES/Famicom emulator'
arch=('x86_64' 'i686')
license=('GPL')
depends=('sdl' 'gtk2' 'gd' 'desktop-file-utils' 'lua51' 'libgl')
makedepends=('scons' 'mesa-libgl' 'glu')
optdepends=('xchm: for viewing the manual')
install=$pkgname.install
source=("https://sourceforge.net/projects/fceultra/files/Source%20Code/$pkgver%20src/$pkgname-$pkgver.src.tar.gz")
sha256sums=('31ad7b478d5c0911a67a4eaeae9de915d44fc95844148a135114e27d7fd1a694')

build() {
  cd "$srcdir/$pkgname"

  msg2 'Compiling...'
  scons
}

package() {
  cd "$srcdir/$pkgname"

  msg2 'Packaging binaries, data and man-pages...'
  scons install --prefix="$pkgdir/usr"

  msg2 'Packaging documentation...'
  install -Dm644 "src/drivers/win/help/$pkgname.chm" \
    "$pkgdir/usr/share/doc/$pkgname/$pkgname.chm"
  cp -r documentation/* "$pkgdir/usr/share/doc/$pkgname/"
  # Removing manpages that are already placed in the right location
  rm -f "$pkgdir/usr/share/doc/$pkgname/*.6"
  install -Dm644 changelog.txt "$pkgdir/usr/share/doc/$pkgname/"

  msg2 'Packaging desktop shortcut and icon...'
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
