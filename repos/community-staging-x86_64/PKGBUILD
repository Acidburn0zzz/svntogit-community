# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=haskell-src-meta
pkgname=haskell-src-meta
pkgver=0.8.0.3
pkgrel=32
pkgdesc="Parse source to template-haskell abstract syntax"
url="https://github.com/bmillwood/haskell-src-meta"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-src-exts' 'haskell-syb' 'haskell-th-orphans')
makedepends=('ghc' 'haskell-test-framework' 'haskell-test-framework-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        haskell-src-exts-1.21.patch)
sha512sums=('aaa17f71f8105abca0047cfa34a941bfe8b5cffb31cf788356f1f8e7c51b446d58b9fcb343c9206746b126b8360a2d0198a06400f0868382e59d9ddec312ad13'
            'f1dde1ef4921d31d2be50e5e0080cb39ef40a8ab16c31640b2166f62921a5f83cf52d6e3cada579fb0a9798e24f507bf6a8d394271baeedad2afb383efb49024')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../haskell-src-exts-1.21.patch
    sed -i -e 's/< *2.14/<3/' -e 's/< *4.12/<5/' -e 's/< *1.21/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
