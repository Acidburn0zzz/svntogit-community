# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgname=dolphin-emu
pkgver=5.0
pkgrel=3
epoch=1
pkgdesc='A Gamecube / Wii / Triforce emulator'
arch=('x86_64')
url='http://dolphin-emu.org'
license=('GPL')

makedepends=('cmake')
depends=('bluez-libs' 'enet' 'ffmpeg' 'libao' 'libevdev' 'mbedtls' 'miniupnpc'
         'openal' 'portaudio' 'soundtouch' 'xdg-utils' 'wxgtk')
optdepends=('pulseaudio: PulseAudio backend')

options=('!emptydirs')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dolphin-emu/dolphin/archive/${pkgver}.tar.gz")
sha256sums=('62c4602055767ab314ff50a3b94ea57f792832aa1629e6b1117ebce10518dc0b')

build() {
  cd dolphin-${pkgver}

  mkdir build && cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_CXX_FLAGS='-fno-pie' \
    -DENABLE_LTO='TRUE' \
    -DUSE_SHARED_ENET='TRUE' \
    -DDISTRIBUTOR='archlinux.org'
  make
}

package() {
  cd dolphin-${pkgver}/build

  make DESTDIR="${pkgdir}" install

  install -Dm 644 ../Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
}

# vim: ts=2 sw=2 et:
