# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=git-annex
pkgver=7.20190615
pkgrel=23
pkgdesc="Manage files with git, without checking their contents into git"
url="https://git-annex.branchable.com/"
license=("AGPL3")
arch=('x86_64')
depends=('git' 'lsof' 'rsync' 'ghc-libs' 'haskell-aeson' 'haskell-async' 'haskell-aws'
         'haskell-blaze-builder' 'haskell-bloomfilter' 'haskell-byteable' 'haskell-case-insensitive'
         'haskell-clientsession' 'haskell-concurrent-output' 'haskell-connection' 'haskell-conduit'
         'haskell-crypto-api' 'haskell-cryptonite' 'haskell-data-default' 'haskell-dav'
         'haskell-dbus' 'haskell-disk-free-space' 'haskell-dlist' 'haskell-edit-distance'
         'haskell-exceptions' 'haskell-fdo-notify' 'haskell-feed' 'haskell-hinotify'
         'haskell-hslogger' 'haskell-http-client' 'haskell-http-client-tls' 'haskell-http-conduit'
         'haskell-http-types' 'haskell-ifelse' 'haskell-magic' 'haskell-memory' 'haskell-microlens'
         'haskell-monad-control' 'haskell-monad-logger' 'haskell-mountpoints' 'haskell-network'
         'haskell-network-info' 'haskell-network-multicast' 'haskell-network-uri'
         'haskell-old-locale' 'haskell-optparse-applicative' 'haskell-path-pieces'
         'haskell-persistent' 'haskell-persistent-sqlite' 'haskell-persistent-template'
         'haskell-quickcheck' 'haskell-random' 'haskell-regex-tdfa' 'haskell-resourcet'
         'haskell-safesemaphore' 'haskell-sandi' 'haskell-securemem' 'haskell-shakespeare'
         'haskell-socks' 'haskell-split' 'haskell-stm-chans' 'haskell-tagsoup' 'haskell-tasty'
         'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-tasty-rerun' 'haskell-torrent'
         'haskell-unix-compat' 'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-uuid'
         'haskell-vector' 'haskell-wai' 'haskell-wai-extra' 'haskell-warp' 'haskell-warp-tls'
         'haskell-yesod' 'haskell-yesod-core' 'haskell-yesod-form' 'haskell-yesod-static')
makedepends=('chrpath' 'ghc')
source=("git+https://git.joeyh.name/git/git-annex.git#tag=$pkgver"
        socks-0.6.patch)
sha512sums=('SKIP'
            '572cfde516ac8efd7c7b7b734d7de2276e7fa4143d8ed66de101d987a81ac71b911161454979f5a6b3d89f0080423f5086eb73aa02e9ebfdde63f0167bcac608')

prepare() {
  cd git-annex
  patch -p1 -i ../socks-0.6.patch
}

build() {
  cd git-annex

  runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --disable-library-vanilla \
    --docdir="/usr/share/doc/$pkgname" \
    -ftorrentparser -fproduction -fpairing -fwebapp \
    -fassistant -fwebdav -fs3 -f-benchmark -fdbus -fmagicmime -f-networkbsd
  runhaskell Setup build
}

package() {
  cd git-annex
  runhaskell Setup copy --destdir="$pkgdir"
  make GHC="ghc -dynamic" BUILDER=true DESTDIR="$pkgdir" install-desktop install-completions

  rm "$pkgdir"/usr/share/doc/git-annex/COPYRIGHT
  rmdir "$pkgdir"/usr/share/doc/git-annex "$pkgdir"/usr/share/doc
}
