# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=2.1.10
pkgrel=4
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang' 'pam' 'iproute2')
backup=(etc/ejabberd/ejabberd.cfg etc/logrotate.d/ejabberd)
install=$pkgname.install
source=("http://www.process-one.net/downloads/ejabberd/${pkgver/_/-}/ejabberd-${pkgver}.tar.gz"
        "$pkgname.logrotate"
        "$pkgname")
sha256sums=('9dc2e5889e919a7d51f2b95b83da835116631db81550a115c02e71909ad932be'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '93f8f9a8751b9e982b332f74e71f766c3c9c4818876991cadef08fe140b83ca6')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-pam --enable-odbc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make DESTDIR="$pkgdir" install
  install -D -m 0755 "$srcdir/$pkgname" "$pkgdir/etc/rc.d/$pkgname"
  install -d "$pkgdir/var/spool/$pkgname"
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" \
    "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  rm -rf "$pkgdir/var/lock"
}

# vim:set ts=2 sw=2 et:
