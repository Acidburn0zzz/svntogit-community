# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <lolix@disroot.org> -> https://github.com/FabioLolix

pkgname=strawberry
pkgver=0.6.12
pkgrel=2
pkgdesc="A music player aimed at audio enthusiasts and music collectors"
arch=(x86_64)
url="https://www.strawbs.org/"
license=(GPL3)
depends=(chromaprint protobuf gst-plugins-base gst-plugins-good qt5-x11extras udisks2 libcdio libgpod libmtp fftw)
makedepends=(cmake boost qt5-tools)
optdepends=('gst-libav: additional codecs'
            'gst-plugins-bad: additional codecs'
            'gst-plugins-ugly: additional codecs')
source=("https://github.com/jonaski/strawberry/releases/download/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('c53207613508da507f0a9cb4b13e23532fe06b9aaf3200d44b5e4b84170aeb79')

prepare() {
  sed -e 's|libplist|libplist-2.0|' -e 's|libusbmuxd|libusbmuxd-2.0|' -i $pkgname-$pkgver/CMakeLists.txt # support libplist 2.2
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_SYSTEM_TAGLIB=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
