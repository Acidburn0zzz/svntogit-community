# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=('libvirt-python2' 'libvirt-python')
pkgver=5.8.0
pkgrel=2
pkgdesc="libvirt python binding"
arch=('x86_64')
url="https://pypi.python.org/pypi/libvirt-python"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("https://libvirt.org/sources/python/libvirt-python-${pkgver}.tar.gz")
sha512sums=('89e02e04c26738180c244a46c1d5ff4c6e839df70ec1cc045997cba25f5182ff616f36821ae180f92901c381e6471f629b4ffd9a69e45843e9412e4e715ba91d')

package_libvirt-python2() {
  depends=('python2' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python2 setup.py clean
  python2 setup.py install --root="${pkgdir}" --optimize=1
}

package_libvirt-python() {
  depends=('python' 'libvirt')

  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py clean
  python setup.py install --root="${pkgdir}" --optimize=1
}
