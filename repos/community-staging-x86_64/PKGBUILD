# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dominik Ryba <domryba@post.pl>

pkgname=hugin
pkgver=2018.0.0
pkgrel=8
pkgdesc="Panorama photo stitcher"
arch=('x86_64')
url="http://hugin.sourceforge.net/"
license=('GPL')
depends=('wxgtk2' 'boost-libs' 'libtiff' 'libpano13' 'libjpeg' 'libpng' 'openexr' 'vigra'
         'exiv2' 'glew' 'sqlite' 'lcms2' 'lapack' 'fftw' 'glu' 'libxi' 'libxmu' 'python'
         'lensfun' 'make' 'enblend-enfuse')
makedepends=('cmake' 'boost' 'tclap' 'mesa' 'swig')
optdepends=('perl-image-exiftool: GPano tags support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver/r/_r}.tar.bz2"
        hugin-cmake-3.14.patch)
sha256sums=('d3af0d066ac50e3bb243a175a64ecda136d87178419457e8822e11bcf0e565cb'
            '873c1b44a9f93a0c8febc12b2c72e0ef5cdd9000ea90b0bf2050bf25198ddae3')

prepare() {
    mkdir -p build

    cd $pkgname-$pkgver
    patch -p1 -i ../hugin-cmake-3.14.patch # Fix build with cmake 3.14
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_LAPACK=yes \
        -DBUILD_HSI=ON
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
