# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Daniel Milde <daniel at milde dot cz>
# Contributor: Danibspi danibspi <at> gmail <dot> com

pkgname=bcunit
pkgver=3.0.2
pkgrel=4
pkgdesc="Lightweight system for writing, administering, and running unit tests in C"
arch=(x86_64)
url="https://github.com/BelledonneCommunications/bcunit"
license=(LGPL)
depends=(glibc)
makedepends=(cmake)
source=("$pkgname-$pkgver.tar.gz::https://github.com/BelledonneCommunications/bcunit/archive/$pkgver.tar.gz"
         bcunit-missing-symbol.patch::"https://github.com/BelledonneCommunications/bcunit/commit/2066c388.patch")
sha256sums=('eb7090aca5006aa0cea425722cb3fa1a7139185a7d9d1734d57844f69084713f'
            '21f7fc5e1fda61c89933ef037dbf55e7e4c68e81e706de25e6a047a65a599d92')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../bcunit-missing-symbol.patch # Fix missing symbols
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DOC=ON \
    -DENABLE_STATIC=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
