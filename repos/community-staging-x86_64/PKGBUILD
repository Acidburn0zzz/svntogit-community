# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Angelo Theodorou <encelo@users.sourceforge.net>

pkgname=warzone2100
pkgver=3.2.2
pkgrel=1
pkgdesc="3D realtime strategy game on a future Earth"
url="http://wz2100.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('sdl2' 'glew' 'fribidi' 'openal' 'libvorbis' 'libtheora' 'physfs' 'ttf-dejavu' 'qt5-script' 'xorg-xrandr')
makedepends=('gawk' 'flex' 'zip' 'unzip' 'asciidoc' 'mesa')
source=("http://downloads.sourceforge.net/project/warzone2100/releases/${pkgver}/${pkgname}-${pkgver}.tar.xz"
        warzone2100.appdata.xml https://github.com/Warzone2100/warzone2100/commit/3509cccd.patch)
md5sums=('6cb1cdf382f824d61fcce6fddee67fa7'
         '103a57b73af180a1ff7301250cbdcf5d'
         '91247c61e33fa7bed81deac49f60a982')

prepare() {
  cd $pkgname-$pkgver
  # fix build
  patch -p1 -i ../3509cccd.patch
}

build() {
  cd ${pkgname}-${pkgver}
  
  # Add missing file from the tarball
  cp ${srcdir}/warzone2100.appdata.xml icons/

  ./configure --prefix=/usr --with-distributor="ArchLinux"

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
