# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-netbook
pkgver=1.18.0
pkgrel=1
pkgdesc="A simple window management tool (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libfakekey' 'mate-panel')
makedepends=('intltool')
groups=('mate-extra')
conflicts=('mate-netbook-gtk3')
replaces=('mate-netbook-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('3498fb639d9ce7d77e7a01520ce0b8d3ea840099')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${pkgname} \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
