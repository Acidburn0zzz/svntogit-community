# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=('lua-cqueues' 'lua51-cqueues' 'lua52-cqueues' 'lua53-cqueues')
pkgver=20200603
pkgrel=2
arch=('x86_64')
url='http://25thandclement.com/~william/projects/cqueues.html'
license=('MIT')
makedepends=('lua' 'lua51' 'lua52' 'lua53')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/wahern/cqueues/archive/rel-$pkgver.tar.gz"
	    "lua54.patch::https://src.fedoraproject.org/rpms/lua-cqueues/raw/5d7117c6ac061cc185f45af448845b0943577b58/f/lua-cqueues-20200603-lua-5.4.patch")
md5sums=('1c93648a274e780542b617d4729d4427'
         '4eba795b85b147b2ea3fedac94bf5567')

prepare() {
	# https://github.com/wahern/cqueues/issues/239
	# https://src.fedoraproject.org/rpms/lua-cqueues/c/5d7117c6ac061cc185f45af448845b0943577b58?branch=master
	cd "cqueues-rel-$pkgver"
	patch -p1 < ../lua54.patch
}

build() {
	cd "cqueues-rel-$pkgver"
	make prefix=/usr
}

package_lua-cqueues() {
	pkgdesc='Continuation Queues: Embeddable asynchronous networking, threading, and notification framework for Lua 5.3'

	cd "cqueues-rel-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install5.4
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-cqueues() {
	pkgdesc='Continuation Queues: Embeddable asynchronous networking, threading, and notification framework for Lua 5.1'

	cd "cqueues-rel-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install5.1
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua52-cqueues() {
	pkgdesc='Continuation Queues: Embeddable asynchronous networking, threading, and notification framework for Lua 5.2'

	cd "cqueues-rel-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install5.2
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua53-cqueues() {
	pkgdesc='Continuation Queues: Embeddable asynchronous networking, threading, and notification framework for Lua 5.3'

	cd "cqueues-rel-$pkgver"
	make DESTDIR="$pkgdir" prefix=/usr install5.3
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
