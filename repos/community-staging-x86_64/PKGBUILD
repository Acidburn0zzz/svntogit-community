# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Fangrui Song <i at maskray.me>

pkgname=ccls
pkgver=0.20190823.5
pkgrel=3
pkgdesc='C/C++/ObjC language server supporting cross references, hierarchies, completion and semantic highlighting'
arch=('x86_64')
url='https://github.com/MaskRay/ccls'
license=('Apache')
depends=('clang' 'llvm-libs' 'rapidjson')
makedepends=("cmake" "llvm")
conflicts=('ccls-git')
source=("https://github.com/MaskRay/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-CLANG_LINK_CLANG_DYLIB.patch::https://github.com/MaskRay/ccls/commit/a7a982ab9232b2730280439781f83f8055363c59.patch
        $pkgname-llvm-10.patch::https://github.com/MaskRay/ccls/commit/86e340caf00b2d337ddb839e646085bf2222be3b.patch)
sha512sums=('3c4ecac663eb463870153734fa6206701ede9d72bae3b6eae9c26adab2979ad79d2ac860c1ffb07f8cfcea4aa63afed90510734d91f830c99a86b3811959583f'
            '54eb68c64562b9cfbdaeacfc7c8292f9f85dc6bfd7d0e5ea7047ee0d72aacbe7b9f276d3d7beae94b5803018cd57e941a816c2614634c16b7c9b46a4929f0a25'
            '0ba84306b739114787dd9dfa0f7d7b48648b5c7a16480f542496fd42c24fa0cc3881ad1af1f9001a061cf4f61a33ae6ca3bdc58ab8a2e1555a586bb15503190d')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../$pkgname-CLANG_LINK_CLANG_DYLIB.patch
  patch -p1 -i ../$pkgname-llvm-10.patch
}

build() {
  cd $pkgname-$pkgver
  cmake -H. -Bbuild -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_CXX_COMPILER=clang++ -DCLANG_LINK_CLANG_DYLIB=1
  cmake --build build
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
