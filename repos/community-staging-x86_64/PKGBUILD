# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-web
pkgver=1.2
pkgrel=9
pkgdesc="Web interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc' 'hledger' "haskell-hledger-lib" "haskell-base-compat" "haskell-blaze-html"
         "haskell-blaze-markup" "haskell-clientsession" "haskell-cmdargs" "haskell-data-default"
         "haskell-hjsmin" "haskell-http-conduit" "haskell-http-client" "haskell-hunit"
         "haskell-conduit-extra" "haskell-safe" "haskell-shakespeare" "haskell-text" "haskell-wai"
         "haskell-wai-extra" "haskell-wai-handler-launch" "haskell-warp" "haskell-yesod"
         "haskell-yesod-core" "haskell-yesod-form" "haskell-yesod-static" "haskell-json" 
         "haskell-megaparsec" "haskell-mtl")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e2ed222e59165d307a87fff9e9374017bfe2f80094deea03b041669db1d9087fb9bff50580f37121b94d1a62d79f2ef446566890687e78198f9ae5f9e7e6d9e4')

prepare() {
    sed -e 's/megaparsec >=5.0 && < 5.3/megaparsec >=5.0 \&\& < 5.4/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-library-only -f-oldtime -fthreaded
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${pkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
