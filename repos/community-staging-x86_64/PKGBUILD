# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cborg
pkgname=haskell-cborg
pkgver=0.2.3.0
pkgrel=14
pkgdesc="Concise Binary Object Representation (CBOR)"
url="https://github.com/well-typed/cborg"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-half' 'haskell-primitive')
makedepends=('ghc' 'haskell-aeson' 'haskell-base-orphans' 'haskell-base16-bytestring'
             'haskell-base64-bytestring' 'haskell-quickcheck' 'haskell-scientific' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck' 'haskell-vector')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        cborg-primitive-0.7.1.patch::https://github.com/well-typed/cborg/pull/241.patch)
sha512sums=('43502fad8a880bee1a068e86336fbea283f8183befa3e838c0a708489efa70a99f33e3b04fd576a552a8f77f8cc56e5b912f686bc414cc947a07b65ec10a6cbf'
            '1a674f16f21e4a4b3937fa488f0907ec6114370bce92fafd2b2993503571fec4b7262573edf4a961c896aa56de2d3b9282f3e546a946d13015a7b72ae01e2126')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../cborg-primitive-0.7.1.patch
    sed -i 's/< *1.5/<2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE.txt
}
