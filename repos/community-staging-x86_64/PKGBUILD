# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: PelPix <kylebloss@pelpix.info>

pkgname=fdkaac
pkgver=1.0.0
pkgrel=2
pkgdesc='Command line encoder frontend for libfdk-aac'
arch=(x86_64)
url='https://github.com/nu774/fdkaac'
license=(custom)
depends=(libfdk-aac.so)
makedepends=(git)
source=(git+https://github.com/nu774/fdkaac.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  cd fdkaac

  autoreconf -fiv
}

build() {
  cd fdkaac

  ./configure \
    --prefix='/usr' \
    --disable-rpath
  make
}

package() {
  cd fdkaac

  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/fdkaac/
}

# vim: ts=2 sw=2 et:
