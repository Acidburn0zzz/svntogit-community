# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hint
pkgname=haskell-hint
pkgver=0.9.0.2
_commit=268315c40e2d2b5a8a6391f1155d4d91d6fbdf54
pkgrel=14
pkgdesc="Runtime Haskell interpreter (GHC API wrapper)"
url="https://github.com/haskell-hint/hint"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-paths' 'haskell-random'
         'haskell-temporary')
makedepends=('ghc' 'haskell-extensible-exceptions' 'haskell-hunit')
source=("https://github.com/haskell-hint/hint/archive/$_commit/$_hkgname-$_commit.tar.gz")
#source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('7cdcef8ef15227e664cb252817509cc9db43d4e16daf1cafd187b5f4092176163ec75507c6cb3864962faeeeb307d11d2eddc8f89c20c35188d82d4f543c93a0')

prepare() {
    mv $_hkgname-{$_commit,$pkgver}
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
