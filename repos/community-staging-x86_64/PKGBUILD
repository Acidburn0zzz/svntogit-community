# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgbase=vhba-module
pkgname=(vhba-module vhba-module-dkms)
pkgver=20190410
pkgrel=24
pkgdesc="Kernel module that emulates SCSI devices"
url="http://cdemu.sourceforge.net/"
arch=(x86_64)
license=(GPL)
depends=('linux')
makedepends=('linux-headers')
_extramodules=extramodules-ARCH
source=("https://downloads.sourceforge.net/cdemu/$pkgbase-$pkgver.tar.bz2"
        60-vhba.rules dkms.conf)
sha256sums=('0c18954a8c2c7e208b20afdf785501b23be9382345e6577eba5d653002862394'
            '3052cb1cadbdf4bfb0b588bb8ed80691940d8dd63dc5502943d597eaf9f40c3b'
            '8cab0ebb4fee72069d63616b0983f105b98d1261e72e9bef5509a6e60bc382a7')

prepare() {
  cd $pkgbase-$pkgver
  sed -i 's/20190302/20190410/' Makefile  # Fixup VHBA_VERSION
}

build() {
  cd $pkgbase-$pkgver
  make KERNELRELEASE="$(cat /usr/lib/modules/$_extramodules/version)"
}

package_vhba-module() {
  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 *.ko
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"

  find "$pkgdir" -name '*.ko' -exec xz {} +
}

package_vhba-module-dkms() {
  depends=(dkms)
  provides=("vhba-module=$pkgver-$pkgrel")
  conflicts=(vhba-module)

  cd $pkgbase-$pkgver
  install -Dt "$pkgdir/usr/src/$pkgbase-$pkgver" -m644 Makefile vhba.c ../dkms.conf
  install -Dt "$pkgdir/usr/lib/udev/rules.d" -m644 ../60-vhba.rules
  echo 'g cdemu - -' | install -Dm644 /dev/stdin "$pkgdir/usr/lib/sysusers.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
