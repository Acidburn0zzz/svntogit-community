# $Id: PKGBUILD 175037 2016-05-12 15:18:07Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(giac libgiac)
_pkgver=1.2.2-57
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="A free computer algebra system"
arch=(i686 x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari-sage ntl lapack libpng libjpeg fltk)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_${_pkgver}.tar.gz")
md5sums=('ad6ea155ee24b52dd7809771ab1a91af')

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_giac() {
  depends=(libgiac fltk)
  provides=(xcas)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari-sage ntl lapack libpng)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}

