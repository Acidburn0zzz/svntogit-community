# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=libvolk
pkgver=2.0.0
pkgrel=2
pkgdesc="The Vector-Optimized Library of Kernels from Gnuradio"
arch=('x86_64')
url="http://libvolk.org/"
license=('GPL3')
depends=('gcc-libs' 'boost-libs' 'orc' 'python2' 'python2-six')
makedepends=('boost' 'cmake' 'python2-mako')
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz"{,.asc})
#source=("http://libvolk.org/releases/volk-$pkgver.tar.gz")
source=("https://github.com/gnuradio/volk/releases/download/v$pkgver/volk-v$pkgver.tar.gz"{,.asc})
md5sums=('f40b09feb2e88bd0a50aecc4301e7246'
         'SKIP')
validpgpkeys=('09E749D885FA881A7E84E823385323EE6402091D'
              'D74F9F146E7F755783583158B343B2BA293E5174')

# doxygen for docs

prepare() {
  cd "$srcdir/volk-v$pkgver"
  sed -i -e "s|#![ ]*/usr/bin/env python$|&2|" $(find ./ -name '*.py')
}

build() {
  export PYTHON=python2
  cd "$srcdir/volk-v$pkgver"

  mkdir -p build
  cd build
  cmake \
    -DPYTHON_EXECUTABLE=$(which python2) \
    -DCMAKE_INSTALL_PREFIX=/usr -Wno-dev ../
  make
}

check() {
  cd "$srcdir/volk-v$pkgver/build"
  export PYTHON=python2
  return
  if [[ "$CARCH" == "x86_64" ]]; then
    make test
  fi
}

package() {
  cd "$srcdir/volk-v$pkgver/build"
  make DESTDIR="$pkgdir" install
}
