# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stack
pkgver=1.5.99.20171022
_commit=7bddfaf7f9f8cd9ec1c710fa83e77262e494eee4
pkgrel=4
pkgdesc="The Haskell Tool Stack"
url="https://github.com/commercialhaskell/stack"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-aeson" "haskell-annotated-wl-pprint" "haskell-ansi-terminal"
         "haskell-attoparsec" "haskell-base64-bytestring" "haskell-bindings-uname"
         "haskell-blaze-builder" "haskell-clock" "haskell-conduit" "haskell-conduit-extra"
         "haskell-cryptonite" "haskell-cryptonite-conduit" "haskell-echo" "haskell-exceptions"
         "haskell-extra" "haskell-fast-logger" "haskell-file-embed" "haskell-filelock"
         "haskell-fsnotify" "haskell-generic-deriving" "haskell-gitrev" "haskell-hackage-security"
         "haskell-hashable" "haskell-hastache" "haskell-http-client" "haskell-http-client-tls"
         "haskell-http-conduit" "haskell-http-types" "haskell-hpack" "haskell-memory"
         "haskell-microlens" "haskell-microlens-mtl" "haskell-mintty" "haskell-monad-logger"
         "haskell-mtl" "haskell-network-uri" "haskell-open-browser" "haskell-optparse-applicative"
         "haskell-optparse-simple" "haskell-path" "haskell-path-io" "haskell-persistent"
         "haskell-persistent-sqlite" "haskell-persistent-template" "haskell-pid1" "haskell-primitive"
         "haskell-project-template" "haskell-regex-applicative-text" "haskell-resourcet"
         "haskell-retry" "haskell-semigroups" "haskell-split" "haskell-stm" "haskell-store"
         "haskell-store-core" "haskell-streaming-commons" "haskell-tar" "haskell-text"
         "haskell-text-metrics" "haskell-tls" "haskell-unicode-transforms" "haskell-unix-compat"
         "haskell-unliftio" "haskell-unordered-containers" "haskell-vector" "haskell-yaml"
         "haskell-zip-archive" "haskell-zlib")
makedepends=('ghc')
conflicts=('haskell-stack')
replaces=('haskell-stack')
install="stack.install"
source=("$pkgname-$_commit.tar.gz::https://github.com/commercialhaskell/stack/archive/$_commit.tar.gz"
        conduit-extra-1.2.0.patch)
sha512sums=('a40f54f6ec020826315098aaed75037b9da6543a132f1f39a31dfb40a394557b3157f0b4bd00f43cf97be5fa4d72cce54e9cdbc28e1f33244dda08d6cd0b63d2'
            '7b10127222a93a2a3891f55219e388ed4f5484dc285ad858a163835eba583ba09367831a1b4e70da430dbc21e70d06ea2cd3c1491a2c957c862302d3584aea39')

prepare() {
  mv $pkgname-$_commit $pkgname-$pkgver

  cd $pkgname-$pkgver
  patch -p1 -i ../conduit-extra-1.2.0.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
      -f-disable-git-info -f-integration-tests -f-static -f-hide-dependency-versions
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

  LD_PRELOAD=$(ls "$pkgdir"/usr/lib/libHSstack-*-ghc*.so) "${pkgdir}"/usr/bin/stack --bash-completion-script /usr/bin/stack > stack_completion_script
  install -Dm644 stack_completion_script "${pkgdir}/usr/share/bash-completion/completions/stack"
}
