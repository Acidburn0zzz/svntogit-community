# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.9
pkgrel=1
pkgdesc="Python bindings for the libwayland library"
arch=(x86_64)
url="https://github.com/flacjacket/pywayland"
license=(Apache)
depends=(python-cffi wayland)
makedepends=(python-setuptools wayland-protocols)
checkdepends=(python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('3d41e54e5bdf50df39253e90a65e5ba0a27dd8797a6015e8b68a8dab918ba76a04f4917e90b99e0ac96cc317e91fa3ce84c7de04f784c0442190c70e4a3c2ca3')
b2sums=('492ace001c4183e070671e996c5f3454b0e7e29d985f88d4b501342c26d444007cae83b209abec7d3daf72a8286663288679c70a7fd391aec5fd009d43d238ed')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp temp
}

build() {
  cd "$pkgname-$pkgver"
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  export XDG_RUNTIME_DIR="${PWD}/temp"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
