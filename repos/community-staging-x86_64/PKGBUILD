# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: orbisvicis

pkgname=zita-convolver
pkgver=4.0.0
pkgrel=1
pkgdesc="A fast, partitioned convolution engine library."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL3')
depends=('fftw')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('249c87bc662d92ffbba6030bb7c1b1d78d573a7f8cb2178f162c3cd82738b16555234c7fe609d976a294d2bb169a00bf828f1505852cf005860d57d688aa6de5')

prepare() {
  cd "${pkgname}-${pkgver}/libs"

  # FS#36248 - strip -march=native
  sed -i '/native/d' Makefile

  # prevent ldconfig call
  sed -i '/ldconfig/d' Makefile
}

build() {
  cd "${pkgname}-${pkgver}/libs"
  make
}

package() {
  cd "${pkgname}-${pkgver}/libs"
  make SUFFIX="" PREFIX=/usr DESTDIR="${pkgdir}/" install

  # missing softlink
  ln -s "lib${pkgname}.so.${pkgver}" \
    "$pkgdir/usr/lib/lib${pkgname}.so.${pkgver%%.*}"

  # docs
  cd ..
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
