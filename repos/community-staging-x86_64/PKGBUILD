# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=8.0.3.33
_cudaver=11.0
_majorver=8
pkgrel=2
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=11')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/container-images/cuda/-/tree/master/dist for the appropriate cuda version
# or make an NVIDIA Developer account.
source=("https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn${_majorver}-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn${_majorver}_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('e7a19fe3495d1bbcbaef858b426d6cf02e5ad8b87515a6270130d94457611099ced174b4d52fd06723f85e08172637aa942bf31e863fdf3ad46a3e0d85271598'
            'd6b3a82e2f64be665148a4fbcc5e2a48a75960eac403baa91ee9d9a54bf91083aac7d137ebfc8fa4c476d4c43712d68545ded2e142441000230df57ac1327211'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
           "libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb")

package() {
  cd "${srcdir}"
  mkdir -p "${pkgdir}"/usr/{lib,include}

  mkdir libcudnn${_majorver}-dev
  cd libcudnn${_majorver}-dev
  ar xv "${srcdir}"/libcudnn${_majorver}-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/include/x86_64-linux-gnu/* "${pkgdir}"/usr/include/
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  # come back to srcdir
  cd "${srcdir}"

  mkdir libcudnn${_majorver}
  cd libcudnn${_majorver}
  ar xv "${srcdir}"/libcudnn${_majorver}_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  
  # come back to srcdir
  cd "${srcdir}"

  # Get rid of some Ubuntu-isms but at the same time still allow for them to
  # work because nobody is ever quite sure what the right names are.
  ln -s cudnn_v${_majorver}.h "${pkgdir}"/usr/include/cudnn.h
  ln -s cudnn_adv_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_adv_infer.h
  ln -s cudnn_adv_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_adv_train.h
  ln -s cudnn_backend_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_backend.h
  ln -s cudnn_cnn_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_cnn_infer.h
  ln -s cudnn_cnn_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_cnn_train.h
  ln -s cudnn_ops_infer_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_ops_infer.h
  ln -s cudnn_ops_train_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_ops_train.h
  ln -s cudnn_version_v${_majorver}.h "${pkgdir}"/usr/include/cudnn_version.h
  ln -s libcudnn.so.${_majorver} "${pkgdir}"/usr/lib/libcudnn.so
  ln -s libcudnn_static_v${_majorver}.a "${pkgdir}"/usr/lib/libcudnn_static.a

  install -Dm644 \
      "${srcdir}"/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      "${pkgdir}"/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
