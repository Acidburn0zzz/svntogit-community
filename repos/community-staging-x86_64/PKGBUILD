# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=5.0.4.1
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect' 'deepin-menu')
makedepends=('vala' 'cmake')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'zssh: for remote management support')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal/archive/$pkgver.tar.gz"
        $pkgname-vte-fix.patch::https://github.com/linuxdeepin/deepin-terminal/commit/8d0a761873c2ef898c9977acc0192bf9ef9e7ca4.patch
        $pkgname-vte-0.59.patch::https://github.com/linuxdeepin/deepin-terminal/commit/d38f972c8cb958524b326986c6dc72b24fa35dad.patch)
sha512sums=('9523d5411b017947b40568fa3d47654e3a9d560c016a7e75311a14c60c9da3368337c4652fdbaa3fb1278cebb01a93f6c0c804bd277c59a009ead3024bf14036'
            '9bdeb82f54664f2c158d29cb7095b1c7fd0edf12ea20d1c738a79f68df820a5320070a31dcd5673dff3c412ca75bd7140e4293ee0ea986c8ba832455b8eb9662'
            '44a6538138c0fed9938048229444d6cbf050af6b26307f09068ad86d6a256aef19ceca55c6149036e62c0a50e9c89f13f89f43412ec0d6d42db1256f5206957f')

prepare() {
  patch -d deepin-terminal-$pkgver -Np1 -i ../$pkgname-vte-fix.patch
  patch -d deepin-terminal-$pkgver -Np1 -i ../$pkgname-vte-0.59.patch
  mkdir -p build
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DTEST_BUILD=OFF \
    -DUSE_VENDOR_LIB=OFF \
    -DVERSION=$pkgver \
    ../deepin-terminal-$pkgver
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
