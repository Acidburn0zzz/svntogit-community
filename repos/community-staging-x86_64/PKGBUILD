# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=contravariant-extras
pkgname=haskell-contravariant-extras
pkgver=0.3.5
pkgrel=2
pkgdesc="Extras for the \"contravariant\" package"
url="https://github.com/nikita-volkov/contravariant-extras"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-prelude' 'haskell-contravariant' 'haskell-tuple-th')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0ae04eca3297df56f1c86063fdc9eb7f99804dc387732212daeadd729d79dbd50ef12b849b2ca8dac7021752818e0779e650102275ff3d15e5aa9551b0b370bc')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e '/semigroups/d' -e 's/base-prelude >= 0.1.19 && < 2,/base-prelude >= 0.1.19 \&\& < 2/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
