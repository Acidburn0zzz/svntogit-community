# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=2.9.1
pkgrel=2
pkgdesc="A tool for examining the internals of a Qt application and to some extent also manipulate it"
arch=(x86_64)
url="http://www.kdab.com/gammaray/"
license=(GPL)
depends=(syntax-highlighting qt5-tools qt5-svg qt5-3d)
makedepends=(cmake kcoreaddons qt5-script qt5-wayland qt5-webengine qt5-scxml qt5-location qt5-connectivity doxygen)
optdepends=('qt5-wayland: Wayland compositor inspector plugin'
            'qt5-webengine: web inspector plugin'
            'qt5-scxml: state machine viewer plugin'
            'qt5-connectivity: bluetooth plugin'
            'qt5-script: script engine debugger plugin'
            'kcoreaddons: KJob tracker plugin')
source=(https://github.com/KDAB/GammaRay/releases/download/v$pkgver/gammaray-$pkgver.tar.gz{,.asc}
        gammaray-qt5.12a.patch::"https://github.com/KDAB/GammaRay/commit/2cd4585e.patch"
        gammaray-qt5.12b.patch::"https://github.com/KDAB/GammaRay/commit/d0c9f9c8.patch")
sha256sums=('ba1f6f2b777c550511a17f704b9c340df139de8ba8fa0d72782ea51d0086fa47'
            'SKIP'
            '7cfc822fd18595b9d274a030fd833d39251ea2c271653f79b1f246eb960d38e3'
            '629e169addfc68cab7e4eb4893e4631dbd96b2838af717fa8bb5b0bb5b3cb9d7')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
  mkdir -p build

# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt/plugins/gammaray|' -i $pkgname-$pkgver/CMakeLists.txt
# Fix build with Qt 5.12
  cd $pkgname-$pkgver
  patch -p1 -i ../gammaray-qt5.12a.patch
  patch -p1 -i ../gammaray-qt5.12b.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/gammaray
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
