# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.8.5
pkgrel=2
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('x86_64')
url='https://github.com/gabime/spdlog'
license=('MIT')
depends=('libfmt.so')
makedepends=('cmake')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz"
    "https://github.com/gabime/spdlog/commit/8bf718671a9eac5517c27a5ffe72089b7f426d8c.patch"
    "https://github.com/gabime/spdlog/commit/5887744d8b6ddaa3044d1c59f06e2cf24b8e0349.patch"
    "https://github.com/gabime/spdlog/commit/7b14a65b2b8cbdca3c6bd1d36fe5b271389c1d07.patch"
)
provides=(
    'libspdlog.so'
)
sha256sums=('944d0bd7c763ac721398dca2bb0f3b5ed16f67cef36810ede5061f35a543b4b8'
            '8f6e53406139312d9302ca88e666c6a28409ba9de01e84ac3bae357beca31e32'
            '31882318089ed5b8875268c75e1256e971fe1d0d1894f50bc37e151cfb4e7daa'
            '5efa9e3b8d4dc4de351dc0e1d2516b2a6e141854e055ae420446785df85d9574')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../8bf718671a9eac5517c27a5ffe72089b7f426d8c.patch
  patch -Np1 -i ../5887744d8b6ddaa3044d1c59f06e2cf24b8e0349.patch
  patch -Np1 -i ../7b14a65b2b8cbdca3c6bd1d36fe5b271389c1d07.patch
  find -type f -name *.orig -delete
}

build() {
    export CFLAGS+=" ${CPPFLAGS}"
    export CXXFLAGS+=" ${CPPFLAGS}"
    cmake -B build -S "$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DSPDLOG_BUILD_SHARED=ON \
        -DSPDLOG_BUILD_TESTS=ON \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
