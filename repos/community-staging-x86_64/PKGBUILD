# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=0.0.7
_extramodules=extramodules-ARCH
pkgrel=8
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('linux')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        kernel-5.1.patch)
sha512sums=('b0951ada69a12123337d276e2cdfdd3f3c7ca3f41ac8688f0e4758b732bbe5635b34e9064dd778fb3cf9e7829e20dfa9b59135db5b19a107f194e71071662924'
            'b23a7ad54866e04bb86ba401b144ea73725451852e8fb1ab54059a4c685203e9bbd16205b558191f0f33caa8c22a6066077a2068a346ff16f007ae7c5e31ccc7')

prepare() {
  patch -d deepin-anything-$pkgver -p1 -i ../kernel-5.1.patch
}

build() {
  cd deepin-anything-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package() {
  cd deepin-anything-$pkgver/kernelmod
  install -Dm644 vfs_monitor.ko "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
  gzip "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
}
