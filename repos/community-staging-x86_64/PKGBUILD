# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=language-c
pkgname=haskell-language-c
pkgver=0.8.3
pkgrel=40
pkgdesc="Analysis and generation of C code"
url="https://visq.github.io/language-c/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-syb')
makedepends=('happy' 'alex' 'ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        $pkgname-gcc-11.patch::https://github.com/visq/language-c/commit/8133e1526750ab74ca4d5180fb140a7e172d473f.patch)
sha512sums=('a2e653c3d6fe4573c0a7f34115d5a5d3db925c174cd574755bb174de834165bce236b354f04e1158309eda654d12104ea03e88f94cdc8b3e11cffba7260282de'
            '7fcb78eaf53912fd5b0644ed8b2ecd260493a7fa6506e06768e304ac9c0ff8fdf27c869b4a0b0c06bd4bb544074ac7767a8aedfc1b87a02b70e53a4177d3f1cb')

prepare() {
    patch -d $_hkgname-$pkgver -p1 < $pkgname-gcc-11.patch
}

build() {
    cd $_hkgname-$pkgver

    rm -fr dist

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fseparatesyb -fusebytestrings -f-allwarnings -fiecfpextension
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test --show-details=direct
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
