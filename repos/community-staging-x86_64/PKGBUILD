# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat)
pkgver=1.3.0
pkgrel=2
pkgdesc='SAX XML parser based on the Expat library'
arch=('i686' 'x86_64')
url='http://matthewwild.co.uk/projects/luaexpat/'
license=('MIT')
makedepends=('gcc' 'lua' 'lua51' 'expat')
source=("http://matthewwild.co.uk/projects/luaexpat/luaexpat-$pkgver.tar.gz"
	    'config.51'
	    'config.53')
md5sums=('3c20b5795e7107f847f8da844fbfe2da'
         '64abbf5b602e0e31b68db2925a5e6b2f'
         '7d5618fe79b110c34fac530744d72495')

build() {
  # 5.1
  msg2 "Building $pkgbase with lua 5.1"
  cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
  cd $pkgbase-$pkgver-51
#  cp -L "$srcdir/config.51" config
  make LUA_V=5.1
  # 5.3
  msg2 "Building $pkgbase with lua 5.3"
  cd "$srcdir/$pkgbase-$pkgver"
#  cp -L "$srcdir/config.53" config
  make LUA_V=5.3 CFLAGS='-DLUA_32BITS'
}

package_lua-expat() {
  depends=('lua' 'expat')

  cd $pkgbase-$pkgver
  make LUA_V=5.3 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}

package_lua51-expat() {
  depends=('lua51' 'expat')
  replaces=('luaexpat')
  conflicts=('luaexpat')

  cd $pkgbase-$pkgver-51
  make LUA_V=5.1 DESTDIR="$pkgdir" install
  install -Dm0644 doc/us/license.html "$pkgdir/usr/share/licenses/$pkgname/license.html"
}
