# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=2.13
pkgrel=5
pkgdesc='Conversion between markup formats'
url='https://pandoc.org'
license=('GPL')
arch=('x86_64')
depends=('ghc-libs' 'haskell-http' 'haskell-juicypixels' 'haskell-sha' 'haskell-aeson'
         'haskell-aeson-pretty' 'haskell-attoparsec' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-case-insensitive'
         'haskell-citeproc' 'haskell-commonmark' 'haskell-commonmark-extensions'
         'haskell-commonmark-pandoc' 'haskell-connection' 'haskell-data-default'
         'haskell-doclayout' 'haskell-doctemplates' 'haskell-emojis' 'haskell-file-embed'
         'haskell-glob' 'haskell-haddock-library' 'haskell-ipynb' 'haskell-jira-wiki-markup'
         'haskell-skylighting' 'haskell-skylighting-core' 'haskell-hslua'
         'haskell-hslua-module-path' 'haskell-hslua-module-system' 'haskell-hslua-module-text'
         'haskell-http-client' 'haskell-syb' 'haskell-hsyaml' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-safe' 'haskell-split' 'haskell-texmath' 'haskell-network'
         'haskell-pandoc-types' 'haskell-random' 'haskell-scientific' 'haskell-tagsoup'
         'haskell-temporary' 'haskell-text-conversions' 'haskell-network-uri'
         'haskell-unicode-transforms' 'haskell-unordered-containers' 'haskell-zip-archive'
         'haskell-xml' 'haskell-xml-conduit' 'haskell-zlib')
optdepends=('pandoc-citeproc: for citation rendering with pandoc-citeproc filter'
            'pandoc-crossref: for numbering figures, equations, tables and cross-references to them with pandoc-crossref filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc' 'uusi' 'haskell-diff' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-lua'
             'haskell-tasty-quickcheck' 'haskell-tasty-golden' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        haddock-library-1.10.patch)
sha512sums=('c894f59e7ff57603fd6a60d11880b9fd6d6e206247346f77070b176fed6f359b3d5a5ba984803717ac4a4e5c038ad0912513da091d4b8672df63af17d21627a8'
            'f6b0fc90d42e2a02c101e0ba3f86884cb957392b0a1d351f63459780df57a4db51bd9ef72230cd0060e5247fa9a0b5274701bb663163561a932e6aed4e0d5b07')

prepare() {
    cd $pkgname-$pkgver
    patch -p0 -i ../haddock-library-1.10.patch
    uusi -u attoparsec -u haddock-library -u random $pkgname.cabal

    # TODO: find a better solution
    sed -i "s|let env' = dynlibEnv ++ |let env' = dynlibEnv ++ [(\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ |" test/Tests/Command.hs
}

build() {
    cd $pkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-trypandoc -f-embed_data_files -f-static
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test || echo "Tests failed"
}

package() {
    cd $pkgname-$pkgver
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
    install -Dm644 man/pandoc.1 -t "${pkgdir}"/usr/share/man/man1/
}
