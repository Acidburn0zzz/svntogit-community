# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hls-haddock-comments-plugin
pkgname=haskell-hls-haddock-comments-plugin
pkgver=1.0.0.1
pkgrel=3
pkgdesc="Haddock comments plugin for Haskell Language Server"
url="https://github.com/haskell/haskell-language-server"
license=("Apache")
arch=('x86_64')
depends=('ghc-libs' 'haskell-ghc' 'haskell-ghc-exactprint' 'haskell-ghcide' 'haskell-hls-plugin-api'
         'haskell-lsp-types' 'haskell-unordered-containers')
makedepends=('ghc' 'uusi' 'haskell-hls-test-utils')
checkdepends=('git')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('b29fa55c05417cdb5fa750642f340799b912480ed1dc5e8387ab214f956f1be724923404d4ba1bcec62fa6faba88a4ea9f465bdcacba526b5b1cc770a605cd35')

prepare() {
  cd $_hkgname-$pkgver
  gen-setup
}

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test --show-details=direct
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
