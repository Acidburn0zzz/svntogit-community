# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=c2hs
pkgver=0.28.6
pkgrel=131
pkgdesc="C->Haskell FFI tool that gives some cross-language type safety"
url="https://github.com/haskell/c2hs"
license=("GPL2")
arch=('x86_64')
depends=('ghc-libs' "haskell-dlist" "haskell-language-c")
makedepends=('ghc' 'haskell-test-framework' 'haskell-test-framework-hunit' 'haskell-hunit'
             'haskell-shelly')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        c2hs-shelly-1.9.patch::https://github.com/haskell/c2hs/pull/258.patch)
sha512sums=('848693ddf787244cc0f100f533030889a1efcef283f06ef609574d9cd0005679fab373172a949408ce6d4b9c97d15a56b0ec11fe39b466ad3f84aa527d2ab1c9'
            'c76f68e393114512c4c217a37aab1c72df271666a30a958b0d4ea18f286c51b3f49352e4dec4d8158a89329f8c9e884f4f39548e4b72258a1ec82f4f7516740c')

prepare() {
    patch -d c2hs-$pkgver -p1 < c2hs-shelly-1.9.patch
    sed -i 's/cmd "ghc"/cmd "ghc" "-dynamic"/' c2hs-$pkgver/tests/test-*.hs
    sed -i 's/HC=ghc/HC="ghc -dynamic"/' c2hs-$pkgver/tests/system/Makefile
    sed -i 's/"ghc", \["-/"ghc", ["-dynamic", "-/' c2hs-$pkgver/tests/test-*.hs
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-executable-dynamic --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
            -f-regression -fbase3 --ghc-option='-pie'
    runhaskell Setup build $MAKEFLAGS
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
}
