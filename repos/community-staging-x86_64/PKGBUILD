# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.8.6.4
pkgrel=3
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-anything' 'file' 'avfs' 'polkit-qt5' 'poppler'
         'ffmpegthumbnailer' 'xdg-user-dirs' 'jemalloc' 'kcodecs' 'taglib')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-movie' 'deepin-gettext-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display'
            'deepin-screensaver: for screensaver chooser'
            'deepin-movie: for video preview'
            'deepin-terminal: for opening in terminal'
            'file-roller: for compress/decompress')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
        deepin-file-manager-qt5.13.patch)
sha512sums=('532cfcce9fc986ae3eb5ddaffcc52230139f8b14f54900dcead59ccf52950d452a2a36a073c7b569bdae0201c7178414c8b521922cac4edc7f27e4d4b77ed9d2'
            '87be44cd6a89f90baef2737ff2a73343f1a69aac919c84ca827c515efe22ff481904df48096c8b095089aae85fc3d0615253582e78f97eb30456dd5fa8e06295')

prepare() {
  cd dde-file-manager-$pkgver
  sed -i 's|systembusconf.path = /etc/dbus-1/system.d|systembusconf.path = /usr/share/dbus-1/system.d|' dde-file-manager-daemon/dde-file-manager-daemon.pro
  patch -p1 -i ../deepin-file-manager-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
