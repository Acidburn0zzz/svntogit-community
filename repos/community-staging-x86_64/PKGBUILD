# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.1.0101.0
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center')
makedepends=('deepin-gettext-tools' 'qt5-tools')
provides=('deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz"
         deepin-session-ui-qt5.15.patch)
sha512sums=('764d0a47320ab4dd3d8ce8ed22d2094e100f720ce552339746edfd123ec081f766ab1874c2b4b9620b6a6f4e82495cf4e7e2e1128e5bbb8a22eac3440b0017a9'
            '421b100161ad9db234ece920e2c2784e1fbca0cd04e1a9a067311f3fdb13cc0ef7d58f7b33af151fabc7637b793d1479011966a741d854d745bcab3b79a9cdfd')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp

  patch -p1 -i ../deepin-session-ui-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
