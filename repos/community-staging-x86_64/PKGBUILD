# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calendar
pkgver=5.0.r112.26275ba6
pkgrel=1
pkgdesc='The Pantheon Calendar'
arch=(x86_64)
url=https://github.com/elementary/calendar
license=(GPL3)
groups=(pantheon)
depends=(
  clutter
  evolution-data-server
  folks
  gdk-pixbuf2
  geoclue
  geocode-glib
  glib2
  gtk3
  libchamplain
  libgee
  libgranite.so
  libical
)
makedepends=(
  git
  intltool
  meson
  vala
)
source=(pantheon-calendar::git+https://github.com/elementary/calendar.git#tag=26275ba6215610aa17a31f3e6d50b27dc214f731)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-calendar

  git describe --tags | sed 's/-/.r/; s/-g/./'
}

prepare() {
  cd pantheon-calendar

  # libecal-2.0
  git cherry-pick -n 54a1df3d49dcbca40db5ae6841a576f818bc7582
}

build() {
  arch-meson pantheon-calendar build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
