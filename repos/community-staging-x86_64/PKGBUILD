# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

_commit='27391f174a1fe385899074f13453667b98af8621'
pkgname=cage
pkgver=0.1.1.r68.g27391f1
pkgrel=1
pkgdesc="A kiosk compositor for Wayland"
arch=('x86_64')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('glibc' 'libxkbcommon' 'polkit' 'pixman' 'wayland')
makedepends=('git' 'meson' 'wayland-protocols' 'wlroots' 'xorg-server-xwayland')
optdepends=('xorg-server-xwayland: X11 support')
# cage 0.1.1 not compatible with wlroots > 0.8.1
# therefore pinning current HEAD to be able to upgrade wlroots
# https://github.com/Hjdskes/cage/issues/107
# source=("$pkgname-$pkgver.tar.gz::https://github.com/Hjdskes/${pkgname}/archive/v${pkgver}.tar.gz"
#         "https://github.com/Hjdskes/cage/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
source=("${pkgname}-${pkgver}::git+https://github.com/Hjdskes/${pkgname}.git#commit=${_commit}?signed"
        "${pkgname}-0.1.1-wlroots-0.11.0.patch")
sha512sums=('SKIP'
            '4273fb848309c329496a61ecc68018e302ec6b7b4b65c8d3a57304b2e1dd95822d08043cf3ec6c6e1673e16d276edb3b7d0c2566da72689e0f6cabe86f2bf96a')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

pkgver() {
  cd "$pkgname-$pkgver"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "../${pkgname}-0.1.1-wlroots-0.11.0.patch"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             -Dxwayland=true \
             build
  ninja -C build
}

package() {
  depends+=('libwlroots.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
