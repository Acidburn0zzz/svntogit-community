# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=reducers
pkgname=haskell-reducers
pkgver=3.12.1
pkgrel=39
pkgdesc="Semigroups, specialized containers and a general map/reduce framework"
url="https://github.com/ekmett/reducers/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc-libs' "haskell-fingertree" "haskell-hashable" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-text" "haskell-unordered-containers")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('56ebaff2f10e8982e420d6f9cbdf1db1b4a75e609d5c5b629815b248b238b1ca1fd6039e47b784ca6555462f4977631b0d40b84c0454cfe19b0fe1c2f8dc96a6')

prepare() {
    sed -i 's/transformers           >= 0.2      && < 0.5,/transformers           >= 0.2      \&\& < 0.6,/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
    sed -e '/instance Measured v a => Semigroup (FingerTree v a) where/d' -e '/(<>) = mappend/d' -i ${_hkgname}-${pkgver}/src/Data/Semigroup/Instances.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
