# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-api
pkgver=1.3
pkgrel=12
pkgdesc="Web API server for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc' 'hledger' "haskell-hledger-lib" "haskell-aeson" "haskell-decimal" "haskell-docopt"
         "haskell-either" "haskell-microlens" "haskell-microlens-platform" "haskell-safe"
         "haskell-servant-server" "haskell-servant-swagger" "haskell-swagger2" "haskell-text"
         "haskell-wai" "haskell-wai-extra" "haskell-warp")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1349f1943afcf7f2bb4000fde580ef48f7f55ecd604c6d1df161449df6ffa00714e06b7ba10b77f07056eeb0711eada1a91559a7606062f83f8f3d4e830ede20')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
