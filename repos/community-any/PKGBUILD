# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=python-aiohttp-socks
pkgver=0.3.7
pkgrel=1
pkgdesc="SOCKS proxy connector for aiohttp. SOCKS4(a) and SOCKS5 are supported."
arch=('any')
url="https://github.com/romis2012/aiohttp-socks/"
license=('Apache')
depends=('python-aiohttp')
makedepends=('python' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio')
source=("https://github.com/romis2012/aiohttp-socks/archive/${pkgver}.tar.gz")
sha256sums=('bc431532c3966c5eef4d66d411403c6bfdb84773f97e1f58fa565a3c58ad2ff2')

check() {
    cd "${pkgname//python-}-${pkgver}"
    pytest
}

build() {
    cd "${pkgname//python-}-${pkgver}"
    python setup.py build

}

package() {
    cd "${pkgname//python-}-${pkgver}"
    python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

