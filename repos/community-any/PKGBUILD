# Maintainer: David Runge <dave@sleepmap.de>
pkgname=tmuxp
pkgver=1.3.5
pkgrel=3
pkgdesc="tmux session manager. built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('BSD')
groups=('tmux')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-libtmux' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest-rerunfailures')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('4af1363a076c58403414d9ae3e1e5d69ebd1457e4865b0a9b6b92e6b920a547f14bf45ce3e6aac4f09e93d8388254f2d27f28e699cff1fa51b9cfe2e9df6f9b5')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # license
  install -vDm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {CHANGES,README.rst}
}

# vim:set ts=2 sw=2 et:}
