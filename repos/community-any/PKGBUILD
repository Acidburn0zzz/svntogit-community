# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=yamllint
pkgver=1.16.0
pkgrel=1
pkgdesc='Linter for YAML files'
arch=('any')
url=https://github.com/adrienverge/yamllint
license=('GPL3')
depends=('python-pathspec' 'python-setuptools' 'python-yaml')
checkdepends=('python-nose')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('57a7a0c5e2681bab9225bffc37e44d1498aa591895c836dfab787b09b86eb67ff5cd89ba4bc8840ebedaf83d5114bb0f5a76a60a6aedb385f3e7e4386f18ea0d')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m unittest discover tests
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
}

# vim:set sw=2 et:
