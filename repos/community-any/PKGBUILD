# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-baron
pkgver=0.8
pkgrel=1
pkgdesc="Full Syntax Tree for python to make writing refactoring code a realist task"
url="https://github.com/PyCQA/baron"
license=('LGPL')
arch=('any')
depends=('python-rply')
makedepends=('python-setuptools' 'python-pypandoc')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/baron/archive/$pkgver.tar.gz")
sha512sums=('99803adc9ccd286c1906126b76a4a03bae3936572c1293e87ce7b7110c019b22d73afa98a71f49b9424160748e90e3a94d378ca8291eced255eb8164bd62dc8e')

build() {
  cd baron-$pkgver
  python setup.py build
}

check() {
  cd baron-$pkgver
  python setup.py pytest
}

package() {
  cd baron-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
