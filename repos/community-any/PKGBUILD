# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=8.0.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.config/archive/$pkgver.tar.gz")
sha512sums=('806b6afdfe0e1c2a12fa936c390d3cbb6ad37515beecc4f8a1317537828326d591b247d3482f9fa836c7e7ed1ca872a9db0c01132ba6245ab31670505a94f668')

export PBR_VERSION=$pkgver

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  stestr run
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
