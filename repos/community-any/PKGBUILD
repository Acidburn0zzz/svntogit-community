# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
pkgver=1.0.0
pkgrel=1
pkgdesc='Enable installed pytest plugins'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-enabler'
depends=('python-toml' 'python-jaraco' 'python-jaraco.context' 'python-more-itertools')
makedepends=('python-setuptools-scm')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-checkdocs'
              'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-enabler/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('44b221b0a3091bec21425fb5c0d9658f85608e61e003342cb09a542ddda3cd192eb0dca1627db34a3c6380f86f8fa39876a589e6b4d5ea93ce06aa16d3f6c0b0')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-enabler-$pkgver
  python setup.py build
}

check() {
  cd pytest-enabler-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-enabler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
