# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=pyVHDLModel
pkgname=python-pyvhdlmodel
pkgver=0.8.0
pkgrel=1
pkgdesc='An abstract language model of VHDL written in Python'
arch=('any')
url='https://github.com/VHDL/pyVHDLModel'
license=('Apache2')
depends=('python' 'python-pydecor')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a0728fcb50902703ae3c4833bca0d8a4b2b6015ec5beeb1458c346e7c7ad09e3ec508d724c9334ae036b9b833ede6abc87f405b011367311890ef3b9b72346d9')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python -m pytest tests
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
