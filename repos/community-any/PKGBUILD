# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>

pkgbase=('python-requests-credssp')
pkgname=('python-requests-credssp' 'python2-requests-credssp')
_module='requests-credssp'
pkgver=1.0.1
pkgrel=1
pkgdesc="HTTPS CredSSP authentication with the requests library"
url="https://github.com/jborean93/requests-credssp"
makedepends=('python-setuptools' 'python2-setuptools')
license=('ISC')
arch=('any')
source=("https://github.com/jborean93/requests-credssp/archive/v${pkgver}.tar.gz")
sha512sums=('f4856871cd0a25e6d45b265d7c3c322c80581a33c1ceb371b153952bc8a26d1632b0ae62768a3e74b2e8c120fa233ee22bf7610a82e1ab467aa950afc584b398')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-requests-credssp() {
    depends+=('python' 'python-pyopenssl' 'python-requests' 'python-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-requests-credssp() {
    depends+=('python2' 'python2-pyopenssl' 'python2-requests' 'python2-ordereddict' 'python2-ntlm-auth')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
