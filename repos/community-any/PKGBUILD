# Maintainer: David Runge <dvzrv@archlinux.org>

# weird name on pypi, because reasons
# _name=djangorestframework
_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.11.0
pkgrel=1
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=('any')
url="https://www.django-rest-framework.org/"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytest-django')
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=('python-django-guardian: for a per-object permission model'
            'python-markdown: for markdown support'
            'python-psycopg2: for PostgreSQL support'
            'python-pygments: for syntax highlighting'
            'python-pyaml: for YAML support')
#            'python-django-filter: for dynamic queryset filters'
# tests not in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('7f49e02fa8d4d91a7aeb832814d6c53f3367fef8048bf6a1ad1f53a9f0b4c19e06cf76884a540144961ebe6a3f296afa9a427e4eea9e08578052dccb46da9c52')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
