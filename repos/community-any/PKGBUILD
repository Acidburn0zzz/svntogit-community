# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Firef0x <Firefgx (at) gmail (dot) com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Lekensteyn <lekensteyn@gmail.com>

pkgname=smali
pkgver=2.1.3
pkgrel=1
pkgdesc="An assembler/disassembler for Android's dex format"
url="https://github.com/JesusFreke/smali"
arch=('any')
license=('BSD')
depends=('java-runtime>=8' 'bash')
makedepends=('java-environment>=8' 'gradle')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/JesusFreke/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('0e91a43efe9e7706ae0cdd39e8d5bbdbf634e52afbf7a93da5d46c6716f69316698f4caea077290a93f8018f41509ff923222f12ebe6cdfa48716544753e582a')

prepare() {
  cd ${pkgname}-${pkgver}
  for file in baksmali smali; do
    # prevent from printing path on launch
    sed '/echo ${newProg}/d' -i scripts/${file}
    # fix ls path
    sed 's|/bin/ls|/usr/bin/ls|' -i scripts/${file}
  done
}

build() {
  cd ${pkgname}-${pkgver}
  gradle --gradle-user-home=. build
}

check() {
  cd ${pkgname}-${pkgver}
  gradle --gradle-user-home=. test
}

package() {
  cd ${pkgname}-${pkgver}
  install -d "${pkgdir}/usr/bin"
  for file in baksmali smali; do
    install -Dm 644 "${file}/build/libs/${file}.jar" "${pkgdir}/usr/share/java/${pkgname}/${file}.jar"
    install -Dm 755 scripts/${file} "${pkgdir}/usr/share/java/${pkgname}/${file}"
    ln -s "/usr/share/java/${pkgname}/${file}" "${pkgdir}/usr/bin/${file}"
  done
  install -Dm 644 NOTICE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
