# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: James Spencer <james.s.spencer@gmail.com>

pkgbase=jmol
pkgname=(jmol jsmol)
pkgver=14.31.0
pkgrel=1
arch=(any)
url="https://jmol.sourceforge.net"
license=(LGPL)
makedepends=(unzip)
source=("https://sourceforge.net/projects/jmol/files/Jmol/Version%20${pkgver%.*}/Jmol%20$pkgver/Jmol-$pkgver-binary.tar.gz")
sha256sums=('eee0703773607c8bd6d51751d0d062c3e10ce44c11e1d7828e4ea3d5f710e892')

package_jmol() {
  pkgdesc="a Java 3D viewer for chemical structures"
  depends=(java-runtime)

  cd $pkgbase-$pkgver

  mkdir -p "$pkgdir"/usr/share/$pkgname
  mkdir -p "$pkgdir"/usr/bin

  cp *.jar jmol.sh "$pkgdir"/usr/share/$pkgname
  ln -s /usr/share/$pkgname/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}

package_jsmol() {
  pkgdesc="a JavaScript 3D viewer for chemical structures"
  cd $pkgbase-$pkgver

  mkdir -p "$pkgdir"/usr/share
  unzip jsmol.zip -d "$pkgdir"/usr/share/
}
