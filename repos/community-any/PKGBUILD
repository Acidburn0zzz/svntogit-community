# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=1.7.8
pkgrel=1
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-beautifulsoup4' 'python-setuptools')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/v$pkgver.tar.gz")
sha512sums=('7292ac86f138fa75b511c3bed5d8f8bb7d586af4d5a82d1c8caea70b5f2f320a9920df49043e731a40dd25b8119d75b52c797b1941b0532da7806158206e057f')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
  sed -e 's|^strDataAssetsDirName = .*|strDataAssetsDirName = "share/COMToolData/assets"|' \
      -e 's|^strDataDirName = .*|strDataDirName = "share/COMToolData"|' \
      -i COMTool/parameters.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

check() {
  cd COMTool-$pkgver
  # Disabled: Tests hang
  # xvfb-run python -m unittest discover
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
