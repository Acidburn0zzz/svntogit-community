# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.10
pkgrel=1
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-pyaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint python-setuptools python-sshpubkeys)
checkdepends=(python-flask python-freezegun python-nose python-sure tk)
optdepends=(
  'python-flask: for moto_server'
)
source=("https://github.com/spulec/moto/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        moto-fix-setup.patch::https://github.com/yan12125/moto/commit/92bf8eff12b49def8e9689c57d26184c548546d0.patch)
sha256sums=('3ecb76df66c47a59dd9cc7218f08265c04f2b963561b9d50a246221a63999d23'
            '913485b0605c1b9cbb445b1862eb91ee6130430d5b735836f8e1b093565924d8')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../moto-fix-setup.patch

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  TZ=UTC nosetests -sv ./tests/ --exclude='test_iot.*' --exclude='test_lambda.*'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
