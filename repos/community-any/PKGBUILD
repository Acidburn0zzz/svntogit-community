# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.9.46
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-numpy' 'python-pytest-runner' 'python-pytest-benchmark')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('01bd135b2e08958749eab1e79abb912d7da94743567b8165f6dc1b0edbbb765f1e7ba7e120fda1fef8bcd24a285d164b017f49527382921018091cc654d28cae')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
