# Maintainer: Tim Meusel <tim@bastelfreak.de>
# Contributor: arakmar
pkgname=r10k
pkgver=3.12.1
pkgrel=1
pkgdesc='Provides a general purpose toolset for deploying Puppet environments and modules.'
arch=('any')
url="https://github.com/puppetlabs/${pkgname}"
license=('Apache')
depends=('ruby' 'ruby-colored2' 'ruby-cri' 'ruby-gettext-setup' 'ruby-log4r' 'ruby-multi_json' 'ruby-semantic_puppet' 'ruby-puppet_forge' 'ruby-rugged' 'ruby-fast_gettext' 'ruby-gettext' 'ruby-faraday' 'ruby-faraday-net_http' 'ruby-jwt')
makedepends=('ruby-minitar' 'ruby-yard')
checkdepends=('ruby-rspec')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz" 'ruby3.patch')
sha512sums=('a46e2b49118977ac93e8f832209f4f01c0ca3b6b293ffd232daf6370473f2dc268d9eed44c1a82d38eca428ececee4d7c299c6b129ee1249075636e24d647f6c'
            '8bff35f0a42b145105ef492e70e1c62fc41a5309c6c921e96098d7463b4a53f3f5d97d7cc4bd87ef8f1fd26345c2faefcd4f5046e5c84eee8c98f6c4ca089576')
options=('!emptydirs')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # update the gemspec to allow newer versions
  sed --in-place 's|~>|>=|g' "${pkgname}.gemspec"
  # don't use git to detect files for the package
  sed --in-place 's|git ls-files|find|g' "${pkgname}.gemspec"
  # allow newer gettext versions
  sed --in-place 's|< 3.3.0|< 4.0.0|' "${pkgname}.gemspec"
  # allow newer version of cri
  sed --in-place "s#, '2.15.10'##" r10k.gemspec
  # drop dev/CI dependencies; they only create package dependecy circles
  sed --in-place '/add_development_dependency/d' "${pkgname}.gemspec"

  # Workaround broken Ruby 3 support
  # https://github.com/puppetlabs/r10k/pull/1234
  patch -p1 -N -i "${srcdir}/ruby3.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${pkgname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rspec --color --format documentation spec/unit
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${pkgname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -Dm 644 r10k.yaml.example CHANGELOG.mkd README.mkd CONTRIBUTING.mkd -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv doc docker integration "${pkgdir}/usr/share/doc/${pkgname}/"

  rm -rf "${pkgdir}/${_gemdir}/gems/${pkgname}-${pkgver}/ext"
  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
