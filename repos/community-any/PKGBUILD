# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=1.0.7
pkgrel=1
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-aioitertools)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        unittest-mock.diff
        pytest-event-loop.diff)
sha256sums=('0789c379925c2f65a639e978bb20a35f4e242a46d2272f212f159f9a14f28f13'
            'ffb6b7e922632f4c2de5c93f61ab53e1730cfe151b0a2dde5dfce6260405866e'
            '02700f174c58d754e501169737cf04f06f85f536b3bb4f44f422e213ab600e46')

prepare() {
  cd $_pkgname-$pkgver
  # community/python-mock is outdated and does not include needed AsyncMock.
  # Use mock from stdlib instead. It has AsyncMock since 3.8
  patch -Np1 -i ../unittest-mock.diff
  patch -Np1 -i ../pytest-event-loop.diff

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  sed --in-place=.orig -r "s#'(botocore.*),<.*',#'\1',#" setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export PYTHONPATH="$PWD/build/lib:$PWD"
  # test_lambda uses moto.awslambda, which requires a running Docker service
  pytest -v -m moto tests \
      --ignore=tests/test_patches.py \
      --ignore=tests/test_lambda.py
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
