# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: David Rodriguez <dissonant.tech@gmail.com>

pkgname=lightdm-webkit-theme-archlinux
pkgver=0.5.0
_bgver=0.7
pkgrel=1
pkgdesc="ArchLinux version of the official Antergos greeter"
arch=('any')
url='https://github.com/Dissonant-Tech/lightdm-webkit-theme-archlinux'
license=('GPL2')
depends=('lightdm' 'lightdm-webkit2-greeter')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
        "http://antergos.com/antergos-wallpapers-$_bgver.zip")
sha512sums=('ffbced0605fbcac680675b49c4ca246d911020c58208b9adf254d3ef87a20019c93dc5c68211867db7b549d5605aa006f4dbb60131ab0c2bb7ca5878c6dfb40d'
            'b5c051b432d5e8d0ebb4870a11b1de63a46cd3c14853e03fe63f429f03e6838b92606b8f0365dcea7f9ab5ca1d67503b1839caf86f4684025d5b4933aca090ca')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|/usr/share/|/usr/share/lightdm-webkit/themes/|g' index.html
}

package() {
  _themedir="$pkgdir"/usr/share/lightdm-webkit/themes/archlinux

  # Install theme
  install -dm 755 "$_themedir"
  cp -dpr --no-preserve=ownership $pkgname-$pkgver/* "$_themedir"

  # Instal wallpapers
  install -dm 755 "$_themedir"/wallpapers
  cp -dpr --no-preserve=ownership antergos-wallpapers-$_bgver "$_themedir"/wallpapers
}

