# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cachetools
pkgname=(python-cachetools python2-cachetools)
pkgver=3.0.0
pkgrel=1
pkgdesc="Extensible memoizing collections and decorators"
url="https://github.com/tkem/cachetools"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tkem/cachetools/archive/v$pkgver.tar.gz")
sha512sums=('6dc8ad183651589cda5d65a80a61c9a23dc577f6b8f50b38498aeb4ab68b842cf573daa0a3e49eb2565703f8e49ffea69461b641a4f10fbfe8dc00321cf6d40e')

prepare() {
  cp -a cachetools-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py build

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cachetools-$pkgver
  python setup.py pytest

  cd "$srcdir"/cachetools-$pkgver-py2
  python2 setup.py pytest
}

package_python-cachetools() {
  depends=('python')

  cd cachetools-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-cachetools() {
  depends=('python2')

  cd cachetools-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
