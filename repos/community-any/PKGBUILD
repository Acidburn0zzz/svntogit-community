# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=openfire
pkgver=4.2.2
pkgrel=3
pkgdesc="High performance XMPP (Jabber) server."
arch=('any')
url='http://www.igniterealtime.org/projects/openfire/'
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('apache-ant')
backup=('etc/openfire/openfire.xml' 'etc/conf.d/openfire' 'etc/openfire/security/keystore' \
        'etc/openfire/security/truststore' 'etc/openfire/security/client.truststore')
source=("http://www.igniterealtime.org/downloadServlet?filename=openfire/openfire_src_${pkgver//./_}.tar.gz" \
        'openfire.conf'
        'openfire.service'
        'user.conf'
        'tmpfile.conf')
sha256sums=('a9fa56332cec9437c751644bf23dbbf65a332605493d4780b97b0da4a5febe7d'
            'c8a612abee90bdc9a2869e562a4bbbf596159c60adcd23368833a0e2ca27c1d4'
            'cc04a614686d3f9ce3b83b5fcfb7c5f4cd5ddb8c4c06830c04f26869f2e8fa5b'
            '743d07158d40f3690684a078d5f35c53c819e45d4ff0c2016b0f33662363614f'
            'c9c7269603d96d59b1a50201a950a47441bd7ecd35852d15037f15761c0ad4e3')

build() {
	cd "$srcdir"/openfire_src/build
	rm -rf ../src/resources/nativeAuth/
	ant openfire
}

package() {
	cd "$pkgdir"
	install -dm755 usr/lib usr/share/openfire/resources
	install -dm750 etc/openfire

	cd "$srcdir"
	install -Dm644 openfire.conf "$pkgdir"/etc/conf.d/openfire
	install -Dm644 openfire.service "$pkgdir"/usr/lib/systemd/system/openfire.service
	install -Dm644 user.conf "$pkgdir"/usr/lib/sysusers.d/openfire.conf
	install -Dm644 tmpfile.conf "$pkgdir"/usr/lib/tmpfiles.d/openfire.conf

	cd openfire_src/target/openfire
	rm -rf bin logs lib/tray.dll

	cp -R conf/* "$pkgdir"/etc/openfire/
	ln -s /etc/openfire "$pkgdir"/usr/share/openfire/conf
	ln -s /var/log/openfire "$pkgdir"/usr/share/openfire/logs

	cp -R resources/security "$pkgdir"/etc/openfire/
	ln -s /etc/openfire/security "$pkgdir"/usr/share/openfire/resources/security

	cp -R plugins "$pkgdir"/usr/share/openfire/
	cp -R resources/{database,spank} "$pkgdir"/usr/share/openfire/resources/
	cp -R lib "$pkgdir"/usr/lib/openfire
}
