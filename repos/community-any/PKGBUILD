# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-net
pkgver=0.0.20190621
_commit=3b0461eec859c4b73bb64fdc8285971fd33e3938
pkgrel=1
pkgdesc='Supplementary Go networking libraries'
arch=('any')
url="https://github.com/golang/net"
license=('BSD')
depends=('go-pie' 'golang-golang-x-crypto' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/net/archive/$_commit.tar.gz")
sha512sums=('5e42e26ac17f52d6408b63eebd740bedc5a78b8023b675688d7b39b20afa53b34ffde764b693828143483c8f5450180f6a00e9eb28b8f3f6e14303cc4cd7c62b')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$srcdir"/build/src/golang.org/x/net
  cd "$srcdir"/build/src/golang.org/x/net
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/net

  install -Dm644 net-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
