# Maintainer: David Runge <dave@sleepmap.de>
_name=spreed
pkgname=nextcloud-app-spreed
pkgver=6.0.3
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud>=16.0.0' 'nextcloud<17.0.0')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('f5b5f22ad3c6fbdae592aa5ef3284868f41018ca8020d1f9340db5689eff55d0cd254f2047a6a5af8f68dcbc3bdc2b4992272872c678413df02727c35c9f2ccb')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
