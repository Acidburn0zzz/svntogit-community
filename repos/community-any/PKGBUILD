# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.4.5
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools'
             'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gabrielfalcao/sure/archive/v$pkgver.tar.gz")
sha512sums=('20999ea7d0b7f7502a8b3cdd0022857f6d0904e5cd91c95555e29716e2a4870f8dd04f268d984320211986f3cfe800f910dd9019d8d2cfa3aa0ca74fd6830de5')

prepare() {
  cp -a sure-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/sure-$pkgver
  python setup.py build

  cd "$srcdir"/sure-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sure-$pkgver
  python setup.py test

  cd "$srcdir"/sure-$pkgver-py2
  python2 setup.py test
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
