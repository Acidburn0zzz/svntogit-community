# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-inflection
pkgver=0.5.0
pkgrel=1
pkgdesc="A port of Ruby on Rails inflector to Python"
url="https://github.com/jpvanhal/inflection"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jpvanhal/inflection/archive/$pkgver.tar.gz")
sha512sums=('65acd5f51071a6ff4073a741102507a93e6a373a49676cc8f84778eb365705c8c58e8314426512f027243d11ce984115970358368de6c1b2f13692971f199e50')

build() {
  cd inflection-$pkgver
  python setup.py build
}

check() {
  cd inflection-$pkgver
  python setup.py pytest
}

package() {
  cd inflection-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
