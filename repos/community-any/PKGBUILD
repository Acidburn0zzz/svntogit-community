# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-cloudflare
pkgver=0.34.0
pkgrel=1
pkgdesc="Cloudflare DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-cloudflare' 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('9d3c7a173bfa19770b2bed61a598b0fa1bc742a741e3ee722b550b8baa3aa0bc8cbe1a65f8e7ba23884280c4e4bc4a0ed62ba079b2746580efd70fd50ad034de'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
