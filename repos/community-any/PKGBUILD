# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rfc3339-validator
pkgver=0.1.3
pkgrel=1
pkgdesc="A pure python RFC3339 validator"
url="https://github.com/naimetti/rfc3339-validator"
license=('MIT')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-hypothesis' 'python-pytest-runner' 'python-strict-rfc3339')
source=("https://github.com/naimetti/rfc3339-validator/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('d89679fb9fabcfa75cf3955744facb235201c0c3ccc4bcfd53b553c5403967226282b68e628d0dd74bb4abe553da6c3734912c63f3a5e16875e57a872c78231d')

build() {
  cd rfc3339-validator-$pkgver
  python setup.py build
}

check() {
  cd rfc3339-validator-$pkgver
  python setup.py pytest
}

package() {
  cd rfc3339-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
