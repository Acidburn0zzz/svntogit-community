# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.9.0
pkgrel=2
pkgdesc="New wave internationalisation support for Ruby"
arch=(any)
url="https://github.com/ruby-i18n/i18n"
license=(MIT)
depends=(ruby ruby-concurrent)
makedepends=(ruby-rdoc)
checkdepends=(ruby-bundler ruby-activesupport ruby-json ruby-minitest ruby-mocha ruby-rake ruby-test_declarative)
options=(!emptydirs)
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/ruby-i18n/${_name}/archive/v${pkgver}.tar.gz"
  "${pkgname}-1.9.0-broken_imports.patch::https://github.com/ruby-i18n/i18n/pull/602/commits/813124cd3cd889578cfe359de6cf46412f1548c9.patch"
)
sha512sums=('3fb0f157d64312c518c986d9b462e73e30bdb9e639672c92a7bab6e68cb3e9b5dd490ed3cfd4101eb9bf4903a6d1172ab6fdf4f6ac0bc4ed8b0de505c9bf8291'
            '810ebe6eab64eb4942506d6f49f85c789f2f13fe2e68ea36e517367e796dd265e405c83ff1abf2279cd50244fb605a9975bc8f780ed8ee09d05a3e1533aae2d1')
b2sums=('3f86850655ff8de1090f21c731ba3a37d538112e4ed97d09ae22b7edd20e32d42b1bc6e67e26a0049f1e3acc422c11ffb7be7d48f4b7fc707193e613032fd8ee'
        '9da69039e81df076fd4968e6efcd85a69449297560ffa93eddd91050d87b484df00c43fcbd8ef6be82382ec0ca61ba6392e7c3231798ae82ff379daba8034f11')

prepare() {
  cd ${_name}-${pkgver}

  # remove broken imports: https://github.com/ruby-i18n/i18n/pull/602
  patch -Np1 -i ../${pkgname}-1.9.0-broken_imports.patch

  # we don't do version pinning
  sed -r 's|~>|>=|g' -i "${_name}.gemspec"

  # we can not rely on Gemfiles as they usually contain all sorts of development dependencies as well
  rm -v Gemfile
}

build() {
  cd ${_name}-${pkgver}
  gem build "${_name}.gemspec"
}

check(){
  cd ${_name}-${pkgver}
  rake test
}

package() {
  local _gemdir="$(gem env gemdir)"

  cd ${_name}-${pkgver}
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem

  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
