# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=zim
pkgver=0.73.4
pkgrel=2
pkgdesc="A WYSIWYG text editor that aims at bringing the concept of a wiki to the desktop."
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://zim-wiki.org/"
depends=('python' 'python-gobject' 'ttf-font' 'gtk3' 'python-xdg')
checkdepends=('xorg-server-xvfb')
optdepends=('bzr: Version Control plugin'
            'git: Version Control plugin'
            'mercurial: Version Control plugin'
            'gnuplot: Insert Gnuplot plugin'
            'ditaa: Insert Ditaa plugin'
            'graphviz: Insert Diagram plugin'
            'xdot: Link Map plugin'
            'gtkspell3: Spell Checker plugin'
            'r: Insert GNU R Plot plugin'
            'scrot: Insert Screenshot plugin'
            'lilypond: Insert Score plugin'
            'gtksourceview3: Source View plugin'
            'texlive-bin: Insert Equation plugin')
source=("https://www.zim-wiki.org/downloads/${pkgname}-${pkgver}.tar.gz"
	"$pkgname-1379.patch::https://github.com/zim-desktop-wiki/zim-desktop-wiki/pull/1379.patch")
md5sums=('a4e62afc7947adf156184a28e9b07db8'
         '92340feb56e46e156813c00745543624')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i "$srcdir/$pkgname-1379.patch"
}


check() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	xvfb-run -w0 ./test.py
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	python setup.py install --root="${pkgdir}" --optimize=1

	install -dm755 "$pkgdir/usr/share/icons/"
	cp -r xdg/hicolor/ "$pkgdir/usr/share/icons/"
}
