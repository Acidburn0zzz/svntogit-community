# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=0.25.1
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
replaces=("letsencrypt-apache")
depends=("certbot=$pkgver" "python-acme=$pkgver" 'python-augeas' 'python-mock' 'python-setuptools'
         'python-zope-component' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('206c8744cd2de3fcf4a3a194273b12cb030929e6f0376907b0b26259e6dc76456e7607822c461a73d7b755068d307b76647b3ed6f9da96fd532ae0db9b6bea38'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  # https://github.com/certbot/certbot/issues/5510
  cd $pkgname-$pkgver
  python setup.py test || warning "Tests failed"
}

package() {
  cd $pkgname-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
