# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dominate
pkgver=2.3.5
pkgrel=1
pkgdesc="A Python library for creating and manipulating HTML documents using an elegant DOM API"
url="https://github.com/Knio/dominate"
license=('LGPL')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Knio/dominate/archive/$pkgver.tar.gz")
sha512sums=('373c5dec999ea4da0736acff7ad852029bfc33d6d32be290eec820182422329f122207459a09618c7b036df9028b8d9f73cc196a4274a4c69d73da8b8b98b0f1')

build() {
  cd dominate-$pkgver
  python setup.py build
}

check() {
  cd dominate-$pkgver
  python setup.py pytest
}

package() {
  cd dominate-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
