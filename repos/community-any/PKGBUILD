# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=3.0.8
pkgrel=1
pkgdesc="JupyterLab computational environment"
arch=(any)
url="https://github.com/jupyterlab/jupyterlab"
license=(custom)
makedepends=(python-setuptools python-jupyter_packaging)
depends=(python-jupyterlab_server jupyter-nbclassic nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('e2250dec8042bb824d9dd5381f38b2b34eff7f93bed693bd486b252abafddcbe')

build() {
  cd $pkgname-$pkgver
  python setup.py build 
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
