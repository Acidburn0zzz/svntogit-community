# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=11.8.0
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('nodejs-nopt' 'npm' 'semver')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('95c0ef6cac5a9de3f6d90c05f3e3c87b40b247818c93c366524451a5c959de649133331b57ea1f901330b19d9bde897e80b67922d21e4848aba999d5068b4487')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Experimental dedup
  rm -r "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/{,.bin/}nopt
  rm -r "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
