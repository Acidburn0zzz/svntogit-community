# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perlbrew
pkgver=0.43
pkgrel=1
pkgdesc='Manage perl installations in your $HOME'
arch=(any)
license=(custom:MIT)
options=(!emptydirs)
depends=('perl>=5.8.0' 'perl-capture-tiny>=0.18'
         'perl-cpan-perl-releases>=0.58' 'perl-devel-patchperl>=0.70'
         'perl-file-path-tiny>=0.3' 'perl-local-lib>=1.008')
makedepends=('perl-extutils-makemaker>=5.59')
checkdepends=(perl-test-exception perl-test-output perl-test-spec)
provides=("perl-app-perlbrew=$pkgver")
url=https://metacpan.org/release/App-perlbrew
source=("http://search.cpan.org/CPAN/authors/id/G/GU/GUGOD/App-perlbrew-$pkgver.tar.gz")
md5sums=(53cc4ca7c7f18e1953561dcfd5bfa2f1)
sha512sums=(a33276ce73e6f08edc393e0aaaaa112b5fbeb907752049c31a4221fd3e5e24beb6fd6359372653e5d3f79eea11419f75d439bc1ce57bc5d84362c6c419908f69)
_dir="$srcdir/App-perlbrew-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
  install -d -m 755 "$pkgdir"/usr/share/licenses/perlbrew
  install -m 644 LICENSE "$pkgdir"/usr/share/licenses/perlbrew/LICENSE
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
