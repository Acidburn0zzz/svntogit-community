# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter_packaging
pkgname=python-jupyter_packaging
pkgver=0.8.0
pkgrel=3
pkgdesc="Jupyter Packaging Utilities"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(python-packaging python-deprecation python-wheel python-tomlkit)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz
        fix-install_npm.patch::"https://github.com/jupyter/jupyter-packaging/commit/612e3f3d.patch")
sha256sums=('276c9f884286695f6ab57a017f4bb9dd4df4f5e232b783050d2aa55b6b9ed650'
            '089d521ef787d55869bbc9b2fd14d09fd629777d2cff2b4e0d23730a6bdd5c74')

prepare() {
  cd $_pipname-$pkgver
  patch -p1 -i ../fix-install_npm.patch
}

build() {
  cd $_pipname-$pkgver
  python setup.py build 
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
