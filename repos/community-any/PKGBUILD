# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-sekizai
pkgname=python-django-sekizai
pkgver=1.1.0
pkgrel=1
pkgdesc="Django Template Blocks with extra functionality"
arch=('any')
url="https://github.com/divio/django-sekizai"
license=('BSD')
depends=('python-django' 'python-django-classy-tags')
makedepends=('python-setuptools')
# test files are missing from 1.1.0 release
# https://github.com/divio/django-sekizai/issues/87
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
source=("${_name}-${pkgver}.tar.gz::https://github.com/divio/${_name}/archive/${pkgver}.tar.gz"
        "${pkgname}-1.0.0-disable_pep8.patch")
sha512sums=('4445adad1b3a9c8960a9bb1712e7a8f097e55d50d1d481a3064ca72fc2c11cda6a5ebf5cfbe0ae7ecb9ec70ae474b84d5c18407194104dd6524f4cd2e276e7d4'
            'a523d4e6870b4224cd69abb40f25d9e214566f01a7021e537fc0eb56e5f9ede61219ef377598c8fc8f787275b3f17dd9c1e31a599d92acbb6bf7f12a9b2048a5')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # disabling pep8 usage in the tests, as it is super ancient:
  # https://github.com/divio/django-sekizai/issues/85
  patch -Np1 -i "../${pkgname}-1.0.0-disable_pep8.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
