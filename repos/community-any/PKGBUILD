# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=5.6.0.2.20200612
_commit=3288d89f4f3d847d3d3220e3be4d1faef0af95bf
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-icon-theme' 'deepin-sound-theme')
makedepends=('python' 'go-pie' 'golang-deepin-lib')
conflicts=('deepin-artwork-themes')
replaces=('deepin-artwork-themes')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$_commit.tar.gz"
        https://github.com/linuxdeepin/deepin-desktop-schemas/commit/bf0c4e43f6b6d508ddd346c2d1e865dae9ae947d.patch)
sha512sums=('6d3c2aac03ad16e3a37008031944f9dd90e9ae49b4cf52edf945562d0fe46eed62566091b8a98b766c61506512166d65dc6132dbe2bdaa65c540d4a7818bf62b'
            'be13e501baf0517da19618011219b53d633a4186840b20b24d134e5d667c4ab1b6b716c09c78faf802b32ecf3f6f6e5e2f84744a5919b28645f002739d07ea82')

prepare() {
  mv deepin-desktop-schemas-{$_commit,$pkgver}

  export GOPATH="$srcdir/build:/usr/share/gocode"
  cd deepin-desktop-schemas-$pkgver
  # disable swap-sched
  patch -Rp1 -i ../bf0c4e43f6b6d508ddd346c2d1e865dae9ae947d.patch
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/common/com.deepin.wrap.gnome.desktop.override
}

build() {
  cd deepin-desktop-schemas-$pkgver
  make ARCH=x86
}

check() {
  cd deepin-desktop-schemas-$pkgver
  make test
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="$pkgdir" install
}
