# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jake
pkgver=10.4.3
pkgrel=1
pkgdesc="JavaScript build tool, similar to Make or Rake"
arch=('any')
url="https://jakejs.com"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('aeb68845fd2e456414134aed8df4d06860353b0d65f4eff66f670d95e4ea4158a1c8cff75b6f95e52e596d7508907a6206765a78db6c09992adb4ef405d96b3d')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
