# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsonpickle
pkgname=(python-jsonpickle python2-jsonpickle)
pkgver=0.9.4
pkgrel=1
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="http://jsonpickle.github.io/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose-show-skipped' 'python2-nose-show-skipped' 'python-numpy' 'python2-numpy'
              'python-feedparser' 'python2-feedparser' 'python-coverage' 'python2-coverage' 'python2-enum34'
              'python-simplejson' 'python2-simplejson' 'python-pymongo' 'python2-pymongo' 'python-ujson'
              'python2-ujson')
license=('BSD')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jsonpickle/jsonpickle/archive/v$pkgver.tar.gz"
        https://github.com/jsonpickle/jsonpickle/commit/a6e870c726a495d9e4e01880f478afe8f89b785d.patch)
sha512sums=('5641e66ab7a7e0a402c24ab1851304b9634e912e5a480f46b02952d0dfac3f7b0869412fb8ea1fa522f5f112fde14c69fae1bc957a55b90a6e8f82b9729f5d49'
            '29fdede1f7cc7ceba3889fa50636ffb7636b11aad725184683fc342c66c2cdb9de6b5184226edf9982e4f82b404cee3890abcfeda14e112660f24084f36c1007')

prepare() {
  (cd jsonpickle-$pkgver; patch -p1 -i ../a6e870c726a495d9e4e01880f478afe8f89b785d.patch)
  cp -a jsonpickle-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/jsonpickle-$pkgver
  python setup.py build

  cd "$srcdir"/jsonpickle-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/jsonpickle-$pkgver
  nosetests3 --show-skipped

  cd "$srcdir"/jsonpickle-$pkgver-py2
  nosetests2 --show-skipped
}

package_python-jsonpickle() {
  depends=('python')

  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-jsonpickle() {
  depends=('python2')

  cd jsonpickle-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
