# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-cairosvg
_pkgname=CairoSVG
pkgver=2.3.0
pkgrel=1
pkgdesc='SVG converter based on Cairo. It can export SVG files to PDF, PostScript and PNG files'
arch=('any')
url='https://cairosvg.org'
license=('LGPL3')
depends=('python' 'python-setuptools' 'python-cairocffi' 'python-pillow' 'python-cssselect2' 'python-defusedxml' 'python-tinycss2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort')
replaces=('cairosvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz")
sha512sums=('1969ac42836d875cc014738b952ff1c92f2eae74c1b78f70274f4de2f6e764e7d25589b43a7c725a4866c245e5fb300c87b653cc8345f802e062947363329158')

prepare() {
  cd $_pkgname-$pkgver

  cp -r cairosvg test_non_regression/cairosvg_reference/
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

