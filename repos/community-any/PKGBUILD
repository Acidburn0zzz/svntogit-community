# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20210212124342
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("https://github.com/v2fly/domain-list-community/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('8e2f529eefc24cdf8b479bf089326a4d6512988d8748979ca7972b65ad9527924d77507f4a3783010d30132272b59dfba961d8b738a345b2059bf72a3e77de98')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2fly
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2fly/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2fly/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm644 dlc.dat "$pkgdir"/usr/share/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
