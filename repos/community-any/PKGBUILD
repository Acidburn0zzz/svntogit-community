# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ashwin Vishnu <ashwinvis+arch at pr0t0nm4il dot com>
# Contributor: Specter119 <specter119 AT gmail DOT com>
# Contributor: xantares

pkgname=python-ipyparallel
_pyname=${pkgname#python-}
pkgver=8.2.0
pkgrel=2
pkgdesc='Interactive Parallel Computing in Python'
url='https://pypi.org/project/ipyparallel/'
arch=(any)
license=(BSD)
source=(https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_pyname-$pkgver.tar.gz)
depends=(jupyterlab python-psutil)
makedepends=(python-build python-installer python-wheel)
checkdepends=(python-pytest)
optdepends=('openmpi: to use the MPI Launchers in ipcluster command')
sha256sums=('0fd9f64a5120980a89b64299806c12bb8df8ceea155e5200b705301eb2401e19')

build() {
  cd $_pyname-$pkgver
  IPP_DISABLE_JS=1 \
  python -m build --wheel --no-isolation
}

check() {
  cd $_pyname-$pkgver
#  pytest
}

package() {
  cd $_pyname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mv "$pkgdir"/{usr/,}etc
}
