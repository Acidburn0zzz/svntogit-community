# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesis
pkgver=5.26.0
pkgrel=1
pkgdesc="Advanced Quickcheck style testing library for Python"
arch=('any')
license=('MPL')
url="https://hypothesis.readthedocs.org"
depends=('python-attrs' 'python-sortedcontainers')
optdepends=('python-pytz: for datetime and django module'
            'python-faker: for fakefactory and django module'
            'python-django: for django module'
            'python-numpy: for numpy module'
            'python-pytest: for pytest module'
            'python-lark-parser: for lark module')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'flake8' 'python-pytz' 'python-numpy' 'python-faker'
              'python-flaky' 'python-django' 'python-mock' 'python-pandas' 'python-dpcontracts'
              'python-pytest-xdist' 'python-lark-parser' 'python-pexpect' 'python-coverage'
              'python-typing_extensions' 'python-black')
source=("$pkgname-$pkgver.tar.gz::https://github.com/HypothesisWorks/hypothesis/archive/hypothesis-python-$pkgver.tar.gz")
sha512sums=('8fc23a5e753cffe4a6a6b1be68fa96042933ef7ec8f8c4f88fbf0901996da9de576457647bea55ff2ceca4d71db322f927847258ca9bda90e1fac9f19c79a42b')

prepare() {
  mv hypothesis-hypothesis-python-$pkgver hypothesis-$pkgver
}

build() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py build
}

check() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" PATH="$PWD/tmp_install/usr/bin:$PATH" pytest
}

package() {
  cd hypothesis-$pkgver/hypothesis-python
  python setup.py install --root="$pkgdir" --optimize=1
}
