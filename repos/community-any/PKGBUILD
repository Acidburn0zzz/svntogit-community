# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='metadata_json_deps'
pkgname="ruby-${_gemname}"
pkgver=0.2.0
pkgrel=1
pkgdesc='Verify all your dependencies allow the latest versions on Puppet Forge'
arch=('any')
url="https://github.com/ekohl/${_gemname}"
license=('Apache')
depends=('ruby' 'ruby-puppet_forge' 'ruby-puppet_metadata')
makedepends=('ruby-rdoc')
options=('!emptydirs')
source=("${url}/archive/${pkgver}/${_gemname}-${pkgver}.tar.gz")
sha512sums=('130517317a72d52bd283ab67c9c3f20848bfa968e9ffc1e5b521e5f8b368b6bf5cd06e44813b0e994b017fbc309a14fea17b5cb2823596c28c5f2704ba40aea6')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # allow latest dependencies
  sed --in-place 's|~>|>=|g' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # upstream has no tests
}

package() {
cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  # license isn't in the latest release
  # https://github.com/ekohl/metadata_json_deps/pull/5
  #install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
