# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.9.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('55cdd65bfbd44cc2c3a570c393d1cab69bf1d09253cc3c6a5ea577452515aefcc27ca935e6d847230057481f3e9a00d9206de77dc0b92f81cc26582e649f02c9')
b2sums=('cc4438bf473a9cbabbaf745467babaed46f06294d27a906ab5f4965eb5060a3bb94873ca920be23acbf168244ebb8895079ccc017b35495a9eaeeb684dd64dde')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
