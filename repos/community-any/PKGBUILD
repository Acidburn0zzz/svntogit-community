# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-astor
pkgname=('python-astor' 'python2-astor')
pkgver=0.6
pkgrel=1
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="http://astor.rtfd.org/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/berkerpeksag/astor/archive/$pkgver.tar.gz")
sha512sums=('6a0da12603db8e9242031b1382275f2dd470aca60a1f7573c95178bc2c72e53ad765585247d395a793e8d48fbb9d1bf8e3c422a3961cc3b655c8fc8242a2b370')

prepare() {
  cp -a astor-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/astor-$pkgver
  python setup.py build

  cd "$srcdir"/astor-$pkgver-py2
  python2 setup.py build
}

check() {
  # https://github.com/berkerpeksag/astor/issues/89
  cd "$srcdir"/astor-$pkgver
  nosetests3 || warning "Tests failed"

  cd "$srcdir"/astor-$pkgver-py2
  nosetests2
}

package_python-astor() {
  depends=('python')

  cd astor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-astor() {
  depends=('python2')

  cd astor-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
