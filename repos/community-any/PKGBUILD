# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9162
pkgrel=1
pkgdesc='Ameliorated interface to the CPAN'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-log-message' 'perl-archive-extract' 'perl-term-ui' 'perl-log-message-simple'
         'perl-module-pluggable' 'perl-object-accessor' 'perl-package-constants')
url=https://metacpan.org/release/CPANPLUS
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
md5sums=('aba86fd925cb1c4ad51241d49bd72971')
sha512sums=('c82bf214171dee44de776207449adbfb0e2a58ab66424f462ecffb661e9ee4fdd6c568c4ea0fddc1ea5ba6b189ec4066f0dcdc596fa2692e287b2591e618124b')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
