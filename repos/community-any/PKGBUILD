# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.1.7
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
install='spyder.install'
# desktop-file-utils needed for update-desktop-database in spyder.install
depends=('python2-qt' 'python2-pyflakes' 'desktop-file-utils')
makedepends=('python2-sphinx')
optdepends=('pylint: powerful code analysis'
            'ipython2: enhanced Python interpreter'
            'python2-rope: editor code completion, calltips and go-to-definition'
            "python2-sphinx: object inspector's rich text mode"
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-matplotlib: 2D/3D plotting')
source=("http://spyderlib.googlecode.com/files/spyder-${pkgver}.zip")
md5sums=('809f1cbafc74af0b3c06893cdedfd336')

build() {
  cd spyder-${pkgver}

  python2 setup.py build
}

package() {
  cd spyder-${pkgver}

  python2 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1

  install -D -m644 spyderlib/__init__.py \
    ${pkgdir}/usr/share/licenses/spyder/LICENSE

  install -D -m644 ${srcdir}/spyder-${pkgver}/scripts/spyder.desktop \
    ${pkgdir}/usr/share/applications/spyder.desktop
}
