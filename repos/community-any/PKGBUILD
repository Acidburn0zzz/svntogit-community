# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.15.0
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-async_generator' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver.tar.gz")
sha512sums=('a936bd8f00cb470cee355b9abe6a6df5f65af5de9144bc0c128732f65fe4bc1e60b08a4e7287ae2f303e56e1764fd0bfe771102439e6d1ba7cb495d06b0677c0')

prepare() {
  cd pytest-asyncio-$pkgver
  # Don't treat DeprecationWarnings as errors
  sed -i '/filterwarnings = error/d' setup.cfg
}

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
