# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=avr-libc
pkgver=1.7.1
pkgrel=1
pkgdesc="The C runtime library for the AVR family of microcontrollers"
arch=('any')
url="http://savannah.nongnu.org/projects/avr-libc/"
license=('BSD')
depends=('gcc-avr')
options=('!strip')
source=(http://savannah.nongnu.org/download/avr-libc/${pkgname}-${pkgver}.tar.bz2{,.sig} \
        avr-libc-1.7.1-gcc46.patch)
sha1sums=('8ac3f03229101c427aa90f8293ddabde6c3a6f87'
          'd33b8bbf6f9b450c419352b352a506ac5a01fe44'
          '8d58c50685f3c8ce462c1f15e847eac106ab92f8')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/avr-libc-1.7.1-gcc46.patch"
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
