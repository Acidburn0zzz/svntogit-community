# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-net
pkgver=0.0.20190211
_commit=3a22650c66bd7f4fb6d1e8072ffd7b75c8a27898
pkgrel=1
pkgdesc='Supplementary Go networking libraries'
arch=('any')
url="https://github.com/golang/net"
license=('BSD')
depends=('go-pie' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/net/archive/$_commit.tar.gz")
sha512sums=('4a8b5aaafd122fe55ed3bab8cf23a2334443f0ba788f5b7b98340ffbc672721ec593008a80857867a17f1bf87cafcbed0326de5a5a75654c2b2b971af7787589')

check() {
  # Tests need permission to send icmp packets

  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$srcdir"/build/src/golang.org/x/net
  cd "$srcdir"/build/src/golang.org/x/net
  go test -v $(go list ./... | grep -v vendor) || warning "Tests failed"
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/net-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/net

  install -Dm644 net-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
