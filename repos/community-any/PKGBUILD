# Maintainer: David Runge <dave@sleepmap.de>
pkgname=tmuxp
pkgver=1.5.0a
pkgrel=2
pkgdesc="tmux session manager. built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('BSD')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux>=0.8.0' 'tmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
# pypi sources don't include tests: https://github.com/tmux-python/tmuxp/issues/431
#source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e98847eb8613f44f114ecb896bc39ec213a4c7a0c29eb45ea243735aa835582db28d69baeb374c09779687ef0be692c7191fe31226f1b89cc6e4c73d226b0aa8')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# test_workspacebuilder.py does not return:
# https://github.com/tmux-python/tmuxp/issues/377
check() {
  cd "${pkgname}-${pkgver}"
  py.test -k "not test_workspacebuilder"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES,README.rst}
}

# vim:set ts=2 sw=2 et:}
