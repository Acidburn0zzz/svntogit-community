# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20210112
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('acd3aefa6df1e2cfec62e09498ec7b5f557d712de2e8f34a17c52d99d2e90ed43fc8ba00f449a9eeb63beed9501f50d9b0ea772c1b696c2747288d8e3ff287ad'
            'SKIP')
b2sums=('26d13f5baefda30223cc5fbb39d199ecfbea2b64612063311b7abc589f47034a9a0725ef2b9b1393f12b4342c7ffd994908afdb872a1f8afa76d9c0c4df44403'
        'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd services
  sed -e "s|etc/init.d|usr/bin|" -i *".service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 *".service" -t "${pkgdir}/usr/lib/systemd/system/"
}
