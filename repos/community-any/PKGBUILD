# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=2.0
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
# setuptools is in depends as pkg_resources is used in sortversion.py
depends=('python' 'python-setuptools' 'python-toml' 'python-structlog' 'python-appdirs' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'git: support for git repositories'
)
source=("https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-exclude-docs.patch::https://github.com/lilydjwg/nvchecker/commit/b3bc7ec04975ab43a651b78cd708e8c32f974293.patch")
sha512sums=('1ae9b52c01ac683e5f74d9a10b7b26d5bad62b54a905f9372afea31fc084f4ff6e426a01634b0ff040270d1dff256f8385426bfe470d434a7f694446f1656ebb'
            'f6a131501d3249e6de23b345d9be70dc7209a638af50bcb9c4e5928f9d19ef85e377bdc6ca97dcc90cc868859f9bfc35c8e3a6cd1abf9f5c022e894119d6cb32')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-exclude-docs.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname

  # bash completion scripts & docs
  install -Dm644 scripts/nvtake.bash_completion "$pkgdir"/usr/share/bash-completion/completions/nvtake
  install -Dm644 docs/usage.rst -t "$pkgdir"/usr/share/doc/$pkgname/
  # Installed with a different filename as it is renamed soon after the release
  # https://github.com/lilydjwg/nvchecker/commit/854399a30aaafed37bdfcf2c8fe561f4702124b7
  install -Dm644 sample_source.toml "$pkgdir"/usr/share/doc/$pkgname/sample_config.toml
}

# vim:set ts=2 sw=2 et:
