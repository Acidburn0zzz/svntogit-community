# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: gardar <aur@gardar.net>
# Contributor: Morten Linderud <morten@linderud.pw>

_name=pytest-testinfra
pkgname=python-pytest-testinfra
pkgver=6.2.0
pkgrel=1
pkgdesc='Testinfra test your infrastructures'
url="https://github.com/pytest-dev/pytest-testinfra"
arch=('any')
license=('Apache')
depends=('python-pytest')
makedepends=('python-hacking' 'python-setuptools_scm' 'python-sphinx')
checkdepends=('ansible' 'python-mock' 'python-paramiko' 'python-pytest-cov'
'python-pytest-xdist' 'python-pywinrm' 'python-tornado' 'salt')
optdepends=('ansible: for tests using ansible inventories'
            'docker: for test running docker containers'
            'kubectl: for test running containers in kubernetes'
            'lxd: for test running LXC or LXD containers'
            'podman: for test running podman containers'
            'python-paramiko: for testing remote hosts'
            'python-pywinrm: for testing on Windows hosts'
            'salt: for tests using saltstack')
conflicts=('python-testinfra')
provides=('python-testinfra')
replaces=('python-testinfra')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('60cb08fa03030ab00cbb5609308aae17af4d6b213769e683ba185277067f3c9ebd84409104d0cfdaf5e899281a2c391f53581ca6502117df392314e77b8499fb')
b2sums=('dbf43324a227d6d3d1b3d0820b117e0995e05cb5820c0cc8581632713dff5617dfc158a3c7de1e6cc4f97cbaeccc04761acfcc74e8a9e266c2c58637ae2c3261')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
  make -C doc man
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:$PYTHONPATH"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  # man page
  install -Dm 644 doc/build/man/*.1 \
    -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {CHANGELOG,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
