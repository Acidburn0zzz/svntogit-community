# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.13.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-cov python-yaml python-mock python-parameterized python-cfn-lint)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/awslabs/serverless-application-model/archive/v$pkgver.tar.gz")
sha256sums=('975023b35ff50632c1f25eae9700507bc5f6564bc309db81569a32d3184a6eeb')

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  pytest -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
