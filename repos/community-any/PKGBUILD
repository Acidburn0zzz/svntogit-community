# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-cpanplus-dist-arch
pkgver=1.22
pkgrel=1
pkgdesc='CPANPLUS backend for building Archlinux pacman packages'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/CPANPLUS-Dist-Arch
source=("http://search.cpan.org/CPAN/authors/id/J/JU/JUSTER/CPANPLUS-Dist-Arch-$pkgver.tar.gz")
md5sums=(b45922c0df445dc512b51fb71004fa65)
sha512sums=(19bbac21883fc2d45858b373b384e103907da1f26a994d125a491c9a3eacb9cc4ac30e80c6c588d11cde69ce01e9d27c3c2bd633dbdbccd1a4451a401b4accee)
_dir="$srcdir/CPANPLUS-Dist-Arch-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  /usr/bin/perl Build.PL
  ./Build
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  ./Build test
)

package()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 MODULEBUILDRC=/dev/null
  unset PERL5LIB PERL_MB_OPT
  ./Build install installdirs=vendor destdir="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
