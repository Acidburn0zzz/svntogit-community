# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Simon Zimmermann <simon@insmo.com>

pkgname='pass'
pkgver=1.7
pkgrel=2
pkgdesc='Stores, retrieves, generates, and synchronizes passwords securely'
arch=('any')
url='https://www.passwordstore.org/'
license=('GPL2')
depends=('xclip' 'bash' 'gnupg' 'tree')
checkdepends=('git')
optdepends=('git: for Git support'
            'dmenu: for passmenu'
            'qrencode: for QR code support')
replaces=('passmenu')
provides=('passmenu')
conflicts=('passmenu')
source=("https://git.zx2c4.com/password-store/snapshot/password-store-${pkgver}.tar.xz"
        "https://git.zx2c4.com/password-store/patch/?id=6f867674272a03273212259b9039c9356a6ed90f")
sha256sums=('161ac3bd3c452a97f134aa7aa4668fe3f2401c839fd23c10e16b8c0ae4e15500'
            '76239495c6f6f227f38bf1e6fdbb6ab44e895ae411b56b1953a2025393440301')

prepare() {
  cd "${srcdir}/password-store-$pkgver/"
  patch -p1 -i '../?id=6f867674272a03273212259b9039c9356a6ed90f'
}

check() {
  cd "${srcdir}/password-store-$pkgver/"
  make test
}

package() {
  cd "${srcdir}/password-store-$pkgver/"
  make DESTDIR="${pkgdir}" WITH_ALLCOMP=yes install

  cd contrib/dmenu
  install -Dm0755 passmenu "${pkgdir}/usr/bin/passmenu"
}
