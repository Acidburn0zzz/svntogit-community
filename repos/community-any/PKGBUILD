# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=8.3.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("https://github.com/openstack/oslo.config/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-stevedore-gt-2.0.1.patch::https://opendev.org/openstack/oslo.config/commit/a2652b3584da06e4c4ebe96261672309b01c1122.patch")
sha512sums=('e77cf721ee371b4a4dddd05cdcdf9920d3c8339ab6bf3500073edf8e0ce88b3875f2bbc7615cdefd6793156d4d43da7622de2b02936fab1ba2c5e6ee114a5822'
            '8219733b4b76f2b76f9cb8fb378382b78e1f88e61a9605afde0bab1f525f4a115cabf322d907352c202bb12f42f8cc2d50eb747f692dc01e6c3daa5f808ac381')

export PBR_VERSION=$pkgver

prepare() {
  cd oslo.config-$pkgver
  patch -p1 -i ../$pkgname-stevedore-gt-2.0.1.patch
}

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  stestr run
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
