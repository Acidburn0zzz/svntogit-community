# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=dex
pkgver=0.8.0
pkgrel=3
pkgdesc='Program to generate and execute DesktopEntry files of type Application'
url='https://github.com/jceb/dex'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('git' 'python-sphinx')
source=("https://github.com/jceb/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        fix-dex-version.patch)
sha512sums=('d87a7306b931736aa302dc883cbd9c635397eda2c119133a3ae8028869a4302ce3713b43cc85fdb2f804d1a74ab2499ef32a2680d5d6be039831006e617aa70a'
            'a8f3e3d6794e7bbe0aa0f27eca0ed8122e9448920379e7a701d38df0c93cf969be5e4b0e531d2e7ef6367c3360ee2244d026836765c8f2b09e06dafb6a1323a9')
b2sums=('de62557d207d257286cc706c07969b10b0d753b197333f1d24c0c0a22a897fb2e3c64a25f71770424e6cc365d028c9a1e994fcf35c68c96be7281d24ce19b1e3'
        'b871cdb4f8c3a91a5722ea969565cd0de8eb72edc2f615706dcffcda8ed460cd3856a28c312e53fe0a46e3f4b6ca51605fe659486e28df8524c2ad01e2a5bd6e')

prepare() {
  cd "$pkgname-$pkgver"
  patch dex <"$srcdir"/fix-dex-version.patch
}

package() {
  cd "$pkgname-$pkgver"
  make install PREFIX=/usr MANPREFIX=/usr/share/man DESTDIR="$pkgdir"
  rm "$pkgdir/usr/share/doc/dex/LICENSE"
}

# vim:set ts=2 sw=2 et:
