# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.22.1
pkgrel=2
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-pyaml python-six python-requests python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools)
checkdepends=(python-pytest python-mock)
source=("https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver/cfn-python-lint-$pkgver.tar.gz"
        cfn-lint-pr1003.patch::https://github.com/aws-cloudformation/cfn-python-lint/pull/1003.patch)
sha256sums=('d24d2efa60bda4596dae759a07943d4785202a90c067f530af55c104dbfacb86'
            'fa60ce826c0438c55520239903312aab816a2016fc39c39900976077b21a5c55')

prepare() {
  cd cfn-python-lint-$pkgver
  patch -Np1 -i ../cfn-lint-pr1003.patch
}

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  PYTHONPATH=build/lib pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
