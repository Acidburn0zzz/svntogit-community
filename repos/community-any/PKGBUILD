# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-httpretty
pkgver=1.1.2
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
depends=('python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-eventlet' 'python-freezegun' 'python-httplib2'
              'python-redis' 'python-rednose' 'python-requests' 'python-sure' 'python-tornado'
              'python-httpx' 'python-boto3')
source=("https://github.com/gabrielfalcao/HTTPretty/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('5816d7533a211348a9aeb61bb6ab97c01aac02d9f4fe679764fa906ba5ba7b573e737a6ae6b5118b82fce18aa17bcf286b895e30d516c6d35e76926f4739b124')

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests
}

package_python-httpretty() {
  cd HTTPretty-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
