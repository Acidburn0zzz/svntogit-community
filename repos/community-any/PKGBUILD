# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Randy Heydon <randy.heydon at clockworklab dot net>

pkgname=xdot
_pkgname=${pkgname}.py
pkgver=0.7
pkgrel=1
pkgdesc="Interactive viewer for graphs written in Graphviz's dot language"
url="https://github.com/jrfonseca/xdot.py"
arch=('any')
license=('LGPL2.1')
depends=('graphviz' 'python-gobject' 'python-cairo' 'gtk3')
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jrfonseca/${_pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('793be28fbd0ee6c2b9dd97532ef8742d598f84055ef67e688593d7c1f689f04593dd89a490f4f48866b18ac595252d03b9cc6d8d8e2d6c0ac1ef1ad28789c512')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim: ts=2 sw=2 et:
