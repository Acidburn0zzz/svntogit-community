# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=5.2.1
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
         'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
         'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-openstackclient/archive/$pkgver.tar.gz")
sha512sums=('16f549c67bd0da825850ab8e1abb965311d7a508b7a49de79e6058041765735e92398d22e1723dbf37262ff7650bb234061d6dddb529649d0b3af4ff437f2f12')

export PBR_VERSION=$pkgver

build() {
  cd python-openstackclient-$pkgver
  python setup.py build
}

check() {
  cd python-openstackclient-$pkgver
  stestr run || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package() {
  cd python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
