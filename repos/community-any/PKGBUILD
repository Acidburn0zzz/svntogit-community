# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Henrik Hodne <henrik@hodne.io>
# Contributor: Ariel AxionL <axionl@aosc.io>
# AUR Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=fira-code
pkgname=('ttf-fira-code' 'otf-fira-code')
pkgver=1.206
pkgrel=1
pkgdesc="Monospaced font with programming ligatures"
arch=('any')
url='https://github.com/tonsky/FiraCode'
license=('custom:OFL')
depends=()
source=("$pkgbase-$pkgver.tar.gz::https://github.com/tonsky/FiraCode/archive/${pkgver}.tar.gz")
sha512sums=('022216fb3b7c553cd0608a51e8e5a2903375a15d3600a694dad68666897a5ed33d7ad6819db55e0ce4b7c58ac1dd3c33d2e05aded62ebf38365ffee8fb7f8ee0')

function _package {
    local dir="$srcdir/FiraCode-$pkgver"
    cd "$dir/distr"
    local ext="${1%%-*}"
    cd "$ext"
    local fonts=(FiraCode*."$ext")
    local installdir="${ext^^}"

    # Prepare destination directory
    install -dm755 "$pkgdir/usr/share/fonts/$installdir"

    # Install fonts
    local font
    for font in "${fonts[@]}"; do
        install -m644 "$font" "$pkgdir/usr/share/fonts/$installdir"
    done

    # Install LICENSE
    install -Dm644 "$dir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

for _pkgname in ${pkgname[@]}; do
    eval "function package_$_pkgname() { _package $_pkgname; }"
done

# vim:set ts=8 sw=4 et:
