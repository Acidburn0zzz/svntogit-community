# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-news
_name=news
pkgver=17.0.0
pkgrel=1
pkgdesc="An RSS/Atom feed aggregator"
arch=(any)
url="https://github.com/nextcloud/news"
license=(AGPL3)
makedepends=(nextcloud ripgrep yq)
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/${pkgver}/${_name}.tar.gz")
sha512sums=('de807636e3c44c2abec30dcca6948aad4c719452bd17169a00c96db3904e2a7e23d8f4142ff5a682e740af3c1e421d36db255da15c05da8ddd9ef46e7dbac61b')
b2sums=('9b61b5cacc2cd1fc5a834293096b17430b5915a6174fc1d40bde0e0f990e8b55a3bd35aeaa819ff21d2128093f9752750566aa82f51183c6194256a5effe1fd4')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
