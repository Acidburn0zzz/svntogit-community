# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-module-scandeps
_pkgname=Module-ScanDeps
pkgver=1.28
pkgrel=1
pkgdesc="Scan file prerequisites"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-requires')
url="https://search.cpan.org/dist/Module-ScanDeps"
source=("https://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/Module-ScanDeps-$pkgver.tar.gz")
sha512sums=('60debdc8e51b0b389fc0bf480e94012592f01bb6de93ac95265cc6fe07645008a30d6c6d84511ad0221239c14dbcb33df01128e9905d0a1a7cfc862b7d9893eb')

build() {
  cd Module-ScanDeps-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-ScanDeps-$pkgver
  make test
}

package() {
  cd Module-ScanDeps-$pkgver
  make DESTDIR="$pkgdir" install
}
