# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pynitrokey
pkgname=python-pynitrokey
pkgver=0.4.13
pkgrel=1
pkgdesc="A command line interface for the Nitrokey FIDO2 and Nitrokey Start"
arch=(any)
url="https://github.com/Nitrokey/pynitrokey"
license=(Apache)
depends=(python-cbor python-cffi python-click python-cryptography
python-dateutil python-ecdsa python-fido2 python-intelhex python-nkdfu
python-pygments python-pyserial python-pyusb python-requests python-urllib3)
makedepends=(git python-build python-flit python-install)
source=(
  "git+https://github.com/nitrokey/pynitrokey#tag=v${pkgver}.nitrokey?signed"
  "${pkgname}-0.4.13-remove_spsdk.patch"
)
sha512sums=('SKIP'
            'd40215ef06c9030abe0617c68063cf34a8a79e6bdb122094d5ab72c3a518952329215b78a5086e1b177beef498b072d7786fe86d499bc483ab64ce734644ed91')
b2sums=('SKIP'
        'cdcaec09cddfca5627f34a2a82e8b2360ab1d9ba0fde9881f8bd40fd46254f911461734a519ebf4a4472cba56ac4729f7841238b360e685772b9cb51a31df1df')
validpgpkeys=(868184069239FF65DE0BCD7DD9BAE35991DE5B22)  # Szczepan Zalega (Nitrokey) <szczepan@nitrokey.com>

prepare() {
  cd "${_name}"
  # remove nitrokey3 support for now, until spsdk is packaged: https://github.com/Nitrokey/pynitrokey/issues/162
  patch -Np1 -i ../"${pkgname}-0.4.13-remove_spsdk.patch"
}

build() {
  cd "${_name}"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "${_name}"
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
