# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Pierre Chapuis <catwell@archlinux.us>
# Contributor: Stefano Esposito <ragnarok@email.it>

pkgbase=python-irc
pkgname=('python-irc' 'python2-irc')
pkgver=3.4.2
pkgrel=1
pkgdesc="IRC (Internet Relay Chat) protocol client library for Python"
makedepends=('python2-distribute' 'python-distribute')
arch=('any')
url="http://pypi.python.org/pypi/irc"
license=('LGPL')
source=(http://pypi.python.org/packages/source/i/irc/irc-$pkgver.zip)
md5sums=('dc0fbb048d684b2a3c202c2911426a70')

build() {
  cd "$srcdir"
  cp -a "irc-$pkgver" "irc2-$pkgver"
}

package_python2-irc()
{
  depends=('python2')

  cd "$srcdir/irc2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
}

package_python-irc()
{
  depends=('python')

  cd "$srcdir/irc-$pkgver"
  export LC_ALL=en_US.UTF-8
  python3 setup.py install --root="$pkgdir" --optimize=0
  find "$pkgdir/" -name '*.pyc' -delete
  find "$pkgdir/" -type d -empty -delete
}

# tests require AUR packages python{,2}-pytest

#check()
#{
#  cd "$srcdir/irc-$pkgver"
#  export LC_ALL=en_US.UTF-8
#  python3 setup.py test 
#
#  cd "$srcdir/irc2-$pkgver"
#  python2 setup.py test 
#}
