# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-linuxdeepin-go-x11-client
pkgver=0.2.0.20190129
_commit=d4cdc0beb6fd09e8369bbbad10ee99affb1a05a0
pkgrel=1
pkgdesc='X11 protocol go language binding'
arch=('any')
url="https://github.com/linuxdeepin/go-x11-client"
license=('GPL')
depends=('go-pie' 'golang-deepin-lib' 'golang-github-stretchr-testify' 'golang-golang-x-text'
         'golang-gopkg-check.v1')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/go-x11-client/archive/$_commit.tar.gz")
sha512sums=('d543f1220b002b436696dea246ea192106602ec7389ee0d6a235fe660cea2f181f7ee4942c3178f526bd3412063d57d2fe1fd7989799993aac280df747326a12')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-x11-client-$_commit "$srcdir"/build/src/github.com/linuxdeepin/go-x11-client
  cd "$srcdir"/build/src/github.com/linuxdeepin/go-x11-client
  xvfb-run go test -v $(go list ./... | grep -v vendor)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-x11-client-$_commit "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-x11-client

  rm -r "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-x11-client/debian
}
