# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=0.9.2
pkgrel=1
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('765f096cf48710c80fa0e131ce8e06de9d61f214829924b3c109d6b938dfa011bbf79965e07e49640c11f74ac262d3fef176ae4f651016ec81d5bc252141b9eb')

build() {
  cd numpydoc-$pkgver
  python setup.py build
}

check() {
  cd numpydoc-$pkgver
  python setup.py pytest
}

package() {
  cd numpydoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
