# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=7.1.0
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='http://cordova.apache.org'
license=('Apache')
depends=('npm' 'acorn' 'semver' 'browserify')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('c7d018743634a8985fd425973d89a93581a537f8160f248ba2e95bf9829a93fde34be9178f007f0106ffbbd645d8913f614b2d2742a37df49711cb06b54a334d')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver npm browserify; do
    rm -r $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
