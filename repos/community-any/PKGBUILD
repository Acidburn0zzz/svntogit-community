# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.8.14
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('ada1853ff9559f19dbb153ef4b60a6daf58dadaf0524fcb35c4a38e8c0a476acfbeec2f4d02ebfa1858c9d2660ffc8695d7ff8055a23e8dc40aca8290c53ac69')

prepare() {
  cp -a construct-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/construct-$pkgver
  python setup.py build

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct-$pkgver
  python setup.py pytest

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py pytest
}

package_python-construct() {
  depends=('python')

  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')

  cd construct-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
