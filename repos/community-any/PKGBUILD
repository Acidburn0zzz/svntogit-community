# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-dance
pkgver=2.1.0
pkgrel=1
pkgdesc="Doing the OAuth dance with style using Flask, requests, and oauthlib"
url="https://github.com/singingwolfboy/flask-dance"
license=('MIT')
arch=('any')
depends=('python-requests' 'python-oauthlib' 'python-requests-oauthlib' 'python-flask'
         'python-urlobject' 'python-six' 'python-lazy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-sphinx' 'python-pytest-cov' 'python-mock'
              'python-responses' 'python-freezegun' 'python-sqlalchemy' 'python-sqlalchemy-utils'
              'python-flask-sqlalchemy' 'python-flask-login' 'python-flask-caching'
              'python-flask-sphinx-themes' 'python-sphinxcontrib-seqdiag')
source=("$pkgname-$pkgver.tar.gz::https://github.com/singingwolfboy/flask-dance/archive/v$pkgver.tar.gz")
sha512sums=('e8d22f24854df854e30154121d540a897da7cde6c7f155101232b7507ad7847296e5003183a73281e1c16fb514e020945233001a12394367f1059631886f2b6a')

build() {
  cd flask-dance-$pkgver
  python setup.py build
}

check() {
  cd flask-dance-$pkgver
  python setup.py pytest
}

package() {
  cd flask-dance-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
