# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=LibCST
pkgname=python-libcst
pkgver=0.3.8
pkgrel=1
pkgdesc="A concrete syntax tree parser and serializer library for Python that preserves many aspects of Python's abstract syntax tree"
arch=('any')
url='https://github.com/Instagram/LibCST'
license=('MIT')
depends=('python-typing_extensions' 'python-typing_inspect' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis' 'python-isort' 'python-black')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f67ba966e998a251930f5b14de95e7edc592d35a02d8b644878ae905c10858172510f61941519e294c55349ffaa4e11652cf9993be22caf9bc6221779071952e')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest --addopts '--ignore libcst/tests/test_fuzz.py'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
