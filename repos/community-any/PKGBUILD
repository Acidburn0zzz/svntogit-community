# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.2
pkgrel=1
pkgdesc='Automate Clojure projects'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(java-environment)
makedepends=(git)
optdepends=('rlwrap: for readline support'
            'leiningen-completions: completion scripts')
source=("$url/releases/download/$pkgver/leiningen-$pkgver-standalone.zip"{,.asc}
        "git+$url#commit=df32368a84c3441b59d0310a991a3e10c91b28b8") # tag: 2.9.2
noextract=("leiningen-$pkgver-standalone.zip")
validpgpkeys=('20242BACBBE95ADA22D0AFD7808A33D379C806C3') # Phil Hagelberg <phil@hagelb.org>
sha256sums=('09805bd809656794cfe7d9155fd4b8bea2646092318690bf68e3c379574a2d3c'
            'SKIP'
            'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein-pkg "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.zip" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# getver: github.com/technomancy/leiningen/releases
# vim: ts=2 sw=2 et:
