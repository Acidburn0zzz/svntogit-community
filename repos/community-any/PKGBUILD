# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-wsproto
pkgname=(python-wsproto python2-wsproto)
pkgver=0.14.0
pkgrel=1
pkgdesc="WebSockets state-machine based protocol implementation"
url="https://github.com/python-hyper/wsproto"
license=('MIT')
arch=('any')
makedepends=('python-h11' 'python2-h11' 'python-setuptools' 'python2-setuptools' 'python2-enum34')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/w/wsproto/wsproto-$pkgver.tar.gz")
sha512sums=('91ffc51229434b8a3c1517b22a082dce0078e38e0f5e7ad016aca9b4d4f2a77c2f9901ea9227e80e61009e70d3ffebb44ed9ae4c988d54a9807b45076e81d43f')

build() {
  cd wsproto-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd wsproto-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-wsproto() {
  depends=('python-h11')

  cd wsproto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/lib/python3.7/site-packages/{,wsproto/}test
}

package_python2-wsproto() {
  depends=('python2-h11' 'python2-enum34')

  cd wsproto-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/lib/python2.7/site-packages/{,wsproto/}test
}
