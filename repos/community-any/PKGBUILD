# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: jeneshicc <jeneshicc@gmail.com>

pkgname=skk-jisyo
pkgver=20190623
pkgrel=1
pkgdesc="Dictionaries for the SKK Japanese input method"
arch=('any')
url="http://openlab.ring.gr.jp/skk/"
optdepends=('skktools: Dictionary maintenance tools')
license=('GPL')
install=${pkgname}.install

_skk_jisyo_gz=(L
               M
               ML
               S
               JIS2
               JIS2004
               JIS3_4
               assoc
               china_taiwan
               fullname
               geo
               itaiji
               itaiji.JIS3_4
               jinmei
               law
               lisp
               mazegaki
               okinawa
               propernoun
               pubdic+
               requested
               station
               wrong
               wrong.annotated)

source=()
for _t in "${_skk_jisyo_gz[@]}" ; do
  source+=("SKK-JISYO.$_t.$pkgver.gz::http://openlab.ring.gr.jp/skk/dic/SKK-JISYO.$_t.gz")
done

md5sums=('6a51b455ead4ce03ad0c6fd3201b61bf'
         'f7aa51134b25c5481daca8cb396b3964'
         '7cf77e039da06101bef47adb30ca2b34'
         'ad07adfb18b270d54faf0b8a63b4a296'
         'dd400ae8446958dc8976b629a5afa37e'
         '87bb25fd8bbaaab7ad0af9ab1c2c6f2a'
         '569935e0fd4a8acf064f3ee5effec762'
         'f4d39ee02f15216456bddc07d2b25559'
         'f07048fb6568a2d6c8a3af0859700afb'
         '03282044a5069ac1baffb35e2e01199f'
         '1c6950951234f06168e16ab3718fbd90'
         'e2df40facb2bc150fa12cdff6f168ab3'
         '2af0a6271df8f8e1e9ddac2d6be92d69'
         '87bab3dd7440c2577e28573a613c63e7'
         'b9c6a38ff2aafe0e24be793c038f78b0'
         '829293eba49a4c48399f43ce43ead238'
         '05536815be073f4eb36e9ad147a2389d'
         '4bf0b1761f03e05caa61c8a6ff295d9f'
         '53407c1d74036759a7db1fa07dd8e5d4'
         'fd4b8dda0e411c82435e97a0a03771e7'
         'be9617afe977227b721ec46f6114aff5'
         '8a7d5efefea00655b4c178dff4c89070'
         'fa64a19e920f984d33980333f0e1e930'
         'bbbf4139679c451a342a55406ad7d907')


package() {
  mkdir jisyo
  for _t in "${_skk_jisyo_gz[@]}" ; do
    cp "SKK-JISYO.$_t.$pkgver.gz" "jisyo/SKK-JISYO.$_t.$pkgver.gz"
    gunzip "jisyo/SKK-JISYO.$_t.$pkgver.gz"
    install -D -m 644 "jisyo/SKK-JISYO.$_t.$pkgver" "$pkgdir/usr/share/skk/SKK-JISYO.$_t"
  done
}
