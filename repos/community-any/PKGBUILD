# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-httpretty
pkgver=1.1.1
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
depends=('python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-eventlet' 'python-freezegun' 'python-httplib2'
              'python-redis' 'python-rednose' 'python-requests' 'python-sure' 'python-tornado'
              'python-httpx' 'python-boto3')
source=("https://github.com/gabrielfalcao/HTTPretty/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-issue417-test-fix.patch::https://github.com/gabrielfalcao/HTTPretty/commit/5878dde4211c97392d89db221bf3ff59b6c18274.patch)
sha512sums=('526ef3de30a7227924ccef68cbe0c679d640bf31852b10e518798dbc7eb7c8390b112962f4f51c5a3157184dce3b145120e7a1750fe22f719247e3c86fc27f58'
            'c57e4c796b99784627587b196e1e4cf07f7bda5f050d75ed469b806417b13fbe6106fc604e3d3d94ad836a0afea2af5e337cee9f6bd84ae9b190718c8e57de39')

prepare() {
  # https://github.com/gabrielfalcao/HTTPretty/pull/427
  patch -d HTTPretty-$pkgver -p1 < $pkgname-issue417-test-fix.patch
}

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests
}

package_python-httpretty() {
  cd HTTPretty-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
