# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

# TODO: See if the waf python files can be placed elsewhere

pkgname=waf
pkgver=1.7.11
pkgrel=1
pkgdesc='General-purpose build system modelled after Scons'
url='http://code.google.com/p/waf/'
arch=('any')
license=('BSD')
depends=('python')
provides=('python-waf')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('913bba166f09f607782cb659de4de5306c97550a6ef34e5ac753ce647f2b288c')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  # Extracting license
  head -n 30 waf | tail -n 25 > LICENSE
  # Python 3 fix
  sed -i '0,/env python/s//python3/' waf
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./waf-light configure --prefix=/usr
  ./waf-light --make-waf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  ./waf-light install -f --destdir="$pkgdir" \
    --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8,eclipse'

  install -Dm755 waf "$pkgdir/usr/bin/waf"

  # Force the generation of .waf.admin files
  cd demos/c
  "$pkgdir/usr/bin/waf" configure build >& /dev/null
  cd ../..

  # Fixing permissions
  chmod -R 0644 "$pkgdir"/usr/bin/.waf*/waflib/Tools/*
  chown -R root:root "$pkgdir"/usr/*

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
