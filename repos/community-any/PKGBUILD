# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Dupuis <lisael@lisael.org>
# Contributor: Rafael Reggiani Manzo <rr.manzo@gmail.com>

pkgname=perl-test-file
pkgver=1.447
pkgrel=1
pkgdesc="This modules provides a collection of test utilities for file attributes."
arch=('any')
url='https://github.com/briandfoy/test-file'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-test-utf8')
source=("https://search.cpan.org/CPAN/authors/id/B/BD/BDFOY/Test-File-$pkgver.tar.gz")
sha512sums=('6bcf4045c5f8e5d6541400987f2e109a30c6d2fa47f43a8ea84fd15fbed1b355b0977e2f5baf2feaeaf21a23f207d28bf37600faef1c8681eb77f5d78e4050f6')

build() {
  cd Test-File-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-File-$pkgver
  make test
}

package() {
  cd Test-File-$pkgver
  make DESTDIR="$pkgdir" install
}
