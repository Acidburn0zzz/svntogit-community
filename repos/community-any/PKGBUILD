# Maintainer: David Runge <dvzrv@archlinux.org>
_name=JACK-Client
pkgname=python-jack-client
pkgver=0.5.1
pkgrel=1
pkgdesc="JACK Audio Connection Kit (JACK) Client for Python"
arch=('any')
url="https://github.com/spatialaudio/jackclient-python/"
license=('MIT')
depends=('jack' 'python-cffi')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
optdepends=('python-numpy: Access input/output buffers as numpy arrays')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c2f52930c3f27b7b1f271104f1b3f41f52dff06574cf8fa3e4b56f44d68481426f0cf12c284606b66fec935dbdf61c152dbdf45ee80d1bd29c136683bf71170e')
validpgpkeys=()

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CONTRIBUTING,NEWS,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # examples
  install -vDm 755 examples/*.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}
