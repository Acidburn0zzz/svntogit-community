# $Id: PKGBUILD 141139 2015-09-20 03:14:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sortedcontainers
pkgname=('python-sortedcontainers' 'python2-sortedcontainers')
pkgver=2.0.3
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/grantjenks/python-sortedcontainers/archive/v$pkgver.tar.gz")
sha512sums=('55324c5dd4223c47f647b0bd1482120811f126da72636bf39ddb19fee9fc3df75041579233757b4029f5c9023c75f48d8f71688d1c72255fbb8e0639f418fb42')

prepare() {
  # Don't use tox
  sed -i '/tests_require/d' python-sortedcontainers-$pkgver/setup.py

  cp -a python-sortedcontainers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-sortedcontainers-$pkgver
  python setup.py build

  cd "$srcdir"/python-sortedcontainers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-sortedcontainers-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-sortedcontainers-$pkgver-py2
  python2 setup.py pytest
}

package_python-sortedcontainers() {
  depends=('python')

  cd python-sortedcontainers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sortedcontainers() {
  depends=('python2')

  cd python-sortedcontainers-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
