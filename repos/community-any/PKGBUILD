# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

pkgbase=python-seaborn
pkgname=('python2-seaborn' 'python-seaborn')
pkgver=0.9.0
pkgrel=3
pkgdesc="Statistical data visualization"
arch=('any')
url="https://seaborn.pydata.org/"
license=('BSD')
makedepends=('python2-setuptools' 'python2-pandas' 'python2-matplotlib' 'python2-scipy' 'python2-beautifulsoup4'
             'python-setuptools' 'python-pandas' 'python-matplotlib' 'python-scipy' 'python-beautifulsoup4')
source=("https://github.com/mwaskom/seaborn/archive/v${pkgver}.tar.gz")
sha1sums=('490781eda5d8ff18020aac87b5f9af0d4088c71c')

prepare() {
  cd "$srcdir"
  cp -a seaborn-${pkgver} seaborn-py2-${pkgver}
}

build() {
  # "Building Python2"
  cd "$srcdir"/seaborn-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/seaborn-${pkgver}
  python setup.py build
}

package_python2-seaborn() {
  depends=('python2-pandas' 'python2-matplotlib' 'python2-scipy')
  optdepends=('python2-statsmodels: for some advanced statistical plots'
              'python2-beautifulsoup4: for getting dataset names')

  cd "$srcdir"/seaborn-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-seaborn() {
  depends=('python-pandas' 'python-matplotlib' 'python-scipy')
  optdepends=('python-statsmodels: for some advanced statistical plots'
              'python-beautifulsoup4: for getting dataset names')

  cd "$srcdir"/seaborn-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
