# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.1
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://flufli18n.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('048b9a53598e42bc7b2733375aaa7c149017a844cf5bd5eb77d058593a2c4efdea66e7341ef300210c9c2e0c0ad265a513e3a40ecfb138e3f81475d20a03fc88')
b2sums=('93e169ca72217954bf67aa7bc912538126616b4f8f1d1892918a329124df33a41438f7bf79b919c6de39d46a25605f3c13b9ca0bd79dc4129d113cb478541c62')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  # upstream accidentally added tests top-level:
  # https://gitlab.com/warsaw/flufl.i18n/-/issues/5
  rm -rvf "${pkgdir}/${site_packages}/test"*
}
