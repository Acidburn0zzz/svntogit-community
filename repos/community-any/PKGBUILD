# $Id: PKGBUILD 174910 2016-05-12 08:52:07Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wallpapers
pkgver=1.7.5
pkgrel=1
epoch=1
pkgdesc='Deepin Wallpapers'
arch=('any')
license=('GPL')
url="https://github.com/linuxdeepin/deepin-wallpapers"
makedepends=('deepin-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wallpapers/archive/$pkgver.tar.gz")
sha512sums=('6f21a2311fde6f0967dd4f41a7ef1c5f1c03c291827a9918ad3d40df16f718083a8959cecfa27639245e06c4f7ad430eb2262051efa4166312c837f9503ac0f5')

prepare() {
  cd $pkgname-$pkgver
  rm deepin-private/*
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/share/wallpapers
  cp -r deepin "$pkgdir"/usr/share/wallpapers/

  install -dm755 "$pkgdir"/var/cache
  cp -r image-blur "$pkgdir"/var/cache/

  # Suggested by upstream
  install -dm755 "$pkgdir"/usr/share/backgrounds/deepin
  ln -s ../../wallpapers/deepin/Hummingbird_by_Shu_Le.jpg "$pkgdir"/usr/share/backgrounds/deepin/desktop.jpg
  ln -s $(echo -n /usr/share/wallpapers/deepin/Hummingbird_by_Shu_Le.jpg | md5sum | cut -d " " -f 1).jpg \
        "$pkgdir"/var/cache/image-blur/$(echo -n /usr/share/backgrounds/deepin/desktop.jpg | md5sum | cut -d " " -f 1).jpg
}
