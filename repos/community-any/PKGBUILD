# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=python-zeroconf
pkgver=0.22.0
pkgrel=1
pkgdesc="A pure python implementation of multicast DNS service discovery"
arch=('any')
url="https://github.com/jstasiak/python-zeroconf"
license=('LGPL')
depends=('python' 'python-netifaces' 'python-six' 'python-ifaddr')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/jstasiak/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('6d451ab2bbb11aa37a54c5b7f83815bb726dd1de54b1f8e19878ad653072bc0d')

build() {
  cd "${pkgbase}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${pkgbase}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

check() {
  cd "${pkgbase}-${pkgver}"
  py.test .
}

# vim:set ts=2 sw=2 et:
