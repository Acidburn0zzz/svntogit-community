# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pm2
pkgver=2.4.3
pkgrel=1
pkgdesc='Production process manager for Node.JS applications with a built-in load balancer'
arch=('any')
url='http://pm2.keymetrics.io'
license=('AGPL3')
depends=('nodejs' 'semver')
makedepends=('npm' 'git')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        pm2.service)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('d00c676be9b784ead14ccb18da875b181eeb97ae585bc073fc61fb682c455f9bce31db0aaf96d67c1b9b084d6d91fd8a441b5beed4a1eed95c391ba9c692bab7'
            '0d80c16d4694511cc19970a3eb4a2e10f706a58c5cdc554809e12a12093a5b29ff6d30027cbe8646f837a42e40f7d9b7fe2853f96a76a38f47ddc14833f141ef')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  install -Dm644 pm2.service "$pkgdir"/usr/lib/systemd/system/pm2.service
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done
}
