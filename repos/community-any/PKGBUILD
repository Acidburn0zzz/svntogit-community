# Maintainer: David Runge <dvzrv@archlinux.org>

_name=enrich
pkgname=python-enrich
pkgver=1.2.3
pkgrel=1
pkgdesc="Extends rich library functionality with a set of changes that were not accepted"
arch=('any')
url="https://github.com/pycontribs/enrich"
license=('MIT')
depends=('python-rich')
makedepends=('python-setuptools_scm')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-mock' 'python-pytest-xdist')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('c27fd9ed534b41da8951637c8deb5eac2c76f9e65edff04fc8ef81d7ef0f31b640bd3d3d24c7945053b5df3679c9ccd4c2297e97350dc5bb1ff3a427f52bed31')
b2sums=('c9b8b4f9093f3b686260219282209d2bbc55fcb4338a848f94b0bfdf1aab91c22c46ae59a73be35fba776f9d846f48fe9ef1d98f9e5379aef4a5526960e05539')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
