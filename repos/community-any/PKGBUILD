# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.0.1
pkgrel=1
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo' 'python-xcffib')
makedepends=('python-cffi' 'python-setuptools' 'cairo'
             'python-xcffib' 'gdk-pixbuf2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-isort')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('296fc03b40d30f03fe2e16202a02e0b22dacd3beb22bc2f111f60c277fb99c54159b2ee18f7ed908cd6fdf2e73a6282f6371a2a2ab593a3283ebc54bb4041f9c')


build() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py build
}

check() {
  export LC_CTYPE=en_US.utf8

  cd "$srcdir/cairocffi-$pkgver"
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
