# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-octaviaclient
pkgver=2.1.0
pkgrel=1
pkgdesc="Octavia client for OpenStack Load Balancing"
arch=('any')
url="https://docs.openstack.org/python-octaviaclient/latest/"
license=('Apache')
depends=('python-cliff' 'python-keystoneauth1' 'python-neutronclient' 'python-openstackclient'
         'python-osc-lib' 'python-oslo-serialization' 'python-oslo-utils' 'python-pbr'
         'python-requests')
checkdepends=('python-hacking' 'python-oslotest' 'python-requests-mock')
source=("https://github.com/openstack/python-octaviaclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c65266630a9c5fe4d7b81db9f3c2b3d933c4abb403861025e7c36cff21b42346cae6e50fec3ebf70cd1297eab272148296acbc25ac9fb1927cae407a0bc671d4')

export PBR_VERSION=$pkgver

build() {
  cd python-octaviaclient-$pkgver
  python setup.py build
}

check() {
  cd python-octaviaclient-$pkgver
  stestr run
}

package() {
  cd python-octaviaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
