# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-stretchr-testify
pkgver=1.4.0
pkgrel=1
pkgdesc='A toolkit with common assertions and mocks that plays nicely with the standard library'
arch=('any')
url="https://github.com/stretchr/testify"
license=('MIT')
depends=('go-pie' 'golang-github-davecgh-go-spew' 'golang-github-pmezard-go-difflib'
         'golang-github-stretchr-objx')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stretchr/testify/archive/v$pkgver.tar.gz")
sha512sums=('7a91c120f5425a1b89239a03ef9419d44c83342665beb3362384fe6512af52c49a5f914e5bbf9725656311ff0b826146f11b7db9f4cf4c1a69ce6033169d8c2d')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$srcdir"/build/src/github.com/stretchr/testify
  cd "$srcdir"/build/src/github.com/stretchr/testify
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/stretchr
  cp -a "$srcdir"/testify-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/stretchr/testify

  install -Dm644 testify-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
