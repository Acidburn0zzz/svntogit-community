# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.29.0
pkgrel=1
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('3a974a495d67312ccc3f5f43deb38bc2dab63251015b127ca599c497fbc61015')
b2sums=('94d8176cd18159883e2ce444a93319a727b3f04efdebf248afecd5aa3735b4a4ed3243283b428e3ba4f7aecedac552040bf1d975a1b9d381eee3e91833db6130')

prepare() {
  cd $pkgname-$pkgver
  # Avoid installing files directly under site-packages/
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  poetry build --format wheel
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
