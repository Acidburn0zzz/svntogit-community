# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyter-retrolab
_pipname=retrolab
pkgver=0.3.20
pkgrel=1
pkgdesc='JupyterLab Distribution with a retro look and feel'
arch=(any)
url='https://github.com/jupyterlab/retrolab'
license=(BSD)
makedepends=(python-build python-installer)
depends=(jupyterlab)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('9fad30fbcc9fb8384a5a239317362ad98df1eb9316dbbea83ba47d4038e58a06')

build() {
  cd $_pipname-$pkgver
  python -m build --wheel --no-isolation
}

package() {
  cd $_pipname-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  mv "$pkgdir"/{usr/,}etc
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
