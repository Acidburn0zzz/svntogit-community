# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-i18n
pkgname=(python-oslo-i18n python2-oslo-i18n)
pkgver=3.16.0
pkgrel=1
pkgdesc="Oslo i18n library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.i18n/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel')
checkdepends=('python-mock' 'python2-mock' 'python-oslotest' 'python2-oslotest' 'python-oslo-config'
              'python2-oslo-config')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.i18n/archive/$pkgver.tar.gz")
sha512sums=('a17461e2fbb3e7ef89c4efb928e9c0f9911a0b2fd905c1e92f73b314cb79d9c3d38d729d76fc0c3725e467ae106c61a263af78de180d58b7d650ae5319df792e')

prepare() {
  cp -a oslo.i18n-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py testr

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-oslo-i18n() {
  depends=('python-six' 'python-pbr' 'python-babel')

  cd "$srcdir"/oslo.i18n-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslo-i18n() {
  depends=('python2-six' 'python2-pbr' 'python2-babel')

  cd "$srcdir"/oslo.i18n-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
