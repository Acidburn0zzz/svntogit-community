# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.47.2
pkgrel=1
_image_pkgver=5.19.0
_podman_pkgver=3.4.4
_skopeo_pkgver=1.5.2
_storage_pkgver=1.38.2
pkgdesc="Configuration files and manpages for containers"
arch=(any)
url="https://github.com/containers"
license=(Apache)
makedepends=(go-md2man)
backup=(
  etc/containers/containers.conf
  etc/containers/mounts.conf
  etc/containers/policy.json
  etc/containers/registries.conf
  etc/containers/seccomp.json
  etc/containers/storage.conf
)
# configuration override directories need to exist
options=(emptydirs)
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('e5036e1570842bada89f930836aaef34573fa6530ece0dc78adfbbc02eadbf7a263e56d63aab7413e998601116f5da8897ca6a497d758bff259f47acb534a669'
            '800887506cc945f81558ddc026cd86abf818abc59fb3024d5ce296426d62e7c49445a10ac5f1f159a94024a2eebefc66bac5a12c58d8b53737eb4c5f15a41618'
            'cfd295bf50ce86ea70741c3e663b409ed47b1e560c962bc579f319151a0fe2b24cdd3045667660083cce89449a9c5de1508c94a9a02375165a72ce7c16166666'
            '7f1dfa41e4fa6a145187413252672b12e59a11ce445d9d5c47c78c4dbb8aa4ffe6925bea20b11034e40725fdb2ac5eacfbb646e7b8cfdd11f07bbb6fc020dda7'
            '98caa95e4456e60ef676e4d9717e5f852cfa21ffe45980f63fab703bc7c369fdae14d33102dcb023908d250c3eba6f121c270203c3abd29fdfcb7e2768319c50'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('770cc9829867d60e34ea7427426c5c382ceec615cbd708abafcb41cb337a5b48c849a06f26da62865ad6e5d603b345963a87da96ea48f2e10ce5866284b22b2e'
        'b509bdafd380d3e5909655c0c46b4ae3dc1c78933b320c47a2812b3cf8afa8d8ca05654a5b0753024b6d56a8adba004cdf55309bb1dd72efedb209c924b658c5'
        '3de69c9bc3bd1334837d21cb2817a9e9757fbb561a0b047658c7401608ecf1fbe09d5cd0c65497a82150a67a3775c77705d0dbd314a54f7b3a1953733cfb2906'
        '009f820d0784ae098d434418e126d4cc7318bbef72839b0438073eee02c4644195a6c5108334b3c1efd3e5c655035460994b151e5f4f3f30ae3046a31eab27b2'
        '466641eb0c810da0e6b5fe9adc47eadbc66cedb57326903fe4cbf917b12d4e937131dcf4ff4d33d0b2372384f754f471fd532aac91809a5481d74be0ce6ffe05'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i "storage-${_storage_pkgver}/docs/Makefile"
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
