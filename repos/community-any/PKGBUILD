# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: unikum <unikum.pm@gmail.com>
# Contributor: speed145a <jonathan@tagchapter.com>

pkgname=firewalld
pkgver=0.4.0
pkgrel=1
pkgdesc='Firewall daemon with D-Bus interface'
arch=('any')
url='https://github.com/t-woerner/firewalld'
license=('GPL2')
depends=('dbus-glib' 'ebtables' 'ipset' 'iptables' 'python-slip')
makedepends=('docbook-xsl' 'intltool')
optdepends=('bash-completion: bash completion'
            'gtk3: firewall-config'
            'libnm-glib: firewall-config and firewall-applet'
            'libnotify: firewall-applet'
            'python-pyqt4: firewall-applet')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install='firewalld.install'
source=("firewalld-${pkgver}.tar.gz::https://github.com/t-woerner/firewalld/archive/v${pkgver}.tar.gz"
        'firewalld-arch.patch')
sha256sums=('97f13b9fe28d7be7726ecc7d09a2ec85d47efade9859d0d0e37f426bc142f64b'
            '0e10b2dd4eb2b3ca436c9539c9cdd5e612c0f150f51f5c7d50743700437a2bee')

prepare() {
  cd firewalld-${pkgver}

  patch -Np1 -i ../firewalld-arch.patch

  ./autogen.sh
}

build() {
  cd firewalld-${pkgver}

  ./configure \
    --prefix='/usr' \
    --localstatedir='/var' \
    --sbindir='/usr/bin' \
    --sysconfdir='/etc' \
    --with-iptables='/usr/bin/iptables' \
    --with-iptables-restore='/usr/bin/iptables-restore' \
    --with-ip6tables='/usr/bin/ip6tables' \
    --with-ip6tables-restore='/usr/bin/ip6tables-restore' \
    --with-ebtables='/usr/bin/ebtables' \
    --with-ebtables-restore='/usr/bin/ebtables-restore' \
    --with-ipset='/usr/bin/ipset' \
    --disable-schemas-compile
  make
}

package() {
  cd firewalld-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
