# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-quotelike
pkgver=0.006
pkgrel=1
pkgdesc="Parse Perl string literals and string-literal-like things"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-QuoteLike"
depends=('perl-ppi')
makedepends=('perl-ppix-regexp')
optdepends=('perl-ppix-regexp: to find variables inside nested regular expressions')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-QuoteLike-$pkgver.tar.gz")
sha512sums=('6ec80ac4df9a80bfd9310b7a5a1fe514dcfaf340de9d88fcece5ecdda9e5a2183408bc4631f546a76f538c7dbb6c4d3827788671fc57cf73d956e18589d09f9f')

build() {
  cd PPIx-QuoteLike-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-QuoteLike-$pkgver
  make test
}

package() {
  cd PPIx-QuoteLike-$pkgver
  make DESTDIR="$pkgdir" install
}
