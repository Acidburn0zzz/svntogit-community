# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=httpcore
pkgname=python-httpcore
pkgver=0.13.3
pkgrel=1
pkgdesc="A minimal HTTP client"
arch=('any')
url="https://github.com/encode/${_pkgname}"
license=('BSD')
depends=('python-h11' 'python-sniffio')
optdepends=('python-h2: for HTTP/2 support')
makedepends=('python-setuptools' 'python-h2')
checkdepends=('hypercorn' 'python-anyio' 'python-curio' 'python-pproxy' 'python-pytest-asyncio' 'python-pytest-trio' 'python-trustme' 'uvicorn')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('7ba6c2cb77c082bcdb8cdfad2bc17646ccc9348f49760150ec1070429f94913b2fb9eae6d3407b404581b48670b866bf11696453325dd70610bd700e94990450')
b2sums=('afc43122a5502770b6efe7052846926e25fa678dcbd5c8ddbcd806df36dcf684d94df12571efaab0ff1b657fef14d5d71dd902be5cbec12c61db13004d7fe5a9')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run coverage in unittests!
    sed -i '/^addopts/d' setup.cfg
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # raise open files limits, many tests will fail otherwise
    ulimit -S -n 4096

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
