# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=netbeans
pkgver=7.1
pkgrel=2
pkgdesc='IDE for Java, PHP, Groovy, C, C++ and Python'
arch=('any')
url='http://netbeans.org/'
license=('CDDL')
depends=('java-environment' 'ttf-bitstream-vera')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'python: for developing programs in python'
            'lib32-glibc')
install=netbeans.install
options=(!strip)
source=("http://download.netbeans.org/$pkgname/$pkgver/final/zip/$pkgname-$pkgver-201112071828-ml.zip"
        'netbeans.desktop')
sha256sums=('04a707a5762c48a32a7d7aac48ddac7ca5f20fd4aab2770744bb7461141a936c'
            '9f3bb0a8976b9e3f460889f0be74ce7d7c01cf0f4cba5d15381e8007834dc777')

package() {
  cd "$srcdir/$pkgname"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/applications"
  cp -r "../$pkgname" "$pkgdir/usr/share"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "nb/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  ln -s "/usr/share/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
