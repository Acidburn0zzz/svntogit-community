# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.4.2
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-xdist' 'python-pytest-asyncio' 'python-flaky')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz")
sha512sums=('f189cac6a2553bcee6da1628c7af27c9e19ca2ec97c8ac6311fe05caefdab5644d5ab847f84e7d0f1acedf871cd49191b9c5b4feaed5e0d2ec249fcfa444731e')

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
