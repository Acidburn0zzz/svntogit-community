# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.28.3
pkgrel=1
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-click' 'python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('70fec5f875794d7dd7cc2ca96170a5735eb09846e270732dcef2c23622d0dbbc')
b2sums=('83282464e6ac2bdd9aec2ec22a54d25d2f56f09f904819c84082c401b5ee493a72cf5a182090c85e845b622b4198fe10de86e5e8273c5f065a30418b4b847c2a')

prepare() {
  cd $pkgname-$pkgver
  # Avoid installing files directly under site-packages/
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  poetry build --format wheel
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
