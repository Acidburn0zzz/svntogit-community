# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pigar
pkgver=0.9.2
pkgrel=1
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
depends=('python-setuptools' 'python-colorama' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Damnever/pigar/archive/v$pkgver.tar.gz")
sha512sums=('7a937fce100ba4f338cf992f7abb1a71092ab1b1dcd3532ca99f23f30f6cf6da70b3a93cad3d8c09c6c352ee3b20456150857171c5ea35091fd148776bb51cce')

build() {
  cd pigar-$pkgver
  python setup.py build
}

check() {
  cd pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v
}

package() {
  cd pigar-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
