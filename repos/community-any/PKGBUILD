# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=4.1.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a42e4d66649b285489ea3bc0773761da19c05a03732492eefaaa008a0ddcf0696ceed380e296da9ff392529d681350ff045aeeec9cecbbafcd3bd15141920592')
b2sums=('50b6f9693df9ee21ae53f27f42a7aeb42e6f50ed305fa1004462bf189366b80d7dc48a40f77eafc41107de91f08015275a69e524421777a8d17e155cb867e943')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
