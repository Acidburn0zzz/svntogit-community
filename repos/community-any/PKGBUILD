# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-h11
pkgver=0.8.1
pkgrel=4
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/njsmith/h11"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('3310dcd8552de4440ed9669fd37b3fc4d76073a9801decb705bceee3c1e98ff83470bd124ecf36e1ec3457ff2fb4da6f4fbf077daa810c28ab73044dfab709d4'
            '7747ba51fa9350d4fa6a5c1654f237e4109a5f0b7175be777ca0e1da83af4f631a240a605c594076fefcec5c26693f55886c0cb47aa75236d65a42dd520cb6e1')

prepare() {
  patch -Np1 -d h11-$pkgver <pytest5.patch
}

build() {
  cd "$srcdir"/h11-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/h11-$pkgver
  python setup.py pytest
}

package_python-h11() {
  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
