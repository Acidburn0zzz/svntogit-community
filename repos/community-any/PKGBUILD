# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgname=perl-net-openssh
pkgver=0.78
pkgrel=2
pkgdesc='Perl SSH client package implemented on top of OpenSSH'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
url=https://metacpan.org/release/Net-OpenSSH
source=("http://search.cpan.org/CPAN/authors/id/S/SA/SALVA/Net-OpenSSH-$pkgver.tar.gz")
md5sums=(7289656f3abf30a7d5e39a748ca40fad)
sha512sums=(5130b9fa0963955397c2fcbf2c317f42bb3a8c181e2d2a73d58b901ff9a9bfb477322fd6b80134f1e67c4c6f661978ffb214317686ab6d8e38a13558d9156307)
_ddir="Net-OpenSSH-$pkgver"

build()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
