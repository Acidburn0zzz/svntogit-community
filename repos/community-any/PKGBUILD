# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.12
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=(any)
url="https://github.com/phillipberndt/autorandr"
license=(GPL3)
depends=(python xorg-xdpyinfo xorg-xrandr)
makedepends=(bash-completion systemd)
optdepends=(
  'bash-completion: auto-completion for autorandr in Bash'
  'zsh-completions: auto-completion for autorandr in ZSH'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('cd3a16932b6183055e991462cdef78e0b345b64fb2b1ea4a28d5e7119ce5d8b4206bd4ee17288e104fe8fb5608a9eddb2efa5f7bfd3854b308ccbe476f50e9ee')
b2sums=('513b197783a527d6ebe7ab1ef2d15bda64b9a3934e9e47ece41a84bb224509b68b9483d2be2e61ea5f07e9de7c1d1a1f8587ff1dded28843022e537a315cb34a')

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  # zsh completion
  install -vDm 644 "contrib/zsh_completion/_${pkgname}" -t "${pkgdir}/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
