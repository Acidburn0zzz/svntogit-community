# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=fakeredis
pkgname=python-fakeredis
pkgver=1.1.0
pkgrel=1
pkgdesc="Fake implementation of redis API (redis-py) for testing purposes"
arch=('any')
url="https://github.com/jamesls/${_pkgname}"
license=('BSD' 'MIT')
depends=('python-redis' 'python-six' 'python-sortedcontainers')
makedepends=('python-setuptools' 'python-lupa')
checkdepends=('python-pytest' 'python-hypothesis')
optdepends=('python-lupa: for lua scripting support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('81b4d8c92b50dcf651746db115c58b4c6eedb32a39a9c960ea6adabe375fe5c6de0f92f5c905644373cc76046abb6b5216d531af933b8d2611918eaf4d77dc41')
b2sums=('dffabfa40bd27ca531ab1e7ef68e424dad466cc7ed80217a3e261db68b614c935b2f7f30a076b55063417ae8c3932bdff5cd65639ce6eeeeecd1aafd9628577c')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
