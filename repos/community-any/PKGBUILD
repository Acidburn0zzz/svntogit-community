# Maintainer: David Runge <dave@sleepmap.de>
_name=spreed
pkgname=nextcloud-app-spreed
pkgver=5.99.10
pkgrel=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud<16.0.0')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('9aae5acdbbbfe49c37e447382d51f59a498260efa0b177b6da470aa16df018ee1c7c719f7032ad97e4b5d935681e1f5aa29b16916a588f63b32a853a45f7ea80')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -dm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/${_name}/"
  cp -av ${_name}/* "${pkgdir}/usr/share/webapps/nextcloud/apps/${_name}/"
}
