# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.22
pkgrel=1
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/newville/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-numpy')
source=("https://github.com/newville/asteval/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('15de2a25c7270cce9508b709466086ba7828ea11a6e8d1c6c5ac6aa604bfbee487f404e1cb868cb6f018f2e8ffba62d9532fc2fb48f2a0a8ceaf478f3ce4ec84')

build() {
  cd asteval-$pkgver
  python setup.py build
}

check() {
  cd asteval-$pkgver
  python -m pytest
}

package() {
  cd asteval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
