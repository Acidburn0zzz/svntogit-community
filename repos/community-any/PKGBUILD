# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-proxy.py
_name=${pkgname#python-}
pkgver=2.4.0
pkgrel=1
pkgdesc='Lightweight HTTP, HTTPS, HTTP2 and WebSockets proxy server'
arch=('any')
url='https://github.com/abhinavsingh/proxy.py'
license=('BSD')
depends=('python')
makedepends=('python-dephell' 'python-setuptools')
checkdepends=('python-httpx' 'python-pytest' 'python-pytest-asyncio' 'python-pytest-cov' 'python-pytest-mock')
optdepends=('python-httpx: Cloudflare DNS resolver plugin support'
            'python-paramiko: proxy over SSH tunnel support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('cc5d1b5655ef109297f9679c9c6a9c12c13889540ee6c70619308adcb632331b')

prepare() {
	cd "$_name-$pkgver"
	dephell deps convert --from pyproject.toml --to setup.py
}

build() {
	cd "$_name-$pkgver"
	python setup.py build
}

check() {
	cd "$_name-$pkgver"
	pytest
}

package() {
	cd "$_name-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

	# Remove entry point usr/bin/proxy to avoid conflict with libproxy (FS#73325)
	rm -r "$pkgdir/usr/bin"
}
