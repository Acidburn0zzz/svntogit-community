# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgname=python-pint
pkgver=0.9
pkgrel=3
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="https://pint.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-numpy' 'python-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz"
        python-3.8.patch)
sha512sums=('d79e0ff6377393f9dd8ce6a938440e8ed3c947642716974a3d69461d75776a39677d6651e6c081076f31e67a7dbffeb2fded42e61c9975f814b5eb24ab626fb6'
            '8a2a6d8c3c82471b18f262d65ac8194c3deeee49cd1751892210ea2d6a0e8d4502c97aae70135ae6ca9a92524598475bab0ef50cfa3b9594987e7c75a492722e')

prepare() {
  cd Pint-$pkgver
  # https://github.com/hgrecco/pint/pull/824
  patch -Np1 -i ../python-3.8.patch
}

check() {
  cd Pint-$pkgver
  python setup.py test
}

package() {
  cd Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
