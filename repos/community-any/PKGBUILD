# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=4.3.0
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/willthames/ansible-lint"
license=('MIT')
depends=('python' 'ansible' 'python-ruamel-yaml' 'python-pyaml' 'git')
source=(${pkgname}-${pkgver}::https://github.com/willthames/ansible-lint/archive/v${pkgver}.tar.gz)
sha256sums=('f22989334d5aa752a82dbd494f5c584e897fd2861bdfab182d1f3ee166e92224')

build() {
  cd "${pkgname}-${pkgver}"
  python -c "from setuptools import setup; setup();" build
}

package() {
  cd "${pkgname}-${pkgver}"
  python -c "from setuptools import setup; setup();" install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
