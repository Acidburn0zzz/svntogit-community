# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.0
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://flufli18n.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('97191353718c2adeff88fc23e8c1de9893f64e836cc5d69141d29a1d163d0cc07b1bfc811e761bc6555d7716c4028843e143663f951c7a81e1e4dd4d2be631a2')
b2sums=('5bb3e315b6114388619cac51ddcd1f114b707474cc1dfa15ec6d53163a831dfab391ef415432edacb7d3e5d98b61c259c5499107909ba1d425e4c68ca4b96e89')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
