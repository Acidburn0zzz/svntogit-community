# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-enabler
pkgver=1.1.0
pkgrel=1
pkgdesc='Enable installed pytest plugins'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-enabler'
depends=('python-toml' 'python-jaraco' 'python-jaraco.context' 'python-more-itertools')
makedepends=('python-setuptools-scm')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-checkdocs'
              'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-enabler/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7b98d553ee5e5f459f5eb94a2b35e9b03e5e7642117ab65a98bbde878e25d15baf57a72ca350c96e6d3e22d93c63f0e57ed1cb523173f75e646da4f077b3c643')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-enabler-$pkgver
  python setup.py build
}

check() {
  cd pytest-enabler-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-enabler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
