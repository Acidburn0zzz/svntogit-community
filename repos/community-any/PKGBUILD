# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20190129
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="http://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("http://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('e433716ef7269a8bb921b1f3e9bb7b75ed521c482172d1bf43e19a4f11b5a211949f1a2b952aee0a1b8b54c456fe629d97d6766bd287b71fd73c22d2c0ed746e'
            'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd service
  sed -e "s|etc/init.d|usr/bin|" -i *".service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 *".service" -t \
    "${pkgdir}/usr/lib/systemd/system/"
}

