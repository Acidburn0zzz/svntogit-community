# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.1.3
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'python-cerberus' 'python-click' 'python-click-completion'
'python-click-help-colors' 'python-colorama' 'python-cookiecutter'
'python-jinja' 'python-packaging' 'python-paramiko' 'python-pexpect'
'python-pluggy' 'python-selinux' 'python-sh' 'python-tabulate'
'python-tree-format' 'python-yaml' 'yamllint')
makedepends=('python-setuptools-scm' 'python-simplejson')
# TODO: python-pytest-plus
checkdepends=('python-ansi2html' 'python-mock' 'python-pytest'
'python-pytest-cov' 'python-pytest-helpers-namespace' 'python-pytest-html'
'python-pytest-mock' 'python-pytest-verbose-parametrize'
'python-pytest-verbose-parametrize' 'python-pytest-xdist' 'python-testinfra')
optdepends=('molecule-docker: for the docker driver'
            'molecule-podman: for the podman driver'
            'molecule-vagrant: for the vagrant driver'
            'python-pywinrm: for Windows support'
            'python-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b0a19fc43b01fae51a16f5a6db1f2011377286904fd5a01d46d3a4889bf4ab71cf77c9dab521799c3149b07b8b6f43939bac932104c3faf19ccde76679e4a85b')
b2sums=('ed2b83f889380aa170ed4e7cfe41e0fc541df22305dd39a72b7ff2399154e7c68a899e6176db1f344c0fb183c614992363a892279e626e85fec819f7617929b7')

prepare() {
  cd "$pkgname-$pkgver"
  # python-pre-commit is absolutely not a runtime dependency
  # remove additional pytest options
  # unpin python-sh
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -e 's/, < 1.14//g' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  # installing into test location, so pytest can pick up the executable
  mkdir -vp test_dir
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="test_dir"
  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  export PYTHONPATH="${PWD}/test_dir/usr/lib/python${python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"
  # do not run tests that require interaction with the docker daemon
  pytest -v \
         --ignore='molecule/test/functional/docker/' \
         --ignore='molecule/test/functional/test_command.py' \
         -k "not test_execute \
             and not test_execute_bakes \
             and not test_execute_bakes_with_ansible_args \
             and not test_executes_cathces_and_exits_return_code_with_stdout"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
