# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nlohmann-json
pkgver=3.8.0
_test_pkgver=2.0.0
# upstream doesn't use annotated tags:
_commit=e7452d87783fbf6e9d320d515675e26dfd1271c5
pkgrel=2
pkgdesc="JSON for Modern C++"
arch=('any')
url="https://github.com/nlohmann/json"
license=('MIT')
makedepends=('cmake' 'git' 'meson')
source=("${pkgname}::git+https://github.com/nlohmann/json#commit=${_commit}?signed"
        "${pkgname}-3.8.0-git_tests.patch::https://github.com/nlohmann/json/commit/e86b3fae98854312fc6e59c62e35919a8d8012b1.patch"
        "json_test_data-${_test_pkgver}.tar.gz::https://github.com/nlohmann/json_test_data/archive/v${_test_pkgver}.tar.gz")
sha512sums=('SKIP'
            'b8002761041d7ee47404fbac082efe161362ba26daca061a0ff37274302866c724cd54827459a3836b93c02b46b90960a5a5686e2eeaf0fc460bd2bc7b2c731e'
            'eb08a31b09929a027cdacded2e11b9caf47fa0079619c0fa67bcb766bff1be1642a11c8ccd2f4c48521387fe1d42625d4f53f6a063a290d4664b17813f784051')
validpgpkeys=('797167AE41C0A6D9232E48457F3CEA63AE251B69') # Niels Lohmann <mail@nlohmann.me>

prepare() {
  mkdir -vp "$pkgname/build"
  mv -v "json_test_data-${_test_pkgver}/" "$pkgname/build/json_test_data/"
  cd "$pkgname"
  patch -Np1 -i "../${pkgname}-3.8.0-git_tests.patch"
}

build() {
  cd "$pkgname"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -DBUILD_TESTING=ON \
        -DJSON_BuildTests=ON \
        -DJSON_MultipleHeaders=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
  # create pkgconfig integration
  meson --prefix=/usr \
        --libdir=lib \
        meson-build
  ninja -C meson-build
}

check() {
  cd "$pkgname/build"
  ctest --output-on-failure -LE git_required
}

package() {
  cd "$pkgname"
  # NOTE: installing first with meson, as it will install the single header
  # file, but cmake will install multiple headers.
  DESTDIR="${pkgdir}" meson install -C meson-build
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {CODE_OF_CONDUCT,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
