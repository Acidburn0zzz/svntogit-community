# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>

pkgname=python-graphene
pkgver=2.1.3
pkgrel=2
pkgdesc='GraphQL framework for Python'
arch=(any)
url=https://graphene-python.org/
license=(MIT)
depends=(
  python-aniso8601
  python-graphql-core
  python-graphql-relay
  python-six
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/graphql-python/graphene.git#tag=v${pkgver})
sha512sums=(SKIP)

build() {
  cd graphene

  python setup.py build
}

package() {
  cd graphene

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-graphene
}

# vim: ts=2 sw=2 et:
