# Maintainer: Moritz Lipp <mlq@pwmt.org>
# Co-Maintainer: fordprefect <fordprefect@dukun.de>
# Contributor: Thor77 <thor77@thor77.org>

pkgname=radicale
_name=Radicale
pkgver=2.1.9
pkgrel=2
pkgdesc='Simple calendar (CalDAV) and contact (CardDAV) server'
arch=('any')
url="http://www.radicale.org/"
license=('GPL3')
depends=('python-bcrypt' 'python-passlib' 'python-setuptools' 'python-vobject')
checkdepends=('python-bcrypt' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort' 'python-pytest-runner')
backup=('etc/radicale/config')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Kozea/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-sysusers.conf"
        "${pkgname}-tmpfiles.conf"
        "${pkgname}.service")
sha512sums=('30df36772f4dd38c9c79ecd9669da8b40c7284afa9e2949c174c159838c33a66edec1a49adf7c110017dc83c592415be6fa43e2de6007d7e154b8707773c4a44'
            '444489ba929680490884e338acc311fb61d81f9d9f0282c92c40a80317af9e61123e4764e803d1698876417170efd40bfe7f56408570891c4e426de3be17c302'
            '4570ed393b73bc5a3fd571947acae78b96b5e17f659e3169f542b9c481635680c724d5c6c0952661fae063caf04c786afe7043c5052945e7a00bb66326dec016'
            '6a3452009bebc0f8680ea8e64b9a398c04f3fb6b20752c377392e22641845a04dbfda78d586271c9a25bd7d5ba42b7a8f74774afed68732f19ddd764f53e11cc')

prepare() {
  mv -v ${_name}-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # removing version pinning, as we are rolling release
  # expect to hit bugs!
  sed -e 's/vobject\=\=0\.9\.5/vobject/' \
      -e 's/python-dateutil\=\=2\.6\.1/python-dateutil/' \
      -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# checks are disabled for now https://bugs.archlinux.org/task/58344
#check() {
#  cd "${pkgname}-${pkgver}"
#  python setup.py test
#}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
    # config
    install -vDm 644 config "${pkgdir}/etc/${pkgname}/config"
    # fcgi/wsgi
    install -t "${pkgdir}/usr/share/${pkgname}/" \
      -vDm 644 "${pkgname}.fcgi" \
      -vDm 644 "${pkgname}.wsgi"
    # systemd service
    install -vDm 644 "${srcdir}/${pkgname}.service" \
      "${pkgdir}/usr/lib/systemd/system/${pkgname}.service"
    # sysusers.d
    install -vDm 644 "${srcdir}/${pkgname}-sysusers.conf" \
      "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
    # tmpfiles.d
    install -vDm 644 "${srcdir}/${pkgname}-tmpfiles.conf" \
      "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
    # docs
    install -t "${pkgdir}/usr/share/doc/${pkgname}/" -vDm 644 {NEWS,README}.md
}

