# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-cpan-perl-releases
pkgver=0.42
pkgrel=1
pkgdesc="Mapping Perl releases on CPAN to the location of the tarballs"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0')
url=https://metacpan.org/release/CPAN-Perl-Releases
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPAN-Perl-Releases-${pkgver}.tar.gz")
md5sums=(b99bf77f07a809ab48bc2b50b0ae09bd)
sha512sums=(d044338789ccc0b754a946244a4d38e580af127d268f89a59c42bd3fb854bed1e615f8cb5e8ec33676d98dd8b84e41c19f5fdde2865b6a81d6114344b59f686a)
_distdir="${srcdir}/CPAN-Perl-Releases-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
