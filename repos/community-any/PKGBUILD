# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='sd_notify'
pkgname="ruby-${_gemname}"
pkgver=0.1.0
pkgrel=1
pkgdesc="Pure Ruby implementation of systemd's sd_notify(3)"
arch=('any')
url='https://github.com/agis/ruby-sdnotify'
license=('MIT')
depends=('ruby')
#checkdepends=('ruby-minitest')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz")
sha512sums=('d2de7cf98e7b1ecf9401bb629b3209285d17328d4f624d6033242f36bbc577280f1cdac56040fed5ea29f913fd2d1950f6fe0b6d788078bbdbf53fb81c2570e9')

prepare() {
  cd "${srcdir}/ruby-sdnotify-${pkgver}"

  # drop useless dependencies
  sed --in-place '/rubocop/d' "${_gemname}.gemspec"
}

build() {
  cd "${srcdir}/ruby-sdnotify-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${srcdir}/ruby-sdnotify-${pkgver}"
  # the latest release has no tests, but master already does
  # ruby -Ilib:test test/*
}

package() {
cd "${srcdir}/ruby-sdnotify-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
 }
