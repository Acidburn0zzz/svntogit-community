# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=python-pony
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.7.6
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
source=("$url/archive/$pkgver.tar.gz")
sha512sums=('ba12b96bb03642588ca95cc83199fad3e7a6fd3043ec74b5b0ccb7f23066592a8b17a0e5fc6ae4be371f6bcb8d8e7828deaf40e46b05010eeaf481f41c8f598e')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

#check() {
#  echo "Running tests for python-$_pkgname $pkgver"
#  cd "$srcdir"/$_pkgname-$pkgver
#  python setup.py test
#
#  echo "Running tests for python2-$_pkgname $pkgver"
#  cd "$srcdir"/$_pkgname-$pkgver-py2
#  python2 setup.py test
#}

package_python-pony() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pony() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

