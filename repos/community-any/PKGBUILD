# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=mat2
pkgver=0.10.0
pkgrel=2
pkgdesc='Metadata removal tool, supporting a wide range of commonly used file formats'
url='https://0xacab.org/jvoisin/mat2'
arch=('any')
license=('LGPL3')
depends=('python' 'python-setuptools' 'python-mutagen' 'python-cairo' 'python-gobject' 'gdk-pixbuf2' 'poppler-glib' 'librsvg' 'perl-image-exiftool' 'mailcap')
optdepends=(
  'ffmpeg: video support'
  'bubblewrap: sandboxing support'
)
checkdepends=('ffmpeg')
source=('https://0xacab.org/jvoisin/mat2/-/archive/0.10.0/mat2-0.10.0.tar.bz2')
sha512sums=('ae09d2fd2c53cdf7f4c8011c01b469ffdc01c586177c2b2a3e1bfb6d8be5f1a8832e1c0094eea66390941011fc45b57876fa26761cbc08f56d015c0dfac34b7b')
b2sums=('72806939fde41e9195b85d2e8c714efb4fe975212a972f5a22b783c29367c188b246cd57ffbe9b2a233c49f423c540c28a17bf412e4e7b7f704ce94d3fb9eaea')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  python -m unittest discover -v
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
}

# vim: ts=2 sw=2 et:
