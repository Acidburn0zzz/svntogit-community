# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-kr-pty
pkgver=1.1.5
pkgrel=1
pkgdesc='PTY interface for Go'
arch=('any')
url="https://github.com/kr/pty"
license=('MIT')
depends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kr/pty/archive/v$pkgver.tar.gz")
sha512sums=('f0b2b3e41a59bc5845218ca91e7752584b81401408e6a80512efbfcec2521d379b0de4d7f467d7a0fa4b63a1b943cd70688270334791232a0971457f4461ef42')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$srcdir"/build/src/github.com/kr/pty
  cd "$srcdir"/build/src/github.com/kr/pty
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/kr/pty

  install -Dm644 pty-$pkgver/License "$pkgdir"/usr/share/licenses/$pkgname/License
}
