# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.8.0
pkgrel=1
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=('any')
url="https://www.sciss.de/mellite/"
license=('AGPL3')
groups=('pro-audio')
depends=('java-environment>=16' 'supercollider')
makedepends=('gendesk' 'sbt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Sciss/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('f50272604eab14bebb4956779e81357f6c96a943d85b9b95e1475c0ea8c2362efda22d8fa593adedb500418f5dd7301750051990b4637865875384eb81fa685b'
            '1a6a572b4c365cdad87930bef805afa229b329cb2ceefb6b9ce42b233ab86f768ae3e3d5fd7cac1129786878c38fd891015aaf6637d9bd7b5ce3b2ef5fe9d30b')
b2sums=('4a1bfb13dadd2e62b5e8235b13e554cf0c71fb92afb01157bf83ec1d3e7edccd3440bceec08c1a4a242158fd6f7b3f762c2c3390672d182ee2f2c50c1fdf869e'
        '4f1fd83d7cf5561e76f746860e5436e7dfab9bd50ad103f499b82de99a67034122f0e8e99e6a58574d6611585d63b2976e5d31979c7233294464683c86cce3ea')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  gendesk -n \
          --pkgname "${_app_name}" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "${pkgname}" \
          --icon "${_app_name}" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 "app/${_name}.jar" -t "${pkgdir}/usr/share/scala/${pkgname}"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 icons/application.png "${pkgdir}/usr/share/icons/hicolor/256x256/de.sciss.mellite.Mellite.png"
  install -vDm 644 icons/application.svg "${pkgdir}/usr/share/icons/hicolor/scalable/de.sciss.mellite.Mellite.svg"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications"
}
