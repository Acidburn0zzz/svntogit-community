# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.bounce
pkgname=python-flufl.bounce
pkgver=3.0.2
pkgrel=1
pkgdesc="Email bounce detectors"
arch=('any')
url="https://fluflbounce.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
provides=('python-flufl-bounce')
replaces=('python-flufl-bounce')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('551815bcd482676e7f121e086d16cbdd1743e408fb0b4c731cf7e508e3b7a9102b0969db2e0f06eedb5d83b155af90cef2d1d6dbe25def9484fc7634ae888fb4')
b2sums=('bed8f62a78045040dfc19817bd226690edd0a248b9a4cdbf3f0e88a85f4f54fd2fba54a3ceb80b78d25dc098691bb9181197eae44820ba9aa6ab564b7f3e3fdf')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
