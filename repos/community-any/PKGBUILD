# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nvchecker
pkgver=0.4.4
pkgrel=1
pkgdesc="New version checker for software"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python-setuptools' 'python-tornado')
checkdepends=('python-pytest-runner' 'python-flaky')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz")
sha512sums=('db18e330a2c5d9599d8910623f994df0b3d594ace209ff38cac0113f1a8da79b24025dcf8590b007c6a7eb9d5460218bfb3e8231f0174ed752854c094200db57')

prepare() {
  sed -i 's/1.3\-7/1.3-8/' nvchecker-$pkgver/tests/test_{archpkg,pacman}.py
}

build() {
  cd nvchecker-$pkgver
  python setup.py build 
}

check() {
  cd nvchecker-$pkgver
  python setup.py pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
