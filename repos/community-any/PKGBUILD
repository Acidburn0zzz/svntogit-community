# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=2.1.1
pkgrel=1
pkgdesc="Python datetimes made easy"
arch=('any')
url="https://pendulum.eustace.io/"
license=('MIT')
depends=('python' 'python-dateutil' 'python-pytzdata')
makedepends=('python-dephell')
checkdepends=('python-babel' 'python-freezegun' 'python-pytest' 'python-pytz' )
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sdispater/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('031a979a0c3b1135b0fc838929c10fe7e59610b5337b588a0c22b22087dfc267e9b26d44d7f03c99961c974e412cd427cf83c0537341c7af2a462f51e539537d')
b2sums=('60c17cda9c770fbf478a66214efbbc24ea700db0898ce94a97d249ae50b6d170eb3e52703de19a94c40d3459ace8239cf5f29d082dde2ee9f4beb82cd348d28b')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
