# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-eventlet
pkgname=('python-eventlet' 'python2-eventlet')
_pyname=eventlet
pkgver=0.17.4
pkgrel=2
pkgdesc='Highly concurrent networking library'
url='http://eventlet.net'
arch=('any')
license=('MIT')
makedepends=('python-greenlet' 'python-setuptools' 'python-sphinx' 'python2-greenlet' 'python2-setuptools' 'python2-sphinx')
#TODO: add 'python-dnspython' if test is fixed
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq'
              'python2-psycopg2' 'python2-nose' 'python2-pyopenssl' 'python2-httplib2' 'python2-mock' 'python2-pyzmq' 'python2-dnspython')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/eventlet/eventlet/archive/v${pkgver}.tar.gz
        fix-mswindows-regression.patch
        fix-openssl.patch)
sha512sums=('a58e39b275c8100e79d837d37c2bfee30e63396c24c7b1058709d08272bd5a39aa4fb5c7278fc72816e371f45deba83664c366996db2261181d4caa9e8526da0'
            '6db8a6e852be0628c7e7b1290241f0918bb2aa204ea1ce969cc030126170bd1e6094c338ca0d7efbba1861c899997bc932c258b778a4610e956b266a83835d14'
            '029b92dc19b2877fc65ee2407f06d9de221e0f4c7a977123ca0977830a15b0336af37fbc3ea20dbb54e5ddb7b59df4d805faf943f510d32837a907ba5715408a')

prepare() {
  (cd ${_pyname}-${pkgver}
    patch -p1 < "${srcdir}/fix-mswindows-regression.patch"
    patch -p1 < "${srcdir}/fix-openssl.patch"
    sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
    sed 's|json|text|g' -i doc/Makefile
  )
  cp -a ${_pyname}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${_pyname}-${pkgver}-py2/examples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C doc text
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C doc text
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests -sv tests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2 -sv tests
  )
}

package_python-eventlet() {
  depends=('python' 'python-greenlet')
  optdepends=('python-psycopg2: non-blocking PostgreSQL support'
              'python-pyopenssl: non-blocking SSL support'
              'python-httplib2: non-blocking HTTP support'
              'python-pyzmq: non-blocking ZeroMQ support'
              'python-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-eventlet() {
  depends=('python2' 'python2-greenlet')
  optdepends=('python2-psycopg2: non-blocking PostgreSQL support'
              'python2-pyopenssl: non-blocking SSL support'
              'python2-httplib2: non-blocking HTTP support'
              'python2-pyzmq: non-blocking ZeroMQ support'
              'python2-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
