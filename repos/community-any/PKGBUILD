# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: robertfoster
# Contributor: Ista Zahn <istazahn[at]gmail[dot]com>

_name=pyppeteer
pkgname=python-pyppeteer
pkgver=0.2.4
pkgrel=1
pkgdesc='Headless chrome/chromium automation library'
arch=('any')
url=https://github.com/pyppeteer/pyppeteer
license=('MIT')
depends=('python-appdirs' 'python-pyee' 'python-tqdm' 'python-urllib3'
         'python-websockets')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('d1bcc61575ff788249d3bcaee696d856fa1153401a5428cb7376d826dd68dd9b')
b2sums=('3f7db7b55f08c743b3f8f661a3e415c50bd3d4ed05bd130af2a13338fb3d5251806cd7e777cb065318d0c52e5ef741a7ab507dd7e668114572b708ff545e835a')

prepare() {
  cd $_name-$pkgver
  sed -i '/^include/,/]/d' pyproject.toml
}

build() {
  cd $_name-$pkgver
  poetry build --format wheel
}

package() {
  cd $_name-$pkgver
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
