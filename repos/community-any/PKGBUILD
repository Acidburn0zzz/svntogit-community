# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=archlinux-wallpaper
pkgver=1.4
pkgrel=4
pkgdesc='A variety of Arch Linux branded wallpapers'
arch=(any)
url='https://archlinux.org/'
license=('CCPL:by-nc-sa')
install=archlinux-wallpaper.install
source=(archlinux.stw
        "https://sources.archlinux.org/other/artwork/$pkgname-$pkgver.tar.xz")
sha256sums=('e2d716fdb37794663a3ec05b4f7bc8ea1c32a8d4e0ea12c576e01eb80b6bd1b5'
            '9184b825e3d538f74334b25fd2f1f7bb786e29a64d975de5a565584af9a400a3')

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/share/"{archlinux/wallpaper,gnome-background-properties}
  install -m644 AUTHORS "$pkgdir/usr/share/archlinux/wallpaper/"
  install -m644 *.jpg "$pkgdir/usr/share/archlinux/wallpaper/"
  install -m644 *.png "$pkgdir/usr/share/archlinux/wallpaper/"
  install -m644 arch-backgrounds-16-9.xml "$pkgdir/usr/share/gnome-background-properties/"
  install -m644 arch-backgrounds-4-3.xml "$pkgdir/usr/share/gnome-background-properties/"
  install -Dm644 "$srcdir/archlinux.stw" "$pkgdir/usr/share/archlinux/archlinux.stw"
}

# vim: ts=2 sw=2 et:
