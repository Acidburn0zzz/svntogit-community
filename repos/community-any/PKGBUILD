# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=starlette
pkgname=python-$_pkgname
pkgver=0.13.6
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=('any')
url='https://github.com/encode/starlette'
license=('BSD')
depends=('python')
optdepends=('python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
            'python-yaml' 'python-requests' 'python-ujson')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner'
              'python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
              'python-yaml' 'python-requests' 'python-ujson'
              'python-unidecode')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('f83a82554e2975a77b4a3a897c86834ca2ec70c1f834fe6c212ee4e9854ee3749228e54c8713812143bf8c30a4dc75702e20713d9e9ed44a1837f5ef27e616fb')

prepare() {
  cd $_pkgname-$pkgver

  # needs python-multipart
  rm tests/test_formparsers.py

  # needs python-databases
  rm tests/test_database.py

  # needs a different graphql version
  rm tests/test_graphql.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_request_form_urlencoded needs python-multipart
  python setup.py pytest --addopts -k-test_request_form_urlencoded
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

