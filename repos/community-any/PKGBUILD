# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=arch-release-promotion
pkgver=0.1.0
pkgrel=1
pkgdesc="Promote official releases with signatures and additional artifacts"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/arch-release-promotion"
license=('GPL3')
depends=('python-dotenv' 'python-email-validator' 'python-gitlab'
'python-orjson' 'python-prometheus_client' 'python-pydantic' 'python-pyxdg'
'python-toml' 'python-torrentool')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7151e06de280abe87398c5c376683fabfde1777a12649b411bb13848bb6e526b')
b2sums=('ce4060605aa3beab009c155c0533aeb80de861a76fb8704c81d0f6922f51c86d95614b051ea9ea0a19d4fac2a6bd283140704413ee955550fe5a859ab637620e')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
