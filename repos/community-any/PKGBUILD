# Maintainer: David Runge <dave@sleepmap.de>

pkgname=autorandr
pkgver=1.8
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python-setuptools' 'xorg-xrandr')
optdepends=('xorg-xdpyinfo: For detecting the primary XRandR output')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('189e3b27d1a7c3018bdf88fb8bed7438e8fafc4d2760306905d694b2f787a75b435fd97edfbe5e53c9e847ea39358731db2c59ce09953e875381e49df1941753')

prepare() {
  cd "${pkgname}-${pkgver}"
  # mandb needs to be called on the system, not during packaging
  # https://github.com/phillipberndt/autorandr/issues/144
  sed -e '/mandb/d' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
