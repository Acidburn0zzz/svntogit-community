# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-bitcoinlib
pkgver=0.10.2
_tag=$pkgname-v$pkgver
pkgrel=1
pkgdesc='Python3 library providing an easy interface to the Bitcoin data structures and protocol'
arch=('any')
url='https://github.com/petertodd/python-bitcoinlib'
license=('GPL3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_tag.tar.gz")
sha512sums=('db9a7cff735d7cab1efb1c353c68a7bfb1a0eb3feb2352fc8bb1b177be76ccb39f5626d860685a594ada4a54ced8c527440f59258e56197a4c27c671237bed62')

build() {
  cd $pkgname-$_tag

  python setup.py build
}

check() {
  cd $pkgname-$_tag

  python setup.py pytest
}

package() {
  cd $pkgname-$_tag

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
