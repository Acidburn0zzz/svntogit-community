# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=python-pony
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.7.9
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
source=("$url/archive/v$pkgver.tar.gz")
sha512sums=('b2bc8e515ea179821b1a28b2ba9406d480ab0a227615bc8624c2907b31901d338cec030df2de21e622ba0e6be42dc63676989ae2344485f143ffb644c10a3778')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  echo "Running tests for python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py test

  echo "Running tests for python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py test
}

package_python-pony() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pony() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

