# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=python-josepy
pkgver=1.13.0
pkgrel=1
pkgdesc="JOSE protocol implementation in Python"
url="https://github.com/certbot/josepy"
license=('PerlArtistic')
arch=('any')
depends=('python-cryptography' 'python-pyopenssl' 'python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://pypi.io/packages/source/j/josepy/josepy-$pkgver.tar.gz")
sha512sums=('fb91946ac4bbb2a81195d200e4fdf06d40865b345005ab4ee328fd72545da2345b44c5b9ffb3115b16ffed65ab3a0c01d5b20d0d2ff3c2c2af30537a6a4721df')
b2sums=('e4f3caed3be0775a49d6a9c38bf6e2b61ecc50b4e3804afa9e597b741821ab63a27fe95d1592531fee7152e2076760d838f4efa50ab8b3e2f1e129c9d7457f8c')

prepare() {
  # thou shalt not enforce coverage in distro packaging
  # https://github.com/certbot/josepy/issues/34
  rm "josepy-$pkgver/pytest.ini"
}

build() {
  cd "josepy-$pkgver"
  python setup.py build
}

check() {
  cd "josepy-$pkgver"
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd "josepy-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}
