# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-repeat
pkgver=0.9.0
pkgrel=1
pkgdesc='pytest plugin for repeating test execution'
arch=('any')
license=('MPL')
url='https://github.com/pytest-dev/pytest-repeat'
depends=('python-pytest')
makedepends=('python-setuptools-scm')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-repeat/archive/v$pkgver.tar.gz")
sha512sums=('309af69c53749d24527c201da371510ed9f6c501964123784668007f63e1fde525f051852a85be393c6be2f368e2118765cca1ffbb2f3fb3ac14c86c052713a4')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-repeat-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-repeat-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.9/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-repeat-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
