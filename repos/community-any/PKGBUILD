# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.0.12
pkgrel=3
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://code.google.com/p/spyderlib/'
license=('MIT')
depends=('python2-qt')
makedepends=('python-sphinx')
optdepends=('pylint: powerful code analysis'
            'ipython-0.10: enhanced Python interpreter'
            'pyflakes: real-time code analysis'
            'python2-rope: editor code completion, calltips and go-to-definition'
            "python-sphinx: object inspector's rich text mode"
            'python2-numpy: N-dimensional arrays'
            'python2-scipy: signal/image processing'
            'python2-matplotlib: 2D/3D plotting')
source=("http://spyderlib.googlecode.com/files/${pkgname}-${pkgver}.zip"
        "spyder.desktop")
md5sums=('035852cfc6109541d72003111cf31bb3'
         '01275d559ef56c33ab68c26340c10cef')

build() {
  cd ${pkgname}-${pkgver}

  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1

  install -D -m644 spyderlib/__init__.py \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
