# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=0.10.0
pkgrel=1
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/aio-libs/aiobotocore/archive/$pkgver.tar.gz"
        aiobotocore-pytest-4_1.patch::https://github.com/aio-libs/aiobotocore/commit/4d3df13d6f375d10df4ed2b11662f3915a434d8e.patch)
sha256sums=('78721fcd1608057709b7c940f28a0f4afde7dfafa8d0b626e404046bbbd3a34e'
            '4eea86e03a0eca41493352519fe0755dca580d8c39e84648beaf082f6a8c5a57')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../aiobotocore-pytest-4_1.patch

  # used in a skipped test only
  sed -i '/from dill.source import getsource/d' tests/test_patches.py
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # https://github.com/spulec/moto/issues/1941
  export AWS_ACCESS_KEY_ID=foobar
  export AWS_SECRET_ACCESS_KEY=foobar

  # test_patches works only for pinned versions of dependencies
  PYTHONPATH=build/lib pytest -v -m moto -k 'not test_patches' tests
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
