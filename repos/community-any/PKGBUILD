# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname='python-envisage'
pkgver=4.8.0
pkgrel=1
pkgdesc="Extensible Application Framework"
arch=('any')
url="https://github.com/enthought/envisage"
license=('BSD')
depends=('python-apptools')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-configobj')
optdepends=('ipython: for IPython shell plugin')
source=("envisage-$pkgver.tar.gz::https://github.com/enthought/envisage/archive/${pkgver}.tar.gz")
sha256sums=('43b49eff50c352392c9228aa4eae6dc466ddf51cbf26f8f7765bf7bb00726add')

build() {
  cd "$srcdir"/envisage-$pkgver

  python setup.py build
}

check() {
  cd "$srcdir"/envisage-$pkgver

  # eggs tests are harmless (see https://github.com/enthought/envisage/issues/135)
  nosetests envisage || warning "tests failed"
}

package_python-envisage() {
  cd "$srcdir"/envisage-$pkgver

  python setup.py install --root="$pkgdir"/  --optimize=1

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
