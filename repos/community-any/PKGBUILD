# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.2.7
pkgrel=1
pkgdesc="New version checker for software"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-xdist' 'python-pytest-asyncio' 'python-flaky')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz")
sha512sums=('33b4c7da7825bd85d6e27ef37cb64ad9c59664fa3d614a4582172a58109cab7552ca6a49f91c29508fcfd8c12559d2fae496180e92d74983fa12ad5721dbf77a')

prepare() {
  cd nvchecker-$pkgver
  # fix broken test
  # partial backport of https://github.com/lilydjwg/nvchecker/commit/d0ee3592097fdd4c427fbec95ca48d08b5c93007
  sed -i 's|1\.3-8|1.3-9|' tests/test_{archpkg,pacman}.py
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
