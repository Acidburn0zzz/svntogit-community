# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=1.0.0
pkgrel=1
pkgdesc='A build tool for Scala, Java, and more'
arch=('any')
url='http://www.scala-sbt.org/'
license=('BSD')
depends=('java-runtime-headless' 'sh')
IFS='.' read -a _pkgver <<< "${pkgver}"
_majorver=${_pkgver[0]}.x
source=(https://github.com/${pkgname}/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tgz{,.asc}
        https://raw.githubusercontent.com/${pkgname}/${pkgname}/${_majorver}/LICENSE)
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('9ae04f4972145f2ac56c4deb868c9a5bb8b8b85c5151885dff3b997712645c5a'
            'SKIP'
            '70b7b72694a56a3e922deaf5482273e7c33bab2c00c0c67fc0ccc5d33927ffed')
#backup=(etc/${pkgname}/sbtopts)

package() {
  cd "${pkgname}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}
  cp -r lib "${pkgdir}"/usr/share/${pkgname}
  cp -r bin "${pkgdir}"/usr/share/${pkgname}
  rm "${pkgdir}"/usr/share/${pkgname}/bin/*.bat
  chmod -x "${pkgdir}"/usr/share/${pkgname}/bin/*
  chmod +x "${pkgdir}"/usr/share/${pkgname}/bin/sbt
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt

  # FIXME move usr/share/conf to /etc/sbt but watch-out, pacman does not handle it well
  # also enable its file as `backup`
  install -D -m 644 conf/sbtopts -t "${pkgdir}"/usr/share/${pkgname}/conf
  mkdir -p "${pkgdir}"/etc/${pkgname}
  ln -s /usr/share/${pkgname}/conf/sbtopts "${pkgdir}"/etc/${pkgname}/sbtopts

  install -D "${srcdir}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
