# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-elasticsearch
pkgver=7.6.0
pkgrel=2
arch=('any')
pkgdesc='Python client for Elasticsearch'
url='https://github.com/elastic/elasticsearch-py'
license=('Apache')
depends=('python-urllib3')
makedepends=('python-setuptools' 'git')
checkdepends=('python-nose' 'python-requests' 'python-coverage' 'python-mock' 'python-pyaml'
              'python-nosexcover')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elastic/elasticsearch-py/archive/$pkgver.tar.gz"
        git+https://github.com/elastic/elasticsearch.git)
sha512sums=('f8a54cd222af94d659087d654bcbe52da5ee90570ff04dbdd6793b870b8fd7e4b5b896620424f85057f2274ed3a16ecd97f53f704c2a97931b4876d246311ea9'
            'SKIP')

prepare() {
  sed -i 's/urllib3<.*,>/urllib3>/' elasticsearch-py-$pkgver/setup.py
  sed -i 's|CA_CERTS = None|CA_CERTS = "/etc/ssl/certs/ca-certificates.crt"|' elasticsearch-py-$pkgver/elasticsearch/connection/http_urllib3.py
}

build() {
  cd elasticsearch-py-$pkgver
  python setup.py build
}

check() {
  cd elasticsearch-py-$pkgver
  TEST_ES_REPO="$srcdir"/elasticsearch python setup.py test
}

package() {
  cd elasticsearch-py-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
