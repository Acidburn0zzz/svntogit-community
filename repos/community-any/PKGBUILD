# Contributor: Timm Preetz <timm@preetz.us>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netbeans
pkgver=6.9.1
pkgrel=1
pkgdesc='Netbeans IDE development platform.'
arch=('any')
url='http://www.netbeans.org'
license=('CDDL')
depends=('java-environment')
optdepends=('php: for developing programs in php'
            'ruby: for developing programs in ruby'
            'python: for developing programs in python')
install=netbeans.install
source=('http://download.netbeans.org/netbeans/6.9.1/final/zip/netbeans-6.9.1-201007282301-ml.zip'
        'netbeans.desktop')
md5sums=('c31060abc6d33cf82b17e255c100fe40'
         '88c631d0d263218e01ea886fde2bc913')

package() {
	rm ${pkgname}/bin/netbeans.exe

	install -d ${pkgdir}/usr/share/${pkgname}
	cp -r ${pkgname}/* ${pkgdir}/usr/share/${pkgname}/

	install -D -m644 netbeans.desktop ${pkgdir}/usr/share/applications/netbeans.desktop

	install -d ${pkgdir}/usr/bin
	ln -s /usr/share/netbeans/bin/netbeans ${pkgdir}/usr/bin/netbeans
}
