# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=httpcore
pkgname=python-httpcore
pkgver=0.12.3
pkgrel=1
pkgdesc="A minimal HTTP client"
arch=('any')
url="https://github.com/encode/${_pkgname}"
license=('BSD')
depends=('python-h11' 'python-sniffio')
optdepends=('python-h2: for HTTP/2 support')
makedepends=('python-setuptools' 'python-h2')
checkdepends=('hypercorn' 'python-anyio' 'python-curio' 'python-pproxy' 'python-pytest-asyncio' 'python-pytest-trio' 'python-trustme' 'uvicorn')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/commit/04539341c0db3b5238aa48815facadca0b546d2b.patch")
sha512sums=('b31535231dc2b6a5f1c9c0c538628f75360f04b4747b447ab0a7168ed32d0eabd463335c2f7fa782dcabb4cbc32891a2839ae50e0999d5a791c01b02533d38f0'
            'eefd2244f96a245e965fefa0a1a847a4caf4d597e2bd83a6087eb086fe5464573ff0f18b648c8241be90f3e0f546b81dfe65863c830efd1084cc48efb08a9e77')
b2sums=('3c0e7a210b21e54564c4c6077fadf66344b887662c71b05a1ad4a2c0224a4d703eccdf576d4443341176edee03b6776bfd49aaef2ea2f29f02614677e1dfad85'
        '2b4bfb44001d190d3f8426757622e066b20ab30c9dff0b8dcb3620ac5b3bd3b8d9fe100f176cfee0ee9f1a5050e00be99c1235f6e45d184ac0b75c1ede1acb9f')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # do not run coverage in unittests!
    sed -i '/^addopts/d' setup.cfg

    # drop flaky connection tests
    patch -Np1 -i ../04539341c0db3b5238aa48815facadca0b546d2b.patch
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    ulimit -S -n 4096
    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.md "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.md
}
