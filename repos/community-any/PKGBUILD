# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-threading
pkgname=python-click-threading
pkgver=0.4.4
pkgrel=10
pkgdesc="Multithreaded Click apps made easy"
arch=("any")
url="https://pypi.python.org/pypi/click-threading"
license=("MIT")
depends=("python-click")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-click")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        fix-click-8.0-compat.patch)
#source=("${_name}-${pkgver}.tar.gz::https://github.com/click-contrib/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('7fd17ad04065160a3278eab4e2c070424d9d69de145e88a8dc2ad9a3064162d91ad5c46b8d30e19640b2c4b420c0bb801ff6fd6dd9985f34ce2ede35cf8373f1'
            '712726854065215ebaddd62e589ec64b5b056a090bcd8fc2bc5e2c784831f48d205b700e04662d653f87306faa02972f9c7d5b55958c552156346b180c99b1f7')

prepare() {
  cd "${_name}-${pkgver}"
  patch -Np1 < ../fix-click-8.0-compat.patch
}

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH=build/lib:${PYTHONPATH}
  py.test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
