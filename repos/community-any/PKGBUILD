# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=comtool
pkgver=1.7.7
pkgrel=1
pkgdesc="A cross platform serial debug tools written by python"
arch=('any')
url="https://github.com/Neutree/COMTool"
license=('LGPL')
depends=('python-pyqt5' 'python-pyserial' 'python-beautifulsoup4' 'python-setuptools')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Neutree/COMTool/archive/V$pkgver.tar.gz")
sha512sums=('8baed14bbe4ed5cb217c6bf077077ba0bccbb31a9cb895c2e296f70bb8bfee3b1a3bc810a71d316ea1b12cb23e798ce7fc88c4a875ed0981cf7762036640b99f')

prepare() {
  cd COMTool-$pkgver
  sed -i "s/'pyqt5>=5',//" setup.py
  sed -e 's|^strDataAssetsDirName = .*|strDataAssetsDirName = "share/COMToolData/assets"|' \
      -e 's|^strDataDirName = .*|strDataDirName = "share/COMToolData"|' \
      -i COMTool/parameters.py
}

build() {
  cd COMTool-$pkgver
  python setup.py build
}

check() {
  cd COMTool-$pkgver
  # Disabled: Tests hang
  # xvfb-run python -m unittest discover
}

package() {
  cd COMTool-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
