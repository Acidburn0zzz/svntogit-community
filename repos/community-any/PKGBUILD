# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mediawiki
pkgver=1.17.0
pkgrel=3
pkgdesc="MediaWiki engine"
arch=('any')
url="http://www.mediawiki.org/wiki/MediaWiki"
license=("GPL")
depends=(php)
optdepends=('texvc: for math rendering'
	    'python2')
backup=(etc/webapps/mediawiki/.htaccess)
install=mediawiki.install
options=(!strip)
source=(http://download.wikimedia.org/mediawiki/1.17/mediawiki-$pkgver.tar.gz
	apache.example.conf)
md5sums=('871a00a8eb6dcae1b7b654ae635af7cd'
         '99c25f04d4093f3333db1be7624fe4e7')

build() {
  cd $srcdir
  sed -i -e 's#is_executable#is_file#' -e 's#is_executable#is_file#' $pkgname-$pkgver/includes/Math.php
  install -vdm0755 $pkgdir/usr/share/webapps
  install -vdm0755 $pkgdir/etc/webapps/mediawiki
  cp -a $pkgname-$pkgver $pkgdir/usr/share/webapps/mediawiki
  ln -s /usr/bin/texvc $pkgdir/usr/share/webapps/mediawiki/math/texvc
  echo "deny from all" >> $pkgdir/etc/webapps/mediawiki/.htaccess
  ln -sf /etc/webapps/mediawiki/.htaccess $pkgdir/usr/share/webapps/mediawiki/.htaccess
  install -vDm0644 $srcdir/apache.example.conf $pkgdir/etc/webapps/mediawiki/apache.example.conf

  # move cache and images to /var
  install -vdm0755 -o http -g http $pkgdir/var/cache/mediawiki
  install -vdm0755 -o http -g http $pkgdir/var/lib/mediawiki

  cd $pkgdir/usr/share/webapps/mediawiki

  mv cache/.htaccess $pkgdir/var/cache/mediawiki/
  rmdir cache
  ln -sf /var/cache/mediawiki cache

  mv images/* $pkgdir/var/lib/mediawiki/
  mv images/.htaccess $pkgdir/var/lib/mediawiki/
  rmdir images
  ln -sf /var/lib/mediawiki images
}
