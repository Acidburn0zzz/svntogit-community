# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-systemd-tool
pkgver=32
pkgrel=1
pkgdesc="Provisioning tool for systemd in initramfs (systemd-tool)"
arch=('any')
url="https://github.com/random-archer/mkinitcpio-systemd-tool"
license=('Apache')
depends=('mkinitcpio' 'systemd')
optdepends=('cryptsetup: for initrd-cryptsetup.service'
            'dropbear: for initrd-dropbear.service'
            'busybox: for initrd-tinysshd.service'
            'mc: for initrd-debug-progs.service'
            'nftables: for initrd-nftables.service'
            'tinyssh: for initrd-tinysshd.service'
            'tinyssh-convert: for initrd-tinysshd.service')
conflicts=('mkinitcpio-dropbear' 'mkinitcpio-tinyssh')
backup=("etc/${pkgname}/config/crypttab"
        "etc/${pkgname}/config/fstab"
        "etc/${pkgname}/config/initrd-nftables.conf"
        "etc/${pkgname}/config/initrd-util-usb-hcd.conf"
        "etc/${pkgname}/network/initrd-network.network" )
source=("$pkgname-$pkgver.tar.gz::https://github.com/random-archer/${pkgname}/archive/v${pkgver}.tar.gz")
install="${pkgname}.install"
sha512sums=('e9f32d4749ee4b6732cf757a38797e113550bbc8251f7175d5cfea817c78f995ea131ce51b4299a46e818998cbad8eef17bb24dc1f0030ea1dc2b97b44648fa3')

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
}
