# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-common
pkgver=1.24.0
pkgrel=1
pkgdesc="Common development macros for MATE"
arch=('any')
license=('GPL')
depends=('autoconf' 'automake' 'gettext' 'gtk-doc' 'libtool'
         'pkg-config')
url="https://mate-desktop.org"
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('e7ef908d7955cf7252330c87f6416b3fdcb833bb682b7bf9b42e1f2e662d821b')

build() {
	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
