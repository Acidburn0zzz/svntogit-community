# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-login
pkgver=0.4.1
pkgrel=4
pkgdesc="User session management for Flask."
arch=('any')
url="https://pythonhosted.org/Flask-Login/"
license=('MIT')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-blinker' 'python-semantic-version')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxcountryman/flask-login/archive/$pkgver.tar.gz"
         python-flask-login-fix-test.patch::"https://github.com/maxcountryman/flask-login/commit/5931bd5c.patch")
sha512sums=('c87a2948ac37439ddd76f3f11584bdd5910cb00e96a7400343cf4dadc5a0f9bc84d40bdc068d2e8c05bd5a510046e18473e9ad71502c5039a5f05b371ce9bb4c'
            '4a59d2a59c556f4d00684456d43a3a8f1bd06fbcf75b7037a0d52817b4227083e82538a8e6fb86055558b3f89b681db8a15c125102fa7df7534aaf29a856fff0')

prepare() {
  cd flask-login-$pkgver
  patch -p1 -i ../python-flask-login-fix-test.patch # Fix X-Forwarded-Host test
}

check() {
  cd "$srcdir"/flask-login-$pkgver
  nosetests3
}

package() {
  cd flask-login-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
