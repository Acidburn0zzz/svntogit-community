# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=java-commons-lang
pkgver=3.10
pkgrel=1
pkgdesc='A host of helper utilities for the java.lang API'
arch=('any')
url='https://commons.apache.org/proper/commons-lang/'
license=('APACHE')
depends=('java-runtime-headless')
makedepends=('maven' 'strip-nondeterminism')
source=("https://www.apache.org/dist/commons/lang/source/commons-lang3-$pkgver-src.tar.gz"{,.asc})
sha512sums=('8c3e90e60bbb0b83c8e5e8d173d5a982d6f5bf3d2392e11cd17b153484a87c42f3003a7ae84ff3c34a11c16df61d7a4fb8e902b31e0e9c2cef2a17115c0c468b'
            'SKIP')
validpgpkeys=('B6E73D84EA4FCC47166087253FAAD2CD5ECBB314'  # Rob Tompkins <chtompki@apache.org>
              '2DB4F1EF0FA761ECC4EA935C86FDC7E2A11262CB') # Gary David Gregory (Code signing key) <ggregory@apache.org>

build() {
	cd "commons-lang3-$pkgver-src"
	mvn -Dmaven.javadoc.skip=true -Dmaven.test.skip=true clean package

	# Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
	# (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" "target/commons-lang3-$pkgver.jar"
}

check() {
	cd "commons-lang3-$pkgver-src"
	mvn test
}

package() {
	cd "commons-lang3-$pkgver-src"
	install -Dm644 "target/commons-lang3-$pkgver.jar" -t "$pkgdir/usr/share/java/commons-lang"
	ln -s "commons-lang3-$pkgver.jar" "$pkgdir/usr/share/java/commons-lang/commons-lang.jar"
}
