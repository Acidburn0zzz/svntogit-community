# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>
# Contributor: Feng Wang <wanng.fenng AT gmail DOT com>
pkgname=gmm
pkgver=5.3
pkgrel=1
pkgdesc="Generic C++ finite element library"
arch=('any')
url="http://getfem.org/"
license=('LGPL')
source=("https://download.savannah.nongnu.org/releases/getfem/stable/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5f5bb25268d4357fc103f448c5bb3def49b16f1a03a54495b8c17c347ea3c6cb87435eb943c0fe3e9b46f75fa2ab7942ac63858c9eae01f865e1e4c6e4e7fa0a')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

check(){
  cd "${pkgname}-${pkgver}"
  make check
}

package(){
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}


