# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.10.0
pkgrel=1
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
arch=('any')
url='https://github.com/tavendo/txaio'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-twisted' 'python2-twisted')
source=("https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz")
sha512sums=('45f85eca04dadc44f47bea0359902965db7c85892303b73d1c227b065965a1e83d8b3e555affcb8b43c6fdbd4d25c8d475d763c8ec8984f3e6fc1ad2befff6c4')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py build

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py test

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py test
}

package_python-txaio() {
  depends=('python-six')

  cd txaio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-txaio() {
  depends=('python2-six')

  cd txaio-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
