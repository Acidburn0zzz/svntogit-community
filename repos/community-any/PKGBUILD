# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-themes
pkgver=1.3.0
pkgrel=1
pkgdesc="Official themes for the UKUI desktop"
arch=('any')
license=('GPL')
url="https://github.com/ukui/ukui-themes"
groups=('ukui')
depends=('dconf')
makedepends=('ruby-sass')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-themes/archive/v$pkgver.tar.gz"
        ukui-themes-fix-icon-theme.patch::https://github.com/ukui/ukui-themes/pull/19.patch)
sha512sums=('638ace972a643e839409940d927441c1fb32ac4781ab8a9e09eb06595df9061bab85ef4686b6b3a06dfde60c9482c0866172382a48ec57bd9ba6bc543ca23cff'
            '0ce3e7f951bdabb1c8c8253351b9a77c064ef4f6e002ca5dcbe23770e0e03d46b9d6e9f283567f850dc7c3abd7d92dbb513a90e47aec499dc515c74faeb45c58')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ukui-themes-fix-icon-theme.patch
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 debian/25_ukui-themes.gschema.override -t "$pkgdir"/usr/share/glib-2.0/schemas/
  install -dm755 "$pkgdir"/usr/share/icons
  cp -a ukui-icon-theme* "$pkgdir"/usr/share/icons/
  install -Dm644 ukui-custom.xml -t "$pkgdir"/usr/share/mime/packages/
}
