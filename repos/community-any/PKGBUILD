# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.2.0
pkgrel=1
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
makedepends=(python-setuptools python-pypandoc)
depends=(python-botocore python-future python-jsonpickle python-requests python-wrapt)
checkdepends=(python-flask-sqlalchemy python-pynamodb python-pytest python-aiohttp
              python-pytest-aiohttp python-testing.postgresql python-psycopg2)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/aws/aws-xray-sdk-python/archive/$pkgver.tar.gz")
sha256sums=('c091c85a8da2ba6e3f00b8ef3ccf92f888ec1b64ec35bcb7eeaaf714ca84576e')

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # aiobotocore is not compatible with botocore 1.11+, which is required by aws-xray-sdk 2.x
  # https://github.com/aio-libs/aiobotocore/issues/629
  # the test suite is not compatible with Django 2.x yet
  # https://github.com/aws/aws-xray-sdk-python/issues/85
  pytest -v --ignore tests/ext/aiobotocore --ignore tests/ext/django
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
