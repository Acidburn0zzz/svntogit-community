# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=nextcloud-app-bookmarks
_name=bookmarks
epoch=1
pkgver=10.0.0
pkgrel=1
pkgdesc="Bookmarks app for nextcloud"
arch=('any')
url="https://github.com/nextcloud/bookmarks"
license=('AGPL3')
depends=('nextcloud')
makedepends=('ripgrep' 'yq')
groups=('nextcloud-apps')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/bookmarks/releases/download/v$pkgver/bookmarks-$pkgver.tar.gz")
sha256sums=('0c43f480dbf04cbb77918a5a94962338bb51e05b3a8411e8b73b8322cd6b6a2d')
b2sums=('ecf401f6906b73fd4f5d47ec462182b3502fefe7769c2e70b178b762bae442ac70164c00735a1d7ae41561652f30ca59f19f49b41530f09f9bdbd3f1088fd2d3')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -d "$pkgdir"/usr/share/webapps/nextcloud/apps
  cp -a "$srcdir"/bookmarks "$pkgdir"/usr/share/webapps/nextcloud/apps/bookmarks
}
