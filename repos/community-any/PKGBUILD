# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-magnumclient
pkgver=3.1.0
pkgrel=1
pkgdesc="Client library for Magnum API"
arch=('any')
url="https://docs.openstack.org/python-magnumclient/latest/"
license=('Apache')
depends=('python-pbr' 'python-babel' 'python-keystoneauth1' 'python-stevedore' 'python-requests'
         'python-oslo-i18n' 'python-oslo-log' 'python-oslo-serialization' 'python-oslo-utils'
         'python-os-client-config' 'python-osc-lib' 'python-prettytable' 'python-cryptography'
         'python-debtcollector')
checkdepends=('python-openstackclient' 'python-oslotest' 'python-osprofiler')
source=("https://github.com/openstack/python-magnumclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('64ef0d2676155eea2940f8c2917b7b5f99698b8b82f472eb0e9737688ac97432ddd30e70c01eb3e10687ba62bdaf3e2422033029872de9525a9023befd4f49b2')

export PBR_VERSION=$pkgver

build() {
  cd python-magnumclient-$pkgver
  python setup.py build
}

check() {
  cd python-magnumclient-$pkgver
  stestr run
}

package() {
  cd python-magnumclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
