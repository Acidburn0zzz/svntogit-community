# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=browserify
pkgdesc='browser-side require() the node way'
pkgver=14.3.0
pkgrel=1
arch=('any')
url='http://browserify.org/'
license=('MIT' 'BSD')
depends=('nodejs' 'acorn')
makedepends=('npm' 'git')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('cffde05d80b0029d94eda7bebb354a1ff7ed5c06b445013f87093e1e214b6d0a6a1ec1efd2860d180cb07a3e5bb1b9b62f9918d88e8abeac7de2dfa1871bfdef')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done
}
