# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgname=python-pint
pkgver=0.16
pkgrel=1
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="https://pint.readthedocs.org"
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-numpy' 'python-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('f81bb5e6be43516645c282c73c695c965b1bac11f27a44278ac84bdc1f7a6ae23e35162c22721f0a6dabb63861688a1f01779d0e493495a6859c7f6e6ce6abad')

prepare() {
  cd Pint-$pkgver
  sed -i 's/pkg_resources.extern.//' pint/quantity.py
}

check() {
  cd Pint-$pkgver
  python setup.py pytest
}

package() {
  cd Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
