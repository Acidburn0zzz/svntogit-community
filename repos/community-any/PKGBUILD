# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=2.4.0
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-six' 'python-numpydoc' 'python-scipy' 'python-sqlitedict' 'python-pycodestyle'
         'python-pydocstyle' 'python-testflo' 'python-parameterized' 'python-pyparsing'
         'python-networkx' 'python-sphinx' 'python-redbaron' 'python-mock' 'python-requests-mock'
         'python-tornado' 'python-pydoe2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver.tar.gz")
sha512sums=('cb9cdb419dc9509edcd8ad23ac7c467a8436d39002227074b30890b0ea0f079ef6870bc99fe810e68584f88c3178151640045c2d895738c83de82085a5cad70b')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/from distutils.core/from setuptools/' \
      -e 's/==/>=/' \
      -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  # Different pycodestyle version?
  testflo -n 16 --timeout=120 openmdao || warning "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
