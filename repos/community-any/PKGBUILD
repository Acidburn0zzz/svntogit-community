# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.23.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-yaml python-mock python-parameterized)
source=("https://github.com/awslabs/serverless-application-model/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b94707e665ff69697e9fa7e3427dd3a35dbf53175dd92cf91a2aeb9e938c78c0')

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver
  # disable upstream pytest.ini, which involves coverage
  pytest -c /dev/null -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -rv "$pkgdir"/usr/lib/python*/site-packages/tests
}
