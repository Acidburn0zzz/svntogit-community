# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-validators
pkgver=0.16.0
pkgrel=1
pkgdesc="Python Data Validation for Humans"
url="https://github.com/kvesteri/validators"
license=('MIT')
arch=('any')
depends=('python-decorator' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kvesteri/validators/archive/$pkgver.tar.gz")
sha512sums=('50da6a190d0248bd58f8ec85c88211a998445b999e6e0b6ca65ebdb10cb842fa5019bb4f8ea1ea5840cd678c82dfd25e106e639d80141bf2a4273c108de1261e')

build() {
  cd validators-$pkgver
  python setup.py build
}

check() {
  cd validators-$pkgver
  python -m pytest
}

package() {
  cd validators-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
