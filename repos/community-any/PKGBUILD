# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-google-auth
pkgver=1.28.0
pkgrel=1
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-setuptools'
         'python-cachetools')
checkdepends=('python-pytest' 'python-mock' 'python-requests' 'python-flask'
              'python-oauth2client' 'python-pytest-localserver' 'python-cryptography'
              'python-freezegun' 'python-responses' 'python-pyopenssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz")
sha512sums=('8eaed0eff1e6b1867230523e6ba23c59c4de5dd2d836950e6af6507272ee2aa7e18cda36d63ae5f37eaeefd9caead1d58bedf637c8fdd2791cd2ac7a08739673')

prepare() {
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         -e 's|import certifi|pass|' \
         google-auth-library-python-$pkgver/google/auth/transport/{urllib3,requests}.py
}

build() {
  cd google-auth-library-python-$pkgver
  python setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd google-auth-library-python-$pkgver
  python -m pytest tests --deselect tests/transport/test_urllib3.py::test__make_default_http_without_certifi
}

package() {
  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
