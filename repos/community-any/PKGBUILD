# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-plop
pkgver=0.3.0
pkgrel=4
pkgdesc="Python Low-Overhead Profiler"
arch=('any')
license=('MIT')
url="https://github.com/bdarnell/plop"
depends=('python-six' 'python-tornado')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/p/plop/plop-${pkgver}.tar.gz"
        disable-flaky-threaded-test.patch)
md5sums=('4a313d6a8dd59ac48cfa3af426d197fc'
         '1d9dc194b65a08f877506b5c5098c251')

prepare() {
  patch -d plop-$pkgver -Np1 < disable-flaky-threaded-test.patch
  # tornado.testing.LogTrapTestCase was removed in Tornado 5.0
  sed -i 's/LogTrapTestCase/ExpectLog/' plop-$pkgver/plop/test/callgraph_test.py
}

build() {
  cd plop-$pkgver
  python setup.py build
}

check() {
  cd plop-$pkgver
  python -m plop.test.runtests
}

package() {
  cd plop-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
