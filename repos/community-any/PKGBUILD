# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-uncertainties
pkgname=('python-uncertainties' 'python2-uncertainties')
pkgver=3.0.3
pkgrel=1
pkgdesc="Transparent calculations with uncertainties on the quantities involved (aka error propagation); fast calculation of derivatives."
arch=('any')
license=('BSD')
url="https://github.com/lebigot/uncertainties"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/lebigot/uncertainties/archive/$pkgver.tar.gz")
sha512sums=('d8cc8f13803ab55f81ce429f61248aaee2be9fa73058ee651302ca642440dfaaa26adcb57c754fb6ee6512d3d4238f34adb4e7e29a97273baba7216ddc39e2ba')

prepare() {
  # Copy needed for 2to3
  cp -a uncertainties-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/uncertainties-$pkgver
  python setup.py build

  cd "$srcdir"/uncertainties-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/uncertainties-$pkgver
  python setup.py nosetests

  cd "$srcdir"/uncertainties-$pkgver-py2
  python2 setup.py nosetests
}

package_python-uncertainties() {
  depends=('python')
  optdepends=('python-numpy: additional support for NumPy arrays and matrices')

  cd uncertainties-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-uncertainties() {
  depends=('python2')
  optdepends=('python2-numpy: additional support for NumPy arrays and matrices')

  cd uncertainties-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
