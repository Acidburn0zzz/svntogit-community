# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=fcitx5-pinyin-zhwiki
pkgname=(fcitx5-pinyin-zhwiki rime-pinyin-zhwiki)
_converterver=0.2.3
_zhwikiver=20211101
_webslangver=20211121
pkgver=$_converterver.$_webslangver
pkgrel=1
epoch=1
pkgdesc="Fcitx 5 Pinyin Dictionary from zh.wikipedia.org"
arch=('any')
url="https://github.com/felixonmars/fcitx5-pinyin-zhwiki"
license=('CCPL:by-sa' 'custom:GFDL')
makedepends=('libime' 'opencc' 'pypinyin')
source=(https://github.com/felixonmars/fcitx5-pinyin-zhwiki/archive/$_converterver/$pkgbase-$_converterver.tar.gz
        https://github.com/felixonmars/fcitx5-pinyin-zhwiki/releases/download/$_converterver/web-slang-$_webslangver.source
        https://dumps.wikimedia.org/zhwiki/$_zhwikiver/zhwiki-$_zhwikiver-all-titles-in-ns0.gz
        https://www.gnu.org/licenses/fdl-1.3.txt)
sha512sums=('b09e4e29e4c3e5e9007f28d56e2ac07663ad56eb1e4004dad647de7f4b41522054a65c40f952bb3752ea6015b3b1adc30f95922749cd5653ea60b9d151dd4013'
            'd8b4068ef3a29cc40048067779ff7414c1f4c7b6e78c1f1ae2201434eaa694cc27565915ec8811744d7c905f0f17b380c3c87d977da8ff8ea3ccd10aaa272d89'
            '2bacc0a30565dbeef4e9889436f8764d3b7d640dee4aa7e584de474415cd37f5850e405764f9e63a21c0f177afc8f80b8705546998e14a397c63fded67fa6118'
            'bea1788b2bdc84f470e459114b871cf4ee991718964a63e18adde65116806d7676484cb30857cf74dece5eef5f96a015ee4a21900e019623e5d3484868b28b7f')

_make="make -L VERSION=$_zhwikiver WEB_SLANG_VERSION=$_webslangver"

prepare() {
  cd $pkgbase-$_converterver
  # Workaround pacman decompression
  touch zhwiki-$_zhwikiver-all-titles-in-ns0.gz
  ln -s ../zhwiki-$_zhwikiver-all-titles-in-ns0
  ln -s ../web-slang-$_webslangver.source
}

build() {
  cd $pkgbase-$_converterver
  $_make zhwiki.dict zhwiki.dict.yaml
}

package_fcitx5-pinyin-zhwiki() {
  cd $pkgbase-$_converterver
  $_make DESTDIR="$pkgdir" install
  install -Dm644 ../fdl-1.3.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

package_rime-pinyin-zhwiki() {
  cd $pkgbase-$_converterver
  $_make DESTDIR="$pkgdir" install_rime_dict
  install -Dm644 ../fdl-1.3.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
