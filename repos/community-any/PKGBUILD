#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2021.7
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2021.7.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2021.7.tar.xz.sig
)
sha512sums=(
  0f0a84abab41bf6e2e72abcb873ae2986300d172a618466d4aa43e6ba1e4bf4a6072dccd6303a59129d16eac5997ef160df23a3424de901ac6ee2414ecc5acb8
  50c5cd9ea560159a3ef0959f6d20db901356553e34d5e27a89639c3937e19deed1175a4561cf6e1c07c00db842825e0cb5f27f836a312a854b99039ed4e971ef
)
md5sums=(
  98dbb6cb343ff0c6cf936ba470d68955
  0305b97b80994b09d283947ce4696e68
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
