# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Cory Farmer <rbgkofi@ameritech.net>

pkgname=gramps
pkgver=5.0.1
pkgrel=2
epoch=2
pkgdesc="Genealogy program, which helps you track your family tree"
arch=('any')
url="https://gramps-project.org/"
license=('GPL')
depends=('gtk3' 'python-bsddb' 'python-cairo' 'python-gobject' 'xdg-utils')
makedepends=('intltool')
optdepends=('osm-gps-map: show maps in the geography view'
            'graphviz: enable creation of graphs (required for tree reports)'
            'python-pyicu: improves localised sorting'
            'gtkspell3: enable spell checking in the notes'
            'rcs: manage multiple revisions of your family trees'
            'python-pillow: crop and convert images to JPG'
            'libgexiv2: manage Exif metadata embedded in your media'
            'geocode-glib: associate a geographical position to a place name')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gramps-project/$pkgname/archive/v$pkgver.tar.gz"
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('3a81265764a314cca0540593ccd639c5b76199615b3b0aace0b2540fc186eba5'
            '0ac8d849c03dc5d17a45e0fb54abe5395552d6d977e1a9d85d62091d6c082914')

prepare() {
  cd $pkgname-$pkgver

  # Fix AppStream metadata
  # https://github.com/gramps-project/gramps/pull/746
  patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch
}

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  echo -n '/usr/share' >"$pkgdir"/usr/lib/python*/site-packages/gramps/gen/utils/resource-path

  # Install application icon to its correct place
  install -dm755 "$pkgdir"/usr/share/icons/hicolor/{16x16,22x22,24x24,48x48,scalable}/apps
  mv "$pkgdir"/usr/share/gramps/images/hicolor/16x16/actions/gramps.png "$pkgdir"/usr/share/icons/hicolor/16x16/apps/gramps.png
  mv "$pkgdir"/usr/share/gramps/images/hicolor/22x22/actions/gramps.png "$pkgdir"/usr/share/icons/hicolor/22x22/apps/gramps.png
  mv "$pkgdir"/usr/share/gramps/images/hicolor/24x24/actions/gramps.png "$pkgdir"/usr/share/icons/hicolor/24x24/apps/gramps.png
  mv "$pkgdir"/usr/share/gramps/images/hicolor/48x48/actions/gramps.png "$pkgdir"/usr/share/icons/hicolor/48x48/apps/gramps.png
  mv "$pkgdir"/usr/share/gramps/images/hicolor/scalable/actions/gramps.svg "$pkgdir"/usr/share/icons/hicolor/scalable/apps/gramps.svg
  rm "$pkgdir"/usr/share/icons/gramps.png
}
