# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Erkin Batu Altunba≈ü <erkinbatu@gmail.com>
pkgname=ponysay
pkgver=1.1
pkgrel=1
pkgdesc="Pony wrapper for cowsay"
url="https://github.com/erkin/ponysay"
arch=('any')
license=('WTFPL')
depends=('cowsay' 'bash')
source=(ponysay-${pkgver}.tar.gz::https://github.com/erkin/ponysay/tarball/${pkgver})
md5sums=('388abb348dbd3a93ee5a105af8e99365')

build() {
  cd ${srcdir}/erkin-ponysay-*
  make all
}

package() {
  cd ${srcdir}/erkin-ponysay-*
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
