# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-dnsmadeeasy
pkgver=0.30.0
pkgrel=1
pkgdesc="DNS Made Easy DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'dns-lexicon' 'python-acme' 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('1c2f020bb044b6a0018ea0cade90188d93e68f2e71958c73a75a6f4d4eb45e44ee2762e80bd4d0eb4daf8a4b627cbc8fd2c213eacd8fba36b8cdeeb155bfea5b'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
