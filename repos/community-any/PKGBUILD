# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.174
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('5f2e44fab68b5b2fec4bc9997ef140251af75762cacb5ec06b7a93aceaaa1e904bcac7baac027ed523686398264d3485e745f8cc435dca1963f8e4f1474f0fc1')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

package() {
  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s ../lib/node_modules/$pkgname/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/usr/lib/node_modules/$pkgname
}

# vim:set ts=2 sw=2 et:
