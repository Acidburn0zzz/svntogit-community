# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=('python-boto3' 'python2-boto3')
pkgver=1.7.45
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
makedepends=('python-setuptools' 'python2-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('fc46744d4112c80ce51ebae279bb0bba8f0eace366944c89cf7412d4a2b1425aca098c35f23bc0b563e48957e091e71af3adddc4c6c8d0e532c906180ccf6311')

prepare() {
  cp -r "boto3-$pkgver" "python-boto3-$pkgver"
  cp -r "boto3-$pkgver" "python2-boto3-$pkgver"
}

build() {
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py build

  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py build
}

check() {
  cd "$srcdir"/boto3-$pkgver
  # scripts/ci/run-tests
}

package_python-boto3() {
  depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
  cd "$srcdir/python-boto3-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-boto3() {
  depends=('python2-botocore' 'python2-jmespath' 'python2-s3transfer')
  cd "$srcdir/python2-boto3-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
