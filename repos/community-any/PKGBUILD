# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Florijan Hamzic <florijanh@gmail.com>

pkgname=python-cairocffi
pkgver=1.0.0
pkgrel=1
pkgdesc="cairocffi is a CFFI-based drop-in replacement for Pycairo, a set of Python bindings and object-oriented API for cairo."
arch=('any')
url="https://cairocffi.readthedocs.io/"
license=('BSD')
depends=('python-cffi' 'cairo' 'python-xcffib')
makedepends=('python-cffi' 'python-setuptools' 'cairo'
             'python-xcffib' 'gdk-pixbuf2')
checkdepends=('python-pytest-runner')
optdepends=('gdk-pixbuf2: for cairocffi.pixbuf')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/cairocffi/archive/v${pkgver}.tar.gz")
sha512sums=('d7436280763cd388ee016e36e1fd2bd02ae3f46cee8d8788eb5c4d283e711ca115e36ffdcc4dc84aeb4635d410fbfd7df4ce0dd6cbe303c3386ab10e05fc9339')


build() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py build
}

check() {
  export LC_CTYPE=en_US.utf8

  cd "$srcdir/cairocffi-$pkgver"
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd "$srcdir/cairocffi-$pkgver"
  python setup.py install --root="$pkgdir" --prefix=/usr -O1 --skip-build

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
