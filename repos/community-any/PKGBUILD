# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sqlalchemy-migrate
pkgname=(python-sqlalchemy-migrate python2-sqlalchemy-migrate)
pkgver=0.11.0
pkgrel=4
pkgdesc="Database schema migration for SQLAlchemy"
arch=('any')
url="https://pypi.python.org/pypi/sqlalchemy-migrate/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-sqlalchemy'
             'python2-sqlalchemy' 'python-decorator' 'python2-decorator' 'python-sqlparse'
             'python2-sqlparse' 'python-tempita' 'python2-tempita')
checkdepends=('python-mock' 'python2-mock' 'python-testrepository' 'python2-testrepository'
              'python-scripttest' 'python2-scripttest' 'python-ibm-db-sa' 'python2-ibm-db-sa'
              'mysql-python' 'python2-psycopg2')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/sqlalchemy-migrate/archive/$pkgver.tar.gz"
        fix-compatibility-with-sqlite-3.26.patch::https://git.openstack.org/cgit/openstack/sqlalchemy-migrate/patch/?id=4a7f9c46cb2b4d500713e099b73b5fe4137158b2)
sha512sums=('07c18d2f197f2bda2caa41672f7e3e278397f3b30d3cf3297200df89a051e9e46147dc1c95e20398ed575e7f3c1dd4c56f9e525ab47a960344f51d276e70639f'
            'd4c2b58de39166706edbf6b1e5dceea8518b5b582f76733e5e3058e3e21390ac19e4e9edebc3fb655ee6ca5c26c38982efd4c3d5f20813468761e55ea6d0fa85')

prepare() {
  pushd sqlalchemy-migrate-$pkgver
  patch -Np1 -i ../fix-compatibility-with-sqlite-3.26.patch
  popd

  cp -a sqlalchemy-migrate-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/sqlalchemy-migrate-$pkgver
  python setup.py build

  cd "$srcdir"/sqlalchemy-migrate-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sqlalchemy-migrate-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/sqlalchemy-migrate-$pkgver-py2
  PYTHON=python2 python2 setup.py testr || warning "Tests failed"
}

package_python-sqlalchemy-migrate() {
  depends=('python-six' 'python-pbr' 'python-sqlalchemy' 'python-decorator' 'python-sqlparse'
           'python-tempita')

  cd "$srcdir"/sqlalchemy-migrate-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sqlalchemy-migrate() {
  depends=('python2-six' 'python2-pbr' 'python2-sqlalchemy' 'python2-decorator' 'python2-sqlparse'
           'python2-tempita')

  cd "$srcdir"/sqlalchemy-migrate-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/migrate{,2}
  mv "$pkgdir"/usr/bin/migrate-repository{,2}
}

# vim:set ts=2 sw=2 et:
