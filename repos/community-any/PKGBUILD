# Contributor: Kevin Piche <kevin@archlinux.org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=zim
pkgver=0.51
pkgrel=2
pkgdesc="A WYSIWYG text editor that aims at bringing the concept of a wiki to the desktop."
arch=(any)
license=('GPL' 'PerlArtistic')
url="http://zim-wiki.org/"
depends=('pygtk' 'ttf-dejavu')
optdepends=('bzr: Version Control plugin'
	    'graphviz: Insert Diagram & Link Map plugins'
	    'gtkspell: Spell Checker plugin'
	    'scrot: Insert Screenshot plugin'
	    'texlive-bin: Insert Equation plugin')
install=zim.install
source=(http://www.zim-wiki.org/downloads/${pkgname}-${pkgver}.tar.gz
"FS#23964.patch")
md5sums=('84e20450f153f83e32b84bc13374d76b'
         '64c437a254bb527f182df55d3f439a4d')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -N -p0 -i "$srcdir/FS#23964.patch"

    # python2 fixes
    for file in zim/_lib/xdot.py zim/_version.py; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

	sed -i 's|\t\tinstall_class.run(self)|&\n\t\treturn None|' setup.py
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	python2 setup.py install --root=${pkgdir} --optimize=1
}
