# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=matrix-appservice-irc
pkgver=0.13.0
pkgrel=1
pkgdesc="Node.js IRC bridge for Matrix"
arch=(any)
url="https://github.com/matrix-org/matrix-appservice-irc"
license=(Apache)
depends=(nodejs)
makedepends=(npm git)
optdepends=('python: for maintenance scripts')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9a3057223f8506f19d52c22030fba08327fdd0627d136512faf6b563c94fff7c')

package() {
  cd ${pkgbase}-${pkgver}
  npm install --cache "${srcdir}"/npm-cache -g --user root --prefix "${pkgdir}"/usr

  # Apparently it now symlinks to the source folderâ€¦
  rm -f "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  mkdir -p "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  cp -r * "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc/
  chmod -R go-w "${pkgdir}"/usr/lib/node_modules/
  chmod go-w "${pkgdir}"/usr/bin
  chown -R root:root "${pkgdir}"/usr
}
