# Maintainer: David Runge <dvzrv@archlinux.org>

_name=calmjs.types
pkgname=python-calmjs.types
pkgver=1.0.0
pkgrel=1
pkgdesc="Base types for calmjs framework"
arch=('any')
url="https://github.com/calmjs/calmjs.types"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# LICENSE not in sdist:
# https://github.com/calmjs/calmjs.types/issues/1
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip"
        "https://raw.githubusercontent.com/calmjs/calmjs.types/7bd37a2cd7ad7112f6d5ac4f7366146f7c45f512/LICENSE")
sha512sums=('9ae76507defba9839ffc46480d6c4f7dd32ef36b9c4ba623712c5cc7ba384c769cdcba256c87c1c3143771dd4714fadfda0b73bf970a55eec001ece4a1a5a0aa'
            '6009d8a9c57655f9c678b8614a284cd1f3a63a67c557aaed4d362ac4022974a4019ddf7bbf92e8fea8adf1b20374cd2916f00319fc0b895ca27e11d20276cfd7')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
