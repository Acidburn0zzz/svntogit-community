# Maintainer: David Runge <dave@sleepmap.de>
_name=spreed
pkgname=nextcloud-app-spreed
pkgver=6.0.4
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud>=16.0.0' 'nextcloud<17.0.0')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('a038ecd14162bb547a737b19eabce522d68e53fdb69a3a48a3a87b13dee71d6f5a40a573369983bc21a0a70a2eb98db1ca90a3fa5e339c3e8cf5ffcf61643918')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
