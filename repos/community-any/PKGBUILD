# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: FJ <joostef@gmail.com>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=gaupol
pkgver=0.20
pkgrel=3
pkgdesc="An editor for text-based subtitles"
arch=('any')
url="http://home.gna.org/gaupol"
license=('GPL')
depends=('python-gobject' 'iso-codes' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('gettext' 'intltool')
optdepends=('python-pyenchant: spell-checking'
            'python-chardet: character encoding auto-detection'
            'mplayer: subtitle preview'
            'vlc: subtitle preview')
install=gaupol.install
source=(http://download.gna.org/$pkgname/0.20/$pkgname-$pkgver.tar.xz
        disable-menu-tool-buttons.patch)
sha256sums=('07a81cfa92c7a72f151d3f712254af0bb0bfe3b07f696cd1f8c4ca2e15b435e4'
            '6c14c446bb061758f1a7b5a0a6eea3a3b1257220c69b72dabd53fa8719f0dc03')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugzilla.gnome.org/show_bug.cgi?id=686608
  patch -Np1 -i "$srcdir/disable-menu-tool-buttons.patch"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python3 setup.py --without-iso-codes install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
