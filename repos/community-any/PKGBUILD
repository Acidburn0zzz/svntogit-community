# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-flask-marshmallow
pkgver=0.12.0
pkgrel=1
pkgdesc='Flask + marshmallow for beautiful APIs'
url=https://flask-marshmallow.readthedocs.io/
arch=(any)
license=(MIT)
depends=(
  python-flask
  python-marshmallow
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/marshmallow-code/flask-marshmallow.git#tag=531f0f39a7005e0e39c59252b3a892c9d17bd1b8)
sha256sums=(SKIP)

pkgver() {
  cd flask-marshmallow

  git describe --tags
}

build() {
  cd flask-marshmallow

  python setup.py build
}

package() {
  cd flask-marshmallow

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-flask-marshmallow
}

# vim: ts=2 sw=2 et:
