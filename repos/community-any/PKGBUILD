# Maintainer: David Runge <dvzrv@archlinux.org>

_name=click-help-colors
pkgname=python-click-help-colors
pkgver=0.7
pkgrel=1
pkgdesc="Colorization of help messages in Click"
arch=('any')
url="https://github.com/click-contrib/click-help-colors"
license=('MIT')
depends=('python-click')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# tests missing from sdist tarball on pypi:
# https://github.com/click-contrib/click-help-colors/issues/10
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/click-contrib/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('c28c18db83e7f4811e9b1a5aa8e8fc8e89b880d98ec80ba01bbb19b8916d88ce04111139da00099a7fc93d01d898bed88a6d910e838c3891e857ceb1e5ef423f')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
