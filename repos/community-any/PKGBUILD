# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netbeans
pkgver=7.2.1
_subpkgver=201210100934
pkgrel=3
pkgdesc='IDE for Java, PHP, Groovy, C and C++'
arch=('any')
url='http://netbeans.org/'
license=('CDDL')
depends=('java-environment' 'ttf-bitstream-vera' 'libxtst' 'giflib')
makedepends=('gendesk>=0.4.2')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'lib32-glibc')
install=$pkgname.install
options=(!strip)
source=("http://download.netbeans.org/$pkgname/$pkgver/final/zip/$pkgname-$pkgver-$_subpkgver-ml.zip")
sha256sums=('2c798e8ebc5b9f6a73bfe06b5d7d6ce190343c7121f5cd60e98efb23044fb28f')

build() {
  cd "$srcdir"

  gendesk -n
}

package() {
  cd "$srcdir/$pkgname"

  mkdir -p "$pkgdir/usr/bin" "$pkgdir/usr/share/applications"
  cp -r "../$pkgname" "$pkgdir/usr/share"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "nb/$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  ln -s "/usr/share/$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
