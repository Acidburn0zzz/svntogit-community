# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qtile
pkgver=0.13.0
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=('any')
url="http://www.qtile.org"
license=('MIT')
depends=('gdk-pixbuf2' 'pango' 'python-xcffib' 'python-cairocffi' 'python-dbus'
'python-gobject' 'python-setuptools' 'python-six')
makedepends=('jupyter_console' 'python-ipykernel' 'python-iwlib'
'python-keyring' 'python-mpd2' 'python-pytest' 'python-setuptools'
'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpdwidget, mpd2widget widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
checkdepends=('imagemagick' 'python-pytest' 'python-xvfbwrapper' 'xorg-xcalc'
'xorg-xclock' 'xorg-xeyes' 'xorg-server-xephyr' 'xorg-xrandr' 'xterm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('06e6b32952c77c6b44872d5a52e0d196d5bb23c38da5d8acc2bba571f4d5fbadd8a7311f47e9301fb72a45c92b0255a90911edd12442392206da311fb7ada286')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# # tests disabled for now, as some are failing without explanation and py.test
# # won't return
# # https://github.com/qtile/qtile/issues/1130
# check() {
#   cd "${pkgname}-${pkgver}"
#   export PYTTHONPATH="build/lib/:${PYTHONPATH}"
#   export LC_TYPE=en_US.UTF-8
#   python libqtile/ffi_build.py
#   py.test -k 'not test_get_cairo_surface \
#              and not test_init \
#              and not test_from_path \
#              and not test_pattern \
#              and not test_pattern_rotate \
#              and not test_svg_scaling \
#              and not test_toggling \
#              and not test_focus_cycle \
#              and not test_focus_lost_hide \
#              and not test_kill \
#              and not test_floating_toggle \
#              and not test_images_good'
# }

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # config
  install -vDm 644 libqtile/resources/default_config.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # desktop file
  install -vDm 644 "resources/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/xsessions/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGELOG,README.rst}
}
