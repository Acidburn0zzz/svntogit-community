# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
pkgver=7.1.9
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('e81bfee17f6c54bce88965ba8a56bd41dcad90508f8d1187d0327fe54a25f2296acc937aa35ad9bea8a9b3f75b5b4458b4e23b4f7931dd1d33f22418f890eea9')

prepare() {
  cp -a python-sdk-$pkgver{,-py2}
}

check() {
  # Extracted from .travis.yml
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd "$srcdir"/python-sdk-$pkgver
  python setup.py pytest || warning "Tests failed"

  cd "$srcdir"/python-sdk-$pkgver-py2
  python2 setup.py pytest || warning "Tests failed"
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd python-sdk-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
