# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsbeautifier
pkgname=('python-jsbeautifier' 'python2-jsbeautifier')
pkgver=1.6.14
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
makedepends=('python-setuptools' 'python2-setuptools' 'python-editorconfig' 'python2-editorconfig')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('dbe7e658fc07863cac67dc3f179e0ee006b8a483b81d081905f354827ea635a3e36534594d8dc5935a8e74847957820a23718dc72651b8e5cd842bd8aa06609e')

prepare() {
  cp -a js-beautify-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/js-beautify-$pkgver/python
  python setup.py build

  cd "$srcdir"/js-beautify-$pkgver-py2/python
  python2 setup.py build
}

check() {
  cd "$srcdir"/js-beautify-$pkgver/python
  python setup.py test

  cd "$srcdir"/js-beautify-$pkgver-py2/python
  python2 setup.py test
}

package_python-jsbeautifier() {
  depends=('python-six' 'python-editorconfig')

  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsbeautifier() {
  depends=('python2-six' 'python2-editorconfig')

  cd js-beautify-$pkgver-py2/python
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/js-beautify{,2}
}

# vim:set ts=2 sw=2 et:
