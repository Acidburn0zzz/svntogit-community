# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.10.0
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=(any)
url="https://tmuxp.git-pull.com/en/latest/"
license=(MIT)
depends=(python-click python-colorama python-kaptan python-libtmux)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-rerunfailures)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha512sums=('5e86c5330e0e5d862bd3ac3f70f1f5fc3d7999ce0ccb7fea5cc6c857f57ce723c0b34b7c36ea64626106816a70b641b6877cabe9debc21dbfe55d16ec6a25351')
b2sums=('d834690e33855487ff6c6371f48e7dee4c44171edd9144f077e65754bfc10aee380b089fe4f3ced3cd7daac32626b20e55c382654a9801d0ca1a9d574f1808e0')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --optimize=1 --root="$pkgdir/"
  # license
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
  # docs
  install -vDm 644 {CHANGES,README.md} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 examples/*.{json,yaml} -t "$pkgdir/usr/share/doc/$pkgname/examples/"
}

# vim:set ts=2 sw=2 et:}
