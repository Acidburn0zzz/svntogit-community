# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.1.5
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
replaces=('deepin-default-gsettings')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz")
sha512sums=('5c2652b45ff95095167f29cecea46d20c750ad6f9032fdbe16b34c051d8e87f9e0b0297a9ae707fc673873732397fa63ea2b27808f4398b7f32c3379d9a42640')

prepare() {
  cd deepin-desktop-schemas-$pkgver
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin_default_background.jpg'#" overrides/x86/com.deepin.wrap.gnome.desktop.override
  # don't override GNOME defaults
  rm overrides/x86/{org.gnome.desktop,other}.override
}

build() {
  cd deepin-desktop-schemas-$pkgver
  make
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="${pkgdir}" install
}
