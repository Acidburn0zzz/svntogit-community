# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=4.0.1
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(python-pbr python-sphinx)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-click)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('4f01cd45ba62f92ed4975bfa61b5795fcb82e6804551cd64d80f428b3e61268429d9c70953eb67fc53d87b807b736458df17691100ccc0c2b3869abd440987f6')
b2sums=('acfcb4d4f0503f45c9c92f54b8112619b240dce880d468f9a97c71ea855c07a781c8f0044e06241252404c119d02836689ee120ae60992f60409347e01bd8cec')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 docs/*.rst -t "$pkgdir/usr/share/doc/$pkgname/docs/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
