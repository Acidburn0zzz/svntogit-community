# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=vint
pkgver=0.3.19
pkgrel=4
pkgdesc='Lint tool for Vim script Language'
arch=('any')
url=https://github.com/Kuniwak/vint
license=('MIT')
depends=('python-ansicolor' 'python-chardet' 'python-setuptools' 'python-yaml')
checkdepends=('python-pytest')
conflicts=('python-vint')
replaces=('python-vint')
source=("$url/archive/v$pkgver/vint-v$pkgver.tar.gz")
sha512sums=('b9024ed1cd8a18a916798e691ba0db31985034b2420e33c094459a48254c7c8f6fe89a1a64858a67ff95b0b48e67a780cf975940646af2685d2b95640e8121f9')

build() {
  cd vint-$pkgver
  python setup.py build
}

check() {
  cd vint-$pkgver
  pytest
}

package() {
  cd vint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
