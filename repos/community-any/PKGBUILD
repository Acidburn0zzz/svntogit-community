# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=python-xlsxwriter
pkgver=1.4.0
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jmcnamara/XlsxWriter/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('56666d2aecd88616886becf4e933268f3ac974286b2f0918ae6e7fd4b4bd2869')

build() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
