# Maintainer: Morten Linderud <foxboron@archlinux.org> 
# Contributor: Alad Wenter <https://wiki.archlinux.org/index.php/Special:EmailUser/Alad>

pkgbase=font-awesome
pkgname=(ttf-font-awesome otf-font-awesome)
pkgver=4.7.0
pkgrel=5
pkgdesc="Iconic font designed for Bootstrap"
url="http://fontawesome.io/"
license=('custom:OFL')
arch=('any')
depends=('fontconfig')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FortAwesome/Font-Awesome/archive/v$pkgver.tar.gz"
	"OFL")
sha256sums=('de512ba0e1dead382bbfce372cde74b3f18971d876fffb635ee9333f0db05d43'
            'e564f06d018e7b95bc3594c96a17f1d41865af4038c375e7aa974dd69df38602')

package_ttf-font-awesome() {
  install -Dm644 OFL "$pkgdir/usr/share/licenses/$pkgname/OFL"
  cd "Font-Awesome-$pkgver/fonts"
  install -d "$pkgdir/usr/share/fonts/TTF"
  install -m644 *.ttf "$pkgdir/usr/share/fonts/TTF"
}

package_otf-font-awesome() {
  install -Dm644 OFL "$pkgdir/usr/share/licenses/$pkgname/OFL"
  cd "Font-Awesome-$pkgver/fonts"
  install -d "$pkgdir/usr/share/fonts/OTF"
  install -m644 *.otf "$pkgdir/usr/share/fonts/OTF"
}
