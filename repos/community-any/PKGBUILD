# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ekin Dursun <ekindursun@gmail.com>
# Contributor: Gregoire Seux <grego_aur@familleseux.net>

pkgname=python-astral
pkgver=2.0.2
pkgrel=1
pkgdesc='Python calculations for the position of the sun and moon'
arch=(any)
url=https://github.com/sffjunkie/astral
license=(APACHE)
depends=(
  python-pytz
  python-requests
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/sffjunkie/astral.git#tag=dc03da349c01a5cec1433f81b5a70543d8094a8a)
sha256sums=(SKIP)

pkgver() {
  cd astral

  git describe --tags
}

prepare() {
  cd astral

  git cherry-pick -n bf9e27385f45d6e52bcc5b4df201705120fe4c3a
}

build() {
  cd astral

  python setup.py build
}

package() {
  cd astral

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
