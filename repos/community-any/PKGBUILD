# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=netsurf-buildsystem
pkgver=1.8
pkgrel=1
pkgdesc='The Netsurf buildsystem'
arch=(any)
url='https://source.netsurf-browser.org/buildsystem.git'
license=(MIT)
source=("$pkgname-$pkgver.tar.bz2::https://source.netsurf-browser.org/buildsystem.git/snapshot/buildsystem-release/$pkgver.tar.bz2")
sha256sums=('86c03394273e8c4a91ad0c930fa91c31d2c6dcffe92fe95c97cffef01ef2526c')

build() {
  cd "buildsystem-release/$pkgver"
  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE=lib-shared
  make PREFIX=/usr COMPONENT_TYPE=lib-static
}

package() {
  cd "buildsystem-release/$pkgver"
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-shared
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE=lib-static
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: git.netsurf-browser.org/buildsystem.git/plain/Makefile
# vim: ts=2 sw=2 et:
