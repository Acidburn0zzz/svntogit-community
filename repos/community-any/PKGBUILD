# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruce Zhang <zttt183525594@gmail.com>

pkgname=feeluown
pkgver=3.6.1
pkgrel=1
pkgdesc="FeelUOwn Music Player"
arch=('any')
url="https://github.com/feeluown/FeelUOwn"
license=('GPL3')
depends=('python-qasync' 'python-pyqt5' 'mpv' 'python-opengl' 'python-janus' 'python-requests'
         'python-tomlkit')
makedepends=('python-setuptools' 'python-pip')
optdepends=('feeluown-local' 'feeluown-netease' 'feeluown-kuwo' 'feeluown-xiami' 'feeluown-qqmusic')
groups=('feeluown-full')
source=("https://pypi.io/packages/source/f/feeluown/feeluown-$pkgver.tar.gz"
        "feeluown.desktop")
sha512sums=('d0ae1d31ca28cbd2c865ec13d5f40d971cc7baa2118702ac9ebdba91d1311b05ad3e051f182f0b1f65a9f115dda9b3edb88e8dd89ffae9239fc792bd748b3b1b'
            '48882f7469c22e5db332663bc1aa8b398b0a10a0c929d4d7e3d7b8b91205d7d3070c5fa295cb9a14f8c352bff57978bfaad167343e0ddc51c92417eda07c8087')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  install -D -m644 "$srcdir/$pkgname-$pkgver/feeluown/feeluown.png" "$pkgdir/usr/share/icons/hicolor/512x512/apps/feeluown.png"
  install -D -m644 "$srcdir/feeluown.desktop" "$pkgdir/usr/share/applications/FeelUOwn.desktop"
}
