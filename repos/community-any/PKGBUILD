# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgname='python-cherrypy'
_pkgname="${pkgname#python-}"
pkgver=18.0.0
pkgrel=1
pkgdesc='A pythonic, object-oriented web development framework'
arch=('any')
url='https://cherrypy.org'
license=('BSD')
depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco'
  'python-more-itertools' 'python-zc.lockfile')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-six'
  'python-cheroot' 'python-portend' 'python-more-itertools' 'python-zc.lockfile')
checkdepends=('python-tox')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/v$pkgver.tar.gz")
sha512sums=('4c1ace8107a436214372b029ba89417b74c82438d281be648abae9f4aa07cae5d9187c0d2ce212125a16ca8acc09b2b26812cd3319386b4be3aa27548cdfa06b')

prepare() {
  :
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  tox
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
