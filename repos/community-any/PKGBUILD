# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_name=mutagen
pkgname=python-mutagen
pkgver=1.45.0
pkgrel=1
arch=('any')
pkgdesc="An audio metadata tag reader and writer (python library)"
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis' 'python-pyflakes'
'python-pycodestyle')
source=("https://github.com/quodlibet/${_name}/releases/download/release-$pkgver/${_name}-$pkgver.tar.gz"{,.sig})
sha512sums=('453594bee9c5fb516d627f9a58939241f5766784c39a2c9eea179e18df1d24585d36d8558e8eb424f6393e5bd99806f470cf4e645f3632bc92cf499422af7ce5'
            'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC') # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  mv -v "${_name}-$pkgver" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  pytest -v --ignore tests/quality/test_flake8.py
}

package() {
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {NEWS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
