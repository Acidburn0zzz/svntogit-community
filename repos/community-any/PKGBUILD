# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-bleach
pkgname=(python-bleach python2-bleach)
pkgver=3.0.2
pkgrel=2

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
arch=('any')
license=('Apache')
makedepends=('python'
             'python-setuptools'
             'python2'
             'python2-setuptools')
checkdepends=('python-pytest'
              'python-webencodings'
              'python2-pytest'
              'python2-webencodings')

source=(https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz)

sha256sums=('48d39675b80a75f6d1c3bdbffec791cf0bbbab665cf01e20da701c77de278718')

prepare() {
	cp -a bleach-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py build
}

check() {
	cd "$srcdir"/bleach-$pkgver
	python -m pytest

	cd "$srcdir"/bleach-$pkgver-python2
	python2 -m pytest
}

package_python-bleach() {
	depends=('python-webencodings')

	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-bleach() {
	depends=('python2-webencodings')

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
}
