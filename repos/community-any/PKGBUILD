#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2020.12
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.tar.xz.sig
)
sha512sums=(
  c2132624a7254cb5c28e5b9fdd6581f1a9914e74e2c6eb2cc6b01f5cc846dfee2d8c44c1d6e930b2590c8c6a61f9316a571c985a942d4ce11615e91380622808
  50cc7079167755d71d0d3069339870db67c4943e316a7006a1eb7b6ca7fd03368cb06e93971885335f2084f417ed6b8c2f17212f074aeebf8901d84db9dc4c64
)
md5sums=(
  af78ffa1b7e96cbac8248e50f9966028
  b4fc43a6ee03c56f05f14b2aef4f0c00
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
