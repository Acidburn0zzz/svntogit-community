# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-glanceclient
pkgname=(python-glanceclient python2-glanceclient)
pkgver=2.16.0
pkgrel=1
pkgdesc="OpenStack Image API Client Library"
arch=('any')
url="http://docs.openstack.org/developer/python-glanceclient"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-prettytable' 'python2-prettytable' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-requests' 'python2-requests' 'python-warlock'
             'python2-warlock' 'python-oslo-utils' 'python2-oslo-utils' 'python-oslo-i18n'
             'python2-oslo-i18n' 'python-wrapt' 'python2-wrapt' 'python-pyopenssl'
             'python2-pyopenssl')
checkdepends=('python-stestr' 'python2-stestr' 'python-mock' 'python2-mock' 'python-requests-mock'
              'python2-requests-mock' 'python-testscenarios' 'python2-testscenarios')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-glanceclient/archive/$pkgver.tar.gz")
sha512sums=('5e8f356a50ea710b7ea31c91b285f0cdb86932341961267552b41b15487b589794a73c11ae31138c01a35f5ef1e883413d5fb93c2d6760bbdad16fca2133d32b')

prepare() {
  cp -a python-glanceclient-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-glanceclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-glanceclient-$pkgver
  stestr run

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-glanceclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
           'python-requests' 'python-warlock' 'python-oslo-utils' 'python-oslo-i18n' 'python-wrapt'
           'python-pyopenssl')

  cd "$srcdir"/python-glanceclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-glanceclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneauth1'
           'python2-requests' 'python2-warlock' 'python2-oslo-utils' 'python2-oslo-i18n'
           'python2-wrapt' 'python2-pyopenssl')

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/glance{,2}
}

# vim:set ts=2 sw=2 et:
