# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apispec
pkgver=3.1.1
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
depends=('python')
optdepends=('python-yaml: for yaml support'
            'python-prance: for validation'
            'python-openapi-spec-validator: for validation')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-bottle' 'python-marshmallow'
              'python-flask' 'python-prance' 'python-yaml' 'python-mock'
              'python-openapi-spec-validator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('361f49eb7154bdc57a090329323454ef45ba552bb44ed0d38be5a234075682ba561d648aaeebb9a51153a2d6fa94620efefff2bfa530171662afb6358b1aa19f')

build() {
  cd apispec-$pkgver
  python setup.py build
}

check() {
  cd apispec-$pkgver
  python setup.py pytest
}

package() {
  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
