# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-news
_name=news
pkgver=15.4.2
pkgrel=1
pkgdesc="An RSS/Atom feed aggregator"
arch=('any')
url="https://github.com/nextcloud/news"
license=('AGPL3')
depends=('nextcloud>21' 'nextcloud<22')
makedepends=('ripgrep' 'yq')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/${pkgver}/${_name}.tar.gz")
sha512sums=('be837c9f50a6e07818d01538b97f8e4bef3b9602b63f04fb0eb1fe8f33eba5f0c5c9090b1914b4086062ccbfcba007edbab77a5fc41ec5bb8122d0c77eb226a4')
b2sums=('e869ba4685302db43b3909b60b5e15e327bbef44dcba9ca040b8f7f78b8b07e18c3465bcc9a8e0bdf5d85cdc8fefc36a871ad6277376a113f2f8ebfb0300c48d')

check() {
  local _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
