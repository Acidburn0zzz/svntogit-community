# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.8.2
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('f6cceb9b8b910808ba72beb8bd50475430db38778b9463b93ee4f9915eae92bed273704be0abd7e066109909117c53ef8c744fc638146707da54abb750461c44')
b2sums=('1f38b12ba5b82593cbbcea915358b99f2f1287bbdfb622f38638beadf7891bcbc56a2da3d4310fb42716a4e838b128ca0d97d8232a3f3535e33806e07e843b5d')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
