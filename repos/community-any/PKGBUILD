# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=3.0.1
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-jinja' 'python-jinja_pluralize' 'python-pluggy'
'python-pygments' 'python-setuptools' 'python-six')
checkdepends=('flake8' 'python-pytest' 'python-mock' 'python-pytest-cov'
'python-pycodestyle' 'python-pyflakes' 'python-pylint' 'python-pydocstyle')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('bb04b110a9601db8734c1dd925571c7f2905e14bc5d838338b4e02370832e807d6f7c60d1f714157ac357526bc87c96efa7796be378f53c7ef7f30f525911b08'
            'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,NOTICE,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
