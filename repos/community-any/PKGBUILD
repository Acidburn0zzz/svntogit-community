# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

pkgname=ts-node
pkgver=8.5.2
pkgrel=1
pkgdesc="TypeScript execution and REPL for node.js, with source map support"
arch=('any')
url="https://github.com/TypeStrong/ts-node"
license=('MIT')
depends=('nodejs' 'typescript')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('5b50cafdae811a857f0f8c7f4975e6e93490c7aab76e51025337794cdfa3e7a604317df23d53291ec2022de754c370ef185dda4d0f217dd47d00a31a1e3222f8')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}

# vim:set ts=2 sw=2 et:
