# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Francois Boulogne <fboulogne@april.org>
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=3.3.6
pkgrel=3
pkgdesc="The Scientific Python Development Environment"
arch=(any)
url="https://www.spyder-ide.org/"
license=(MIT)
makedepends=(python-setuptools)
depends=(
    python-cloudpickle
    python-rope
    python-jedi
    python-pyflakes
    python-pygments
    python-qtconsole
    jupyter-nbconvert
    python-sphinx
    python-pycodestyle
    python-pylint
    python-psutil
    python-qtawesome
    python-qtpy
    python-pickleshare
    python-pyzmq
    python-chardet
    python-numpydoc
    python-spyder-kernels
    python-keyring
    python-pyqt5
    python-pyqtwebengine
    qt5-webkit
)
optdepends=(
    'cython: run Cython files in the IPython Console'
    'python-matplotlib: 2D/3D plotting in the IPython Console'
    'python-numpy: support for N-dimensional arrays in the Variable Explorer'
    'python-pandas: support for DataFrames and Series in the Variable Explorer'
    'python-scipy: support for Matlab workspace in the Variable Explorer'
    'python-sympy: symbolic mathematics in the IPython Console'
)
checkdepends=(
    python-pytest
    python-pytest-qt
    python-pytest-mock
    python-pytest-cov
    python-pytest-xvfb
    python-mock
    python-flaky
    python-pandas
    python-scipy
    python-sympy
    python-pillow
    python-matplotlib
    cython
)
source=(https://github.com/spyder-ide/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        spyder3_to_spyder.patch)
sha256sums=('aa3e46e30da987a2532854d3ba30143f7876bddb3be1b0d5ced11a7ab7abea83'
            '42b51d2696f06e4ad69be7379e913fd90e5c09c3e8244b6f38ef127ae86cfe89')

prepare() {
  cd ${pkgname}-${pkgver}
  # Replace spyder3 with spyder
  patch -p1 -i ../spyder3_to_spyder.patch
  mv scripts/spyder{3,}.desktop
  mv scripts/spyder{3,}.appdata.xml
  # Allow our jedi version
  sed -i "s|JEDI_REQVER = '=0.14.1'|JEDI_REQVER = '>=0.14.1'|" -i spyder/dependencies.py
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

# Tests are segfaulting the build
#check() {
#  cd ${pkgname}-${pkgver}
#  pytest
#}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE.txt -t ${pkgdir}/usr/share/licenses/${pkgname}/
  # Install icon file
  install -Dm644 spyder/images/spyder.svg -t "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/
  # Remove useless spyder_win_post_install script
  rm -f "${pkgdir}"/usr/bin/spyder_win_post_install.py
}
