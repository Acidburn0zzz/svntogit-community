# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pprofile
pkgname=python-pprofile
pkgver=2.0.4
pkgrel=1
pkgdesc="Line-granularity, thread-aware deterministic and statistic pure-python profiler"
arch=('any')
url="https://github.com/vpelletier/pprofile"
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4154102bb2c7cdc26f900018914e35f203ac0d14d677ebbb7473bd9e7ed877a51f961ce1cd107153b2de398dca7bd9e49516b0f0a1215c94de6763d968a4c1d8')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
