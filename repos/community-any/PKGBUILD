# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-crypto
pkgver=0.0.20190325
_commit=a5d413f7728c81fb97d96a2b722368945f651e78
pkgrel=1
pkgdesc='Go supplementary cryptography libraries'
arch=('any')
url="https://github.com/golang/crypto"
license=('BSD')
depends=('go-pie' 'golang-golang-x-net' 'golang-golang-x-sys')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/crypto/archive/$_commit.tar.gz")
sha512sums=('57a9606845a27459703d3757e001303d2f67c88efa4d021d4aa09314ddcc85fe95c9db2ff4aa08639b6be49cc751d82c35134a587987a896f64bb103af89adee')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$srcdir"/build/src/golang.org/x/crypto
  cd "$srcdir"/build/src/golang.org/x/crypto
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/crypto

  install -Dm644 crypto-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
