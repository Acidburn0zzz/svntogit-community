# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.0.4
pkgrel=3
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=(any)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(python-gobject cdemu-daemon)
makedepends=(intltool cmake)
conflicts=('cdemu')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        fixes.diff)
sha256sums=('e415a98672ac1ed908dab2ba43a94bc0af7653c470a87d6d9338a59d9856fd86'
            'f918450e3eff8123b830817fe763702f123f8ad6d0186798aa2defa8f0f9782f')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np2 -i ../fixes.diff
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
