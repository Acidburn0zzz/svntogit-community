# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-bleach
pkgver=3.2.3
pkgrel=1

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
arch=('any')
license=('Apache')
depends=('python-packaging'
         'python-six'
         'python-webencodings')
makedepends=('python'
             'python-setuptools')
checkdepends=('python-pytest')

source=(https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz
        $pkgname-py39.patch::https://github.com/mozilla/bleach/commit/9ba905729f17.patch)

sha256sums=('c6ad42174219b64848e2e2cd434e44f56cd24a93a9b4f8bc52cfed55a1cd5aad'
            '5b47567a3d47c8e4464b63d5d50abc38da55f2a67d2fa2f7c65041191ea6c156')

prepare() {
	cd "$srcdir"/bleach-$pkgver
	# https://github.com/mozilla/bleach/pull/565
	patch -Np1 -i ../$pkgname-py39.patch
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build
}

# Upstream broke the test suite.
check() {
	cd "$srcdir"/bleach-$pkgver
	python -m pytest
}

package() {
	cd "$srcdir"/bleach-$pkgver
	export PYTHONHASHSEED=0
	python setup.py install --skip-build --optimize=1 --root="$pkgdir"
}
