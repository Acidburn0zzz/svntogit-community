# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" "${pkgbase/-/2-}")
pkgver=4.50
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.python.org/packages/23/4e/7d865eeb61c0d1ca5f63dd6f3d9f9a993e6a7e566ebc350c08feb18a012d/Pyro4-$pkgver.tar.gz")
sha256sums=('cb199540c2ceae9d67d5f2b20dc002d93f909d5072c3da4381c119d7a4b6d1cf')

package_python-pyro() {
  depends=('python')
  cd "Pyro4-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')
  cd "Pyro4-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
