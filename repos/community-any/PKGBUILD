# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Ding Xiao <tinocodfcdsa10@mails.tsinghua.edu.cn>
# Contributor: Firmy <firmianay@gmail.com>

pkgname="python-pwntools"
_pyname="pwntools"
pkgver="4.0.0"
pkgrel=3
pkgdesc="A CTF framework and exploit development library"
arch=('any')
url='https://github.com/Gallopsled/pwntools'
license=('MIT' 'GPL2' 'BSD')
depends=('python'
         'python-mako'
         'python-pyelftools'
         'python-capstone'
         'ropgadget'
         'python-pyserial'
         'python-requests'
         'python-pip'
         'python-pygments'
         'python-pysocks'
         'python-dateutil'
         'python-packaging'
         'python-psutil'
         'python-intervaltree'
         'python-sortedcontainers'
         'python-unicorn'
         'python-tox'
         'python-paramiko'
         'ropgadget'
         'python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/Gallopsled/pwntools/archive/${pkgver}.tar.gz")
sha256sums=('31b114914fe78929516ded8cf0bd02d91051c506d91640216b635646b3e47164')
b2sums=('117fc85c22976dbac41f326b9545c9f4141af61b6408118b74defd0bd0aa4a084b00dd64f723ad79fe0e1a1aecdcf4995bff4c6cbd3ed3d2c72c94d104666013')

build() {
  cd "${_pyname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_pyname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build --only-use-pwn-command
  install -D -m 644 LICENSE-pwntools.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -f "${pkgdir}"/usr/lib/python*/site-packages/*.{txt,md}
}

# vim:set ts=2 sw=2 et:
