# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=5.0.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=('any')
url="https://github.com/openapitools/openapi-generator/"
license=('Apache')
depends=('bash' 'java-runtime=8')
makedepends=('maven' 'java-environment=8' 'strip-nondeterminism')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('35a1178bf4bc07203d6d5dd15e409b1f175c16e99102ae747ac0733296b176acc864678cd9f44844c9b6be9c382e39b7af819bbbec6e5bdc4e71920c2bd4ee30'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('6573d85501f9438e6a6df2b8601506f2ae669f29f0c5b534040e20e3bee221a980bfd89c2407c053fd74c9a9643c4002fa94491c1bbf94b3b33f849e143a2440'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd "$pkgname-$pkgver"
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" \
    modules/openapi-generator/target/openapi-generator-${pkgver}.jar \
    modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    modules/openapi-generator-core/target/openapi-generator-core-${pkgver}.jar \
    modules/openapi-generator-maven-plugin/target/openapi-generator-maven-plugin-${pkgver}.jar \
    modules/openapi-generator-online/target/openapi-generator-online.jar

}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644  modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    -t "${pkgdir}/usr/share/java/${pkgname}/"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -svf "${pkgname}" "${pkgdir}/usr/bin/${pkgname}-cli"
  install -vDm 644 scripts/openapi-generator-cli-completion.bash \
    "${pkgdir}/usr/share/bash/bash-completion/completions/openapi-generator"
}
