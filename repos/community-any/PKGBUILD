# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-whitenoise
pkgname=('python-whitenoise' 'python2-whitenoise')
pkgver=4.1.1
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-django' 'python2-django' 'python-requests' 'python2-requests' 'python-brotli'
              'python2-brotli')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('689b781461a343334eb43d68f232e86357d8b5431acfcef59892fd5d9f98f9e69080269c4abf83724b3268d687f52edce9c3832536444ea4aa441d399c46f8b6')

prepare() {
  cp -a whitenoise-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/whitenoise-$pkgver
  python setup.py build

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 setup.py build
}

check() {
  export DJANGO_SETTINGS_MODULE=tests.django_settings
  export PYTHONWARNINGS=all

  cd "$srcdir"/whitenoise-$pkgver
  python -m unittest discover

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 -m unittest discover
}

package_python-whitenoise() {
  depends=('python')

  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-whitenoise() {
  depends=('python2')

  cd whitenoise-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
