# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-bleach
pkgname=(python-bleach python2-bleach)
pkgver=2.1.3
pkgrel=2

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
arch=('any')
license=('Apache')
makedepends=('python'
             'python-setuptools'
             'python-html5lib'
             'python2'
             'python2-setuptools'
             'python2-html5lib')
checkdepends=('python-pytest'
              'python2-pytest')

source=(https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz)

sha256sums=('eb7386f632349d10d9ce9d4a838b134d4731571851149f9cc2c05a9a837a9a44')

prepare() {
	cp -a bleach-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py build
}

check() {
	cd "$srcdir"/bleach-$pkgver
	python -m pytest

	cd "$srcdir"/bleach-$pkgver-python2
	python2 -m pytest
}

package_python-bleach() {
	depends=("python-html5lib")
	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-bleach() {
	depends=("python2-html5lib")
	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
}
