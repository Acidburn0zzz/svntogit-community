# Maintainer: Konstantin Gizdov <arch at kge dot pw>

pkgname='msgpack-cxx'
pkgver=4.0.1
pkgrel=1
pkgdesc='An efficient object serialization library for C++'
arch=("any")
url='https://msgpack.org/'
license=('custom:Boost')
makedepends=('boost' 'cmake' 'doxygen' 'graphviz')
source=("https://github.com/msgpack/msgpack-c/releases/download/cpp-${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "LICENSE-${pkgname}-${pkgver}::https://raw.githubusercontent.com/msgpack/msgpack-c/cpp_master/COPYING")
sha256sums=('acba76ebc3ff9af2fd2649f319b25cd2387c66f75ae74a7a652fe3386e45d498'
            '664550b43996452a5a2c7471bb0ff77185a58c69ccafa60f983dc4c162e1ee22')
_builddir='build-cxx'

prepare() {
  cd "${srcdir}"
  mkdir "${_builddir}"
}

build() {
  cd "${srcdir}/${_builddir}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=ON \
        -DMSGPACK_CXX17=ON \
        -DMSGPACK_BUILD_EXAMPLES=OFF \
        -DMSGPACK_BUILD_TESTS=ON \
        "${srcdir}/${pkgname}-${pkgver}"
  make
}

check() {
  cd "${srcdir}/${_builddir}"
  make test 
}

package() {
  cd "${srcdir}/${_builddir}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 "${srcdir}/LICENSE-${pkgname}-${pkgver}" "${pkgdir}/usr/share/licenses/${pkgname}"/LICENSE
}
