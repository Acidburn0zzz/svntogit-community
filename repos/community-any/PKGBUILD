# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Wes Brewer <brewerw@gmail.com>
# Contributor: VuDu <vudu.curse@gmail.com>

pkgname=nfoview
pkgver=1.27
pkgrel=2
pkgdesc="Simple NFO file viewer in GTK+ 3"
arch=('any')
url="https://otsaloma.io/nfoview/"
license=('GPL3')
depends=('gtk3' 'python-gobject')
checkdepends=('python-pytest' 'xorg-server-xvfb')
makedepends=('python-setuptools')
optdepends=('terminus-font: recommended font')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/otsaloma/${pkgname}/archive/${pkgver}.tar.gz"
        'x-nfo.desktop')
sha512sums=('157257f9f289bdeac140054320ad43893c73995e11e6ccc91ebd2ca53878f00d45945c4e3ac3c11ec2f589a0349598f96dfecad3b0e3cfbe06e5a036bcedc3b5'
            'a800af55df103ee12ce7a8815780e15896d7ad3eee947760ff4486ffb57fea8cf4105b5ffc61dc81e849ddf03eeac77f593a4ffe4fd73b5ea026672c72dff108')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run py.test -vvv "${pkgname}"
}

package() {
  cd "${pkgname}-${pkgver}"

  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}/"
  # docs
  install -vDm 644 {AUTHORS,NEWS,README,TODO}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"

  install -vDm 644 "${srcdir}/x-nfo.desktop" \
    -t "${pkgdir}/usr/share/mimelnk/text/"
}

# vim:set ts=2 sw=2 et:
