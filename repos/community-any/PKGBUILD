# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-digitalocean
pkgver=0.28.0
pkgrel=1
pkgdesc="DigitalOcean DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-digitalocean' 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('a761206dac8e23875596a8db01e1feb35b932e75c9f2875a40d5f6e32600d51bf488d1a947d9840b292dd52df99547bcbd5d7c7b6b43bc97b6e0823eec75b052'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
