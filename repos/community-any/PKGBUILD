# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgbase=python-watchdog
pkgname=('python-watchdog' 'python2-watchdog')
pkgver=0.10.1
pkgrel=1
pkgdesc="Python API and shell utilities to monitor file system events."
arch=('any')
url="https://pypi.python.org/pypi/watchdog"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pathtools' 'python2-pathtools'
             'python-yaml' 'python2-yaml' 'python-argh' 'python2-argh')
checkdepends=('python-pytest-cov' 'python2-pytest-cov' 'python-pytest-timeout'
              'python2-pytest-timeout' 'python-pytest-runner' 'python2-pytest-runner')
source=($pkgbase-$pkgver.tar.gz::https://github.com/gorakhargosh/watchdog/archive/v$pkgver.tar.gz)
sha512sums=('4ae8ca4a28a6b5c3e86d47d8d63e133341b5c229414af36b6ee556bd82c003a33a21cf0c41639e42cb291e4ac24d77f6d7829c8ed6fdc9ed26235de746e54256')

build() {
  cd watchdog-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd watchdog-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-watchdog() {
  depends=('python-pathtools' 'python-yaml' 'python-argh')

  cd watchdog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-watchdog() {
  depends=('python2-pathtools' 'python2-yaml' 'python2-argh')

  cd watchdog-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/watchmedo{,2}
}

# vim:ts=2:sw=2:et:
