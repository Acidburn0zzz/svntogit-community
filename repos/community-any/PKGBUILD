# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-kr-pretty
pkgver=0.2.0
pkgrel=2
pkgdesc='Pretty printing for Go values'
arch=('any')
url="https://github.com/kr/pretty"
license=('MIT')
depends=('go' 'golang-github-kr-text')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kr/pretty/archive/v$pkgver.tar.gz")
sha512sums=('2788a36217b96d4161dcfee34e691a6a3dbde47c42bdffc3f984dce8da5d5b150457e22819667bb8ad9fe46bcd2c166700c2d56435fe103abf4f6b46228bf37e')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/kr
  cp -a "$srcdir"/pretty-$pkgver "$srcdir"/build/src/github.com/kr/pretty
  cd "$srcdir"/build/src/github.com/kr/pretty
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/kr
  cp -a "$srcdir"/pretty-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/kr/pretty

  install -Dm644 pretty-$pkgver/License "$pkgdir"/usr/share/licenses/$pkgname/License
}
