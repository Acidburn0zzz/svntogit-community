# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

_name=smmap
pkgname=python-smmap
pkgver=3.0.5
pkgrel=1
epoch=1
pkgdesc="A pure git implementation of a sliding window memory map manager"
arch=('any')
url="https://github.com/gitpython-developers/smmap"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://pypi.org/packages/source/s/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('82d0167ba439164b5b9e6ca6081558b7f32af364f7935aa40500b34ca80e0fcf8d014e53eb2963e189d28ef18c8dd48da0b515392d149608fc3179fae3f69ee1'
            'SKIP')
b2sums=('ccc36c33e9239f69a41eff291dcde7e455a84d886c2b087464b1b084ad8506b9d0f862b083635d51465089564edbd27ed0b7c9c5a0d1e58605b3db6f84dea857'
        'SKIP')
validpgpkeys=('27C50E7F590947D7273A741E85194C08421980C9') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
