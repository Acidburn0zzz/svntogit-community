# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=0.4.1
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-setuptools' 'python-termcolor' 'python-colorama')
makedepends=('python-pypandoc' 'python-setuptools-scm')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tldr-pages/tldr-python-client/archive/$pkgver.tar.gz")
sha512sums=('5b6163595c2d0a4107bdd467ffbe95fd8e4f97abbcb5484601454c575a944a3dc9e3a1212233cde276fc573a5344fb96a02fd979ee7bc3066a3231bd1435d037')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python setup.py pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
