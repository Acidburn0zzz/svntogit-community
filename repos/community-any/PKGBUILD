# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perlbrew
pkgver=0.61
pkgrel=1
pkgdesc='Manage perl installations in your $HOME'
arch=(any)
license=('MIT')
options=(!emptydirs)
depends=('perl>=5.8.0' 'perl-capture-tiny>=0.18'
         'perl-cpan-perl-releases>=0.58' 'perl-devel-patchperl>=0.70'
         'perl-file-path-tiny>=0.3' 'perl-local-lib>=1.008')
makedepends=('perl-extutils-makemaker>=5.59')
checkdepends=(perl-test-exception perl-test-output perl-test-spec 
              perl-path-class perl-io-all)
provides=("perl-app-perlbrew=$pkgver")
url="https://metacpan.org/release/App-perlbrew"
source=("http://search.cpan.org/CPAN/authors/id/G/GU/GUGOD/App-perlbrew-$pkgver.tar.gz")
md5sums=('364359c35f4d36ce7740fd14ab2af960')

build()
(
  cd App-perlbrew-$pkgver
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd App-perlbrew-$pkgver
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd App-perlbrew-$pkgver
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  install -D -m 644 LICENSE "$pkgdir"/usr/share/licenses/perlbrew/LICENSE
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
