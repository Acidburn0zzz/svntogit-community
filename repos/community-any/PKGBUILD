# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.10.2
pkgrel=1
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-pydantic' 'python-typeguard')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('5b50a1931613f36af446b16c644a85b52ceb2beceb09bddd05f9ce054f13e9ab')
b2sums=('bc3855f9f9e2ad3e31b35ce6e994dca73269bd5bf9aa358be038aa6334f2e0c8bc8e205d07688e39421b41e4e2fdecb2e0c2425e1b87b46701be26a5cd2c54e7')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
