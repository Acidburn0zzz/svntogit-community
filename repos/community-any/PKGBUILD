# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=roundcubemail
pkgver=1.4.1
pkgrel=2
pkgdesc="A PHP web-based mail client"
arch=('any')
url="https://roundcube.net/"
license=('GPL')
depends=('php' 'php-gd')
options=('emptydirs')
makedepends=('php-composer' 'git')
optdepends=('python: password change script')
backup=('etc/webapps/roundcubemail/.htaccess'
        'etc/webapps/roundcubemail/apache.conf')
validpgpkeys=('F3E4C04BB3DB5D4215C45F7F5AB2BAA141C4F7D5')
source=("https://github.com/roundcube/roundcubemail/releases/download/$pkgver/roundcubemail-$pkgver-complete.tar.gz"{,.asc}
        "apache.conf" 0001-plugins-password-make-Python-3-compatible.patch)
sha256sums=('640423d21e1aa733ddea7462f156e194b051f086a3f2a70430e0aa2c9b09b44a'
            'SKIP'
            'c90981405527ebaf153a407af6b8178b41d078bd4472d63b837b3b4cd5ae36b0'
            '1b30d0cc316decbdd668f47a39ed9d9a029bdeeb88e364a2002a1476d1d48cac')

prepare() {
  cd "$srcdir"/roundcubemail-${pkgver}
  sed -i \
    -e "s|RCUBE_INSTALL_PATH . 'temp.*|'/var/cache/roundcubemail';|" \
    -e "s|RCUBE_INSTALL_PATH . 'logs.*|'/var/log/roundcubemail';|" \
    config/defaults.inc.php \
    program/lib/Roundcube/rcube_config.php

    patch -Np1 -i ${srcdir}/0001-plugins-password-make-Python-3-compatible.patch
}

package() {
  mkdir -p "$pkgdir"/etc/webapps/roundcubemail
  mkdir -p "$pkgdir"/usr/share/webapps
  cd "$pkgdir"/usr/share/webapps
  cp -ra "$srcdir"/roundcubemail-${pkgver} roundcubemail
  cd roundcubemail

  mv .htaccess "$pkgdir"/etc/webapps/roundcubemail/
  ln -s /etc/webapps/roundcubemail/.htaccess .htaccess

  mv config "$pkgdir"/etc/webapps/roundcubemail/
  ln -s /etc/webapps/roundcubemail/config config

  install -Dm0644 "$srcdir"/apache.conf "$pkgdir"/etc/webapps/roundcubemail/apache.conf
  chown http:http "$pkgdir"/etc/webapps/roundcubemail/config/config.inc.php.sample
  chmod 0640 "$pkgdir"/etc/webapps/roundcubemail/config/config.inc.php.sample

  install -dm0750 -o http -g http "$pkgdir"/var/cache/roundcubemail
  install -dm0750 -o http -g http "$pkgdir"/var/log/roundcubemail

  rm -rf temp logs
}
