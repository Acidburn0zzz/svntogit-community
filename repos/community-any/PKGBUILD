# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-semver
pkgver=2.10.0
pkgrel=1
pkgdesc="Python helper for Semantic Versioning"
url="https://github.com/k-bx/python-semver"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/k-bx/python-semver/archive/$pkgver.tar.gz")
sha512sums=('84f34d78ae8ea880857c0380a29e64728deffb22216228ed79b17bd065529eb5b69667599be90faee5b851f1a909412f848dba7625a40696c7db8de1d067c24a')

prepare() {
  sed -i '/tests_require/d' python-semver-$pkgver/setup.py
}

build() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py pytest
}

package() {
  cd python-semver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
