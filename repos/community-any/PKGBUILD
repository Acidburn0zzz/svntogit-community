# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Kessia 'even' Pinheiro <kessiapinheiro at gmail.com>
# Contributor: Sorin Ionescu <sorin.ionescu@gmail.com>

pkgname=ufw
pkgver=0.30.1
pkgrel=3
pkgdesc="Uncomplicated and easy to use CLI tool for managing a netfilter firewall"
arch=('any')
url="https://launchpad.net/ufw"
license=('GPL3')
depends=('iptables' 'python2')
backup=('etc/ufw/before.rules'
        'etc/ufw/before6.rules'
        'etc/ufw/after.rules'
        'etc/ufw/after6.rules'
        'etc/ufw/ufw.conf'
        'etc/ufw/user.rules'
        'etc/ufw/sysctl.conf'
        'etc/default/ufw'
        'usr/lib/ufw/user.rules'
        'usr/lib/ufw/user6.rules')
source=(http://launchpad.net/${pkgname}/0.30/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz \
        $pkgname.rc)
md5sums=('3182fad2249cf5f7e5589f44f0f078bd'
         'e15e82645bcb61e85ecc6e6c7c2fc46e')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  # FS#28769 - move from /lib to /usr/lib
  sed -e 's|/lib|/usr/lib|' -i setup.py

  python2 setup.py install --root=${pkgdir}

  install -Dm755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname

  install -D -m755 ${pkgdir}/usr/sbin/$pkgname ${pkgdir}/usr/bin/$pkgname
  rm -rf ${pkgdir}/usr/sbin
  chmod 644 ${pkgdir}/etc/ufw/*.rules ${pkgdir}/usr/lib/ufw/*.rules
}
