# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-novaclient
pkgver=15.1.0
pkgrel=1
pkgdesc="Client library for OpenStack Compute API"
arch=('any')
url="https://docs.openstack.org/python-novaclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneauth1'
         'python-requests' 'python-iso8601' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-novaclient/archive/$pkgver.tar.gz")
sha512sums=('4ba658349b53648b35127d85cbcfdd34fbb008fa3c689a29686ba6387c3d7ee81541a245fccdd8735cbd07fff630d465d288ebe9e62894e5357c131509455530')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-novaclient-$pkgver/requirements.txt
}

build() {
  cd python-novaclient-$pkgver
  python setup.py build
}

check() {
  cd python-novaclient-$pkgver
  stestr run || warning "Tests failed"
}

package() {
  cd "$srcdir"/python-novaclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
