# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=grunt-cli
pkgver=1.3.1
pkgrel=1
pkgdesc="The grunt command line interface."
arch=('any')
url="http://gruntjs.com/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=('nodejs-grunt-cli')
conflicts=('nodejs-grunt-cli')
replaces=('nodejs-grunt-cli')
install=${pkgname}.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('530051bbf4290230dce770d12c49328a515d2f4cde9e9c6ef9f75d4c896c17f289a9d35c1da426bf2bb55b77037ac67daeaa9974a2b903cf900c8c8b5045a865')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE-MIT" "$pkgdir/usr/share/licenses/$pkgname/"
}
