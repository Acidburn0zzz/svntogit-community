# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgname=python-icalendar
pkgver=4.0.5
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('BSD')
depends=('python-dateutil' 'python-pytz')
makedepends=('python-setuptools')
optdepends=('python-setuptools: for icalendar')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('48f6ecc24ee154f25982e54c1c0ef75146bc27ae90e3743d46407c730124a1b0fb4be8123e1f779d46aaf4297642f0f053794f5cc354816d9926dcf2a2b76912')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python -m unittest discover src
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
