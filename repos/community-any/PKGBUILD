# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000050
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
# Specify version bounds to pick up the one in [community], since perl includes an old one.
depends=('perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('4de0988cb44846b39c569a6ccc2c3414adb6ee714286d63af61982fa43c0423b2726c749d6eb87711cb7c6ac768bd93889cb8566640d623bd41e6b7fe376c623')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
