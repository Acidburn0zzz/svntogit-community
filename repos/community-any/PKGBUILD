# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-freezegun
pkgver=0.3.15
pkgrel=6
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
depends=('python-dateutil')
makedepends=('python-setuptools' 'python-dateutil')
checkdepends=('python-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/spulec/freezegun/archive/$pkgver.tar.gz")
sha512sums=('7b89fa2ba56cbcf274e33eea868d8f3653b0f2c47e91b1df9f72b65a0f1cb04f6364a00d9666afbc629707c5eaa238f36e9ef36b79dacd74d7fd00faea4a1a29')

build() {
  cd "$srcdir"/freezegun-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/freezegun-$pkgver
  # https://github.com/spulec/freezegun/issues/250
  pytest || echo "Tests failed"
}

package() {
  cd freezegun-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
