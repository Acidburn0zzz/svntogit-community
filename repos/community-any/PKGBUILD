# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-base
pkgver=2019.01.28
pkgrel=1
epoch=2
pkgdesc='Base component for Deepin'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-base"
license=('GPL3')
depends=('deepin-wallpapers')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-base/archive/$pkgver.tar.gz")
sha512sums=('bad31184dd7e7feaa52874960b6b3dc9bdef8f8e9722c1f8cbbf9adb52309b6748e34910dfd97c948d2070c5b885a35de1a336703d664f5ca29f0dfa45d65e6b')

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Remove Deepin distro's lsb-release
  rm "$pkgdir"/etc/lsb-release

  # Don't override systemd timeouts
  rm -r "$pkgdir"/etc/systemd

  # Make a symlink for deepin-version
  ln -s ../usr/lib/deepin/desktop-version "$pkgdir"/etc/deepin-version

  # Remove apt-specific templates
  rm -r "$pkgdir"/usr/share/python-apt
}
