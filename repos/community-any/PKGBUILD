# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>

pkgbase=python-pyserial
pkgname=('python-pyserial' 'python2-pyserial')
pkgver=3.0.1
_commit=7bb26e45f8889f0b52b4103dbc7e9be734fb2996
pkgrel=2
pkgdesc="Multiplatform Serial Port Module for Python"
arch=('any')
url="http://pyserial.sf.net"
license=('custom:PYTHON')
makedepends=('python2' 'python')
#source=(http://pypi.python.org/packages/source/p/pyserial/pyserial-$pkgver.tar.gz)
source=("https://github.com/pyserial/pyserial/archive/${_commit}.tar.gz")
md5sums=('28e25ca398ad707fba71db24c9719d56')

build() {
  cp -a $srcdir/pyserial-${_commit} $srcdir/py2serial-${_commit}
}

package_python-pyserial() {
  depends=('python')

  cd $srcdir/pyserial-${_commit}
  python setup.py install --root=$pkgdir
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-pyserial() {
  depends=('python2')

  cd $srcdir/py2serial-${_commit}
  python2 setup.py install --root=$pkgdir
  install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/LICENSE.txt
  mv $pkgdir/usr/bin/miniterm.py $pkgdir/usr/bin/miniterm2.py
}
