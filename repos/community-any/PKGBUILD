# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-httpretty
pkgver=0.9.7
_commit=c9d0ab7ddeb8f15769e358d4549e4f8c9053fb56
pkgrel=2
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
depends=('python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-mock' 'python-sure' 'python-tornado'
              'python-requests' 'python-httplib2' 'python-nose-randomly' 'python-rednose')
source=("$pkgname-$_commit.tar.gz::https://github.com/gabrielfalcao/HTTPretty/archive/$_commit.tar.gz")
sha512sums=('6a20c7c78b402ed2c38719dc94490495852d83284fba101a6f7448940afcd62035a90ec9955b6abcc711e2791331404bdf6e1e2f7979186c6513a74fd9273de3')

prepare() {
  mv HTTPretty-{$_commit,$pkgver}
}

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests
}

package_python-httpretty() {
  cd HTTPretty-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
