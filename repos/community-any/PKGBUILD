# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=3.10.4
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn python2)
source=("https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz")
sha256sums=('60db36c6f0f1f9ba104ad1107c9ef3efdbd740d159f74eb6dd52f06eb29b9bd5')

build() {
	cd "$pkgname-$pkgver"
	yarn install --pure-lockfile
	yarn build
}

check() {
	cd "$pkgname-$pkgver"
	yarn test
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r . "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
