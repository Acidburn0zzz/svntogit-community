# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='toml'
pkgname="ruby-${_gemname}"
pkgver=0.2.0
pkgrel=1
pkgdesc='Parser construction library with great error reporting in Ruby'
arch=('any')
url="https://github.com/jm/${_gemname}"
license=('MIT')
makedepends=('ruby-rake' 'ruby-rdoc' 'ruby-bundler')
checkdepends=('ruby-multi_json' 'ruby-minitest')
depends=('ruby' 'ruby-parslet')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz" 'disable_simplecov.patch' 'toml.gemspec.patch')
options=("!emptydirs")
sha512sums=('6587501de81dfe76d0516755ecb7c581d915fffeac00e8bc0cbcab71c2e49f557285a7ee596f3277b4959e19b84f76624caa248d3dd9d8221a9163bf4d87353f'
            'c363263059cad44c3faa10553f1d8760c6f3cc072efa1f1cbdcf0a715da4173e9fac98dabdf72144d796c2ebc1496554438096018a1e41c088f8e513e0d8efdb'
            '7caad4d0f0672cb2f7d25d506f0bde3b7ff7ad94f9196017dd86c67676cf81e91c88613eaf4f3b862b4ccf90fe1799ebecdc728d0dd9dc6e3a25661a7693f1b1')

prepare() {
  cd "${_gemname}-${pkgver}"
  sed --in-place '/simplecov/d' Gemfile
  patch --forward --verbose --strip=1 --input='../disable_simplecov.patch'
  patch --forward --verbose --strip=1 --input='../toml.gemspec.patch'
  # fix broken test
  # that's fixed on master
  sed --in-place 's/assert_equal original_date.to_time.to_s, parsed_date.to_time.to_s/assert_equal original_date.to_time.utc.to_s, parsed_date.to_time.utc.to_s/' test/test_generator.rb
}

build() {
  cd "${_gemname}-${pkgver}"
  gem build "${_gemname}.gemspec"
}

check() {
  cd "${_gemname}-${pkgver}"
  rake test
}

package() {
  cd "${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
