# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=prettier
pkgver=1.19.1
pkgrel=1
pkgdesc="An opinionated code formatter for JS, JSON, CSS, YAML and much more"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("yarn")
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('5fe18a6f7933479531e937858f83a4edd08e7478c3edf711c8438987ebec5c7e')


build() {
	cd "$pkgname-$pkgver"
	yarn
	yarn build
}


package() {
	install -d "$pkgdir/usr/lib/$pkgname"
	cd "$pkgdir/usr/lib/$pkgname"
	cp -a "$srcdir/$pkgname-$pkgver/dist/"* .

	# https://github.com/prettier/prettier/issues/6900
	chmod +x "bin-$pkgname.js"

	install -d "$pkgdir/usr/bin"
	ln -s "/usr/lib/$pkgname/bin-$pkgname.js" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/$pkgname-$pkgver/LICENSE"
}
