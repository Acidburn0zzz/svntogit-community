# Maintainer: David Runge <dave@sleepmap.de>
_name=libtmux
pkgbase=python-libtmux
pkgname=('python-libtmux' 'python2-libtmux')
pkgver=0.8.1
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'tmux')
checkdepends=('python-pytest' 'python2-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
#source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a26410aa6840325f2572e430427a1805c7086a19ea49ff7794a6542e8c2805b15d306f1a01adb3bea23b4c99980c4c40b0de431dde16bd7f35491011314df13e')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python -m pytest
  python2 -m pytest
}

package_python-libtmux() {
  depends=('python' 'tmux')
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm 644 {CHANGES,README.rst,TODO}
}

package_python2-libtmux() {
  depends=('python2' 'tmux')
  cd "${_name}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm 644 {CHANGES,README.rst,TODO}
}

# vim:set ts=2 sw=2 et:
