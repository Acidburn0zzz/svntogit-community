# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pdfminer
pkgname=python-${_pkg}
pkgver=20200124
pkgrel=1
pkgdesc="Python PDF Parser"
arch=(any)
url="https://github.com/pdfminer/pdfminer.six"
license=(MIT)
depends=(python-chardet python-pycryptodome python-sortedcontainers)
makedepends=(python-setuptools cython)
source=(${url}/releases/download/${pkgver}/${_pkg}.six-${pkgver}.tar.gz)
sha256sums=('9f34f8f61cd72ae23ef572e0ea1c93cd2b1e4a6d1137d14ed23763b5b2094e13')

build() {
  cd ${_pkg}.six-${pkgver}
  python setup.py build
}

package() {
  cd ${_pkg}.six-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  # Fix ownership
  chmod a+r "${pkgdir}"$(python -c "import site; print(site.getsitepackages()[0])")/pdfminer/cmap/*
}
