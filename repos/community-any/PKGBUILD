# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgname=python-paramiko
pkgver=1.7.6
pkgrel=3
pkgdesc="Python module that implements the SSH2 protocol"
url="http://www.lag.net/paramiko/"
license=('LGPL')
arch=('any')
depends=(python2 'pycrypto')
replaces=('paramiko')
conflicts=('paramiko')
provides=('paramiko')
source=(http://pypi.python.org/packages/source/p/paramiko/paramiko-$pkgver.zip)

build() {
  cd "$srcdir/paramiko-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
}

package() {
  cd "$srcdir/paramiko-$pkgver"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py

  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

md5sums=('b1cfe0cd55772115f808a11c1baba8a0')
