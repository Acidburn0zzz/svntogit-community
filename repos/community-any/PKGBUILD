# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=firefox-adblock-plus
pkgver=3.6
pkgrel=1
_file=3369259
pkgdesc="Extension for firefox which block ads and banners"
arch=('any')
url="https://adblockplus.org/"
license=('GPL3')
groups=('firefox-addons')
# https://addons.mozilla.org/en-US/firefox/addon/adblock-plus/versions/
source=("https://addons.mozilla.org/firefox/downloads/file/${_file}/adblock-plus-${pkgver}.xpi")
noextract=("adblock-plus-$pkgver.xpi")
sha256sums=('db060b73d352a2f701a1afb72cb0219eeb899b3fbcdfb5304c46dd1534eeddf9')
b2sums=('a36ec161a87995edb6f333ece090490cc60cf5137b0375ddbb7b44cd7fa911141c4eb4e9b42089b7e3c21c0c915a966b1aa4b570173dcc607bb86fae9424e91c')

package() {
  depends=("firefox")

  _extension_id="{d10d0bf8-f5b5-c8b4-a8b2-2b9879e08c5d}"
  _extension_dest="${pkgdir}/usr/lib/firefox/browser/extensions/${_extension_id}"
  install -Dm644 "${srcdir}"/adblock-plus-${pkgver}.xpi "${_extension_dest}.xpi"
}
