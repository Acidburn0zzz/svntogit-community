# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ekin Dursun <ekindursun@gmail.com>
# Contributor: Gregoire Seux <grego_aur@familleseux.net>

pkgname=python-astral
pkgver=2.1
pkgrel=1
pkgdesc='Python calculations for the position of the sun and moon'
arch=(any)
url=https://github.com/sffjunkie/astral
license=(APACHE)
depends=(
  python-pytz
  python-requests
)
makedepends=(
  git
  python-setuptools
)
source=(git+https://github.com/sffjunkie/astral.git#tag=82471cfadd194e7169cb97e89d51fa421754c923)
sha256sums=(SKIP)

pkgver() {
  cd astral

  git describe --tags
}

build() {
  cd astral

  python setup.py build
}

package() {
  cd astral

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
