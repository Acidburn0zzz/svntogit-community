# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=2.5.2
pkgrel=1
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz")
sha512sums=('0a1deb50b038a35aaee5b2df5d76ae496562057c33c871f6529539f9b88d54cdb24fdc807a5fd4fcbd00987a7a8008fd249ff8d82a3b06a945b66c0a6a251d11')

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
