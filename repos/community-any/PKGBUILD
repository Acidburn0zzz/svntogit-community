# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-praw
pkgver=7.1.2
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
depends=('python-prawcore' 'python-update-checker' 'python-websocket-client')
makedepends=('python-setuptools' 'python-pytest-runner')
checkdepends=('python-mock' 'python-betamax-serializers' 'python-betamax-matchers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('8d2a343b17cc86070ad02eae5846084aeb3ae34dda69204f1e5ec9b2dad7c751e89c07d53fb4bfc06b66c145dd06e7885200f7224311b5939d3cc37b3e192136')

build() {
  cd praw-$pkgver
  python setup.py build
}

check() {
  cd praw-$pkgver
  python setup.py pytest
}

package() {
  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
