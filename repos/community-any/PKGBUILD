# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-appconf
pkgname=python-django-appconf
pkgver=1.0.3
pkgrel=1
pkgdesc="An app to handle configuration defaults of packaged Django apps gracefully"
arch=('any')
url="https://github.com/django-compressor/django-appconf"
license=('BSD')
depends=('python-django' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-pytest')
# pypi sdist has no tests:
# https://github.com/django-compressor/django-appconf/issues/54
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/django-compressor/${_name}/archive/${pkgver}.tar.gz"
        "${pkgname}-1.0.3-fix_test_config.patch::https://github.com/django-compressor/django-appconf/pull/47.patch")
sha512sums=('5a93cd5762c188710db427e280f6664c905e1ec0b939440747dfa8a0493164cc969feb77a46859836436f50ae6bff286b45bf7375897e7531090ca2727f72220'
            '8e889daa8d0f848db62960b30e8faaf887ad2ff30aa1b0fbed8997848dfe53a0fd077392fea70c1dc2c1f63052449a53bbda58bcfa7ee1bb98548a44039c56d8')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # fixing test config for python-django > 2.2:
  # https://github.com/django-compressor/django-appconf/issues/56
  patch -Np1 -i "../${pkgname}-1.0.3-fix_test_config.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}:build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=tests.test_settings coverage run /usr/bin/django-admin.py test -v2 tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
