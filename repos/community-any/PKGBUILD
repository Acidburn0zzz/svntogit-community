# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.4.8
pkgrel=1
pkgdesc="File identification library for Python"
arch=(any)
url="https://github.com/pre-commit/identify"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-ukkonen python-pytest)
optdepends=('python-editdistance-s: for returning license identifiers')
optdepends=('python-ukkonen: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/pre-commit/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('5e6be3bd3debb295621bfa666f00f293cfda7dbe744b77e6c792cf1e44f0ba9134ecbef9faac4d3b8c93655e7f2f5bd3556281e077798a830d90368917483a50')
b2sums=('2f7d74799f624147427db96b6271e41a69f776ca4263571d4bfd7d43698694ed05dd0ff2c5176073da3af2a0582a540a473679508e239efa134411e0b3abc6e0')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
