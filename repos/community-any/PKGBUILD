# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-python-socks
pkgver=2.0.0
pkgrel=1
pkgdesc='Core proxy client (SOCKS4, SOCKS5, HTTP) functionality for Python'
arch=('any')
url='https://github.com/romis2012/python-socks'
license=('APACHE')
depends=('python') 
makedepends=('python-setuptools')
checkdepends=('python-async-timeout' 'python-curio' 'python-flask' 'python-pytest'
              'python-pytest-asyncio' 'python-pytest-trio' 'python-trio' 'python-yarl')
optdepends=('python-async-timeout: asyncio support'
            'python-curio: curio support'
            'python-trio: trio support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('088a606ca6e13de79cfa8b180d76e91cca129b3e1ffad2243e5924dc10530340924ea815eb9af0e53b5369daa9cf3e66b2f623da6913ed32916b91d24a47c1e4')

build() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py build
}

check() {
	cd "${pkgname#python-}-$pkgver"
	pytest
}

package() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
