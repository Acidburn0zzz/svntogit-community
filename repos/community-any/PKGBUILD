# Maintainer: David Runge <dave@sleepmap.de>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.42.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=('any')
url="https://github.com/pennersr/django-allauth"
license=('MIT')
depends=('python-django' 'python-openid' 'python-requests'
'python-requests-oauthlib')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest')
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('db12bdd770bf2aac089a9437c9ae4f89d73c9469dd164ea44b0b50a71010274b15d447836b8aec99bdad7eb12767728c68679feb82738410eabc50ce5a71a7ef')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  coverage run manage.py test allauth
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
