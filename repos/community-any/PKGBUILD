# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=8.0.8
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('baa608059006f2e20fed517120bb62a57f673e8308779471d810c8088c36e80370985d1ed8af9cd6ef3bdf82bbc55ae1d4c5199367fdc2a163e5c7210082b632')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
