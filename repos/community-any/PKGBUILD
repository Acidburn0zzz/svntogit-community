# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2.23.0
pkgver=2.23.0
_upstreamver=${_pkgver%[[:lower:]]}
_patchver=$_upstreamver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with vaultwarden'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-fermium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'bfd08114bd701366189d626b5c768affc45bde84dc6f0aa0f86ffa866ab4b9c79f896ae1f9c25b4d42fbfb709577ed10e5b9d43f7ed25aaa5b13d3f832c07d25')

pkgver() {
  echo $_pkgver | sed 's/[[:lower:]]$/.&/'
}

prepare() {
  cd bitwarden-web
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch
  npm ci
}

build() {
  cd bitwarden-web
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
