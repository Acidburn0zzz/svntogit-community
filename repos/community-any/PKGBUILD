# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgbase=python-pint
pkgname=('python-pint' 'python2-pint')
pkgver=0.9
pkgrel=1
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="http://pint.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-numpy' 'python2-numpy' 'python-uncertainties' 'python2-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('d79e0ff6377393f9dd8ce6a938440e8ed3c947642716974a3d69461d75776a39677d6651e6c081076f31e67a7dbffeb2fded42e61c9975f814b5eb24ab626fb6')

prepare() {
  cp -a Pint-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/Pint-$pkgver
  python setup.py test || warning "https://github.com/hgrecco/pint/issues/577"

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py test || warning "https://github.com/hgrecco/pint/issues/577"
}

package_python-pint() {
  depends=('python')

  cd "$srcdir"/Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pint() {
  depends=('python2')

  cd "$srcdir"/Pint-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
