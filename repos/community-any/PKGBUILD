# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Marcel Campello <tucho@prafrentex.com.br>

pkgname=leiningen
pkgver=2.9.1
pkgrel=1
pkgdesc='Automate Clojure projects without setting your hair on fire'
arch=(any)
url='https://github.com/technomancy/leiningen'
license=(EPL)
depends=(bash java-environment)
makedepends=(git)
optdepends=('rlwrap: for readline support'
            'leiningen-completions: completion scripts')
source=("$url/releases/download/$pkgver/leiningen-$pkgver-standalone.zip"{,.asc}
        "git+$url#commit=f3864bc35d8280a8461b7f6c593b1919a75bef7f") # tag: 2.9.1
noextract=("leiningen-$pkgver-standalone.zip")
validpgpkeys=('2B72BF956E23DE5E830D50F6002AF007D1A7CC18') # Phil Hagelberg <phil@hagelb.org>
sha256sums=('ea7c831a4f5c38b6fc3926c6ad32d1d4b9b91bf830a715ecff5a70a18bda55f8'
            'SKIP'
            'SKIP')

package() {
  install -Dm755 $pkgname/bin/lein-pkg "$pkgdir/usr/bin/lein"
  install -Dm644 $pkgname/doc/lein.1 "$pkgdir/usr/share/man/man1/lein.1"
  install -Dm644 "$srcdir/leiningen-$pkgver-standalone.zip" \
                 "$pkgdir/usr/share/java/leiningen-$pkgver-standalone.jar"
}

# vim: ts=2 sw=2 et:
