# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.10.6
pkgrel=3
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'flake8' 'python-pytest-localserver')
source=("$pkgname-$pkgver.tar.gz::https://github.com/getsentry/responses/archive/$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('7fee4150de9b477ca2fb01e71ee9dffae0e3e2138502af54c39bb9291d950d5feb6a6f8db3e37a8a58b6561d625ae8e42a90921463ecb0c5a0f9213466047334'
            '828e675cc27cf9c58b8e453053c8a4a12e560f07c77fa80aacd57decb2a8abb0ff9e91afdee11ff132bf15be1ae45c90b0ca09fdcdf97617532f835909f1ba2d')

prepare() {
  patch -Np1 -d responses-$pkgver <pytest5.patch
}

build() {
  cd "$srcdir"/responses-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
