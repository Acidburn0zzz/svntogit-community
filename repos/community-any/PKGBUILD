# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dominate
pkgver=2.3.4
_commit=89516cb2b532e73c82b697a302ccc65767a05841
pkgrel=1
pkgdesc="A Python library for creating and manipulating HTML documents using an elegant DOM API"
url="https://github.com/Knio/dominate"
license=('LGPL')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$_commit.tar.gz::https://github.com/Knio/dominate/archive/$_commit.tar.gz")
sha512sums=('21005c8dc9affe4ceb0876ff4d2f948f5e72cd8cfb7a593771365f7d86fc0758c160b445a3c5e564affcc9f2fe2735283f3400cdba811fd34951153f81662624')

prepare() {
  mv dominate-{$_commit,$pkgver}
}

build() {
  cd dominate-$pkgver
  python setup.py build
}

check() {
  cd dominate-$pkgver
  python setup.py pytest
}

package() {
  cd dominate-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
