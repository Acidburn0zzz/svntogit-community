# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=3.2.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="http://docs.python-zeep.org"
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-defusedxml'
         'python-isodate' 'python-lxml' 'python-requests' 'python-requests-toolbelt' 'python-six'
         'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-tornado'
              'python-aioresponses' 'python-xmlsec')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('d608e66d62120edab72c1d8bc70e8de8d22646255dbf838ae94b2e790b542c547479607d29832125e6ce7455f8a165e4815587c06f3ba57d0ca3a45c312eae64')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest || warning "https://github.com/mvantellingen/python-zeep/issues/724"
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
