# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: FJ <joostef@gmail.com>
# Contributor: Sebastien Piccand <sebcactus gmail com>

pkgname=gaupol
pkgver=0.19.2
pkgrel=1
pkgdesc="An editor for text-based subtitles"
arch=('any')
url="http://home.gna.org/gaupol"
license=('GPL')
depends=('pygtk' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('gettext' 'intltool')
optdepends=('python2-chardet: character encoding detection'
            'pyenchant: spell-checking'
            'iso-codes: translate language and country codes for spell-checker'
            'mplayer: subtitle preview'
            'vlc: subtitle preview')
install=gaupol.install
source=(http://download.gna.org/$pkgname/0.19/$pkgname-$pkgver.tar.gz)
sha256sums=('4e30fb7f4ff328561243450367be0c1ec80878f7107cac8eda6db9877a90aff4')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
