# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
pkgname=python-atpublic
pkgver=2.1.3
pkgrel=1
pkgdesc="@public decorator for Python"
arch=('any')
url="https://gitlab.com/warsaw/public"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('deba5654471330454b6327e0911f71426e31f78128b8c5607e4dfca292f017da2c2326a6a59e76097ed30560e3404a47a0c0e034bc053c8348c20f40c002ec9f')
b2sums=('a93a53fbee7b46eb949090d6c8ffa911db613b926f9acdd56932576b492a5ac3949ba958ff6bb988e0f2783c4630a7bffd9610af41cc983a63983d198962b342')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
