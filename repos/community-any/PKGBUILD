# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-glanceclient
pkgname=(python-glanceclient python2-glanceclient)
pkgver=2.7.0
pkgrel=1
pkgdesc="OpenStack Image API Client Library"
arch=('any')
url="http://docs.openstack.org/developer/python-glanceclient"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-babel'
             'python2-babel' 'python-prettytable' 'python2-prettytable' 'python-keystoneclient'
             'python2-keystoneclient' 'python-requests' 'python2-requests' 'python-warlock'
             'python2-warlock' 'python-oslo-utils' 'python2-oslo-utils' 'python-oslo-i18n'
             'python2-oslo-i18n')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-glanceclient/archive/$pkgver.tar.gz")
sha512sums=('c7e98ea0f4c847735cd848d7c6ae50f227450cf2f5f4ab9fb69c7cf70e3fa64f7cbf8be416f6dc53a412d4048f037293419e4d54ab6e8bb3cec07b0edb4020fa')

prepare() {
  cp -a python-glanceclient-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-glanceclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-glanceclient-$pkgver
  python setup.py testr

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  PYTHON=python2 python2 setup.py testr
}

package_python-glanceclient() {
  depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
           'python-requests' 'python-warlock' 'python-oslo-utils' 'python-oslo-i18n')

  cd "$srcdir"/python-glanceclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-glanceclient() {
  depends=('python2-six' 'python2-pbr' 'python2-babel' 'python2-prettytable' 'python2-keystoneclient'
           'python2-requests' 'python2-warlock' 'python2-oslo-utils' 'python2-oslo-i18n')

  cd "$srcdir"/python-glanceclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/glance{,2}
}

# vim:set ts=2 sw=2 et:
