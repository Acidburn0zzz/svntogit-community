# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Francois Boulogne <devel at sciunto dot org>

pkgname=mat2
pkgver=0.10.0
pkgrel=3
pkgdesc='Metadata removal tool, supporting a wide range of commonly used file formats'
url='https://0xacab.org/jvoisin/mat2'
arch=('any')
license=('LGPL3')
depends=('python' 'python-mutagen' 'python-cairo' 'python-gobject' 'gdk-pixbuf2' 'poppler-glib' 'librsvg' 'perl-image-exiftool' 'mailcap')
makedepends=('python-setuptools')
optdepends=(
  'ffmpeg: video support'
  'bubblewrap: sandboxing support'
)
checkdepends=('ffmpeg')
source=(https://0xacab.org/jvoisin/mat2/-/archive/${pkgver}/mat2-${pkgver}.tar.gz
        https://0xacab.org/jvoisin/mat2/uploads/8c62aff50acf08820d5e2bbc30aca64e/mat2-${pkgver}.tar.gz.asc
        fix-test-perl-exiftool.patch)
sha512sums=('7434365766f5ba893b5b51e852d73af6b1dd562f3804e46b2aa5bf2097694d5465f0ddb5e401b157ed899a86caca2dd405424c5a7c86d66190cefd30eee3ba65'
            'SKIP'
            '7d6419815e8801df2b153d0d5711f74329e0cf1717be4c1f83d1a63e450680d2f41329611f6c6b3aae28c2df529cdd16a502ccd6b8634a7ea4a3ef14369a45c9')
b2sums=('3aeb1b5fb1e5b39d5a59e84de48a1fefed8afff31d7ae2827324b9d54eab8a95f28f0e1de94d6c96ba250249531782f4a2cff4a82fdccc85da7382d00294fdaf'
        'SKIP'
        '35f48327677b4f9dbbc55aa84ff10743b8ca5cffe1f8ce7a193bb20658673cf7c25d0bc47963854a7e42fce14715a36ca22dcbf302302367308b196409bed7b1')

validpgpkeys=('9FCDEE9E1A381F311EA62A7404D041E8171901CC') # Julien (jvoisin) Voisin

prepare() {
  cd ${pkgname}-${pkgver}
  patch --forward --strip=1 --input="${srcdir}/fix-test-perl-exiftool.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  python -m unittest discover -v
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 dolphin/mat2.desktop -t "${pkgdir}/usr/share/kservices5/ServiceMenus"
  install -Dm 644 doc/mat2.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 doc/*.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
