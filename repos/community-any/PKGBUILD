# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aresponses
pkgver=2.1.3
pkgrel=1
pkgdesc="Asyncio http mocking. Similar to the responses library used for 'requests'"
url="https://github.com/circleup/aresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp' 'python-pytest-asyncio')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov')
source=("https://github.com/circleup/aresponses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e06b59706d1e4627813b704e68400011c12c5b5e1641a258474ee7c82aff10b150e20ca469c509fbb6dc69b467766fd96d2f7a340e4571b0f282e09c0f698531')

build() {
  cd aresponses-$pkgver
  python setup.py build
}

check() {
  cd aresponses-$pkgver
  python -m pytest
}

package() {
  cd aresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
