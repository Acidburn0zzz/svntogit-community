# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: noonov <noonov@gmail.com>

pkgname=zshdb
pkgver=1.0.1
pkgrel=1
pkgdesc='A debugger for zsh scripts.'
arch=('any')
url='http://bashdb.sourceforge.net/'
license=('GPL')
depends=('zsh')
optdepends=('python-pygments: for syntax highlighting')
source=("https://github.com/rocky/$pkgname/archive/release-$pkgver.tar.gz")
md5sums=('2610e89a892c870035936d7744704297')

build() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-release-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
