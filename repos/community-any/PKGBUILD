# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-dance
pkgver=1.3.0
pkgrel=1
pkgdesc="Doing the OAuth dance with style using Flask, requests, and oauthlib"
url="https://github.com/singingwolfboy/flask-dance"
license=('MIT')
arch=('any')
depends=('python-requests' 'python-oauthlib' 'python-requests-oauthlib' 'python-flask'
         'python-urlobject' 'python-six' 'python-lazy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-sphinx' 'python-pytest-cov' 'python-mock'
              'python-responses' 'python-freezegun' 'python-sqlalchemy' 'python-sqlalchemy-utils'
              'python-flask-sqlalchemy' 'python-flask-login' 'python-flask-caching'
              'python-flask-sphinx-themes' 'python-sphinxcontrib-seqdiag')
source=("$pkgname-$pkgver.tar.gz::https://github.com/singingwolfboy/flask-dance/archive/v$pkgver.tar.gz")
sha512sums=('a2bde84540927db54edce79347dd3a09f258e3c706d2ca70095e1d464796c51614f7df8ad6e6d3021b2a9b883c5c5534abb7e97184b3c2958e30baa331991a0f')

build() {
  cd flask-dance-$pkgver
  python setup.py build
}

check() {
  cd flask-dance-$pkgver
  python setup.py pytest
}

package() {
  cd flask-dance-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
