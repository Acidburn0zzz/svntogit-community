# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tomas A. Schertel <tschertel@gmail.com>

pkgname=cherrytree
pkgver=0.23.1
pkgrel=1
pkgdesc="A hierarchical note taking application featuring rich text and syntax highlighting"
arch=('any')
url="http://www.giuspen.com/cherrytree/"
license=('GPL')
depends=('pygtksourceview2' 'hicolor-icon-theme' 'desktop-file-utils')
install=$pkgname.install
source=(http://www.giuspen.com/software/${pkgname}-${pkgver}.tar.gz)
md5sums=('2804507e3e52b18378d6aff2db951c06')

build() {
	cd "$srcdir/${pkgname}-${pkgver}"

	# python2 fix
	for file in $(find . -name '*.py') cherrytree; do
 		echo "Applying python2 fix for ${file}"
 		sed -i 's_/usr/bin/env python_/usr/bin/env python2_' ${file}
 	done
}
	
package() {
	cd "$srcdir/${pkgname}-${pkgver}"
  	python2 setup.py install --root="$pkgdir/" --optimize=1
}
