# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=ropper
_pkgname=Ropper
pkgver=1.7.3
pkgrel=1
pkgdesc="Show information about binary files and find gadgets to build rop chains for different architectures"
url="https://github.com/sashs/Ropper"
arch=('any')
license=('GPL2')
depends=('python-capstone')
optdepends=('sqlite: gadgets database support')
makedepends=('python-setuptools')
checkdepends=('sqlite')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/sashs/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('af3b849aae588b050b378429fc04083cf7c660647a0bc9022660b783a8a7aff01eac21a26a9e5fb4564917f5d54701e97c9ba2c0150e91a0f549c673bdfb7a27')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python test.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 sample.py "${pkgdir}/usr/share/doc/${pkgname}/sample.py"
}

# vim: ts=2 sw=2 et:
