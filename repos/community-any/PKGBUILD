# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django_compressor
pkgname=python-django-compressor
pkgver=3.0
pkgrel=1
pkgdesc="Compresses linked and inline javascript or CSS into a single cached file"
arch=(any)
url="https://github.com/django-compressor/django-compressor"
license=(Apache BSD MIT)
depends=(python-django-appconf python-rcssmin python-rjsmin)
makedepends=(python-setuptools)
checkdepends=(python-beautifulsoup4 python-brotli python-calmjs python-coverage
python-csscompressor python-django-sekizai python-html5lib python-jinja
python-lxml python-mock slimit)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fa3876197f32e1a977aa84a4dc1cac2649c9be84f0a2f38c2363b558d940a861bda08ed038994f2451321573dcb751674ea58ec30be60b46060f82094244b380')
b2sums=('d02bb396f98a05babfd2a6cdcaf9acacf784e9115ece5ea13813a759ac01c3b472b5e4a1f8487b0641faeb082d1ffbb21db97d65e7e78bd2da0604345f3cfe72')

prepare() {
  cd "${_name}-$pkgver"
  sed -e 's/rcssmin ==/rcssmin >=/' \
      -e 's/rjsmin ==/rjsmin >=/' \
      -i setup.py
}

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  django-admin test --settings=compressor.test_settings compressor
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
