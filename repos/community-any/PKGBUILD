# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.2.1
pkgrel=1
pkgdesc='A powerful IMAP/Maildir synchronization tool'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('docutils')
changelog=$pkgname.changelog
source=("http://cloud.github.com/downloads/spaetz/${pkgname}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('996aa89bcc27fbbbb40f021d7043b5606059587d79105a5c9dbc0096754c95ad')

package() {
  cd "${srcdir}/${pkgname}"

  python2 setup.py build install --root="${pkgdir}"

  sed 's/env python/env python2/' -i "${pkgdir}"/usr/lib/python2.7/site-packages/offlineimap/imaplib2.py

#man page
  sed -i 's/\^/=/g' docs/MANUAL.rst
  rst2man docs/MANUAL.rst $pkgname.1
  install -D -m644 $pkgname.1 ${pkgdir}/usr/share/man/man1/$pkgname.1

#config file examples
  install -D -m644 $pkgname.conf.minimal ${pkgdir}/usr/share/$pkgname/$pkgname.conf.minimal
  install -D -m644 $pkgname.conf ${pkgdir}/usr/share/$pkgname/$pkgname.conf
}
