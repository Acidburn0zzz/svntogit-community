# Maintainer: David Runge <dave@sleepmap.de>
_name=micawber
pkgname=python-micawber
pkgver=0.3.8
pkgrel=1
pkgdesc="A small library for extracting rich content from urls."
arch=('any')
url="https://github.com/coleifer/micawber"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-beautifulsoup4' 'python-django')
optdepends=('python-django: django support'
            'python-flask: flask support'
            'python-redis: redis caching support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9f2ede4df0eeb6fade9815c91a9a94ba53e7906433eebcce95dcb250870501e289f75c9d30e7109923f5718971b0a34a5cbdc43cbde6dcc3bbc2502e038c7472')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build/lib:${PYTHONPATH}
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
