# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.3.1
pkgrel=1
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('python2-docutils')
#source=("http://cloud.github.com/downloads/spaetz/${pkgname}/${pkgname}-v${pkgver}.tar.gz")
source=("ftp://ftp.archlinux.org/other/offlineimap/spaetz-offlineimap-ebd5fe2.tar.gz")
sha1sums=('2c2c1ec46f07a5cb0b344611fd8a41fa4c0fc87a')

package() {
  #cd "${srcdir}/${pkgname}"
  cd "${srcdir}/spaetz-offlineimap-ebd5fe2"

  python2 setup.py build install --root="${pkgdir}"

  sed 's/env python/env python2/' -i "${pkgdir}"/usr/lib/python2.7/site-packages/offlineimap/imaplib2.py

#man page
  sed -i 's/\^/=/g' docs/MANUAL.rst
  rst2man2 docs/MANUAL.rst $pkgname.1
  install -D -m644 $pkgname.1 "${pkgdir}"/usr/share/man/man1/$pkgname.1

#config file examples
  install -D -m644 $pkgname.conf.minimal "${pkgdir}"/usr/share/$pkgname/$pkgname.conf.minimal
  install -D -m644 $pkgname.conf "${pkgdir}"/usr/share/$pkgname/$pkgname.conf
}
