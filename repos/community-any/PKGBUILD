# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=img2pdf
pkgver=0.3.4
pkgrel=1
pkgdesc="Losslessly convert raster images to PDF"
arch=(any)
url="https://gitlab.mister-muffin.de/josch/img2pdf"
license=(LGPL3)
depends=(python-pillow python-setuptools)
checkdepends=(python-pdfrw)
source=(https://files.pythonhosted.org/packages/source/i/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('0b482b7f317b76051fabf6e50e5eea1584b20bdf76b6b54c16b6ef1d5298ee49')

build() {
    cd ${pkgname}-${pkgver}
    python setup.py build
}

check() {
    cd ${pkgname}-${pkgver}
    python setup.py test
}

package() {
    cd ${pkgname}-${pkgver}
    python setup.py install --skip-build --prefix=/usr --root="${pkgdir}" --optimize=1
}
