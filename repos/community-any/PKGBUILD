# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xpybutil
pkgname=(python-xpybutil python2-xpybutil)
pkgver=0.0.3
pkgrel=1
pkgdesc="An incomplete xcb-util port plus some extras"
arch=('any')
url="https://github.com/fennerm/xpybutil"
license=('custom:WTFPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-xcffib' 'python2-xcffib')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/fennerm/xpybutil/archive/$pkgver.tar.gz"
        https://github.com/fennerm/xpybutil/commit/78ff923c022433c819c88c0159440306bd3ed4f5.patch)
sha512sums=('8eae9275fe5f3ce56bedb20c865ac84d971f59432dce48c8f55c76910e5e32e803890c04ebe076ae248b002fc1fc2fa6e0120260c8a57178bb4031b787eac398'
            '49454bb4fa9e45de8ed288a7cf87de4eb507197db73fa65fc7b5ccfae60f508dfbc07b6bd737bae6ee6cb3a106334aa8b4e8a4dc23e0c796ab0e04b63b43a0f9')

prepare() {
  (cd xpybutil-$pkgver; patch -p1 -i ../78ff923c022433c819c88c0159440306bd3ed4f5.patch)
  cp -a xpybutil-$pkgver{,-py2}
}

build() {
  cd "$srcdir/xpybutil-$pkgver"
  python setup.py build

  cd "$srcdir/xpybutil-$pkgver-py2"
  python2 setup.py build
}

package_python-xpybutil() {
  depends=('python-xcffib')

  cd "${srcdir}/xpybutil-$pkgver"
  python setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv "$pkgdir/usr/share/doc/"{,python-}xpybutil
}

package_python2-xpybutil() {
  depends=('python2-xcffib')

  cd "${srcdir}/xpybutil-$pkgver-py2"
  python2 setup.py install --root="${pkgdir}/" --optimize=1

  install -Dm664 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  mv "$pkgdir/usr/share/doc/"{,python2-}xpybutil
}
