# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=13.2.0
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path/archive/v$pkgver.tar.gz")
sha512sums=('59c9df45996eb227df101a13da968bcb006d9b89cb5c36ffcd08bdfb1102e7bd91bbe340b04ffea7478a0dda3dfa2df33412fcf9b6e6fb5af06952a82a6b6914')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  python -m pytest
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
