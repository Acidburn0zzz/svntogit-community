# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-news
_name=news
pkgver=16.0.0
pkgrel=1
pkgdesc="An RSS/Atom feed aggregator"
arch=('any')
url="https://github.com/nextcloud/news"
license=('AGPL3')
depends=('nextcloud>21' 'nextcloud<22')
makedepends=('ripgrep' 'yq')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/${pkgver}/${_name}.tar.gz")
sha512sums=('1f5ff1f1379e3bbce91d9144268eb199d902a9d2c06ce459239b76625e4d241a05d6c891632ee50da10f417dc328682526a13fe16726703339d3bb24f7eb80d1')
b2sums=('046fb0595fcbbd3c4a7d12de211f0be04d3dc72262fca7b63123eef6bc5ea1eb65f997cb1a89f287db66d41a8e2e7969ab1f9711bc8079328cc67060cdb288d2')

check() {
  local _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
