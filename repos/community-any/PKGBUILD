# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.11.1
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=('any')
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9f1445b558a2a1cca93e3f0715eeda885e58a846c01b6f063a7d94a0334f32d3095afc9d308f6dfad3caa69835801eb2907aeb712cebe3136fa9866ecf17cf36')
b2sums=('ed07fa1c6f7dfb28b03a005f24a11d383ae772f662e873219bafaa1332b2c70615686b346b672f352b76d56b567b1cf47da67a76af0c339be1caca38a08d22a2')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
