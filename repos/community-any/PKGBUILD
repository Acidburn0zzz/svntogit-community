# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-shims
pkgver=0.3.2
pkgrel=2
pkgdesc="Compatibility shims for pip versions 8 thru current"
url="https://github.com/sarugaku/pip-shims"
license=('ISC')
arch=('any')
depends=('python-pip' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest-runner' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sarugaku/pip-shims/archive/$pkgver.tar.gz")
sha512sums=('c2313be7f1b96be6f53611e54ac50844099a38fa451f41fb9182c3d900477c295d915fe2da3a32fcda0787d295099f2de5e5d231757eca623e0fb77c576dc956')

build() {
  cd pip-shims-$pkgver
  python setup.py build
}

check() {
  cd pip-shims-$pkgver
  python setup.py pytest
}

package() {
  cd pip-shims-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
