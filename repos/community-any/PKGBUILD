# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python-breathe
pkgver=4.26.1
pkgrel=3
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output"
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=('python' 'python-sphinx' 'python-docutils' 'python-six')
makedepends=('git' 'python-setuptools')
checkdepends=('python-pytest')
source=("git+https://github.com/michaeljones/breathe#tag=v${pkgver}?signed"
        '010-python-breathe-support-sphinx-3.5.patch'::'https://github.com/michaeljones/breathe/commit/2514a9020b19f64cc7c8132b4f9fc8578bdf33e8.patch')
sha512sums=('SKIP'
            'c2107e676d874bc75e52b8cf7826f6fac74acf4f55c65105759221c12d08770b357ddaf431a866506387a00dd7c3b73ab71f86059824c7800812917c91fc5e67')
validpgpkeys=('8AED58021FEACDD5F27BA0E6A72F627716EA9D96') # Melvin Vermeeren <vermeeren@vermwa.re>

prepare() {
    patch -d breathe -Np1 -i "${srcdir}/010-python-breathe-support-sphinx-3.5.patch"
}

check() {
  cd breathe
  PYTHONPATH="$PWD" make test
}

build() {
  cd breathe
  python setup.py build
}

package_python-breathe() {
  cd "breathe"
  PYTHONHASHSEED=0 python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
