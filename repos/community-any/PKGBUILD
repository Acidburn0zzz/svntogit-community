# Contributor: Martin Wimpress <code@flexion.org>

_ver=3.20
pkgname=mate-themes
pkgver=${_ver}.10
pkgrel=1
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
makedepends=('gtk2' 'intltool')
optdepends=('gtk-engines: for gtk2 themes'
            'gtk-engine-murrine: for gtk2 themes'
            'mate-icon-theme: default icon theme')
options=('!emptydirs')
groups=('mate' 'mate-gtk3')
source=("http://pub.mate-desktop.org/releases/themes/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('d44f484987c269820662a037f763c248a5d24356')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
