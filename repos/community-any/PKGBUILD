# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase='python-webob'
pkgname=('python-webob' 'python2-webob' 'python-webob-docs')
pkgver=1.7.2
pkgrel=1
pkgdesc="WSGI request and response object"
arch=('any')
url="http://pythonpaste.org/webob/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-sphinx')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
optdepends=('python-webob-docs: documentation')
source=("https://pypi.io/packages/source/W/WebOb/WebOb-$pkgver.tar.gz")
md5sums=('ab54739f9f716c3cdf66ad3dc4816113')

prepare() {
  cp -a WebOb-$pkgver{,-py2}
  cp -a WebOb-$pkgver{,-docs}
}

build() {
  cd "$srcdir"/WebOb-$pkgver
  python setup.py build

  cd "$srcdir"/WebOb-$pkgver-py2
  python2 setup.py build

  cd "$srcdir"/WebOb-$pkgver-docs
  python setup.py build_sphinx
}

check() {
  cd "$srcdir"/WebOb-$pkgver
  python setup.py ptr --addopts "--ignore=docs"

  cd "$srcdir"/WebOb-$pkgver-py2
  python2 setup.py ptr --addopts "--ignore=docs"
}

package_python-webob() {
  depends=('python')

  cd "$srcdir"/WebOb-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-webob() {
  depends=('python2')

  cd "$srcdir"/WebOb-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python-webob-docs() {
  optdepends=()

  cd "$srcdir"/WebOb-$pkgver-docs
  _doc_dir="$pkgdir"/usr/share/docs/python-webob
  mkdir -p "$_doc_dir"
  cp -a "build/sphinx/html" "$_doc_dir"
  install -D -m644 "docs/license.txt" \
                   "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
