# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=0.4.3
pkgrel=1
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-termcolor' 'python-colorama')
makedepends=('python-setuptools-markdown' 'python-setuptools-scm')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tldr-pages/tldr-python-client/archive/$pkgver.tar.gz")
sha512sums=('6c576b7b488c49d7995be33a59d94773075cd143adf0b732f9cda67aa56c1c238a33644b5ac3c852ff8a31722c67973b099b8e158adaf8e3c60238e604333550')

prepare() {
  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python setup.py pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm755 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
