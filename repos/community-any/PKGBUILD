#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2020.12.7.1
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.7.1.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.7.1.tar.xz.sig
)
sha512sums=(
  c5ed14bfd2c87ef5610dd47f25a4d05cbd3135f1ebe96ef561f15cf22e1aba9f6187f7e559ebcb710421cea469133ae25df41120f7aa039cf593df4a4bdf0099
  44f9c296a791340fa68a8b36ae2e36ac02f50b45a39ad64c9a39f4399368b56fccb5a13410b2f6cd139d30c042c856b62ca1af07bd46fa9602e1ea1e9f8a8eaa
)
md5sums=(
  37fbf996520ef477cd0da992717bf45c
  c98ea75033579be65c9b20bba4466873
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm755 "$srcdir/$pkgname-$pkgver/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
