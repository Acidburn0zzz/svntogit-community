# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-mock
pkgver=3.1.0
pkgrel=1
pkgdesc="Thin-wrapper around the mock package for easier use with py.test"
arch=('any')
license=('LGPL3')
url="https://github.com/pytest-dev/pytest-mock/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
# TODO: figure out why they fail
#checkdepends=('python-pytest-asyncio')
source=("https://github.com/pytest-dev/pytest-mock/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bbc787f9b39edf2e29446028f00c957b60fef311d1b3b0ace234c037904c97b10ea8e63041370d891bbcd5f260582d8f292958f447547f1f77d280be8f249a16')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-mock-$pkgver
  python setup.py build
}

check() {
  cd pytest-mock-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD:$PWD/src" pytest
}

package() {
  cd pytest-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
