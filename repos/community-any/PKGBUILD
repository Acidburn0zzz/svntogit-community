# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.10.53
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-numpy' 'python-pytest-runner' 'python-pytest-benchmark')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('611bf785059165cbe6cc7c9d3d3a7cb29ba1830db2367ba7582ee00d02d8586ecb17dc705a3b0ffacfacd0f93e0e5b45dcd803d71d055a576200709355c7102f')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
