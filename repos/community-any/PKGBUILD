# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-osc-lib
pkgver=2.3.1
pkgrel=1
pkgdesc="OpenStackClient Library"
arch=('any')
url="https://docs.openstack.org/osc-lib/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneauth1'
         'python-os-client-config' 'python-oslo-i18n' 'python-oslo-utils' 'python-stevedore'
         'python-simplejson' 'python-openstacksdk')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/osc-lib/archive/$pkgver.tar.gz")
sha512sums=('7f71a55e8d0e79d187574fb5a5a922cf76a3391fd80c664f2bc42f8b1656954dd30cd3264cf9de8aa514b86f159bbe8ce4e801f37bab46bc73b8e113bbea456c')

export PBR_VERSION=$pkgver

prepare() {
  # https://storyboard.openstack.org/#!/story/2003142
  sed -i 's/issubclass/isinstance/' osc-lib-$pkgver/osc_lib/utils/__init__.py

  sed -i 's/assertItemsEqual/assertCountEqual/' osc-lib-$pkgver/osc_lib/tests/{cli,utils}/*.py
}

build() {
  cd osc-lib-$pkgver
  python setup.py build
}

check() {
  cd osc-lib-$pkgver
  # https://storyboard.openstack.org/#!/story/2008461
  stestr run || echo "Tests failed"
}

package() {
  cd osc-lib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
