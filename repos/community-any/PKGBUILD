# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.518
_portal_pkgver=0.9.7
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://gitlab.gnome.org/gnumdk/lollypop'
license=('GPL')
depends=(
  'gst-plugins-base-libs' 'gtk3' 'python-beautifulsoup4' 'python-cairo'
  'python-dbus' 'python-gobject' 'totem-plparser'
)
makedepends=(
  'git' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'python'
)
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pillow: Blur background in lyrics view'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support')
source=("git+https://gitlab.gnome.org/World/lollypop.git#tag=${pkgver}"
        "git+https://gitlab.gnome.org/gnumdk/lollypop-portal.git#tag=${_portal_pkgver}")
sha256sums=('SKIP'
            'SKIP')

build() {
  for d in lollypop{,-portal}; do
    arch-meson $d build-$d \
      --libexecdir='lib/lollypop'
    ninja -C build-$d
  done
}

package() {
  for d in lollypop{,-portal}; do
    DESTDIR="${pkgdir}" ninja -C build-$d install
  done
}

# vim: ts=2 sw=2 et:
