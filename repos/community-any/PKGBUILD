# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.8.0
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-typing_extensions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agronholm/typeguard/archive/$pkgver.tar.gz")
sha512sums=('53f82065585ce3ebbf9d9e9caad5a8ae251737e94315a806dd2516df3724dbbc73103d6ba84db9c97f4d8e9d3dd5c81bc51a223d26e8c9dc49c64dddd49ab1e9')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  python -m pytest
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
