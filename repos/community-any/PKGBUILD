# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.512
_portal_pkgver=0.9.7
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://gitlab.gnome.org/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-plugins-base-libs' 'gtk3' 'python-beautifulsoup4' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'meson' 'python')
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pillow: Blur background in lyrics view'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
source=("https://gitlab.gnome.org/gnumdk/lollypop/uploads/8a084e3edb9f0d700454554323d9d960/lollypop-0.9.512.tar.xz"
        "https://gitlab.gnome.org/gnumdk/lollypop-portal/uploads/42bd030f9c8391db93a335c6a23783bf/lollypop-portal-0.9.7.tar.xz")
sha256sums=('a4d301152a4c12422f4094c5c0d4b60ee08c9b308c8d00b8bed6ae030e7b8675'
            'fef0d7b5a4065884582fef6e6e40ae781fe182063a07cfcc5d98a69602f4d57b')

build() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    pushd $d
    arch-meson build \
      --libexecdir='lib/lollypop'
    ninja -C build
    popd
  done
}

package() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    DESTDIR="${pkgdir}" ninja -C $d/build install
  done
}

# vim: ts=2 sw=2 et:
