# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=python-xlsxwriter
pkgver=1.4.4
pkgrel=1
pkgdesc="A Python module for creating Excel XLSX files"
arch=('any')
url="https://github.com/jmcnamara/XlsxWriter"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/RELEASE_${pkgver}.tar.gz")
sha256sums=('00fd4b77dba856a0074610915be0e02c0393d61e8a08ffa73fde7d6df813a120')

build() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py build
}

check() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python -m unittest discover
}

package() {
  cd "$srcdir"/XlsxWriter-RELEASE_${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -D -m644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
