# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.2.2.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="https://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
_tarname=jruby-dist-$pkgver-bin.tar.gz
noextract=($_tarname)
source=(https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/$_tarname)
md5sums=('be678559801be029fe233c33396e34d2')
sha1sums=('95559e381844231b6a89dca52b60dc70552a5540')
sha256sums=('0daadec9b9416558cba36908593bf9ec6cd625f853203a54b397abeb7a5d1c42')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$_tarname"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -print -delete
  rm -rv opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}

  chmod -cR g-w opt/jruby

  mkdir -p usr/bin
  ln -srt usr/bin opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}

  mkdir -p usr/share/licenses/$pkgname
  ln -srt usr/share/licenses/$pkgname opt/jruby/COPYING
}
