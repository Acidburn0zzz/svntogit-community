# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Maintainer: Konstantin Gizdov (kgizdov) <arch@kge.pw>

pkgname=python-tensorflow-estimator
pkgver=1.14.0rc1
_pkgver=1.14.0-rc1
pkgrel=1
pkgdesc="A high-level TensorFlow API that greatly simplifies machine learning programming"
url="https://github.com/tensorflow/estimator"
license=('APACHE')
arch=('any')
depends=('python-wrapt')
makedepends=('bazel' 'python-wheel' 'python-setuptools' 'python-pip' 'python-tensorflow')
source=("https://github.com/tensorflow/estimator/archive/v${_pkgver}.tar.gz")
sha512sums=('59d9505b01c8bb1f88ab310f341c8a96c3699676e0440b04ade76d66a3940c94b50a2f8ba1704873aae9c6dde0be3d734f0c8f3cf99901b17baa82704d784085')

build() {
  cd estimator-${_pkgver}

  bazel build //tensorflow_estimator/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_estimator/tools/pip_package/build_pip_package ${srcdir}/estimator_pip
}

check() {
  cd estimator-${_pkgver}

  # Of course, upstream doesn't care about working tests...
  # Revisit this at some point.
  # bazel test //tensorflow_estimator/...
}

package() {
  cd estimator-${_pkgver}

  WHEEL_PACKAGE=$(find ${srcdir}/estimator_pip -name "tensor*.whl")
  pip install --ignore-installed --upgrade --root "$pkgdir"/ $WHEEL_PACKAGE --no-dependencies
}
