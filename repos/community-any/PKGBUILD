# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=soundconverter
pkgver=2.9.0_beta2
_gitrev=b101a833fbc55ec43811691338e1e67789d23b98
pkgrel=1
pkgdesc="A simple sound converter application for GNOME"
arch=('any')
license=('GPL3')
url="http://soundconverter.org/"
depends=('gconf' 'gst-plugins-base' 'gst-plugins-good' 'gtk3' 'libnotify' 'python-gobject')
makedepends=('intltool')
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-libav: Extra media codecs')
source=("$pkgname-$_gitrev.tar.gz::https://codeload.github.com/kassoulet/$pkgname/tar.gz/$_gitrev")

prepare() {
  cd "${srcdir}/${pkgname}-${_gitrev}"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${_gitrev}"
  ./configure --prefix=/usr 

  make
}

package() {
  cd "${srcdir}/${pkgname}-${_gitrev}"
  make DESTDIR="${pkgdir}" install
}

md5sums=('d091a75f5839b64c80c566e487366c96')
