# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.5
pkgrel=1
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org/
license=('GPL3')
depends=('python-aiohttp' 'python-defusedxml' 'python-dulwich'
         'python-icalendar' 'python-jinja' 'python-multidict')
makedepends=('python-setuptools')
optdepends=('python-prometheus_client: for metrics with the standalone server'
            'uwsgi-plugin-python: for the WSGI interface')
source=("https://github.com/jelmer/xandikos/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
b2sums=('e1d700adb413df062cb0f13a55fa1a4a3b75f3ce56cbfc91b66e8c350649c260df14d1eb1a39435d8c4b8156728c6504bc545e52a5ad16a8bad37eef12b173ea')

build() {
  cd xandikos-$pkgver
  python setup.py build
}

check() {
  cd xandikos-$pkgver
  python -m unittest xandikos.tests.test_suite
}

package() {
  cd xandikos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos/examples examples/*
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 xandikos.1
}

# vim:set ts=2 sw=2 et:
