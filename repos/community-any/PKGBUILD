# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-dkim
pkgver=0.9.3
_branch=${pkgver%.*}
pkgrel=1
pkgdesc='Python library that implements DKIM and ARC email signing and verification'
arch=('any')
url=https://launchpad.net/dkimpy
license=('ZLIB')
depends=('python-authres' 'python-dnspython' 'python-pynacl' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/d/dkimpy/dkimpy-$pkgver.tar.gz"
        "LICENSE-dkimpy-$pkgver::https://git.launchpad.net/dkimpy/plain/LICENSE?id=$pkgver")
sha256sums=('6d7f335e1e901695aba1a2659102147775502ed7d0664f00319357ba4457d6e6'
            'c047360d48ec9a45c4d70dca260c1a0d4806d521be3aee4b4300106d2b2618fe')

build() {
  cd dkimpy-$pkgver
  python setup.py build
}

check() {
  cd dkimpy-$pkgver
  python -m unittest dkim.tests.test_suite
}

package() {
  cd dkimpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 ../LICENSE-dkimpy-$pkgver "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
