# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-digitalocean
pkgname=('python-digitalocean' 'python2-digitalocean')
pkgver=1.11
pkgrel=1
pkgdesc='digitalocean.com API to manage Droplets and Images'
arch=('any')
license=('LGPL')
url='https://github.com/koalalorenzo/python-digitalocean'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-responses' 'python2-responses')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/koalalorenzo/python-digitalocean/archive/v$pkgver.tar.gz")
sha512sums=('b09e0b9bb89e1e31870e18358d86cf69f704d18663749235842cc60c16d67241badb0541edbd4027d9e3a2624fd311a9db52c9a4932eb7567214a0dd8aee4e0a')

prepare() {
  cp -a python-digitalocean-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py build

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py pytest
}

package_python-digitalocean() {
  depends=('python-requests')

  cd python-digitalocean-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-digitalocean() {
  depends=('python2-requests')

  cd python-digitalocean-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
