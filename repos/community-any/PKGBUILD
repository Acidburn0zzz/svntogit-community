# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.5
pkgrel=2
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="https://github.com/leenooks/phpLDAPadmin"
license=('GPL')
depends=('libldap' 'php')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=($pkgname-$pkgver.tar.gz::https://github.com/leenooks/phpLDAPadmin/archive/$pkgver.tar.gz
        disable-mcrypt.patch
        phpldapadmin-1.2.3-php7_4.patch
        sort-in-templates.patch)
sha256sums=('74581f5af1abdbf672b6a1655e9fb3c34a16c7510019348812571fcc10fdde1b'
            'b31774abab4dd603ec9acbeda49adb188890173ee93042a811b5724e01edd837'
            'd6e9b6361549be4a4e5a05ffac0917307973fe35717041f7cdde4171271ad168'
            'fc2d4bdf4d43a90aa541fe20eeed494caa339eacf3f08b06539efcdbacc3578f')

prepare() {
  cd "$srcdir"/phpLDAPadmin-${pkgver}/
  find -type f -name '*.png' -exec optipng -quiet -force -fix {} \;
  patch -p1 -i "$srcdir"/disable-mcrypt.patch
  patch -p1 -i "$srcdir"/phpldapadmin-1.2.3-php7_4.patch
  patch -p1 -i "$srcdir"/sort-in-templates.patch
  find -type f -name '*.orig' -exec rm -f {} \;
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpLDAPadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
