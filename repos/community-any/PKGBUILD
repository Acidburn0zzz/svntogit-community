# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flex
pkgver=6.14.0
pkgrel=2
pkgdesc="Swagger schema validator"
url="https://github.com/pipermerriam/flex"
license=('BSD')
arch=('any')
depends=('python-six' 'python-yaml' 'python-validate-email' 'python-rfc3987' 'python-requests'
         'python-strict-rfc3339' 'python-click' 'python-jsonpointer')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-factory-boy' 'python-responses' 'python-pytest-httpbin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pipermerriam/flex/archive/v$pkgver.tar.gz")
sha512sums=('4e3ab6cfc0d4ce214d1fe8565739bf80c6d0e083053770e57e3ba7b91560b264227ac39d1bacc724876f0555a55ed57ddc3b6b4e9b28a2605618f5e99b7d1d5c')

prepare() {
  sed -i 's/<2/<3/' flex-$pkgver/setup.py
}

build() {
  cd flex-$pkgver
  python setup.py build
}

check() {
  cd flex-$pkgver
  python setup.py pytest
}

package() {
  cd flex-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
