# Maintainer: David Runge <dvzrv@archlinux.org>

_name=atpublic
_gitlab_name=public
pkgname=python-atpublic
pkgver=2.1
pkgrel=1
pkgdesc="@public decorator for Python"
arch=('any')
url="https://gitlab.com/warsaw/public"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
# upstream removed tests in sdist tarball: https://gitlab.com/warsaw/public/-/issues/8
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("https://gitlab.com/warsaw/${_gitlab_name}/-/archive/${pkgver}/${_gitlab_name}-${pkgver}.tar.gz")
sha512sums=('0c5f603dfc9bccca3e8d3a4b49280f2c844579154de5d6c7dec0b520930e1303180e719d2322cc4ac995fbe20fef1d251bb751a9a0e4d1c27275639775c4fd94')
b2sums=('d3db0fc6513f15a078ded1f0dd69c979f252dd4426d987fe5936cb72144ab54a58f43d5027c1bfa458b0c79530a0a7a580215e42dc0dc773e6e227d6f49a0a67')

prepare() {
#  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  mv -v "${_gitlab_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
