# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.2.4.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="https://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
_tarname=jruby-dist-$pkgver-bin.tar.gz
noextract=($_tarname)
source=(https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/$_tarname)
md5sums=('f538f2be459f0f0e517a53446e78a354')
sha1sums=('bbaa2ba3e5279187784ae7356183664489fa1ed7')
sha256sums=('b9638c82c85d89f6e8b2da1b876ac235bb9ed47f2163b3c851f0496c9bd58a0c')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$_tarname"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -print -delete
  rm -rv opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}

  chmod -cR g-w opt/jruby

  mkdir -p usr/bin
  ln -srt usr/bin opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}

  mkdir -p usr/share/licenses/$pkgname
  ln -srt usr/share/licenses/$pkgname opt/jruby/COPYING
}
