# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=absl-py
pkgver=0.8.1
pkgrel=4
pkgdesc="Abseil Python Common Libraries"
arch=(any)
url="https://github.com/abseil/abseil-py"
license=(APACHE)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/a/absl-py/absl-py-$pkgver.tar.gz
        fix_for_Python_3.8_BC_breaking_logging_API.patch)
sha512sums=('fbf1f601021850686fa6430001ea370a26490f3c8c841ba2e34048c69ca382a6f6baf9480e989edddfff69c15a5ea5bbc6d4afa83b3f2b1512dfee40ba940096'
            'f6463aa595e572e2bf5dd2684ed9088f5f85676ac6c99ac92d35849b08cf6c1a72671812898f3bb3eb31d19751530b8a0e127fc0e77bb9f2cef88938e92dd54a')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}"/fix_for_Python_3.8_BC_breaking_logging_API.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  python setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python setup.py install -O1 --root="${pkgdir}" --skip-build
  chmod -R +r "${pkgdir}"/usr/lib
}

# vim:set ts=2 sw=2 et:
