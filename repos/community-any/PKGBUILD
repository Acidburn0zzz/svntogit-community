# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=5.0.2
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://gitlab.com/warsaw/flufl.lock"
license=('Apache')
depends=('python-atpublic' 'python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
replaces=('python-flufl.lock')
# upstream removed tests from sdist tarballs: https://gitlab.com/warsaw/flufl.lock/-/issues/26
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("https://gitlab.com/warsaw/${_name}/-/archive/${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('08fbd8772222f076e268b22b56613bf7a1c849bcb09028b674d3aa74fbddbb4993c44a8787e7e954bb3be8693d1bd6bb573145a43378772da3217f9d005b4f7f')
b2sums=('e3852821631bd531c00da4aaa947c3c10c3c3a1e5ff6daf10e322d4a588e91928f0b1484e6d6bdf163e93a418dc57dcf9a59a9093961bd74b3c359762e4b4d2c')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't use coverage with pytest
  sed -s '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
