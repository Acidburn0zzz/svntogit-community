# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>

pkgname=jsampler
_name=JSampler
pkgver=0.9
pkgrel=7
pkgdesc="LinuxSampler Java GUI"
arch=('any')
url="http://jsampler.sourceforge.net/"
license=('LGPL2.1')
groups=('pro-audio')
depends=('java-runtime=8' 'linuxsampler')
makedepends=('gendesk' 'java-environment=8' 'apache-ant')
source=("https://downloads.sourceforge.net/${pkgname}/${_name}-${pkgver}.tar.bz2"
        "${pkgname}.sh"
        "${pkgname}-classic.sh")
sha512sums=('cbb1d476b1f3882592cc0fb7a61348802c0c399dfc3fcc35f4a8ec3396e8417754cafc3e1a14a90b0014bebd29d28b9d3771dcbb772df40d241eaadedad5299f'
            '632d6a160e026267a85c589b364076b7c439207022a2cdee7a6f432f13c9e34ca17a511fcdf35cd39030ed20dc5c92c670b06cf8ba862cb2f94ee8b552969a40'
            '8bf4db721e1dc6489186b7c70d9bea1c00cd20d567d16e9940497ed2ba17eb02481faff43ab83efd15e2f254896abada19ae6c1148fffca8860d3d52fee3aaf8')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  declare -A comments=(
    ["${pkgname}"]="Fantasia Java GUI"
    ["${pkgname}-classic"]="Classic Java GUI"
  )
  declare -A names=(
    ["${pkgname}"]="Fantasia"
    ["${pkgname}-classic"]="Classic"
  )
  for _app in "${pkgname}" "${pkgname}-classic"; do
    gendesk -n \
            --pkgname "${_app}" \
            --name "${_name} ${names[${_app}]}" \
            --pkgdesc "${comments[${_app}]} for LinuxSampler" \
            --genericname "Audio Sampler" \
            --categories "AudioVideo;Audio"
  done
}

build() {
  cd "${pkgname}-${pkgver}"
  ant build-fantasia
  ant build-jsclassic
}

package() {
  cd "${pkgname}-${pkgver}"
  # java files
  install -vDm 644 dist/Fantasia-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/fantasia.jar"
  install -vDm 644 dist/JS_Classic-${pkgver}.jar \
    "${pkgdir}/usr/share/java/${pkgname}/js-classic.jar"
  # shell scripts
  install -vDm 755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 755 "${srcdir}/${pkgname}-classic.sh" \
    "${pkgdir}/usr/bin/${pkgname}-classic"
  # freedesktop.org compatibility
  install -vDm 644 res/fantasia/icons/app_icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -vDm 644 res/js-classic/icons/app-icon.png \
    "${pkgdir}/usr/share/pixmaps/${pkgname}-classic.png"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
