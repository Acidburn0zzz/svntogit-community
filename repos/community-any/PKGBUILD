# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=5.12.1
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='http://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('e78355f89913a53bb477cf9461203c98c2801b85c0b1a3aba3303dac25bbb609de83599ebdc2fbc08a2d3c2f9f6744a45b076136aa17a319d0093069ed4bd3b2')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
