# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.8.2
pkgrel=1
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'python-pyperclip' 'python2-pyperclip' 'python2-subprocess32' 'python2-contextlib2')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-mock' 'python2-mock' 'vi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('6a6c8250ddeed60b52dd390fa651d3ffe00cc68762f3515639e4aa1ff25c0b205ccc334430d3a54750b75cbb4b97da4dd657e7c58f878283f86dcffd1573364e')

prepare() {
  cp -a cmd2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py build

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py pytest

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py pytest
}

package_python-cmd2() {
  depends=('python-pyparsing' 'python-pyperclip')

  cd "$srcdir"/cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-pyparsing' 'python2-pyperclip' 'python2-subprocess32' 'python2-contextlib2')

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
