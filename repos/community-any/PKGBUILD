# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=1.4.0
pkgrel=1
pkgdesc="Adds caching support to your Flask application"
url="https://github.com/sh4nks/flask-caching"
license=('BSD')
arch=('any')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-redis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sh4nks/flask-caching/archive/v$pkgver.tar.gz")
sha512sums=('512b1c66d80a500f91a0f0ff1ae7461e023cd6907eaf765ca811d40debda29424ec73515f5ff6495bfefb5c2c8c10f40f134b8a4b91bafbf26ee1c413b6f3e55')

build() {
  cd flask-caching-$pkgver
  python setup.py build
}

check() {
  cd flask-caching-$pkgver
  python setup.py pytest
}

package() {
  cd flask-caching-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
