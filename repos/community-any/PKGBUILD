# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-manuel
pkgname=('python-manuel' 'python2-manuel')
pkgver=1.10.0
pkgrel=1
pkgdesc="Manuel lets you build tested documentation"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/manuel"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testing' 'python2-zope-testing')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/benji-york/manuel/archive/$pkgver.tar.gz")
sha512sums=('17973724a7ec1a8cddd8f99ba0cf9c4066669e01b27efd8fc54df9e3b862ae5b202e27bead2460b0ab4ce61d31793945616edc7820f8d6151292cbca462bf304')

build() {
  cd manuel-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd manuel-$pkgver
  python setup.py test
  python2 setup.py test
}

package_python-manuel() {
  depends=('python-setuptools')

  cd manuel-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-manuel() {
  depends=('python2-setuptools')

  cd manuel-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
