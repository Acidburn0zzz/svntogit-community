# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cryptography-vectors
pkgname=('python-cryptography-vectors' 'python2-cryptography-vectors')
pkgver=1.5.2
pkgrel=1
pkgdesc="Test vectors for the cryptography package"
arch=('any')
license=('Apache')
url="http://pypi.python.org/pypi/cryptography-vectors"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/c/cryptography-vectors/cryptography_vectors-${pkgver}.tar.gz")
md5sums=('ab3f748d02e4d26b5c7fb5faf7f7ff04')

prepare() {
   cp -a cryptography_vectors-$pkgver{,-python2}
}

package_python-cryptography-vectors() {
   depends=('python')
 
   cd cryptography_vectors-$pkgver
   python3 setup.py install --root="$pkgdir" --optimize=1
}
 
package_python2-cryptography-vectors() {
   depends=('python2')
 
   cd cryptography_vectors-$pkgver-python2
   python2 setup.py install --root="$pkgdir" --optimize=1
}
