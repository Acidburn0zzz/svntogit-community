# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.26.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('6a2c7dbf9b2014361ccca20caa6fdf677da1605e820db80d0a7dadb83d87daeb0012ddf48ef3cdc7ad259add1092b6471fab14a6ea55aa86449abefb5d9b8d4a')

prepare() {
  cp -a yapf-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-$pkgver
  python setup.py build

  cd "$srcdir"/yapf-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-$pkgver
  nosetests3 || warning "https://github.com/google/yapf/issues/586"

  cd "$srcdir"/yapf-$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
