# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-qiniu
pkgname=(python-qiniu python2-qiniu)
pkgver=7.0.8
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/q/qiniu/qiniu-$pkgver.tar.gz"
        LICENSE)
sha512sums=('0fccab898262eea5f4d8e21a89814d992b3568f0c2e533d2afc44777fa8ca016e41dafc33759ef842220443614fde1e6b392aca7d0a55136acf84e360a073547'
            '4fbb1090b3ee0d230d40b45db7bcd164873872d1860bd83b51197ed5d03e9d4be7785a400f1d314d6e3c0a6fcba7e1afdf3fdab04ae9970df59f96b18862c529')

prepare() {
  cp -a qiniu-$pkgver{,-py2}
}

package_python-qiniu() {
  depends=('python-requests' 'python-setuptools')

  cd qiniu-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-qiniu() {
  depends=('python2-requests' 'python2-setuptools')

  cd qiniu-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"

  install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Don't conflict with python 3.x version
  mv "$pkgdir"/usr/bin/qiniupy{,2}
}
