# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.3.0
_jarversion=2.2.1
pkgrel=1
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle' 'keycloak')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('2dc9937b4738cf979f9533d909b8d84fbaf0fa54b3f739424728f194b9a0bb749123e329249fdcc49c8aa4f7706b83eab938dada7a737b3a65ebdc5311220002')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/keycloak/standalone/deployments/"
  install -Dm644 "build/libs/keycloak-metrics-spi-${_jarversion}-SNAPSHOT.jar" "${pkgdir}/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar"
  ln -s "/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar" "${pkgdir}/opt/keycloak/standalone/deployments/keycloak-metrics-spi-${pkgver}.jar"
}
