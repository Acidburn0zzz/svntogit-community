# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Nishit Joseph (reachjlight at gmail dot com)
# Contributor: Martin Corley <Martin.Corley@ed.ac.uk>

pkgname=python-openpyxl
pkgver=3.0.1
_pkgtag='a2ee53b78166'
pkgrel=1
pkgdesc="A Python library to read/write Excel 2007 xlsx/xlsm files"
arch=('any')
url="https://openpyxl.readthedocs.org/"
license=('MIT')
depends=('python-jdcal' 'python-et-xmlfile')
makedepends=('python-setuptools')
optdepends=('python-pillow: needed to include images'
            'python-lxml: alternative XML backend'
            'python-defusedxml: guard against various XML vulnerabilities'
            'python-pandas: for iteration over Pandas DataFrames')
source=("https://bitbucket.org/openpyxl/openpyxl/get/${pkgver}.tar.bz2"
        "LICENCE")
sha256sums=('027de047510d0655c2f0aa1331702018d6f3a9347b18f650370e0515b06ff6c2'
            '0c84bb42f5d367e5ebf9fc2dde35b16141df5ee0fdc189250858bc6c5560f69e')

build() {
  cd "$srcdir"/openpyxl-openpyxl-${_pkgtag}
  python setup.py build
}

package() {
  cd "$srcdir"/openpyxl-openpyxl-${_pkgtag}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}
