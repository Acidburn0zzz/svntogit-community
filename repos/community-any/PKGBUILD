# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-checkdocs
pkgver=2.6.0
pkgrel=1
pkgdesc='Check the README when running tests'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-checkdocs'
depends=('python-docutils' 'python-jaraco-functools' 'python-pep517')
makedepends=('python-pip' 'python-setuptools-scm' 'python-toml')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-checkdocs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('31c14e3714375928cea52c3caa8e9b889b070013d3624586776da4bc4abd8914539fde73540c6247768271bdbb5a4325efa6c5e5b3de6df5fa4a458ed8d63373')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-checkdocs-$pkgver
  python setup.py build
}

check() {
  cd pytest-checkdocs-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-checkdocs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
