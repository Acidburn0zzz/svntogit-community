# $Id$
# Maintainer: Florian Pritz <bluewind@xinu.at>

_pkgbasename=harfbuzz
pkgname=(lib32-harfbuzz lib32-harfbuzz-icu)
pkgver=1.4.1
pkgrel=2
pkgdesc="OpenType text shaping engine (32-bit)"
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(lib32-glib2 lib32-freetype2 lib32-icu $_pkgbasename gcc-multilib ragel git)
_commit=af596a5655ee2ed09da17a7de79f619e1547dcd9  # tags/1.4.1^0
source=("git+https://anongit.freedesktop.org/git/harfbuzz#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgbasename
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename
 
  CFLAGS+=' -DNDEBUG'
  CXXFLAGS+=' -DNDEBUG'
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --with-glib --with-freetype --without-cairo --with-icu --without-graphite2 \
    --with-gobject --disable-gtk-doc
  make
}

check() {
  cd $_pkgbasename
}

package_lib32-harfbuzz() {
  depends=(lib32-glib2 lib32-freetype2 harfbuzz)

  cd $_pkgbasename
  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s harfbuzz "$pkgdir/usr/share/licenses/lib32-harfbuzz"

# Split harfbuzz-icu
  mkdir -p ../hb-icu/usr/lib32/pkgconfig; cd ../hb-icu
  mv "$pkgdir"/usr/lib32/libharfbuzz-icu* ./usr/lib32
  mv "$pkgdir"/usr/lib32/pkgconfig/harfbuzz-icu.pc ./usr/lib32/pkgconfig
}

package_lib32-harfbuzz-icu() {
  pkgdesc="OpenType text shaping engine (32-bit, ICU integration)"
  depends=(lib32-harfbuzz lib32-icu harfbuzz-icu)

  mv hb-icu/* "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s harfbuzz-icu "$pkgdir/usr/share/licenses/lib32-harfbuzz-icu"
}
