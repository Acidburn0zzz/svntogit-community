# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.6
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
depends=(lib32-glibc "alsa-lib=${pkgver}")
provides=(libasound.so libatopology.so)
source=("https://www.alsa-project.org/files/pub/lib/${_name}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('ae95718813abf62811fe44c6df2a8de5a1227da4b022c4dd97dd8e7c1001f48307ba36bf04208bdbe24e8a8eebf4ed5a59f40ee6750dcbe976678071c3f2f5b7'
            'SKIP')
b2sums=('84a7a20fbea4025f2ac08acefa3a9ea9d2737585c5d5f09c8df3b5ccb998eaa09a7ec16a019a40c4d3e3e4b5209b961f9da23ec57c0bb9824bade9a3a0dd4350'
        'SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91') # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -fiv
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --without-debug \
              --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
