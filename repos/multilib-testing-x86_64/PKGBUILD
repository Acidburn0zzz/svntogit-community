# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=lib32-p11-kit
pkgver=0.23.19
pkgrel=2
pkgdesc="Provides a way to load and enumerate PKCS#11 modules (32-bit)"
arch=(x86_64)
url="https://p11-glue.freedesktop.org"
license=(BSD)
depends=(lib32-glibc lib32-libtasn1 lib32-libffi p11-kit)
makedepends=(git meson lib32-systemd)
provides=(libp11-kit.so)
source=("git+https://github.com/p11-glue/p11-kit?signed#tag=$pkgver")
sha256sums=('SKIP')
validpgpkeys=('C0F67099B808FB063E2C81117BFB1108D92765AF'  # Stef Walter
              '462225C3B46F34879FC8496CD605848ED7E69871') # Daiki Ueno

prepare() {
  cd p11-kit
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson p11-kit build \
    --buildtype debugoptimized \
    --libdir /usr/lib32 \
    -D trust_paths=/etc/ca-certificates/trust-source:/usr/share/ca-certificates/trust-source
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  rm -rf "$pkgdir"/etc "$pkgdir"/usr/{bin,include,lib,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s p11-kit "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
