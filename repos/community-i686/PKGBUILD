# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=0.1.4
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-tool-kit' 'procps-ng' 'nethogs' 'libcap')
makedepends=('qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('5fe2f456c61befb7489e1d57b616aad17474dbca2bd9ad2b9a5addc7055714030e981e235cb6d3f73a71d71d94ef3a7d2350874d6a9aceb08d6dc8e3bbe2feb0')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
