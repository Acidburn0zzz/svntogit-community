# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=pptpd
pkgver=1.3.4
pkgrel=9
pkgdesc="Poptop server"
arch=(i686 x86_64)
url="http://poptop.sourceforge.net/"
license=('GPL')
depends=(ppp glibc)
backup=(etc/pptpd.conf)
source=(http://downloads.sourceforge.net/sourceforge/poptop/pptpd-$pkgver.tar.gz
	pptpd
	unrecognized-opt.patch)
md5sums=('b38df9c431041922c997c1148bedf591'
         'e09f81beb5d59a0fdfba94e3a60d992b'
         'cfa02c86d0413eb94fac15599986f54c')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/unrecognized-opt.patch
  autoreconf -i
  ./configure --prefix=/usr --libdir=/usr/lib --exec-prefix=/usr --mandir=/usr/share/man
  make
  sed -i 's|install -o root|install|g' plugins/Makefile
  sed -i 's|?=|=|g' plugins/Makefile
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/etc/
  echo -e "# Read man pptpd.conf, see samples in /usr/share/doc/pptpd\n# and write your pptpd configuration here" >$pkgdir/etc/pptpd.conf
  install -Dm0755 ../pptpd $pkgdir/etc/rc.d/pptpd
  mkdir -p $pkgdir/usr/share/doc/pptpd
  cp -a samples $pkgdir/usr/share/doc/pptpd/
}
