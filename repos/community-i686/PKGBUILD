# Maintainer: Hans Janssen <janserv@gmail.com>
# Contributor: my64 <packages@obordes.com>
# Contributor: Colin Pitrat <colin.pitrat@gmail.com>

pkgname=openscenegraph
pkgver=2.8.2
pkgrel=3
pkgdesc="An Open Source, high performance real-time graphics toolkit"
arch=('i686' 'x86_64')
license=('OSGPL')
url="http://www.openscenegraph.org"
depends=('giflib' 'jasper' 'librsvg' 'xine-lib' 'curl' 'pth')
makedepends=('cmake' 'libvncserver')
optdepends=('libvncserver')
conflicts=('openthreads')
provides=('openthreads')
source=(http://www.openscenegraph.org/downloads/stable_releases/OpenSceneGraph-$pkgver/source/OpenSceneGraph-$pkgver.zip
        libpng14.patch)
md5sums=('99d0b37f403b85b4559514b6b9849d9e'
         'efb77d8a0583d1b7ad35e935850d56fa')

build() {
  cd "$srcdir/OpenSceneGraph-$pkgver"
  patch -Np1 -i "$srcdir/libpng14.patch" || return 1
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make || return 1
  make DESTDIR="$pkgdir" install || return 1
  install -D -m644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE" || return 1

  [ -d "$pkgdir/usr/lib64" ] && mv "$pkgdir/usr/lib64" "$pkgdir/usr/lib" || true
}
