# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.3.1
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
changelog=$pkgname.changelog
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('2b4ae3df8dbcdabbc76f9ef13055e66caa0d438ef59522e98b396be3109f00da')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
