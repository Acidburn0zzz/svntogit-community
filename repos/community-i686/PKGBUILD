# $Id$
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgver=0.9.4
pkgrel=2
pkgdesc='A simple daemon to allow session software to update firmware'
arch=('i686' 'x86_64')
url='https://github.com/hughsie/fwupd'
license=('GPL2')
depends=('appstream-glib' 'fwupdate' 'colord')
makedepends=('meson' 'valgrind' 'docbook-utils' 'docbook-sgml' 'perl-sgmls'
             'gobject-introspection' 'gtk-doc' 'python-pillow' 'python-cairo'
             'ttf-dejavu' 'adobe-source-han-sans-cn-fonts' 'python-gobject')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz"
        'fix-missing-name.patch'
        'fix-test-failure.patch')
sha256sums=('ac6c02df4a00d541a205f17c0fa681cc5fe9161dd20cc28045c28cbe43b042e0'
            '2a620759ccceaa1e06e81ad47467804f5bffc2a0e83e1cc25f351f94d2444900'
            'e684937330f4fffd32fea3db0f444fcbd0250009905c3c76693bdc6eccb425da')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fix-missing-name.patch
    patch -p1 -i ../fix-test-failure.patch
}

build() {
    cd ${pkgname}-${pkgver}

    meson --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib \
        --buildtype=release \
        ../build

    ninja -v -C ../build
}

check() {
    cd build
    mesontest
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install

    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
}
