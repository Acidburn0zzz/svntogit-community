# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.1
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz")
sha512sums=('980d9397e2aedfa589d11b69a4a1a28cc47b94112bf8c2f1c3b046a5f8f746efc5b5d46cb3be6b718caa6aa99a93af5abed5e7436704702d11d64a066b6b1e96')

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
