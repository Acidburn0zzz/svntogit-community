# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor:  3ED <kas1987@o2.pl>

pkgname=mcabber
pkgver=1.0.4
pkgrel=1
pkgdesc="A small Jabber console client, includes features: SSL, PGP, MUC, UTF8"
arch=('i686' 'x86_64')
url="http://www.lilotux.net/~mikael/mcabber/"
license=('GPL')
depends=('ncurses' 'glib2' 'loudmouth' 'gpgme' 'libotr' 'aspell')
makedepends=('pkg-config')
validpgpkeys=('EACADFF156849BC89653139E3C2900DEACB7FC95')
source=(https://www.lilotux.net/~mikael/mcabber/files/$pkgname-${pkgver/_/-}.tar.bz2{,.asc})
sha256sums=('63b6bc003fcceba4dc4b273ed1c71643c4f8d95e8696543d53f64a7672b1ce0a'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}
  ./configure --prefix=/usr --enable-hgcset \
      --mandir=/usr/share/man \
      --enable-aspell --enable-otr
  make
}

package() {
  cd "${srcdir}"/$pkgname-${pkgver/_/-}

  make DESTDIR="${pkgdir}" install
  mkdir -p "${pkgdir}"/usr/share/mcabber/example
  msg "Copying mcabberrc.example to usr/share/mcabber/example/mcabberrc"
  cp -a "${srcdir}"/$pkgname-${pkgver/_/-}/mcabberrc.example \
	"${pkgdir}"/usr/share/mcabber/example/mcabberrc
}

