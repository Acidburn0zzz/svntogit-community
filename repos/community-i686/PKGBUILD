# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com> 
# Contributor: Ner0 <darkelfdarkelf666 at yahoo dot co dot uk>
# Contributor: dcelasun <dcelasun at gmaild dot com>

pkgname=plank
pkgver=0.3.0
pkgrel=4
pkgdesc="Elegant, simple, clean dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/plank"
license=('GPL3')
depends=('bamf' 'libdbusmenu-gtk3' 'libgee' 'python2')
makedepends=('gnome-common' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://launchpad.net/${pkgname}/1.0/${pkgver}/+download/${pkgname}-${pkgver}.tar.xz")
sha256sums=('9b73a6ef99bdf6ef0bb07ae5a461339b3c78c5237b0363b7174f2ffc60b5b523')

prepare() {
  cd ${pkgname}-${pkgver}

  sed -i 's|^#!.*python$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix='/usr' --sysconfdir='/etc' --localstatedir='/var'
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/share/apport}
}

# vim: ts=2 sw=2 et:
