# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: HuntXu <huntxu@live.cn>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pngcrush
pkgver=1.8.2
pkgrel=1
pkgdesc="A tool for optimizing the compression of PNG files"
url="http://pmt.sourceforge.net/pngcrush"
arch=('i686' 'x86_64')
license=('custom')
depends=('libpng')
source=(http://downloads.sourceforge.net/pmt/${pkgname}-${pkgver}-nolib.tar.xz{,.asc}
        LICENSE)
sha512sums=('f0bfd95a84c9eef48abe624ca8ea4c2064862e9b6330776f91d7647b2796b1221a9edccf8de73fdbad7031cdee0d161072ce44063c60a6a7880a51130fb5c0f9'
            'SKIP'
            'f18613b9da9b4f31a317411e726def5122246087ff57887fba6b46f0a7a7fae7109388ecb2ce049bbe992cc6ef0a3fbbe0f5c5bc816705efd7583836cbd4e69d')
validpgpkeys=('8048643BA2C840F4F92A195FF54984BFA16C640F') # Glenn Randers-Pehrson (pngcrush) <glennrp@users.sourceforge.net>

prepare() {
  cd ${pkgname}-${pkgver}-nolib
  sed -r 's| -g||g' -i Makefile
  sed -r 's|^(CFLAGS )=|\1+=|' -i Makefile
  sed -r 's|^(LDFLAGS )=|\1+=|' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}-nolib
  make
}

package() {
  cd ${pkgname}-${pkgver}-nolib
  make DESTDIR="${pkgdir}" install
  install -Dm 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
