# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: mickele
# Contributor: marcus fritzsch <fritschy@googlemail.com>

pkgname=soqt
pkgver=1.5.0
pkgrel=3
pkgdesc='SoQt is a library which provides the glue between Coin and Qt.'
arch=('i686' 'x86_64')
url='http://www.coin3d.org/lib/soqt/'
license=('GPL')
depends=('coin' 'qt')
makedepends=('doxygen')
options=('!libtool')
source=("http://ftp.coin3d.org/coin/src/all/SoQt-${pkgver}.tar.gz")
md5sums=('9f1e582373d66f556b1db113a93ac68e')

build() {
  cd SoQt-${pkgver}

  ./configure --prefix=/usr \
    --enable-optimization \
    --enable-man \
    --enable-exceptions \
    --disable-debug \
    --disable-maintainer-mode \
    --disable-dependency-tracking \
    --enable-shared \
    --disable-static \
    --with-qt=/usr

  make
}

package() {
  cd SoQt-${pkgver}

  make DESTDIR=${pkgdir} install
}
