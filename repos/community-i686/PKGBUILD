# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-bsddb
pkgname=('python2-bsddb' 'python-bsddb')
_hgrel=600
pkgver=5.1.2.${_hgrel}
pkgrel=1
pkgdesc="Python interface for BerkeleyDB"
license=('MIT')
arch=('i686' 'x86_64')
url="http://www.jcea.es/programacion/pybsddb.htm"
makedepends=('python2-distribute' 'python-distribute' 'mercurial')
source=("ftp://ftp.archlinux.org/other/community/${pkgbase}/${pkgbase}-${pkgver}.src.tar.xz"
        'LICENSE')
sha1sums=('50e7b4c9c11b06c6d871dd93af1cc228687a0227'
         'ef4e4caf618781104dbf5824279ed39d127b4713')

# source PKGBUILD && mksource
mksource() {
   _hgroot="http://hg.jcea.es/pybsddb/"
   _hgrepo=pybsddb
   [ -d "${_hgrepo}" ] && hg pull -u -r ${_hgrel} || hg clone ${_hgroot} -r ${_hgrel}

  _dirname=${pkgbase}-${pkgver}
  mv ${_hgrepo} ${_dirname}
  tar -cJvf ${_dirname}.src.tar.xz ${_dirname}
  rm -fr ${_dirname}
}

build () {
  cd "${srcdir}"
  
  cp -r ${pkgbase}-${pkgver}{,-python2}

  # Build python 3 module
  cd ${pkgbase}-${pkgver}
  python setup.py --berkeley-db=/usr build

  # Build python 2 module
  cd ../${pkgbase}-${pkgver}-python2
  python2 setup.py --berkeley-db=/usr build
}

package_python2-bsddb() {
  depends=('db' 'python2>=2.7')
  cd "${srcdir}/${pkgbase}-${pkgver}-python2"

  python2 setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-bsddb() {
  depends=('db' 'python>=3.2')
  cd "${srcdir}/${pkgbase}-${pkgver}"

  python setup.py --berkeley-db=/usr install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
