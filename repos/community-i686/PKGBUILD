# Maintainer:  Mihails Strasuns <public@dicebot.lv>
# Contributor: Moritz Maxeiner <moritz@ucworks.org>
pkgname=dub
pkgver=1.3.0
pkgrel=1
pkgdesc="Developer package manager for D programming language"
arch=('i686' 'x86_64')
url="https://github.com/D-Programming-Language/dub"
license=('MIT')
makedepends=('dmd' 'libphobos-devel')
depends=('curl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dlang/dub/archive/v${pkgver}.tar.gz)
sha512sums=('71d646d8d50a80bb9a99b1d427f6e2836dd4e65b54a20e85489fc793108c2e027d6d0e2fb7531ca67b70da3afaba328e3ba57563802a675e11d5902f3047172d')

build()
{
  DC=dmd
  cd ${pkgname}-${pkgver}

  echo Generating version file...
  GITVER=${pkgver}
  echo "module dub.version_;" > source/dub/version_.d
  echo "enum dubVersion = \"$GITVER\";" >> source/dub/version_.d
  echo "enum initialCompilerBinary = \"$DC\";" >> source/dub/version_.d

  $DC -ofbin/dub -w -g -version=DubUseCurl -Isource -L-lcurl @build-files.txt
}

package()
{
  cd ${pkgname}-${pkgver}
  install -D -m755 bin/dub "${pkgdir}/usr/bin/dub"
  install -D -m644 scripts/bash-completion/dub.bash $pkgdir/usr/share/bash-completion/completions/dub
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"
}
