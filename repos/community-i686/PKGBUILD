# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=5.6.5
_srcver=${pkgver//./}
pkgrel=2
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('i686' 'x86_64')
url="http://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
source=("http://www.cryptopp.com/cryptopp${_srcver}.zip"
        'libcrypto++.pc')
md5sums=('df5ef4647b4e978bba0cac79a83aaed5'
         'ea27f28fe0303fcaa24d83cf4a964d53')

build() {
  CXXFLAGS+=" -DNDEBUG -fPIC" make -f GNUmakefile-cross  
  make libcryptopp.so
}

package() {
  install -d ${pkgdir}/usr/{lib/pkgconfig,include/cryptopp}
  install -m644 *.h ${pkgdir}/usr/include/cryptopp/
  install -m644 libcryptopp.so "${pkgdir}/usr/lib/libcryptopp.so"
  install -m644 ${srcdir}/libcrypto++.pc "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
