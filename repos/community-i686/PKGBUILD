# $Id: PKGBUILD 133367 2015-05-13 12:31:10Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=3.0.77
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='http://moosefs.com/'
replaces=('mfs-master' 'mfs-chunkserver' 'mfs-client')
conflicts=('mfs-master' 'mfs-chunkserver' 'mfs-client')
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fuse' 'python2' 'bash' 'xfsprogs')
install="$pkgname.install"
source=("http://ppa.moosefs.com/src/moosefs-${pkgver}-1.tar.gz")
sha256sums=('07364ea631f93707e1ea12e4e6002b707d8d4eb160de20a139ae0a73c818ca29')

build() {
  cd "moosefs-${pkgver%_*}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --with-default-user=mfs \
    --with-default-group=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system
  make
}

package() {
  make DESTDIR="$pkgdir" -C "moosefs-${pkgver%_*}" install

  # Move mount.moosefs from sbin to usr/bin
  mv "$pkgdir/sbin/mount.moosefs" "$pkgdir/usr/bin/mount.moosefs"
  rmdir "$pkgdir/sbin"
}


# getver: moosefs.com/download/sources.html
# vim:set ts=2 sw=2 et:
