# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Douglas Soares de Andrade <dsandrade@gmail.com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=sqlitebrowser
pkgver=3.9.0
pkgrel=1
pkgdesc="SQLite Database browser is a light GUI editor for SQLite databases, built on top of Qt"
arch=('i686' 'x86_64')
url="http://sqlitebrowser.org/"
license=('GPL')
depends=('qt5-base' 'sqlite' 'libxkbcommon-x11')
makedepends=('cmake' 'qt5-tools')
#source=(https://github.com/sqlitebrowser/sqlitebrowser/archive/sqlb-$pkgver.tar.gz)
source=("$pkgname-$pkgver.tar.gz::https://github.com/sqlitebrowser/sqlitebrowser/archive/v$pkgver.tar.gz")
md5sums=('6471af3f02e1802ec4548d561ee8f4ae')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|Icon=.*|Icon=sqlitebrowser|' distri/sqlitebrowser.desktop
}

build() {
  cd $srcdir/$pkgname-$pkgver
  cmake -DUSE_QT5=TRUE -DCMAKE_INSTALL_PREFIX=/usr .
  sed -i \
    -e 's|DB Browser for SQLite_OBJECTS|DB_Browser_for_SQLite_OBJECTS|g' \
    -e 's|DB Browser for SQLite_EXTERNAL_OBJECTS|DB_Browser_for_SQLite_EXTERNAL_OBJECTS|g' \
    "CMakeFiles/DB Browser for SQLite.dir/build.make"
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's|DB Browser for SQLite\.|sqlitebrowser.|g' cmake_install.cmake
  make install DESTDIR=$pkgdir
  install -Dm0644 distri/sqlitebrowser.desktop $pkgdir/usr/share/applications/sqlitebrowser.desktop
  install -dm0755 $pkgdir/usr/share/icons/
  install -m0644 images/sqlitebrowser.svg $pkgdir/usr/share/icons/
}
