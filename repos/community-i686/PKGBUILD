# $Id$
# Maintainer:  Martin Wimpress <code@flexion.org>
# Contributor: Kyle <kyle@gmx.ca>

pkgname=java-atk-wrapper
pkgver=0.33.2
pkgrel=1
pkgdesc="ATK wrapper for screen reader access to Java Swing applications"
arch=(i686 x86_64)
url="https://git.gnome.org/browse/java-atk-wrapper/"
license=('LGPL')
depends=('gtk2' 'java-runtime' 'xorg-xprop' 'gtk3')
makedepends=('java-environment')
source=("http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz" removeNotExistingManifestInclusion.patch)
md5sums=('a1884b4e1a00730088f9a83d56be4b95'
         '0bab0ce964961111b3abe5f1b4ecb392')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i ../removeNotExistingManifestInclusion.patch # Fedora patch
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

# make install is broken
  mv "$pkgdir"/build/.local/share/java-atk-wrapper/usr/lib/jvm "$pkgdir"/usr/lib
  rm -r "$pkgdir"/build
}
