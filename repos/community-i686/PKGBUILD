# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

pkgname=afl
pkgver=2.05b
pkgrel=1
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url="http://lcamtuf.coredump.cx/afl/"
arch=('i686' 'x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=(
  'gcc: gcc instrumentation support'
  'clang: clang instrumentation support'
  'llvm: experimental clang-fast instrumentation support'
  'gnuplot: graph plotting support'
)
makedepends=('clang' 'llvm' 'gcc')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(${pkgname}-${pkgver}.tgz::http://lcamtuf.coredump.cx/${pkgname}/releases/${pkgname}-${pkgver}.tgz)
sha512sums=('9cda831a7374187c0fd0f88d91d83f5de6dfbfb10c2301ec0298a613193e5a25e868c3432c1caccf9e469c5c3b8199b2f8464059d3ab85703bbb5c14ad541dc9')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm "${pkgdir}/usr/share/doc/${pkgname}/README.llvm"
}

# vim: ts=2 sw=2 et:
