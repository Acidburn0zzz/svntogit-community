# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=ShellCheck
pkgname=shellcheck
pkgver=0.4.5
pkgrel=1
pkgdesc="Shell script analysis tool"
url="http://www.shellcheck.net"
license=("GPL")
arch=('i686' 'x86_64')
depends=('libffi' 'gmp')
makedepends=("ghc=8.0.1" "haskell-json" "haskell-mtl" "haskell-parsec" "haskell-quickcheck"
             "haskell-regex-tdfa")
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('53039ac314b99af691a99aec111572ee51b0579280c7fa5795ac48d0c4e02fa7')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    # Remove library and doc (license only)
    rm -r "$pkgdir"/usr/{lib,share}
}
