# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=3.20
pkgrel=2
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('i686' 'x86_64')
depends=('gmp' 'opencl-icd-loader')
optdepends=('libxnvctrl: NVIDIA X driver configuration support')
makedepends=('opencl-headers')
license=('MIT')
source=(https://hashcat.net/files/${pkgname}-${pkgver}.tar.gz{,.asc}
        fix-make-flags.patch)
sha512sums=('2d227b6d73eeb06461dba995368cad2e1b0df9ab5c60a6aec101a30cca213ff50424576b1e6674d362e258bb8fd2158ca6c19925d781f9fefcaa8024217460af'
            'SKIP'
            '04d86c0480c221f3e781534b0a9a50950a376a8c798ebee8042446f5d2e11f78677f7c670deb5f132fcf38c962ea13c64ac522e2a18712efbad559a5077dc211')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/fix-make-flags.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
