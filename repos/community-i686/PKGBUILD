# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=viking
pkgver=1.1
pkgrel=2
pkgdesc="GTK+2 application to manage GPS data"
arch=('i686' 'x86_64')
url="http://viking.sourceforge.net/"
license=('GPL2')
depends=('curl' 'gpsd' 'gtk2' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gpsbabel: for GPS management')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/viking/$pkgname-$pkgver.tar.gz
	gpsd-2.96.patch)
sha256sums=('5c34a76c09b6fda092e8170f7546606814382cf9d13174e87c4ee43fffa36743'
            '7277a6f0bbe7b16440ca92a5975c6f0b38261f13bbbb6f04e4fc684b8965c902')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -p1 <$srcdir/gpsd-2.96.patch
  autoreconf

  ./configure --prefix=/usr
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
