# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: BlackEagle <ike.devolder@gmail.com>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine
pkgver=1.3.1
pkgrel=8
pkgdesc='A modern music player and library organizer'
url='http://www.clementine-player.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('chromaprint' 'crypto++' 'fftw' 'gcc-libs' 'glew' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'libcdio' 'libechonest' 'libgl'
         'libgpod' 'liblastfm' 'libmtp' 'libpulse' 'libx11' 'protobuf' 'qjson'
         'qt4' 'sqlite' 'taglib' 'zlib')
makedepends=('boost' 'cmake' 'mesa' 'sparsehash')
optdepends=('gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'gst-libav: Libav plugin'
            'gvfs: Various devices support')
source=("clementine-${pkgver}.tar.gz::https://github.com/clementine-player/Clementine/archive/${pkgver}.tar.gz"
        clementine-gcc6.patch::"https://github.com/clementine-player/Clementine/commit/e31278.patch"
        clementine-1.3.1-chromaprint-1.4.0.patch::"https://github.com/clementine-player/Clementine/commit/ded3126.patch")
sha256sums=('f885931a9ab7c88607d07b50c64fcce46fc05f13dd2c0a04188c94eff938f37c'
            '785c485d0a2398a31728a012f6869573f6714359c0d65fbc06f7532c86b82643'
            'cf5c1c625885cc4436722ac38dc201cdc491478dc73e171ea2af95bfb59fd0c6')

prepare() {
  cd Clementine-${pkgver}

# Fix build with GCC 6
  patch -p1 -i ../clementine-gcc6.patch
  # Fix build with chromaprint 1.4.0
  patch -p1 -i ../clementine-1.3.1-chromaprint-1.4.0.patch

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd Clementine-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd Clementine-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
