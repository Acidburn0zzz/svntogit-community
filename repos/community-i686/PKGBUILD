# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hasktags
pkgver=0.69.2.1
pkgrel=1
pkgdesc="Produces ctags \"tags\" and etags \"TAGS\" files for Haskell programs"
url="http://github.com/MarcWeber/hasktags"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=8.0.1" "haskell-hunit" "haskell-json" "haskell-utf8-string")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9d369830ac5e146f7b2a2ed584eec732b0da85af00ed7b91a72e208f6ef5144a')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
            -f-debug
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

    rm -r "$pkgdir"/usr/{lib,share/doc}
}
