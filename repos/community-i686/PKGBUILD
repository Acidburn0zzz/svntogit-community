#$Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=qmmp
pkgver=0.6.6
pkgrel=5
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt' 'taglib' 'xdg-utils')
makedepends=('cmake' 'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libcdio-paranoia' 'libcddb' 'libmms' 'libsamplerate' 'libmodplug' 'libsndfile' 'wavpack' 'projectm' 'mesa')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libcdio-paranoia: Compact Disc input and control support'
            'libcddb: CDDB server support'
            'libmms: MMS stream protocol support'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'projectm: visual efects'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
source=(http://qmmp.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2
        cdio-includes.patch)
sha256sums=('dfa973cca80c020a85a11bb66701a3804f9fde326440abb179559c98bf3b5b99'
            '7b3ff46dd30070cd79c5ca2dd6dfa1b9872bc09041f4bd985edf20a4333db07c')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/cdio-includes.patch

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_INSTALL_LIBDIR=/usr/lib
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install

  if [[ $CARCH == 'x86_64' ]]; then
    mv $pkgdir/usr/lib64 $pkgdir/usr/lib
  fi
}
