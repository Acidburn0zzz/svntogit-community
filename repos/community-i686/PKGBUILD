# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: bitwave <aur [aT] oomlu [d0T] de>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=yara
pkgver=3.5.0
pkgrel=1
pkgdesc='Tool aimed at helping malware researchers to identify and classify malware samples'
url='https://github.com/VirusTotal/yara'
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('openssl' 'file' 'python-setuptools' 'python2-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/VirusTotal/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('52b815c59ad086020bd8a002d13b89d671a218c2f3a3214b93fb215f18f7f4b57ed6d27c2aa03218626eb1da1f541a0b1d0b20a3d467950089e5150bc90fe76b')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf --force --install
  ./configure --prefix=/usr --with-crypto --enable-magic
  make
}

package() {
  depends=('openssl' 'file')
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
