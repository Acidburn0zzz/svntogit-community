# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-ghc-paths
pkgver=0.1.0.9
pkgrel=13
pkgdesc="Knowledge of GHC's installation directories"
url='http://hackage.haskell.org/package/ghc-paths'
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=8.0.1')
source=("https://hackage.haskell.org/packages/archive/ghc-paths/$pkgver/ghc-paths-$pkgver.tar.gz")
sha256sums=('afa68fb86123004c37c1dc354286af2d87a9dcfb12ddcb80e8bd0cd55bc87945')

build() {
  cd "ghc-paths-$pkgver"

  runhaskell Setup configure \
    -O \
    -p \
    --enable-split-objs \
    --enable-shared \
    --prefix=/usr \
    --docdir="/usr/share/doc/$pkgname" \
    --libsubdir=\$compiler/site-local/\$pkgid

  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script

  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/ghc-paths-$pkgver"

  install -Dm744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -Dm744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
  install -dm755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
    "$pkgdir/usr/share/doc/ghc/html/libraries/ghc-paths"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
