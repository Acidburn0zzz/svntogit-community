# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Jkkyll Wu <adaptee at gmail [dot] com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgbase=qbittorrent
pkgname=(qbittorrent qbittorrent-nox)
pkgver=3.3.3
pkgrel=2
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org"
license=('custom' 'GPL')
makedepends=('libtorrent-rasterbar' 'qt5-base' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'boost' 'qt5-tools')
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/$pkgname-$pkgver.tar.xz"
        'qbittorrent.service'
	'qbittorrent@.service')
sha256sums=('9c8d8bdc3fb2632019cb3ee5f811baaa41b41fe7ce542c753c6771bcc110db53'
            '8c5879673c66368ada97f6d70a1d8fe3b6a4995f79aab4fc6bf54fbdcbe811d0'
            '12dfd06104eaf302b79328c8096248c051208f69348e33f7fb2e4a2fb49caa29')

build() {
  cd $pkgbase-$pkgver

  mkdir $pkgbase
  pushd $pkgbase
  ../configure --prefix=/usr
  make

  # Build nox variant
  popd
  mkdir "$pkgbase-nox"
  pushd "$pkgbase-nox"
  ../configure --prefix=/usr --disable-gui
}

package_qbittorrent() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar."
  depends=('libtorrent-rasterbar' 'qt5-base' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
  optdepends=('python: needed for torrent search tab')
  install=$pkgname.install

  cd $pkgbase-$pkgver/$pkgbase

  make INSTALL_ROOT="$pkgdir/" install
  install -Dm644 "$srcdir/$pkgbase-$pkgver/COPYING" "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_qbittorrent-nox() {
  pkgdesc="An advanced BitTorrent client programmed in C++, based on Qt toolkit and libtorrent-rasterbar, w/o gui"
  depends=('libtorrent-rasterbar' 'qt5-base')

  cd $pkgbase-$pkgver/$pkgbase-nox

  make INSTALL_ROOT="$pkgdir/" install
  install -Dm644 "$srcdir/$pkgbase-$pkgver/COPYING" "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm755 "$srcdir/qbittorrent.service" "$pkgdir/usr/lib/systemd/user/qbittorrent.service"
  install -Dm755 "$srcdir/qbittorrent@.service" "$pkgdir/usr/lib/systemd/system/qbittorrent@.service"
}
