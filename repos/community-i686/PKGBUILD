# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=camlp5
pkgver=5.15
pkgrel=1
pkgdesc='A preprocessor-pretty-printer of OCaml.'
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
conflicts=('camlp5-transitional')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/${pkgname}-${pkgver}.tgz")
md5sums=('67ccbf37ffe33dec137ee71ca6189ea2')
options=('!makeflags')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -strict 

  make world.opt 
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install 

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
