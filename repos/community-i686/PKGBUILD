# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=17.07
pkgrel=2
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"cache_tab::git://github.com/processone/cache_tab#commit=b6af49e8f71c69cedd044017e06d7e2022a3173e"
	"elixir::git://github.com/elixir-lang/elixir#commit=867da6f876c48279823a41a33030c95ef37379b2"
	"epam::git://github.com/processone/epam#commit=0ead169d7f6a3f2d0b64cca282487e4fedfbffd9"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=fe18688ecdfe2d31da595f16a35e0d54a5e04b54"
	"ezlib::git://github.com/processone/ezlib#commit=e42ce61e60dd312ac60ef299d7f6f4522af577e5"
	"fast_tls::git://github.com/processone/fast_tls#commit=d0f8fc07657f4409e1d115cbf63e98015525913f"
	"fast_xml::git://github.com/processone/fast_xml#commit=46c24036ff3c6150ad7dabb95e1aaff73a6437e6"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=a1649cd561a315ad40a1461f8a2038680cd146b2"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=8f1b715d36b650ec1e1f5612c00e28af6ab0de82"
	"hamcrest::git://github.com/basho/hamcrest-erlang.git#commit=98bc7aa19ea081478c816824aa05fc5a48acae66"
	"iconv::git://github.com/processone/iconv#commit=888cbaa256bd8eff17ba73742a4b2f5706135cc6"
	"jiffy::git://github.com/davisp/jiffy#commit=1febce3ca86c5ca5d5a3618ed3d5f125bb99e4c5"
	"lager::git://github.com/erlang-lager/lager#commit=a10c23c51dea6bbd63e5048c8a5a702169973114"
	"luerl::git://github.com/rvirding/luerl#commit=f7b2cc0ab6fa4245ebeda0169fc994aff0628bf9"
	"meck::git://github.com/eproxus/meck#commit=70d6a33ce7407029dc59e22a5b3c1c61c1348b23"
	"moka::git://github.com/processone/moka.git#commit=3eed3a6dd7dedb70a6cd18f86c7561a18626eb3b"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=eab8ad1ef00bb953e61ea80b40443a7f7c5a833c"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=34f9b20fd68134a4646130bdcf1abf320f815a00"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=d33b40694f510b88481e6a62356183799fbf1b24"
	"p1_utils::git://github.com/processone/p1_utils#commit=2431d02f3364cbd21411a2a5b2c0ee37b3d7da6a"
	"protobuffs::git://github.com/basho/erlang_protobuffs.git#commit=0d22220a8e56adab129836d6ff582a9bca103a0c"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"riakc::git://github.com/basho/riak-erlang-client#commit=261df630afe4a998e099a8ab209a2dc7419f85de"
	"riak_pb::git://github.com/basho/riak_pb#commit=9ca2395f9d6464b82bab466bbdca410cb85a0ac7"
	"samerlib::git://github.com/processone/samerlib#commit=fbbba035b1548ac4e681df00d61bf609645333a0"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=6ee3570060fbbf793a0bc873831e8eabb6c39851"
	"stringprep::git://github.com/processone/stringprep#commit=c4bc2aa6ebb6444dce7546d6818ac9506be274f6"
	"stun::git://github.com/processone/stun#commit=15fc3daa90f39936bfdddc8f7c06b20d5992c517"
	"xmpp::git://github.com/processone/xmpp#commit=2826fc70da079cc7216fafce3829c798c57e1a19"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf")
sha256sums=('576b7bf7374d2f31df2692e23af27b34365f724e0d58675910011a6907cbd8f2'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '94582b1df76f6d86abfcc78bf4c4ac79276761c75483dd40a45b7ff2b19dc8a8'
            '0599a3d20153d620e9ac4fdb5bd7370854136235382a2fed4110b1950cf07d20')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in cache_tab elixir eredis esip ezlib fast_tls fast_xml fast_yaml \
    goldrush hamcrest iconv jiffy lager luerl meck moka p1_mysql p1_oauth2 \
    epam p1_pgsql p1_utils protobuffs rebar_elixir_plugin riakc riak_pb \
    samerlib sqlite3 stringprep stun xmpp; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
  sed -i "s|REQUIRE_ERLANG_MAX=\".*\"|REQUIRE_ERLANG_MAX=\"9.0.1\"|" configure.ac
  sed -i 's#R15|R16|17|18#&|19|20#' deps/riakc/rebar.config
  sed -i 's|crypto:rand_bytes|crypto:strong_rand_bytes|g' deps/esip/src/esip.erl
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/epam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 "$srcdir"/ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd/priv/bin/epam
}

# vim:set ts=2 sw=2 et:
