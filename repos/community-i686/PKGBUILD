# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.9
pkgrel=1
pkgdesc='Deepin Window Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'granite' 'deepin-mutter' 'gnome-desktop'
         'libgee')
makedepends=('gnome-common' 'intltool' 'vala' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz")
sha512sums=('4de4aa7e823bbc4259b2a6e56e0cd5413671b964bb2bc2c1d7e6c1be63db6ff1aa04748bb1fd5ecd862967010360acae79ad2b2db39bc44c142a7511e8101f10')

prepare() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
