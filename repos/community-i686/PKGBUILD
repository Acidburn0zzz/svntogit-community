# $Id$
# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <facter_aur (at) emeraldreverie.orgr>
# Contributor: Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail.com>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>

pkgname=facter
pkgver=3.7.1
pkgrel=1
pkgdesc="Collect and display system facts"
arch=('i686' 'x86_64')
url="http://puppetlabs.com/facter"
license=('APACHE')
depends=('ruby' 'yaml-cpp' 'boost-libs' 'curl')
makedepends=('boost' 'cmake' 'java-environment' 'leatherman' 'cpp-hocon')
optdepends=('java-environment: jruby support')
replaces=('cfacter')
source=(http://downloads.puppetlabs.com/$pkgname/$pkgname-$pkgver.tar.gz
        1581.patch)
md5sums=('65180901d3815c6ac306eba7b36c49dc'
         '6b2fddd8709f235e2e971e603f959cf6')

prepare() {
  cd $pkgname-$pkgver

  # Replace rb_data_object_alloc symbol with rb_data_object_wrap
  # https://tickets.puppetlabs.com/browse/FACT-1291
  sed -i 's/rb_data_object_alloc/rb_data_object_wrap/g' \
    $( grep -rl rb_data_object_alloc lib/src/ruby )

  # Openssl 1.1 patch
  # https://tickets.puppetlabs.com/browse/FACT-1588
  patch -p1 -i ../1581.patch
}

build() {
  cd $pkgname-$pkgver

  JAVA_HOME=/usr/lib/jvm/default cmake -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
