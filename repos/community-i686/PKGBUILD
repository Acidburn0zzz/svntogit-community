# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Grayhatter
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Madotsuki <madotsuki at cock dot li>

pkgname=utox
_pkgname=uTox
pkgver=0.12.1
pkgrel=2
_signature="grayhatter"
pkgdesc='Lightweight Tox client'
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')
url="https://github.com/uTox/uTox"
license=('GPL3')
depends=('fontconfig'
         'libfilteraudio'
         'libxext'
         'libxrender'
         'openal'
         'toxcore'
         'v4l-utils')
optdepends=('gtk3: GTK file picker' 'libdbus: Notification support')
makedepends=('toxcore>=0.1.0' 'cmake')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/uTox/uTox/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgname}/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-v${pkgver}.${_signature}.tar.gz.asc")
sha512sums=('a5b4a0f9b8986c2493f69833b18470a973456cf3457b3d3dee0d2100fef7ec1b7fb470b1ed442056313a4de87f8edbc926564741c31e0112fb661d51c25b9b40'
            'SKIP')
validpgpkeys=("15D3B9A6B3951DF9854FCA93E786548AE0A0B56B"  # RobinLindén<mail+gitlab@robinlinden.eu>
              "4C4E8E7E23A85F2474DC2F1A0558CBEA7BE857DD") # Grayhatter

prepare() {
    # Fix man path (fixed in 0.12.2) https://github.com/uTox/uTox/pull/654
    cd "$_pkgname-${pkgver}/src/xlib"
    sed -i 's#"man/man1"#"share/man/man1"#g' CMakeLists.txt
}

build() {
  cd "$_pkgname-${pkgver}"
  CFLAGS="-Wl,-z,noexecstack" cmake . -DCMAKE_BUILD_TYPE=Release -DENABLE_ASAN=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$_pkgname-${pkgver}"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
}
