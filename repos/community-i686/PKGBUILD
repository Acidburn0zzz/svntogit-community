# $Id: PKGBUILD 146876 2012-01-19 02:37:24Z eric $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.0.6
pkgrel=1
pkgdesc="Portable library mainly aimed at video game and multimedia programming"
arch=('x86_64' 'i686')
url="http://alleg.sourceforge.net/"
license=('custom')
depends=('jack' 'libxpm' 'libxxf86vm' 'libxxf86dga' 'libxcursor' 'libpng' 'libgl' 'libjpeg')
makedepends=('cmake' 'mesa' 'freetype2')
source=("http://downloads.sourceforge.net/alleg/$pkgname-$pkgver.tar.gz")
sha256sums=('d981a1cac937085e6636e597c492ddb743066516c1d215c2a39e4049bd70dc24')

build() {
  mkdir build
  cd build
  cmake "../$pkgname-$pkgver" \
  	-DCMAKE_BUILD_TYPE=Release \
  	-DCMAKE_INSTALL_PREFIX=/usr \
	-DWANT_DOCS=OFF
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
  install -Dm644 "../$pkgname-$pkgver/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
