# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Markus Martin <markus@archwyrm.net>

pkgname=alure
pkgver=1.1
pkgrel=3
pkgdesc='Utility library to help manage common tasks with OpenAL applications.'
arch=('i686' 'x86_64')
url='http://kcat.strangesoft.net/alure.html'
license=('LGPL')
depends=('openal')
makedepends=('cmake' 'libsndfile' 'libvorbis' 'flac' 'mpg123' 'dumb' 'fluidsynth')
optdepends=('libsndfile: for uncompressed audio support'
            'libvorbis: for OGG Vorbis support'
            'flac: for FLAC support'
            'mpg123: for MPEG support'
            'dumb: for IT, XM, S3M and MOD support'
            'fluidsynth: for SoundFont 2 support')
source=("http://kcat.strangesoft.net/alure-releases/${pkgname}-${pkgver}.tar.bz2")
md5sums=('a2f6934d3783c8478c460965a13e4e12')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
