# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: <david@pako.st>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=chicken
pkgver=4.8.0
pkgrel=1
pkgdesc='Compiler for R5RS scheme, featuring handy extensions'
arch=('x86_64' 'i686')
url='http://www.call-with-current-continuation.org/'
license=('BSD')
depends=('gcc')
source=("http://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('5b2e8c6b4b13b502d8c684b394d80ed490d2884c03789353d895ab85d19979ab')

build() {
  cd $pkgname-$pkgver

  # parallel builds are not supported..
  export MAKEFLAGS="${MAKEFLAGS/-j?/}"

  # some yummy make options there. check out README in the tarball.
  make PLATFORM=linux PREFIX=/usr
}

package() {
  cd $pkgname-$pkgver
  
  # parallel builds are not supported..
  export MAKEFLAGS="${MAKEFLAGS/-j?/}"
  make PLATFORM=linux PREFIX=/usr DESTDIR=$pkgdir install

  install -D -m644 LICENSE \
    $pkgdir/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
