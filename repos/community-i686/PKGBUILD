# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=dvdstyler
pkgver=1.8.4.3
pkgrel=1
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'libexif' 'wxsvg' 'xine-ui' 'mjpegtools' 'libgnomeui')
makedepends=('zip' 'xmlto' 'docbook-xsl')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
source=(http://downloads.sourceforge.net/dvdstyler/DVDStyler-${pkgver}.tar.bz2 dvdstyler-ffmpeg.patch)
md5sums=('418de47c4e09a89a633b905af2a7d4fb'
         '4f053158965b2be3ae26506470aeda58')

build() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  patch -p1 -i "${srcdir}/dvdstyler-ffmpeg.patch"
  sed -i 's/rmdir "$$t"//' docs/Makefile.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/DVDStyler-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
