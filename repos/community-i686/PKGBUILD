# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org> 
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>

pkgname=perl-sub-identify
pkgver=0.13
pkgrel=1
pkgdesc="Retrieve names of code references"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
checkdepends=('perl-test-pod')
url='http://search.mcpan.org/dist/Sub-Identify'
source=("http://search.mcpan.org/CPAN/authors/id/R/RG/RGARCIA/Sub-Identify-$pkgver.tar.gz")
sha512sums=('16655ea9324202e578ab44139816d61ef961d61c97e2393c19337692670a3f7a27d8dacb7db7b1130ed762f3b6dfc98fe39fe379b546b32576c19bdfa7df70a0')

build() {
  cd "$srcdir/Sub-Identify-$pkgver"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make test
}

package() {
  cd "$srcdir/Sub-Identify-$pkgver"
  make DESTDIR="$pkgdir"/ install
}

# vim:set ts=2 sw=2 et:
