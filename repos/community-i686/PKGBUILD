# $Id$
# Maintainer: Aaron Bull Schaefer <aaron@elasticdog.com>
# Contributor: Ron Huang <ronhuang+aur at gmail dot com>

pkgname=fcgiwrap
pkgver=1.0.3
_gitrev='2e301c8'
pkgrel=1
pkgdesc='A simple server for running CGI applications over FastCGI'
arch=('i686' 'x86_64')
url='http://nginx.localdomain.pl/wiki/FcgiWrap'
license=('MIT')
depends=('fcgi' 'spawn-fcgi')
backup=('etc/conf.d/fcgiwrap')
source=("http://github.com/downloads/gnosek/fcgiwrap/$pkgname-$pkgver.tar.gz"
        'fcgiwrap.conf.d' 'fcgiwrap.rc.d' 'LICENSE')
md5sums=('be73d90df7c4442084463e2815fc213d' '9fbb5b0e861ffea74e655143a4c1a8f9'
         '71ea12f43aebca39f73ff2e0b684f361' '5aee62c27b4308f25ab32f05da387366')

build() {
  cd "$srcdir/gnosek-$pkgname-$_gitrev"

  autoreconf --install
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/gnosek-$pkgname-$_gitrev"

  make DESTDIR="$pkgdir" install
  install -D -m644 ../fcgiwrap.conf.d "$pkgdir/etc/conf.d/$pkgname"
  install -D -m755 ../fcgiwrap.rc.d "$pkgdir/etc/rc.d/$pkgname"
  install -D -m644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
