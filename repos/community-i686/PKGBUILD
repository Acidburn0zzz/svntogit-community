# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgname=distorm
pkgver=3.3.3
pkgrel=1
pkgdesc="Powerful disassembler library for x86/AMD64"
url='https://github.com/gdabah/distorm'
arch=('i686' 'x86_64')
license=('BSD')
depends=('python2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gdabah/distorm/archive/v${pkgver}.tar.gz)
sha512sums=('abc37d050efcb7fd7c6e0f1ef055537f06059528fb48a07d159621adca8d905ee3a44b394d75e10aa69f2429fb1023e9282b349281acb3a9474148f195306fbf')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|(install)|\1 -D|g' -i make/linux/Makefile
  sed -r 's|/usr/local/lib|${DESTDIR}/usr/lib/${TARGET}|g' -i make/linux/Makefile
  sed -r 's|(CFLAGS	)=|\1+=|g' -i make/linux/Makefile
  sed -e '1i#!/usr/bin/env python2' -i python/distorm3/sample.py
}

build() {
  cd ${pkgname}-${pkgver}
  make -C make/linux
}

package() {
  cd ${pkgname}-${pkgver}

  make -C make/linux DESTDIR="${pkgdir}" install
  python2 setup.py install --root="${pkgdir}" -O1

  install -Dm 755 python/distorm3/sample.py "${pkgdir}/usr/bin/disasm"
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
