# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Tom Killian <tomk@runbox.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libnewt
pkgver=0.52.12
_tclver=8.6
pkgrel=5
pkgdesc="Not Erik's Windowing Toolkit - text mode windowing with slang"
arch=('i686' 'x86_64')
url='http://www.redhat.com/'
license=('GPL')
depends=('slang' 'popt' 'gpm')
makedepends=("tcl>=$_tclver" 'python2')
optdepends=('tcl: whiptcl support' \
            'python2: libnewt api through _snack module')
options=('!makeflags')
source=("https://fedorahosted.org/releases/n/e/newt/newt-$pkgver.tar.gz")
sha256sums=('2ba88dc3d118daf509c58e3707c43ad57dd3415d8164054e93fe76439f348529')

build() {
  cd "$srcdir/newt-$pkgver"
  
  sed -i "s:tcl8.4:tcl$_tclver:" Makefile.in
  sed -i 's:bin/python:bin/python2:' *.py
  ./configure --prefix=/usr --with-gpm-support
  echo '#define USE_INTERP_RESULT 1' >> config.h
  make
}

package() {
  cd "$srcdir/newt-$pkgver"
  
  make prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
