# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=0.0.4
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('dtkwidget' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('2777948b7acd0f220b4aba43571dde0dff6a1c88d8b6914df5da0cf63fc3de2b5c0538d4cb32e1ae460081b3b35ffffdf97ad94c1409c11426d0dfb09ce46aca')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' deepin-clone.pro deepin-clone-ionice deepin-clone-pkexec com.deepin.pkexec.deepin-clone.policy{,.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
