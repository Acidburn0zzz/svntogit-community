# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sebastian Lau <archlinux _at_ slau _dot_ info>
# Contributor: Eric Ozwigh <ozwigh at gmail dot com>

pkgname=veracrypt
_pkgname=VeraCrypt
pkgver=1.18
pkgrel=1
pkgdesc='Disk encryption with strong security based on TrueCrypt'
url='http://veracrypt.codeplex.com/'
arch=('i686' 'x86_64')
license=('custom:TrueCrypt')
depends=('fuse>=2.8.0' 'wxgtk>=3.0' 'libsm' 'device-mapper')
optdepends=('sudo: mounting encrypted volumes as nonroot users'
            'desktop-file-utils: update desktop database')
makedepends=('nasm' 'pkg-config')
source=("${pkgname}_${pkgver}_Source.tar.bz2::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.tar.bz2/download"
        "${pkgname}_${pkgver}_Source.tar.bz2.sig::https://sourceforge.net/projects/${pkgname}/files/${_pkgname} ${pkgver}/${_pkgname}_${pkgver}_Source.tar.bz2.sig/download"
        no-makeself.patch
        no-exec-stack.patch
        veracrypt.desktop)
sha512sums=('cf2140253179c8d130b890a00d4ef448695b726abcad5099bce84b1721eec833c84dc72995d54f48317f5ecd8e517e75462c809b65606f15be988d4d41a2234e'
            'cacccc246c8725d47a880266c0cdb7ada26bf50a159bc78581b32c1ac181787a195a93fdb8e61e975028cebf8be27e535ab3ca28fa9c03c1cf5bf91f762d9ff9'
            'e3972fd239fd83807c5ea9dd9268a0e962b0bd9cb9cf8f6251ac51a44a94f3c57f0b1e6857b42e4296cff531030c8278c543b386ebf463d4e19fe9a536acbcf9'
            '94f1b0bd00a84a8294c8efa72058dba71acbe5c846c9761874faa36ba9c3f1ac3d8a7d3b7452c62e2baf770efa4eecf2d989fc6d2181b3b3e11808e5cd6eb726'
            'f689ca64bac7042030de7714aed8cc89f2c5f87b407444b1b121491c1d89c147babaaa454ddc2a93b70ae20d4da59f96ad64f01b04bea9017d658c377faeb75d')
validpgpkeys=('993B7D7E8E413809828F0F29EB559C7C54DDD393') # VeraCrypt Team <veracrypt@idrix.fr>

prepare() {
  cd src
  patch -p1 < "${srcdir}/no-makeself.patch"
  patch -p0 < "${srcdir}/no-exec-stack.patch"
}

build() {
  cd src
  make PKG_CONFIG_PATH=/usr/lib/pkgconfig \
    WX_CONFIG=/usr/bin/wx-config \
    TC_EXTRA_LFLAGS+="-ldl ${LDFLAGS}" \
    TC_EXTRA_CXXFLAGS="${CXXFLAGS}" \
    TC_EXTRA_CFLAGS="${CFLAGS}"
}

package() {
  cd src
  install -Dm 755 Main/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "${srcdir}/veracrypt.desktop" -t "${pkgdir}/usr/share/applications"
  install -Dm 644 Resources/Icons/VeraCrypt-256x256.xpm "${pkgdir}/usr/share/pixmaps/veracrypt.xpm"
  install -Dm 644 License.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
