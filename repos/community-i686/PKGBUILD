# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=5.0
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=(i686 x86_64)
url="http://www.xm1math.net/texmaker/index.html"
license=(GPL)
depends=(qt5-script)
optdepends=('ghostscript: LaTeX to HTML conversion command')
source=("http://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('2a0b329c0ff5c408f72bcefbef16361d26da74e3ca1f39d6b8c78d84c37245a9')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr texmaker.pro
  make
}
package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
