# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.2.0
pkgrel=2
pkgdesc="Light eye-candy fully themable animated dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/cairo-dock-core"
license=('GPL')
depends=('curl' 'dbus-glib' 'gtk3' 'librsvg')
makedepends=('cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
options=('!libtool')
source=("http://launchpad.net/${pkgname}-core/3.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz" 'gldit-rpath.patch')
sha256sums=('69d57dab16e70bb0683ba91f1dfd19a9ee0de0b1642ca6480fef9dacbb9c2fd2'
            '5a5fbc67aaa210387ef4410701747fe741942c99c4bd84ae771b96a3bdd1c4cc')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i ../gldit-rpath.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
