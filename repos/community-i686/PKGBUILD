# $Id$
#Maintainer: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=4.0
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=('i686' 'x86_64')
url="http://www.xm1math.net/texmaker/index.html"
license=('GPL')
depends=('poppler-qt' 'qtwebkit')
optdepends=('ghostscript: LaTeX to HTML conversion command'
	    'ibus-qt: accented characters support'
	    'poppler: PDF rendering support')
install="$pkgname.install"
source=("http://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2")
md5sums=('3599fd142d8d3596b7bd14724fc1888e')

build() {  
  cd "$pkgname-$pkgver"
  qmake PREFIX=/usr texmaker.pro
  make
}
package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -dm755 "$pkgdir/usr/share"
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
