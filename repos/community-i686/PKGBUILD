pkgname=pacutils
pkgver=0.4.0
pkgrel=1

pkgdesc='Helper tools for libalpm'
url='https://github.com/andrewgregory/pacutils'
arch=('i686' 'x86_64')
license=('MIT')

depends=('pacman')
makedepends=('git')

source=("git+https://github.com/andrewgregory/pacutils.git#tag=v$pkgver"
        "git+https://github.com/andrewgregory/mINI.c.git"
        "git+https://github.com/andrewgregory/tap.c.git")
sha1sums=('SKIP' 'SKIP' 'SKIP')

prepare() {
  cd pacutils
  git submodule init
  git config submodule.ext/mini.c.url "$srcdir/mINI.c"
  git config submodule.t/tap.c.url "$srcdir/tap.c"
  git submodule update
}

build() {
  cd pacutils
  make SYSCONFDIR=/etc LOCALSTATEDIR=/var
}

check() {
  cd pacutils
  make check
}

package() {
  cd pacutils
  make DESTDIR="$pkgdir" PREFIX=/usr install
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/"$pkgname"/COPYING
}

# vim: set ft=PKGBUILD et sw=2:
