# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=1.0.10
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-tool-kit')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('5b3ff283f50ea0beaf2461c6b17a97b7ff250209cada0d4bc59fbd636ce316069530d59a32abf07361a432ecbe9a012626788dac4ce5074c8ce59852de7fdf21')

build() {
  cd dde-calendar-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-calendar-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
