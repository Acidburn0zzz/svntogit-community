# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor:  Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jakob Matthes <jakob.matthes@gmail.com>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: rakoo (AUR)
# Contributor: SanskritFritz (gmail)

pkgname=bup
pkgver=0.28
pkgrel=1
pkgdesc='Efficient backup system based on the git packfile format'
arch=('x86_64' 'i686')
url='https://bup.github.io/'
license=('GPL')
depends=('python2-fuse' 'par2cmdline' 'pylibacl' 'python2-pyxattr' 'git')
makedepends=('ruby-ronn' 'git' 'setconf' 'pandoc')
checkdepends=('rsync' 'python2-tornado')
optdepends=('python2-tornado: for bup web')
source=("git://github.com/bup/bup.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd $pkgname

  # Make test suite happy
  git config --global user.email "bob@zombo.com"
}

build() {
  cd $pkgname
  ./configure
  make
}

check() {
  cd $pkgname
  make test -j1
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
