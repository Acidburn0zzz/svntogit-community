# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=netsurf
pkgver=2.8
pkgrel=3
pkgdesc="Lightweight and fast web browser"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/"
license=('GPL')
depends=('libmng' 'curl' 'libglade' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'hubbub' 'libcss' 'hubbub')
makedepends=('re2c' 'lcms')
provides=('netsurf')
conflicts=('netsurf')
install=netsurf.install
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://www.netsurf-browser.org/downloads/releases/$pkgname-$pkgver-src.tar.gz"
        "$pkgname.desktop")
md5sums=('4f28912e5df4b826b819e175265bcc9c'
         '49c41f281f3e9ca28a24da505fad2e3a'
         '85e0af265e77607596fd90db9ac707ad')

build() {
  cd "$srcdir/$pkgname"

  sed 's:libpng:libpng15:' -i gtk/Makefile.target
  sed 's:libpng:libpng15:' -i monkey/Makefile.target
  make PREFIX=/usr TARGET=gtk
}

package() {
  cd "$srcdir/$pkgname"

  make install PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
