# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=1.8.2
pkgrel=1
pkgdesc='IPv6 Router Advertisement / Router Solicitation daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc')
arch=('i686' 'x86_64')
install=radvd.install
source=("http://www.litech.org/radvd/dist/$pkgname-$pkgver.tar.gz" 'radvd.rc.d')
md5sums=('ac8a862d2b232d25ea2622274a2da8a4'
         '65a585f5850a1ac2a0c2c2a7096ec92a')

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --mandir=/usr/share/man

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=$pkgdir install

  install -Dm644 radvd.conf.example \
    $pkgdir/usr/share/doc/radvd/radvd.conf.example
  install -Dm644 COPYRIGHT \
    $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT 
  install -Dm755 $srcdir/radvd.rc.d $pkgdir/etc/rc.d/radvd
}
