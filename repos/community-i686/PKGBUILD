# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=dvdstyler
pkgver=1.8.2
pkgrel=1
pkgdesc="Cross-platform DVD authoring application"
arch=('i686' 'x86_64')
url="http://www.dvdstyler.de/"
license=('GPL')
depends=('cdrkit' 'dvdauthor' 'dvd+rw-tools' 'ffmpeg' 'gettext' 'libexif' 'wxgtk' 'wxsvg' 'xine-ui' 'mjpegtools' 'xmlto' 'docbook-xsl' 'libgnomeui')
makedepends=('zip')
optdepends=('dvdisaster: ECC support')
options=('!makeflags')
source=("http://downloads.sourceforge.net/dvdstyler/DVDStyler-${pkgver}.tar.bz2")
md5sums=('0cb08645b75d08e48baae02450f63902')

build() {
  cd DVDStyler-${pkgver}

  ./configure \
    --prefix=/usr

  sed -i '/^LINGUAS=/d' locale/Makefile
  sed -i '/^LINGUAS_ALL=/d' locale/Makefile
  sed -i '5i LINGUAS=cs da de el es fr it pt sk' locale/Makefile
  sed -i '6i LINGUAS_ALL=cs da de el es fr it pt sk' locale/Makefile

  make
}

package() {
  cd DVDStyler-${pkgver}

  make DESTDIR=${pkgdir} install
}
