# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-persistent
pkgname=('python-persistent' 'python2-persistent')
pkgver=4.2.4
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('i686' 'x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('6af8e8209c5c40d1079161bafa98e24b026299a264cd5e53399584d4ada24e6be2b5651b244fe82878e91e9f8757ddb2f5e3b02b21bb50c0e1d48c2198c03409')

prepare() {
  cp -a persistent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/persistent-$pkgver
  python setup.py build

  cd "$srcdir"/persistent-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/persistent-$pkgver
  nosetests3

  cd "$srcdir"/persistent-$pkgver-py2
  nosetests2
}

package_python-persistent() {
  depends=('python-zope-interface')

  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-persistent() {
  depends=('python2-zope-interface')

  cd persistent-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
