# $Id: PKGBUILD,v 1.21 2009/04/09 09:51:51 sergej Exp $
# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
pkgname=ejabberd
pkgver=2.0.4
pkgrel=1
pkgdesc="An erlang jabber server"
arch=(i686 x86_64)
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang' 'pam')
makedepends=()
backup=(etc/ejabberd/ejabberd.cfg \
	etc/logrotate.d/ejabberd)
install=ejabberd.install
source=(http://www.process-one.net/en/projects/ejabberd/download/$pkgver/$pkgname-${pkgver}.tar.gz \
	ejabberd \
	ejabberdctl.patch \
	ejabberd.logrotate)
md5sums=('1ff37c329e9bc1ddf3c458e051b81d63'
         'd000f6ab2b26a04501cbbc1558214f00'
         '79895c2944531d906f107d0cbebdd1ab'
         '65b55a547952097d354a73c33e34b11e')

build() {
  cd $srcdir/$pkgname-$pkgver/src
  sed -i "s|EJABBERDDIR = \$(DESTDIR)@prefix@/var/lib/ejabberd|EJABBERDDIR = \$(DESTDIR)@prefix@/usr/lib/ejabberd|g" ./Makefile.in
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-pam --enable-odbc
  make || return 1
  make DESTDIR=$pkgdir install && \
  mv $pkgdir/var/lib $pkgdir/usr/ && \
  install -D -m 0755 $srcdir/ejabberd $pkgdir/etc/rc.d/ejabberd && \
  install -d $pkgdir/var/spool/ejabberd && \
  install -d $pkgdir/var/lib/ejabberd || return 1

  patch -p0 $pkgdir/usr/sbin/ejabberdctl <$srcdir/ejabberdctl.patch || return 1

  install -D -m0644 $srcdir/ejabberd.logrotate $pkgdir/etc/logrotate.d/ejabberd
}
