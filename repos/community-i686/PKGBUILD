# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.0
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkitgtk3' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview3' 'gtkspell3' 'gnome-icon-theme' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils' 'intltool')
install=$pkgname.install
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('67eaddbc33a079a74b65bf5f71ef163573f7d4a6f29ed6dca2ad6811d95519b4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-ssl \
        --with-gpgme=gpgme-config \
        --with-gss \
        --with-ldap \
        --with-gtksourceview \
        --with-gtkspell \
        --with-rubrica \
        --with-sqlite \
        --without-nm \
        --without-gnome \
        --with-html-widget=webkit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
