# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.4
pkgrel=1
_pkgrev=2027
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64' 'i686')
url='http://code.google.com/p/grafx2/'
license=('GPL2')
install=grafx2.install
depends=('sdl_ttf' 'sdl_image' 'lua' 'desktop-file-utils')
makedepends=('gendesk' 'setconf')
options=('!emptydirs')
source=("http://grafx2.googlecode.com/files/$pkgname-$pkgver.$_pkgrev-src.tgz")
sha256sums=('97088d07f1bcd668c619da9f1efe7c5782da707306c90c05564df91f51afe9d7')
_name=('GrafX2')
_exec=('grafx2 %U')
_genericname=('Drawing program')
_mimetype=('image/bmp;image/gif;image/png;image/x-tga;image/tiff')

build() {
  cd $srcdir
  gendesk -n
  setconf $pkgname.desktop Categories 'Graphics;2DGraphics;RasterGraphics'
  cd $pkgname/src
  make
}

package() {
  cd $srcdir/$pkgname/src

  make prefix=/usr DESTDIR=$pkgdir install
  install -Dm644 $srcdir/$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop
  install -Dm644 $srcdir/$pkgname/doc/README.txt \
    $pkgdir/usr/share/doc/$pkgname/readme.txt
}

# vim:set ts=2 sw=2 et:
