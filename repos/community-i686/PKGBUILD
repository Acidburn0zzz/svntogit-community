# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=(python-cypari2 python2-cypari2)
pkgver=1.0.0
pkgrel=1
pkgdesc="Cython bindings for PARI"
arch=(i686 x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(cython2 cython python2-cysignals python-cysignals)
source=("http://mirrors.mit.edu/sage/spkg/upstream/cypari/cypari2-1.0.0.tar.bz2")
#source=("https://github.com/defeo/cypari2/releases/download/$pkgver/$pkgbase-$pkgver.tar.bz2")
sha256sums=('606b7a49667d4f3f29734f40f3ec80ab442cce033cae69fedda74c5cc224f539')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cypari2() {
  depends=(python-cysignals)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-cypari2() {
  depends=(python2-cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"
}
