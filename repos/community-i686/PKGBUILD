# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=0.9.93
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
source=("https://github.com/libpinyin/libpinyin/archive/${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/libpinyin/model6.text.tar.gz")
noextract=("model6.text.tar.gz")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/model6.text.tar.gz" "${srcdir}/${pkgname}-${pkgver}/data/"
  sed -i '/wget.*model6\.text\.tar\.gz/ d' ${srcdir}/${pkgname}-${pkgver}/data/Makefile.am

  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd "${srcdir}/libpinyin-$pkgver"
  make DESTDIR="${pkgdir}" install
}
sha512sums=('7214c7913ed116a5557e89958d0d117e7a2781dcbbcc8b1c6713fb569ca121a5763e5cf1dedfdb4607638f773875c374112865d3a306876906a126954c4ef387'
            '8719d99b2efb22438ec067c046f8d45addb3e0d336e8d6ec4b8ed22370fe81366d19a34934bb059230f86e74c379acb62822ee3458850437880d531de8ff3e0e')
