# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=2.2.7
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('i686' 'x86_64')
url="https://github.com/manateelazycat/deepin-terminal"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect' 'deepin-shortcut-viewer' 'deepin-menu'
         'deepin-manual')
makedepends=('vala' 'cmake')
groups=('deepin-extra')
#options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/manateelazycat/deepin-terminal/archive/$pkgver.tar.gz")
sha256sums=('4a1f37f5929425e463207eb8af349dc81aab0a5889c2b35020920bcbb00b8c1e')

prepare() {
  mkdir -p build

  sed -i 's|return __FILE__;|return "/usr/share/deepin-terminal/project_path.c";|' deepin-terminal-$pkgver/project_path.c
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../deepin-terminal-$pkgver
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
