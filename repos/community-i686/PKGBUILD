# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: napa3um <napa3um@gmail.com>

pkgname=wxsqlite3
pkgver=3.4.1
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('i686' 'x86_64')
url=" http://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk' 'sqlite')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('0070eff9c7d2b2c2b1e6223c364c7027168667c0752eca60e0fa5640db3f9cb9')

prepare() {
  cd $pkgname-$pkgver
  ln -s build30 build
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install

  # license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
