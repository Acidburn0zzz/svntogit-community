# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.0.16
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'dtkwidget' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon' 'xorg-server'
         'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('0cb3de8e76b4c232789426c4b2808316f551099926ef79cc1239492bafebeb83025fc360cf27d80af7fcddc7c2c4099aa1356c30e72a72ab736c8be9946e6a5e')

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
