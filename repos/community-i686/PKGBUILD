# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
# Contributor: Matt Portas <matt.r.portas(at)gmail(dot)com>

pkgname=ghdl
_gccver=4.3.4
pkgver=0.29
pkgrel=4
arch=('i686' 'x86_64')
pkgdesc='A complete VHDL simulator, using GCC technology.'
url='http://ghdl.free.fr/'
license=('GPL')
depends=('mpfr')
makedepends=('gcc-ada')
source=("http://ghdl.free.fr/${pkgname}-${pkgver}.tar.bz2"
        "ftp://gcc.gnu.org/pub/gcc/releases/gcc-${_gccver}/gcc-core-${_gccver}.tar.bz2"
        'gcc_pure64.patch')
md5sums=('fd3dc7a71ba521e442bef76f48177767'
         '575b3220bb8e7060939c429fc8608d22'
         '22cec272f9cc2801d3cd348feaca888b')

build() {
  mv ${pkgname}-${pkgver}/vhdl gcc-${_gccver}/gcc

  cd gcc-$_gccver

  if [ ${CARCH} = 'x86_64' ]; then
    patch -Np1 -i ${srcdir}/gcc_pure64.patch
  fi

  ./configure \
    --prefix=/usr \
    --infodir=/usr/share/info \
    --libdir=/usr/lib \
    --libexecdir=/usr/lib \
    --mandir=/usr/share/man \
    --enable-clocale=gnu \
    --enable-languages=vhdl \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --disable-bootstrap \
    --disable-libstdcxx-pch \
    --disable-multilib

  make -j1
}

package() {
  cd gcc-${_gccver}

  make -j1 DESTDIR=${pkgdir} install

  # Clean up
  rm ${pkgdir}/usr/bin/{cpp,gcc{,bug},gcov,${CHOST}-gcc{,-${_gccver}}}
  rm ${pkgdir}/usr/share/man/man1/{cpp,gcc,gcov}.1
  rm ${pkgdir}/usr/share/man/man7/{fsf-funding,gfdl,gpl}.7
  rm ${pkgdir}/usr/share/info/{cpp{,internals},gcc{,install,int},libgomp}.info
  rm ${pkgdir}/usr/lib/lib{iberty.a,gcc_s*,gomp*,mudflap*,ssp*}
  rm -r ${pkgdir}/usr/{include,share/locale}
}
