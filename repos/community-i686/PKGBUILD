# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

_pkg=OpenImageIO
pkgname=openimageio
pkgver=0.10.4
pkgrel=5
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew')
makedepends=('cmake' 'qt' 'python2' 'boost')
optdepends=('qt: iv image viewer' 'python2: bindings support')
options=(!buildflags)
source=("https://github.com/$_pkg/oiio/tarball/Release-${pkgver/_/-}"
        "openimageio-0.10.2-libpng15.patch")
md5sums=('08a1bc2b82862ad5547bba569f4f633f'
         'd12829808610a54b6bc3350dc5419db3')

build() {
  cd "$srcdir"/$_pkg*

  # remove insecure rpath
  sed -i "/RPATH/d" src/CMakeLists.txt

  patch -d src/ -Np0 < $srcdir/openimageio-0.10.2-libpng15.patch

  # strange bug on i686, see https://github.com/OpenImageIO/oiio/issues/204
  [[ $CARCH == "i686" ]] && make debug || make
}

package() {
  cd "$srcdir"/$_pkg*/dist/*

  # prepare dirs
  install -d "$pkgdir"/usr/{share/doc/$_pkg,lib/python2.7/site-packages}

  # bin
  cp -r bin include lib "$pkgdir/usr"

  # docs
  cp -r doc/* "$pkgdir/usr/share/doc/$_pkg"

  # python module
  cp -r python/* "$pkgdir/usr/lib/python2.7/site-packages"

  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
