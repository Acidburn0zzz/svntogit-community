# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.29
pkgrel=3
pkgdesc="Qt5 Configuration Utility"
arch=(i686 x86_64)
url="http://qt5ct.sourceforge.net"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2" qt5ct-fix-color-scheme.patch)
sha256sums=('a016142a04a5edfbdda627fc203622f512fae9e84bcf89ce63a7107f01721adc'
            '171c5802dc77a63aa7fff806b5f63b8f4fe7e1f52653f4c11d8597ae910a4af7')

prepare() {
  cd $pkgname-$pkgver
# Fix color scheme https://sourceforge.net/p/qt5ct/tickets/32/
  patch -p3 -i ../qt5ct-fix-color-scheme.patch
}

build() {
    cd $pkgname-$pkgver
    qmake-qt5 $pkgname.pro 
    make
}

package() {
    cd $pkgname-$pkgver
    make INSTALL_ROOT="$pkgdir" install

    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

