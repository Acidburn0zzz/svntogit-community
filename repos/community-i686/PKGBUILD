# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=1.8.8
pkgrel=4
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'clutter-gtk' 'gjs' 'gnome-bluetooth'
         'gnome-icon-theme' 'gnome-menus' 'gnome-settings-daemon' 'gnome-session'
         'gnome-themes-standard' 'gstreamer0.10' 'libgnome-keyring' 'librsvg'
         'networkmanager' 'muffin' 'pygtk' 'python2-dbus' 'python2-imaging'
         'python2-pyinotify' 'python2-lxml' 'webkitgtk3' 'gnome-settings-daemon-compat'
         'gnome-panel')
makedepends=('gnome-common' 'intltool')
optdepends=('cinnamon-control-center: extended configurations for Cinnamon'
            'cinnamon-screensaver: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nemo: the official Cinnamon file manager (forked from Nautilus)')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/archive/$pkgver.tar.gz"
        "remove_GC.patch"
        "keyboard_applet.patch"
        "fix-control-center-check.patch"
        "gnome-3.8.patch"
        "switch-applications.patch"
        "fallback-helpers.patch"
        "idle-dim.patch"
        "background.patch"
        "bluetooth_obex_transfer.patch"
        "disable-mpris-support.patch")
sha256sums=('1bce982e6333e7bd27a1df9f37eb9139360c2fef667c7a998a79f216d4a0921d'
            '3d362efd15f8cfeca1713f5bcf88d4be787b39d7c7f24b73cd13f867af33a680'
            'a0c05c995102b16f1060cbd43931eeaefeafd0265a0335e4ca14a143bd4c8c30'
            'ee5694bdc997ffa35a817f691b15bae13747137d35ec2aecd0da298d7edbe426'
            '01508c4f41664d5e29f700dc77c9f5c5441f128ab759f0ae8325c5fdda70b00e'
            '921a1f63d2890dd54c149aa27a3d3209ac2fb843be597ae4ef3b4621e76a2262'
            'fc8e8f5b7772ff331212280b0d4cf624c5ca2a442e8e8defc319cc2f2b060f2e'
            'b34c30299fb88228c59f36fced90d56346847019a080bc7b8157b72caa659100'
            '373d80cdb23250fbde846ed493ba422672cc42b03a111c2ce044467ee782df7f'
            '4497f3e0a97c364845d8a3a3b3b75e7dc9d475dc39f56b2106f8c4b9e5111ac2'
            'f1eb8110718434e1dc2cf0a308757a787390382dfccd1c8cc80488b93cf6a9f7')

build() {
  cd ${srcdir}/Cinnamon*

  # Python2 fix
  sed -i 's|#! /usr/bin/python|#! /usr/bin/python2|' \
    files/usr/bin/$pkgname-menu-editor \
    files/usr/share/$pkgname/applets/panel-launchers@$pkgname.org/$pkgname-add-panel-launcher.py
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Fix crasher when disconnecting from wifi
  patch -Np1 -i ../remove_GC.patch

  # Fix Keyboard applet
  # https://github.com/linuxmint/Cinnamon/issues/1337
  patch -Np1 -i ../keyboard_applet.patch

  # Check for the cc-panel path, not for the unneeded binary
  patch -Np1 -i ../fix-control-center-check.patch

  # https://github.com/linuxmint/Cinnamon/pull/1888
  patch -Np1 -i ../gnome-3.8.patch

  # Fix windows switcher keybinding with default settings in GNOME 3.8
  patch -Np1 -i ../switch-applications.patch

  # Start media keys and mount helpers with the session
  patch -Np1 -i ../fallback-helpers.patch

  # Fix brightness applet for GNOME 3.8
  patch -Np1 -i ../idle-dim.patch

  # Fix missing backgrounds
  patch -Np1 -i ../background.patch

  # Remove "Browse Files..." option as it's gone from gnome-bluetooth 3.8
  patch -Np1 -i ../bluetooth_obex_transfer.patch

  # Disable MPRIS support (workaround for FS#35282)
  patch -Np1 -i ../disable-mpris-support.patch

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
  make
}

package() {
  cd ${srcdir}/Cinnamon*
  make DESTDIR="${pkgdir}" install

  # Remove leftover files after patching
  find "$pkgdir" -type f -name *.orig | xargs rm

  # Prefix 'System Settings' with 'Cinnamon' to avoid confusion with gnome-control-center
  sed -i 's/^Name\(.*\)=\(.*\)/Name\1=Cinnamon \2/' "$pkgdir/usr/share/applications/cinnamon-settings.desktop"
}
