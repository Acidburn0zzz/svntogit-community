# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=1.7.2
pkgrel=2
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'clutter-gtk' 'gjs' 'gnome-bluetooth'
         'gnome-icon-theme' 'gnome-menus' 'gnome-settings-daemon' 'gnome-session'
         'gnome-themes-standard' 'gstreamer0.10' 'networkmanager' 'muffin' 'python2-dbus'
         'python2-gconf' 'python2-imaging' 'python2-pyinotify' 'python2-lxml')
makedepends=('gnome-common' 'intltool')
optdepends=('cinnamon-control-center: extended configurations for Cinnamon'
            'cinnamon-screensaver: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nemo: the official Cinnamon file manager (forked from Nautilus)'
            'webkitgtk3: download applets from Spices website in cinnamon-settings')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/$pkgver"
        "gnome-autogen.sh"
        "keyboard_applet.patch"
        "fix-control-center-check.patch")
md5sums=('d136978607d2584ea1ca29db436f96df'
         'a925691c9b57a6a884dcf07da057fd1f'
         '2b1ece84416c3e4de030be15f1774d82'
         'fd429779aa986ffb3f481149f19a0baf')

build() {
  cd ${srcdir}/linuxmint-Cinnamon*

  # Fix Keyboard applet
  # https://github.com/linuxmint/Cinnamon/issues/1337
  patch -Np1 -i ${srcdir}/keyboard_applet.patch

  # Check for the cc-panel path, not for the unneeded binary
  patch -Np1 -i ${srcdir}/fix-control-center-check.patch

  cp ${srcdir}/gnome-autogen.sh .
  sed -i 's/\ --warn-all\ --warn-error//' src/Makefile.am
  sed -i 's/gnome-autogen.sh/.\/gnome-autogen.sh/g' autogen.sh
  chmod +x gnome-autogen.sh

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
 
  make
}

package() {
  cd ${srcdir}/linuxmint-Cinnamon*
  make DESTDIR="${pkgdir}" install

  # Install autostart files for gnome-fallback-mount-helper
  install -Dm644 /etc/xdg/autostart/gnome-fallback-mount-helper.desktop \
    "${pkgdir}/etc/xdg/autostart/gnome-fallback-mount-helper-cinnamon.desktop"
  sed -i 's/^AutostartCondition=.*/AutostartCondition=GNOME3 if-session cinnamon/' \
    "${pkgdir}/etc/xdg/autostart/gnome-fallback-mount-helper-cinnamon.desktop"

  install -Dm644 /etc/xdg/autostart/gnome-fallback-mount-helper.desktop \
    "${pkgdir}/etc/xdg/autostart/gnome-fallback-mount-helper-cinnamon2d.desktop"
  sed -i 's/^AutostartCondition=.*/AutostartCondition=GNOME3 if-session cinnamon2d/' \
    "${pkgdir}/etc/xdg/autostart/gnome-fallback-mount-helper-cinnamon2d.desktop"

  # Python2 fix
  sed -i 's|#! /usr/bin/python|#! /usr/bin/python2|' \
    "${pkgdir}/usr/bin/$pkgname-menu-editor" \
    "${pkgdir}/usr/share/$pkgname/applets/panel-launchers@$pkgname.org/$pkgname-add-panel-launcher.py"

  find "${pkgdir}" -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}


