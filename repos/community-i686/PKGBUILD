# $Id$
# Maintainer:  Lukas Fleischer <archlinux@cryptocrack.de>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>
# Contributor: Ricardo Catalinas Jim√©nez <jimenezrick@gmail.com>

pkgname=erlang
pkgver=R15B02
pkgrel=1
pkgdesc='A general-purpose concurrent functional programming language developed by Ericsson'
arch=(i686 x86_64)
url='http://www.erlang.org'
license=('custom')
depends=('ncurses' 'glibc')
makedepends=('java-environment' 'perl' 'openssl' 'wxgtk' 'unixodbc' 'mesa')
optdepends=('wxgtk: for wx support'
            'mesa: for wx support'
            'unixodbc: for database support'
            'java-environment: for Java support')
source=("http://www.erlang.org/download/otp_src_$pkgver.tar.gz"
        "http://www.erlang.org/download/otp_doc_man_$pkgver.tar.gz")
md5sums=('ccbe5e032a2afe2390de8913bfe737a1'
         '974020ba533242fca759a7f5eaf628e5')

build() {
  cd "$srcdir/otp_src_$pkgver"

  ./configure --prefix=/usr --enable-smp-support
  make
}

package() {
  cd "$srcdir/otp_src_$pkgver"

  make DESTDIR="$pkgdir" install

  # Install documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -m0644 "$srcdir/otp_src_$pkgver"/README.md "$srcdir"/{README,COPYRIGHT} \
    "$pkgdir/usr/share/doc/erlang"

  # Install compressed man pages
  for page in "$srcdir/man/man?/*"; do
    gzip $page
  done
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang"

  # Install license
  install -Dm0644 "$srcdir/otp_src_$pkgver/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"
}
