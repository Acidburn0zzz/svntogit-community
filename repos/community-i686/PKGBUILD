# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pgpdump
pkgver=0.30
pkgrel=1
pkgdesc="PGP packet visualizer which displays the packet format"
url="http://www.mew.org/~kazu/proj/pgpdump"
arch=('i686' 'x86_64')
license=('BSD')
depends=('zlib' 'bzip2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kazu-yamamoto/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('429772b84b9a7de8686d2cc66ef33ba2fb53a1d26c6df9c3ecc911fdcc43bdf4cbf0d124aec9c8e5d59f6d6c481abed046cc7da684f991ff2bf90bc24a8bd30f')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
