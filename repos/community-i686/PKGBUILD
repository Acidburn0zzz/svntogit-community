# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.3.0
pkgrel=1
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('desktop-file-utils'
         'libfilteraudio'
         'libxkbcommon-x11'
         'libxss'
         'openal'
         'opencv'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('qt5-tools')
install=$pkgname.install
source=("https://github.com/tux3/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9147e9f6c80098a6d1c3d9deaf92d5ae2a62894f0628b058e59a66616873aebbca8a1ba04c0436a1b2793ae766a2ba9056a832be1550b47e1799de24216f19e8')

build() {
  cd $_pkgname-$pkgver

  # fixing qtox desktop icon and avoid conflict with status icon
  sed -i "s/Icon=qtox/Icon=qTox/" qTox.desktop

  install -d build
  cd build
  qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO ..
  make
}

package() {
  # executable
  cd $_pkgname-$pkgver

  install -Dm755 build/$pkgname "$pkgdir/usr/bin/$pkgname"

  # xdg desktop file
  install -Dm644 qTox.desktop "$pkgdir/usr/share/applications/qTox.desktop"

  # icons
  cd img/icons
  for _icon in *.png; do
    _size=$(sed 's|^[^-]*-||;s|\.png||' <<< "$_icon")
    install -Dm644 "$_icon" "$pkgdir/usr/share/icons/hicolor/$_size/apps/qTox.png"
  done
  install -Dm644 $pkgname.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/qTox.svg"
}
