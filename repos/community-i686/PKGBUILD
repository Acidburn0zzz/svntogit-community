# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.3.2
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'deepin-tool-kit' 'qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher'
         'deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('282bd74bc654a42a80942845a7b791f1bba8b5148505af259ae25dacaa6ede8cc4dc7ec430a52ae964f8819f772256ec5d44171313948276f322ab403959f99b')

build() {
  cd dde-dock-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
