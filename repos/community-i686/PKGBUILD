# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=libutf8proc
pkgver=1.3.1
pkgrel=1
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=('x86_64' 'i686')
url='http://git.netsurf-browser.org/libutf8proc.git/'
license=('MIT' 'custom')
replaces=('utf8proc')
depends=('glibc')
makedepends=('git' 'ninja' 'cmake')
source=("git://git.netsurf-browser.org/libutf8proc.git#tag=v$pkgver"
        'libutf8proc.pc')
sha256sums=('SKIP'
            '6c6c05ff6c91777987d76f935c24c7a3f67eba12e420a437f03ee1545f404d31')

build() {
  cd "$pkgname"
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -G Ninja
  ninja
}

package() {
  cd "$pkgname"

  install -Dm644 utf8proc.h "$pkgdir/usr/include/utf8proc.h"
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
  install -Dm644 ../libutf8proc.pc "$pkgdir/usr/lib/pkgconfig/libutf8proc.pc"
  install -Dm644 build/libutf8proc.so.1.3.1 "$pkgdir/usr/lib/libutf8proc.so.1.3.1"
  ln -s /usr/lib/libutf8proc.so.1.3.1 "$pkgdir/usr/lib/libutf8proc.so.1"
  ln -s /usr/lib/libutf8proc.so.1.3.1 "$pkgdir/usr/lib/libutf8proc.so"
}

# getver: git.netsurf-browser.org/libutf8proc.git/
# vim:set ts=2 sw=2 et:
