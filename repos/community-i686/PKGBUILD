# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
_pkgbase=mate-netbook
pkgname=${_pkgbase}
pkgver=${_ver}.1
pkgrel=1
pkgdesc="A simple window management tool (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libfakekey' 'mate-panel')
makedepends=('intltool')
groups=('mate-extra')
conflicts=("${_pkgbase}-gtk3")
source=("http://pub.mate-desktop.org/releases/${_ver}/${_pkgbase}-${pkgver}.tar.xz")
sha1sums=('94790796ce349f009482c8bdb8647aef89f6f28b')

build() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexec=/usr/lib/${_pkgbase} \
        --sysconfdir=/etc \
        --with-gtk=2.0
    make
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
