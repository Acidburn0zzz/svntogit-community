# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jon Gjengset <jon@tsp.io>
pkgname=rustup
pkgver=0.7.0
pkgrel=1
pkgdesc="The Rust toolchain installer"
arch=('i686' 'x86_64')
url="https://github.com/rust-lang-nursery/rustup.rs"
license=('MIT' 'Apache')
makedepends=('cargo')
provides=('rust' 'cargo')
conflicts=('rust' 'cargo')
install='post.install'
source=("rustup-${pkgver}.tar.gz::https://github.com/rust-lang-nursery/rustup.rs/archive/${pkgver}.tar.gz")
md5sums=('12fca8508b2f22f25bb703b6a09d654e')

build() {
    msg2 "Building rustup"
    cd "$srcdir/$pkgname.rs-${pkgver}"
    # cargo update
    cargo build --release --features no-self-update --bin rustup-init

    msg2 "Running rustup-init"
    mkdir -p "$srcdir/tmp/.cargo"
    env -u CARGO_HOME "HOME=$srcdir/tmp" target/release/rustup-init -y #--no-modify-path https://github.com/rust-lang-nursery/rustup.rs/issues/856
}

package() {
    cd "$pkgname.rs-${pkgver}"
    install -dm755 "$pkgdir/usr/bin"
    cp "$srcdir/tmp/.cargo/bin"/* "$pkgdir/usr/bin/"
}

# vim:filetype=sh:
