# Maintainer: Anatol Pomazau
# Contributor: Ingo BÃ¼rk <admin at airblader dot de>

pkgname=xcb-util-xrm
pkgver=1.0
pkgrel=3
pkgdesc='XCB utility functions for the X resource manager'
arch=(i686 x86_64)
url='https://github.com/Airblader/xcb-util-xrm'
license=(MIT)
depends=(xcb-util)
makedepends=(xorg-util-macros libx11 git)
source=($pkgname-$pkgver.zip::https://github.com/Airblader/xcb-util-xrm/archive/v$pkgver.zip
        git://anongit.freedesktop.org/xcb/util-common-m4.git)
md5sums=('fae8b245807e8cb07444db60dbcab8f7'
         'SKIP')

prepare() {
  cd xcb-util-xrm-$pkgver
  rm -r m4
  ln -sf $srcdir/util-common-m4 m4
}

build() {
  cd xcb-util-xrm-$pkgver
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd xcb-util-xrm-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

