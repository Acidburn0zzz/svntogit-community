# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname='packagekit'
pkgver=1.1.0
pkgrel=1
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dbus-glib' 'pacman>=5.0.0' 'polkit' 'shared-mime-info' 'sqlite')
makedepends=('gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'bash-completion' 'vala' 'autoconf-archive')
optdepends=('networkmanager: detect connection status'
	'bash-completion: command completion in bash')
install='packagekit.install'
backup=('var/lib/PackageKit/transactions.db'
	'etc/PackageKit/alpm.d/pacman.conf'
	'etc/PackageKit/alpm.d/repos.list')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"{,.asc}
        '0001-alpm-port-to-g_autoptr.patch::https://github.com/eworm-de/PackageKit/commit/80be181752a6ff396147802e818f7c38c1651050.patch')
sha256sums=('4f1d878391ccc63a1dda99006a9f918f54f2568a328991534b5d1a06d042c36a'
            'SKIP'
            'f299d9f1c65659e4d388aa20958e1e2110f2d473cfad07738771ce53423a0dbb')

prepare() {
	cd "${srcdir}/PackageKit-${pkgver}"

	patch -Np1 < "${srcdir}/0001-alpm-port-to-g_autoptr.patch"
}

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--disable-static \
		--disable-gtk-doc \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-dummy \
		--enable-alpm
	make
}

package() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# install directory with root owner, polkit group and
	# correct permission
	install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"

	make DESTDIR="${pkgdir}" install
}

