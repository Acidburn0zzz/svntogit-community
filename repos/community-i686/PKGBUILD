# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Jonas Heinrich <onny@project-insanity.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=libreoffice-online
pkgver=2.1.1
pkgrel=2
pkgdesc="HTML5-based/cloud-based version of the office suite"
arch=('x86_64' 'i686')
url="https://cgit.freedesktop.org/libreoffice/online/"
license=("MPL")
makedepends=("cppunit" "poco" "libreoffice-fresh-sdk" "jake" "npm" "chrpath" "python-polib")
depends=("libpng12" "poco" "pcre" "cpio" "libreoffice" "openssl-1.0")
backup=("etc/loolwsd/loolwsd.xml")
install="libreoffice-online.install"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LibreOffice/online/archive/${pkgver}.tar.gz"
	"loolwsd.service"
	"build-fix.patch")
sha512sums=('fbb939631e9938800389e09842cf0bf079c0c2907f93ba13cc2e1c2dc4950f36a7206f2145c9f905c916820fed4fcd02b1b72765f0d299df804b67d82e3d045f'
            '4a809d8420f42eef99a831b32c94d9ce336b5aa5ccac3c44b99346b94e9d7a6ae8b4e06760057f20e844837c3ab2175146bef63ba99e1a50d016a890704cc2c9'
            '049036898f59f1a7f18c6f186042b7ecee84b010256c813c633e0854b206b1b94343972cc8a374990eb22919a223429de4db712f70a89851f266e6500b67c2b1')

prepare() {
  cd "${srcdir}/online-${pkgver}"
  patch -p1 <"$srcdir"/build-fix.patch
}

build() {
  cd "${srcdir}/online-${pkgver}"
  ./autogen.sh
  export PKG_CONFIG_PATH=/usr/lib/openssl-1.0/pkgconfig
  CPPFLAGS="-I/usr/include/openssl-1.0 -Wno-implicit-fallthrough -Wno-format-truncation" \
  CFLAGS="-I/usr/include/openssl-1.0" \
  LDFLAGS="-L/usr/lib/openssl-1.0" \
      ./configure --enable-silent-rules \
	--with-lokit-path=/usr/include/libreoffice \
	--with-lo-path=/usr/lib/libreoffice \
	--prefix=/usr \
	--sysconfdir=/etc
  BUILDING_FROM_RPMBUILD=yes make
}

package() {
  cd "${srcdir}/online-${pkgver}"
  BUILDING_FROM_RPMBUILD=yes make DESTDIR=${pkgdir} install
  install -Dm644 "${srcdir}/loolwsd.service" "${pkgdir}/usr/lib/systemd/system/loolwsd.service"
  mkdir -p "${pkgdir}/var/lib/lool"
  mkdir -p "${pkgdir}/var/cache/loolwsd"
  mkdir -p "${pkgdir}/var/lib/lool/child-roots"
  chmod u+w "${pkgdir}/var/lib/lool/child-roots"
  sed -i 's|/usr/var/cache/loolwsd|/var/cache/loolwsd|g' ${pkgdir}/etc/loolwsd/loolwsd.xml
  mkdir -p "${pkgdir}/usr/share/loolwsd/loleaflet"
  cp -r "loleaflet/dist" "${pkgdir}/usr/share/loolwsd/loleaflet/"
  chrpath -d "$pkgdir/usr/bin/"{loolmount,loolforkit}
}
