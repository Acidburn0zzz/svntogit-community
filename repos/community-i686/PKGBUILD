# $Id$
# Maintainer:  Chris Brannon <chris@the-brannons.com>
# Maintainer:  Bartłomiej Piotrowski 
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit 
pkgver=2.0.0b10
pkgrel=1
pkgdesc="A Just-In-Time Compiler for Lua"
arch=('i686' 'x86_64')
url="http://luajit.org/"
license=('MIT')
depends=('gcc-libs') 
source=("http://luajit.org/download/LuaJIT-${pkgver/b/-beta}.tar.gz"
        'luaconf.patch'
        'Makefile.patch')
md5sums=('ed66689b96f7ad7bfeffe0b4ff2d63d4'
         'c8da9c6790e20a12a0e2496f438d9dc3'
         '8997c5e5fa4f6e8e0b86cc6b888de631')

build() { 
  cd "$srcdir/LuaJIT-${pkgver/b/-beta}"

  patch -Np0 -i "$srcdir/luaconf.patch"
  patch -Np0 -i "$srcdir/Makefile.patch"

  make
}

package() {
  cd "$srcdir/LuaJIT-${pkgver/b/-beta}"
  make install DESTDIR="$pkgdir" PREFIX=/usr

  install -Dm644 "$srcdir/LuaJIT-${pkgver/b/-beta}/COPYRIGHT" \
                 "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
}
