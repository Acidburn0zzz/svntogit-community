# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=1.1.3
pkgrel=2
pkgdesc='Deepin Image Viewer'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-tool-kit' 'libexif' 'freeimage' 'libraw' 'qt5-svg' 'deepin-shortcut-viewer' 'deepin-manual')
makedepends=('git' 'qt5-tools')
groups=('deepin')
source=("git+https://cr.deepin.io/deepin-image-viewer.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  sed -i 's|#include <exif-data.h>|#include <libexif/exif-data.h>|' deepin-image-viewer/viewer/utils/imageutils_libexif.h

  sed -i '/vendor\/prebuild/d' deepin-image-viewer/viewer/viewer.pro
  sed -i 's|env PKG_CONFIG_PATH=\$\$PWD/lib/pkgconfig ||' deepin-image-viewer/viewer/vendor/vendor.pri
  rm -r deepin-image-viewer/viewer/vendor/{src,dutil-vendor.pri,prebuild}
}

build() {
  cd deepin-image-viewer
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd deepin-image-viewer
  make INSTALL_ROOT="$pkgdir" install

  # Fix broken /share folder
  mv "$pkgdir"/share/kde4 "$pkgdir"/usr/share/kde4
  rmdir "$pkgdir"/share
}
