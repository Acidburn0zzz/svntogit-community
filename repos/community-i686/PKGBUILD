# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.0.1
pkgrel=1
pkgdesc='a lexical analyser generator for Haskell'
arch=('i686' 'x86_64')
url='http://www.haskell.org/alex/'
license=('custom:BSD3')
depends=('gmp')
makedepends=('ghc=7.4.1-2' 'haskell-quickcheck=2.4.2-1')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('5dec239c89165d159faa2154bc0724cd')

build() {
  cd $srcdir/$pkgname-$pkgver
  runghc Setup.lhs configure --prefix=/usr --datasubdir=$pkgname
  runghc Setup.lhs build
}

package() {
  cd $srcdir/$pkgname-$pkgver
  runghc Setup.lhs copy --destdir=$pkgdir
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/alex/BSD3

  # doc/ contains only license which already installed into proper place
  rm -rf $pkgdir/usr/share/doc
}
