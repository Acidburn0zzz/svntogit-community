# $Id: PKGBUILD 145358 2015-10-31 12:11:57Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-mutter
pkgver=3.20.15
pkgrel=1
pkgdesc="Base window manager for deepin, fork of gnome mutter"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-mutter"
license=('GPL3')
depends=('deepin-clutter' 'dconf' 'gobject-introspection-runtime' 'deepin-desktop-schemas'
         'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'upower'
         'libxkbcommon-x11' 'libgudev' 'deepin-cogl')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'intltool')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-mutter/archive/$pkgver.tar.gz")
sha512sums=('6d545a15ad54678660d6ed230945134fff5632e372e20b045efb9289d48ea9aa38ec8cb5d5ae2d2d6bb531ef440d5cf1b3f6bd34604fbdd4bdb4359767ce5609')

build() {
  cd deepin-mutter-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/deepin-mutter \
    --localstatedir=/var \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-native-backend \
    --disable-static \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd deepin-mutter-$pkgver
  make DESTDIR="$pkgdir" install
}
