# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gnome-mplayer
pkgver=1.0.3
pkgrel=1
pkgdesc='A simple MPlayer GUI.'
arch=('i686' 'x86_64')
url='http://gnome-mplayer.googlecode.com/'
license=('GPL')
depends=('mplayer' 'dbus-glib' 'libnotify' 'gtk2' 'dconf' 'hicolor-icon-theme')
makedepends=('gnome-power-manager' 'nautilus')
install="${pkgname}.install"
source=("http://gnome-mplayer.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('72a1c7d640a67eb2d60f2671108919c2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install --without-gconf
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  sed -i 's@Exec=gnome-mplayer %U@Exec=gnome-mplayer %F@' \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
