# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.2.1
pkgrel=1
pkgdesc='Simple scanning utility'
arch=('i686' 'x86_64')
url='http://launchpad.net/simple-scan'
license=('GPL3')
depends=('sane' 'dconf' 'gtk3' 'colord' 'hicolor-icon-theme')
makedepends=('gnome-doc-utils' 'intltool')
install=simple-scan.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('10fba94b60cd5159d54d3491f228ac9fe83ac0ab02b983d7cd8390aa26cf2ecc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
