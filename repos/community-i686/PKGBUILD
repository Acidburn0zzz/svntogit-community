# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=picocom
pkgver=1.7
pkgrel=1
pkgdesc='Minimal dumb-terminal emulation program, very much like minicom'
url='http://code.google.com/p/picocom/'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('glibc')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.gz")
sha1sums=('bde6e36af71db845913f9d61f28dee1b485218fa')

build() {
  cd $pkgname-$pkgver
  make --environment-overrides UUCP_LOCK_DIR=/run/lock
}

package() {
  cd $pkgname-$pkgver
  install -D -m 755 picocom "$pkgdir/usr/bin/picocom"
  install -D -m 644 picocom.8 "$pkgdir/usr/share/man/man8/picocom.8"
}

# vim:set ts=2 sw=2 ft=sh et:
