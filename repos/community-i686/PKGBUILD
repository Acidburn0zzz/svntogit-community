# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
pkgbase=mate-desktop
pkgname=(${pkgbase} ${pkgbase}-gtk3 ${pkgbase}-schemas)
pkgver=${_ver}.0
pkgrel=2
pkgdesc="Library with common API for various MATE modules"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL')
options=('!emptydirs')
depends=('gtk2' 'gtk3' 'startup-notification')
makedepends=('gobject-introspection' 'intltool')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('624d2e7af6416cb81d1d0946ab66bbb34fc7d150')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --with-gtk=2.0 \
        --disable-schemas-compile

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --with-gtk=3.0 \
        --disable-schemas-compile

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package_mate-desktop() {
    pkgdesc+=' (GTK2 version)'
    groups=('mate')
    conflicts=("${pkgbase}-gtk3")
    depends=('dconf' 'gtk2' 'mate-desktop-schemas' 'startup-notification')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
    make -C schemas DESTDIR="${pkgdir}" uninstall
}

package_mate-desktop-gtk3() {
    pkgdesc+=' (GTK3 version)'
    groups=('mate-gtk3')
    conflicts=("${pkgbase}")
    depends=('gtk3' 'mate-desktop-schemas' 'startup-notification')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
    make -C schemas DESTDIR="${pkgdir}" uninstall
}

package_mate-desktop-schemas() {
    pkgdesc='GSettings schemas for MATE Desktop'
    depends=()

    cd "${srcdir}/${pkgbase}-gtk3"
    make -C schemas DESTDIR="${pkgdir}" install
}
