# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=libsieve
pkgver=2.3.1
pkgrel=2
pkgdesc="sieve interpretator lib"
arch=(i686 x86_64)
url="http://libsieve.sourceforge.net/"
license=('GPL')
options=('!libtool')
source=("https://github.com/downloads/sodabrew/libsieve/libsieve-$pkgver.tar.gz"
	"glibc-regex.patch")
md5sums=('bb48eecf877cc7fdb2b0dfe757b48d7a'
         '0d98d9e8f0583857616b2ba98f1833fa')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  rm -rf src/sv_regex
  patch -p1 <$srcdir/glibc-regex.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  aclocal
  automake
  autoconf
  ./configure --prefix=/usr
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver/src/sv_test"
  ls -1 script* | while read script; do
    ls -1 message* | while read msg; do
      msg "checking $script $msg"
      ./example $script $msg || true # upstream bugs?
    done
  done
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
