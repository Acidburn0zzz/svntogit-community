# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7.0.RC4
pkgrel=2
pkgdesc="Script based raytracer for creating 3D graphics"
arch=('i686' 'x86_64')
license=('custom')
url="http://povray.org/"
depends=('libtiff' 'libpng' 'boost-libs')
makedepends=('boost')
backup=("etc/povray/${pkgver%.*.*}/povray.conf" "etc/povray/${pkgver%.*.*}/povray.ini")
source=("http://povray.org/redirect/www.povray.org/beta/source/povray-3.7.0.RC4.tar.gz"
        'povray3.7.0_rc3-user-conf.patch'
        'distribution-license.txt'
        'LICENSE'
        'source-license.txt')
sha256sums=('1dc41976168fe6c4b36840b41154b6c69ad4f1f8da75f96e5f7a2786205dd80d'
            '95b04ec4973e6e8a80fc83df2e3caff0b4d6699c4154144f3017d42cab7236e0'
            'bcf6893c741568bbff6839bb7310b0c43af8cb54bae792da21123e1026f91ae9'
            '9162060797de4f7f91ccf5e1fac5ec3378a0b0fed382393797ba3f4f27896402'
            '7d80992314f7947f5f672a4ad8b1d072a6179f01fb0228a88e4224a8bd45de96')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../povray3.7.0_rc3-user-conf.patch
  COMPILED_BY="Arch Linux" ./configure --prefix=/usr --sysconfdir=/etc \
    --disable-optimiz #--with-boost-thread=boost_thread-mt
  make
}

package() {
  cd "$srcdir"

  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -Dm644 LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 distribution-license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/distribution-license.txt"
  install -Dm644 source-license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/source-license.txt"
}

# vim:set ts=2 sw=2 et:
