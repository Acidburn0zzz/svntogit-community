# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.56
pkgrel=1
pkgdesc="A tool for static C/C++ code analysis"
arch=('i686' 'x86_64')
url="http://cppcheck.wiki.sourceforge.net/"
license=('GPL')
depends=('pcre' 'qt')
makedepends=('docbook-xsl')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('63999e1a492f73408fcad87b4bef3c2775feb7e5')

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make
   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.77.1/manpages/docbook.xsl man

   cd gui
   export HAVE_RULES=yes 
   qmake
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin
}
