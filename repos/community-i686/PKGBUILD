# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.0.14.20170401
_commit=171145c2d3db2c992123e7fa348f21141db638fe
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'deepin-tool-kit' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::http://github.com/linuxdeepin/dde-control-center/archive/$_commit.tar.gz")
sha512sums=('ac19a042ada78d931c988582e2a6b60955ff55f89016e10cb6708aa8069fa1da4ecd8a905543cd59a83cd5da3bd9682ecff8b266cd15ef4d88f35fb45b68c3f1')

build() {
  cd dde-control-center-$_commit
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$_commit
  make INSTALL_ROOT="$pkgdir" install
}
