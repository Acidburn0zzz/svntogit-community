# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.3.2
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('i686' 'x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('36ca2d59a63960b1883e73c70f07d12843fb1fd6117d2ad7e2042240affe611463e81809da0269b260553546128ef22e898acfde4f7e0ca210bbd6dfc2e46dc8')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
