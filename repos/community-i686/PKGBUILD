# $Id$

# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pcb
pkgver=20110918
pkgrel=2
pkgdesc='Interactive printed circuit board editor'
url='http://pcb.gpleda.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gtkglext' 'gd' 'hicolor-icon-theme')
optdepends=('tk: additional tools'
            'tcl: additional tools'
            'perl: additional tools')
makedepends=('intltool' 'tk')
source=("http://downloads.sourceforge.net/pcb/${pkgname}-${pkgver}.tar.gz")
sha1sums=('53ca27797d4db65a068b56f157e3ea6c5c29051f')

install=install

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr \
		--enable-dbus \
		--disable-update-mime-database \
		--disable-update-desktop-database
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make -j1 prefix="${pkgdir}"/usr install

	rm "${pkgdir}"/usr/share/info/dir
	rm "${pkgdir}"/usr/include/gts.h
	rm "${pkgdir}"/usr/lib/libgts.a
	rmdir "${pkgdir}"/usr/include
	rmdir "${pkgdir}"/usr/lib
}
