# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=2.0.12.1
pkgrel=2
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kio libxss vlc)
makedepends=(extra-cmake-modules kidletime kdoctools)
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/src/$pkgname-$pkgver.tar.xz")
sha256sums=('abd3b4a97ae2af930f506652984198e7ec929640f8cb0a6dbb4af6555cd06ec5')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
