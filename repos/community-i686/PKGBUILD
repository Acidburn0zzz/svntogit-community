# $Id$
# Maintainer:
# Contributor : Ionut Biru <ibiru@archlinux.org>
# Contributor: totoloco <totoloco at gmail _dot_com>

pkgname=mysql-workbench
epoch=1
pkgver=5.2.35
pkgrel=1
pkgdesc="A cross-platform, visual database design tool developed by MySQL"
arch=('i686' 'x86_64')
url="http://wb.mysql.com/"
license=('GPL2')
depends=('libzip' 'libmysqlclient' 'lua' 'gtkmm' 'libgl'
         'libsigc++' 'python-paramiko' 'python-pexpect' 'libgnome-keyring')
makedepends=('boost' 'curl' 'mesa' 'python-sphinx')
options=('!libtool')
source=(ftp://ftp.gwdg.de/pub/misc/mysql/Downloads/MySQLGUITools/${pkgname}-gpl-${pkgver}-src.tar.gz
        ArchLinux.xml arch.patch python27.patch gcc46.patch)
md5sums=('e8b158d2ba0413f5659cda7da9cec63e'
         'c2f986cd5737413d020c55db32713c48'
         '23430d24400fd65c9fd872bd8e2411e5'
         '47bc4cdfaa035a0132392e94c462ebf7'
         '43917ebc79794ba15a64f7ba820fa20b')

build() {
  cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

  cp "${srcdir}/ArchLinux.xml" res/mysql.profiles
  patch -Np1 -i ${srcdir}/arch.patch

  patch -Np0 -i ${srcdir}/python27.patch
  patch -Np1 -i ${srcdir}/gcc46.patch

  #we use System provided libraries
  rm -rf ext/curl
  rm -rf ext/yassl
  rm -rf ext/boost
  rm -rf ext/libsigc++

  ./autogen.sh --prefix=/usr --disable-debug --disable-static
  make

  pushd ext/mysql-utilities
  python2 setup.py build
  popd

  pushd ext/connector-python
  python2 setup.py build
  popd
}

package() {
  cd "${srcdir}/${pkgname}-gpl-${pkgver}-src"

  make DESTDIR="${pkgdir}" install

  pushd ext/mysql-utilities
  python2 setup.py install --skip-profile --root $pkgdir --prefix=/usr --optimize=1
  popd

  pushd ext/connector-python
  python2 setup.py install --root $pkgdir --prefix=/usr --optimize=1

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      $(find ${pkgdir} -name '*.py')
}
