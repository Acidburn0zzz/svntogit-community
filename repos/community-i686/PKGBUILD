# $Id: PKGBUILD,v 1.15 2009/02/22 02:59:30 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Aaron, phrakture, Griffin <aaron@archlinux.org>
# Contributor: erkaN kaplaN <erkan@linux-sevenler.org>

pkgname=gwget
pkgver=1.0.1
pkgrel=1
pkgdesc="Gwget is a download manager for the Gnome Desktop."
arch=('i686' 'x86_64')
url="http://gnome.org/projects/gwget/index.html"
license=('GPL')
depends=('libgnomeui' 'libnotify')
makedepends=('perlxml' 'epiphany>=2.24' 'gnome-doc-utils>=0.11.2' 'intltool')
install=gwget.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/1.0/$pkgname-$pkgver.tar.bz2)
md5sums=('780d2d8569032cbc4405b88c33e431cc')
options=('!libtool' '!emptydirs')

build() {
  cd $srcdir/$pkgname-$pkgver

  CFLAGS="-I/usr/include/libgnomeui-2.0 -I/usr/include/libgnome-2.0 ${CFLAGS}"
  
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-epiphany-extension --with-epiphany-version=2.24 \
    --with-extension-prefix=/usr/lib/epiphany/2.24/extensions || return 1
  make || return 1;
  make -j1 GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install || return 1

  mkdir -p $pkgdir/usr/share/gconf/schemas
  gconf-merge-schema $pkgdir/usr/share/gconf/schemas/${pkgname}.schemas \
    $pkgdir/etc/gconf/schemas/*.schemas
  rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
