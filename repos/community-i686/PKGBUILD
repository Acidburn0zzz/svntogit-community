# $Id$
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=mc
pkgver=4.8.5
pkgrel=1
pkgdesc="Midnight Commander is a text based filemanager/shell that emulates Norton Commander"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/mc/"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'pcre' 'gpm' 'slang')
makedepends=('libxt' 'libx11')
optdepends=('p7zip: support for 7zip archives'
	'mtools: a+ extfs'
	'cdparanoia: audio extfs'
	'gawk: hp48+ extfs'
	'cdrkit: iso9660 extfs'
	'perl: needed by several extfs scripts'
	'python-boto: s3+ extfs'
	'python2-pytz: s3+ extfs'
	'p7zip: u7z extfs'
	'unace: uace extfs'
	'unarj: uarj extfs'
	'cabextract: ucab extfs'
	'unrar: urar extfs'
	'zip: uzip extfs')
options=('!emptydirs' '!makeflags')
backup=('etc/mc/edit.indent.rc'
        'etc/mc/edit.spell.rc'
        'etc/mc/filehighlight.ini'
        'etc/mc/mcedit.menu'
        'etc/mc/mc.ext'
        'etc/mc/mc.keymap'
        'etc/mc/mc.menu'
        'etc/mc/sfs.ini')
source=("http://www.midnight-commander.org/downloads/${pkgname}-${pkgver}.tar.bz2")

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure \
	    --prefix=/usr --sysconfdir=/etc --mandir=/usr/share/man \
	    --enable-background --enable-charset --enable-largefile \
	    --with-edit --with-gpm-mouse --with-mmap --enable-vfs-smb \
	    --with-screen=slang --with-subshell --with-vfs --with-x \
	    --without-debug --without-gnome --without-included-gettext \
	    --libexecdir=/usr/lib

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	# Fix FS#15177
	sed 's|op_has_zipinfo = 0|op_has_zipinfo = 1|' \
		-i "${pkgdir}/usr/lib/mc/extfs.d/uzip"

	sed 's#/usr/bin/env python#/usr/bin/python2#' \
		-i "${pkgdir}/usr/lib/mc/extfs.d/s3+"
}

md5sums=('87f03efc65cf962d775c3811d157e402')
