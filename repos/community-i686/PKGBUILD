# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Elmo Todurov <todurov@gmail.com>
# Contributor: Alexander De Sousa <archaur.xandy21@spamgourmet.com>

pkgname=scanmem
pkgver=0.15.5
pkgrel=1
pkgdesc="Memory scanner designed to isolate the address of an arbitrary variable in an executing process"
url="https://github.com/scanmem/scanmem"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('pygtk' 'python-gobject' 'polkit')
makedepends=('intltool')
install=scanmem.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz
        drop-gtk-update-icon.patch)
sha512sums=('e9d5c63a89a30462a8ab65a5426d13d36324984ca341f737a65756ee7b27e5621810ec730dc3c46718ce90400b63b7540594110d177496b43b8fe2ced7618247'
            '5df78e2dcdaf5c2bf355911f9851375ffeed819dd50bfdc47c5cb8096eaa9b7aa69b7852b937b782661e27d213d8543309dedd92ada38e225fd1375e3f12fe3c')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Rp1 < "${srcdir}/drop-gtk-update-icon.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
  ./configure --enable-gui --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
