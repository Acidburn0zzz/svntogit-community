# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=solidity
pkgver=0.4.15
_commit=8b45bddb559d17250c8a5619efa1a21f296d4e03
pkgrel=1
pkgdesc='Contract-Oriented Programming Language'
arch=('i686' 'x86_64')
url='https://github.com/ethereum/solidity'
license=('GPL3')
depends=('boost-libs' 'jsoncpp')
makedepends=('boost' 'cmake' 'git')
source=("git+https://github.com/ethereum/solidity.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
    cd ${pkgname}
    
    commithash=$(git rev-parse --short=8 HEAD)
    echo -n ${commithash} > commit_hash.txt
    echo -n > prerelease.txt

    sed -e '/jsoncpp\.cmake/d' -i CMakeLists.txt
    
    # don't use -Werror, breaks build with GCC 7
    sed -e '/Werror/d' -i cmake/EthCompilerSettings.cmake
}

build() {
    cd ${pkgname}

    mkdir build
    cd build
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTESTS=OFF \
        ..
    make
}

package() {
    cd ${pkgname}/build

    make DESTDIR="${pkgdir}" install
}
