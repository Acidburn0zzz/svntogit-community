# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.0.18
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.2.6
pkgrel=1
pkgdesc="Fully rewritten Sieve implementation for Dovecot v2.0"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
makedepends=()
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://www.rename-it.nl/dovecot/2.0/dovecot-2.0-$pkgname-$pkgver.tar.gz"
        "dovecot.conf")
options=('!libtool')
sha256sums=('49829e4aed763848b63b2bf9e288d4bc94020c924ce0621bc850e0a4bf4821ab'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')

build() {
  cd "$srcdir/dovecot-2.0-$pkgname-$pkgver"

  ./configure --prefix=/usr --with-dovecot=/usr/lib/dovecot
  make
}

package() {
  cd "$srcdir/dovecot-2.0-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}

