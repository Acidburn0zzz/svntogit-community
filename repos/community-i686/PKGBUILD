# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=viking
pkgver=1.2.1
pkgrel=1
pkgdesc="GTK+2 application to manage GPS data"
arch=('i686' 'x86_64')
url="http://viking.sourceforge.net/"
license=('GPL2')
depends=('curl' 'gpsd' 'gtk2' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gpsbabel: for GPS management')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('7c4b36398a9ec121ab11b66f042d17368dd46cf328a2a0e00bda38fd357a9743')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed '/#include <curl\/types.h>/d' -i src/osm-traces.c

  ./configure --prefix=/usr --disable-realtime-gps-tracking --enable-geocaches --enable-expedia 
  make
}

package(){
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
