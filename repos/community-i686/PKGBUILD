# $Id: $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=0.7.0
pkgrel=1
pkgdesc="Personal finances manager for KDE4, allowing you to keep track of your incomes and expenses"
arch=('i686' 'x86_64')
url="http://skrooge.org"
license=('GPL')
depends=('kdebase-runtime' 'libofx' 'qca')
makedepends=('cmake' 'automoc4')
install=${pkgname}.install
source=(http://skrooge.org/files/${pkgname}-${pkgver}.tar.bz2)
md5sums=('9bfbeaef28baaf514c45560266dcdaee')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr
  make || return 1
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
