# $Id: PKGBUILD 175037 2016-05-12 15:18:07Z arojas $
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.2.3-37
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc="A free computer algebra system"
arch=(i686 x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(mpfi gsl pari ntl lapack libpng libjpeg fltk)
source=("http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz")
sha256sums=('5dc0b3d8ec8215910710b38cfcafc4081bc7b40fa817d14d5bb43b54b32101a0')

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

package_xcas() {
  depends=(libgiac fltk)
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
