# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>
pkgname=python-sympy
pkgver=0.6.7
pkgrel=2
pkgdesc="Symbolic manipulation package (Computer Algebra System), written in pure Python"
url="http://code.google.com/p/sympy"
license=('BSD')
depends=('python2')
conflicts=('sympy')
provides=('sympy')
arch=('i686' 'x86_64')
source=(http://sympy.googlecode.com/files/sympy-${pkgver}.tar.gz)
md5sums=('d73e0a5a128f38f930d566110f4b668b')

build() {
  cd "${srcdir}/sympy-${pkgver}"

  # python2 fix
  for file in  sympy/galgebra/GA.py sympy/galgebra/tests/test_GA.py; do
      sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
  done
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' sympy/mpmath/tests/runtests.py

  python2 setup.py install --root "${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
