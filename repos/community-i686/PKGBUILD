# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.1.1
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gtk2' 'gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-tool-kit' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory')
makedepends=('git' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('ee387cf2180f89e883dbcc2f6cf6fd8d5f2cd2f2cc0be11212b9b369ecfd99aeffdb66c8dce2807b63246e048853fb05b5446556260d516fa02fe68ab81b81b1')

build(){
  cd dde-launcher-$pkgver
  qmake-qt5 PREFIX=/usr WITHOUT_UNINSTALL_APP=1
  make
}

package() {
  cd dde-launcher-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
