# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Bjorn Lindeijer <bjorn lindeijer nl>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Jesse McClure <jesse [dot] mcclure [at] umassmed [dot] edu>

pkgname=manaplus
pkgver=1.6.3.12
pkgrel=1
pkgdesc="Free OpenSource 2D MMORPG client for Evol Online, The mana world and similar servers"
arch=('i686' 'x86_64')
url="https://manaplus.org/"
license=('GPL')
depends=('glu' 'libxml2' 'physfs' 'sdl_image' 'sdl_mixer' 'sdl_net' 'sdl_ttf' 'sdl_gfx' 'curl')
makedepends=('mesa')
replaces=('tmw')
source=($pkgname-$pkgver.tar.gz::https://github.com/ManaPlus/ManaPlus/archive/v$pkgver.tar.gz)
sha256sums=('4226d52080da74d67f6462fdc7a584fb2e216d0bf803d56ef9fe0304b7b8cc11')

build() {
  cd ManaPlus-$pkgver

  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd ManaPlus-$pkgver

  make DESTDIR="${pkgdir}" install
}
