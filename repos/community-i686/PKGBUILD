# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=2.92.4
_commit=9d927e1a74a684edea6d7d80bca096952355c75b
#_golibver=0.4.4
_golibrev=c276a48ce147db8cba1951da6164f8979d021cb4
#_dbusfactoryver=2.92.3
_dbusfactoryrev=bef8c791e07beb144312b0eee83b00d1c7f68618
#_girgeneratorver=0.9.1
_girgeneratorrev=6e53a3b74f2076b2f472920cd2bc1ae4752e0608
pkgrel=1
pkgdesc='Go-lang bingdings for dde-daemon'
arch=('i686' 'x86_64')
url="https://gitcafe.com/Deepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill' 'gcc-libs>=5.3.0-3' 'poppler-glib' 'deepin-metacity' 'xcur2png')
makedepends=('git' 'deepin-dbus-generator' 'deepin-gir-generator' 'bzr' 'gcc-go')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=(#"git+https://github.com/linuxdeepin/dde-api.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_dbusfactoryrev"
        "git+https://github.com/linuxdeepin/go-gir-generator.git#commit=$_girgeneratorrev")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i "s|\"\${LDFLAGS}\"|\"\${LDFLAGS} $CFLAGS $LDFLAGS\"|" dde-api/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/lib
  cp -a go-lib/* build/src/pkg.deepin.io/lib

  cd "$srcdir/dde-api"
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2
}

build(){
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/dde-api"
  make USE_GCCGO=1
}

package() {
  cd dde-api
  make DESTDIR="${pkgdir}" SYSTEMD_LIB_DIR=/usr/lib install-binary
}
