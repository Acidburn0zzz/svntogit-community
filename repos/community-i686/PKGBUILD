# $Id$
# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=pcmanfm
pkgver=1.2.2
pkgrel=2
pkgdesc='Extremely fast and lightweight file manager'
arch=('i686' 'x86_64')
url='http://pcmanfm.sourceforge.net/'
license=('GPL')
groups=('lxde')
depends=('gtk2' 'desktop-file-utils' 'libfm' 'lxmenu-data')
makedepends=('intltool')
optdepends=('gvfs: for trash support, mounting with udisks and remote filesystems'
            'udisks: alternative for mounting volumes')
install=$pkgname.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.xz
        0001-Fix-Open-With-default-application-with-glib-2.41.patch)
md5sums=('ac0ba2f8e2b4d47014a62ccf43388e0f'
         'a63df1e714980eb4300f0028aa99f654')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../0001-Fix-Open-With-default-application-with-glib-2.41.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
