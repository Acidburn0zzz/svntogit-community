# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=8.3
pkgrel=1
pkgdesc='Very fast network logon cracker which support many different services'
url='https://www.thc.org/thc-hydra/'
arch=('i686' 'x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'libmariadbclient' 'libidn' 'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2' 'pkg-config')
source=(${pkgname}-${pkgver}.tar.gz::https://www.thc.org/releases/${pkgname}-${pkgver}.tar.gz)
sha512sums=('40479d6ebf98add8e7805ed9447d98a739cb6ac02d4933bed7846a9d5e027f33073fcb03b8f18e68f771007ef56041e6b64f4d01dd68ac1d0f8b2e36325bb499')
DLAGENTS=('https::/usr/bin/curl -fLC - --retry 3 --retry-delay 3 -o %o %u -k')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --fhs
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
