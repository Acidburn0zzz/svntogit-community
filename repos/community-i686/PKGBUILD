# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=0.63.03
pkgrel=2
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg' 'fontconfig' 'libpng'
	 'libtiff' 'libdv' 'libmng')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
options=(!libtool)
source=(http://downloads.sourceforge.net/project/synfig/synfig/$pkgver/synfig-$pkgver.tar.gz
	build-fix.patch
	ffmpeg-0.8.patch)
md5sums=('9bd77cd2fc2381adfbd50a7b9c1c6e7c'
         'b5568b06fae1a7ee465b5380b1139b83'
         'ebf41046097cfd778dee9a9854561dae')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  patch -p1 -i $srcdir/build-fix.patch
  patch -Np1 -i $srcdir/ffmpeg-0.8.patch
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
