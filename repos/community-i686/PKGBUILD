# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=hasql
pkgname=haskell-hasql
pkgver=0.19.18.1
pkgrel=1
pkgdesc="An efficient PostgreSQL driver and a flexible mapping API"
url="https://github.com/nikita-volkov/hasql"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-attoparsec' 'haskell-base-prelude' 'haskell-bytestring-strict-builder'
         'haskell-contravariant' 'haskell-contravariant-extras' 'haskell-data-default-class'
         'haskell-dlist' 'haskell-either' 'haskell-hashable' 'haskell-hashtables' 'haskell-loch-th'
         'haskell-mtl' 'haskell-placeholders' 'haskell-postgresql-binary' 'haskell-postgresql-libpq'
         'haskell-profunctors' 'haskell-semigroups' 'haskell-text' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6a6c9a6d6c2399d5dd91c990a3d6777437124c317405654d6eb182baf8897acbda514ed9d81999878576ddca816c15b537276c5d86ed564d8ff50d2741aecbcd')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
