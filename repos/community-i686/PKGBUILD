# $Id: PKGBUILD 148562 2015-12-05 17:09:20Z foutrelis $
# Maintainer: Alexander F RÃ¸dseth <rodseth@gmail.com>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=5.0.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
url='http://www.grinninglizard.com/tinyxml2/'
arch=('x86_64' 'i686')
license=('zlib')
depends=('gcc-libs')
makedepends=('git' 'cmake' 'ninja')
source=("git+https://github.com/leethomason/tinyxml2#commit=81b219446220f5475ff4b5eaf9b21d99b8c4ed6b")
md5sums=('SKIP')

build() {
  cd "$pkgname"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "$pkgname"

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 readme.md "$pkgdir/usr/share/licenses/$pkgname/README.md"
}

# vim:set ts=2 sw=2 et:
