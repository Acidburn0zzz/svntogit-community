# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=simple-scan
pkgver=3.6.0
pkgrel=1
pkgdesc='Simple scanning utility'
arch=('i686' 'x86_64')
url='http://launchpad.net/simple-scan'
license=('GPL3')
depends=('sane' 'dconf' 'gtk3' 'colord' 'hicolor-icon-theme')
makedepends=('yelp-tools' 'intltool')
install=simple-scan.install
source=(https://launchpad.net/$pkgname/${pkgver:0:3}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('d00684d4697de6753b6633d7c87b9c067fdbf99b732da9007ea2bb6cbdad1cde')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
