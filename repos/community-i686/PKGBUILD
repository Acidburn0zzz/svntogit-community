# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.0.2
pkgrel=1
pkgdesc='a lexical analyser generator for Haskell'
arch=('i686' 'x86_64')
url='http://www.haskell.org/alex/'
license=('custom:BSD3')
depends=('gmp')
makedepends=('ghc=7.4.2-1' 'haskell-quickcheck=2.4.2-2')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
md5sums=('b8f0d831b65d05e5f54ff08bd3d9d10d')

build() {
  cd $srcdir/$pkgname-$pkgver
  runghc Setup.lhs configure --prefix=/usr --datasubdir=$pkgname
  runghc Setup.lhs build
}

package() {
  cd $srcdir/$pkgname-$pkgver
  runghc Setup.lhs copy --destdir=$pkgdir
  install -D -m644 LICENSE $pkgdir/usr/share/licenses/alex/BSD3

  # doc/ contains only license which already installed into proper place
  rm -rf $pkgdir/usr/share/doc
}
