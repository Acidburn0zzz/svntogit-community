# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.1.4
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=('x86_64' 'i686')
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=('BSD')
backup=('etc/stubby/stubby.yml')
depends=('getdns')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/getdnsapi/stubby/archive/v${pkgver}.tar.gz"
        "${pkgname}.sysusers"
        'fix-makefile.patch')
sha256sums=('cc10f253b6f0334cb5865982e8dbb25e012df4fff1ee01ea1860a10d41abf4b1'
            '2acedcf2b65d6e802a70deb55a5befbb71ff83be68a0ba5231a09c126910142b'
            'b9ea6a864f33dfeac7ad6cc20b35956bba3d61c8362512529f212604bc124939')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fix-makefile.patch
}

build() {
    cd ${pkgname}-${pkgver}
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}

    install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 systemd/${pkgname}.conf -t "${pkgdir}"/usr/lib/tmpfiles.d/
    install -Dm644 ../${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
