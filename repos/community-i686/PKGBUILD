# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Tyler Langlois <ty |at| tjll |dot| net>

pkgbase=beats
pkgname=(filebeat packetbeat metricbeat)
pkgver=5.0.0
pkgrel=1
pkgdesc='Data shippers for Elasticsearch'
arch=('i686' 'x86_64')
url='https://www.elastic.co/products/beats'
license=('APACHE')
depends=('glibc')
makedepends=('go' 'git' 'libpcap')
options=('!strip')
source=("https://github.com/elastic/beats/archive/v$pkgver/beats-$pkgver.tar.gz"
        "filebeat.install"
        "filebeat.sysusers"
        "filebeat.service"
        "packetbeat.service"
        "metricbeat.service")
sha256sums=('3e6b7cf2ee5f52e78ae87ef04ab9dd49977c89f86a09416586896aeaea844e34'
            'fd765921479d391d613c75fe592a4193e538dd9feffc1ca36bc687bec0f0e1aa'
            '33feb3690f8b31563cc1e2da557c2aa326501ce9ccd7e0a142036902bfdb05ff'
            '1ab98d3e387c3970e3005f72f4ce266c4fa19ec3938a576da0a407c7db2fa7ce'
            'cabf11f35ced8bb61fc26ace4d409dfdb4ed812ff4bc70e168410cb1dc324318'
            '8a1bcfdb057e4f57d5f16570079055d1822fdfe8d453409891682f6e580f4ff0')

prepare() {
    cd beats-$pkgver

    # Avoid installing extraneous configs
    sed '/[- ]win/d ; /[- ]darwin/d ; /[- ]binary/d' -i */Makefile
    # Install the Linux config as default
    sed -r 's#/([a-z]+)-linux.yml#/\1.yml#' -i */Makefile

    # Perform some timestomping to avoid make warnings
    LANG=C _t="$(date -r Makefile +'%Y-%m-%d %k:%M:%S')"
    touch -m -d "$_t" */Makefile

    # Workaround to place extracted release into GOPATH
    mkdir -p "$srcdir"/gopath/src/github.com/elastic
    ln -sf "$srcdir"/beats-$pkgver \
        "$srcdir"/gopath/src/github.com/elastic/beats
}

build() {
    export GOPATH="$srcdir"/gopath
    for beat in ${pkgname[@]}; do
        msg2 "Building $beat..."
        cd "$srcdir"/beats-$pkgver/$beat
        make
    done
}

package_filebeat() {
    pkgdesc='Collects, pre-processes, and forwards log files to remote sources'
    optdepends=('elasticsearch: for standalone installation')
    backup=("etc/$pkgname/$pkgname.yml" "etc/$pkgname/$pkgname.template.json")
    install="$pkgname.install"

    cd beats-$pkgver/$pkgname

    install -dm755 "$pkgdir"/var/lib/$pkgname

    install -dm755 "$pkgdir"/etc/$pkgname
    cp $pkgname.{yml,template.json} "$pkgdir"/etc/$pkgname

    install -Dm755 $pkgname \
                   "$pkgdir"/usr/bin/$pkgname
    install -Dm644 "$srcdir"/$pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$pkgname.service

    install -Dm644 "$srcdir"/$pkgname.sysusers \
                   "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
}

package_packetbeat() {
    pkgdesc='Network packet analyzer that ships data to Elasticsearch'
    depends=('libpcap')
    optdepends=('elasticsearch: for standalone installation')
    backup=("etc/$pkgname/$pkgname.yml" "etc/$pkgname/$pkgname.template.json")

    cd beats-$pkgver/$pkgname

    install -dm755 "$pkgdir"/etc/$pkgname
    cp $pkgname.{yml,template.json} "$pkgdir"/etc/$pkgname

    install -Dm755 $pkgname \
                   "$pkgdir"/usr/bin/$pkgname
    install -Dm644 "$srcdir"/$pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}

package_metricbeat() {
    pkgdesc='Server monitoring agent that stores metrics in Elasticsearch'
    optdepends=('elasticsearch: for standalone installation')
    backup=("etc/$pkgname/$pkgname.yml" "etc/$pkgname/$pkgname.template.json")
    conflicts=('topbeat')
    replaces=('topbeat')

    cd beats-$pkgver/$pkgname

    install -dm755 "$pkgdir"/etc/$pkgname
    cp $pkgname.{yml,template.json} "$pkgdir"/etc/$pkgname

    install -Dm755 $pkgname \
                   "$pkgdir"/usr/bin/$pkgname
    install -Dm644 "$srcdir"/$pkgname.service \
                   "$pkgdir"/usr/lib/systemd/system/$pkgname.service
}
