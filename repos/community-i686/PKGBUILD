# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=0.10.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='http://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('c85e2ff849df5812939a7061aafd2693c14906421b71c9964fe75d5b37c764537a2008e990ca02ef720bc9d07bdc6486e12144204be0187da70b0b75097889c3')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-syscapstone --with-syszip --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
