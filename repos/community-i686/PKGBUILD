# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: geekitus

pkgname=blobwars
pkgver=1.18
pkgrel=3
pkgdesc="Platform action game featuring a variety of different weaponry and multiple objectives"
arch=('i686' 'x86_64')
url="http://www.parallelrealities.co.uk/projects/blobWars.php"
license=('GPL')
depends=('hicolor-icon-theme' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b6e8b7e628533bf32912587bc6666012')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  sed -i -e 's/-g games//' -e 's/-Werror//' \
         -e 's/USEPAK = 0/USEPAK = 1/' makefile
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make PREFIX=${pkgdir}/usr BINDIR=${pkgdir}/usr/bin/ install
  chmod a+r ${pkgdir}/usr/share/applications/$pkgname.desktop
  chmod a+r -R ${pkgdir}/usr/share/icons/hicolor/
  install -dm755 ${pkgdir}/usr/share/pixmaps
  install -m644 icons/blobwars.png ${pkgdir}/usr/share/pixmaps/
}
