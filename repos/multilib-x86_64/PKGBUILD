# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=primus_vk
pkgname=lib32-primus_vk
pkgver=1.2
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/primus_vk"
license=('BSD')
depends=('lib32-nvidia-utils' 'bumblebee' 'lib32-vulkan-icd-loader' 'lib32-primus')
makedepends=('vulkan-validation-layers')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('48c03a2f0fdb7d4609c3f6e0bf24ee2306805330e381722a2fd0485c9916daa802725117c8b3cc0d13d80376c7384193ea9aff69c9e13a4f48becf6dbf94d0f0')

build() {
  cd ${_pkgname}-${pkgver}

  export CXXFLAGS+=' -m32 -DNV_DRIVER_PATH=\"/usr/lib32/libGLX_nvidia.so.0\"'
  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -D libnv_vulkan_wrapper.so -t "$pkgdir"/usr/lib32/
  install -D libprimus_vk.so -t "$pkgdir"/usr/lib32/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}
