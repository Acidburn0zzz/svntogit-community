# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-plugins
pkgver=1.2.1
pkgrel=1
pkgdesc="Extra alsa plugins (32-bit)"
arch=(x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(lib32-alsa-lib alsa-plugins)
makedepends=(lib32-libpulse lib32-jack lib32-speex lib32-libsamplerate)
optdepends=('lib32-libpulse: PulseAudio plugin'
            'lib32-jack: Jack plugin'
            'lib32-libsamplerate: libsamplerate resampling plugin'
            'lib32-speex: libspeexdsp resampling plugin')
source=("ftp://ftp.alsa-project.org/pub/plugins/alsa-plugins-$pkgver.tar.bz2")
sha256sums=('4d94de7ad41734b8604a652521200bb6554fcf0c2c00fdbd302b1710d76548da')

prepare() {
  cd alsa-plugins-$pkgver
  autoreconf -fvi
}

build() {
  cd alsa-plugins-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr --libdir=/usr/lib32 --sysconfdir=/etc
  make
}

package() {
  cd alsa-plugins-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/{etc,usr/share}
}
