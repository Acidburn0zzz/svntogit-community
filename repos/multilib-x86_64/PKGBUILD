# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.3.0.2
pkgrel=1
pkgdesc="Concurrent, on-the-fly audio programming language"
arch=('x86_64' 'i686')
url="http://chuck.cs.princeton.edu/"
license=('GPL')
depends=('gcc-libs' 'libsndfile')
makedepends=('bison' 'flex' 'setconf') 
source=("http://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('05a9d2d54b6a5068a8bb186805a1376c3f353743b95b5691770ffadea0ca13a6')

if [[ $CARCH == x86_64 ]]; then
  depends=('gcc-libs-multilib' 'lib32-libsndfile' 'lib32-alsa-lib')
  makedepends+=('gcc-multilib')
fi

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  msg2 "Configuring..."
  if [[ $CARCH == x86_64 ]]; then
    setconf makefile CC 'gcc -m32'
    setconf makefile CXX 'g++ -m32'
    setconf makefile LD 'g++ -m32'
  fi
	CFLAGS+=' -fno-strict-aliasing -lpthread'
	CXXFLAGS+=' -fno-strict-aliasing -fpermissive -lpthread'

  msg2 "Compiling..."
  # Alternatives to linux-alsa are: linux-jack linux-oss osx win32
  make linux-alsa
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  install -Dm755 chuck $pkgdir/usr/bin/chuck
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -r ../examples "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
