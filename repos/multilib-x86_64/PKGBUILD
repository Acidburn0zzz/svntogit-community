# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=lib32-virtualgl
pkgver=2.6.3
pkgrel=1
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-turbojpeg' 'lib32-opencl-driver' 'lib32-ocl-icd')
makedepends=('lib32-libxv' 'lib32-glu' 'virtualgl' 'lib32-libjpeg-turbo' 'cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-mesa-libgl' 'lib32-libxtst' 'opencl-headers')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig}
        faedcc1e36b4ed89a325e01822447900840a0b77.patch)
sha512sums=('c01c602335d51c94840c32021a5ce4f982939d67efbe1458fce67582c5a76ac42f5b733998a8c8f04a77d57d7c2de01377f7cabcf848c01bfd9b2d8e0b318fea'
            'SKIP'
            '6845a60071aac333ac1c2ad5c3861f9fbe8cdf9f28425817a18c2b6f8c280b5469ce3be4831a0bf8a6600955bf5177217ff684c61f728783f2d09be6885f2815')
validpgpkeys=('ECF01671D05E2A105FF84DC46BBEFA1972FEB9CE') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  cd VirtualGL-$pkgver

  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  patch -Np1 -i "$srcdir"/faedcc1e36b4ed89a325e01822447900840a0b77.patch

  mkdir build
}

build() {
  cd "VirtualGL-$pkgver"/build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DX11_X11_LIB=/usr/lib32/libX11.so \
      -DX11_Xext_LIB=/usr/lib32/libXext.so \
      -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so
  make
}

package() {
  cd "VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres32
  rm -rf bin
  mkdir -p bin
  mv glxspheres32 bin/

  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/include"
}
