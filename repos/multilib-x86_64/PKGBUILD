# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.13.91+24+g75eadca
pkgrel=1
epoch=2
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=(custom)
depends=(lib32-expat lib32-freetype2 fontconfig)
makedepends=(git autoconf-archive gperf python-lxml python-six lib32-json-c)
install=lib32-fontconfig.install
_commit=75eadca26648abf69497691ff0f4c7803b9ff23c  # master
source=("git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#commit=$_commit"
        fontconfig-32.hook)
sha256sums=('SKIP'
            'd97c0c5b88023da5a2acf64cf560265390a9365305c43b8e86b4f89348e727b3')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

pkgver() {
  cd fontconfig
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd fontconfig
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd fontconfig

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/local/share/fonts
  make
}

check() {
  cd fontconfig
  make -k check
}

package() {
  cd fontconfig
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/{etc,usr/{include,share}}

  mv "$pkgdir"/usr/bin/fc-cache{,-32}
  find "$pkgdir/usr/bin" -type f -not -name '*-32' -delete

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 ../*.hook

  # Install license
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s fontconfig "$pkgdir/usr/share/licenses/$pkgname"
}
