# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: josephgbr <rafael.f.f1 at gmail.com>

_pkgbase=libtasn1
pkgname=lib32-${_pkgbase}
pkgver=3.1
pkgrel=1
pkgdesc="The ASN.1 library used in GNUTLS (32 bit)"
arch=('x86_64')
license=('GPL3' 'LGPL')
url="http://www.gnu.org/software/libtasn1/"
depends=('lib32-glibc' ${_pkgbase})
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnu.org/gnu/libtasn1/libtasn1-${pkgver}.tar.gz{,.sig}
        buffer_overflow_fix.diff)
md5sums=('a3c713c41a9a73851235dc37051be46f'
         '43500f54858ffe021e5fd20896b165ad'
         '4c9e248a4198d9710bf2f822a7c447a5')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd ${_pkgbase}-${pkgver}

  # corrected possible buffer overflow in parser errors. 
  # http://git.savannah.gnu.org/gitweb/?p=libtasn1.git;a=commit;h=aa6e97f68d6c8d06f531d9b6bb462dc1c0720f77
  patch -Np1 -i ${srcdir}/buffer_overflow_fix.diff
  
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}/usr/"{bin,include,share}
}
