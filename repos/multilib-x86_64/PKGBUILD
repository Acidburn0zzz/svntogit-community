# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.24.1
pkgrel=3
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('lib32-pcre' 'lib32-zlib' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/2.24/glib-${pkgver}.tar.bz2)
sha256sums=('014c3da960bf17117371075c16495f05f36501db990851ceea658f15d2ea6d04')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/glib-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --enable-static --enable-shared \
      --with-pcre=system --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # What to do with gio-querymodules ?
  rm -rf "${pkgdir}"/usr/{bin,share,include}
}
