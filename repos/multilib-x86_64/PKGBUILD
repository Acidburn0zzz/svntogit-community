# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-pipewire
pkgname=(lib32-pipewire lib32-pipewire-jack)
pkgver=0.3.28
pkgrel=1
epoch=1
pkgdesc="Low-latency audio/video router and processor - 32-bit client library"
url="https://pipewire.org"
license=(MIT)
arch=(x86_64)
makedepends=(git meson lib32-alsa-lib lib32-systemd lib32-dbus)
_commit=fb55cc4786ce5e78b0987248dd44e54daec77f80  # tags/0.3.28
source=("git+https://gitlab.freedesktop.org/pipewire/pipewire.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pipewire
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pipewire
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pipewire build \
    --libdir /usr/lib32 \
    -D examples=disabled \
    -D media-session=disabled \
    -D man=disabled \
    -D tests=disabled \
    -D gstreamer=disabled \
    -D gstreamer-device-provider=disabled \
    -D systemd-user-service=disabled \
    -D spa-plugins=disabled \
    -D pw-cat=disabled \
    -D sdl2=disabled \
    -D sndfile=disabled \
    -D libpulse=disabled \
    -D avahi=disabled
  meson compile -C build
}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_lib32-pipewire() {
  depends=(lib32-glibc libasound.so pipewire)
  optdepends=('lib32-pipewire-jack: JACK support')
  provides=(libpipewire-$_ver.so)

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"

    _pick jack usr/lib32/pipewire-$_ver/jack

    rm -r usr/{bin,include,lib32/pipewire-$_ver,share}
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-jack() {
  pkgdesc+=" - JACK support"
  license+=(GPL2)  # libjackserver
  depends=(libpipewire-$_ver.so pipewire-jack)

  mv jack/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

# vim:set sw=2 et:
