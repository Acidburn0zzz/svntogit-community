# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: K900 <k0009000@gmail.com>

pkgname=steam
pkgver=1.0.0.52
pkgrel=4
pkgdesc="Valve's digital software delivery system"
url='http://steampowered.com/'
arch=('i686' 'x86_64')
license=('custom')
depends=('bash' 'desktop-file-utils' 'hicolor-icon-theme' 'curl' 'dbus' 'freetype2' 'gdk-pixbuf2' 'ttf-font' 'zenity')
optdepends=('steam-native-runtime: steam native runtime support')
depends_x86_64=('lib32-libgl' 'lib32-gcc-libs' 'lib32-libx11' 'lib32-libxss')
optdepends_x86_64=(
  'lib32-mesa-dri: for open source driver users'
  'lib32-catalyst-utils: for AMD Catalyst users'
  'lib32-nvidia-utils: for NVIDIA proprietary blob users'
  'lib32-alsa-plugins: for pulseaudio on some games'
)
install=steam.install
source=(http://repo.steampowered.com/${pkgname}/pool/${pkgname}/s/${pkgname}/${pkgname}_${pkgver}.tar.gz
        steam-native.sh
        steam-native.desktop
        alsa_sdl_audiodriver.patch
        80-steam-controller-permission.rules)
sha512sums=('eefff7b2d223305fa8ff8b2419f9806e21784044c2832b42c5583bac4b9391d66444903e2753153842a85eab196418ca47d9b4b3a1daaf839e1eb15edfff891e'
            '1fb84e481c6c417cd095f5ae73d04a27936851a6bc60ed39a118173532b697d3bd0cc0adf6ddd746f6791440ab0b36407fb0230b13337bda316bb6d8af5169cd'
            '088db8265e9f35a61c3ff1e26654c1d3d53fe51ca6791f8210db0edc21e8ddc698c4c943895b9682e8d34c93675548d7b6a5bc687f02228e0cc85e267d12df33'
            '5c6117d86cb95071246434851bae3bf7d1c2174b0c8744e33e9c98ccc22efae0c956e1d7e3969c572aa96259b1b8164992d265541bce49a24daa12555d825fd5'
            'f2b45659524a23e97ac271629ae525823f3d613e94f3fb72dfb67a21b7edfda6d07bff623187582c6d0888501e98536d114ccb27d9a7d7065c51fc11f7d8b18f')
prepare() {
  patch -d "${pkgname}" -Np1 < "${srcdir}/alsa_sdl_audiodriver.patch"
}

package() {
  make -C "${pkgname}" DESTDIR="${pkgdir}" install

  install -Dm 755 "${srcdir}/steam-native.sh" "${pkgdir}/usr/bin/steam-native"
  install -Dm 644 "${srcdir}/steam-native.desktop" -t "${pkgdir}/usr/share/applications"

  install -Dm 644 "${pkgdir}/usr/share/doc/steam/steam_install_agreement.txt" \
    "${pkgdir}/usr/share/licenses/steam/LICENSE"

  # blank steamdeps because apt-get
  ln -sf /usr/bin/true "${pkgdir}/usr/bin/steamdeps"

  install -Dm 644 "${srcdir}/80-steam-controller-permission.rules" \
    "${pkgdir}/usr/lib/udev/rules.d/80-steam-controller-permission.rules"

}

# vim: ts=2 sw=2 et:
