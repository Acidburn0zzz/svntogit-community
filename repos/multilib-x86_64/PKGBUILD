# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=2.0.0
pkgrel=2
pkgdesc='Super Nintendo emulator'
url='https://github.com/xyproto/zsnes'
arch=(x86_64)
license=(GPL)
makedepends=(gendesk git mesa nasm)
depends=(lib32-libpng lib32-opengl-driver lib32-sdl)
optdepends=('lib32-alsa-lib: sound support for ALSA'
            'lib32-libpulse: sound support for Pulse Audio')
source=("git+$url#commit=af4fcf97ca0892bb6b47d6e03a17ada3539ea55a") # tag: 2.0.0
b2sums=(SKIP)

prepare() {
  gendesk -f --pkgname $pkgname --pkgdesc "$pkgdesc"
}

build() {
  make -C $pkgname
}

package() {
  install -Dm644 zsnes.desktop "$pkgdir/usr/share/applications/zsnes.desktop"
  install -Dm644 zsnes.png "$pkgdir/usr/share/pixmaps/zsnes.png"
  install -Dm755 zsnes/zsnes "$pkgdir/usr/bin/zsnes"
  install -Dm644 zsnes/linux/zsnes.1 "$pkgdir/usr/share/man/man1/zsnes.1"
}
