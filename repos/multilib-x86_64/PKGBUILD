# Maintainer: Maxime Gauduin <alucryd@archlonux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=lib32-libva1
pkgver=1.8.3
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=(
  'lib32-libdrm' 'lib32-libgl' 'lib32-libx11' 'lib32-libxext' 'lib32-libxfixes'
  'lib32-wayland' 'libva1'
)
makedepends=('git' 'lib32-libglvnd' 'lib32-mesa')
provides=(
  'libva-drm.so' 'libva-glx.so' 'libva-wayland.so' 'libva-x11.so' 'libva.so'
)
source=("git+https://github.com/intel/libva.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd libva

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libva

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-va-messaging
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libva install
  rm -rf "${pkgdir}"/usr/{include,lib32/{pkgconfig,*.so}}
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libva1 "${pkgdir}"/usr/share/licenses/lib32-libva1
}

# vim: ts=2 sw=2 et:
