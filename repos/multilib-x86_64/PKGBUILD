# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=lib32-rust
pkgver=1.37.0
pkgrel=1

pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=('llvm' 'libffi' 'perl' 'python' 'curl' 'cmake')
depends=('lib32-gcc-libs' "rust=1:$pkgver-1")

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml)

sha256sums=('120e7020d065499cc6b28759ff04153bfdc2ac9b5adeb252331a4eb87cbe38c3'
            'SKIP'
            'b832b7db40338740e7080a60f515cb8eb86d7b5a0ec3c9ebe6d6c868878c9061')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  python ./x.py build
}

package() {
  cd "rustc-$pkgver-src"

  target="$pkgdir"/"$(rustc --print sysroot)"/lib/rustlib
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
