# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Chris Kitching <chriskitching@linux.com>

pkgname=lib32-gstreamer
pkgver=1.8.3
pkgrel=1
pkgdesc="GStreamer Multimedia Framework (32-bit)"
arch=('x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
depends=('lib32-libxml2' 'lib32-glib2' 'gstreamer')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection')
source=(${url}/src/gstreamer/gstreamer-${pkgver}.tar.xz)
sha256sums=('66b37762d4fdcd63bce5a2bec57e055f92420e95037361609900278c0db7c53f')

build() {
  cd "${srcdir}/gstreamer-${pkgver}"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure\
    --build=i686-pc-linux-gnu\
    --prefix=/usr\
    --sysconfdir=/etc\
    --localstatedir=/var\
    --libexecdir=/usr/lib32\
    --libdir=/usr/lib32\
    --disable-maintainer-mode\
    --disable-examples\
    --disable-tests\
    --with-package-name="GStreamer (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/" \
    --disable-static\
    --disable-docbook\
    --disable-gtk-doc-html\
    --disable-gtk-doc-pdf\
    --disable-gtk-doc\
    --disable-benchmarks

  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd "${srcdir}/gstreamer-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -R "${pkgdir}"/usr/{share,include}

  for _i in "${pkgdir}"/usr/bin/*; do
    mv "${_i}" "${_i}-32"
  done
}
