# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Maintainer: Tom Gundersen <teg@jklm.no>

pkgname=lib32-systemd
_pkgbasename=systemd
pkgver=229
pkgrel=1
pkgdesc="system and service manager (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2' 'LGPL2.1' 'MIT')
depends=('lib32-libgcrypt' 'lib32-xz' 'lib32-libcap' 'lib32-acl' 'lib32-libidn' 'lib32-gcc-libs' 'systemd')
makedepends=('lib32-gcc-libs' 'gcc-multilib' 'lib32-libidn' 'lib32-glib2' 'intltool' 'gperf'
             'lib32-curl' 'lib32-bzip2' 'git')
source=("git://github.com/systemd/systemd.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd systemd

  # networkd: FIONREAD is not reliable on some sockets
  git cherry-pick -n 4edc2c9b6b5b921873eb82e58719ed4d9e0d69bf

  # fix assertion failure in src/core/timer.c on bootup (FS#48197)
  git cherry-pick -n 3f51aec8647fe13f4b1e46b2f75ff635403adf91

  # fix udevd error checking from cg_unified() (FS#48188)
  git cherry-pick -n 6d2353394fc33e923d1ab464c8f88df2a5105ffb

  # revert "core: resolve specifier in config_parse_exec()"
  git cherry-pick -n bd1b973fb326e9b7587494fd6108e5ded46e9163

  ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd systemd

  local timeservers=({0..3}.arch.pool.ntp.org)

  ./configure \
    --libexecdir=/usr/lib32 \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-audit \
    --disable-tests \
    --disable-ima \
    --disable-seccomp \
    --disable-pam \
    --disable-kmod \
    --disable-networkd \
    --disable-blkid \
    --disable-libiptc \
    --disable-lz4 \
    --disable-manpages \
    --without-python \
    --disable-libcryptsetup \
    --with-sysvinit-path= \
    --with-sysvrcnd-path= \
    --with-ntp-servers="${timeservers[*]}"

  make
}

package() {
  cd systemd
    
  make DESTDIR="$pkgdir" install
    
  rm -rf "${pkgdir}"/{etc,var}
  rm -rf "${pkgdir}"/usr/{bin,include,lib,share}

  install -m755 -d "${pkgdir}/usr/share/licenses"
  ln -s systemd "$pkgdir/usr/share/licenses/lib32-systemd"
}
