# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: FigoDaPaura <ffigoDaPaura>

pkgname=lib32-virtualgl
pkgver=2.6.4
pkgrel=2
pkgdesc="32-bit serverside components for 64-bit VirtualGL servers"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('virtualgl' 'lib32-libxv' 'lib32-glu' 'lib32-libjpeg-turbo' 'lib32-opencl-driver' 'lib32-ocl-icd')
makedepends=('lib32-libxv' 'lib32-glu' 'virtualgl' 'cmake' 'gcc-multilib' 'lib32-mesa' 'lib32-mesa-libgl' 'lib32-libxtst' 'opencl-headers')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig})
sha512sums=('a475e5b7373d0c86ec7d1274f7527cd7bb1d82acdb38d42366faae58175052f957e090a4c8fdb4e3ba4b2b9063957c14b46d55316b69420247d2f9cf3afb1280'
            'SKIP')
b2sums=('34794897f62ccdf7f5d80b1c4465af5e22d72408e14ae9243c66315c9ffeedb21eb5dc6afc95c889c000c46685b8ab6e0756edaee4f34d9d28ea0b71459efba3'
        'SKIP')
validpgpkeys=('ECF01671D05E2A105FF84DC46BBEFA1972FEB9CE') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  cd VirtualGL-$pkgver

  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"
}

build() {
  cd "VirtualGL-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
        -DCMAKE_INSTALL_BINDIR=/usr/bin \
        -DCMAKE_BUILD_TYPE='None' \
        -DTJPEG_INCLUDE_DIR=/usr/include \
        -DTJPEG_LIBRARY=/usr/lib32/libturbojpeg.so \
        -DX11_X11_LIB=/usr/lib32/libX11.so \
        -DX11_Xext_LIB=/usr/lib32/libXext.so \
        -DOPENGL_gl_LIBRARY=/usr/lib32/libGL.so \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "VirtualGL-$pkgver"
  make install DESTDIR="$pkgdir" -C build

  cd "$pkgdir/usr"
  mv bin/glxspheres glxspheres32
  rm -rf bin
  mkdir -p bin
  mv glxspheres32 bin/

  rm -rf "$pkgdir/usr/share"
  rm -rf "$pkgdir/usr/include"
}
