# $Id$
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

_pkgbasename=readline
pkgname=lib32-$_pkgbasename
_basever=6.2
_patchlevel=001 #prepare for some patches
pkgver=$_basever.$_patchlevel
pkgrel=1
pkgdesc="GNU readline library (32-bit)"
arch=(x86_64)
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('lib32-glibc' 'lib32-ncurses' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz)
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p))
    done
fi
md5sums=('67948acb2ca081f23359d0256e9a271c'
         '83287d52a482f790dfb30ec0a8746669')

build() {
  cd ${srcdir}/${_pkgbasename}-$_basever

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  for p in ../readline61-*; do
    [ -e "$p" ] || continue
    msg "applying patch ${p}"
    patch -Np0 -i ${p}
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  ./configure --prefix=/usr --libdir=/usr/lib32
  make SHLIB_LIBS=-lncurses
}

package() {
  cd ${srcdir}/${_pkgbasename}-$_basever

  make DESTDIR=${pkgdir} install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
