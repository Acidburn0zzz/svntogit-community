# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jason Stryker <inbox at jasonstryker dot com>

pkgname=('wine-nine')
pkgver=0.4
pkgrel=1
pkgdesc="Gallium Nine Standalone"
arch=('x86_64')
url="https://github.com/iXit/wine-nine-standalone"
license=('LGPL2')
makedepends=('git' 'lib32-mesa' 'wine' 'meson')
source=("${url}/archive/v${pkgver}.tar.gz")
sha512sums=('6af31e828400c6f711c76d91fb65215f4ba79c31b8d98f3e953b319593d96d1e5e78e07aca96529de37bbb89f3479a4d4db5f58e32ce7b98667284be10a9e532')

build() {
  cd ${srcdir}/wine-nine-standalone-${pkgver}
    
  ./bootstrap.sh

  meson \
    --cross-file "tools/cross-wine64" \
    --buildtype "release" \
    --prefix "/" \
    --bindir bin \
    --libdir lib \
    "build64"
  ninja -C "build64"
  DESTDIR="${srcdir}/fakeinstall" ninja -C build64 install

  meson \
    --cross-file "tools/cross-wine32" \
    --buildtype "release" \
    --prefix "/" \
    --bindir bin32 \
    --libdir lib32 \
    "build32"
  ninja -C "build32"
  DESTDIR="${srcdir}/fakeinstall" ninja -C build32 install
}

package() {
  depends=('lib32-mesa' 'wine')

  install -m 755 -d ${pkgdir}/usr/lib{,32}/wine/fakedlls

  install -m 755 "${srcdir}/fakeinstall"/bin/ninewinecfg.exe.so ${pkgdir}/usr/lib/wine/ninewinecfg.exe.so
  install -m 755 "${srcdir}/fakeinstall"/bin/ninewinecfg.exe.fake ${pkgdir}/usr/lib/wine/fakedlls/ninewinecfg.exe

  install -m 755 "${srcdir}/fakeinstall"/bin32/ninewinecfg.exe.so ${pkgdir}/usr/lib32/wine/ninewinecfg.exe.so
  install -m 755 "${srcdir}/fakeinstall"/bin32/ninewinecfg.exe.fake ${pkgdir}/usr/lib32/wine/fakedlls/ninewinecfg.exe

  install -m 755 "${srcdir}/fakeinstall"/lib/d3d9-nine.dll.so ${pkgdir}/usr/lib/wine/d3d9-nine.dll.so
  install -m 755 "${srcdir}/fakeinstall"/lib/d3d9-nine.dll.fake ${pkgdir}/usr/lib/wine/fakedlls/d3d9-nine.dll

  install -m 755 "${srcdir}/fakeinstall"/lib32/d3d9-nine.dll.so ${pkgdir}/usr/lib32/wine/d3d9-nine.dll.so
  install -m 755 "${srcdir}/fakeinstall"/lib32/d3d9-nine.dll.fake ${pkgdir}/usr/lib32/wine/fakedlls/d3d9-nine.dll
}
