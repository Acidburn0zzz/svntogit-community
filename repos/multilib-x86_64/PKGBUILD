# Maintainer:
# Contributor: Judd Vinet <jvinet@zeroflux.org>

_pkgbasename=libldap
pkgname=lib32-$_pkgbasename
pkgver=2.4.49
pkgrel=1
pkgdesc="Lightweight Directory Access Protocol (LDAP) client libraries (32-bit)"
arch=('x86_64')
license=('custom')
url="https://www.openldap.org/"
depends=('lib32-openssl' $_pkgbasename)
makedepends=(gcc-multilib)
source=("ftp://ftp.openldap.org/pub/OpenLDAP/openldap-release/openldap-${pkgver}.tgz"
        'ntlm.patch')
sha256sums=('e3b117944b4180f23befe87d0dcf47f29de775befbc469dcf4ac3dab3311e56e'
            '099eb217bf989da9c10e0df375f2c4dacf745db55cd1e89be07a09e502552f42')

prepare() {
  cd openldap-${pkgver}
  patch -Np1 -i ../ntlm.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd openldap-${pkgver}

  ./configure --prefix=/usr \
              --libexecdir=/usr/sbin \
              --sysconfdir=/etc \
	      --mandir=/usr/share/man \
              --localstatedir=/var/lib/openldap \
              --enable-crypt --enable-dynamic \
              --with-threads --disable-wrappers \
	      --disable-spasswd --without-cyrus-sasl \
	      --disable-bdb --disable-hdb --libdir=/usr/lib32

  cd include
  make

  cd ../libraries
  make depend
  make

}

package() {
  cd openldap-${pkgver}

  cd include
  make DESTDIR="${pkgdir}" install

  cd ../libraries
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/etc"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
