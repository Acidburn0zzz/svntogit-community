# $Id$
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libdrm
pkgname=lib32-$_pkgbasename
pkgver=2.4.22
pkgrel=4
pkgdesc="Userspace interface to kernel DRM services (32-bit)"
arch=(x86_64)
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
url="http://dri.freedesktop.org/"
source=(http://dri.freedesktop.org/${_pkgbasename}/${_pkgbasename}-${pkgver}.tar.bz2
        no-pthread-stubs.patch
	git_nouveau.diff)
md5sums=('3bdfa33f35d1c902e5115cceb5500c83'
         '9b5ec3bbebe86921e2057694c42f65b8'
         '3321345270053234c0071d804e5b89f3')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  patch -Np1 -i "${srcdir}/no-pthread-stubs.patch"
  # add future nouveau api
#  patch -Np1 -i ${srcdir}/git_nouveau.diff
  
  libtoolize --force
  aclocal
  autoconf
  automake
  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --enable-intel --enable-radeon \
      --enable-vmwgfx-experimental-api --enable-nouveau-experimental-api \
      --enable-udev
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
