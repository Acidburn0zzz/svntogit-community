# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.1.8
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="http://www.alsa-project.org"
depends=('lib32-glibc' 'alsa-lib')
makedepends=('gcc-multilib')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/alsa-lib-$pkgver.tar.bz2)
sha256sums=('3cdc3a93a6427a26d8efab4ada2152e64dd89140d981f6ffa003e85be707aedf')

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
}
