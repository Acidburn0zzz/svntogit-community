# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libogg
pkgver=1.3.4
pkgrel=1
pkgdesc='Ogg bitstream and framing library (32-bit)'
arch=('x86_64')
url='https://www.xiph.org/ogg/'
license=('BSD')
depends=(lib32-glibc libogg)
makedepends=(cmake ninja)
source=("https://downloads.xiph.org/releases/ogg/libogg-${pkgver}.tar.xz")
sha256sums=('c163bc12bc300c401b6aa35907ac682671ea376f13ae0969a220f7ddf71893fe')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake ../libogg-${pkgver} \
    -G Ninja \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib32' \
    -DBUILD_SHARED_LIBS='ON' \
    -DINSTALL_DOCS=OFF
  ninja
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "$pkgdir"/usr/include

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s libogg "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
