# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.2.1.1
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('lib32-glibc' 'alsa-lib')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/alsa-lib-$pkgver.tar.bz2)
sha512sums=('8a0b6f1ecc58d60991b4509e791b1581eebae57cd754b0b4829e66b1cfee81d588b04f42a17f598f06310b9658d9f46fd748cca516cc48b6f2989389c22c42e9')

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
}
