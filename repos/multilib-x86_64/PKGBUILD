# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farwayer <farwayer [at] gmail [dot] com>

_pkgbase=opus
pkgname=lib32-$_pkgbase
pkgver=1.1.4
pkgrel=1
pkgdesc="Totally open, royalty-free, highly versatile audio codec (32-bit)"
arch=('x86_64')
url="https://www.opus-codec.org/"
license=('BSD')
depends=('lib32-glibc' 'opus')
makedepends=('gcc-multilib')
source=("http://downloads.xiph.org/releases/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('9122b6b380081dd2665189f97bfd777f04f92dc3ab6698eea1dbb27ad59d8692')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --enable-custom-modes --disable-static \
    --enable-intrinsics --enable-rtcd --enable-ambisonics --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
  install -d "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/$pkgname
}
