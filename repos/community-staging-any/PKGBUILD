# $Id$
# Maintainer: David Runge <dave@sleepmap.de>

pkgname=python-imdbpy
pkgver=6.5
pkgrel=2
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alberanid/${pkgname/python-/}/archive/${pkgver}.tar.gz")
sha512sums=('4147088c2558fcd2e30f6e338b72ebd4b1c3917a54df5a6e0ee8fb51ba2b9fe370b32553a899e74108fa089aaa1fff4ff248ab6b14ca959b80c6b5aade7b58a7')

prepare() {
  mv -v "${pkgname/python-/}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc\/python-imdbpy\/'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
  # disable flaky tests: https://github.com/alberanid/imdbpy/issues/177
  sed -e 's/test_movie_taglines_single_should_be_a_list_of_phrases/disabled_&/' \
      -i tests/test_http_movie_taglines.py
  sed -e 's/test_movie_ratings_demographics_should_be_19/disabled_&/' \
      -i tests/test_http_movie_votes.py
  sed -e 's/test_death_date_should_be_in_ymd_format/disabled_&/' \
      -e 's/test_birth_date_should_be_in_ymd_format/disabled_&/' \
      -e 's/test_trade_mark_should_be_a_list/disabled_&/' \
      -i tests/test_http_person_bio.py
  sed -e 's/test_found_many_result_should_contain_correct_number_of_movies/disabled_&/' \
      -i tests/test_http_search_movie.py

}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH=${srcdir}/${pkgname}-${pkgver}/build:${PYTHONPATH}
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
