# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=distributed
pkgname=python-${_pkg}
pkgver=2.30.1
pkgrel=3
pkgdesc="Distributed task scheduler for Dask"
arch=(any)
url="https://distributed.dask.org/"
license=(BSD)
depends=(
    python
    python-click
    python-cloudpickle
    python-dask
    python-msgpack
    python-psutil
    python-setuptools
    python-sortedcontainers
    python-tblib
    python-toolz
    python-tornado
    python-yaml
    python-zict
)
checkdepends=(
    python-pytest
    python-fsspec
    python-numpy
    python-pandas
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz
        py39.patch)
#source=(https://github.com/dask/distributed/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('1421d3b84a0885aeb2c4bdc9e8896729c0f053a9375596c9de8864e055e2ac8e'
            'e872d3655fc634f226b178d579377ed862c14ff488058586e61c82fd903ed680')

prepare() {
  cd ${_pkg}-${pkgver}
  # https://github.com/spyder-ide/spyder-kernels/issues/258
  # https://github.com/dask/distributed/pull/4234
  patch -Np1 -i ../py39.patch
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

# test suite gets stuck at distributed/tests/test_semaphore.py
#check() {
#  cd ${_pkg}-${pkgver}
#  pytest distributed
#}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
