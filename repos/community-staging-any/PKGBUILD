# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=vim-ultisnips
pkgver=2.1
_scriptid=17463
pkgrel=3
pkgdesc='TextMate-style snippets for Vim.'
arch=('any')
url='http://www.vim.org/scripts/script.php?script_id=2715'
license=('GPL2')
depends=('gvim' 'python')
groups=('vim-plugins')
replaces=('vim-snipmate')
install=vimdoc.install
source=("${pkgname}.tar.gz"::"http://www.vim.org/scripts/download_script.php?src_id=${_scriptid}")
md5sums=('223eb9836d5096b6ee9d82b2d56f2346')

package() {
  cd "${srcdir}/UltiSnips-$pkgver"

  install -dm755 "$pkgdir/usr/share/vim/vimfiles/"
  cp -r -t "$pkgdir/usr/share/vim/vimfiles/" UltiSnips after doc ftdetect ftplugin plugin syntax \
    utils
}
