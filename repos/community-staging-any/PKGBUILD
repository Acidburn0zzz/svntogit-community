# Maintainer: David Runge <dave@sleepmap.de>
pkgname=khard
pkgver=0.14.0
pkgrel=2
pkgdesc="Console CardDAV client"
arch=('any')
url="https://github.com/scheibler/khard"
license=('GPL3')
depends=('python-atomicwrites' 'python-configobj' 'python-ruamel-yaml'
'python-setuptools' 'python-unidecode' 'python-vobject')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest')
optdepends=('vdirsyncer: Synchronization of address books with a DAV server'
            'diffutils: Using sdiff_khard_wrapper.sh')
# upstream doesn't always upload srcdist to pypi: https://github.com/scheibler/khard/issues/215
# source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/scheibler/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6024efdccd15d847f4aa317d48d364f8d4ccd0e063f10e2ce52b24d3d3e7fd4f2ab3432dd17ebfbba4572a86b9658b3495bc92bd0ab14f2a51e90817a42eb2d2')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  make -C doc singlehtml man
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # additional wrapper script
  install -vDm 755 "misc/sdiff/sdiff_${pkgname}_wrapper.sh" \
    "${pkgdir}/usr/lib/${pkgname}/sdiff_${pkgname}_wrapper.sh"
  # twinkle integration
  install -vDm 644 misc/twinkle/scripts/*.py \
    -t "${pkgdir}/usr/share/${pkgname}/twinkle/scripts/"
  install -vDm 644 misc/twinkle/sounds/*.wav \
    -t "${pkgdir}/usr/share/${pkgname}/twinkle/sounds/"
  # zsh
  install -vDm 644 "misc/zsh/_"*"${pkgname}" \
    -t "${pkgdir}/usr/share/zsh/site-functions/"
  # docs
  install -vDm 644 {AUTHORS,CHANGES,README.md} \
                   "misc/${pkgname}/"*.{example,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # man
  install -vDm 644 "doc/build/man/${pkgname}.1" \
    -t "${pkgdir}/usr/share/man/man1"
  # html
  install -vDm 644 "doc/build/singlehtml/index.html" \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 "doc/build/singlehtml/_static/"*.{css,js,png} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/html/_static"
}
