# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=KiKit
pkgname=python-kikit
pkgver=0.6.1
pkgrel=3
pkgdesc='Automation tools for KiCAD'
arch=('any')
url='https://github.com/yaqwsx/KiKit'
license=('MIT')
depends=('python' 'kicad' 'python-numpy' 'python-shapely' 'python-click' 'python-markdown2'
         'python-pybars3' 'python-solidpython')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('42ef83b16be4bc1305ab3725737d349c2f881c28eb83163e3db149a58b607085834efb519de052745dea20b7d8c6eeded425593fe2fbdc7400087c5f875177e4')

prepare() {
  sed -i '/versioneer/d' $_pkgname-$pkgver/setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

# vim:set ts=2 sw=2 et:
