# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgbase=python-beautifulsoup4
pkgname=('python-beautifulsoup4' 'python2-beautifulsoup4')
pkgver=4.5.1
pkgrel=2
pkgdesc="A Python HTML/XML parser designed for quick turnaround projects like screen-scraping"
arch=('any')
url="http://www.crummy.com/software/BeautifulSoup/index.html"
license=('PSF')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://www.crummy.com/software/BeautifulSoup/bs4/download/${pkgver%.*}/${pkgbase#*-}-$pkgver.tar.gz")
md5sums=('994abd90e691beaf7d42c00ffb2f3a67')

prepare() {
  cp -a beautifulsoup4-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/beautifulsoup4-$pkgver
  python setup.py build

  cd "$srcdir"/beautifulsoup4-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/beautifulsoup4-$pkgver/build
  py.test

  cd "$srcdir"/beautifulsoup4-$pkgver-py2/build
  py.test2
}

package_python-beautifulsoup4() {
  depends=('python')
  optdepends=('python-chardet: universal encoding detector'
              'python-lxml: pythonic binding for the libxml2 and libxslt libraries')

  cd beautifulsoup4-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-beautifulsoup4() {
  depends=('python2')
  optdepends=('python2-chardet: universal encoding detector'
              'python2-lxml: pythonic binding for the libxml2 and libxslt libraries')

  cd beautifulsoup4-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
