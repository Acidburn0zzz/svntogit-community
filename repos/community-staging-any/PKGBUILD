# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi-intercept
pkgbase=python-wsgi-intercept
pkgname=('python-wsgi-intercept' 'python2-wsgi-intercept')
pkgver=1.7.0
pkgrel=2
pkgdesc='Intercept socket connection to wsgi applications for testing.'
arch=('any')
url="https://github.com/cdent/wsgi-intercept"
license=('MIT')
makedepends=('python-setuptools' 'python-sphinx' 'python2-setuptools' 'python2-sphinx')
checkdepends=('python-httplib2' 'python-pytest' 'python-requests'
'python-urllib3' 'python2-httplib2' 'python2-pytest' 'python2-requests'
'python2-urllib3')
source=("${_name}-${pkgver}.tar.gz::https://github.com/cdent/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('d83e513166cb827346bad31732c1f9117e12e6efd186676336c9635994c3afff607f116103e2ba493388f6045b5f2d0b409587620b7619bcd6ca8c5707acfc9c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd docs
  make man
  cd ../../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
  cd docs
  make man
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  py.test
  cd ../"${pkgname[1]}-${pkgver}"
  py.test2
}

package_python-wsgi-intercept() {
  depends=('python-six')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 644 docs/_build/man/wsgi_intercept.1 \
    -t "${pkgdir}/usr/share/man/man1/"
}

package_python2-wsgi-intercept() {
  depends=('python2-six')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm644 README.rst -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm644 docs/_build/man/wsgi_intercept.1 \
    "${pkgdir}/usr/share/man/man1/wsgi_intercept2.1"
}

