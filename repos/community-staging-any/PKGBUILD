# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-schema
pkgver=4.9.3
pkgrel=4
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')
checkdepends=('python-zope-testrunner' 'python-zope-testing' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.schema/archive/$pkgver.tar.gz")
sha512sums=('bac09d9106cca1560d968f35ba414860d42da9a1637c261a18e42b97c6ada3a3cc78381b536d478c505c7eaade189e47012b1726b8d8815622c904a79f3e89e1')

build() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.schema-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
