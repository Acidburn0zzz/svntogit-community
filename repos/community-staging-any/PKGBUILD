# Maintainer: David Runge <dvzrv@archlinux.org>

_name=natsort
pkgname=python-natsort
pkgver=7.0.1
pkgrel=3
pkgdesc="Simple yet flexible natural sorting in Python"
arch=('any')
url="https://github.com/SethMMorton/natsort"
license=('MIT')
depends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-mock' 'python-pyflakes'
              'flake8' 'python-hypothesis' 'python-mock')
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        py39.patch)
sha512sums=('48bfda8a42b951287d984e38c474b71233d56a578d8bec12e657413b3cdb71dd27103acda8dca359d87efd761e775deff0775a81d8dfa18df24c364921028c2b'
            '58bf2e058094728e50f40c4dae15a3d29c1dbf94ffc15a212876517a596da7ce03237983d32f47dbd7df0cb4067decea4b9f7123aa4c411119670437d4d5946a')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  patch -Np1 -d "$pkgname-$pkgver" <py39.patch
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
