# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.8.8
_commit=f11f07f9b70a9e5719c2f2e237a45d6c791144cd
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest' 'python-numpy' 'python2-numpy')
source=("git+https://github.com/construct/construct.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a construct{,-py2}
}

build() {
  cd "$srcdir"/construct
  python setup.py build

  cd "$srcdir"/construct-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct
  PYTHONPATH="$PWD" py.test

  cd "$srcdir"/construct-py2
  PYTHONPATH="$PWD" py.test2
}

package_python-construct() {
  depends=('python')

  cd construct
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')

  cd construct-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
