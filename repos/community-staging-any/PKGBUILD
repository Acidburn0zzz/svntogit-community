# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=pylev
pkgbase=python-pylev
pkgname=('python-pylev' 'python2-pylev')
pkgver=1.3.0
# PyPI does not have testsuite, Github does not have version tags
_commit=72e3d490515c3188e2acac9c15ea1b466f9ff938
pkgrel=3
pkgdesc="A pure Python Levenshtein implementation that's not freaking GPL'd"
arch=('any')
url="https://github.com/toastdriven/${_pkgname}"
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=("${_pkgname}-${_commit}.tar.gz::${url}/archive/${_commit}.tar.gz")
sha256sums=('f05a9f4eb31355f0536c323df6855a033fd88f4c92bcf23a1802fea4f379aeeb')
b2sums=('03793e831c8034e31638d9c469fd386d3acac1dae75f2ef2a0af5c38ee5f11e6a7a20d423078cc42c647edec258db79486c01179e4284d93e64cca0bf99badca')

build(){
    cd "${srcdir}"/${_pkgname}-${_commit}

    python setup.py build
    python2 setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${_commit}

    python -m unittest tests
    python2 -m unittest tests
}

package_python-pylev() {
    depends=('python')

    cd "${srcdir}"/${_pkgname}-${_commit}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pylev() {
    depends=('python2')

    cd "${srcdir}"/${_pkgname}-${_commit}
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
