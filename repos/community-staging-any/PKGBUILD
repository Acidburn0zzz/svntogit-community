# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=2.1.0
pkgrel=2
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz")
sha512sums=('b7cd12dcc64fa7ebdfe10e72c51005bdcd5418e4cecbf05f07929bed758be544ab0e1003351eaae94c2d3a10fee8ec7dfc3b2c86775066de1fd5d3604b58ba28')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
