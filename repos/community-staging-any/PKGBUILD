# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.4.2
pkgrel=6
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
makedepends=(python-setuptools python-pypandoc)
depends=(python python-botocore python-future python-jsonpickle python-wrapt)
checkdepends=(python-flask-sqlalchemy python-pynamodb python-pytest python-aiohttp
              python-pytest-aiohttp python-testing.postgresql python-psycopg2
              python-pg8000 python-requests python-aiobotocore)
# Not using PyPI source tarball as it does not include tests
source=("https://github.com/aws/aws-xray-sdk-python/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "aws-xray-sdk-psycopg2.8.patch"::"https://github.com/aws/aws-xray-sdk-python/commit/4b4422a3ad9bb9a28399b34851dd574f6155bb66.patch"
        "aws-xray-sdk-pynamodb4.patch"::"https://github.com/aws/aws-xray-sdk-python/commit/1dd185ba2fb946cea711a9620f04516c7f6eecdf.patch")
sha256sums=('e480a6dc21d314faf6a9af4cf871fd7b782fae1e704e0fe5977161707f101374'
            'b0740901e4c3bd0a678b217b1121c5f827e8535722a560d9f84fb9a8b90f8f74'
            'c2ad5a109841600d271c0b4b49b0fb2b54bcac6e220bf6a7c4cd6f68e17f377e')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../aws-xray-sdk-psycopg2.8.patch
  # https://github.com/aws/aws-xray-sdk-python/issues/171
  patch -Np1 -i ../aws-xray-sdk-pynamodb4.patch
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # the test suite is not compatible with Django 2.x yet
  # https://github.com/aws/aws-xray-sdk-python/issues/85
  pytest -v --ignore tests/ext/django
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
