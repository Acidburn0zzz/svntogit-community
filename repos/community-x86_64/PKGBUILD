# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.6.20
pkgrel=2
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm.'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/duplicity/'
license=('GPL')
depends=('librsync' 'ncftp' 'python2' 'python2-boto' 'python2-gnupginterface' 'python2-paramiko')
source=("http://savannah.nongnu.org/download/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig}
        u1-ascii-error.patch)
md5sums=('83a56c4c17fc50961c961943867deae4'
         '2bd2d4a3a331ab728473f46963dc87da'
         '7e19cc49547848ce847297b1880f5a8e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Fix ascii error in U1 backend
  patch -Np0 -i "${srcdir}/u1-ascii-error.patch"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  
  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix broken documentation permissions until upstream does (FS#27301)
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
  chmod 644 "$pkgdir/usr/share/man/man1/rdiffdir.1"
  chmod 644 "$pkgdir/usr/share/doc/$pkgname-$pkgver"/*
}
