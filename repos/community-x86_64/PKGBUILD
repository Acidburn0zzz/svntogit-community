# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.11.3+711+g4e27949d
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=('gnome-keyring' 'gmime' 'libcanberra' 'libgee' 'libnotify' 'librsvg'
         'webkit2gtk')
makedepends=('cmake' 'gnome-doc-utils' 'gobject-introspection' 'intltool'
             'vala' 'git')
_commit=4e27949d872a8be6c9f8ea5d1ab607964c672f2a  # master
source=("git+https://git.gnome.org/browse/geary#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^geary-//;s/-/+/g' | sed 's/0.11.0/0.11.3/'
}

prepare() {
  cd $pkgname

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd $pkgname/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DDESKTOP_UPDATE='FALSE' \
    -DICON_UPDATE='FALSE' \
    -DGSETTINGS_COMPILE='FALSE' \
    -DGSETTINGS_COMPILE_IN_PLACE='FALSE'
  make
}

package() {
  cd $pkgname/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
