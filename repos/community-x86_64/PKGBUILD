# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alessio Sergi <asergi at archlinux dot us>

pkgname=synapse
pkgver=0.2.99.2
pkgrel=3
pkgdesc="A semantic file launcher"
arch=('x86_64')
url="https://launchpad.net/synapse-project"
license=('GPL3')
depends=('libkeybinder3' 'hicolor-icon-theme' 'json-glib' 'libgee' 'libnotify' \
         'zeitgeist' 'rest' 'xdg-utils')
makedepends=('intltool' 'vala')
optdepends=('bc: calculator plugin'
            'devhelp: documentation plugin'
            'gnome-dictionary: dictionary plugin'
            'openssh: ssh plugin'
            'pastebinit: pastebin plugin'
            'rhythmbox: rhythmbox plugin'
            'xnoise: xnoise plugin')
source=("https://launchpad.net/$pkgname-project/0.3/$pkgver/+download/$pkgname-$pkgver.tar.xz"
        fix-build.patch)
sha1sums=('6720537a239419833fc5b4e521b25935e30a3274'
          '0ddb104dd7ec2d8de663c48f9d4f3614d17ee824')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i ../fix-build.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
