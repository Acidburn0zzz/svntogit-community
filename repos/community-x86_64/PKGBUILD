# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=purple-facebook
pkgver=0.9.6
pkgrel=3
pkgdesc="Facebook protocol plugin for libpurple"
arch=('x86_64')
url="https://github.com/dequis/purple-facebook"
license=('GPL')
depends=('json-glib' 'libpurple')
source=("https://github.com/dequis/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('1db6ed9e8f81cbd4ae10d75c04f5393e5cd4ca11ced74408ca6d07c7b888f3f7')

prepare() {
  cd $pkgname-$pkgver
  # Bump the FB_ORCA_AGENT version
  # https://github.com/dequis/purple-facebook/commit/3f4e9500bed9c1a7f9bb6c5381823a4facde6602
  sed -i 's/192.0.0.31.101/537.0.0.31.101/' pidgin/libpurple/protocols/facebook/api.h
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
