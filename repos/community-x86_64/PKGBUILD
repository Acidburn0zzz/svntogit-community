# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Aleksandar Trifunović <akstrfn@gmail.com>
# Contributor: Iván Ruvalcaba <mario.i.ruvalcaba@gmail.com>
# Contributor: Ian Ker-Seymer <i.kerseymer@gmail.com>

pkgname=neovim-qt
pkgver=0.2.11
pkgrel=1
pkgdesc='Qt5 GUI for Neovim'
arch=(x86_64)
url='https://github.com/equalsraf/neovim-qt'
license=(ISC)
depends=(neovim qt5-base)
makedepends=(cmake ninja)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('f678f25499b6e086d8fce83778be30508df080eb0ab808d7c204e261b5630498')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_C_FLAGS:string="$CFLAGS" \
    -DCMAKE_CXX_FLAGS:string="$CXXFLAGS" \
    -DCMAKE_EXE_LINKER_FLAGS:string="$LDFLAGS" \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=release \
    -DUSE_SYSTEM_MSGPACK=on \
    -GNinja
  ninja
}

# works when not building in a chroot
#check() {
#  cd build
#  ninja test
#}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: sw=2 ts=2 et
