# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=pingus
pkgver=0.7.6
pkgrel=1
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('i686' 'x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'libgl' 'boost-libs>=1.48')
makedepends=('scons' 'boost>=1.48' 'mesa')
source=(http://pingus.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2 pingus.desktop)
md5sums=('561798686f34d3fa4e69135d655f47ac'
         '9eec34047bdcff49e08f41e81764e20c')
sha1sums=('b5f5a25d71beb197c9466fb8928018a377f56487'
          '579a1144f161ce89e6e024cea37210149b89c0c0')

build() {
  cd ${pkgname}-${pkgver}

  scons prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR="${pkgdir}" PREFIX="/usr"

  install -D -m644 "${srcdir}/pingus.desktop" "${pkgdir}/usr/share/applications/pingus.desktop"
}
