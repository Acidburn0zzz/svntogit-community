# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: hydro <hydro@freenet.de>

pkgbase=mediainfo
pkgname=(mediainfo mediainfo-gui)
pkgver=19.09
pkgrel=1

pkgdesc="Supplies technical and tag information about a video or audio file"
url="https://mediaarea.net/"
arch=('x86_64')
license=('BSD')

depends=('libmediainfo' 'wxgtk3')

source=(https://mediaarea.net/download/source/mediainfo/$pkgver/mediainfo_$pkgver.tar.xz)

sha256sums=('02f5b0ae42825776b51500f023ff883fd632dc4cbe229f00d7528bf0e8d45aa8')

build() {
  cd MediaInfo/Project/GNU/CLI
  ./autogen.sh
  ./configure --prefix=/usr
  make

  cd "$srcdir/MediaInfo/Project/GNU/GUI"
  ./autogen.sh
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package_mediainfo() {
  pkgdesc+=" (CLI interface)"
  depends=('libmediainfo')

  cd MediaInfo/Project/GNU/CLI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_mediainfo-gui() {
  pkgdesc+=" (GUI interface)"
  depends=('libmediainfo' 'wxgtk3')

  cd MediaInfo/Project/GNU/GUI
  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfo/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
