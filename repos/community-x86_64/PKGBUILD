# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=1.2.6
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('2ac2438de1f79a955435737c57c793288a2ebeb32e9ac9e9ed07eb5f25f291a2681c48cbad1008bd82822979558216ae3fefbeb61855cc66ce611e5cf3204773')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
