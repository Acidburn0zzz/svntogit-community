# Maintainer: David Runge <dave@sleepmap.de>
pkgname=polyphone
pkgver=2.1.1
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'flac' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libogg' 'libvorbis' 'openssl' 'qt5-svg' 'portaudio' 'qcustomplot' 'rtmidi'
'stk')
makedepends=('qt5-tools')
# xdg desktop integration/ man pages on separate branch:
# https://github.com/davy7125/polyphone/issues/78
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}.1::https://raw.githubusercontent.com/davy7125/polyphone/7e7257b04dc10922c9c7f3d2ec643246cb98b4de/debian/polyphone.1"
        "${pkgname}.fr.1::https://github.com/davy7125/polyphone/blob/7e7257b04dc10922c9c7f3d2ec643246cb98b4de/debian/polyphone.fr.1"
        "${pkgname}.desktop::https://raw.githubusercontent.com/davy7125/polyphone/7e7257b04dc10922c9c7f3d2ec643246cb98b4de/debian/polyphone.desktop"
        "${pkgname}.xml::https://raw.githubusercontent.com/davy7125/polyphone/7e7257b04dc10922c9c7f3d2ec643246cb98b4de/debian/polyphone.sharedmimeinfo")
sha512sums=('65f24a1dc77450c145e0fb45eaa4f6b2a01e72a0bc966bbd82303176bcf3c4ed9189af1422f6e270f713821213f0e1f0355b9edfd6c182e35eef16596c71e9dd'
            '26b437f09977507220119223b817e02408fe6484e9dadd06af19a2c5e80d9f4232b013b5ed8a9631703a0933c7493a79d3e3a0ce012994b395c93a6df45481d2'
            '658da61c125efcde6207e4deae7ab1439a1ced8df786c3f7e0a631dbb1ece0d7dfd406d2fc70a9144cddf8e1cd01075589b326136f09abbb0043d83ee6a8d900'
            '510e28cb2444e050cb36e54eae22237717ad5d153c31628f362792c33e6c4bab802b7d372851fca67be0847fd2af29429cf5222f71c37dfdceecce4e0a26103a'
            'c1ff6c2d50c7a6afab8e9e71811bdd155c9f4f6ece60ba3b3ed6664c2cf0f1cc003b6d0f5688adde6ad698a8d7f0117229b3d5db6aa2173bc54dec52330592dd')

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro"
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "resources/logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -vDm 644 "resources/${pkgname}.png" \
    -t "${pkgdir}/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 "${srcdir}/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "${srcdir}/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "${srcdir}/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
