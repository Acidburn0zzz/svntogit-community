# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.9.12
pkgrel=1
pkgdesc="Old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=('x86_64')
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'libjack.so' 'liblo.so' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'libjack.so: for standalone application'
            'liblo.so: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0554522029d2f0be17a33182e9b43b68c3c38a654aa272a1da3bd1c4845b7898cd8c6bd59ad2b3171df61f6391573d5870b4c4c1b88f7e2c9b16358372b88e78')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
