# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpac
pkgver=4288
pkgrel=1
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
license=('LGPL')
depends=('ffmpeg' 'libjpeg' 'libpng' 'glu' 'libxv')
makedepends=('jack' 'a52dec' 'freetype2' 'faad2' 'libmad' 'mesa')
optdepends=('jack: for jack support' 'a52dec: for A52 support'
            'faad2: for AAC support' 'libmad: for mp3 support')
options=('!makeflags')
source=(ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.xz{,.sig})
sha1sums=('6ba29de672b6e4260e510e0e16d5bc0ab09cabb6'
          '794b0e4a497af527e093ff0328f1acc8995f2b04')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/svn ]] || (echo "svn not found. Install subversion." && return 1)
  _svnver=${pkgver}
  _svntrunk="https://gpac.svn.sourceforge.net/svnroot/gpac/trunk/gpac"
  _svnmod="$pkgname-${pkgver}"
  mkdir ${pkgname}-${pkgver}
  pushd ${pkgname}-${pkgver}
  svn co $_svntrunk --config-dir ./ -r $_svnver $_svnmod
  revision="$(svnversion ${pkgname}-${pkgver})"
  echo "#define GPAC_SVN_REVISION     \"$revision\"" > ${pkgname}-${pkgver}/include/gpac/revision.h
  find . -depth -type d -name .svn -exec rm -rf {} \;
  tar -cJf ../${pkgname}-${pkgver}.tar.xz ${pkgname}-${pkgver}/*
  popd
  rm -r ${pkgname}-${pkgver}
  gpg --detach-sign --use-agent -u ${GPGKEY} ${pkgname}-${pkgver}.tar.xz
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-lib
}
