# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=2.7.6
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'byzanz' 'ffmpeg')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver.tar.gz")
sha512sums=('90e30fe45452eece0cd3ad9ffd4082a458904c6077d65dbb29560055c800c453d67015550dd30ca5d9da71a39fe2e711c6de88af4cce002234edb17fd99d353d')

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
