# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=batctl
pkgver=2019.3
_advver=$pkgver
pkgrel=1
pkgdesc='B.A.T.M.A.N. advanced control and management tool'
arch=('x86_64')
url='http://www.open-mesh.net/'
license=('GPL')
depends=('libnl')
source=("http://downloads.open-mesh.net/batman/releases/batman-adv-$_advver/batctl-$pkgver.tar.gz"{,.asc})
sha512sums=('24f23f24d5e5045ce80f09bc05be6bb8953ddb5a24526d67bd62cb4172a2958190c672c5fd57441dd19a3ca85780cc812ae72e681bb565e51ec652ffce7d32bb'
            'SKIP')
validpgpkeys=('2DE9541A85CC87D5D9836D5E0C8A47A2ABD72DF9')  # Simon Wunderlich

build(){
  cd batctl-$pkgver
  make
}

package() {
  cd batctl-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr SBINDIR=/usr/bin install
}
