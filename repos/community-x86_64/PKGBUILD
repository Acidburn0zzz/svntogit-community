# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=weston
pkgver=1.0.5
pkgrel=2
pkgdesc='Reference implementation of a Wayland compositor'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
options=(!libtool)
depends=('libxkbcommon' 'wayland' 'mesa' 'cairo' 'poppler-glib' 'mtdev' 'libxcursor' 'glu')
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz")
sha1sums=('a97030613e066c05e267ea0d7888fd5f7a08eea2')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston \
		--disable-android-compositor
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	# embed more clients
	for _c in clickdot cliptest dnd editor eventdemo flower gears image \
	 keyboard resizor simple-egl simple-shm smoke view; do
		install -Dm755 "clients/$_c" "$pkgdir/usr/bin/weston-$_c"
	done
}
