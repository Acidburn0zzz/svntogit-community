# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-0-3
_patchver=4
pkgver=${_majver//-/.}.p${_patchver}
pkgrel=3
pkgdesc="Computer Algebra System for polynomial computations"
arch=(i686 x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
        singular-ntl10.patch::"https://github.com/Singular/Sources/commit/861b7899.patch" revert-86ce458e.patch)
md5sums=('5b5c67b2ff2e8fa12d2fc5afc327e2d8'
         '3dce1e6533a1b4686f9730e0b534df65'
         'cc5eaa95cf24e5e295917f9ec96dea5e')

prepare() {
  cd singular-${_majver//-/.}
# Fix build with NTL 10
  patch -p1 -i ../singular-ntl10.patch
# Revert commit that breaks Sage
  patch -Rlp1 -i ../revert-86ce458e.patch
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

# fix permissions
  chown -R root:root "$pkgdir"/usr/share
  chmod 755 "$pkgdir"/usr/share/singular/html
  chmod 644 "$pkgdir"/usr/share/singular/html/*
  chmod 644 "$pkgdir"/usr/share/info/singular.hlp
# move singular.hlp to the path expected by SageMath
  install -Dm644 "$pkgdir"/usr/share/{info,singular}/singular.hlp
}
