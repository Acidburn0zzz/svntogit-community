# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=fscrypt
pkgver=0.2.8
pkgrel=1
pkgdesc='A tool for managing Linux filesystem encryption'
arch=('x86_64')
url='https://github.com/google/fscrypt'
license=('Apache')
makedepends=('go')
depends=('pam')
source=("fscrypt-$pkgver.zip::https://github.com/google/fscrypt/archive/v$pkgver.zip"
        "pam_config")
sha256sums=('e9a48b6c10db2f55cd39f2be3c9a8e80ae32134b95ddac99fb6585ae3380cc92'
            'ae6ceaefc6d936c95a9b7a3f925111ffb946e6fd0152373247f1d40132f05aef')

build() {
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd "fscrypt-$pkgver"
  BUILDDATE="`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}"`" make
}

package() {
  cd "fscrypt-$pkgver"
  make PREFIX="${pkgdir}/usr" install
  install -Dm644 ../pam_config "${pkgdir}/etc/pam.d/fscrypt"
  install -Dm644 -t "$pkgdir/usr/share/licenses/fscrypt/" LICENSE

  # Remove Ubuntu-specific PAM file
  rm -rf "$pkgdir"/usr/share/pam-configs/
}
