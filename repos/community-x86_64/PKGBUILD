# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Colin Arnott

pkgname=purple-skypeweb
_pkgname=skypeweb
_parentname=skype4pidgin
pkgver=1.6
pkgrel=1
arch=('x86_64')
license=('GPL3')
pkgdesc="SkypeWeb Plugin for Pidgin"
url="https://github.com/EionRobb/skype4pidgin/tree/master/skypeweb"
depends=('libpurple' 'json-glib')
makedepends=('pkg-config')
conflicts=('skype4pidgin')

source=("$pkgname-$pkgver.tar.gz"::"https://github.com/EionRobb/$_parentname/archive/$pkgver.tar.gz")
sha256sums=('e646cf83acf2ae453ff47333e41bfd2c593faf5a18ed77f6bc82807ad1d068e2')

build() {
    cd "$srcdir/$_parentname-$pkgver/$_pkgname"
    make
}

package() {
    cd "$srcdir/$_parentname-$pkgver/$_pkgname"
    make DESTDIR="$pkgdir" install
}
