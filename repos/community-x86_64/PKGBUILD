# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=0.0.7
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-tool-kit' 'procps-ng' 'nethogs' 'libcap')
makedepends=('qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('f0d05575881fa27a2048fa3daf03af73a898cb8943db63f366d05c2038e167ff26300c2f772f74f51f9d23ffc8dc2483e14cda6452fda01009fec0fe23aa75a0')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr APPDIR=/usr/share/applications DSRDIR=/usr/share/$pkgname QMAKE_STRIP=
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm755 deepin-system-monitor "$pkgdir"/usr/bin/deepin-system-monitor
}
