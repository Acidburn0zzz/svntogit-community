# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: issue <issue at archlinux dot info>

pkgname=rust-racer
_pkgname=racer
pkgver=2.1.31
pkgrel=1
pkgdesc="Code completion for Rust"
url="https://github.com/racer-rust/racer"
makedepends=('rustup' 'cmake')
arch=('x86_64')
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://github.com/racer-rust/racer/archive/v${pkgver}.tar.gz")
sha256sums=('0ff33fa15ac0384376741d16ddb05a65263dde4e2c5d0f7a9f3747db788764aa')

build() {
  cd "$_pkgname-$pkgver"
  rustup default nightly-2020-03-11
  cargo build --release --locked
}

# Currently borked :<
# check() {
#   cd "$_pkgname-$pkgver"
#   rustup default nightly-2020-03-11
#   cargo test --release --locked
# }

package() {
  cd "$_pkgname-$pkgver"
  install -Dm755 target/release/racer "$pkgdir/usr/bin/racer"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
