# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=cysignals
pkgver=1.3.2
pkgrel=3
pkgdesc="Interrupt and signal handling for Cython"
arch=(i686 x86_64)
url="https://www.sagemath.org"
license=(LGPL)
depends=(python2 pari-sage)
makedepends=(python2-sphinx cython2)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgname-$pkgver.tar.bz2")
md5sums=('abc98cb7b07c3a40979ee24dead06a38')

prepare() {
  sed -e 's|sphinx-build|sphinx-build2|g' -i $pkgname-$pkgver/docs/Makefile
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir"

# Fix include dir
  sed -e 's|/build/cysignals/pkg/cysignals||' -i "$pkgdir"/usr/lib/python2.7/site-packages/cysignals/__init__.pxd
}
