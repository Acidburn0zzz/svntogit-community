# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=galculator
pkgver=2.1
pkgrel=1
pkgdesc='GTK+ based scientific calculator'
arch=('i686' 'x86_64')
url='http://galculator.sourceforge.net/'
license=('GPL')
depends=('gtk3' 'hicolor-icon-theme' 'gtk-update-icon-cache' 'desktop-file-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://downloads.sourceforge.net/galculator/galculator-$pkgver.tar.bz2)
sha256sums=('f3ae03601a123287cddf6f79e79cf37bbe5ce87a98997e0fd623f1cd08e6f6be')

build() {
  cd $srcdir/$pkgname-$pkgver
  
  LDFLAGS+=' -lm'
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
