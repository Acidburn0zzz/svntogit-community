# $Id$
# Maintainer: Aaron Schaefer <aaron@elasticdog.com>

pkgname=tarsnap
pkgver=1.0.27
pkgrel=1
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl' 'xz')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/$pkgname-autoconf-$pkgver.tgz")
md5sums=('391d9e3dbda19b487a77548ff618e742')
sha256sums=('e26dc7c2aa64c17d8063bc61462d1a0f546b56e33b41fbd2fd6925e506c6914f') 

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
