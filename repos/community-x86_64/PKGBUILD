# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=adriconf
pkgver=1.3
pkgrel=3
pkgdesc="GUI tool to configure Mesa drivers by setting options and writing them to the standard drirc file"
arch=(x86_64)
url="https://github.com/jlHertel/adriconf"
license=(GPL3)
depends=(boost-libs gtkmm3 libxml++2.6 pciutils)
makedepends=(boost cmake git gtest)
_commit=93e4ac64999c3ef3f73aab593cca642ef1d9da6f  # tags/v1.3
source=("git+https://github.com/jlHertel/adriconf#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.appdata.xml "$pkgdir/usr/share/metainfo/br.com.jeanhertel.adriconf.appdata.xml"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.desktop "$pkgdir/usr/share/applications/br.com.jeanhertel.adriconf.desktop"
  install -Dm644 flatpak/br.com.jeanhertel.adriconf.png "$pkgdir/usr/share/icons/hicolor/256x256/apps/br.com.jeanhertel.adriconf.png"
}
