# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.6.2
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-cffi')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('db9f03b32336be0adf4f80ead97cec774c3ba4fa52a3daffbb4a7684f223d346b0f42c1d905a1cee47307a4e66a548b4d7615c807c21698d1a9c84b1f5c58cad')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=.
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
