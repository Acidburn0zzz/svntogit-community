# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=photoflare
pkgver=1.6.7
pkgrel=1
pkgdesc='Quick, simple but powerful Qt-based image editor inspired by PhotoFiltre'
arch=(x86_64)
url='https://photoflare.io/'
license=(GPL3)
depends=(graphicsmagick hicolor-icon-theme qt5-base qt5-imageformats)
makedepends=(qt5-tools)
source=(https://github.com/PhotoFlare/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dd0d98bd927c127d8547b5357e6fca34709da2740220dff3133e44d951585974')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/pixmaps \\/d' Photoflare.pro
}

build() {
  cd $pkgname-$pkgver
  qmake Photoflare.pro PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
