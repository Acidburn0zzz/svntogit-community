# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=0.2.0
pkgrel=2
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
depends=('gcc-libs' 'glibc')
makedepends=('cmake' 'jack' 'libsndfile')
checkdepends=('lv2lint')
optdepends=('jack: for sfizz_jack')
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/v${pkgver}/${pkgname}-v${pkgver}-src.tar.gz")
sha512sums=('935bf08f4443da38c9422ea71af52983f81ce623bef3dd7fa89e322f964727ca0e416a8ec7d7885fe83f764ab9160e15c63b477b839b83aef25aafb7eec84d8a')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DSFIZZ_TESTS=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  LV2_PATH="${PWD}/build" lv2lint "http://sfztools.github.io/sfizz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
