# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
_pkgver=3.6
_patchver=
#pkgver=$_pkgver.r$_patchver
pkgver=$_pkgver
pkgrel=2
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt
         perl-term-readline-gnu perl-term-readkey perl-json sympol)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk permlib)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${_pkgver}-minimal.tar.bz2"
         polymake-normaliz-3.8.3.patch)
sha256sums=('b3378e0cc108e77894e4f2ec4b2f9e22190a59bba6482716900d741cb6775bab'
            'cbcff6ffd486c4c2ac8dede32c3103b4b3a87d89199c083390ad48b961905776')

prepare() {
  cd polymake-$_pkgver
  patch -p1 -i ../polymake-normaliz-3.8.3.patch # Fix build against normaliz 3.8.3
}

build() {
  cd polymake-$_pkgver
  ./configure --prefix=/usr --with-cdd=/usr --with-lrs=/usr --with-permlib=/usr --with-sympol=/usr
  make
}

package() {
  cd polymake-$_pkgver
  make DESTDIR="$pkgdir" install
}
