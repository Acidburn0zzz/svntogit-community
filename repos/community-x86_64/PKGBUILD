# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgname=python-fuse
pkgver=1.0.2
pkgrel=1
arch=('x86_64')
pkgdesc="Python bindings for FUSE"
url="https://github.com/libfuse/python-fuse"
license=('LGPL')
makedepends=('python-setuptools' 'fuse2')
depends=('fuse2' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libfuse/python-fuse/archive/v$pkgver.tar.gz")
sha512sums=('9c71e621db41ea70fe0cf824df73b81704f08f38470b6cddd2d7c5933c90edd80af86a0aa20f19e0e4e4aec1dde1c49b65646931cf7debdc5dc875cb3e2d1c31')

build() {
  cd python-fuse-$pkgver
  python setup.py build
}

package() {
  cd python-fuse-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
