# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=4.4.3
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libnotify
  pango
  sqlite
  zeitgeist
)
makedepends=(
  clang
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'gvfs: Cleaner devices section'
  'tumbler: Thumbnails generation'
)
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=02ad7a20d0703b12d88b6ee03e7b4a5491b3c8ca)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-files

  git describe --tags
}

build() {
  export CC=clang
  export CFLAGS="$CFLAGS -Wno-return-type"

  arch-meson pantheon-files build \
    -D with-unity=disabled
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
