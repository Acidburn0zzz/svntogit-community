# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=13
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject')
makedepends=('glib2' 'jack')
optdepends=('new-session-manager: for session management')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('bc6cfab074a9a1f4e0a0f587a33509048526de684cd4cdc0fab7a13fe1ed120577d706e65729a046c3a26d3faa2ab0812166a766eaa902ef1a4d71e2205b47c5')
b2sums=('26a2bd1df73e6563692bb131693f147db563df3e6f9105dad018e390e4d4b070d890b6a5746339dac33484223df332359a5fa9c1922d687249abfe9bd8f8be47')

prepare() {
  mv -v "$pkgname-release-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # keep autotools happy by creating README
  touch README
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
