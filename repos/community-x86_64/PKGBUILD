# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=phonegap
pkgver=8.2.2
pkgrel=3
pkgdesc="PhoneGap command-line interface and node.js library"
arch=('x86_64')
url="https://github.com/phonegap/phonegap-cli"
license=('Apache')
depends=('semver' 'cordova')
makedepends=('npm' 'python')
install=${pkgname}.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('40e536acd4620e9eaf8cfea8ea097a6b0bf9e38cd716dc345430949cc65dcf0e035ebb369cc47a2f85e664ea452e119320025ee7580827af967c19652cfd6845')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Delete npm installed pyc files
  find "$pkgdir" -name "*.pyc" -delete

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver cordova; do
    rm -r $dep;
  done
}

# vim:set ts=2 sw=2 et:
