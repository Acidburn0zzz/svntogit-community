# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.1.5
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=('x86_64')
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=('BSD')
backup=('etc/stubby/stubby.yml')
depends=('getdns')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/getdnsapi/stubby/archive/v${pkgver}.tar.gz"
        "${pkgname}.sysusers"
        'fix-makefile.patch'
        'ignore-sigpipe.patch')
sha256sums=('f909cd56922e861c830ad6fe3c6f18e1100704345f17891368df9f9430aef80c'
            '2acedcf2b65d6e802a70deb55a5befbb71ff83be68a0ba5231a09c126910142b'
            '82d8e44bda864d634a5e917064af1ea7f172e662b4d6c5a0046570c581418d03'
            'f5cca7d4cdc5cd879c3120996f4817fcd9b81d694528e8dd2680be7e42b9b838')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../fix-makefile.patch
    patch -p1 -i ../ignore-sigpipe.patch
}

build() {
    cd ${pkgname}-${pkgver}
    autoreconf -vfi
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
    rmdir "${pkgdir}"/var{/run,}

    install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 systemd/${pkgname}.conf -t "${pkgdir}"/usr/lib/tmpfiles.d/
    install -Dm644 ../${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
}
