# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2420
pkgname=sonic-visualiser
pkgver=3.2
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('capnproto' 'desktop-file-utils' 'hicolor-icon-theme' 'libfishsound'
'libid3tag' 'liblo' 'liblrdf' 'libmad' 'liboggz' 'libpulse' 'portaudio'
'qt5-svg' 'rubberband' 'sord')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('213cf6bf871628560a9e89e58445e9ee9dfcc050c3c013b535e37ebff08c9952d03001d08d20cedd965539e49aae3bdc1df7e6edad585597638ea0071109b84f')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"
  ./configure --prefix=/usr
  qmake-qt5 ${pkgname}.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"

  make install
  # install some things separately, because build system is broken:
  # https://sourceforge.net/p/sv1/bugs/247/

  # desktop files
  install -t "${pkgdir}/usr/share/applications/" -vDm 644 "${pkgname}.desktop"
  # samples
  install -t "${pkgdir}/usr/share/${pkgname}/samples" -vDm 644 samples/*
  # templates
  install -t "${pkgdir}/usr/share/${pkgname}/templates" -vDm 644 templates/*
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGELOG,CITATION,README.{md,OSC}}
}
