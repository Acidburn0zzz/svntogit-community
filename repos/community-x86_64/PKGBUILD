# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-at1
pkgver=0.6.0
pkgrel=5
pkgdesc="An 'autotuner' Jack application."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/zita-at1-doc/quickguide.html"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'clxclient' 'clthreads' 'fftw' 'jack' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}-pkgconf.patch")
sha512sums=('835bc336c35c9364753f1adaca9569852ed9eabdff20d618acc128735dafd383dfcb9a8b8c9f631f7bc6fe549dae3af9c5508ad378ed85169406855d37b4608d'
            '1888e5b5bdfe0d3fac0cd655dce0f9f0d38cdbb027d289ba604a05fce5dd124ef9c1a1859d2ade1c7cf35e68ab553796c9fb3e0f82b0daa33e9e297108858971')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  patch -Np2 -i "../../${pkgname}-pkgconf.patch"
  # strip march=native
  sed -e '/native/d' -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,README} \
    -vDm 644 ../doc/*
}
# vim:set ts=2 sw=2 et:
