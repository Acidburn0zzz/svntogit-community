# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=20.2
pkgrel=3
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('i686' 'x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'gtk3' 'libgnome-keyring' 'libnotify' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala' 'intltool' 'itstool' 'nautilus')
optdepends=('nautilus: backup extension')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/20/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('678530be84f00af19c837f134613af21')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/\"python\"/\"python2\"/' common/PythonChecker.c

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --without-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
