# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: ViNS <gladiator@fastwebnet.it>
# Contributor: Daenyth
pkgname=nethogs
pkgver=0.8.1
pkgrel=1
pkgdesc="A net top tool which displays traffic used per process instead of per IP or interface"
url="http://nethogs.sourceforge.net"
license=('GPL')
depends=('libpcap' 'ncurses' 'gcc-libs')
arch=('i686' 'x86_64')
#source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
source=(https://github.com/raboof/nethogs/archive/v$pkgver.tar.gz
        gcc44.patch)
md5sums=('cc0aed87c4cc67fc2ffc5f60aa67bf3d'
         '62eb824fbbadd1c60d0749593c16d293')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's/sbin/bin/g' Makefile

  # Patch the code to work with gcc 4.4
  patch -Np1 -i "$srcdir/gcc44.patch"

  make prefix="/usr"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make prefix="/usr" DESTDIR="$pkgdir" install
}
# vim:set ts=2 sw=2 et:
