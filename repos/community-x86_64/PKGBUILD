# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=2.11
pkgrel=1
pkgdesc='A tool to diagnose issues with power consumption and power management'
arch=('x86_64')
url='https://01.org/powertop/'
license=('GPL2')
depends=('gcc-libs' 'libnl' 'ncurses' 'pciutils')
optdepends=('xorg-xset: for the --calibrate function')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/fenrus75/powertop/archive/v$pkgver.tar.gz)
sha256sums=('770d848005ce0b134bb603f3a3ab2e65acc3236675827d47108b13a75e42b66b')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
# version 2.11 info still needs fix
  sed -i 's:RUN-VERSION-SCRIPT-IN-GIT-REPOSITORY-ONLY:v2.11:' scripts/version
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
