# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.2
_pkgver=$pkgver
pkgrel=2
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$_pkgver.tar.gz"
"missing-icons.patch::https://github.com/pdfpc/pdfpc/commit/126a0843bb860e525e141a0ffac6529fd6d73ee3.patch")

sha256sums=('6a980415fd914a07e3a0b28c96a4b3e1e2541146869f2c8dd2e12e4ae3b9c2fb'
            '45dd3e40771033b0e3d11e8d50f57629f80cb9143a857c602b52412fc5558cc1')

prepare() {
    cd "$srcdir/$pkgname-$_pkgver"
    patch -Np1 -i ../missing-icons.patch
}

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
