# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.35.0
pkgrel=3
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
b2sums=('1a179cfa8e4a6bb5f7543e7a22ef2de596b5f03cb0c401e1751d525cbf7e3466704495b815a84e609cc67d99fe18f9be2b840dd5b380afd2c05667418ca9431b')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --all-features --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
