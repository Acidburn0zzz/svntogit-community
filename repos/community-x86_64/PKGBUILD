# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.4.2
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(help2man meson jack)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c70fca7cb0f6b791e0c1b507327e2b5673f59be94b9d74b05007558cd0a403ced6a256752c0d84ae752d6bb54a309d864647a0fc8af79fa95ab35ea6ff3beaf8')
b2sums=('008dca9803d11c4b082cb97b1b2bb99dd9a3a804090d0c92c456c8eb04d20a47db9ad3d536d7b6ae3230cdd944d0a58238f2336295cc198fad9484e56cdb7de3')

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -fv build
}

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson -Drtaudio=disabled build
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build

  # licenses
  install -vDm 644 LICENSE.md LICENSES/MIT.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
