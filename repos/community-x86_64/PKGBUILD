# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=atril
pkgver=1.20.3
pkgrel=1
pkgdesc="MATE document viewer"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('djvulibre' 'libspectre' 'libgxps' 'mathjax' 'poppler-glib' 'webkit2gtk')
makedepends=('caja' 'gobject-introspection' 'intltool' 'itstool' 'texlive-bin')
optdepends=('caja: Document Properties extension'
            'texlive-bin: DVI support')
groups=('mate-extra')
conflicts=('atril-gtk3')
replaces=('atril-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('79dbcdf663a7a40194ece1b1ac3304a35b6828b0a68eed286c0e9c319dc77903')

prepare() {
    cd $pkgname-$pkgver

    # Fix mathjax path
    sed -i 's|/usr/share/javascript/mathjax|/usr/share/mathjax|' backend/epub/epub-document.c
}

build() {
    cd $pkgname-$pkgver
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgbase} \
        --with-gtk=2.0 \
        --enable-djvu \
        --enable-dvi \
        --enable-epub \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd $pkgname-$pkgver
    make DESTDIR="$pkgdir" install
}
