# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: arjan <arjan@archlinux.org>

pkgname=allegro
pkgver=5.0.8
pkgrel=1
pkgdesc='Portable library mainly aimed at video game and multimedia programming'
arch=('x86_64' 'i686')
url='http://alleg.sourceforge.net/'
license=('custom')
depends=('jack' 'libxpm' 'libxxf86dga' 'libgl' 'physfs' 'gtk2' 'libpulse')
makedepends=('cmake' 'mesa' 'glu')
source=("http://downloads.sourceforge.net/alleg/$pkgname-$pkgver.tar.gz")
sha256sums=('272e77d0ca0cef7586d7207887f87bf271e1d0db5f8f208dc7dd9a5e3bb6140b')

build() {
  cd "$srcdir"

  mkdir build
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWANT_DOCS=OFF \
    -DWANT_PHYSFS=ON
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
  install -Dm644 "../$pkgname-$pkgver/LICENSE.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
