# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.5.6
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/iikira/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("$pkgname-$pkgver.tar.gz::https://github.com/iikira/BaiduPCS-Go/archive/v$pkgver.tar.gz")
sha512sums=('fcec0e07b984dd83e554c98866db771737284c3ce1321e4fa05137e5d7ae59f5d4d1280024984dab3a5aa0a097312d0454c6d0d16d97c823d97342c44c8ca31e')

prepare() {
  cd BaiduPCS-Go-$pkgver

  mkdir -p .gopath/src/github.com/iikira
  ln -sf "$PWD" .gopath/src/github.com/iikira/BaiduPCS-Go
  export GOPATH="$PWD/.gopath:/usr/share/gocode"

  go get github.com/iikira/args github.com/olekukonko/tablewriter github.com/peterh/liner \
         github.com/urfave/cli
}

build() {
  cd BaiduPCS-Go-$pkgver
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
