# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cantonese
pkgver=0.0.0.20200623
_commit=48dfdaf66ae5fc2bac3bc879feb58526b7cdc015
pkgrel=1
pkgdesc="Cantonese input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cantonese"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-cangjie' 'rime-emoji' 'rime-luna-pinyin' 'rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cantonese/archive/$_commit/rime-cantonese-$_commit.tar.gz")
sha512sums=('34c84fd5722e027a9872033cf4c10baef92f689545a36e54affafcb1fcdd85c53fa8cafca3da58d3cedf91dc9e4c7abeadfbeec2a978d2001828fb25435e8a51')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
