# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=34
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('openssl')
makedepends=('systemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz")
sha512sums=('05e6c0c28a76d2f7d327cb0725a8e89d4db7bb5519a6d381680c1b01e75f98a8777b718e6a6c27a864c3f4f3dc90c2bbd9946ed78f2c64bd3f49b67477ae26e0')

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  make DESTDIR="$pkgdir" RUNSTATEDIR=/run SBINDIR=/usr/bin install

  rm -r "$pkgdir"/etc/init.d
}
