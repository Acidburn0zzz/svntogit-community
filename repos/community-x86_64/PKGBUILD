# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.14
pkgname=nomacs
pkgver=3.14
pkgrel=1
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('exiv2' 'gcc-libs' 'glibc' 'libtiff' 'libraw' 'opencv' 'quazip'
'qt5-base' 'qt5-svg')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('09d4c222f421661a09b7dfd5d2d162a5bb773dd9b262563dfe9f8af4fafd5d653892a1c42993154cf362fb69668bc4bac42da3a0b32b04b421513eca1ae54ec3'
            'c0bca87e50b7b91d80a9d6afd95eeb1d6730b17b9a28ad4109514b36c42781b3f67b25f68f48c0953110d1e02bb03762e813af9c1ec2851c072b4aea74969cbd')

prepare() {
  cd "${pkgname}-${pkgver}"
  # symlinking plugins into place
  ln -sv "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}" "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DUSE_SYSTEM_QUAZIP=ON \
        -DENABLE_TRANSLATIONS=true \
        -B build \
        -S ./ImageLounge
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
