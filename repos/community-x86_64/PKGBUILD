# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.3.0.15
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center' 'deepin-wallpapers')
makedepends=('deepin-gettext-tools' 'qt5-tools')
provides=('deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('c40af77125dc7d7b9a9149d788b4e1fe075be24215e930ee6e054606c631511041ad7a128d7ef7e316710522b803f279f3a8f25470660c92c2ef0a735b7f05fd')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp

  # Well this is so broken
  # https://github.com/linuxdeepin/dde-session-ui/issues/170
  sed -i '7s/dtkwidget$/dtkwidget testlib/' dde-osd/dde-osd.pro
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
