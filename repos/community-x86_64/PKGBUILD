# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cantonese
pkgver=0.0.0.20200510
_commit=a9ce8ade7376599bf94a2388a443ba4d23b1f286
pkgrel=1
pkgdesc="Cantonese input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cantonese"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-cangjie' 'rime-emoji' 'rime-luna-pinyin' 'rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cantonese/archive/$_commit/rime-cantonese-$_commit.tar.gz")
sha512sums=('0c720e2f18834d8264fa4cbe296bd42e49ee9a9203744a77a5e01645255b0799a47243fd9db113669a8a1cb05c3692390fc166a69560692478fa2d01842c5af9')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
