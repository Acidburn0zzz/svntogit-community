# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5platform-plugins
pkgver=5.0.13
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5platform-plugins"
license=('GPL3')
provides=('deepin-qt5dxcb-plugin')
conflicts=('deepin-qt5dxcb-plugin')
replaces=('deepin-qt5dxcb-plugin')
depends=('cairo' 'kwayland' 'qt5-wayland' 'qt5-x11extras')
makedepends=('expac' 'qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5platform-plugins/archive/$pkgver.tar.gz"
         deepin-qt5platform-plugins-qt5.15.patch)
sha512sums=('2abaa535b1450a430334ed46a179bf525c71014c9187d8ac8274af761f39f537611ed97f80cab68626ccb71bb09c3b3a9e2552284d30e772f1028e7e1c32181d'
            '92ef3be31a679a5df77ef6c9acf2325e7ef398eb255cdc950684e8ce2c7ecc9b4c1d9b3599bca788f06e55c984db26e94ec21094964e1719a5544dcf24df4775')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r xcb/libqt5xcbqpa-dev wayland/qtwayland-dev
  # Disable wayland for now: https://github.com/linuxdeepin/qt5platform-plugins/issues/47
  sed -i '/wayland/d' qt5platform-plugins.pro

  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' xcb/linux.pri
  sed -i "/qtwayland-dev/a /usr/include/qt/QtWaylandClient/$(expac %v qt5-wayland | cut -d - -f 1) /usr/include/qt/QtXkbCommonSupport/$(expac %v qt5-base | cut -d - -f 1) \\\\" wayland/wayland.pro

  patch -p1 -i ../deepin-qt5platform-plugins-qt5.15.patch # Fix build with Qt 5.15
  # https://github.com/linuxdeepin/qt5platform-plugins/pull/48
  sed -i 's/xcbWindow-/window-/' xcb/windoweventhook.cpp
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
