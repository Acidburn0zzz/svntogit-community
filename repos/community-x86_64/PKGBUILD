# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=solidity
pkgver=0.4.10
pkgrel=1
pkgdesc='Contract-Oriented Programming Language'
arch=('i686' 'x86_64')
url='https://github.com/ethereum/solidity'
license=('GPL3')
depends=('boost-libs' 'jsoncpp')
makedepends=('boost' 'cmake')
source=("${url}/releases/download/v${pkgver}/solidity_${pkgver}.tar.gz")
sha256sums=('a1103f91d240101a90c9bec5243f2436563d3772c4571a352ea7bc21de8706d1')

prepare() {
    cd solidity_${pkgver}

    sed -e '/jsoncpp\.cmake/d' -i CMakeLists.txt
}

build() {
    cd solidity_${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTESTS=OFF \
        ..
    make
}

package() {
    cd solidity_${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
