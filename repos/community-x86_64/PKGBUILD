# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-calendar
pkgver=5.0.6
pkgrel=1
pkgdesc='The Pantheon Calendar'
arch=(x86_64)
url=https://github.com/elementary/calendar
license=(GPL3)
groups=(pantheon)
depends=(
  clutter
  evolution-data-server
  folks
  gdk-pixbuf2
  geoclue
  geocode-glib
  glib2
  gtk3
  libchamplain
  libgee
  libgranite.so
  libical
)
makedepends=(
  git
  intltool
  meson
  vala
)
_tag=74d53fffaf378ef09c52adf555453145c9ac69c0
source=(pantheon-calendar::git+https://github.com/elementary/calendar.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd pantheon-calendar

  git describe --tags
}

build() {
  arch-meson pantheon-calendar build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
