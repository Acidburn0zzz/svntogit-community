# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=hey
pkgver=0.1.3
pkgrel=3
pkgdesc='HTTP load generator'
url="https://github.com/rakyll/hey"
arch=(x86_64)
license=('GPL')
depends=('glibc')
makedepends=('go')
source=($pkgname-$pkgver.tar.gz::https://github.com/rakyll/hey/archive/v$pkgver.tar.gz)
sha512sums=('4c2a9ea1797b7345c182c3d80bc55e666e7e66f7e8067fd49fc8fead34756036b126aae7f7a41d6d38014458ad5d5015c6bcd4009b77e35699ce5376aa297b07')


build() {
  cd $pkgname-$pkgver
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-extldflags \"${LDFLAGS}\"" \
    .
}


package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
}
