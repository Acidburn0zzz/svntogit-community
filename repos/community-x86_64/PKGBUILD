# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: ThecaTTony <thecattony at gmx dot com>
# Contributor: <giacomogiorgianni at gmail dot com>

pkgname=quiterss
pkgver=0.18.10
pkgrel=2
pkgdesc='Fast and light RSS/Atom feed reader written in Qt/С++'
arch=(x86_64)
url='https://quiterss.org/'
license=(GPL3)
depends=(gst-plugins-good qt5-multimedia qt5-webkit)
makedepends=(qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/QuiteRSS/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('843475913fa876e38cab147aa458a8a282efaee4d25f828d8b731e70799a4f32')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 CONFIG+=release PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}

