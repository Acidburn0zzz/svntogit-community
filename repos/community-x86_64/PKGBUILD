# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.13
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'fftw' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e8f44286081b87481bc7d78dd284124eea6def165f4db2a0f88c2443ecb760b46664ce0371f5f50b441d9febaf62adfbbb024e0474674d3062cbd1ae5397460d')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  depends+=('libfftw3.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
