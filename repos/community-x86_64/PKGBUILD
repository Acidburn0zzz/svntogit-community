# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.8.0
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("https://github.com/qjfoidnh/BaiduPCS-Go/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6c3792d4f7be4f022cb8c075cf0a107fe055fa9e5b970362a92af0bc17dbe2eca50f9de35db7b567ff42bdd8123ed6cd7161ddeddfa4ff969643e99c55b48c17')

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
