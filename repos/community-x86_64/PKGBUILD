# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Vaporeon <vaporeon@vaporeon.io>

pkgname=rgbds
pkgver=0.4.0
pkgrel=3
pkgdesc='Rednex GameBoy Development System'
arch=(x86_64)
url=https://github.com/rednex/rgbds/
license=(MIT)
depends=(
  glibc
  libpng
)
makedepends=(git)
_tag=4e96cf98759de41c118cbeb4e5d7765737b95982
source=(git+https://github.com/rednex/rgbds.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd rgbds

  git cherry-pick -n 65121e6d5d2b9cb15283a0d5c46addeb67d20a7b # GCC 10 fix
}

build() {
  make \
    VERSION_STRING="${pkgver}" \
    CFLAGS="${CFLAGS}" \
    LDFLAGS="${LDFLAGS}" \
    -C rgbds
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr mandir=/usr/share/man -C rgbds install
  install -Dm 644 rgbds/LICENSE -t "${pkgdir}"/usr/share/licenses/rgbds/
}

# vim: ts=2 sw=2 et:
