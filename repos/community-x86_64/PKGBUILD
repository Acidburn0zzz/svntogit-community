# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=2.6
pkgrel=1
pkgdesc='Python driver for MongoDB'
arch=('x86_64' 'i686')
license=('APACHE')
url='http://pypi.python.org/pypi/pymongo/'
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha256sums=('0397584348e5b9efa678878ef686ea3bb77f9b83e634480033ce67467d43675a')

build() {
  cd "$srcdir"

  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

package_python2-pymongo() {
  depends=('python2')

  cd "$srcdir/pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "$srcdir/pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
