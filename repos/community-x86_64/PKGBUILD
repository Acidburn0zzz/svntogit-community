# Maintainer: Mihails Strasunse <public@dicebot.lv>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Anders Bergh <anders1@gmail.com>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=('dmd' 'libphobos')
pkgdesc="D programming language compiler and standard library"
groups=('dlang' 'dlang-dmd')
pkgbase=dmd
pkgver=2.075.0
pkgrel=3
epoch=1
arch=('x86_64')
url="http://www.dlang.org"
makedepends=('git' 'dmd')
source=("git+https://github.com/D-Programming-Language/dmd.git#tag=v$pkgver"
        "git+https://github.com/D-Programming-Language/druntime.git#tag=v$pkgver"
        "git+https://github.com/D-Programming-Language/phobos.git#tag=v$pkgver"
        "dmd.conf")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '3d639e89528fed1da90006f4dfb2b0fdc41308da5a96d953381ff4ccf257c035')

build() {
    cd dmd/src
    make -f posix.mak RELEASE=1 PIC=1

    cd "$srcdir"/druntime
    make -f posix.mak DMD="$srcdir"/dmd/src/dmd RELEASE=1 PIC=1 

    cd "$srcdir"/phobos
    make -f posix.mak DMD="$srcdir"/dmd/src/dmd RELEASE=1 PIC=1
}

package_dmd() {
    pkgdesc="The D programming language reference compiler"
    backup=('etc/dmd.conf')
    depends=('gcc' 'libphobos')
    optdepends=(
        'dtools: collection of useful utilities for development in D'
        'gcc-multilib: to cross-compile 32-bit applications'
    )
    provides=("d-compiler=$pkgver")
    license=('custom')

    install -Dm755 dmd/src/dmd "$pkgdir"/usr/bin/dmd

    mkdir -p "$pkgdir"/etc
    install -Dm644 dmd.conf "$pkgdir"/etc/dmd.conf

    mkdir -p "$pkgdir"/usr/share/man/man1
    mkdir -p "$pkgdir"/usr/share/man/man5
    cp -r dmd/docs/man/man1/* "$pkgdir"/usr/share/man/man1/
    cp -r dmd/docs/man/man5/* "$pkgdir"/usr/share/man/man5/

    install -Dm644 dmd/src/ddmd/backend/backend.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-backend
    install -Dm644 dmd/src/ddmd/boostlicense.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-frontend

    mkdir -p "$pkgdir"/usr/share/d/samples/
    cp -r dmd/samples/* "$pkgdir"/usr/share/d/samples/

    find "$pkgdir"/usr -type f | xargs chmod 0644
    chmod 755 "$pkgdir"/usr/bin/*
}

package_libphobos() {
    pkgdesc="The Phobos standard library for D programming language"
    options=('staticlibs')
    depends=('gcc-libs')
    conflilcts=('libphobos-devel')
    provides=("d-runtime=$pkgver" "d-stdlib=$pkgver" "libphobos-devel=$pkgver")
    replaces=('libphobos-devel')
    license=('custom')

    mkdir -p "$pkgdir"/usr/lib
    install -Dm644 phobos/generated/linux/release/*/libphobos2.* "$pkgdir"/usr/lib/
    install -Dm644 phobos/LICENSE_1_0.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

    mkdir -p "$pkgdir"/usr/include/dlang/dmd
    cp -r phobos/{*.d,etc,std} "$pkgdir"/usr/include/dlang/dmd
    cp -r druntime/import/* "$pkgdir"/usr/include/dlang/dmd/

    find "$pkgdir"/usr -type f | xargs chmod 0644

    install -Dm644 druntime/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-druntime
}
