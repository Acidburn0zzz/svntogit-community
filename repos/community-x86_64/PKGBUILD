# Maintainer: Anatol Pomozov
# Maintainer: Filipe Lains (FFY00) <lains@archlinux.org>
# Contributor: Thomas Krug <t.krug@elektronenpumpe.de>

pkgname=libsigrokdecode
pkgver=0.5.2
pkgrel=3
pkgdesc='C library that provides protocol decoding for logic analyzers, protocol decoders, etc.'
arch=('x86_64')
url='https://www.sigrok.org/wiki/Libsigrokdecode'
license=('GPL3')
depends=('glib2' 'python')
source=("https://sigrok.org/download/source/libsigrokdecode/libsigrokdecode-$pkgver.tar.gz"
	    python38.patch::"https://sigrok.org/gitweb/?p=libsigrokdecode.git;a=patch;h=afae4fa61ffd8dee62667e60f931e371061c7d7d")
sha512sums=('49d0cba3a5fe17593165c3669dc8530e4e68497369b461f39b63c903f41a3060766c23d44c4112c9b9a5e740ae1e2c03a735494ac798dc631f3913079be04e22'
            'ad942f5bc407bd9a8682317c7217750054bd7af37015e51ae7c3e1a73aaeffc7d866375da9962be142eb75b2d4bc0e0fd146d1d0c59980ca9c128e64e545fcc8')

prepare() {
  cd $pkgname-$pkgver

  # FS#64518
  patch -p1 < ../python38.patch
}

build() {
  cd $pkgname-$pkgver

  autoreconf --force --install . # it is a content of upstream's ./autogen.sh
  ./configure --prefix=/usr

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
