# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=5.1.0099.0
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'xdg-user-dirs')
makedepends=('cmake' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('f32a2c3743cbd3319a7490b2addabdd7e87e252d121edeac77d0d9761a71ed96adf2d732034dc38fb9e4f2a571530238a7d999bb57de2605bc997fec099f72d9')

prepare() {
  cd dde-launcher-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  make
}

package() {
  cd dde-launcher-$pkgver
  make DESTDIR="$pkgdir" install
}
