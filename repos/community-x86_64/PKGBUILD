# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=xpad
pkgver=5.3.0
pkgrel=1
pkgdesc="Stickies-like notes application"
arch=('x86_64')
depends=('gtksourceview3' 'libsm')
makedepends=('intltool')
url="https://launchpad.net/xpad"
license=('GPL3')
source=(https://launchpad.net/$pkgname/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2{,.asc}
        xpad.appdata.xml)
validpgpkeys=('3930646349B9DD3886D9087392BF2FCCA6F2322B'
	      '8450BEE19B722715FFDB18C6683EAA45951FC552')
sha256sums=('6444ab85110e84571f02551ac0f87fd850235d2518371cd35e5364189b8d693f'
            'SKIP'
            '37a5d234a672438476cc410be3c0bf4bf23a0b5a1c9e882d782c675a9f4d5618')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../xpad.appdata.xml "$pkgdir/usr/share/metainfo/xpad.appdata.xml"
}
