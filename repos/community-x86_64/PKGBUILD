# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.0.0
pkgrel=5
pkgdesc="Free, open source software for digital DJ'ing."
arch=('i686' 'x86_64')
url='http://www.mixxx.org'
license=('GPL')
depends=('fftw' 'libid3tag' 'libmad' 'libogg' 'libshout' 'libsndfile' 'portaudio' 'portmidi'
         'taglib' 'qtwebkit' 'vamp-plugin-sdk' 'libusbx' 'protobuf' 'faad2' 'libmp4v2'
         'rubberband' 'chromaprint' 'sqlite' 'opus' 'opusfile')
makedepends=('mesa' 'scons' 'libshout' 'glu')
source=("https://downloads.mixxx.org/${pkgname}-${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        mixxx-2.0.0-gcc-6.patch)
md5sums=('5520a2f110f1078197d2ff914ccca37a'
         '957245cf7a06e57d42fb7f8e3421a934')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ../mixxx-2.0.0-gcc-6.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons build=release optimize=portable virtualize=0 localecompare=1 qt_sqlite_plugin=0 opus=1 \
    qtdir=/usr/lib/qt4 prefix=/usr faad=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  scons qtdir=/usr/lib/qt4 prefix=/usr install_root="${pkgdir}/usr" install
}
