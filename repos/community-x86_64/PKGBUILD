# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=deferred-folds
pkgname=haskell-deferred-folds
pkgver=0.9.14
pkgrel=1
pkgdesc="Abstractions over deferred folds"
url="https://github.com/metrix-ai/deferred-folds"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-foldl' 'haskell-hashable' 'haskell-primitive'
         'haskell-unordered-containers' 'haskell-vector')
makedepends=('ghc' 'haskell-quickcheck' 'haskell-quickcheck-instances' 'haskell-rerebase'
             'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('a56d4123b495d9c69b4210c640a817fde5d095fb062c8643d3a4f04d98a053d2493a9bd73683ecafe2d18922930b63c82c62ce1649c00e0b797593f4fa6bfa54')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
