# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-persistent
pkgname=('python-persistent' 'python2-persistent')
pkgver=4.4.0
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-cffi' 'python2-cffi')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-manuel' 'python2-manuel')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('c1884a30ea19e694364c6aa50e0d75250c5fcd3e3b5e2ce472e281e0412760a785b66d839e9fa7a778acb6b5f314a26ae681f4330721d558f61507d3548ca28b')

prepare() {
  cp -a persistent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/persistent-$pkgver
  python setup.py build

  cd "$srcdir"/persistent-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/persistent-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" python -m zope.testrunner --test-path=.

  cd "$srcdir"/persistent-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 -m zope.testrunner --test-path=.
}

package_python-persistent() {
  depends=('python-zope-interface')

  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-persistent() {
  depends=('python2-zope-interface')

  cd persistent-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
