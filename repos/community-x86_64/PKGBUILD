# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.16.0
pkgrel=2
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit' 'curl' 'jq' 'libb64')
makedepends=('cmake' 'pandoc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "unified-cgroup-fix.patch::https://github.com/draios/sysdig/commit/2044108ef1f863f77735aa05ccd930ed462aa09c.patch")
sha256sums=('73a0190c973e4a591013d0c73ff2ea9f623ab50b78ff78f7a33fe31460ba24a1'
            '3d8dc29f3d69a4befa8dd3939ccf831ef2c46c56c2bcd3500f41f1eceacb7bba')
install=$pkgname.install

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  sed 's|${DIR_ETC}/bash_completion.d|share/bash-completion/completions/|' \
      -i scripts/CMakeLists.txt

  patch -p1 -i "$srcdir"/unified-cgroup-fix.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
