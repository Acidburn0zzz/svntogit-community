# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.12
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'glibc' 'gcc-libs' 'hicolor-icon-theme' 'libaubio.so'
'libjack.so' 'liblilv-0.so' 'liblo.so' 'libmad' 'libogg' 'librubberband.so'
'libsamplerate' 'libsndfile' 'libsuil-0.so' 'libvorbisenc.so'
'libvorbisfile.so' 'libvorbis.so' 'qt5-base' 'qt5-x11extras' 'zlib')
makedepends=('dssi' 'ladspa' 'lv2' 'qt5-tools')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cdd7414e2b287272164e3876603f4b051f22399e14aeb61c0e8d30a159a146d0028d36463400a239630cb31ba66bcd96302fb522fd8304342e533fd9d420cb2d')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
