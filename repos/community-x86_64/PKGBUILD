# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=2.11.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('45bf4c9693eda2c37e2c2fa5e9239535237e56205a1e0a5afeace37f96d7b595495a270e3f18c0dbfb92cb63e62fae957ea6c08808e7354fc66309d2ed1bba95')
b2sums=('1c35f7c016b3b07ddf0101a49fe84bed407694f6108e4208b7ce875a4dbd69f7049e472c7124da2ba35228ded7c2df08425665ec8154a966447a141c6dfcc5fa')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  # workaround, see https://github.com/vultr/vultr-cli/issues/92#issuecomment-877722219
  export VULTR_API_KEY=x
  ./vultr-cli completion bash > "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  ./vultr-cli completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  ./vultr-cli completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
