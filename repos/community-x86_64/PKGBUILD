# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=3.0.0
pkgrel=2
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('python-cffi') # c-ares is bundled
makedepends=('python-setuptools')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('b253f5dcaa0ac7076b79388a3ac80dd8f3bd979108f813baade40d3a9b8bf0bd')

build() {
  cd pycares-$pkgver
  python setup.py build
}

# 11 tests fail
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local sitepackages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --skip-build --root=temp
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$sitepackages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
