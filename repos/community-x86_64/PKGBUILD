# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-compute-runtime
pkgver=19.52.15209
pkgrel=1
pkgdesc="Intel(R) Graphics Compute Runtime for OpenCL(TM). Replaces Beignet for Gen8 (Broadwell) and beyond."
arch=(x86_64)
url="https://01.org/compute-runtime"
license=(MIT)
depends=(gcc-libs intel-gmmlib intel-graphics-compiler)
makedepends=(cmake libva)
optdepends=('libva: for cl_intel_va_api_media_sharing'
            'libdrm: for cl_intel_va_api_media_sharing')
provides=(opencl-driver)
source=("https://github.com/intel/compute-runtime/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '010-neo-move-GmmClientContext.patch'::'https://github.com/intel/compute-runtime/commit/8cc2a6c28e8f0d10b34081931e0d961bdeeec7ea.patch'
        '020-neo-intel-gmmlib-19.4.1.patch'::'https://github.com/intel/compute-runtime/commit/87e948eef584c7666d4dd7ccf0b5384077deedbf.patch')
sha256sums=('91405bcdc2241073161adc5d01885121adf78454d6dbd3dd796a5fd3fdae2812'
            'b43bdcca78321b3353b43115f6ad527447d6b9bdbd09bd1217ee0435c0de29f6'
            'c142a9bd886bfe3643dd4d9e48b0031ca6b83870603614359146faf7783fb3b0')

prepare() {
    # add support for intel-gmmlib 19.4.1
    patch -d compute-runtime-${pkgver} -Np1 -i "${srcdir}/010-neo-move-GmmClientContext.patch"
    patch -d compute-runtime-${pkgver} -Np1 -i "${srcdir}/020-neo-intel-gmmlib-19.4.1.patch"
}

build() {
    cmake -B build -S compute-runtime-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DNEO_DRIVER_VERSION=${pkgver}
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -Dm644 compute-runtime-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
