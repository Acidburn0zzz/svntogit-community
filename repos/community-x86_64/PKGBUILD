# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.6.0
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
depends=('ncurses')
makedepends=('rust')
url=https://github.com/lotabout/skim
license=('MIT')
source=("$url/archive/v$pkgver/skim-v$pkgver.tar.gz")
sha512sums=('807f16ec27f113b929b2691c47a447a716d94d16cc0e65e4defcbaa706374cf54c90266ef2bf045783106286cb818b6012ed492fe792b61662da3aa5cd100eab')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 shell/skim.1
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}
