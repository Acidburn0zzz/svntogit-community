# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.5.3
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('a28abf820852d564c4eb36801e0141230041b1f9602cda26c271f9d72a5bd6b1a0fc0156cb7c98ea130661b4a3f2e6c8c200e8591b334b40d654c3db4c7c0d55')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core github.com/sun8911879/shadowsocksR
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
