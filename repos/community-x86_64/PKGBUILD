# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.6.8
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'procps-ng' 'libcap')
makedepends=('cmake' 'deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz"
        deepin-system-monitor-qt5.15.patch)
sha512sums=('a1053dfd9fee6c234a60ad21c59733ec997da01270e02ecf9c505a6b083b1c93eb05a7b533e449055f2228e77cb3c21654a78368da92b7ea62da01f51b5fb52e'
            '45f9938885c6e29abf61174db3bc5654646baa7a8ec067ee4e048511eb62ae79996976c02d9275e8fdf1a7321dd0219b58ae54d8d370c5acf8346dd77959c608')

prepare() {
  cd deepin-system-monitor-$pkgver
  patch -p1 -i ../deepin-system-monitor-qt5.15.patch # Fix build with Qt 5.15
  sed -i '/<QPixmap>/a #include <QUrl>' src/process/stats_collector.cpp

# Workaround build failure with GCC 10
  sed -e 's|print_err|print_err_system|g' -i src/process/system_stat.cpp
  sed -e 's|print_err|print_err_process|g' -i src/process/process_stat.cpp
  sed -e 's|print_err|print_err_desktop|g' -i src/process/desktop_entry_stat.cpp
}

build() {
  cd deepin-system-monitor-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make DESTDIR="$pkgdir" install
}
