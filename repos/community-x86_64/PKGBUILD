# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-control-center
pkgver=1.1.7.1
pkgrel=1
pkgdesc="Utilities to configure the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-control-center"
groups=('ukui')
depends=('gtk3' 'mate-desktop' 'peony' 'ukui-indicators' 'ukui-media' 'ukui-menus' 'ukui-settings-daemon' 'upower')
makedepends=('mate-common' 'yelp-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-control-center/archive/v$pkgver.tar.gz")
sha512sums=('324cb1472cd22f6348d418fa5e626991d90ca9d06ab07ab83972b6cba62ee3f34d23bfdc25fe553a79b06e5c5d9c0c06a4952b45346eb06d8ac3d3b00c14c27a')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
