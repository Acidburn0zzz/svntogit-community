# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Tom Payne <twpayne@gmail.com>
# Contributor:  <godeater@gmail.com>

pkgname=chezmoi
pkgver=2.9.2
pkgrel=1
pkgdesc="Manage your dotfiles across multiple machines"
arch=('x86_64')
url='https://www.chezmoi.io/'
license=('MIT')
makedepends=('go' 'git')
depends=('glibc')
checkdepends=('zip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/twpayne/chezmoi/archive/v${pkgver}.tar.gz")
sha512sums=('b47bf6a17f80b03151d365e0f67d24b40eea0c197ce9cc46d27c2b8e1c34dda9d4e153037509acc160799ca1eb25ea90140995bf13c64b89c3df2777ba714d01')

prepare() {
  cd "$pkgname-$pkgver"

  go mod download
}

build() {
  cd "$pkgname-$pkgver"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOPROXY=off
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -tags noupgrade \
    -ldflags "-X github.com/twpayne/chezmoi/cmd.DocsDir=/usr/share/doc/$pkgname \
              -X main.version=$pkgver \
              -X main.date=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -extldflags ${LDFLAGS}" \
    .
}

check() {
  cd "$pkgname-$pkgver"

  go test -v ./...
}

package() {
  cd "$pkgname-$pkgver"

  install -D "$pkgname" "$pkgdir/usr/bin/$pkgname"

  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 docs/*.md "$pkgdir/usr/share/doc/$pkgname/"

  install -Dm644 completions/chezmoi-completion.bash "$pkgdir/usr/share/bash-completion/completions/chezmoi"
  install -Dm644 completions/chezmoi.fish "$pkgdir/usr/share/fish/vendor_completions.d/chezmoi.fish"
  install -Dm644 completions/chezmoi.zsh "$pkgdir/usr/share/zsh/site-functions/_chezmoi"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
