# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.6.3
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('crypto++' 'cups' 'deepin-qt5integration' 'libcups' 'smbclient')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-printer/archive/$pkgver.tar.gz")
sha512sums=('b782df54a508784011ebe505a45cf7ef3d8f6251055af500ec7e65ede922060b3b5152567127cbd10e3d3993530c6afb9236533ef534f9c159bab1004defb4f6')

prepare() {
  cd dde-printer-$pkgver
  sed -i '/include <QEventLoop>/a #include <QCloseEvent>' src/Printer/ui/dprintersshowwindow.h
  sed -i '/include <QList>/a #include <QFile>' src/Printer/vendor/zdrivermanager.h
  sed -i 's/-lcrypto++/-lcryptopp/' src/Printer/Printer.pro
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
