# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.16
pkgname=mate-notification-daemon
pkgver=${_ver}.0
pkgrel=1
provides=('notification-daemon')
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
groups=('mate' 'mate-gtk3')
depends=('dbus-glib' 'libcanberra' 'libwnck3' 'libnotify')
makedepends=('intltool')
conflicts=('mate-notification-daemon-gtk3')
replaces=('mate-notification-daemon-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('e3dbd1b91706503162f75103b3aa6d46335a4c94')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname}

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
