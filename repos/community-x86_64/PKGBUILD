# Maintainer: Remi Gacogne <rgacogne@archlinux.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=hiredis
pkgver=1.0.1
pkgrel=1
pkgdesc='Minimalistic C client library for Redis'
arch=('x86_64')
url="https://github.com/redis/hiredis/"
license=('BSD')
depends=('glibc')
checkdepends=('redis')
source=(https://github.com/redis/$pkgname/archive/v$pkgver/hiredis-$pkgver.tar.gz)
sha256sums=('a420df40775ac7b4b46550dd4df78ffe6620616333496a17c9c9fc556815ba4b')

prepare() {
  # work around the accidental soname bump in 1.0.1
  # https://github.com/redis/hiredis/issues/987
  cd $pkgname-$pkgver
  sed -i "s,#define HIREDIS_SONAME 1.0.1,#define HIREDIS_SONAME 1.0.0," hiredis.h
}

build() {
  cd $pkgname-$pkgver
  make PREFIX=/usr
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
