# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.30
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('eeadd3ccbc312d178c2c01c2f5b4f5282f0ac139cc7dff70bd63a0a1584f7aebfd83f625b30ae3f92638167b19e51eacf96dac93857e947bc56fd376ac2b8b17')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '1i#include <QPainterPath>' src/modules/simplelistdelegate.cpp src/widgets/equalbutton.cpp

  # debundle gmock/gtest
  sed -i '/third_party/d' CMakeLists.txt
  rm -r third_party
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
