# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=primus_vk
pkgver=1.2
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/primus_vk"
license=('BSD')
depends=('nvidia-utils' 'bumblebee' 'vulkan-icd-loader' 'primus')
makedepends=('vulkan-validation-layers')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('48c03a2f0fdb7d4609c3f6e0bf24ee2306805330e381722a2fd0485c9916daa802725117c8b3cc0d13d80376c7384193ea9aff69c9e13a4f48becf6dbf94d0f0')

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS+=' -DNV_DRIVER_PATH=\"/usr/lib/libGLX_nvidia.so.0\"'
  make all primus_vk_diag
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}"/usr install
  install -Dm755 primus_vk_diag -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
