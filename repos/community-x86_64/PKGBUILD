# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ejabberd
pkgver=13.10
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc' 'libyaml')
makedepends=('git') # TODO: build all deps as packages
backup=('etc/ejabberd/ejabberd.cfg'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"git://github.com/processone/cache_tab"
	"git://github.com/processone/tls"
	"git://github.com/processone/stringprep"
	"git://github.com/processone/xml"
	"git://github.com/processone/p1_yaml"
	"git://github.com/rds13/xmlrpc"
	"git://github.com/processone/epam"
	"git://github.com/processone/zlib"
	"git://github.com/basho/lager"
	"git://github.com/processone/eiconv"
	"git://github.com/DeadZen/goldrush.git"
	"$pkgname.logrotate"
	"ejabberd.service")
md5sums=('70a78db576e36c1e714205c983c8a568'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'f97c8a96160f30e0aecc9526c12e6606'
         'df32aa17e40a7bc5d9946d3ba6fe5e16')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --enable-pam --enable-odbc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  mkdir -p $pkgdir/usr/bin
  mv $pkgdir/usr/sbin/ejabberdctl $pkgdir/usr/bin/ejabberdctl
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock" "$pkgdir/usr/sbin"
  install -Dm0644 $srcdir/ejabberd.service $pkgdir/usr/lib/systemd/system/ejabberd.service
}

# vim:set ts=2 sw=2 et:
