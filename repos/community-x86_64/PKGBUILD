# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: kleintux <reg-archlinux AT klein DOT tuxli DOT ch> 

_pkgname=FishFight
pkgname=fishfight
pkgver=0.4
pkgrel=1
pkgdesc="A tactical 2D shooter game"
arch=('x86_64')
url="https://fishfight.org/"
license=('MIT' 'Apache')
depends=('pkg-config' 'libx11' 'libxi' 'mesa-libgl' 'alsa-lib')
makedepends=('cargo' 'cmake')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/fishfight/FishFight/archive/v$pkgver.tar.gz"
  "$pkgname.sh"
)
sha512sums=('4042a8381d3b83a62a50925a14fa5d829ca98bf9c0d6e42bbfa0f1becbfb57a209d8932ff6f2156b0b09646fd0fea71dca25e3d7cb162232b4024d49ad0c78a1'
            '73fc2765f9820f64fbce0b16d4d4d9a40563d4070a39815c6f4cdcefaa186c1346094ffcb972287fba487bc3f5dcaa29df38a150a9ad91e4022f252f2824f4fd')

prepare() {
  cd "$_pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$_pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/lib"
  install -Dm 755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  mkdir -p "$pkgdir/opt/$pkgname"
  cp -r "assets" "$pkgdir/opt/$pkgname/"
  find "$pkgdir/opt/$pkgname/assets/sounds/" -type f -exec chmod 444 {} \;

  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 licenses/LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
