# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vitaliy Berdinskikh ur6lad[at]i.ua
# Previous Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=couchdb
pkgver=1.1.0
pkgrel=1.svn20110823
pkgdesc="A document-oriented database that can be queried and indexed in a MapReduce fashion using JSON"
arch=('i686' 'x86_64')
url="http://couchdb.apache.org"
license=('APACHE')
depends=('icu' 'erlang' 'js' 'openssl' 'curl')
makedepends=('gcc')
install=couchdb.install
options=('!libtool')
backup=('etc/couchdb/local.ini'
	'etc/conf.d/couchdb'
	'etc/logrotate.d/couchdb')
#source=("http://www.apache.org/dist/$pkgname/$pkgver/apache-$pkgname-$pkgver.tar.gz"
source=("http://arch.p5n.pp.ru/~sergej/dl/apache-couchdb-$pkgver.svn20110823.tar.gz"
	"rc-script.patch")
md5sums=('b5a6e2dab45f19ef39f04a46ac146a0a'
         '12171b097d2f98f4874e002bc1eabfc5')

build() {
  cd "$srcdir/apache-$pkgname-$pkgver"

  [ -x configure ] || ./bootstrap
  sed -i 's|-ljs|-lmozjs185|' configure
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
  patch etc/init/couchdb <$srcdir/rc-script.patch
}

package() {
  cd "$srcdir/apache-$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm755 etc/default/couchdb $pkgdir/etc/conf.d/couchdb
  sed -i 's|\(CONFIGURATION_FILE=/etc/\)default\(/couchdb\)|\1conf.d\2|' $pkgdir/etc/rc.d/couchdb
  sed -i 's|\(COUCHDB_OPTIONS=\)|\1"-p /var/run/couchdb/couchdb.pid"|' $pkgdir/etc/conf.d/couchdb

  rm -rf $pkgdir/etc/default/ $pkgdir/var/run
}
