# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=5.3.0.41
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'xdg-user-dirs')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dde-launcher/commit/46d8d026c9859e59ae00320549b0c876d963abae.patch)
sha512sums=('2e934eb959e20b323b2918f540fd6cd4875cbf527d5e8c88b10dd41a7bee46862ff1a9daff712222a54f77cdb9085ccb0804b16da947e3a69bcf70de33be61d4'
            '5f205156fd5151be3800473c95d53f389d905fc631039aca1cb96548b79e105dbe2da9cb7887f3a0f711d2ba2a7d53fab0bb37126107f07c82c4d0f7525a2d59')

prepare() {
  cd dde-launcher-$pkgver
  patch -p1 -i ../46d8d026c9859e59ae00320549b0c876d963abae.patch
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
