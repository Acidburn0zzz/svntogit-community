# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=2.1.0
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="https://drumstick.sourceforge.io/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'doxygen' 'docbook-xsl' 'fluidsynth' 'graphviz'
'libpulse' 'qt5-tools')
provides=('libdrumstick-alsa.so' 'libdrumstick-file.so' 'libdrumstick-rt.so'
'libdrumstick-widgets.so')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('112bcdf5eeb4d0139e2f57b53775dbeb1e4940a0269d9edbf51166a0c9fd99662fa652cdcb6e5019769637ea21593d485db65ef72f28a035d45e8e0b4229680b')
b2sums=('06c2971a87313f051dd0577541a0a8660786f9312ce93a1482be19ed276ed4ddd2b299d5d8cad9ec6bbef19afada727c0aae61cea1c53873396cbf9be960b51a')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  # some tests fail due to insufficient system privileges, hence only run those
  # that make sense in the build environment
  # https://sourceforge.net/p/drumstick/bugs/24/
  make test -C build/tests/fileTest1
  make test -C build/tests/fileTest2
}

package() {
  depends+=('libasound.so' 'libfluidsynth.so' 'libpulse.so' 'libpulse-simple.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO}
}
