# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.1.7.20170713
_commit=9556ff4d4cea378eac99c5de6b77c5bf10d99a85
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$_commit.tar.gz")
sha512sums=('693029a8262e05e97c00b0cca582fac91da3d8931433574e97ec4affbfd38f8383c8326cac25bfa437424d9d51348da3345ea937c2323e70d4ec89ebebf053f0')

prepare() {
  mv dde-file-manager-{$_commit,$pkgver}
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
