# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Contributor: Tom Payne <twpayne@gmail.com>
# Contributor:  <godeater@gmail.com>

pkgname=chezmoi
pkgver=1.5.6
pkgrel=13
pkgdesc="Multi-machine dotfile management system"
arch=('x86_64')
url='https://github.com/twpayne/chezmoi'
license=('MIT')
makedepends=('go-pie' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/twpayne/chezmoi/archive/v${pkgver}.tar.gz")
sha256sums=('4abd6be864e1e4bd6768e7217247a097233a0351438b1e183dac602959f36750')

build() {
  cd "$pkgname-$pkgver"

  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    .
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
