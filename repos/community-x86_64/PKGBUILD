# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=('python-scipy' 'python2-scipy')
pkgver=0.10.0
pkgrel=1
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('i686' 'x86_64')
url="http://www.scipy.org/"
license=('BSD')
makedepends=('gcc-fortran' 'python-numpy' 'python2-numpy')
source=("http://downloads.sourceforge.net/scipy/scipy-${pkgver}.tar.gz")
md5sums=('e357c08425fd031dce63bc4905789088')

build() {
  unset LDFLAGS
  unset FFLAGS

  # Changing the arithmetic parameter (Thanks to Fabrizio Castellano)
  sed -i "s/\#define\ UNK\ 1/\#define\ IBMPC\ 1/" \
    scipy-${pkgver}/scipy/special/cephes/mconf.h

  # 2 builds
  cp -r scipy-${pkgver} scipy-${pkgver}-py2

  # build for python3
  cd scipy-${pkgver}
  python setup.py config_fc --fcompiler=gnu95 build

  # build for python2
  cd ../scipy-${pkgver}-py2

  for file in $(find . -name '*.py' -print); do
       sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
       sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py config_fc --fcompiler=gnu95 build
}

package_python-scipy() {
  depends=('python-numpy')
  provides=('python3-scipy' 'scipy')

  cd scipy-${pkgver}

  python setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python-scipy/LICENSE"
}

package_python2-scipy() {
  depends=('python2-numpy')
  conflicts=('python-scipy<0.9.0')

  cd scipy-${pkgver}-py2

  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root=${pkgdir} --optimize=1

  install -Dm644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/python2-scipy/LICENSE"
}

# vim:set ts=2 sw=2 et:
