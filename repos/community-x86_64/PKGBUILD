# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20190110
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(x86_64)
license=('GPL' 'LGPL')
url="https://www.prevanders.net/dwarf.html"
depends=('elfutils')
options+=('staticlibs')
_commit="245262d6ccaeed17f7564c31b78da2d4df1a3ee0"
source=(https://sourceforge.net/code-snapshots/git/l/li/libdwarf/code.git/libdwarf-code-$_commit.zip)
#source=(https://www.prevanders.net/libdwarf-$pkgver.tar.gz)
sha512sums=('722841824d5a3fd45740e88e401ee2a8e0358833e817c138e904a9a7ddd2a169116f1512f3d6f5a165f988017d510e6efa1e230fd7ec295dacd924c2ad9bbadf')

build() {
  # cd "$srcdir"/libdwarf-$pkgver
  cd "$srcdir"/libdwarf-code-$_commit
  ./configure --prefix=/usr --includedir=/usr/include/libdwarf --enable-shared
  make
}

check() {
  # cd "$srcdir"/libdwarf-$pkgver
  cd "$srcdir"/libdwarf-code-$_commit
  make check
}

package() {
  # cd "$srcdir"/libdwarf-$pkgver
  cd "$srcdir"/libdwarf-code-$_commit
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README NEWS "$pkgdir"/usr/share/doc/$pkgname/
}
