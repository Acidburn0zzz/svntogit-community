# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rsibreak
pkgver=0.12.10
pkgrel=1
pkgdesc="Takes care of your health and regularly breaks your work to avoid repetitive strain injury (RSI)"
url="https://userbase.kde.org/RSIBreak"
arch=(x86_64)
license=(GPL)
depends=(knotifyconfig kidletime hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('93f790e107c9ec98c6a5f45f89b318a0b72c906c69828f2f91094b859510cc51'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}

