# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=20.09
pkgrel=2

pkgdesc="shared library for mediainfo"
url="https://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen')

source=(
  https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz
  https://github.com/MediaArea/MediaInfoLib/commit/7bab1c3a043784be2c90f2e54a0e5a8d7263eead.patch
)

sha256sums=('d07ce857330a9f9eedc4e5748f8022af1e9540e88a732d4e45c818c8ec4dd196'
            'e0cf2ba8c71f5c65446b800484edcb2f5a220fcfdda0e2a1f04eed5c06e6a8d9')

prepare() {
  cd MediaInfoLib

  patch -p1 <"$srcdir"/7bab1c3a043784be2c90f2e54a0e5a8d7263eead.patch
}

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --with-libcurl \
              --with-libmms

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
