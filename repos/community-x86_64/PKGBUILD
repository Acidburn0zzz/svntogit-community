# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=1.7.4
pkgrel=1
pkgdesc='Menu for Xfce4'
arch=('x86_64' 'i686')
url='https://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('xfce4-panel' 'garcon')
makedepends=('cmake' 'ninja')
source=("http://archive.be.xfce.org/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2") # no https available
sha256sums=('248e02c5a2854a1631e57ccb70541b9c6303b1460e4cd5b401226e817661c749')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: -u=2 gottcode.org/xfce4-whiskermenu-plugin
# vim: ts=2 sw=2 et:
