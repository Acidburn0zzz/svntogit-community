# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Surge
pkgname=surge
pkgver=1.6.6
pkgrel=3
pkgdesc="An Open Source Digital Synthesizer"
arch=('x86_64')
url="https://surge-synthesizer.github.io/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio' 'vst3-plugins')
depends=('cairo' 'fontconfig' 'gcc-libs' 'glibc' 'libx11' 'libxcb'
'libxkbcommon-x11' 'xcb-util' 'xcb-util-cursor' 'xcb-util-image'
'xcb-util-keysyms' 'xcb-util-renderutil' 'xdg-utils')
makedepends=('cmake' 'freetype2' 'libsndfile' 'lv2' 'python')
checkdepends=('lv2lint')
replaces=('surge-synthesizer')
source=("${pkgname}-${pkgver}.tgz::https://github.com/surge-synthesizer/releases/releases/download/${pkgver}/SurgeSrc_${pkgver}.tgz"
        "${pkgname}-1.6.6-extend_datapath.patch::https://github.com/surge-synthesizer/surge/pull/1592/commits/278899401a2c27b5cefa59234c632fdf1fe06217.patch"
        "Makefile"
        "${pkgname}-lv2.make"
        "${pkgname}-vst3.make")
sha512sums=('f212135b83a80e1b9501fbd22793a40c4ae57e6497a84a4547fe55ebc6e9a0aeb031e7e66bf31ae282342fd104afce4a8d06c04edfb9223dd0023d23f4bb464a'
            'd841f971bf0abb022b3b0f9765ad9a008a7bd4a28c2855c84cea7e0e528ea0dfbe515a368e6d46ed5c4919a681641c0338df99504605e89340c666ef30be1db2'
            'e9e7b8e244b6b642ccc581ffc59646c104f4c18d6e4aae115b3030f74d45aaf7ce381df7d75123dfddb17ebda5f38b11fc64f2f7cd0d724f9e0023255565539a'
            'afba8b923e8d0d07452528f3cf702dfd36f8ba6dcff55063ee345fd5befc1c08a8688c072fbac5128d2db06f0e8a012f0ad2911d67a980cb2082fa3231cd0cec'
            'dbc917b8f596c023a091cd4e496c38d6df2621a80600f4d0fe0cf1415517885348b43035cb1797908debed686704f652c2f0e77bbf0ed020d24cf1cc702ed442')
validpgpkeys=()

prepare() {
  mv -v "$pkgname" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # adding Makefiles created with `premake5 --cc=gcc --os=linux gmake2`
  # premake5 is still in alpha (premake-git)
  # https://github.com/surge-synthesizer/surge/issues/1206
  # https://github.com/premake/premake-core/issues/1423
  ln -svf ../{Makefile,${pkgname}-{lv2,vst3}.make} .

  # add more possible datapath locations (already upstreamed):
  # https://github.com/surge-synthesizer/surge/pull/1592
  # https://bugs.archlinux.org/task/66228
  patch -Np1 -i "../${pkgname}-1.6.6-extend_datapath.patch"

  # optional: move file, as bsdtar is run with C locale
  # https://github.com/surge-synthesizer/surge/issues/1699
  # mv -v resources/data/patches_factory/Monosynth/{Âµ,mu}computer.fxp
}

build() {
  cd "$pkgname-$pkgver"
  # binaries and shared libs have executable stack:
  # https://github.com/surge-synthesizer/surge/issues/1701
  export LDFLAGS="${LDFLAGS},-z,noexecstack"
  # build surge-headless
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -B build \
        -S .
  make surge-headless -C build VERBOSE=1
  # build lv2 and vst3 plugins
  make config=release_x64
  # move lv2 and (conveniently also vst3) plugin out of the way
  mv -v target/{lv2,vst3} build/

  # set datapath to local dir for testing
  sed -e 's|/usr/share/Surge|resources/data|' -i src/common/SurgeStorage.cpp
  # build surge-headless (test-suite
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -B build-test \
        -S .
  make surge-headless -C build-test VERBOSE=1
  # build lv2 against test datapath
  make config=release_x64 surge-lv2
}

check() {
  cd "$pkgname-$pkgver"
  build-test/surge-headless
  LV2_PATH="${PWD}/target/lv2/Release/" \
    lv2lint "https://surge-synthesizer.github.io/lv2/surge" \
    || echo "Known to fail: https://github.com/surge-synthesizer/surge/issues/1698"
}

package() {
  depends+=('libfreetype.so')
  cd "$pkgname-$pkgver"
  # install lv2 and vst3 plugins
  install -vDm 755 "build/lv2/Release/${_name}.lv2/${_name}.so" \
    -t "${pkgdir}/usr/lib/lv2/${_name}.lv2/"
  install -vDm 644 "build/lv2/Release/${_name}.lv2/"*.ttl \
    -t "${pkgdir}/usr/lib/lv2/${_name}.lv2/"
  install -vDm 755 "build/vst3/Release/${_name}.so" \
    -t "${pkgdir}/usr/lib/vst3/${_name}.vst3/Contents/${CARCH}-linux/"
  install -vdm 755 "${pkgdir}/usr/share/${pkgname}"
  # install resources
  cp -av resources/data/* "${pkgdir}/usr/share/${pkgname}"
  install -vDm 644 {AUTHORS,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
