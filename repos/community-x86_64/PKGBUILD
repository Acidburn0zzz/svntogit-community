# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=kdesvn
pkgver=1.7.0
pkgrel=1
pkgdesc="A pure C++ SVN-client for KDE"
arch=('i686' 'x86_64')
url="http://projects.kde.org/kdesvn"
license=('GPL')
depends=('kdebase-runtime' 'subversion')
makedepends=('automoc4' 'cmake' 'git')
install=$pkgname.install
changelog=$pkgname.changelog
source=($pkgname-$pkgver::git://anongit.kde.org/kdesvn.git#commit=1aea688aa8e1ca9219a150d56ea788dd4be4c70b)
sha256sums=('SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#removing conflicting files - kdesdk
  rm -f "${pkgdir}"/usr/share/kde4/services/svn*
#removing $srcdir reference
  rm -rf "${pkgdir}"/usr/share/doc
}
