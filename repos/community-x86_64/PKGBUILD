# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Cédric Bellegarde cedric bellegarde at adishatz dot org

pkgname=eolie
pkgver=0.9.45
pkgrel=2
pkgdesc="Simple web browser for GNOME"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Eolie"
license=('GPL3')
depends=('gtkspell3' 'python-cairo' 'python-dateutil' 'python-gobject' 'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('python-beautifulsoup4: Import html bookmarks'
            'python-crypto: Firefox Sync support' 
            'python-fxa: Firefox Sync support'
            'python-pyopenssl: Show SSL certificates'
            'python-requests-hawk: Firefox Sync support')
_commit=4cae6126129620b40396df9fd8e533434ec0eeee  # tags/0.9.45
source=("git+https://gitlab.gnome.org/World/eolie.git#commit=$_commit"
        "git+https://gitlab.gnome.org/gnumdk/eolie-po.git"
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('SKIP'
            'SKIP'
            'd32b04aaf035dd7c2e09b383f37537db7bd916c12c77eb31f81e0094976521a6')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/-/+/g'
}

prepare() {
	cd $pkgname
	git config submodule.subprojects/po.url "$srcdir/eolie-po"
	git submodule update --init

	# Fix AppStream metadata
	# https://gitlab.gnome.org/World/eolie/merge_requests/271
	git cherry-pick -n ba5cc5b90267bcbf9c6bf9b9c06ee45bd9ed9768
	patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch
}

build() {
	arch-meson $pkgname build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
