# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.5.2
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'openssl' 'libmariadbclient')
optdepends=('ca-certificates: server certificate verification'
            'mariadb: advanced user management')
makedepends=('cmake' 'boost' 'openssl' 'libmariadbclient')
checkdepends=('openssl' 'python' 'curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json'
        'etc/trojan/config.json')
sha512sums=('4048b987a6126ef9e7ba8562bd713a7beafda0171a53a7e8e94750a6461a1493694a00c726c3059b2bdd7a80efb53074315130f8067fbfdf73e1778c3e10e0bf')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
    cd $pkgname-$pkgver
    ctest
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
