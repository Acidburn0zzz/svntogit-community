# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=0.3.1
pkgrel=1
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
groups=('lv2-plugins' 'pro-audio' 'vst3-plugins')
# TODO: devendor lv2 and abseil-cpp
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'xcb-util' 'xcb-util-cursor'
'libxkbcommon' 'libxkbcommon-x11')
makedepends=('cmake' 'jack' 'freetype2' 'libsndfile' 'lv2' 'xcb-util-keysyms')
checkdepends=('lv2lint')
optdepends=('jack: for sfizz_jack')
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz")
sha512sums=('4b162e986ca3f7c04a3d7ac88885ffe9297418c4d66819bdd9235e5781b77a25acdbee89bc72337a819a30c59262a7926990f0ebc0a7fea7fab6a40518539e6e')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE='Release' \
        -DSFIZZ_TESTS=ON \
        -DSFIZZ_VST=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  LV2_PATH="${PWD}/build:/usr/lib/lv2" lv2lint "http://sfztools.github.io/sfizz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
