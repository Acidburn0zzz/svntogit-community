# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=vector-algorithms
pkgname=haskell-vector-algorithms
pkgver=0.7.0.3
pkgrel=1
pkgdesc="Efficient algorithms for vector arrays"
url="https://github.com/erikd/vector-algorithms/"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' "haskell-mwc-random" "haskell-primitive" "haskell-vector")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('929dee06930449d003c3ec0376c743e992aa6f80ee22984d06301ed48bb1a647b384cfcd1653556baa7bae125f1e934d10ef38166dcbb6b87f20d1d173cacaac')

prepare() {
    sed -i 's/vector >= 0.6 && < 0.12,/vector >= 0.6 \&\& < 0.13,/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fproperties -f-bench -f-internalchecks -f-unsafechecks -fboundschecks -f-llvm
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
