# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=1.2.9
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('540058cade02fbf837c5105bf910758dae4e1d9dccfd4ad68e727e990a0ca686e50a860d31315d09f720d7676c98375105079479cdf53a3eb52489a9471274b6')

build() {
  cd dde-calendar-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd dde-calendar-$pkgver
  make DESTDIR="$pkgdir" install
}
