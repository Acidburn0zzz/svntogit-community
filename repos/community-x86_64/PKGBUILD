# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cbor2
pkgname=python-cbor2
pkgver=5.4.1
pkgrel=2
pkgdesc="Pure Python CBOR (de)serializer with extensive tag support"
arch=('x86_64')
url="https://github.com/agronholm/cbor2"
license=('MIT')
depends=('python')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('a8bf432f6cb595f50aeb8fed2a4aa3b3f7caa7f135fb57e4378eaa39242feac9')
b2sums=('7e81436c066dc245038fea6c5c8fb25ad87aae4c8b0a4d3e8ffa1a262dd4b08c6b0d8a79214dc13c87dc4ab6902b7762847b75f217a83769ef97783e2a25a6f4')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not care about coverage
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
