# Maintainer: BlackEagle <ike.devolder@gmail.com>>

pkgname=kodi-addon-game-libretro-mupen64plus-nx
epoch=1
pkgver=2.1.0.10
_codename=Matrix
pkgrel=1
pkgdesc="Nintendo 64 mupen64plus-next for kodi"
arch=('x86_64')
url='https://github.com/kodi-game/game.libretro.mupen64plus'
license=('GPL')
groups=('kodi-addons' 'kodi-addons-game')
makedepends=('cmake' 'kodi-dev' 'kodi-addon-game-libretro' 'libretro-mupen64plus-next')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kodi-game/game.libretro.mupen64plus/archive/$pkgver-$_codename.tar.gz")
sha512sums=('a45751fa5e3a41f20a45832ccba86a9aa47f7a87e0f5936a4407dc46316665d55f9e03cac0c0d3aea9720618943a3d7d75f17a0c6df58da1abda205136d1d280')

build() {
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
    sed -e 's/mupen64plus_libretro/mupen64plus_next_libretro/g' \
        -i CMakeLists.txt
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBUILD_SHARED_LIBS=1 \
        -DUSE_LTO=1 \
        .
    make
}

package() {
    depends=('kodi-addon-game-libretro' 'libretro-mupen64plus-next')
    cd "game.libretro.mupen64plus-nx-$pkgver-$_codename"
	make DESTDIR="$pkgdir/" install
}

