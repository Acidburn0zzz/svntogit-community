# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.28.0
pkgrel=1
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('sqlite')
makedepends=('python' 'python2')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('7c7fa277b78ef99823c854dd073ca8d572f682742858063730d81b5dd3b9edb3bbb60a96e6b74cb84781649728a141a0f192db91c2bfcb9e7e9b9e3f47de1df3')
b2sums=('64d07b2a6ebe3278eed7369544807ce9b87ae5c299a6cc48eb3a4861437fb0ee014271e1a46b87f0097f2f54fe8c7bfcd3081ca4b570163804fb5955ab67dbc0')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
  python2 setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH-"2*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
  PYTHONPATH="${pythonpaths[1]}" python2 setup.py test
}

package_python-apsw() {
depends+=('python')
pkgdesc="Python wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apsw() {
depends+=('python2')
pkgdesc="Python2 wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
