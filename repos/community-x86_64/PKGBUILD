# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Grayhatter
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Håvard Pettersson <mail@haavard.me>
# Contributor: Madotsuki <madotsuki at cock dot li>

pkgname=utox
_pkgname=uTox
pkgver=0.13.1
pkgrel=1
_signature=""
pkgdesc='Lightweight Tox client'
arch=('i686' 'x86_64')
url="https://github.com/uTox/uTox"
license=('GPL3')
depends=('fontconfig'
         'libfilteraudio'
         'libxext'
         'libxrender'
         'openal'
         'toxcore'
         'v4l-utils')
optdepends=('gtk3: GTK file picker'
            'libdbus: Notification support')
makedepends=('toxcore>=0.1.0' 'cmake' 'gtk3' 'libdbus')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/uTox/uTox/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgname}/${_pkgname}/releases/download/v${pkgver}/${_pkgname}-${pkgver}${_signature}.tar.gz.asc"
        )
sha512sums=('9aecd37ab1b1816caacc39415909b25845e0aa4336261da61205629d85bdfaa757dcf73367dc66abd51c7fa5a0cefabefdb74796957704e73913ad9d3ae28433'
            'SKIP')
validpgpkeys=("B7008FF12C079BF6E6EA19D1601A604B7E605776"  # Robin Lindén <mail@robinlinden.eu>
              "4C4E8E7E23A85F2474DC2F1A0558CBEA7BE857DD") # Grayhatter

build() {
    cd "${_pkgname}-${pkgver}"
    cmake . -DCMAKE_BUILD_TYPE=Release -DENABLE_ASAN=OFF -DCMAKE_INSTALL_PREFIX=/usr
    make
}

package() {
    cd "${_pkgname}-${pkgver}"
    make PREFIX="/usr" DESTDIR="${pkgdir}" install
}
