# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.24
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
depends=('glibc')
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz"
        "test-backport.patch")
sha512sums=('026b4c30438459d5fa433ea606e8e020f2ff71ba6e14a5caeecef32f5800a0699cb192ece4bf9b5005b8e0fb2e375f70602d44d94b0b8efa3bac20f9a30e14e6'
            '5c7468f47c19867598bafe07322d1c5a7314286eabab6012aa70a55967967467e620f5a56aeecadf6ebd13021f2c01a4e54c70c41ee09dfd702a082507b3ae47')
changelog="CHANGELOG.md"

prepare() {
	cd "$pkgname-$pkgver"
  patch -Np1 < "${srcdir}/test-backport.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath -mod=vendor"
	go build -o terraform-binary
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -mod=vendor ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
