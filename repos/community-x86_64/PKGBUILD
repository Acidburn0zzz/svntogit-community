# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.10
pkgrel=1
pkgdesc="Python bindings for the libwayland library"
arch=(x86_64)
url="https://github.com/flacjacket/pywayland"
license=(Apache)
depends=(python-cffi wayland)
makedepends=(python-setuptools wayland-protocols)
checkdepends=(python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7abf6ad1aaba50142e5d38d4f9c16390ed10a8bd8157909f34bdd45731c5a0c4463c7d3c5727c9c00949cc19f13e1e09236af585f647c3a7a42eca62ad6139dd')
b2sums=('c94994b4f27636087c89a02d87996df717037706d3f85005cd012459ad61c2e8df384770077cbead09a7f72a53b329980f001d02083de2d2354eed0f1b9d4acc')

prepare() {
  cd "${_name}-$pkgver"
  mkdir -vp temp
}

build() {
  cd "${_name}-$pkgver"
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "${_name}-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  export XDG_RUNTIME_DIR="${PWD}/temp"
  pytest -v
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
