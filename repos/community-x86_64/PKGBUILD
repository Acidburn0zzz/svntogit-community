# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=scite
pkgver=3.2.2
pkgrel=1
pkgdesc='Editor with facilities for building and running programs'
arch=('x86_64' 'i686')
url='http://www.scintilla.org/SciTE.html'
license=('custom:scite')
depends=('desktop-file-utils' 'gtk2')
makedepends=('setconf')
backup=('usr/share/scite/SciTEGlobal.properties')
install=$pkgname.install
source=("http://downloads.sourceforge.net/sourceforge/scintilla/${pkgname}${pkgver//./}.tgz")
sha256sums=('9f17fe2a5dc294bd1077bf64f713bb06733eec271bab770841e55474e6c781c8')

build() {
  cd "$srcdir"

  if [ $?CXXBASEFLAGS == 1 ]; then 
    sed '0,/CXXTFLAGS=/s//nop=/' -i scite/gtk/makefile
    setconf scite/gtk/makefile CXXTFLAGS "-DNDEBUG ${CXXFLAGS} $(CXXBASEFLAGS)"
  fi
  sed '0,/CXXFLAGS=/s//nop=/' -i scintilla/gtk/makefile
  setconf scintilla/gtk/makefile CXXFLAGS "-DNDEBUG ${CXXFLAGS} \$(CXXBASEFLAGS) \$(THREADFLAGS)"
  setconf scintilla/gtk/makefile CXXBASEFLAGS "-Wall -Wno-missing-braces -Wno-char-subscripts -pedantic ${CXXFLAGS} -DGTK -DSCI_LEXER \$(INCLUDEDIRS)"
  cd "$srcdir/scintilla/gtk"
  make
  cd "$srcdir/$pkgname/gtk"
  make
}

package() {
  cd "$srcdir/$pkgname/gtk"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname/License.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-scite"
  install -Dm644 "$srcdir/scintilla/License.txt" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-scintilla"
  ln -sf SciTE "$pkgdir/usr/bin/scite"
}

# vim:set ts=2 sw=2 et:
