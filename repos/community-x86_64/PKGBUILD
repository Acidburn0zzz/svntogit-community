# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=matterbridge
pkgver=1.14.2
pkgrel=1
pkgdesc="Multi-protocols (IRC/XMPP/Mattermost/Slack/Matrix/etc) bridge"
arch=('x86_64')
url="https://github.com/42wim/matterbridge"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
backup=('etc/matterbridge.toml')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz.asc::"${url}/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc"
        'matterbridge.service')
sha256sums=('a6ec749158244a6cb08e5f1978739b05cd2094c2430ec276b2cc9b718b19197d'
            'SKIP'
            '89e52388054d3c83199ccbfe8f2dc02a6ae02ec1a2d024460b11464324a1fb9b')
validpgpkeys=('CC7D978417C1AEA1E4CDD7240E41AB4BF4C610B4') # wim <wim@42.be>

build() {
  cd ${pkgname}-${pkgver}
  go build -v \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}" \
    -o ${pkgname} .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 matterbridge.toml.sample "${pkgdir}"/etc/matterbridge.toml
  install -Dm644 ../${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
