# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
pkgver=1.2.1
pkgrel=2
pkgdesc="multi-platform EPUB2/EPUB3 ebook editor"
arch=('x86_64')
url="https://sigil-ebook.com/"
license=('GPL3')
depends=('hicolor-icon-theme' 'hunspell' 'mathjax2' 'minizip' 'python-css-parser'
         'python-dulwich' 'python-lxml' 'qt5-webengine')
makedepends=('qt5-tools' 'cmake')
optdepends=('hunspell-en_US: for English dictionary support'
            'hyphen-en: for English hyphenation support in plugins'
            'pageedit: external editor to replace BookView'
            'python-html5lib: recommended for plugins'
            'python-chardet: recommended for plugins'
            'python-cssselect: recommended for plugins'
            'python-pillow: recommended for plugins'
            'python-regex: recommended for plugins'
            'python-pyqt5: recommended for plugins'
            'tk: recommended for plugins')
install=${pkgname}.install
source=("Sigil-${pkgver}.tar.gz::https://github.com/Sigil-Ebook/Sigil/archive/${pkgver}.tar.gz"
        "https://github.com/Sigil-Ebook/Sigil/releases/download/${pkgver}/Sigil-${pkgver}.tar.gz.sig"
        "https://github.com/Sigil-Ebook/Sigil/commit/50280fae48717e76d56685bf0fda19783c8904e1.patch")
sha256sums=('5187084486513f9bd3239eb4f4961cd98192c34fe6e161f277b52511fd760861'
            'SKIP'
            '5c57da8d1bc29fdaddcf6b52747bf453bc40af166fb4c77e49b7869a2776458f')
b2sums=('b615304d80ae1bc8b202398e4e00e25e6f13130ba5f27239d78cb847813fac6d13ddd868ed8daa496b5a5f2f37914603b3014b2c0ecb9f8ae2f0e5f6f2bdf84b'
        'SKIP'
        '61c4b8de1d7e6a65668f45b6aadebb7c93bad6c82ed415caad3900f009f88f253aa10ca678c76580ef5d3b344cead91390b001aa824e49ad963a45ff259971d3')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

prepare() {
    cd "${srcdir}"/Sigil-${pkgver}

    patch -p1 -i ../50280fae48717e76d56685bf0fda19783c8904e1.patch
}

build() {
    mkdir -p "${srcdir}"/Sigil-${pkgver}/build
    cd "${srcdir}"/Sigil-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DUSE_SYSTEM_LIBS=1 \
        -DSYSTEM_LIBS_REQUIRED=1 \
        -DINSTALL_BUNDLED_DICTS=0 \
        -DMATHJAX_DIR=/usr/share/mathjax2 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_SKIP_RPATH=ON ..

    make
}

package() {
    cd "${srcdir}"/Sigil-${pkgver}/build
    make install DESTDIR="${pkgdir}"

    for _pic in 16 32 48 128 256; do
        install -D -m 0644 ../src/Resource_Files/icon/app_icon_${_pic}.png \
            "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png
    done
    install -Dm644 ../src/Resource_Files/icon/app_icon_128.png \
        "$pkgdir/usr/share/pixmaps/$pkgname.png"

    # Compile python bytecode
    python -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
    python -O -m compileall "${pkgdir}"/usr/share/sigil/{plugin_launchers/python/,python3lib}
}
