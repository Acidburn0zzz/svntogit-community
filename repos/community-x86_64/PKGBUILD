# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: fauno <fauno at kiwwwi.com.ar>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
# Contributor: Richard Murri <admin@richardmurri.com>

pkgbase=notmuch
pkgname=('notmuch' 'notmuch-mutt')
pkgver=0.14
pkgrel=3
arch=('i686' 'x86_64')
url="http://notmuchmail.org/"
license=('GPL3')
depends=('xapian-core' 'gmime' 'talloc')
makedepends=('python2' 'python' 'emacs' 'gnupg' 'ruby' 'pkgconfig')
options=(!distcc !makeflags)
source=("http://notmuchmail.org/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('5eb3f225d3eb37862932f6baa5780d15')

build() {
    cd "$srcdir/${pkgname}-$pkgver"

    ./configure --prefix=/usr --sysconfdir=/etc
    make 

    cp -dpr --no-preserve=ownership "$srcdir/$pkgname-$pkgver" "$srcdir/$pkgname-mutt-$pkgver"

    cd bindings/ruby
    ruby extconf.rb
    make

    cp -dpr --no-preserve=ownership "$srcdir/${pkgname}-$pkgver"/bindings/python{,2}

    cd "$srcdir/${pkgname}-${pkgver}/bindings/python"
    env LD_LIBRARY_PATH="." python setup.py build

    cd "$srcdir/${pkgname}-${pkgver}/bindings/python2"
    find "." -name '*.py' -print0 |xargs -0 \
        sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
    env LD_LIBRARY_PATH="." python2 setup.py build

}

check() {
    cd "$srcdir/${pkgname}-"*
    make test
}



package_notmuch(){
    pkgdesc="Notmuch is not much of an email program"
    optdepends=('emacs: for using the emacs interface'
                'vim: for using the vim interface'
                'python2: for using the python2 bindings'
                'ruby: for using the ruby bindings'
                'gnupg: for email encryption')
    provides=(notmuch-mutt)
    cd "$srcdir/${pkgname}-$pkgver"
    make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 install

    install -D notmuch "$pkgdir/usr/sbin/notmuch"
    install -D contrib/notmuch-mutt/notmuch-mutt "$pkgdir/usr/bin/"

    mkdir -p "$pkgdir"/usr/share/vim/vimfiles/{plugin,syntax}

    make -C vim PREFIX="$pkgdir/usr/share/vim/vimfiles" install

    # Install python bindings

    cd "$srcdir/${pkgname}-${pkgver}/bindings/python2"
    env LD_LIBRARY_PATH="." python2 setup.py install --prefix=/usr --root="$pkgdir"

    cd "$srcdir/${pkgname}-${pkgver}/bindings/python"
    env LD_LIBRARY_PATH="." python setup.py install --prefix=/usr --root="$pkgdir"

    # Install ruby bindings
    cd "$srcdir/${pkgname}-${pkgver}/bindings/ruby"
    sed -i -e 's,/site_ruby,,g' Makefile
    make prefix="${pkgdir}/usr" install

    # Remove conflicting zsh completion
    rm -r "${pkgdir}/usr/share/zsh"
}

package_notmuch-mutt(){
    pkgdesc="The mail indexer"
    conflicts=('notmuch')
    provides=('notmuch')
    cd "$srcdir/$pkgbase-mutt-$pkgver"
    make DESTDIR="$pkgdir/" LIBDIR_IN_LDCONFIG=0 install

    install -D notmuch "$pkgdir/usr/sbin/notmuch"
    install -D contrib/notmuch-mutt/notmuch-mutt "$pkgdir/usr/bin/"

    mkdir -p "$pkgdir"/usr/share/vim/vimfiles/{plugin,syntax}

    make -C vim PREFIX="$pkgdir/usr/share/vim/vimfiles" install

    # Remove conflicting zsh completion
    rm -r "${pkgdir}/usr/share/zsh"
}

#vim: set filetype=PKGBUILD sw=4 ts=4 et
