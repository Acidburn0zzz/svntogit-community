# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.19
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver.tar.gz"
        deepin-calculator-qt5.15.patch)
sha512sums=('07402d894dfb927cba23cf1fa2d47342613e0580e2ae9c06e88432d22e90ac7555fc07a3f6dd77408bc98c518b71c6dab3b862d12d7c0db2589da8cb2f5c6025'
            'd38fed51d16655c4662bdea3a7e19cbcb27b2d04bccaad90e0089652068b6041b73a8512e717ff18c47426046dcfe188ba1535ef19fc21b74fae7dd4e4933e03')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '/<QList>/i #include <QObject>' src/core/session.h

  patch -p1 -i ../deepin-calculator-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-calculator-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd deepin-calculator-$pkgver
  make DESTDIR="$pkgdir" install
}
