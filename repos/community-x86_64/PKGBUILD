# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.2.0
pkgrel=2
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good' 'mpv')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dde-file-manager/commit/ddc018cef1172324c1b6773fc8695b8f9f96ca1d.patch)
sha512sums=('1050786b101e953cd73e32fb332fcc781f073f2a8aefdb1281b08c346595b993d87552560083a7f8b7f16edd99adf2b21deca866b5e1eab12078500bd4ba4758'
            'beb94ea5e3a2aaa0c178da217390cf9f206842970852d2eeb8d06ca5e4a33ea797bb76a2251899d7d729bdc860a9c069747ff0730e331953e360ff5a3b865019')

prepare() {
  cd dde-file-manager-$pkgver
  patch -R -p1 -i ../ddc018cef1172324c1b6773fc8695b8f9f96ca1d.patch
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
