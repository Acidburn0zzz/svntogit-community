# Maintainer: Sven-Hendrik Haase <sh@lutzhaase>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.1.5
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11')
makedepends=('mesa' 'qt5-tools' 'python2')
optdepends=('python2: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('4c1d03825c76ba14b0469af564fe5eb78fa51790b41ddc51f3f72a8247b0aba1118142144453f4cbdaeedb54ac23812086eeac91bf61457887f1d9635508dbe7')

build() {
  cd $srcdir/$pkgname-$pkgver
  qmake -r INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT=$pkgdir
}
