# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.6.6
pkgrel=5
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('libusb')
makedepends=('pkgconfig')
options=('!libtool' '!docs')
source=(https://alioth.debian.org/frs/download.php/3479/pcsc-lite-${pkgver}.tar.bz2
        pcscd
        pcsclite-1.6.6-noautostart.patch
        pcsclite-1.6.6-noautopowerdown.patch)
md5sums=('f80d3ecd9569b71d557f283f16295c74'
         '706033b2145509493a2a7da5ce1abbae'
         'e73a8d8820f2cca43e5ef78c456ca1a8'
         '7f90d401ab08421fbcf9a10893ee0516')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"
  patch -Np1 -i "${srcdir}/pcsclite-1.6.6-noautostart.patch" || return 1
  patch -Np1 -i "${srcdir}/pcsclite-1.6.6-noautopowerdown.patch" || return 1

  ./configure --prefix=/usr \
              --disable-libhal \
              --enable-libusb \
              --sysconfdir=/etc \
              --enable-usbdropdir=/usr/lib/pcsc/drivers || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

  install -D ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE || return 1
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd || return 1
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
