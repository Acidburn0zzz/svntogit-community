# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.16
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('i686' 'x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python2: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('c073b24dc1cd595db32f80732156ba904683289106557e3df47fa46328c55ffd7a92153946381bb0a06abe2eef96d6690608712852516ca54164ca058451b517')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
