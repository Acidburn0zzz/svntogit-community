# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxterminal
pkgver=0.3.1
pkgrel=3
pkgdesc="VTE-based terminal emulator (part of LXDE)"
arch=('x86_64')
url="https://lxde.org/"
license=('GPL2')
groups=('lxde' 'lxde-gtk3')
depends=('vte3')
makedepends=('intltool')
conflicts=('lxterminal-gtk3')
replaces=('lxterminal-gtk3')
source=(https://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.xz
        vte-0.54.patch)
sha256sums=('cd8df387c2dd40e03b48caacf700820f9421b421954a0ed56395f505411d2c4b'
            '81d2748782c30b29d4cba0ec910d794d4a87231efb06e9e279c995a84dc0fd75')

prepare() {
  cd $pkgname-$pkgver
  # Fix close button for vte 0.54
  patch -Np1 -i ../vte-0.54.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr --enable-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
