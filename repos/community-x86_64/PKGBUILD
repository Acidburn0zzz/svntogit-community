# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kube
pkgver=0.6.0
pkgrel=1
pkgdesc="A modern groupware client based on QtQuick and Sink"
arch=(x86_64)
url="https://kube.kde.org/"
license=(LGPL)
depends=(sink qgpgme kpackage qt5-quickcontrols2 qt5-webengine qt5-quickcontrols)
makedepends=(extra-cmake-modules python)
#source=("https://download.kde.org/unstable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/KDE/kube/archive/v$pkgver.tar.gz")
sha256sums=('58522a73460c52fe23190498d01fe30e279e23f31cc6f0d69e91e709e3e2d172')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
