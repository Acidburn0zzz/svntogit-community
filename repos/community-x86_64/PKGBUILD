# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.8.2
pkgrel=1
pkgdesc="A library for creating and manipulating (un)directed graphs."
arch=(x86_64)
url="https://igraph.org/c/"
license=(GPL2)
depends=(glpk arpack libxml2)
source=("https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('718a471e7b8cbf02e3e8006153b7be6a22f85bb804283763a0016280e8a60e95')

build() {
  cd $pkgname-$pkgver
  ./configure \
   --prefix=/usr \
   --with-external-blas \
   --with-external-lapack \
   --with-external-arpack \
   --with-external-glpk
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
