# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=1.1.6
pkgrel=1
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libice' 'libsm' 'libwnck3' 'mate-desktop' 'ukui-menus')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('c86ab9d7891b4cc111f1e21ae0c588c9242c33b0c8957f731d0bccfb66549b7d121cdb981a4b371028d14d72fd4036cad040a0b2b40a9ccda6013882a71c9711')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-introspection
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
