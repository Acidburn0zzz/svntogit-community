# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.31.1
pkgrel=1
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('sqlite')
makedepends=('python' 'python2')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('5dbf770f33d35402e36ab79e167c48742b257cd14c95df5d22f1fe172b07ddf2419cf02fb4e0700732c4dd6a90d145415f2baed92063fd9d80f77db8a28ef83f')
b2sums=('1541223ca52051277424f753b7f17ab89f19db053f68c1c05d5aab018a32fb15a87b393fcefe5040d2c2bb0ec6ee1aa9f3217a37bae7c6d27a23d9d81828e0cc')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
  python2 setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH-"2*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
  PYTHONPATH="${pythonpaths[1]}" python2 setup.py test
}

package_python-apsw() {
depends+=('python')
pkgdesc="Python wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apsw() {
depends+=('python2')
pkgdesc="Python2 wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
