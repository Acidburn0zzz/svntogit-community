# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Stephen Smith <stephen304@gmail.com>

pkgbase=lua-bit32
_rockname=${pkgbase#lua-}
pkgname=("$pkgbase" "lua51-$_rockname" "lua52-$_rockname" "lua53-$_rockname")
_compatrel=0.9
pkgver=5.3.5_$_compatrel
_rockrel=1
pkgrel=1
pkgdesc='Lua 5.2 bit manipulation library'
arch=(x86_64)
url='https://github.com/keplerproject/lua-compat-5.3'
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="lua-compat-5.3-$_compatrel"
source=("$_archive.tar.gz::$url/archive/v$_compatrel.tar.gz")
sha256sums=('ad05540d2d96a48725bb79a1def35cf6652a4e2ec26376e2617c8ce2baa6f416')

_package() {
	cd "$_archive"
	depends=("${pkgname%-*}")
	luarocks --lua-version=$1 --tree="$pkgdir/usr/" \
		make --deps-mode=none --no-manifest "rockspecs/$_rockname-${pkgver%_*}-$_rockrel.rockspec"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-bit32() {
	_package 5.4
}

package_lua51-bit32() {
	_package 5.1
}

package_lua52-bit32() {
	_package 5.2
}

package_lua53-bit32() {
	_package 5.3
}
