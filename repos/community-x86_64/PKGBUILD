# Maintainer: David Runge <dave@sleepmap.de>

pkgname=lv2lint
pkgver=0.2.0
pkgrel=2
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=('Artistic2.0')
depends=('glibc' 'libcurl.so' 'libelf' 'lilv')
makedepends=('lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('cbbe7743afd5c983b40a267f0d2358404d2c0cd25593ca046ddcaa751a1e3bd083a02d8068432761f650035b5da7ca0f8b5fe0f06dcac61e5bad8cc79961d064')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        -Donline-tests=true \
        -Delf-tests=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C build install
  install -vDm 644 {ChangeLog,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
