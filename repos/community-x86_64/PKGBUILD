# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-igraph
pkgver=0.9.0
pkgrel=1
url="https://igraph.org/python/"
license=(GPL)
arch=(x86_64)
pkgdesc="Python bindings for the igraph library"
depends=(python-texttable igraph)
makedepends=(python-setuptools)
source=("https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('7aa1d77fa02e27475eb4f14503f3cb342c3ed8990d9224640fd29c70797f2dd6')

build() {
  cd $pkgname-$pkgver
  export CFLAGS+=" -I/usr/include/igraph"
  python setup.py build --use-pkg-config
}

check() {
  cd $pkgname-$pkgver
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1 
}
