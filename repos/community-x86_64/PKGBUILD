# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.14.5
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=('x86_64')
url="https://github.com/flacjacket/pywlroots"
license=('custom:UIUC')
depends=('glibc' 'python-cffi' 'python-pywayland' 'python-xkbcommon' 'wlroots'
'libwlroots.so')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
provides=('pywlroots')
conflicts=('pywlroots')
replaces=('pywlroots')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('6441c5fdb06b20e63d5afb34400763a36a6e83bfdc99b36e937b031391b9a8189e2947015e086ca217cc4b24711a753f9fb35446d7234aee570ebda041eec153')
b2sums=('c7c948f98012f599eb0043220ff0409c8f707b89818a7b01da0c6fb148b9ce00c951d09b169c7980e72078e116ba622fc6e72bf2dcfa19858e9105abebea4dc3')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python wlroots/ffi_build.py
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
