# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7.2_alpha
pkgrel=3
epoch=1
pkgdesc='Script based raytracer for creating 3D graphics'
arch=('x86_64' 'i686')
license=('AGPL3')
url='http://povray.org/'
depends=('libtiff' 'libpng' 'boost-libs' 'openexr')
makedepends=('boost' 'git' 'glu')
backup=("etc/povray/${pkgver%.*}/povray.conf"
        "etc/povray/${pkgver%.*}/povray.ini")
source=('git+https://github.com/POV-Ray/povray#commit=5410fd423cebbf2270fe16888edbe2ca28b99ec7' # 2017-06-24
        'povray3.7.0_rc3-user-conf.patch')
sha256sums=('SKIP'
            '95b04ec4973e6e8a80fc83df2e3caff0b4d6699c4154144f3017d42cab7236e0')

prepare() {
  cd "$pkgname/unix"

  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac
  ./prebuild.sh

  cd ..
  ./bootstrap
  patch -p1 -i "$srcdir/povray3.7.0_rc3-user-conf.patch"
}

build() {
  cd "$pkgname"

  ./configure \
    LIBS="-lboost_system -lboost_thread" \
    COMPILED_BY='Arch Linux' \
    --sysconfdir=/etc \
    --prefix=/usr

  make CXXFLAGS+="-std=c++03 -lboost_system -lboost_thread -w"
}

package() {
  cd "$pkgname"

  install -d "$pkgdir"/usr/share/{doc/,}"$pkgname-${pkgver%.*}"
  cp -r icons include ini scenes scripts "$pkgdir/usr/share/$pkgname-${pkgver%.*}"
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-${pkgver%.*}"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/${pkgver%.*}/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/${pkgver%.*}/povray.ini"
  install -Dm644 povray.1 "$pkgdir/usr/share/man/man1/povray.1"
}

# getver: povray.org/documentation
# vim:set ts=2 sw=2 et:
