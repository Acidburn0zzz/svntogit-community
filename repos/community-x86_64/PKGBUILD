# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200629
_pkgver=2020-06-29
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('68673aae5675280fb3f1026f618b80f57a5aaf08ad3ef735acf75c22245a5efc7b96b82972ceacd1b4a4cf48b30a197925e27fac0c6dd3719dde57ad65a0bc70')

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

# https://github.com/rust-analyzer/rust-analyzer/issues/4218#issuecomment-625612149
# check() {
#   cd $pkgname-$_pkgver
#   cargo test --release --locked
# }

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
