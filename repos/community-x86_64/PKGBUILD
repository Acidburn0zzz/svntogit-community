# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.1.0
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'dtkwidget' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon' 'xorg-server'
         'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools' 'deepin-gettext-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('1921024d237bcbbe7eb760498c8ea6c13aac8d25ae9fcf39aa49d352162cb84d42bda32fa9460362d0651153753a89c895970da739d7152a374b4022ce38988b')

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
