# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.17.1
pkgrel=2
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL')
depends=('gcc-libs' 'nettle')
makedepends=(clang)
checkdepends=('cppunit')
source=("https://download.filezilla-project.org/libfilezilla/libfilezilla-$pkgver.tar.bz2")

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  export CXX=clang++
  export CC=clang
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
sha512sums=('c9c88bf3cbeb5284c3e55571b40b07f780cb23aeb802284d385a2564bf46396875a34739f9819b0c3f89923950f19a13f6454940434a987bb91b2f23b898f774')
