# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=1.2.0
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('788b5bbb032a870b6a2920dd0b18a6517e632151b375c4b482c8ea01e593cffb0c65e8566585fb865123fb633a6bc7fdb6c9e4849564fd4a0e73d690334efb86')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
