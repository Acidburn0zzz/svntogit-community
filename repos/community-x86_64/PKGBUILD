# $Id: PKGBUILD 95035 2013-08-04 09:44:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy3
pkgver=5.8.0
pkgrel=1
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'sqlite' 'tk' 'python2-pycparser')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("https://bitbucket.org/pypy/pypy/downloads/pypy3-v${pkgver}-src.tar.bz2"
        "opensslfix.patch::https://bitbucket.org/pypy/pypy/commits/29c9a89359e4cd684f586ceaf8d99ff28fd8aa6d/raw")
sha512sums=('d78b4c899a5643028664365ed973a7b292a8e5b3989cc75203cd381ea3cda7dd73121c574726e23dca86e8364fcfcf42c372c9deee438c805f30d6e1c4ac115a'
            '2860e20c3fba647c9857046d8808c2f3f88e34b29087dffb59c387f98b48b86f28f0d0c802b02885606716586e5536820e3ea75777ba37b2a458e65da04997cd')

prepare() {
  cd pypy3-v${pkgver}-src

  patch -Np1 < ${srcdir}/opensslfix.patch
}

build() {
  cd pypy3-v${pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone

  # Compile binary modules
  PYTHONPATH=../.. ./pypy3-c ../tool/build_cffi_imports.py
}

package() {
  cd pypy3-v${pkgver}-src

  # Prepare installation
  python2 pypy/tool/release/package.py --archive-name pypy --targetdir .
  mkdir unpacked
  tar xf pypy.tar.bz2 -C unpacked

  # Install pypy
  cd unpacked
  install -Dm755 pypy/bin/pypy3 "${pkgdir}"/opt/pypy3/bin/pypy3
  install -Dm755 pypy/bin/libpypy3-c.so "${pkgdir}"/usr/lib/libpypy3-c.so
  cp -r pypy/include pypy/lib_pypy pypy/lib-python pypy/site-packages "${pkgdir}"/opt/pypy3
  cd ..

  # Install symlink
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy3 "${pkgdir}"/usr/bin/pypy3

  # Install misc stuff
  install -Dm644 README.rst "${pkgdir}"/opt/pypy3/README.rst
  install -Dm644 LICENSE "${pkgdir}"/opt/pypy3/LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE
}
# vim: ts=2 sw=2 et:
