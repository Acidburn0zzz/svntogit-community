# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=7.5.1.10
_cudaver=10.1
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=10')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/cuda/tree/centos7 for the appropriate cuda version.
source=("https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('506bb47a3ec3e667bf781dff4dfc4c1279269e17f6f9dd573f795656ed3a31db6c1188f7c57044b10d4caf14cba1a51fca8d5090e174012f9dcb2fb6babc702a'
            '03407d484126a6ed0698fde41b6594faa6410133e6369e9a056d70376693ce2fa6a11b834d7683bc27d894cad33d33840b325bec1fc7df820edf9d6dbfbb1958'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb"
           "libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb")

package() {
  mkdir -p "${pkgdir}"/usr/{lib,include}

  mkdir libcudnn7-dev
  cd libcudnn7-dev
  ar xv "${srcdir}"/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/include/x86_64-linux-gnu/* "${pkgdir}"/usr/include/
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  mkdir libcudnn7
  cd libcudnn7
  ar xv "${srcdir}"/libcudnn7_${pkgver}-1+cuda${_cudaver}_amd64.deb
  tar xf data.tar.xz
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  # Get rid of some Ubuntu-isms but at the same time still allow for them to
  # work because nobody is ever quite sure what the right names are.
  ln -s cudnn_v7.h "${pkgdir}"/usr/include/cudnn.h
  ln -s libcudnn.so.7 "${pkgdir}"/usr/lib/libcudnn.so
  ln -s libcudnn_static_v7.a "${pkgdir}"/usr/lib/libcudnn_static.a

  install -Dm644 \
      ${srcdir}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      ${pkgdir}/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
