# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=10.1.2
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('328cae05d1b1dd966fb4f47df3550c78c15a8f4db7f3aa9ac3e630e8b61488e5e23ebe4d4f2f07a6db629686dd6db7227628acbb2986aa43ee5fe656eeacc9cc')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # --locked won't work until https://github.com/XAMPPRocky/tokei/issues/360 is fixed.
    cargo build --release --features all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
