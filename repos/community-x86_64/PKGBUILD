# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.6.0.2
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'fltk' 'gcc-libs' 'glibc' 'libjack.so' 'libncursesw.so' 'libreadline.so' 'mxml' 'zlib')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('2c99d616c9b29ce9423a9c995f568c0fe29cdadca962f8454b137b586ed4c2b4105a78ced05c39599aa8fe3b9abe376220cc0f1678cbbb5e7d14c49a0eb88454')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake . \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}
# vim:set ts=2 sw=2 et:
