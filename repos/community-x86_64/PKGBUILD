# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2381
pkgname=sonic-visualiser
pkgver=3.1
pkgrel=2
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('capnproto' 'libfishsound' 'libid3tag' 'liblo'
'liblrdf' 'libmad' 'liboggz' 'libpulse' 'portaudio' 'qt5-svg'
'rubberband' 'sord' 'hicolor-icon-theme')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('dc44e101d0ffbe858ff43dbd32eed8edea9fcfee166c7a13b08da7059fcd24f434814a73e78f5ded5d3adbf1420c1d448c3bf891525048424ac9213e0801b998')

build() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"
  ./configure --prefix=/usr
  qmake-qt5 ${pkgname}.pro
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make install
  # install some things separately, because build system is broken:
  # https://sourceforge.net/p/sv1/bugs/247/

  # desktop files
  install -t "${pkgdir}/usr/share/applications/" -vDm 644 "${pkgname}.desktop"

  # icons
  for _s in 16 22 24 32 48 64 128; do
    install -vDm 644 icons/sv-${_s}x${_s}.png \
      "${pkgdir}/usr/share/icons/hicolor/${_s}x${_s}/apps/sv-icon.png"
  done
  install -vDm 644 icons/sv-icon.svg \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/sv-icon.svg"

  # samples
  install -t "${pkgdir}/usr/share/${pkgname}/samples" -vDm 644 samples/*

  # templates
  install -t "${pkgdir}/usr/share/${pkgname}/templates" -vDm 644 templates/*

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGELOG,CITATION,README.{md,OSC}}
}
