# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.9.2
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack2' 'jack2-dbus' 'qt6-tools' 'portaudio')
optdepends=(
  'jack2-dbus: for JACK dbus support'
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('87e5cbf241139b1e225ea2e2cc45b6f44f19fddf866560e5b9e690ef3a2f7a358ff6a28065841c9599b321568a85d5b4767907aff071c4a768608fc273a7e8fb')
b2sums=('43e3f3d48295727d7a3848ed6c59c72fb6c2947476720af802d53d152848a355aac79448d88fce78c2b12fbae54d64b4a1582f334b2a416f0b235cdf8d557e40')


build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libjack.so' 'libportaudio.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
