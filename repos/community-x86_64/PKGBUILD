# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=0.4.0
pkgrel=2
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
#checkdepends=('unzip')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('a9266ba0e674ac6c7222a6666ac5f54581b447ed6a88924b8c306dcee7769fa903ff32b0664d24037fb8a694598a3e34461d66853ca3ac97cf94a0ffb31c90a0')

prepare(){
  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf "$pkgname-$pkgver" "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  export GO111MODULE=on

  go mod vendor
  go mod download
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	-v .
}

#check() {
#  export GOPATH="$srcdir"/gopath
#  cd "$GOPATH"/src/github.com/arduino/arduino-cli
#
#  go test .
#}

package() {
  install -Dm 755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
}

