# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=vivid
pkgver=0.5.0
pkgrel=1
pkgdesc='LS_COLORS manager with multiple themes'
arch=('x86_64')
url=https://github.com/sharkdp/vivid
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$url/archive/v$pkgver/vivid-v$pkgver.tar.gz")
sha512sums=('110b5530bae7d0c35a48830874c0c3d3add335b512a7a98db3009078a51d1efb5fd287c78166144f17c12dc114f0b9b9849207b7a9b43cb01f6369853c875a70')

build() {
  cd vivid-$pkgver
  cargo build --release --locked
}

package() {
  cd vivid-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/vivid
  install -Dm644 -t "$pkgdir"/usr/share/vivid config/filetypes.yml
  install -Dm644 -t "$pkgdir"/usr/share/vivid/themes themes/*
  install -Dm644 -t "$pkgdir"/usr/share/licenses/vivid LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
