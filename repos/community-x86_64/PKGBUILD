# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=obconf-qt
pkgver=0.14.1
pkgrel=1
pkgdesc="Openbox configuration tool. Qt port of ObConf"
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("GPL2")
depends=("hicolor-icon-theme" "openbox" "qt5-x11extras")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('7ae4feb8daa3853f768e9cb613fc99046f98744e03efc673ee9e5cb3d4ffa2861f2a9f762acd9c54ef2b9ca3f288d2901cdcce5c09db84689dd186006b8e26ed'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
