# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jakob Matthes <jakob.matthes@gmail.com>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>
# Contributor: henning mueller <henning@orgizm.net>
# Contributor: rakoo (AUR)
# Contributor: SanskritFritz (gmail)

pkgname=bup
pkgver=0.29.2
pkgrel=1
pkgdesc='Efficient backup system based on the git packfile format'
arch=('x86_64')
url='https://bup.github.io/'
license=('GPL')
depends=('python2-fuse' 'par2cmdline' 'pylibacl' 'python2-pyxattr' 'git')
makedepends=('ruby-ronn' 'git' 'setconf' 'pandoc')
checkdepends=('rsync' 'python2-tornado')
optdepends=('python2-tornado: for bup web')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/bup/bup/archive/${pkgver}.tar.gz")
sha512sums=('10c433d6779b1fd7e1935c587676492d96a330dc3280556332bb55b6e5c62de8631fd755d07d2eaf80cb9fa209dc25b241d5ecf3a6fe2b8210a3d0e1ce4fa4bf')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
}
