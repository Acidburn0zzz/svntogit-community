# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.4.1
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11' 'qt5-declarative' 'qt5-quickcontrols2' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python3')
optdepends=('python3: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('68543e70a5b601e1426b12116da47d8c9b961a1d321bdf410287149d3f5cc76e3ef9724e61ae7c9a8b1314679eacd9b34f60faf5e49a090199e4e1707fc061e6')

build() {
  cd $pkgname-$pkgver
  qmake -r INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT="$pkgdir"
}
