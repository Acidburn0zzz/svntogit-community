# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.32
pkgrel=1
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=("geany>=$pkgver" 'ctpl' 'desktop-file-utils' 'gpgme' 'gtkspell' 'libgit2' 'lua51' 'pygtk' 'python2' 'vte')
makedepends=('cppcheck' 'gdb' 'intltool' 'libgit2' 'vala')
optdepends=('hspell: hebrew spell checker')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('78d4b394fe22fda298e80a22616421efdd4a419eb227b99eadca73f65abac117')

build() {
  cd "$pkgname-$pkgver"

  # Looking forward to Python 3 being supported
  export PYTHON=/usr/bin/python2

  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
