# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: David <david@pako.st>

pkgname=chicken
pkgver=5.0.0
pkgrel=1
pkgdesc='Feature rich R5RS Scheme compiler and interpreter'
arch=('x86_64')
url='http://call-cc.org/'
conflicts=('mono') # /usr/bin/csc
license=('BSD')
depends=('tk')
makedepends=('chicken')
options=('staticlibs')
source=("https://code.call-cc.org/releases/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('a8b94bb94c5d6a4348cedd75dc334ac80924bcd9a7a7a3d6af5121e57ef66595')

build() {
  make -C "$pkgname-$pkgver" PLATFORM=linux PREFIX=/usr -j1
}

package() {
  cd "$pkgname-$pkgver"
  make PLATFORM=linux DESTDIR="$pkgdir" PREFIX=/usr -j1 install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: code.call-cc.org/releases/current/NEWS
# vim: ts=2 sw=2 et:
