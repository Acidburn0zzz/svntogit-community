# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=haskell-hslogger
pkgver=1.2.10
pkgrel=3
pkgdesc="Versatile logging framework"
url="https://hackage.haskell.org/package/hslogger"
license=('LGPL')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-mtl' 'haskell-network' 'haskell-old-locale')
source=("https://hackage.haskell.org/packages/archive/hslogger/${pkgver}/hslogger-${pkgver}.tar.gz")
sha256sums=('d7ca6e94a4aacb47a8dc30e3960ab8deff482d2ec9dca9a87b225e03e97e452b')

build() {
  cd "$srcdir"/hslogger-${pkgver}

  runhaskell Setup configure -p --prefix=/usr --docdir=/usr/share/doc/${pkgname} -O \
	--enable-split-objs --enable-shared --libsubdir=\$compiler/site-local/\$pkgid \
	--enable-library-profiling
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "${srcdir}/hslogger-${pkgver}"
  
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/hslogger
  runhaskell Setup copy --destdir="$pkgdir"
}
