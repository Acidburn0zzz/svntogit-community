# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Samuel Littley <aur@toastwaffle.com>
# Contributor: rnons <remotenonsense at gmail dot com>

pkgname=python-ruamel-yaml
pkgver=0.14.4
pkgrel=1
pkgdesc="YAML parser/emitter that supports roundtrip preservation of comments, seq/map flow style, and map key order"
arch=('i686' 'x86_64')
url="https://bitbucket.org/ruamel/yaml"
license=("MIT")
makedepends=('python-setuptools' 'python-pip')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.bz2::https://bitbucket.org/ruamel/yaml/get/$pkgver.tar.bz2")
sha512sums=('8008b259e95ade5dc11fd356f55a68e8d6219376c8424f03a38e3118fd304029aaa8bdbc42bd58b4bb39bb7ca7a67ac6e7a53dd488756b0b3d10ecbca8ad8ef1')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/ruamel-yaml-*
  python setup.py build
}

check() {
  cd "$srcdir"/ruamel-yaml-*
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.6:$PYTHONPATH" py.test _test/test_*.py
}

package() {
  depends=('python')

  cd ruamel-yaml-*
  pip install . --root="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
