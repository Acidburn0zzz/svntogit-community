# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lucas Raab <tuftedocelot@fastmail.fm>

pkgname=exa
pkgver=0.9.0
pkgrel=3
pkgdesc='ls replacement'
arch=(x86_64)
url='https://the.exa.website/'
license=(MIT)
depends=(gcc-libs libssh2 zlib)
makedepends=(cargo cmake git rust)
source=("$pkgname-$pkgver.tar.gz::https://github.com/ogham/exa/archive/v$pkgver.tar.gz"
        # fix panic on dangling symlink
        "https://github.com/ogham/exa/pull/584/commits/a7a8e99cf3a15992afb2383435da0231917ffb54.patch")
b2sums=('79319085cec312ca04e490a455ad0dd921f3ee6ab16d537f0085dd4fb525212a59ff65619e166b64ff605ddecf5fc44d3945543503816491d3962e8004da8818'
        '90ba19880a56cde0be1ffbfc0cf0e67b7367c31eed4aedd28d1ba91c8ed2bbd571850a9e23d48a9a193ae8860771e1a4b5bc2f985282e0a355f91cc58491563a')

prepare() {
  cd $pkgname-$pkgver
  patch --strip=1 < ../a7a8e99cf3a15992afb2383435da0231917ffb54.patch
}

build() {
  make -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver

  make PREFIX="$pkgdir/usr" install

  install -Dm644 contrib/completions.bash \
    "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm644 contrib/completions.zsh \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgname"
  install -Dm644 contrib/completions.fish \
    "$pkgdir/usr/share/fish/vendor_completions.d/$pkgname.fish"
  install -Dm644 LICEN?E \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
