# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor : Todd Maynard <arch@toddmaynard.com>

pkgname=(python-genshi python2-genshi)
pkgver=0.7
pkgrel=6
_commit=4f491e42a97395f64d968df756172ea0b12e5d85
pkgdesc="Python toolkit for stream-based generation of output for the web."
url="https://genshi.edgewall.org/"
license=("BSD")
arch=('x86_64')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/edgewall/genshi/archive/${_commit}.tar.gz")
sha256sums=('d69c8807789822a354aa9528a37e00db6c6d5df16de4a705c46aa2d4bed24a6d')

prepare() {
    # uses 2to3 so need to build python2/python3 in separate dirs
    cp -a "${srcdir}"/genshi-${_commit}{,-py2}
}

build() {
    cd "${srcdir}"/genshi-${_commit}
    python setup.py build

    cd "${srcdir}"/genshi-${_commit}-py2
    python2 setup.py build
}

check() {
    cd "${srcdir}"/genshi-${_commit}
    python setup.py test

    cd "${srcdir}"/genshi-${_commit}-py2
    python2 setup.py test
}

package_python-genshi() {
    depends=('python')

    cd "${srcdir}"/genshi-${_commit}
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}

package_python2-genshi() {
    depends=('python2')

    cd "${srcdir}"/genshi-${_commit}-py2
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
