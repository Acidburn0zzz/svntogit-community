# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: bartus <arch-user-repoá˜“bartus.33mail.com>
# Contributor: pingplug <pingplug@foxmail.com>
# Contributor: cornholio <vigo.the.unholy.carpathian@gmail.com>

pkgname=magma
pkgver=2.5.1
pkgrel=1
pkgdesc="Matrix Algebra on GPU and Multicore Architectures"
arch=('x86_64')
url="https://icl.cs.utk.edu/magma/"
license=('custom')
depends=('blas' 'lapack' 'cuda')
makedepends=('gcc-fortran' 'cmake')
optdepends=('python2: for examples and tests')
source=("http://icl.cs.utk.edu/projectsfiles/${pkgname}/downloads/${pkgname}-${pkgver}.tar.gz")
sha256sums=('ce32c199131515336b30c92a907effe0c441ebc5c5bdb255e4b06b2508de109f')

build() {
  cd "${srcdir}/magma-${pkgver}"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DGPU_TARGET="sm_30 sm_32 sm_35 sm_37 sm_50 sm_53 sm_60 sm_61 sm_62 sm_70 sm_72 sm_75"
  make
}

package() {
  cd "${srcdir}"/magma-${pkgver}/build
  make DESTDIR="${pkgdir}" install

  mkdir -p ${pkgdir}/usr/share/magma/example
  cp -r ${srcdir}/magma-${pkgver}/example/* ${pkgdir}/usr/share/magma/example/
  mkdir -p ${pkgdir}/usr/share/magma/testing
  cp -r ${srcdir}/magma-${pkgver}/testing/* ${pkgdir}/usr/share/magma/testing/
  install -Dm644 ${srcdir}/magma-${pkgver}/COPYRIGHT ${pkgdir}/usr/share/licenses/magma/LICENSE
}

# vim:set ts=2 sw=2 et:
