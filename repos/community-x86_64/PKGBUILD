# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libratbag
pkgver=0.9.905
pkgrel=2
pkgdesc='A DBus daemon to configure gaming mice'
arch=('x86_64')
url='https://github.com/libratbag/libratbag'
license=('MIT')
depends=('glib2' 'libevdev' 'libudev.so' 'python' 'python-evdev')
makedepends=('meson' 'swig' 'git' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('check' 'valgrind' 'python-gobject' 'python-lxml')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('7b1486846ecfc1fc5988c5c99cbe8c79cd9c6d01714e63c896b97caa1d323bc9538ebe10c73a9d0d46df497d408ce5302d673f59814de2a744bfeee6fc72d4ac')
provides=('ratbagd' 'liblur')
conflicts=('ratbagd' 'liblur')

prepare() {
  sed -i 's|sphinx-build3|sphinx-build|' $pkgname-$pkgver/doc/meson.build
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson .. \
  	-Dsystemd-unit-dir=/usr/lib/systemd/system \
  	-Ddocumentation=true

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  meson test --no-rebuild
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname

  # Install license
  install -Dm 644 ../COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

