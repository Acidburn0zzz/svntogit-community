# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=1.1.6
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('atril' 'gtksourceview3' 'mate-desktop' 'libnotify' 'libsm' 'udisks2' 'webkit2gtk')
makedepends=('gobject-introspection' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('8142787069be5fa6bb232df501703ca2fbaac2826a1ee60716dd1e7c98bcefd170f87bac291e90737b229c6c1432b1a5feb04cbdceb01c878177a95e27ac1f6f')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-introspection --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
