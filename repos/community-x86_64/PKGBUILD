# Maintainer: Anatol Pomozov

pkgname=aws-sdk-cpp
pkgver=1.9.118
pkgrel=1
pkgdesc='AWS SDK for C++'
arch=(x86_64)
url='https://github.com/aws/aws-sdk-cpp'
license=(Apache)
depends=(openssl curl zlib util-linux-libs aws-c-common aws-c-event-stream libpulse aws-crt-cpp)
makedepends=(cmake ninja)
source=(aws-sdk-cpp-$pkgver.zip::https://github.com/aws/aws-sdk-cpp/archive/$pkgver.zip
       fix_null.patch) # https://github.com/aws/aws-sdk-cpp/pull/1506
sha256sums=('6fd92dae061b08c324801be03bd285b56a52e0c3ac2a13e37cfeffe86587b4ca'
            '2260270b5f05c79232b106ad4c73856c05db1f5866174934e0f03e56668868c3')

prepare() {
  cd aws-sdk-cpp-$pkgver

  patch -p1 < ../fix_null.patch
}

build() {
  cd aws-sdk-cpp-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCUSTOM_MEMORY_MANAGEMENT=OFF -DENABLE_TESTING=OFF -DBUILD_DEPS=OFF \
      -DCMAKE_MODULE_PATH=/usr/lib/cmake \
      -S . -B build -G Ninja
  ninja -C build -j 2 # the build is so heavy that it gets OOM killed even at powerful machines
}

package() {
  cd aws-sdk-cpp-$pkgver

  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
