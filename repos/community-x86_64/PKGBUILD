# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=uncrustify
pkgver=0.68.1
pkgrel=1
pkgdesc="A source code beautifier"
arch=('x86_64')
url="http://uncrustify.sourceforge.net/"
license=('GPL')
depends=("gcc-libs")
makedepends=("cmake" "python")
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('799170da8c0665af5f6f28d982d59b811bb239b7f283181735ee7e66138082e9')

build() {
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" PREFIX=/usr install

  cd ..
  install -d -m755 "$pkgdir"/usr/share/uncrustify
  install -m644 etc/*.cfg "$pkgdir"/usr/share/uncrustify
}
