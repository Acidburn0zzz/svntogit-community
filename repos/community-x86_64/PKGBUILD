# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=2.1.2
pkgrel=2
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gsettings-qt' 'poppler-qt5' 'qt5-x11extras')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('c44727b5922de7abcc96e4d69a899e7a896ac4e0609b4e010a2e69706dc833aca63159dc6b2733aaf5f4695ae8e200da10facf68f4921ac5dfc9494a740b5afb')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
