# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Contributor: kritoke <kritoke@nospam.gmail.com>

pkgname=ruby-gtk2
pkgver=0.90.8
pkgrel=2
pkgdesc="Ruby bindings for gtk2."
arch=('i686' 'x86_64')
url="http://ruby-gnome2.sourceforge.jp"
license=('LGPL')
depends=('ruby-glib2' 'ruby-pango' 'ruby-atk' 'ruby-gdkpixbuf2')
makedepends=('ruby-pkgconfig' 'ruby-cairo')
source=(http://downloads.sourceforge.net/ruby-gnome2/ruby-gtk2-$pkgver.tar.gz
	'mkmf-gnome2.patch')
md5sums=('74966bde5366bc9d09701e9f464d60c9'
	'5674ec349bec878c36f70102ca7acb0b')

build() {
	cd "$srcdir/ruby-gtk2-$pkgver"

	# Apply mkmf-gnome2.patch
	patch -Np1 glib2/lib/mkmf-gnome2.rb < ${srcdir}/mkmf-gnome2.patch

	export LANG="en_US.UTF-8"
	ruby extconf.rb gtk2 --topdir=$pkgdir
	make
}
package() {
	cd "$srcdir/ruby-gtk2-$pkgver"
	make DESTDIR="$pkgdir" install
}
