# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-settings-daemon
pkgver=1.2.1
pkgrel=1
pkgdesc="Daemon handling the UKUI session settings"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-settings-daemon"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libmatekbd' 'libmatemixer' 'libnotify' 'libpulse' 'libxklavier' 'mate-desktop' 'nss')
makedepends=('intltool' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-settings-daemon/archive/v$pkgver.tar.gz")
sha512sums=('8297232623541d79ff6d6b64d3f4b6c172212816319df61d914e6bb731bfb541a4d24272ddd328be84fde55c08338527f005f15e9b000ba5f6d451d292ca29e3')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
