# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=rdesktop
pkgver=1.8.6
pkgrel=1
pkgdesc="An open source client for Windows Remote Desktop Services"
arch=('x86_64')
url="https://www.rdesktop.org/"
license=('GPL3')
depends=('libao' 'libsamplerate' 'xorg-xrandr' 'pcsclite' 'libgssglue')
source=(https://github.com/rdesktop/rdesktop/archive/v${pkgver}.tar.gz rdesktop-send_physical_buttons.diff)
sha512sums=('364a69e4b32d2c2a8a2eb16c8bcdc7fa9534afbb91b8a235b80f7d66943e6574a8db5ee52c9a92dfea73b56988b8108c6f4e06c70c486a0d3c5ce7690477d8a6'
            'a51d3d8a048cc5b67d872e9bfee91bfecd055c4bbf202cca3e768eb51dfd12e30e55ccee5431cec6ba20de818e1f0b94d4fe953f43828ee8b84f1fb7e4ffccec')

prepare() {
  cd ${pkgname}-${pkgver}
  # FS#15113
  patch -i "${srcdir}/rdesktop-send_physical_buttons.diff"
  
  ./bootstrap
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-smartcard \
    --with-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
