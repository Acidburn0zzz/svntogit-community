# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-2
_patchver=2
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=6
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="https://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen polymake)
optdepends=('polymake: Polymake module')
source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
#source=("ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz"
        singular-polymake-3.3.patch::"https://github.com/Singular/Sources/commit/23a8f24e.patch"
        singular-missing-header.patch::"https://github.com/Singular/Sources/commit/10ccdf4e.patch")
sha256sums=('07b22773d982d43687f15ba73de7968d23cc15d2c8f23434742134f7bfc68ef5'
            '5d3e18a1f519232f19d5e5150f551cc2d32e8a1022b448ee8ae9d45ef11e5977'
            '413670eaaa393dd9594b7be4a1053483c16bf7ef578b89b3423e8f052cfd2810')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -p1 -i ../singular-polymake-3.3.patch # Fix build with polymake 3.3
  patch -p1 -i ../singular-missing-header.patch # Install missing header
  ./autogen.sh
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info "$pkgdir"/usr/share/singular/singular.idx
}
