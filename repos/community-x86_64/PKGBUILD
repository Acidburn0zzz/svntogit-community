# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.15.1616
pkgname=nomacs
pkgver=3.16.1705
pkgrel=1
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('exiv2' 'gcc-libs' 'glibc' 'libraw' 'libtiff' 'opencv' 'qt5-base'
'qt5-svg' 'quazip')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('0eb4f4365efcd5f22aa2f24e4c3154a828641442255890b7f95129feab78a327950f80602632594bfda8f06750276650da16aa65e8cdd0055e53b474642e38ae'
            '8cf49ade3b71724034f4e18b9a65764a24d77cee9ef8e463813087022f9fe3273f2bf69582bec7821ec4d6d1ea36b6537fde7f7f041a502fce3ff4bb58dcf6e3')
b2sums=('5eaa79a0a2945196d7ebcab09f4ec969ee9820b16a644ce3ddcc7a7c4efdb74b569b1c02a10704e16637bb17b8954a77f6649c698c932770a1a6640f632107c7'
        '029b99251e85b0bc1b5e12c44474dee4c1c9153b6e4e689d2081d210b895e4e277671c4dad3b3a630c549f22cf5c281b65dc9bb40ddc19a3db82577e2ad72cf6')

prepare() {
  cd "${pkgname}-${pkgver}"
  # copy plugin sources into place
  cp -av "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}/"* "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DUSE_SYSTEM_QUAZIP=ON \
        -DENABLE_AVIF=ON \
        -DENABLE_TRANSLATIONS=ON \
        -W no-dev \
        -B build \
        -S ./ImageLounge
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
