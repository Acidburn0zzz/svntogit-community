# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.2.4
pkgrel=3
pkgdesc="Modern desktop environment from the Solus Project"
arch=('i686' 'x86_64')
url="https://solus-project.com/budgie"
license=('GPL' 'LGPL')
depends=('gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
install=$pkgname.install
source=("https://github.com/solus-project/budgie-desktop/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "git+https://github.com/City-busz/budgie-helper.git#commit=4b68fcf"
        "0001-panel-Fix-missing-panel-background.patch"
        "0001-Port-CSS-files-to-GTK-3.20.patch")
sha256sums=('b01e7be9349cbc26cd77db74a0c138d21c31d8bad21a02b31828014b416256f7'
            'SKIP'
            '7212d6f08c7ceaeefc0f97ad125b8a04b6f689a31a7059677067f9e111f54eb6'
            '4215679511ac5d8cc2930b9de86dab50dfe8f79b2605d02e0168b068ed1b7784')

prepare() {
	cd $pkgname-$pkgver
	# Disable workspaces applet as it's broken
	# https://github.com/solus-project/budgie-desktop/issues/302
	sed -i ':a;N;$!ba;s/ \\\n\tworkspaces//' panel/applets/Makefile.am
	autoreconf -fi

	# Remove preferences button from rundialog as it's broken
	# https://github.com/solus-project/budgie-desktop/issues/360
        sed -i '/box.add(preferences);/d' rundialog/RunDialog.vala

	# panel: Fix missing panel background
	# https://github.com/solus-project/budgie-desktop/pull/358
	patch -Np1 -i ../0001-panel-Fix-missing-panel-background.patch

	# Port CSS files to GTK+ 3.20 (prepared for upgrade)
	# https://github.com/solus-project/budgie-desktop/issues/359
	# patch -Np1 -i ../0001-Port-CSS-files-to-GTK-3.20.patch

	cd "$srcdir/budgie-helper"
	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make

	cd "$srcdir/budgie-helper"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	cd "$srcdir/budgie-helper"
	make DESTDIR="$pkgdir" install
}
