# Maintainer:
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: K. Piche <kpiche@rogers.com>

pkgname=avfs
pkgver=1.0.6
pkgrel=1
pkgdesc='Virtual filesystem that allows browsing compressed files'
arch=('x86_64')
license=('GPL' 'LGPL')
url='https://avf.sourceforge.net/'
depends=('fuse2' 'perl' 'xz' 'zstd')
options=('!emptydirs')
source=("https://downloads.sourceforge.net/sourceforge/avf/$pkgname-$pkgver.tar.bz2")
sha512sums=('6b9c070896c5d5abbb6a8c14a7dfd39d9b58d13a6db122b5bf1756b2da7435dfbc00a1fd04bedf99b94b76d9ec7f8c05fccc21de6b9e713fc60cfe0ec88064b4')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --enable-library --enable-fuse --disable-static
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
