# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=discord-canary
_pkgname=DiscordCanary
pkgver=0.0.91
pkgrel=2
pkgdesc="All-in-one voice and text chat for gamers that's free and secure. (canary build)"
arch=('x86_64')
url='https://discordapp.com'
license=('custom')
depends=('libnotify' 'libxss' 'nspr' 'nss'
         'opera-ffmpeg-codecs' 'libegl' 'libgles') # Replacements
optdepends=('libpulse: Pulseaudio support'
            'xdg-utils: Open files')
provides=('discord')
source=("https://dl-canary.discordapp.net/apps/linux/$pkgver/$pkgname-$pkgver.tar.gz"
        'LICENSE.html::https://discordapp.com/terms'
        'OSS-LICENSES.html::https://discordapp.com/licenses')
sha512sums=('7bc9682c8a843138da8b282ba50e47bf20f70ef166f0ba8443a6cb6c662b76d5217a3816a1a0c6b663b1655d08a47e40e8fdb288ddb13fb0e353c013b58c2cc5'
            'e9a77e969adc32f5cf6f30e031b4302e34e803f0aec2f80b1c3fc556663eb7b0b7abfe31a7e76532dd1e8cb6f56272e0ed770e95595c8ae85e1fceb084a7d341'
            '3e20b9360801200a9f4ee793aad10395293077f83296a9b10455b74917b9ed75ae1a89041c4586588197c268a18dba015e79a807a2e6eec414dd8cc1c24a00ab')

prepare() {
  sed -i "s|Exec=.*|Exec=/usr/bin/$pkgname|" $_pkgname/$pkgname.desktop
}

package() {
  # Install the app
  install -d "$pkgdir"/opt/$pkgname
  cp -a $_pkgname/. "$pkgdir"/opt/$pkgname

  chmod 755 "$pkgdir"/opt/$pkgname/$_pkgname

  rm "$pkgdir"/opt/$pkgname/postinst.sh

  install -d "$pkgdir"/usr/{bin,share/{pixmaps,applications}}
  ln -s /opt/$pkgname/$_pkgname "$pkgdir"/usr/bin/$pkgname
  ln -s /opt/$pkgname/discord.png "$pkgdir"/usr/share/pixmaps/$pkgname.png
  ln -s /opt/$pkgname/$pkgname.desktop "$pkgdir"/usr/share/applications/$pkgname.desktop

  # Replacement symlinks
  ln -sf /usr/lib/opera/lib_extra/libffmpeg.so "$pkgdir"/opt/$pkgname/libffmpeg.so
  ln -sf /usr/lib/libEGL.so "$pkgdir"/opt/$pkgname/libEGL.so
  ln -sf /usr/lib/libGLESv2.so "$pkgdir"/opt/$pkgname/libGLESv2.so
  ln -sf /usr/lib/libEGL.so "$pkgdir"/opt/$pkgname/swiftshader/libEGL.so
  ln -sf /usr/lib/libGLESv2.so "$pkgdir"/opt/$pkgname/swiftshader/libGLESv2.so

  # Licenses
  install -Dm 644 LICENSE.html "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.html
  install -Dm 644 OSS-LICENSES.html "$pkgdir"/usr/share/licenses/$pkgname/OSS-LICENSES.html
}

