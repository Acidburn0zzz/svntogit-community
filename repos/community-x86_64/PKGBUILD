# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.5.2
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(help2man meson jack python-jinja python-yaml)
source=($pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/v$pkgver.tar.gz)
sha512sums=('4c1bf760360474543514f1830bb238e33c53a4c39f54b9fd36e8cbd57bdbf014f977a4869839e438eb559d5d26af03cbc81ebe1909054ca7e3bc822a23724fd7')
b2sums=('4382545893debc723b398e81c4a0c2fde27e91ca18a950b633973f04bf6e8da07aabe55a2802aa5327e0f933da8883df411ace4bedda52762957cce89f2e56b4')

build() {
  arch-meson -Drtaudio=disabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="$pkgdir" meson install -C build
  install -vDm 644 $pkgname-$pkgver/{LICENSE.md,LICENSES/MIT.txt} -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $pkgname-$pkgver/README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
