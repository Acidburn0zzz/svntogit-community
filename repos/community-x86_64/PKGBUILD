# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: John D Jones III <jnbek1972 -_AT_- g m a i l -_Dot_- com>
# Generator  : CPANPLUS::Dist::Arch 1.28

pkgname=perl-net-dns-sec
pkgver=1.17
pkgrel=1
pkgdesc="DNSSEC extensions to Net::DNS"
arch=('x86_64')
license=('PerlArtistic' 'GPL')
depends=('perl-crypt-openssl-bignum' 'perl-crypt-openssl-dsa' 'perl-crypt-openssl-rsa' 'perl-digest-bubblebabble' 'perl-mime-base32' 'perl-net-dns' 'perl')
checkdepends=('perl-test-pod')
url='https://search.mcpan.org/dist/Net-DNS-SEC'
options=('!emptydirs')
source=("https://search.mcpan.org/CPAN/authors/id/N/NL/NLNETLABS/Net-DNS-SEC-$pkgver.tar.gz")
sha512sums=('1698d94387a28258f78bc0d233aa3b0be4d80595fc2e3351bcdf0ae38261c01f46f0752c93fba28ef7d4df1b9f796c351d4d096db72faf9eb75f8561094b3b0d')
_distdir="Net-DNS-SEC-$pkgver"

build() {
  cd "$srcdir/$_distdir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_distdir"
  make test
}

package() {
  cd "$srcdir/$_distdir"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
