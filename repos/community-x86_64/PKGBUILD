# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=3.0
pkgrel=5
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gmock libdbi-drivers libmariadbclient postgresql-libs)
optdepends=('gnucash-docs: documentation'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('a2dd7be61d4795717295b7629f35df72161a882fd1668dc744a299f63735ba23')

build() {
  cd $pkgname-$pkgver
  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DHAVE_GWEN_GTK3=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
