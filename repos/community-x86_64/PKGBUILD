# Maintainer: David Runge <dvzrv@archlinux.org>

pkgbase=cardinal
pkgname=(cardinal cardinal-data cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)
pkgver=22.02
pkgrel=1
pkgdesc="Virtual modular synthesizer plugin"
arch=(x86_64)
url="https://github.com/DISTRHO/Cardinal"
license=(GPL3)
groups=(lv2-plugins pro-audio vst-plugins vst3-plugins)
makedepends=(dbus file gcc-libs git glibc jansson libarchive libglvnd liblo
libsndfile libsamplerate libx11 libxext libxrandr mold python speexdsp)
# lv2lint needs the additional lv2-extensions from mod-sdk:
# https://github.com/moddevices/mod-sdk/blob/master/mod.lv2/mod.ttl#L174
# checkdepends=(kxstudio-lv2-extensions lv2 lv2lint xorg-server-xvfb)
source=(https://github.com/DISTRHO/$pkgbase/releases/download/$pkgver/$pkgbase-$pkgver.tar.xz)
sha512sums=('9ec540366f61437f5b7103a08fa7865bfa7e17e92757c3a742ba950eeb6483688be3335fe4ebf830c76a70c18789b698f9121762312aa7cfb3da629730268fe7')
b2sums=('0b4c2d2ee7cb3b191df25b6f5643db1c923c0d74515f4c2c03665ce16b38c5c11968096b1e6d330bf64f99419039669ebba14ce0a29fafde32d9e07ec4b42c90')

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

build() {
  export CFLAGS+=" -B/usr/lib/mold"
  export CXXFLAGS+=" -B/usr/lib/mold"
  make PREFIX=/usr SYSDEPS=true WITH_LTO=true -C $pkgname-$pkgver
}

# check() {
#   declare -A _links=(
#     ["Cardinal"]="https://distrho.kx.studio/plugins/cardinal"
#     ["CardinalFX"]="https://distrho.kx.studio/plugins/cardinal#fx"
#     ["CardinalSynth"]="https://distrho.kx.studio/plugins/cardinal#synth"
#   )
# 
#   for _name in Cardinal{,FX,Synth}; do
#     xvfb-run lv2lint -s "lv2_generate_ttl" -Mpack -I $pkgname-$pkgver/bin/$_name.lv2 "${_links[$_name]}"
#   done
# }

package_cardinal() {
  depends=(cardinal-jack cardinal-lv2 cardinal-vst cardinal-vst3)

  make PREFIX=/usr SYSDEPS=true DESTDIR="$pkgdir" install -C $pkgbase-$pkgver

  # add missing symlinks for resources
  for _name in Cardinal{,FX,Synth}; do
    ln -sv /usr/share/$pkgbase "$pkgdir/usr/lib/lv2/$_name.lv2/resources"
    ln -sv /usr/share/$pkgbase "$pkgdir/usr/lib/vst3/$_name.vst3/Contents/Resources"
  done
  for _name in Cardinal{FX,Synth}; do
    ln -sv /usr/share/$pkgbase "$pkgdir/usr/lib/vst/$_name.vst/resources"
  done

  (
    cd "$pkgdir"
    _pick $pkgbase-data usr/share/$pkgbase
    _pick $pkgbase-data usr/share/doc
    _pick $pkgbase-jack usr/bin/
    _pick $pkgbase-lv2 usr/lib/lv2/
    _pick $pkgbase-vst usr/lib/vst/
    _pick $pkgbase-vst3 usr/lib/vst3/
  )
}

package_cardinal-data() {
  pkgdesc+=" - data"

  mv -v $pkgbase-data/* "$pkgdir"
}

package_cardinal-jack() {
  pkgdesc+=" - JACK standalone"
  depends=(cardinal-data gcc-libs glibc jack jansson libglvnd libx11 libxext
  libxrandr speexdsp libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-jack/* "$pkgdir"
}

package_cardinal-lv2() {
  pkgdesc+=" - LV2 plugin"
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr lv2-host speexdsp libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-lv2/* "$pkgdir"
}

package_cardinal-vst() {
  pkgdesc+=" - VST2 plugin"
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr speexdsp vst-host libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-vst/* "$pkgdir"
}

package_cardinal-vst3() {
  pkgdesc+=" - VST3 plugin"
  depends=(cardinal-data gcc-libs glibc jansson libglvnd libx11 libxext
  libxrandr speexdsp vst3-host libdbus-1.so libarchive.so liblo.so libmagic.so
  libsndfile.so libsamplerate.so)
  optdepends=(
    'carla: for carla module and ildaeil plugin host'
    'mpv: for experimental video support'
  )

  mv -v $pkgbase-vst3/* "$pkgdir"
}
