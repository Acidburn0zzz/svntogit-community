# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-m17n
pkgver=5.0.1
pkgrel=1
pkgdesc="m17n Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-m17n"
license=('GPL')
depends=('fcitx5' 'm17n-lib')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('29a1aa2c126c300994f2198736a7401a9c44648abb181fc74036a47ff197bf2afd96cc43bc39233a7b76e0d90107d366498f97a14a20f5df68f11e3ee8c890dc'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
