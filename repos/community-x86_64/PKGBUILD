# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmendez534 at gmail dot com>

pkgname=lttng-ust
pkgver=2.10.0
pkgrel=2
pkgdesc="LTTng user space tracing libraries for LTTng"
arch=('i686' 'x86_64')
url="https://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
makedepends=('asciidoc' 'xmlto' 'libxml2')
depends=('liburcu>=0.7.2' 'util-linux' 'python')
optdepends=(
    'lttng-tools: LTTng tracing control tools'
    'babeltrace: trace viewer'
)
source=("https://lttng.org/files/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a1661681d09b08ce4ca25dfad6c42c51a1a82f1647ddb9560b8b369f2ff16b7e07618554e1c81941da6f8d50cec2501611f7f3e89bbafccdfc984434f47ff397')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --enable-man-pages --disable-python-agent
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/lttng-ust/COPYING"
}
