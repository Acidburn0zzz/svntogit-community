# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Francisco Lopes

pkgname=interception-tools
pkgver=0.4.4
pkgrel=1
pkgdesc='A minimal composable infrastructure on top of libudev and libevdev'
arch=('x86_64')
license=('GPL3')
url='https://gitlab.com/interception/linux/tools'
depends=(
    'libevdev' 'libevdev.so'
    'yaml-cpp' 'libyaml-cpp.so'
    'systemd-libs' 'libudev.so'
)
makedepends=('cmake' 'systemd' 'boost')
source=(
    "$pkgname-$pkgver.tar.gz::https://gitlab.com/interception/linux/tools/-/archive/v$pkgver/tools-v$pkgver.tar.gz"
    udevmon.service
)
sha256sums=('5a0034dd39c83e66640522425404b5a0c6d1ab477282a3eb0ec9e4ff5b8ccab7'
            'cb4e046aa95cf591492f99543ce2fdc6d3dc94420893cc069fd568f9822d9e4b')

build() {
    cmake -B build \
          -S "tools-v${pkgver}" \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=None \
          -Wno-dev
    cmake --build build
}

package() {
    install -dm755 "$pkgdir/etc/interception/udevmon.d"
    install -Dm644 udevmon.service -t "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "tools-v$pkgver/README.md" -t "$pkgdir/usr/share/doc/$pkgname"

    cd build
    make DESTDIR="$pkgdir/" install
}
