# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-control-center:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=5.0.1
pkgrel=1
pkgdesc="The Control Center for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-settings-daemon' 'cinnamon-menus' 'colord'
         'libgnomekbd' 'libmm-glib' 'nm-connection-editor')
optdepends=('cinnamon-translations: i18n'
            'gnome-color-manager: for color management tasks'
            'gnome-online-accounts: for the online accounts module')
makedepends=('meson' 'samurai' 'gnome-online-accounts')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('170ac90d3a11ac10258b1fff20f4ee792078520e8eb4e18e13a2c4caf8de3ede')
b2sums=('f9c88aa3ad585f9fd867055b247d3cbc7442fb1b4e205dbac7de889e8dd6faa3c2cdee9a6f98b877054ace9d3ff5694e469df3ca35481bd768e019bf6c611541')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --buildtype=plain \
        ..

    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    DESTDIR="${pkgdir}" samu install

    # https://github.com/linuxmint/Cinnamon/pull/7382#issuecomment-374894901
    # /usr/bin/cinnamon-control-center is not meant for users, it is a development troubleshooting tool.
    # Just install the shell libs/headers.
    rm "${pkgdir}"/usr/bin/cinnamon-control-center
    rm "${pkgdir}"/usr/share/cinnamon-control-center/ui/shell.ui
}
