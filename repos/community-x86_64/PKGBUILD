# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=gammastep
pkgver=2.0.6
pkgrel=1
pkgdesc="Adjust the color temperature of your screen according to your surroundings."
arch=('x86_64')
url="https://gitlab.com/chinstrap/gammastep"
license=('GPL3')
depends=('geoclue' 'glibc' 'hicolor-icon-theme' 'libdrm' 'libx11' 'libxcb'
'libxxf86vm' 'wayland')
optdepends=('python-gobject: for gammastep-indicator'
            'python-xdg: for gammastep-indicator'
            'gtk3: for gammastep-indicator')
makedepends=('glib2' 'intltool' 'python' 'systemd')
source=("https://gitlab.com/chinstrap/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('3c56c69e8c0f69fbd06100eded10b1cd5378d8bbbe8ab4c5eb2b07c91b0f7a0f8cfd7b0d2ed759add36954d689246dc079692e7d314b700ab3fcdd6bf805e419')
b2sums=('1d7a1098b234f8219a81d3475e7eef992554cef221129ac0a75e23794cb42abf0e79bfe373bffcd432698f80e4fc186bfe5f7ce557100f6359ec11f12656a74c')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  ./bootstrap
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libgio-2.0.so' 'libgobject-2.0.so' 'libglib-2.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {CONTRIBUTING,NEWS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
