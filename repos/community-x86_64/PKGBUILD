# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

pkgname=mlton
pkgver=20170725
pkgrel=1
pkgdesc='Whole-program, optimizing Standard ML compiler'
arch=('x86_64' 'i686')
url='http://mlton.org/'
license=('BSD' 'MIT' 'LGPL')
depends=('gmp')
makedepends=('git' 'setconf' 'mlton')
options=('staticlibs')
source=("git+https://github.com/MLton/mlton#commit=159190284e1268fded189104705939fe3b691b12")
md5sums=('SKIP')

prepare() {
  sed 's/_BSD_SOURCE/_DEFAULT_SOURCE/g' -i mlton/runtime/cenv.h
  find mlton -name Makefile -type f -exec setconf {} CFLAGS "$CFLAGS -fPIC" \;
}

build() {
  # Latest master does not build on x86_64. Older releases does not build on x86_64. WIP

  export CFLAGS="$CFLAGS -fPIC"
  make -C mlton COMPILE_ARGS="-codegen c -cc-opt '-fPIC'" -j1 all-no-docs
  #make -C mlton COMPILE_ARGS="-codegen c" all-no-docs
}

package() {
  make -C mlton DESTDIR="$pkgdir" -j1 install-no-docs

  # Copy over the handful of licenses and the README as explanation
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp "mlton/doc/license/"* "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
