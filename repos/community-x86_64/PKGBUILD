# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=3.5.3
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('i686' 'x86_64')
url="http://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('4a538b20cb694b821fe842b17bcb9ee0f5a2c666ff12c33d77c6d5abce2e1118')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
