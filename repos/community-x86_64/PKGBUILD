# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Xmonk
pkgname=xmonk.lv2
pkgver=0.3
pkgrel=1
pkgdesc="A LV2 toy"
arch=('x86_64')
url="https://github.com/brummer10/Xmonk.lv2"
license=('BSD')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint' 'sord')
source=("$pkgname-$pkgver.tar.gz::https://github.com/brummer10/${pkgname}/releases/download/${pkgver}/${_name}_${pkgver}.tar.gz"
        "$pkgname-$pkgver-exec_stack_full_relro.patch::https://github.com/brummer10/Xmonk.lv2/commit/4374b0a34869d441c1c3d234399d85f022575eea.patch")
sha512sums=('5b111440e53ab7cea576e74de531c2c451147f39f6479db9e7918318e63262b02aeb8a7958fbc10d7e49cf53bb9056c0834a54687f26611d9f7d41861edf90c6'
            '4913fad7defea716cb844e7a18e53ca5639fc390f84adb11600ac0262b5cebcee31e0536d3d5264029f4c2dd220fa48b807894d5ccab66b89b376cb53f8b61af')

prepare() {
  mv -v "${_name}_${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  # fixing executable stack and missing full RELRO
  patch -Np1 -i "../$pkgname-$pkgver-exec_stack_full_relro.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  LV2_PATH="${PWD}/${_name}" lv2lint "https://github.com/brummer10/Xmonk.lv2" || echo "Known to fail: https://github.com/brummer10/Xmonk.lv2/issues/3"
  sord_validate "${PWD}/${_name}/${_name}.lv2/"*.ttl || echo "Known to fail: https://github.com/brummer10/Xmonk.lv2/issues/3"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
