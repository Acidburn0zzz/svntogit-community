# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.5.2
pkgrel=1
pkgdesc='Korean input engine for IBus'
arch=('x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul' 'iso-codes' 'gtk-update-icon-cache' 'python-gobject')
makedepends=('intltool' 'gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/choehwanjin/ibus-hangul/archive/${pkgver}.tar.gz")
sha512sums=('66fe5da9b8d680eca8d5e90b6256d10fb6f13415c7227fdf38a8f728ae0a0961d0e6c0be4999f18645484885e5ea4cd28f88a4cef8116935db617a042bfd3bf7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf --verbose --force --install
  ./configure --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make NO_INDEX=true DESTDIR="${pkgdir}" install

  # Fix symlink
  ln -sf ../lib/ibus/ibus-setup-hangul "$pkgdir/usr/bin/ibus-setup-hangul"
}
