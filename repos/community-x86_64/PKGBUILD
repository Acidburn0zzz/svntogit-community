# $Id: PKGBUILD,v 1.22 2009/05/03 01:57:31 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=0.17
pkgrel=1
pkgdesc="A fast and lightweight IDE"
arch=('i686' 'x86_64')
url="http://www.geany.org"
license=('GPL')
depends=('gtk2>=2.8.0')
makedepends=('perlxml' 'intltool')
optdepends=("vte: for terminal support")
source=(http://download.geany.org/$pkgname-$pkgver.tar.gz)
md5sums=('cb4c325586ea6791d5db60599f1843d2')
options=('!libtool')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr
  make || return 1

  # FS#10318
  sed -i 's|MimeType=text/plain;|MimeType=|' geany.desktop || return 1
  # Add colors for PKGBUILD file
  sed -i 's|Sh=|Sh=PKGBUILD;|' data/filetype_extensions.conf || return 1

  make DESTDIR=${pkgdir} install || return 1
}
