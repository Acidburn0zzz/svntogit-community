# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Xentec <xentec at aix0 dot eu>

pkgname=glbinding
pkgver=3.0.2
pkgrel=2
pkgdesc="A C++ binding for the OpenGL API, generated using the gl.xml specification"
arch=('x86_64')
url="http://www.glbinding.org"
license=('MIT')
depends=('libgl' 'glfw')
makedepends=('cmake' 'mesa')
checkdepends=('python')
source=($pkgname-$pkgver.tar.gz::"https://github.com/cginternals/glbinding/archive/v$pkgver.tar.gz")
sha256sums=('23a383f3ed31af742a4952b6c26faa9c346dd982ba9112c68293a578a6e542ad')

prepare() {
  cd $pkgname-$pkgver

  mkdir build
}

build() {
  cd $pkgname-$pkgver/build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_BUILD_TYPE=Release \
      -DOPTION_BUILD_TESTS=ON
  make
}

check() {
  cd $pkgname-$pkgver/build

  make test
}

package() {
  cd $pkgname-$pkgver

  make -C build DESTDIR="$pkgdir/" install

  rm "$pkgdir"/usr/bin/glinfo
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README.md AUTHORS "$pkgdir"/usr/share/doc/$pkgname
}
