# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=n2n
pkgver=2.8
pkgrel=1
pkgdesc="A Peer-to-peer VPN software which makes it easy to create virtual networks bypassing intermediate firewalls"
arch=('x86_64')
url="https://github.com/ntop/n2n"
license=('GPL')
depends=('libcap' 'openssl')
optdepends=('libpcap: for n2n-decode')
makedepends=('cmake' 'ninja' 'libpcap')
source=("https://github.com/ntop/n2n/archive/$pkgver/n2n-$pkgver.tar.gz")
sha512sums=('24eaabe1b454fbcf47c251d93fad31bf10241fe7d79b3df8cfd758652fe60f18e4f123054a4319c6b58ae96d130faa93f3d56b1bfec6c91d9c834c8bd5ae71d4')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/RUNTIME DESTINATION sbin/RUNTIME DESTINATION bin/' CMakeLists.txt
}

build() {
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
