# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=2.3
pkgrel=1
pkgdesc='Deepin Screen Recorder'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-tool-kit' 'byzanz' 'ffmpeg')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/manateelazycat/deepin-screen-recorder/archive/$pkgver.tar.gz")
sha512sums=('96b69d1ce588dddd2e287f852bb864f1dc0ab736e309c691087797438ec7d968b30b945bd89b75572170439766c0d5a025e0b2c992163250f9884e98c8ac3952')

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
