# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-manual
pkgver=2.0.20
pkgrel=1
pkgdesc='Deepin User Manual'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-manual"
license=('GPL3')
depends=('deepin-qt5integration' 'qcef' 'gtk2')  # gtk2: todo
makedepends=('cmake' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-manual/archive/$pkgver.tar.gz")
sha512sums=('875d8b0ab761801c3ec1e430e8e55a3873cd3f2db142b9aa4fcbdea6247bd904e4c91d15b2895142f7f1c9840a98dbcd77daaa0b73fdbc9fe682e4b872770a42')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
