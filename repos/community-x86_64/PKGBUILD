# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympow
pkgver=2.023.5
pkgrel=1
pkgdesc="A package to compute special values of symmetric power elliptic curve L-functions"
arch=(x86_64)
url="http://www.sagemath.org"
license=(GPL)
depends=(pari)
makedepends=(help2man)
source=("https://gitlab.com/rezozer/forks/sympow/-/archive/v$pkgver/sympow-v$pkgver.tar.gz")
sha256sums=('24be227753bb1c895bedaddf7c2a89bb38cb0feea9912e10be92524e70e44b74')

prepare() {
  cd $pkgname-v$pkgver
  sed -e 's|VERBOSE_DEFAULT 1|VERBOSE_DEFAULT 0|' -i sympow.h
}

build() {
  cd $pkgname-v$pkgver

  PREFIX=/usr ./Configure
  make
}

package() {
  cd $pkgname-v$pkgver

  make DESTDIR="$pkgdir" install
}
