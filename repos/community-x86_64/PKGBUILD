# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Fabio 'Lolix' Loli <fabio.loli@disroot.org> -> https://github.com/FabioLolix
# Contributor: Moritz Sch√∂nherr <moritz.schoenherr@gmail.com>

pkgname=mdbook
pkgver=0.4.4
pkgrel=2
pkgdesc="Create book from markdown files, like Gitbook but implemented in Rust"
url="https://github.com/rust-lang-nursery/mdBook"
arch=(x86_64)
license=(MPL2)
depends=(glibc)
makedepends=(rust cargo)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-lang-nursery/mdBook/archive/v${pkgver}.tar.gz")
sha256sums=('eaf01085bd25e2efa07b561148afa5e3da3386e5f2c35b245961dc485562c154')
# https://github.com/rust-lang-nursery/mdBook/blob/master/CHANGELOG.md

build() {
  cd "mdBook-${pkgver}"
  cargo build --release
}

check() {
  cd "mdBook-${pkgver}"
  cargo test
}

package() {
  cd "mdBook-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
}
