# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.10.6
pkgrel=3
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'python' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        llvm9.patch)
sha512sums=('5d3a735fa33bfebdba9082345dcfeb89c17e876c9958d8af78f4e2150153062013a88644fc612b5778236eebb16a5ebae9a6d0adb4ff1e152a5c451905220481'
            'a52e0c103da6c800115f7140b3d29b78e20cb1a6a704bfe7440c50f1e398997dfd77723bfa9d6001aea7d2a249c3ebcbda3e94ea3ad512830f012a0b0b4bd77b')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver
  patch -Np1 -i ../llvm9.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DENABLERTTI=ON \
    -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install
  mkdir "$pkgdir"/usr/share/OSL
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
