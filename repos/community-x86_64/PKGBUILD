# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=tachyon
pkgver=0.98.9
pkgrel=6
pkgdesc="Multithreaded ray tracing software"
arch=(x86_64)
url="http://jedi.ks.uiuc.edu/~johns/raytracer/"
license=(BSD)
depends=(libjpeg-turbo libpng)
makedepends=()
#source=("http://jedi.ks.uiuc.edu/~johns/raytracer/files/$pkgver/$pkgname-$pkgver.tar.gz" 'enable-jpeg-png.patch')
source=("https://mirrors.mit.edu/sage/spkg/upstream/tachyon/tachyon-$pkgver.tar.bz2" enable-jpeg-png.patch tachyon-system-cflags.patch)
sha256sums=('e8888a410610e2726dca32f40f31ff34a83cb474b41e5a1e0ac8bbb175e1a466'
            'ecd6ab6274879e0f971e2eba17871fffe2e24dcc9a332013a0b100ee4857cc2c'
            '34f703af96afe117c851a5825b7fd6b71211c09e4a8c3b836d012d250bb6918f')
_target=linux-64-thr

prepare(){
  cd $pkgname-$pkgver
# enable JPEG and PNG support
  patch -p0 -i ../enable-jpeg-png.patch
# use system build flags
  patch -p1 -i ../tachyon-system-cflags.patch
  sed -e 's|${CC} ${CFLAGS}|${CC} ${CFLAGS} ${LDFLAGS}|g' -i unix/Makefile
}

build() {
  cd $pkgname-$pkgver/unix
  make $_target CC="gcc ${CFLAGS} ${LDFLAGS}"
}

package() {
  cd $pkgname-$pkgver/compile/$_target
  install -D -m 755 tachyon $pkgdir/usr/bin/tachyon
  install -D -m 644 libtachyon.a $pkgdir/usr/lib/libtachyon.a

  install -d "$pkgdir"/usr/share/licenses/tachyon
  install "$srcdir"/$pkgname-$pkgver/Copyright "$pkgdir"/usr/share/licenses/tachyon
}
