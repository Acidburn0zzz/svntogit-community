# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-gphoto2
pkgver=2.3.1
pkgrel=1
pkgdesc="Python interface to libgphoto2"
arch=('x86_64')
url="https://github.com/jim-easterbrook/python-gphoto2"
license=('GPL3')
depends=('libgphoto2' 'python')
makedepends=('swig' 'python-setuptools')
source=($pkgname-$pkgver.tar.gz::https://github.com/jim-easterbrook/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('c2be40ab8367b54549b52d67d36566bee7e35dc9390b236e05d2cc63260494ba')

prepare() {
  cd $pkgname-$pkgver
#https://github.com/jim-easterbrook/python-gphoto2/issues/136
  python developer/build_swig.py system
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package(){
  cd $pkgname-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
