# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=2.9.2
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=('glibc' 'ocaml>=4.13.1' 'ocaml-findlib' 'ocaml-csexp')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('34ba699b4e33bd6d849553f0349b117b4ab8820f1d087b928e9c99ad8b5734a0618089cff57c4a23f14f09112df26c12903183ed5fd1647358e99cb6494a66d4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make dune.exe  # this runs `ocaml bootstrap.ml`, but keeps upstream's choice
    dune_release_pkgs='dune,dune-action-plugin,dune-build-info,dune-configurator,dune-glob,dune-private-libs,dune-site'
    ./dune.exe build -p "${dune_release_pkgs}" --profile dune-bootstrap
}

# Tests require a bunch of (currently) unpackaged dependencies
# check() {
#     cd "${srcdir}/${pkgname}-${pkgver}"
#     make test
# }

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    DESTDIR="${pkgdir}" ./dune.exe install --prefix "/usr" --libdir=$(ocamlfind printconf destdir)
    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
