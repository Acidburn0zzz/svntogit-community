# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libyuv
pkgver=r2212+dfaf7534
pkgrel=1
pkgdesc="Library for YUV scaling"
arch=(x86_64)
url="https://chromium.googlesource.com/libyuv/libyuv/"
license=(custom)
depends=(gcc-libs libjpeg)
makedepends=(cmake git)
source=(git+${url})
sha512sums=(SKIP)

pkgver() {
  cd ${pkgname}
  printf "r%s+%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
