# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=tokei
pkgver=10.0.0
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('1e8a0d6f8fa4bc471380159086060465dfa1639a04919d2d7494389582f14fab921d4cf2e57914e4e896e8cf37c2eca2298ab75d95169400ce401739f5d09117')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cargo build --release
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
