# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=1.9.9
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('file'  'python-pyqt5' 'qt5-svg')
makedepends=('fluidsynth' 'gtk2' 'gtk3' 'liblo' 'linuxsampler' 'projectm'
'pulseaudio' 'python-rdflib' 'qt4' 'qt5-tools' 'zynaddsubfx')
optdepends=('fftw: extra native plugin support'
            'fluidsynth: .sf2 sampler support'
            'gtk2: LV2 GTK2 UI support'
            'gtk3: LV2 GTK3 UI support'
            'linuxsampler: .gig and .sf2 sampler support'
            'mxml: extra native plugin support'
            'ntk: extra native plugin support'
            'projectm: native visualization plugin'
            'python-pyliblo: OSC support'
            'python-rdflib: LADSPA-RDF support'
            'qt4: LV2 QT4 UI support'
            'zlib: extra native plugin support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('74f5880c60ca95b0173f2a408ae00235e1f422a8672436ab925caf9c1fd718da90182f092ad304f578082119381ef100ec210b6f3556a58a3cd1b28f50977a2e')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
