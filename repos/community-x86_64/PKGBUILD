# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Daniel Milde <daniel at milde dot cz>

pkgname=fq
pkgver=0.0.4
pkgrel=1
pkgdesc="Tool, language and decoders for inspecting binary data"
arch=('x86_64')
url="https://github.com/wader/fq"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('39c1722ea04bf395f961e7bd5c40ff440838e3d95f7a2daf1145c6d384258912')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -p dist/
}

build() {
  cd "${pkgname}-${pkgver}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\" ${_BUILDINFO}" \
    -o dist/fq \
    .
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "dist/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE  -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim:set ts=2 sw=2 et:
