# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Brent Carmer
# Contributor: Alex J. Malozemoff <amaloz@galois.com>

pkgname=yices
pkgver=2.6.3
pkgrel=1
pkgdesc="The Yices SMT Solver"
arch=('x86_64')
url="http://yices.csl.sri.com/"
license=('GPL')
depends=('cudd' 'gmp' 'libpoly')
makedepends=('gperf')
# Since 2.6.3 sources are not published in http://yices.csl.sri.com/releases/ anymore.
source=("https://github.com/SRI-CSL/yices2/archive/Yices-$pkgver.tar.gz")
sha512sums=('6b80a34dde63e11af9695750ba7528b516e34a29af086ba9a50405a9e0d4637af878d6aac866c3ad8fb5eafd56e7af760b8a700e2963b0176c6de70a9ddea809')

build() {
  cd yices2-Yices-$pkgver
  autoconf
  ./configure --prefix=/usr --enable-mcsat
  make
}

package() {
  cd yices2-Yices-$pkgver
  make DESTDIR="$pkgdir/" install
}
