# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Clement Tsang (xoronth) <cjhtsang@uwaterloo.ca>

pkgname=bottom
pkgver=0.6.7
pkgrel=1
pkgdesc="A graphical process/system monitor"
arch=('x86_64')
url="https://github.com/ClementTsang/bottom"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('71bf22e0651ed8917d6c77536708ba8581b2409abecd32552bfb5bb6af4e46fffea02af665104632f2a969f10ff5fc6e4c2cc47f1aad62c37aceb6e64a52ae26')
b2sums=('13128bf2d7e9aeb30ce0caa69c3392a7af8422f41f064346e32bdead1d2b3cfb51a208ad143c9c733c92a2d9720031cc26d1def754e104656f7599b78d747b53')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-$pkgver"
  CARGO_HUSKY_DONT_INSTALL_HOOKS=true cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/btm

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md
  cp -vr docs sample_configs "$pkgdir/usr/share/doc/$pkgname"

  # completions
  local target_dir=$(ls target/release/build/bottom-*/out/btm.bash | head -n1 | xargs dirname)
  install -Dm644 "$target_dir"/_btm "$pkgdir/usr/share/zsh/site-functions/_btm"
  install -Dm644 "$target_dir"/btm.bash "$pkgdir/usr/share/bash-completion/completions/btm"
  install -Dm644 "$target_dir"/btm.fish "$pkgdir/usr/share/fish/vendor_completions.d/btm.fish"
}

# vim:set ts=2 sw=2 et:
