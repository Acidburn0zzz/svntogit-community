# $Id$
# Maintainer: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=ejabberd
pkgver=2.1.2
pkgrel=1
pkgdesc="An erlang jabber server"
arch=(i686 x86_64)
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang' 'pam')
makedepends=()
backup=(etc/ejabberd/ejabberd.cfg \
	etc/logrotate.d/ejabberd)
install=ejabberd.install
source=(http://www.process-one.net/downloads/ejabberd/${pkgver/_/-}/ejabberd-$pkgver.tar.gz \
	ejabberd \
	ejabberd.logrotate)
md5sums=('9102802ae19312c26f85ceb977b519aa'
         'd000f6ab2b26a04501cbbc1558214f00'
         '65b55a547952097d354a73c33e34b11e')

build() {
  cd $srcdir/$pkgname-$pkgver/src

  sed -i "s|EJABBERDDIR = \$(DESTDIR)@prefix@/var/lib/ejabberd|EJABBERDDIR = \$(DESTDIR)@prefix@/usr/lib/ejabberd|g" ./Makefile.in
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-pam --enable-odbc
  make || return 1
  make DESTDIR=$pkgdir install && \

  install -D -m 0755 $srcdir/ejabberd $pkgdir/etc/rc.d/ejabberd && \
  install -d $pkgdir/var/spool/ejabberd && \
  install -d $pkgdir/var/lib/ejabberd || return 1

  install -D -m0644 $srcdir/ejabberd.logrotate $pkgdir/etc/logrotate.d/ejabberd && \
  chmod ug+r $pkgdir/etc/ejabberd/*
}

