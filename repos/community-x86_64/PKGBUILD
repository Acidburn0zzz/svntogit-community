# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.9.4
pkgrel=3
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dtkwidget/commit/a380128ede3626b79964f7128c54697f6fd6c637.patch)
sha512sums=('da9d164b4bd9e53f7cbc9a4a37ecd0f8f3fc586b09ad179a0ddac35973b9b502ef82d58bd12a6b7da2c457f800a364f7c46cc95994e7dcccb567ade28d781901'
            '7789e82a86283080044c8ff95ddd91d48a0d924534e1baa5c502419c256e73435aeb77b3a39bacd1ab932c238b01c59e2b365051707a1586da773028e18c5eac')

prepare() {
  cd dtkwidget-$pkgver
  patch -p1 -i ../a380128ede3626b79964f7128c54697f6fd6c637.patch
}

build() {
  cd dtkwidget-$pkgver
  LDFLAGS=${LDFLAGS/,-z,now/}
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
