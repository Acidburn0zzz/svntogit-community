# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.43.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=(x86_64)
url="https://guitarix.org"
license=(GPL3)
groups=(ladspa-plugins lv2-plugins pro-audio)
depends=(atkmm bluez-libs cairo cairomm gcc-libs glibc glibmm
gtkmm3 libsigc++ libx11 pangomm ttf-roboto)
makedepends=(avahi boost curl gdk-pixbuf2 eigen faust fftw
glib2 gperf gtk3 intltool jack ladspa liblo liblrdf lilv
lv2 pango sassc waf zita-convolver zita-resampler)
optdepends=('new-session-manager: for session management')
provides=(guitarix2 libgxw.so libgxwmm.so ladspa-host lv2-host)
conflicts=(guitarix2)
replaces=(guitarix2)
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz")
sha512sums=('20dec12a3bfa689c254817e73718da6306404499f95202b2721f72bdea04281a3ab76bb91d38d48a28cc7e7c2d4173f1972a53e0875ffe2ac24c4cd508672563')
b2sums=('84d98e1033922986e42f2897cb28d33e142699e9c7b46e8e636e6a549f92ead5579a37d0e17bd7475866ea3c4569c60b4100718c224c1407cce7ed26f5fb09c9')

build() {
  cd "${pkgname}-${pkgver}"
  export LINKFLAGS="$LDFLAGS"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=(libavahi-common.so libavahi-gobject.so libboost_iostreams.so
  libcurl.so libfftw3f.so libgdk-3.so libgdk_pixbuf-2.0.so libgio-2.0.so
  libglib-2.0.so libgobject-2.0.so libjack.so liblilv-0.so liblo.so liblrdf.so
  libpangocairo-1.0.so libpango-1.0.so libsndfile.so libzita-convolver.so
  libzita-resampler.so)

  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
