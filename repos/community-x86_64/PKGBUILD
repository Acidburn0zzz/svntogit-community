# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.5
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoc' 'source-highlight' 'cmake' 'pkg-config' 'perl' 'python2'
             'lua' 'tcl' 'ruby' 'aspell' 'guile2.0' 'asciidoctor')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile2.0')
source=("${pkgname}-${pkgver}.tar.xz::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz.asc")
sha512sums=('af0135009b799d40790c09da5419c950a6c750264a556de418b0ba4f396ecbde2aadeae5cbea3f2e98fc9926f07439e28beac00cb0019dc6752c1a5640a9834e'
            'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
