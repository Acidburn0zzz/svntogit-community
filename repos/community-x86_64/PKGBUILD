# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=bat
pkgver=0.9.0
pkgrel=1
pkgdesc='Cat clone with syntax highlighting and git integration'
arch=('x86_64')
url='https://github.com/sharkdp/bat'
license=('APACHE' 'MIT')
makedepends=('cmake' 'rust')
depends=('libssh2')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('4ce9c118cf5da1159a882dea389f3c5737b5d98192e9a619b0fe8c1730341cc6')

build() {
  cd "$pkgname-$pkgver"

  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 target/release/$pkgname "$pkgdir/usr/bin/$pkgname"
  install -Dm644 doc/bat.1 "$pkgdir/usr/share/man/man1/bat.1"

  # Shell completions will probably be re-enabled for the next release of bat
  # See: https://github.com/sharkdp/bat/issues/372

  #install -Dm644 target/release/build/bat-*/out/bat.bash \
  #  "$pkgdir/usr/share/bash-completion/completions/bat"
  #install -Dm644 target/release/build/bat-*/out/bat.fish \
  #  "$pkgdir/usr/share/fish/vendor_completions.d/bat.fish"
  #install -Dm644 target/release/build/bat-*/out/_bat \
  #  "$pkgdir/usr/share/zsh/site-functions/_bat"

  install -Dm644 LICENSE-APACHE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-APACHE"
  install -Dm644 LICENSE-MIT \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}

# vim: ts=2 sw=2 et:
