# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.25
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
depends=('glibc')
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz"
        "test-backport.patch")
sha512sums=('875b41987972f7f6e3fea0444a98ba6b033b3a932995693fe047f8751cf556f785a54efd9f53a0ceef598f73df7c6064908fa51824dd8a5b6c3686ba557e9730'
            '5c7468f47c19867598bafe07322d1c5a7314286eabab6012aa70a55967967467e620f5a56aeecadf6ebd13021f2c01a4e54c70c41ee09dfd702a082507b3ae47')
changelog="CHANGELOG.md"

prepare() {
	cd "$pkgname-$pkgver"
  patch -Np1 < "${srcdir}/test-backport.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath -mod=vendor"
	go build -o terraform-binary
}

check() {
  cd "${pkgname}-${pkgver}"
  go test -mod=vendor ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
