# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.4.4
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'openssl')
makedepends=("cmake")
source=("http://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('8c72653efbe6888e9e4fb1e74be05ee583af79d607348087ca62bd390d36b2a80409d4cf7369527b37edf05c9c9089112fc24b6e9c24a1ecefc20ee699e6d883')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's_#include <ncursesw/curses.h>_#include <cursesw.h>_' src/tui-main.cpp
  sed -i 's_#include <ncursesw/_#include <_g' src/*
  sed -i 's_ncursesw/curses.h_cursesw.h_' configure.in
  sed -i 's_ncursesw/__g' configure.in
}

build() {
  cd $pkgname-$pkgver
  autoconf
  ./configure --prefix=/usr --without-gnutls
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
