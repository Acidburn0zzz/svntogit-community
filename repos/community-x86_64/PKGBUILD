# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vico
pkgver=1.1
pkgrel=1
pkgdesc="Minimalistic Midi sequencer intended to be used in parallel with other software"
arch=('x86_64')
url="https://www.laborejo.org/vico.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('glib2' 'jack' 'nuitka' 'libsndfile' 'libutil-linux')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('9d8c92d5863d98f7dca22bb0fb69ca85730cd21131fc448d65982d77edaeb0506a334eb8b3f59e116e4f0daf86f58ce4f9456f864c7cd91d000f92986c2475cb'
            'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so' 'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
