# Maintainer: Jens Staal <staal1978@gmail.com>

pkgname=kernel-headers-musl
pkgver=3.12.6_5
pkgrel=2
pkgdesc="Linux kernel headers sanitized for use with musl libc"
arch=('x86_64')
url="https://github.com/sabotage-linux/kernel-headers"
license=('LGPL')
depends=('musl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sabotage-linux/kernel-headers/archive/v${pkgver/_/-}.tar.gz")
sha512sums=('e76f9b0789f2a2e174f7d2a0bec5d79b10fa7486a202fd29ffbae67657c6b89b947b667e2f2edfac8767b3e8491697b58d92035a8e632717f3f0a79a17bccc60')

build() {
  cd "$srcdir"/kernel-headers-${pkgver/_/-}
  _CARCH=$CARCH
  [ $CARCH == "i686" ] && _CARCH=x86
  make ARCH=${_CARCH} prefix=/usr/lib/musl
}

package() {
  cd "$srcdir"/kernel-headers-${pkgver/_/-}
  _CARCH=$CARCH
  [ $CARCH == "i686" ] && _CARCH=x86
  make ARCH=${_CARCH} prefix=/usr/lib/musl DESTDIR="$pkgdir" install
}
