# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Johan Rehnberg <cleanrock@gmail.com>

pkgname=naev
pkgver=0.8.0
pkgrel=1
pkgdesc='2D action/rpg space game'
arch=('x86_64')
url="https://github.com/naev/naev"
license=('GPL3')
depends=('glu' 'openal' 'libvorbis' 'sdl2' 'sdl2_image' 'sdl2_mixer' 'libgl' 'libxml2' 'freetype2' 'libpng' 'libzip' 'luajit')
makedepends=('freeglut' 'zip' 'meson' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/naev/naev/archive/v${pkgver}.tar.gz")
sha512sums=('3b4544d1f916e1426775c0229a6e294a80d2b8de0657d761b911ddd43070d51e5986ca0f5f37731b65479de2fee484db0f893f8599947e0ebb5e58e817514bb0')

build() {
  cd "$pkgname-$pkgver"

  meson setup -Dprefix=/usr -Dndata_path=/usr/share/naev/ndata build .
  cd build
  meson compile
}

package() {
  cd "$pkgname-$pkgver/build"

  DESTDIR="$pkgdir" meson install
}
