# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=oidentd
pkgver=2.4.0
pkgrel=1
pkgdesc='An RFC1413 compliant ident daemon'
arch=('x86_64')
url="https://oidentd.janikrabe.com/"
license=('GPL')
depends=('glibc' 'libnetfilter_conntrack')
backup=(etc/oidentd.conf)
source=(https://files.janikrabe.com/pub/oidentd/releases/$pkgver/oidentd-$pkgver.tar.xz{,.asc}
        service socket sysusers.conf)

md5sums=('76e28b88120cc2765f870ae103846af4'
         'SKIP'
         '8e1fec6ceece6bbf394381c58b531f76'
         '4442fb9fc10754914568e6ffc3d5fc42'
         '62cda7be7064da326e091bf7110c6ca4')

validpgpkeys=('FAC12F424E0A7A684D1D15262BD37AD723D597D9') # Janik Rabe

build() {
	cd oidentd-$pkgver
	./configure \
		--sysconfdir=/etc \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man
	make
}

package() {
	cd oidentd-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir"/service "$pkgdir"/usr/lib/systemd/system/oidentd@.service
	install -D -m644 "$srcdir"/socket "$pkgdir"/usr/lib/systemd/system/oidentd.socket
	install -D -m644 "$srcdir"/sysusers.conf "$pkgdir"/usr/lib/sysusers.d/oidentd.conf
}

