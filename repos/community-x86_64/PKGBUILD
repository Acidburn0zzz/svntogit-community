# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=consul
pkgver=1.4.0
pkgrel=2
pkgdesc="A tool for service discovery, monitoring and configuration."
arch=('x86_64')
_gocli_commit='3d22a244be8aa6fb16ac24af0e195c08b7d973aa' # HEAD
url="https://www.consul.io"
license=('MPL2')
depends=('glibc')
makedepends=('git' 'go-pie' 'procps-ng' 'zip')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/consul/archive/v${pkgver}.tar.gz"
        "gocli::git+https://github.com/mitchellh/cli#commit=${_gocli_commit}"
        'consul.service'
        'consul.default'
        'consul.sysusers'
        'example.json'
        'consul-ldflags.patch'
        'fix-build-version-info.patch'
        'disable-syslog-test.patch'
        'unparallelize-or-disable-flaky-tests.patch')
install=consul.install
backup=('etc/default/consul')
sha512sums=('a9f253ef5baa4e43800a0982ecb6893bf9487775cdcbe3a17bc7c45d601b6dca4e4c398ae3b70cdc1880577dbe1504d1a1f0cb702a1dd8c98b108e059fc721a0'
            'SKIP'
            'c70b9d1556f6c7ecb2e915ab685f289cef0e31198bd2e50c74a0483bbfb387beec67334f539a90adbf68b61b07946e98b300ab8a8e26e53b35f4ab4894adeb04'
            'ec5a800529a297c709fa383c094ecf106351cf0f8ac7b613b972d415d77fe001088902d7ab805e63e78a8e6360323fec1b795db5a4446df1e21b9b4ed31e7079'
            'ef872aedb2bc022a29292b7972a792b22e684c1ccb904a2b2cfec6d8966c28fb19be1452ce060821c419f1b646b236ba2e783175595e4bb6926d164c27a15c87'
            'c4292b8f56ee955ed7385a49843fd90d6434029891b3e1e724cb2fc841514c06e2554a26d3937c114371b18c2168c4e64319eb2cbd726ee8b35870df19089348'
            'cca5c71839c0a93515e8b2b6fa9d0a70e55b9e3cd1dcc9b8f9cbd94e7982ee695daec793a8ca9cb6245ff822dbbac4bf1af104c7ddef0ae9605d97a5fc08ed99'
            '34a02f05216ff9a0274b1be384f92d15f1c2d4c6bdd502d7b133e54850074105c3b2068bbb8f7902f083efbb319ecf1e448025b452eac5d420cb5fe322befe0b'
            '2a2e31469708f66877885c9e38f2044da13067c4111fc081ffea6187ff39acea6b17c0d33b2d0ada614315c3e5759a7592fbf7b0e9e9094ba2c31003bf1dbd4c'
            '509c0b615f1a282f80004449e94ca8a51ac52cd4babfda0e670f22bcdaa5f9b2bb88189d09764333b31e8b3a449524979919fabb77886573a63c8bd582398933')

prepare() {
  mkdir -p "src/github.com/mitchellh"
  mkdir -p "src/github.com/hashicorp"

  mv "${pkgname}-${pkgver}" "${srcdir}/src/github.com/hashicorp/${pkgname}"
  mv "gocli" "${srcdir}/src/github.com/mitchellh/cli"

  cd "${srcdir}/src/github.com/hashicorp/${pkgname}"

  # go ldflags hardening (e.g. RELRO, ...)
  patch -p1 -N -l -i "${srcdir}/consul-ldflags.patch"

  # use proper release build version string (w/o '-dev' suffix)
  patch -p1 -N -l -i "${srcdir}/fix-build-version-info.patch"

  # disable syslog test (requires running syslog service)
  patch -p1 -N -l -i "${srcdir}/disable-syslog-test.patch"

  # workaround/disable flaky tests tests
  patch -p1 -N -l -i "${srcdir}/unparallelize-or-disable-flaky-tests.patch"
}

build() {
  cd "${srcdir}/src/github.com/hashicorp/${pkgname}"
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  export GOOS='linux'
  export GOARCH='amd64'
  make linux
}

check() {
  cd "${srcdir}/src/github.com/hashicorp/${pkgname}"
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  # weird race conditions when being run overparallelized
  export GOMAXPROCS="2"
  export GOOS='linux'
  export GOARCH='amd64'
  export GOTEST_FLAGS="-p 2 -parallel 2"
  export CONSUL_TEST_SKIP_SYSLOG='true'
  make -j1 test
}

package() {
  cd "${srcdir}/src/github.com/hashicorp/${pkgname}"

  install -D -d -m750 -o 208 -g 208 "${pkgdir}/var/lib/consul"
  install -D -d -m750 -o   0 -g 208 "${pkgdir}/etc/consul.d"

  install -D -m644 "${srcdir}/consul.default" "${pkgdir}/etc/default/consul"
  install -D -m644 -o 0 -g 0 "${srcdir}/example.json" "${pkgdir}/usr/share/doc/consul/config.example.json"
  install -Dm755 "bin/consul" "${pkgdir}/usr/bin/consul"

  install -Dm644 "${srcdir}/consul.service" "${pkgdir}/usr/lib/systemd/system/consul.service"
  install -Dm644 "${srcdir}/consul.sysusers" "${pkgdir}/usr/lib/sysusers.d/consul.conf"
}

# vim:set ts=2 sw=2 et:
