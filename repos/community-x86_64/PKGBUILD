# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Max Roder <maxroder@web.de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=libhx
pkgver=3.23
pkgrel=1
pkgdesc='A library providing queue, tree, I/O and utility functions'
arch=('x86_64')
url='http://libhx.sourceforge.net/'
license=('GPL')
depends=(glibc)
source=("https://downloads.sourceforge.net/${pkgname}/libHX-${pkgver}.tar."{xz,asc})
sha256sums=('6433dd5d48ad9a64c372ba70bf3e78aa003d8b1c4394ced6c2ca0d776a4c68ec'
            'SKIP')
validpgpkeys=(2F8BFE96D14F8B560FBC1F1DA28BDFEEB5A2B2FF) # Jan Engelhardt <jengelh@inai.de>

build() {
  cd libHX-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd libHX-${pkgver}
  make DESTDIR="$pkgdir" install
}
