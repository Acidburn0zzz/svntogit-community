# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jesse Luehrs <archlinux@tozt.net>

pkgname=rbw
pkgver=1.4.0
pkgrel=1
pkgdesc="Unofficial Bitwarden CLI"
arch=('x86_64')
url="https://git.tozt.net/rbw"
license=('MIT')
depends=('pinentry')
makedepends=('rust')
source=("$url/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('450e5a64ce3aa43d071cd53507cc6f50b6c0b58a928556733d3fbcd7e3c1566de1279617491fbbc2b5b282eb9bea6fda2604cf36808c2ba28c5b0d503f542225')
b2sums=('318bb93541fd5bf9ea9eddeb5bf67896e680c93b6b26e95320b50c8b310ad87396b6fd04a26181cd3a1f381deb53dc58a97a1cb669c64af4ccfa1fc255b2fefa')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features

  # generate shell auto-completions
  for completion in bash fish zsh; do
    cargo run --frozen --release --bin rbw -- \
      gen-completions "$completion" > "$completion-completions"
  done
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rbw{,-agent}

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md CHANGELOG.md

  # shell auto-completions
  install -vDm644 bash-completions "$pkgdir/usr/share/bash-completion/completions/rbw"
  install -vDm644 fish-completions "$pkgdir/usr/share/fish/vendor_completions.d/rbw.fish"
  install -vDm644 zsh-completions "$pkgdir/usr/share/zsh/site-functions/_rbw"
}
