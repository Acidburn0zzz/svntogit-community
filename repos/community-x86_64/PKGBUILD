# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chinese-addons
pkgver=0.0.0.20200605
_commit=c3859a011403b4fd89b76a15e852827bf3b3be03
_pytablever=20121124
pkgrel=1
pkgdesc="Addons related to Chinese, including IME previous bundled inside fcitx4"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chinese-addons"
license=('GPL')
conflicts=('fcitx')
depends=('curl' 'fcitx5-qt' 'libime' 'opencc' 'qt5-webkit')
makedepends=('boost' 'extra-cmake-modules' 'fcitx5-lua')
optdepends=('fcitx5-lua: Lua and imeapi support from pinyin')
source=("https://github.com/fcitx/fcitx5-chinese-addons/archive/$_commit/fcitx5-chinese-addons-$_commit.tar.gz"
        https://download.fcitx-im.org/data/py_table-$_pytablever.tar.gz
        https://download.fcitx-im.org/data/py_stroke-$_pytablever.tar.gz)
noextract=(py_table-$_pytablever.tar.gz)
sha512sums=('4b475945ce340aca518c12ee63be7766276f78dc65a8c676d1bb2a95f817288b2852b36c459576ee7d8e93281ae2fe54d61f5f45ce13c3f3cd63ae337ef53862'
            '0fa72ce6820a316e9729bc0d7a08af5ca3468134de9ff1bf0c559a398846a7bcf7313a3609d943e6bab637d930cf45ad9acfe25a8d3df078a774b071a7296486'
            'd80ff9a3549e07c7a6935e686785f9a076c58f1a782a832ae28ee65e2a213d67f089f450ce09bed87ec2a141c32b9c1fcb83c221ddcab436858fa9009f161fb3')

prepare() {
  cd $pkgname-$_commit/modules/pinyinhelper
  ln -s "$srcdir"/py_table-$_pytablever.tar.gz
  ln -s "$srcdir"/py_stroke-$_pytablever.tar.gz
}

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
