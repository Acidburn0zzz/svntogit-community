# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-picker
pkgver=5.0.14
pkgrel=1
pkgdesc='Color picker tool for deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-picker"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'qt5-svg')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-picker/archive/$pkgver.tar.gz")
sha512sums=('e7f2a090ceadbb0475a7ef823c83431d5a545b237b0b49fce8aafd25f3ed45c92378f2f23668e0f617ce3fe5e91a7dd69c7784e37fa6d17458fc0d414338e486')

prepare() {
  cd deepin-picker-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/animation.cpp src/picker.cpp
}

build() {
  cd deepin-picker-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-picker-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
