# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Severen Redwood <severen@shrike.me>
# Contributor: Robert Welin <robert.welin@gmail.com>
# Contributorr: dsboger <https://github.com/dsboger>

pkgname=gtkd
pkgver=3.8.5
pkgrel=3
pkgdesc='D bindings for GTK+ and related libraries.'
arch=('x86_64')
url='https://gtkd.org'
license=('LGPL')
depends=('liblphobos' 'gtk3')
makedepends=('ldc')
optdepends=('pango' 'atk' 'gdk-pixbuf2' 'gtksourceview3' 'gstreamer' 'vte3' 'libpeas')
source=("https://github.com/gtkd-developers/GtkD/archive/v$pkgver.tar.gz")
sha512sums=('b75f8cb46de371a1f0ea344f0471b89667f84581e318daa30333a4646c132fe5431e58b9d31a2cfc789ddfc50d0cf9dc8d8a7e349823392c15a2eae5cecc88d1')

build() {
    cd GtkD-$pkgver

    make \
    	DC='ldc' \
    	LDFLAGS="-L=\"$LDFLAGS\"" \
    	libdir='lib/' \
    	shared-{gtkd,gtkdgl,sv,gstreamer,vte,peas}
}

package() {
    cd GtkD-$pkgver

    make \
    	prefix='/usr' \
    	libdir='lib/' \
    	DESTDIR="$pkgdir" \
    	install-{shared,headers}-{gtkd,gtkdgl,gtkdsv,gstreamer,vte,peas}
}

