# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Pranaitis <jens@jenux.homelinux.org>

pkgname=busybox
pkgver=1.19.4
pkgrel=1
pkgdesc="Utilities for rescue and embedded systems"
arch=("i686" "x86_64")
url="http://www.busybox.net"
license=('GPL')
makedepends=("make" "gcc" "sed" "ncurses")
source=($url/downloads/$pkgname-$pkgver.tar.bz2
	http://git.exherbo.org/arbor.git/plain/packages/sys-apps/busybox/files/linux-headers-fix.patch
	config)
install=busybox.install
md5sums=('9c0cae5a0379228e7b55e5b29528df8e'
         '853097e2f1477f962d6f268c1b3ebd78'
         'edd5c66c974ccc1b0030fd00beaa1b02')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p1 <$srcdir/linux-headers-fix.patch
  cp $srcdir/config .config
  # if you want to run menuconfig uncomment the following line:
  # make menuconfig ; return 1
  sed -i '1,2i#define __packed __attribute__((packed))' miscutils/ubi_tools.c
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm755 busybox $pkgdir/bin/busybox
}
