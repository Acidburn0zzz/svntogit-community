# $Id: PKGBUILD,v 1.5 2009/03/22 03:58:34 Snowman Exp $
# Maintainer: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.3.28
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url='http://balsa.gnome.org'
depends=('gtk2>=2.10' 'libgtkhtml>=2.11.0' 'libesmtp>=1.0.4' 'gmime>=2.2.6'
         'gtkspell>=2.0.11' 'gpgme>=1.1.3' 'gtksourceview2>=2.2' 'pcre>=7.0'
         'libgnomeui>=2.18.1-2' 'libnotify' 'heimdal' 'hicolor-icon-theme')
makedepends=('perlxml' 'gnome-doc-utils>=0.11.2' 'intltool')
install=balsa.install
source=(http://balsa.gnome.org/$pkgname-$pkgver.tar.bz2)
md5sums=('c9ea1f5683fc094b1ffcab7b9e66dd5c')

build() {
  cd $startdir/src/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --with-gtkhtml=2 --disable-more-warnings \
    --with-ssl --enable-threads --with-gss --with-ldap \
    --with-gpgme --with-gtkspell --with-gtksourceview=2 || return 1
  make || return 1
  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR=$startdir/pkg install || return 1
}
