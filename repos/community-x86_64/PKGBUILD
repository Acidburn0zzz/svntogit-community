# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.7.0
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('deepin-dock' 'qt5-tools')
optdepends=('redshift: automatic color temperature support')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        fix-crash.patch)
sha512sums=('f86c97ae232b4603cb1b9f87995733aca774cefcb73e57a5aaed06a5798d3813d7a6e8f2cb2cbb72129f2a064b390c1040996e436f0dc85f6c39aed8c326c92d'
            'ee4d2ca70a5f418425fdc2eefe4286346d38d2685ac28c9dca490687c5d519442e9c5b5b45a4fdf747352d154f06fae9457a329bae3b4b1c39f8e584521526e0')

prepare() {
  patch -d dde-control-center-$pkgver -p1 -i ../fix-crash.patch
}

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
