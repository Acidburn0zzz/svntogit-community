# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.1
pkgrel=6
pkgdesc="Multi-machine audio network performance over the Internet"
arch=('x86_64')
url="https://github.com/jacktrip/jacktrip"
license=('MIT')
groups=('pro-audio')
depends=('glibc' 'qt5-base')
makedepends=('alsa-lib' 'jack')
source=("https://github.com/${pkgname}/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4c6b99deb424910348a3a0b8621dba4abfcbdf1a93b3d2a63fe8098113dffc03c472ab536de6c58e9202cd25823af7ebde4ab818e5a74bedcf8b353417dfda91')

prepare() {
  mv -v "${pkgname}-${pkgname}-${pkgver}/${pkgname}" "${pkgname}-${pkgver}"
  mv -v "${pkgname}-${pkgname}-${pkgver}/README.md" "${pkgname}-${pkgver}/README.md"
  cd "${pkgname}-${pkgver}"
  # split out custom license:
  # https://github.com/jacktrip/jacktrip/issues/57
  sed -n '1,30p' documentation/documentation.cpp > LICENSE
}

build() {
  cd "${pkgname}-${pkgver}/src"
  qmake-qt5 -spec linux-g++ jacktrip.pro
  make release
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "src/${pkgname}" -t "${pkgdir}/usr/bin/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGESLOG.txt,README.md,TODO.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
