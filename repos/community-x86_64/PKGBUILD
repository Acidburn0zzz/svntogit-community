# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pyname=xxhash
pkgname=python-${_pyname}
pkgver=1.4.4
pkgrel=1
pkgdesc='Python binding for xxHash'
arch=('x86_64')
url="https://github.com/ifduyue/${pkgname}"
depends=('python' 'xxhash')
makedepends=('python-setuptools')
license=('BSD')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ifduyue/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('60e6cb2f8c1bdfa01ac6cad8f3950bf36da317fb6fb2b94a44ce5b855d45f5385fd70e3ac9e5e9b9870823c255e23d02738b8c6f82ba4bce5bf16dbadf92345c')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # link to xxHash
    export XXHASH_LINK_SO=1
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py build
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
