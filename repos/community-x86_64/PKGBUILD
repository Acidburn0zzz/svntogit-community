# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.12
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=('x86_64')
url="https://drumkv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libsndfile' 'qt5-base')
makedepends=('alsa-lib' 'libjack.so' 'liblo.so' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'libjack.so: for standalone application'
            'liblo.so: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4a2036d5b08202c8fe1c029149e5382d41cca0274e160ff089c8a22ab992bf2b91bd4cfd7e492d6bd6245a7053a1380b1b35b752ef244c99c19976c7ff14531d')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
