# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.3.4
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11' 'qt5-declarative' 'qt5-quickcontrols2' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python3')
optdepends=('python3: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('f78772f74d9f7a241ff9191ed6152f965b7a5342db877fa63a2d84f266d956cb2fa487776cf503db36bc5da9aa825c8bab87e13293e43a129f560020e323bd61')

build() {
  cd $pkgname-$pkgver
  qmake -r INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT="$pkgdir"
}
