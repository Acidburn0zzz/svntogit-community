# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-readme-renderer
pkgname=(python-readme-renderer python2-readme-renderer)
pkgver=22.0
_commit=12d6bd0d57666ece3f75d1ee11cf0eb29cecd104
pkgrel=1
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('BSD')
arch=('x86_64')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bleach' 'python2-bleach'
             'python-cmarkgfm' 'python2-cmarkgfm' 'python-docutils' 'python2-docutils'
             'python-future' 'python2-future' 'python-pygments' 'python2-pygments' 'python-six'
             'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock')
source=("$pkgbase-$_commit.tar.gz::https://github.com/pypa/readme_renderer/archive/$_commit.tar.gz")
sha512sums=('2d28c92b902670c08671fe00a6d590f21bc670f98fb739977a7a7fcc2bfeab3da91fa7a20101887824c912010b4f6fb7b245d0dbf9e440489a77d55679bfde5c')

prepare() {
  mv readme_renderer-{$_commit,$pkgver}
}

build() {
  cd readme_renderer-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || warning "Tests failed"
  python2 setup.py pytest || warning "Tests failed"
}

package_python-readme-renderer() {
  depends=('python-bleach' 'python-cmarkgfm' 'python-docutils' 'python-future' 'python-pygments'
           'python-six')

  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-readme-renderer() {
  depends=('python2-bleach' 'python2-cmarkgfm' 'python2-docutils' 'python2-future' 'python2-pygments'
           'python2-six')

  cd readme_renderer-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
