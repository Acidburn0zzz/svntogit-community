# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-media
pkgver=2.0.0
pkgrel=1
pkgdesc="UKUI media utilities"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-media"
groups=('ukui')
depends=('libmatemixer' 'mate-desktop')
makedepends=('autoconf-archive' 'intltool' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-media/archive/v$pkgver.tar.gz")
sha512sums=('d741e96e2961135913d65108839722d81d414ddd1094c41707bd149b7bc26d4d9d96208bdface885acb5abf252a4fe6e9412c9316c24ae3a3a5eab8e0bca78c8')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
