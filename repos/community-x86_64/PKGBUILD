# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma5-applets-window-buttons
pkgver=0.9.0
pkgrel=3
pkgdesc='Plasma 5 applet in order to show window buttons in your panels'
arch=(x86_64)
url='https://github.com/psifidotos/applet-window-buttons'
license=(GPL)
depends=(plasma-workspace)
makedepends=(extra-cmake-modules)
conflicts=(libappletdecoration)
replaces=(libappletdecoration)
source=(https://github.com/psifidotos/applet-window-buttons/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/psifidotos/applet-window-buttons/commit/6f881c25.patch
        https://github.com/psifidotos/applet-window-buttons/commit/164ad16f.patch)
sha256sums=('053201441e2cc7c0589c920028b985752ad9a87dc0f7fb35070cf44c9fcfbab7'
            '1db8b6f262f6f6b45c239e11698bfbdfe079b408710a16cc7902c01cf4b46b25'
            '3bf1971a094476655c84e2ac7b653866bc74ddd34ee2bb7f59903ff6aa6c7d8b')

prepare() {
  patch -d applet-window-buttons-$pkgver -p1 < 6f881c25.patch # Plasma 5.23 compatibility
  patch -d applet-window-buttons-$pkgver -p1 < 164ad16f.patch
}

build() {
  cmake -B build -S applet-window-buttons-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
