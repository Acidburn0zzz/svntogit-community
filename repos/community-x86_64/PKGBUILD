# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSEQuencer
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BSEQuencer"
pkgname=bsequencer
pkgver=1.8.0
pkgrel=1
pkgdesc="Multi channel MIDI step sequencer LV2 plugin with a variable matrix"
arch=('x86_64')
url="https://github.com/sjaehn/BSEQuencer"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('05e2542790b3d921521cdb15c832161e55f60d4163e519a11b3ec3f52e697ceea88487861669c25e7bc79d0ed3acc1834469cf4ac33de0e830a192184d16c58b')
b2sums=('b35cbf720862906b242b7d589cd15ed0b8e8adf7c95c3e5a7d565741ecaec35e6ecdd41a325794e8c18914d7f2d0836a1d1cbb2989515d26320cfac43eaa22bf')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "${_plugin_uri}"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
