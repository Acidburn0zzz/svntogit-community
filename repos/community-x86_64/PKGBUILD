# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.6.3
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=('x86_64')
url="https://qsynth.sourceforge.net/"
license=('GPL')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('fluidsynth' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7320a355b5fbb347b04b46fdfe86c13ab18b264cff09f2ed133e2b065febac27e58bd709eea284b4d0f98034059647ad6e9b97638fdd75edb1524fd3cf9f07a2')
b2sums=('37565c86b077602cd681b92aaea20d4aa07db1c4cf51d391562b5226ddde70dad0a3f641f0fcf54f35c6bb1a9e487a779c22237b0c8087666b7a2c1dea8a511e')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
