# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-anthy
pkgver=5.0.1
pkgrel=1
pkgdesc="Anthy Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-anthy"
license=('GPL')
depends=('fcitx5' 'anthy')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-anthy/fcitx5-anthy-$pkgver.tar.xz"{,.sig})
sha512sums=('eb5eb51067fbac0be44e9250b157e2ae2d8fc8af66b059bc78e7c4fcc0a9bae8a8ef78cdf7b47f69214f99bf9356fc18c2ed0d9d110eb19a2ef43c9c75c34838'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
