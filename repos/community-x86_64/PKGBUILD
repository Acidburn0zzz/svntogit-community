# Maintainer: Ivy Foster <code@iff.ink>

pkgbase=frotz
pkgname=(frotz-dumb frotz-ncurses frotz-sdl)
pkgver=2.50
pkgrel=1
pkgdesc='Z-machine interpreter for interactive fiction games'

makedepends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		ncurses

		freetype2
		libjpeg-turbo
		libpng
		sdl2
		sdl2_mixer
		zlib
)

arch=(x86_64)
url='https://gitlab.com/DavidGriffith/frotz'
license=(GPL)
source=("https://gitlab.com/DavidGriffith/frotz/-/archive/$pkgver/frotz-$pkgver.tar.gz")
# Warning: checksums provided by packager, not upstream.
sha256sums=(0352dfc458fb5cc7a932c568bd86aabdde943bee25ea0cce58c46f8c893f554f)

prepare() {
	cd "frotz-$pkgver"
	# I guess there was a last-minute change to the Makefile
	sed 's/^ \+/	/g' -i Makefile
}

build() {
	cd "frotz-$pkgver"
	make PREFIX=/usr dumb ncurses sdl
}

package_frotz-dumb() {
	pkgdesc='Z-machine interpreter for interactive fiction games, for dumb terminals or scripting'
	provides=(dfrotz)

	cd "frotz-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install_dumb
}

package_frotz-ncurses() {
	pkgdesc='ncurses-based Z-machine interpreter for interactive fiction games'
	provides=(frotz)
	depends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		ncurses
	)

	cd "frotz-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install_frotz
}

package_frotz-sdl() {
	pkgdesc='Graphical Z-machine interpreter for interactive fiction games'
	provides=(sfrotz)
	depends=(
		libao
		libmodplug
		libsamplerate
		libsndfile
		libvorbis

		freetype2
		libjpeg-turbo
		libpng
		sdl2
		sdl2_mixer
		zlib
	)

	cd "frotz-$pkgver"
	make DESTDIR="$pkgdir" PREFIX=/usr install_sdl
}
