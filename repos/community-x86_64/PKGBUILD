# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=kernelshark
pkgver=1.2
pkgrel=1
pkgdesc="GUI frontend for trace-cmd based Linux kernel Ftrace captures"
arch=('x86_64')
url="https://kernelshark.org/"
license=('GPL2')
depends=('freeglut' 'gcc-libs' 'glibc' 'glu' 'libglvnd' 'qt5-base' 'trace-cmd')
makedepends=('cmake' 'json-c')
provides=('libkshark.so' 'libkshark-plot.so' 'libkshark-gui.so')
source=("https://git.kernel.org/pub/scm/utils/trace-cmd/trace-cmd.git/snapshot/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('a64f36921d5be8cb4a1e00b84e6ffc5a866f75ce3b6138a446742688963b83851c65c02a90b641395ec6726ac3f08a3f5eb4ce7c20fd1e0d1120939848f6c262')
b2sums=('291cb20adb2272c775951eae092af5716e1740d6745fc29dff7e65b51a109c411e8ee141c063b4ebdad39ae7e7e1a015f66ae26ee4102bb1dfae961e662fd589')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make prefix='/usr' V=1
  cd "kernel-shark"
  cmake -D_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libjson-c.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" prefix='/usr' VERBOSE=1 -C kernel-shark/build install
}
