# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200720
_pkgver=2020-07-20
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('062db45f48f00afabf35c6fb4ce25e3c5cd7a565352ef696fec20c836e68241f4694fab9276bf42005afb203328e1b3c5b07f3341f151eb3a1df6c6506cc55f8')

prepare() {
  cd $pkgname-$_pkgver
  sed -i '/ensure_rustfmt()?;/d' xtask/src/lib.rs
}

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$_pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
