# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgname=vifm
pkgver=0.10
pkgrel=1
pkgdesc="A file manager with curses interface, which provides Vi[m]-like environment"
arch=('x86_64')
url="https://vifm.info/"
license=('GPL')
depends=('ncurses' 'desktop-file-utils')
changelog=$pkgname.changelog
source=(http://prdownloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2{,.asc})
validpgpkeys=('22349CDBE5227D2108A5667799DC5E4DB05F6BE2') # xaizek <xaizek@posteo.net>
sha256sums=('e05a699f58279f69467d75d8cd3d6c8d2f62806c467fd558eda45ae9590768b8'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
