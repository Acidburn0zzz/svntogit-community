# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.6.2
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'procps-ng' 'nethogs' 'libcap')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz"
        qt-5.14.patch)
sha512sums=('dfc2b42ee89dc0c2c35fcb6387fe7b175f796e877cf002963f8d39cd458879767dedc351e3def4b8d3e2a98a4df265da1e60306aa37bcc8e1e45e2e5e1a32017'
            '6e3efe9a52c6a1d1b221570c8c9997d5d88e57c8220cda29daa00263ef5578dcdf6d6dbf5721da8d055ec98ad01277458f2cfc9981fc034481b567d3f4c3940d')

prepare() {
  cd deepin-system-monitor-$pkgver
  patch -p1 -i ../qt-5.14.patch
  sed -i '/<QPixmap>/a #include <QUrl>' src/process/stats_collector.cpp

  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro

  sed -i 's/nethogsmonitor_loop(&onNethogsUpdate);/nethogsmonitor_loop(\&onNethogsUpdate, NULL, 1000);/' src/network_traffic_filter.cpp
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
