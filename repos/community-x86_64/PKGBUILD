# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=1.4.8.1
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'procps-ng' 'nethogs' 'libcap')
makedepends=('qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('900400e9e3259558eb75305641c008c77007ab9f6e605a80da989d555693cced1eb87a1223685cc365b1d66f6b72e394148abaa834e68ac8e4fbd0abffd70796')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
