# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.3.0.5
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'gsettings-qt')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('7ac8ba86b492bcb0614e559cc0228e5cf1d4146d83a92e27562c18bb83d856bb331ed890bbcb1d0d818c98c9686ca28cd9b66f5bf6327c0070c5108bde4be66f')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of commercial company's homepage
  sed -i '/www.uniontech.com/i \    "https://www.archlinux.org/favicon.ico",' connectivitychecker.cpp
}

build(){
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
