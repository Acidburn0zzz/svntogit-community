# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.7.6.80
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'libxtst' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz")
sha512sums=('a294bec2160b04214519cf529d0cf47eaf20b86b5bc44b811fb0f3f4c3e77d23b320b11b9ab784bfcbf7ae068a7b2bfbfcec4996af46b90258436cf7a581daf9')

prepare() {
  cd deepin-movie-reborn-$pkgver
  sed -i '/#include <DPalette>/a #include <QPainterPath>' src/widgets/{tip,toolbutton}.h
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  ninja
}

package() {
  cd deepin-movie-reborn-$pkgver
  DESTDIR="$pkgdir" ninja install
}
