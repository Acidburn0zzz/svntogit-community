# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: quininer <quininer@live.com>
pkgbase='alacritty'
pkgname=('alacritty' 'alacritty-terminfo')
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
pkgver=0.2.1
pkgrel=1
arch=('x86_64')
url="https://github.com/jwilm/alacritty"
license=('Apache')
makedepends=('rust' 'cargo' 'cmake' 'fontconfig' 'ncurses' 'desktop-file-utils')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/jwilm/${pkgbase}/archive/v${pkgver}.tar.gz")
sha256sums=('d335f09ba914faf8d8b2ba91a67672aab3acd1a3bb1528ec3d9339381697f6a1')

build(){
  cd $pkgbase-$pkgver
  env CARGO_INCREMENTAL=0 cargo build --release --locked
}

check(){
  cd $pkgbase-$pkgver
  env CARGO_INCREMENTAL=0 cargo test --release
}

package_alacritty() {
  depends=('freetype2' 'fontconfig' 'xclip')
  
  cd $pkgbase-$pkgver
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "alacritty.desktop"
  
  install -D -m755 "target/release/alacritty" "$pkgdir/usr/bin/alacritty"
  install -D -m644 "alacritty.man" "$pkgdir/usr/share/man/man1/alacritty.1"
  install -D -m644 "alacritty-completions.bash" "$pkgdir/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "alacritty-completions.zsh" "$pkgdir/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "alacritty-completions.fish" "$pkgdir/usr/share/fish/completions/alacritty.fish"
}

package_alacritty-terminfo() {
  pkgdesc="Terminfo files for the alacritty terminal emulator"
  depends=('ncurses')
  
  cd $pkgbase-$pkgver
  
  install -dm 755 "$pkgdir/usr/share/terminfo/a/"
  tic -o "$pkgdir/usr/share/terminfo" alacritty.info
}
