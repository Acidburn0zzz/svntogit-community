# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgbase=himalaya
pkgname=('himalaya' 'vim-himalaya')
pkgver=0.5.2
pkgrel=1
pkgdesc="A CLI email client"
arch=('x86_64')
url="https://github.com/soywod/himalaya"
license=('BSD')
depends=('gcc-libs' 'openssl')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ef2c78f51759774c27c2912b64dbb4a272a7c24eb33659477c6d1cd3f7f3d8822d1a48f50c3486d6468d8eead95461ac6487907b47b65a92f75e0c4472162b26')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package_himalaya() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}

package_vim-himalaya() {
  depends=('vim-plugin-runtime' 'himalaya')
  pkgdesc+=' - Vim UI plugin'

  cd "$pkgbase-$pkgver/vim/"
  find autoload doc ftplugin lua plugin syntax \
    -type f -exec install -Dm 644 '{}' "$pkgdir/usr/share/vim/vimfiles/{}" \;
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
