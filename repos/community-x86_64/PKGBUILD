# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=1.8.3
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=('x86_64')
url="https://github.com/linuxmint/xreader"
license=('GPL')
depends=('poppler-glib' 'webkit2gtk' 'xapps')
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'texlive-bin: support for dvi files')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools' 'djvulibre' 'libgxps' 'libspectre' 'texlive-bin')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('ab9b391aa8ca576165075ff8262981f737b35849a765001f90bd867267169ced')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix="/usr" \
                --localstatedir="/var" \
                --libexecdir="/usr/lib/${pkgname}" \
                --enable-introspection \
                --enable-t1lib
    make
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
