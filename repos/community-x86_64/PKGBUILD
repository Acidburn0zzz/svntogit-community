# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sympol
pkgver=0.1.9
pkgrel=3
pkgdesc="A C++ tool to work with symmetric polyhedra"
arch=(x86_64)
url="http://www.math.uni-rostock.de/~rehn/software/sympol.html"
license=(GPL2)
source=($pkgname-$pkgver::"https://github.com/tremlin/SymPol/archive/v$pkgver.tar.gz")
depends=(boost-libs lrs cddlib)
makedepends=(cmake permlib eigen bliss)
sha256sums=('6753b8fb30745b66a0886e125c18b539417afcf62b804799eb220bd5a59ccc37')

prepare() {
  mkdir -p build

  cd SymPol-$pkgver
  sed -e '/(external/d' -i CMakeLists.txt # Don't use bundled cddlib and lrslib
  sed -e 's|lrsgmp|lrs|' -i sympol/CMakeLists.txt # Fix linking to system lrs
  rm -r external/{lrslib*,cddlib*}
  sed -e '/lrs_mp_close/d' -i sympol/raycomputationlrs.cpp # Fix build with lrs 0.5 (Fedora)
}

build() {
  cd build
  cmake ../SymPol-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -I/usr/include/lrslib -DMA -DGMP -DBLISS_USE_GMP"
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  sed -r -e 's|(#include ").*/(.*")|\1\2|' -i "$pkgdir"/usr/include/sympol/*.h
}
