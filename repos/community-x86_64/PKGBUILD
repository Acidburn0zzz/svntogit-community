# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=darkstat
pkgver=3.0.715
pkgrel=7
pkgdesc="Network statistics gatherer (packet sniffer)"
url="http://dmr.ath.cx/net/darkstat/"
license=("GPL")
arch=(i686 x86_64)
depends=('libpcap' 'zlib' 'gcc-libs')
backup=('etc/conf.d/darkstat.conf')
source=(http://dmr.ath.cx/net/darkstat/darkstat-$pkgver.tar.bz2
	darkstat
	darkstat.service
	darkstat.conf)
install=darkstat.install
md5sums=('5b7abc7538dcd8e30667dac150e81d77'
         '1021f95fabe16cfab4c95f6264d535e5'
         'e2d9efc856201146e2abae145b4c5a1f'
         'aa9a9effd1e8f08860afcd6439fc94c5')

build() {
  cd $srcdir/darkstat-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/darkstat-$pkgver
  make DESTDIR=$pkgdir install

  # darkstat script
  install -D -m755 "$srcdir"/darkstat "$pkgdir"/etc/rc.d/darkstat
  install -D -m644 "$srcdir"/darkstat.conf "$pkgdir"/etc/conf.d/darkstat.conf
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
