# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
pkgver=1.4.2
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Edu4rdSHL/findomain'
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=("https://github.com/Edu4rdSHL/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('189f520e92e7c86146789ab66c39cf51308341691b6967a24c9dfab208cc5e1ba869aa538f26ca18f5e6cbefe612e4838fcdfc8744123368554e572c43b84e67')
b2sums=('392d521cd00a52481dc50e421c2989d7062a5a9198d321c220de34accfb3379f3050275bb4ba2bf2cd438342ae9e766d37cdb072944d36fcf0ef10e4a26a6125')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
