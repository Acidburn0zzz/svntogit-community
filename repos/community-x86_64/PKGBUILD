# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=1.8.1
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=('x86_64')
url="https://github.com/linuxmint/xreader"
license=('GPL')
depends=('poppler-glib' 'webkit2gtk' 'xapps')
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'texlive-bin: support for dvi files')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools' 'djvulibre' 'libgxps' 'libspectre' 'texlive-bin')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('25feb99b468c968d7c08acd53a0e91c9933cc5646a6bcec2bdfe5fbd38671876')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix="/usr" \
                --localstatedir="/var" \
                --libexecdir="/usr/lib/${pkgname}" \
                --enable-introspection \
                --enable-t1lib
    make
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
