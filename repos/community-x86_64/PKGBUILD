# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ospray
pkgver=1.7.3
pkgrel=1
pkgdesc="A Ray Tracing Based Rendering Engine for High-Fidelity Visualization"
arch=('x86_64')
url="http://www.ospray.org/"
license=('Apache')
depends=('intel-tbb' 'embree' 'ispc' 'libgl' 'glfw-x11' 'glu' 'openmpi')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/ospray/OSPRay/archive/v${pkgver}.tar.gz")
sha256sums=('08a7b4cb7ada50d60c30685a73d3e6617b0f02beece163c5ff0afcb9e4ba8d38')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DOSPRAY_BUILD_ISA=ALL \
        -DOSPRAY_ENABLE_TESTING=ON \
        -DOSPRAY_MODULE_MPI=ON
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    # Remove tests
    rm "${pkgdir}"/usr/bin/test_*
    rm -r "${pkgdir}"/usr/scripts
}
