# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=netCDF4
_mpi=openmpi
pkgbase=python-${_pkg,,}-${_mpi}
pkgname=("python-${_pkg,,}-${_mpi}" "python2-${_pkg,,}-${_mpi}")
pkgver=1.5.3
pkgrel=3
pkgdesc="Python/NumPy interface to the netCDF C library (${_mpi} version)"
arch=(x86_64)
url="https://unidata.github.io/${_pkg,,}-python"
license=(MIT)
makedepends=(cython python-setuptools cython2 python2-setuptools python-numpy python-cftime python-mpi4py python2-numpy python2-cftime python2-mpi4py "netcdf-${_mpi}")
source=("https://files.pythonhosted.org/packages/source/n/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('2a3ca855848f4bbf07fac366da77a681fcead18c0a8813d91d46302f562dc3be')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    PYTHONPATH="../build/lib.linux-${CARCH}-3.8" python -B ./run_all.py

    cd ../../${_pkg}-${pkgver}-py2/test
    PYTHONPATH="../build/lib.linux-${CARCH}-2.7" python2 -B ./run_all.py
}

package_python-netcdf4-openmpi() {
    depends=(python-numpy python-cftime python-mpi4py "netcdf-${_mpi}")
    conflicts=(python-netcdf4)
    provides=(python-netcdf4)

    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-netcdf4-openmpi() {
    depends=(python2-numpy python2-cftime python2-mpi4py "netcdf-${_mpi}")
    conflicts=(python2-netcdf4)
    provides=(python2-netcdf4)

    cd ${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py install --root="${pkgdir}" --skip-build --optimize=2

    for binary in "${pkgdir}"/usr/bin/* ;
        do mv ${binary}{,-py2} ;
    done
    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
