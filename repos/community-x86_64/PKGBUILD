# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.15.9
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(custom:UIUC)
depends=(glibc python-cffi python-pywayland python-xkbcommon wlroots
libwlroots.so)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
options=(debug)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha512sums=('bf3ff401b23802bcfd7767e9469c1a3f666a32b1dfe80b0ec0e35675873d8ad6e9978a8cca611de6a1f8bd8fe6604a5ccab7853c7030728757516f87841588be')
b2sums=('91a27c4cc89fe24c88a0760d7bb6d97d02b79844d25ae5b925b385af47ed850ec6824056deb58b4abd1446f544efb44eda5c469de22307ad12346a0effd33350')

build() {
  cd $_name-$pkgver
  python wlroots/ffi_build.py
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd $_name-$pkgver
  export PYTHONPATH="build/lib.linux-$CARCH-$_py_ver:$PYTHONPATH"
  pytest -v
}

package() {
  cd $_name-$pkgver
  python setup.py install --optimize=1 --root="$pkgdir"
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
