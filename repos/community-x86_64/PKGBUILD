# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: fordprefect
# Contributor: Jonathon Fernyhough <jonathon_at_manjaro_dot_org>
# Contributor: Brenton Horne

pkgname=hugo
pkgver=0.68.0
pkgrel=1
pkgdesc="Fast and Flexible Static Site Generator in Go"
arch=('x86_64')
url="https://gohugo.io/"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
optdepends=('pygmentize: syntax-highlight code snippets')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/gohugoio/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('f45fc7581908b847b70652388d458f06d21eadfd1aca5bae26711a0a4cb6f86cec2be84d1c7cf9eea4919f0f57579c2c25cfbf5524bd96ec7f5193dab9a086d8')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  go build -tags extended -ldflags "-extldflags ${LDFLAGS}"
  ./hugo gen man
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  install -Dm755 "${pkgname}" "${pkgdir}"/usr/bin/${pkgname}
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  install -Dm644 "${srcdir}"/${pkgname}-${pkgver}/man/*.1  -t "${pkgdir}"/usr/share/man/man1/
}
