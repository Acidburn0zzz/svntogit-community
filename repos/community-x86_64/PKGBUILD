# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=wishbone-utils
pkgver=0.6.9
pkgrel=1
pkgdesc='Utilities for working with a Wishbone bridge'
arch=(x86_64)
url="https://github.com/litex-hub/wishbone-utils"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::https://github.com/litex-hub/wishbone-utils/archive/v$pkgver.tar.gz)
sha512sums=('16de81d300fdbfd2a66c82eb9d87396284d948183f2d012b59f4a0bcb0e8c38cc0f5b6a8efa6fa0376543aa8f88c63e0cf617c4863145f6a46c81ad53e8238fa')

build() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 wishbone-tool/target/release/wishbone-tool -t "${pkgdir}/usr/bin/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
