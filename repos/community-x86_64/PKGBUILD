# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.15.8
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=(x86_64)
url="https://github.com/flacjacket/pywlroots"
license=(custom:UIUC)
depends=(glibc python-cffi python-pywayland python-xkbcommon wlroots
libwlroots.so)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
provides=(pywlroots)
conflicts=(pywlroots)
replaces=(pywlroots)
options=(debug)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7212632572b2e4287f0775f49a4814631c89f5f91fa0b433bcdd30b41db7dd6e4c600ef462955db2df0bdb34926725f524cb1ddfe1fa7ade9b48a06eb7084e20')
b2sums=('001bcbaec61644ac67745fdbcf34cf518d5f80b73de53ee68eacb2a138289b7468f65710c5b1a8540f4c8b2fb0c0df5fe027541c195f93aebc81a5731b8b9c05')

build() {
  cd "${_name}-$pkgver"
  python wlroots/ffi_build.py
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "${_name}-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
