# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.1.6.20170630
_commit=e2305e61afe79cf90883bf2363d47e8b3df1e5b4
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$_commit.tar.gz")
sha512sums=('f82ccc0ee618e1d428757c5a93062783921ced431547cc44cdd74b08afd2c0d6a8fc057157f34d9d7e22ce2d1d7c7c7f97a8260d9426b0e8be6b7d15aa7c265d')

prepare() {
  mv dde-file-manager-{$_commit,$pkgver}
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
