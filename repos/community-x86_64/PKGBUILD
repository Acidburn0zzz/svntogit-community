# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.13.9
pkgrel=1
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/sunng87/cargo-release'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('gcc-libs' 'openssl' 'zlib')
makedepends=('cargo')
source=(https://github.com/sunng87/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('1fd7a48526589bbd531923fcefe4af5255befda2d7c5c475da7987d4ae98ef437baa2ed2dd15f07c9478b49e3d11b9146a8281966759caa3a83b634d8429cb00')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
