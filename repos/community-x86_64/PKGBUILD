# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.0
pkgrel=2
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.zip::https://github.com/v2ray/v2ray-core/releases/download/v$pkgver/src_all.zip"
        'v2ray.service')
sha512sums=('af16a2869b3c82b52fc80d5edec7a6653ddab15a86cf3597fe09eb931bc1d37fd245a1e1b5fac61abfda11a4d81648beb2fa94f5338900197f299ada7b806589'
            'ddf312c4af1ab31a54bcc037f0e78272193aa0dca66ed57066ec8c69d44f93559e9833a4c79becdd746557d9660474762f560dbe8ba21956432f09eb2d7c6ada')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p "$GOPATH"/src

  mv *.com *.io *.org "$GOPATH"/src/
}

build() {
  go build -o v2ray v2ray.com/core/main
  go build -o v2ctl v2ray.com/ext/tools/control/main
}

check() {
  cd "$GOPATH"/src/v2ray.com/core
  go test -p 1 -tags json -v v2ray.com/core/...
}

package() {
  cd "$GOPATH"/src/v2ray.com/core
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i '/ExecStart/i Environment=v2ray_location_asset=/etc/v2ray' "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/geoip.dat release/config/geosite.dat release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 "$srcdir"/v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 "$srcdir"/v2ctl -t "$pkgdir"/usr/bin/

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
