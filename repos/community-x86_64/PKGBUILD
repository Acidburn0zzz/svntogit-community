# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: FigoDaPaura <ffigoDaPaura>
# Contributor: Andres Jimenez

pkgname=virtualgl
pkgver=2.6.1
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('libxv' 'glu' 'libjpeg-turbo' 'xcb-util-keysyms')
makedepends=('cmake' 'mesa' 'libxtst')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig})
sha512sums=('36580e55e11ba3e93945d62e865fe0b63ecd5aa1ee3889fb4d0940c68da06737fff4689a77638f5c8c57dbc9d020ba821bdec9aa2f6b2f0f144b18f1c3d8c5e5'
            'SKIP')
validpgpkeys=('ECF01671D05E2A105FF84DC46BBEFA1972FEB9CE') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  mkdir "VirtualGL-$pkgver"/build
}

build() {
  cd "VirtualGL-$pkgver"/build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/${pkgname} \
      -DVGL_FAKEXCB=1
  make
}

package() {
  cd "VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir"/usr/bin/vglxinfo
}
