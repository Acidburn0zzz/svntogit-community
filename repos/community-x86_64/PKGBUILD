# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rclone
pkgver=1.41
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="http://rclone.org/"
license=('MIT')
depends=('glibc')
makedepends=('python2' 'pandoc' 'go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ncw/rclone/archive/v$pkgver.tar.gz")
sha512sums=('efce2827a659d83c2fce74c50e6d4182dc5916ba8e47a335f2719cce5c3eb4a9622ee0f53c5923af1ed84b340b6dd7ac00bcf48dbb3de53644dc3f2298523112')

prepare() {
  cd rclone-$pkgver

  sed -i "1s/python$/&2/" bin/make_manual.py

  mkdir -p .gopath/src/github.com/ncw
  ln -sf "$PWD" .gopath/src/github.com/ncw/rclone
  export GOPATH="$PWD/.gopath"

  go get github.com/ncw/rclone
}

build() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone

  PATH="$GOPATH/bin:$PATH" make TAG=v$pkgver rclone doc
}

check() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone
  make TAG=v$pkgver test || warning "Tests failed"
}

package() {
  cd rclone-$pkgver/.gopath/src/github.com/ncw/rclone
  make TAG=v$pkgver DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  install -Dm644 rclone.1 "$pkgdir/usr/share/man/man1/rclone.1"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -t "$pkgdir/usr/share/doc/$pkgname" -m644 MANUAL.html MANUAL.txt
}
