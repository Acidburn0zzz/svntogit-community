# Maintainer: David Runge <dave@sleepmap.de>
pkgname=dpf-plugins
pkgver=1.2
pkgrel=1
pkgdesc="Collection of DPF-based plugins"
arch=('x86_64')
url="https://github.com/DISTRHO/DPF-Plugins"
license=('ISC' 'GPL2' 'GPL3' 'LGPL3' 'MIT')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('libglvnd')
makedepends=('dssi' 'ladspa' 'lv2' 'projectm')
optdepends=('jack: for standalone applications'
            'liblo: for dssi plugins'
            'projectm: for ProM plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/DISTRHO/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6790673ba44757e461f56cce360395870b127eef454f22822b7f4a1a6f15ad4ee573e84def3b2b39e969415fc1f934b3d1222a72d4a285a7dc51a9f9c9ae4cf8')

prepare() {
  mv -v "DPF-Plugins-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make BUILD_VST=true \
       BUILD_DSSI=true \
       BUILD_LV2=true \
       BUILD_JACK=true
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" \
       PREFIX=/usr \
       install
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # licenses
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 dpf/LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/dpf/"
  install -vDm 644 plugins/Kars/LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/Kars/"
  install -vDm 644 plugins/AmplitudeImposer/LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/AmplitudeImposer/"
  install -vDm 644 plugins/CycleShifter/LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/CycleShifter/"
  install -vDm 644 plugins/SoulForce/LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/SoulForce/"
}
