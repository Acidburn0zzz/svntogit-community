# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Rob Til Freedmen <rob.til.freedman@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=hydrogen
pkgver=1.0.0beta1
_beta=beta1
pkgrel=2
pkgdesc="An advanced drum machine"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
url="http://www.hydrogen-music.org/"
depends=('alsa-lib' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libarchive.so'
'libjack.so' 'liblrdf.so' 'liblo.so' 'libportaudio.so' 'libportmidi.so'
'libpulse' 'libsndfile' 'qt5-base' 'qt5-xmlpatterns')
makedepends=('cmake' 'ladspa' 'python' 'qt5-tools')
checkdepends=('cppunit')
optdepends=('rubberband: Time stretch samples')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}-music/${pkgname}/archive/${pkgver/${_beta}/-${_beta}}.tar.gz"
        "${pkgname}-1.0.0beta1-python2.patch::https://github.com/hydrogen-music/hydrogen/commit/f57b76040cd5ada2fa494a73ef1f9d4fcec1afcd.patch")
sha512sums=('73963a9f400a4d9f6faca41f5f9af2c4b00ebefb16f394a78fe81f7aacb23acee49633803a484e91705da01cb59b4699133f618fac34f6caa7734bcc67bf4518'
            '9343531248b741bdb99e3db3d8f5ea76f8747c13314fcebb0fd934c6d1e33928f35da374909fd84b896f55d78c60caba662b573f97abbd762dfd90f41a2d10c0')

prepare() {
  mv -v "${pkgname}-${pkgver/${_beta}/-${_beta}}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # make i18n script python3 compatible:
  # https://github.com/hydrogen-music/hydrogen/issues/730
  patch -Np1 -i "../${pkgname}-1.0.0beta1-python2.patch"
  # fix desktop file icon name
  # https://github.com/hydrogen-music/hydrogen/issues/558
  sed -e "s/h2-icon/${pkgname}/" -i "linux/${pkgname}.desktop"
  # fix man page location (seemingly fixed on HEAD)
  sed -e 's|/man/|/share/man/|' -i CMakeLists.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWANT_DEBUG=OFF \
        -DWANT_CPPUNIT=ON \
        -DWANT_LRDF=ON \
        -DWANT_PORTAUDIO=ON \
        -DWANT_PORTMIDI=ON \
        -DWANT_PULSEAUDIO=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
  # update translations
  (
    cd data/i18n
    ./updateTranslations.sh
  )
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build

  # install desktop file icon to correct location
  # https://github.com/hydrogen-music/hydrogen/issues/558
  install -vDm 644 "${pkgdir}/usr/share/${pkgname}/data/img/gray/h2-icon.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  rm -rv "${pkgdir}/usr/share/pixmaps"
  rm -rv "${pkgdir}/usr/share/${pkgname}/data/doc"

  # docs
  install -vDm 644 {ChangeLog,DEVELOPERS,INSTALL.txt,README.txt} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"

  # clean up data dir
  # https://github.com/hydrogen-music/hydrogen/issues/559
  rm -v "$pkgdir/usr/share/${pkgname}/data/i18n/"*.{ts,py,sh}
}

# vim:set ts=2 sw=2 et:
