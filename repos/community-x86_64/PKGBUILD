# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=entr
pkgver=4.2
pkgrel=1
pkgdesc='Run arbitrary commands when files change'
arch=('x86_64')
url='http://entrproject.org'
license=('MIT')
makedepends=('mercurial')
source=("$pkgname::hg+https://bitbucket.org/eradman/entr#tag=$pkgname-$pkgver")
sha512sums=('SKIP')

build() {
  cd "$srcdir"/$pkgname

  export PREFIX='/usr'

  ./configure

  make
}

check() {
  cd "$srcdir"/$pkgname

  make test
}

package() {
  cd "$srcdir"/$pkgname

  make DESTDIR="$pkgdir" install

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
