# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSEQuencer
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BSEQuencer"
pkgname=bsequencer
pkgver=0.4
pkgrel=2
pkgdesc="Multi channel MIDI step sequencer LV2 plugin with a variable matrix"
arch=('x86_64')
url="https://github.com/sjaehn/BSEQuencer"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint' 'sord')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('7630cbbcc89e1675ab13897407bc8e95193b74ca64b66c807be00aabfb38f187eee6aacfae5598bb249c159b71c002ad8c85ce5fbb4a9c4a0edefa6fb0665fd4')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  sord_validate "${_name}.lv2/"*.ttl || echo "Known to fail https://github.com/sjaehn/BSEQuencer/issues/12"
  LV2_PATH="${PWD}" lv2lint "${_plugin_uri}" || echo "Known to fail https://github.com/sjaehn/BSEQuencer/issues/12"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
