# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=2.10.0
pkgrel=2
pkgdesc="A tool for examining the internals of a Qt application and to some extent also manipulate it"
arch=(x86_64)
url="https://www.kdab.com/gammaray/"
license=(GPL)
depends=(syntax-highlighting qt5-tools qt5-svg qt5-3d)
makedepends=(cmake kcoreaddons qt5-script qt5-wayland qt5-webengine qt5-scxml qt5-location qt5-connectivity doxygen)
optdepends=('qt5-wayland: Wayland compositor inspector plugin'
            'qt5-webengine: web inspector plugin'
            'qt5-scxml: state machine viewer plugin'
            'qt5-connectivity: bluetooth plugin'
            'qt5-script: script engine debugger plugin'
            'kcoreaddons: KJob tracker plugin')
source=(https://github.com/KDAB/GammaRay/releases/download/v$pkgver/gammaray-$pkgver.tar.gz{,.asc}
        gammaray-qt-5.13a.patch::"https://github.com/KDAB/GammaRay/commit/621dbd9.patch"
        gammaray-qt-5.13b.patch::"https://github.com/KDAB/GammaRay/commit/3cb4f283.patch")
sha256sums=('0554e43067c2eba3517cf746a921385cf15675db79f879e1c3a7851c4951ffbe'
            'SKIP'
            'b2922a5207821402f013588dd89a17625704964518f4bdc6a3a622425aed50b2'
            '4be04460e980f1308d40d9621af3171e688d7420e69c9d59cc5743562d4188cd')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
  mkdir -p build

# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt/plugins/gammaray|' -i $pkgname-$pkgver/CMakeLists.txt

  cd $pkgname-$pkgver
  patch -p1 -i ../gammaray-qt-5.13a.patch # Fix build with Qt 5.13
  patch -p1 -i ../gammaray-qt-5.13b.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/gammaray
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
