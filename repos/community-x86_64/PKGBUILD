# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.6.0
pkgrel=1
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11'
         'qt5-declarative' 'qt5-quickcontrols2' 'qt5-svg' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python' 'qbs')
optdepends=('python: Python plugin'
            'qt5-imageformats: Support for additional image formats (including WebP)')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('73c8fbd8af6c47632317fe33207cc01fcd5cab357372f6322406d6315c1d8d66363aa0849b72a78c2b2fe952ee70e6c98efc5ab8f99ea98bf1c319d346594eae')

build() {
  cd $pkgname-$pkgver
  qbs qbs.installPrefix:"/usr" projects.Tiled.useRPaths:false projects.Tiled.installHeaders:true projects.Tiled.enableZstd:true
}

package() {
  cd $pkgname-$pkgver
  qbs install --install-root "$pkgdir"
}
