# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=polyphone
pkgver=2.1.3
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL3')
groups=('pro-audio')
depends=('desktop-file-utils' 'flac' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libjack.so' 'libogg' 'libqcustomplot.so' 'libvorbis.so' 'libvorbisenc.so'
'libvorbisfile.so' 'openssl' 'qt5-base' 'qt5-svg' 'portaudio' 'rtmidi' 'stk')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('a3c8eb83dcb1652575e6cecf2c8041957ccbaeb31c68345b3a1fd70583d87af21995c4af7a484de74ee3eb34f56897ff1eeefb0f4921d59ba7a3c099cb6cc445')

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro"
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "resources/logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -vDm 644 "resources/${pkgname}.png" \
    -t "${pkgdir}/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 "contrib/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "contrib/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "contrib/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
