# Maintainer: David Runge <dave@sleepmap.de>
pkgname=gmsynth.lv2
pkgver=0.3.3
pkgrel=1
pkgdesc="General MIDI LV2 Synth"
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-gmsynth"
license=('GPL2')
groups=('pro-audio' 'lv2-plugins')
depends=('glib2')
makedepends=('lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c3f74b799b902aa8441082f2c72346a766af8e1fb1a864a06d514529e2e8a09584924af4963087227bc9677344d7829af225a46a124dae3cc20bfa95acd07f2a')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
