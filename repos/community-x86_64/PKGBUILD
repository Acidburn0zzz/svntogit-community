# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=traefik
pkgver=2.1.1
pkgrel=1
pkgdesc="Simple tool for making locally-trusted development certificates"
arch=('x86_64')
url="https://containo.us/traefik/"
license=('MIT')
makedepends=('go-pie' 'git' 'go-bindata')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/traefik/releases/download/v${pkgver}/traefik-v${pkgver}.src.tar.gz")
sha512sums=('4ee2aa281030e93e70126c3cb143354f42d00df86895470d6e0536a774d32cebb55d20c1efd28985d8010ffabcad78e1837e849109c85a1f45959da95ea5e139')

build() {
  go generate
  cd cmd/traefik
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}"
}

package() {
  install -Dm755 cmd/traefik/"${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 contrib/systemd/traefik.service "${pkgdir}/usr/lib/systemd/system/traefik.service"
}
