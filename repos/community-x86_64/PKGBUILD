# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Joost Bremmer <contact at madeofmagicandwires dot online>
# Contributor: Eliezio Oliveira <eliezio@pm.me>
# Contributor: Matthew Gamble <internet@matthewgamble.net>

pkgname=sad
pkgver=0.4.8
pkgrel=4
pkgdesc="Space Age seD"
arch=('x86_64')
url="https://github.com/ms-jpq/sad"
license=('MIT')
makedepends=('rust')
optdepends=(
  'fzf: for tui usage'
  'diff-so-fancy: for diff colourizer'
  'git-delta: for diff colourizer'
)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz"
  "https://raw.githubusercontent.com/ms-jpq/sad/c3a204d095e6ef9e58d4c36bb8621d96ec166ada/Cargo.lock")
b2sums=('95591e5bce26dfa9507ced99b0197e8d103a9b0fbda6b712ec0296188563092d9dfcd0744462c53111f6a3ca1ab7aa2834b621614edd3731f2d01db557fbb6f7'
        '15502d79b357b2905f49372c59cc60e91a253af23fee1e381774184f385a4fe60428fd3595c415b30a706c3036130d449a514701e4ecdde6c84e7511bf5d7f00')

prepare() {
  cd "$pkgname-$pkgver"

  # v0.4.8 lockfile is currently out of sync
  # backported from https://github.com/ms-jpq/sad/issues/85
  cp -f "$srcdir/Cargo.lock" Cargo.lock

  # download dependencies
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --offline --locked --all-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --locked
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: set ts=2 sts=2 sw=2 et :
