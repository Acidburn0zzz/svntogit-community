# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kustomize
pkgver=3.5.4
pkgrel=1
_kustomize_commit=3af514fa9f85430f0c1557c4a0291e62112ab026
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=('x86_64')
url="https://github.com/kubernetes-sigs/kustomize"
license=('Apache')
makedepends=('go-pie' 'git')
depends=('glibc')
source=("git+https://github.com/kubernetes-sigs/kustomize#commit=${_kustomize_commit}")
sha512sums=('SKIP')

build(){
  cd "${pkgname}/${pkgname}"
  go build  \
  -trimpath \
  -ldflags "-extldflags ${LDFLAGS}" .
  chmod +x ./kustomize
}

check(){
  cd "${pkgname}/${pkgname}"
  go test ./...
}

package(){
  cd "${pkgname}/${pkgname}"
  install -Dsm755 ./kustomize "${pkgdir}/usr/bin/kustomize"
}
