# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=rust
pkgver=0.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='A safe, concurrent, practical language'
url='http://www.rust-lang.org/'
license=('MIT' 'Apache')
depends=(gcc shared-mime-info)
makedepends=(libffi perl python2 curl chrpath emacs-nox)
optdepends=('haskell-pandoc: documentation generator (rustdoc)')
source=("http://static.rust-lang.org/dist/rust-${pkgver}.tar.gz")
sha256sums=('0b88b8a4489382e0a69214eaab88e2e7c316ec33c164af0d3b53630b17590df0')
install=rust.install

build() {
  cd rust-$pkgver

  ./configure --prefix=/usr --disable-docs

  # avoid python makedepend (force fallback to python2)
  sed -i 's/^PYTHONVERSION.*/PYTHONVERSION := 3/' src/llvm/Makefile.rules

  make
}

#check() {
  #cd rust-$pkgver
  #make check
#}

package() {
  cd rust-$pkgver
  make DESTDIR="$pkgdir" install

  mkdir -p "$pkgdir/usr/share/vim" "$pkgdir/usr/share/licenses/$pkgname"

  cp -a src/etc/vim "$pkgdir/usr/share/vim/vimfiles"
  find "$pkgdir/usr/share/vim" -type f -exec chmod 644 {} +
  find "$pkgdir/usr/share/vim" -type d -exec chmod 755 {} +

  install -Dm644 src/etc/zsh/_rust "$pkgdir/usr/share/zsh/site-functions/_rust"
  install -Dm644 src/etc/kate/rust.xml "$pkgdir/usr/share/apps/katepart/syntax/rust.xml"
  install -Dm644 src/etc/gedit/share/mime/packages/rust.xml \
    "$pkgdir/usr/share/mime/packages/rust.xml"
  install -Dm644 src/etc/gedit/share/gtksourceview-3.0/language-specs/rust.lang \
    "$pkgdir/usr/share/gtksourceview-3.0/language-specs/rust.lang"

  install -m644 LICENSE-APACHE "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname"

  cd src/etc/emacs
  make
  mkdir -p "$pkgdir/usr/share/emacs/site-lisp/"
  install -Dm644 cm-mode.el{,c} rust-mode.el{,c} "$pkgdir/usr/share/emacs/site-lisp/"

  cd "$pkgdir/usr"

  # https://github.com/mozilla/rust/issues/5219
  chrpath -d bin/* lib/*.so lib/rustc/*/lib/*.so
}
