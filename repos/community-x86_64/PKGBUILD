# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.6.9.24
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
         deepin-album-qt5.15.patch)
sha512sums=('aeb0d62207cd0dda8e538719fe3d637b740e3e615c31d9b9cbf2e28be68ffa6ba8f36b8b9463463d7f848b8bc953427dda5b64ea5753f8df29dab8ec8f8fcc0a'
            '52c48e5d2efe603cfa0d9ab9db9619370dfcf7fdeab8fb52d94c5481ad98fe3ca5bfadf240e3acc5542dbe5fa1be766ad7189316fc9db3632b76fbfd6ed9c640')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../deepin-album-qt5.15.patch # Fix build with Qt 5.15
  sed -i '1i #include <QPainterPath>' widgets/dlmenuarrow.h module/view/contents/imageinfowidget.cpp widgets/dialogs/dfmdarrowlineexpand.cpp
}

build() {
  cd deepin-album-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-album-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
