# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.3
pkgrel=3
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('libmng' 'librsvg' 'curl' 'lcms' 'desktop-file-utils' 'libnsbmp>=0.1.1' 'libnsgif>=0.1.1' 'libcss>=0.5.0' 'libdom>=0.1.2' 'gtk2' 'libnsutils' 'libutf8proc>1.3.1-2') # 'libwebp' 'gtk3'
makedepends=('re2c' 'netsurf-buildsystem' 'perl-html-parser' 'nsgenbind' 'inetutils' 'libutf8proc>1.3.1-2' 'git') # 'libwebp'
install="$pkgname.install"
#source=("git://git.netsurf-browser.org/netsurf.git#commit=3cc80"
source=("git://git.netsurf-browser.org/netsurf.git#tag=release/$pkgver"
        'netsurf.sh')
sha256sums=('SKIP'
            '70310682d1612457d7bb3096549110b6ec127f50e97853259fada6be0c52924b')

prepare() {
  cd "$pkgname"

  sed 's:_BSD_SOURCE:_DEFAULT_SOURCE:' -i gtk/Makefile.target
  sed 's:netsurf-gtk:netsurf:' -i gtk/res/netsurf-gtk.desktop
  sed 's:libutf8proc/::' -i utils/idna.c
  sed 's:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' -i utils/idna.c
  sed 's:utf8proc_normalise:utf8proc_reencode:' -i utils/idna.c
  # There are several errors. Asking netsurf upstream to look at it.
}

build() {
  make -C "$pkgname" \
    PREFIX=/usr \
    TARGET=gtk \
    NETSURF_USE_WEBP=NO \
    NETSURF_USE_VIDEO=NO \
    NETSURF_USE_JS=NO \
    NETSURF_USE_MOZJS=NO # Javascript is disabled, for now. See FS#41582
}

package() {
  cd "$pkgname"

  make install PREFIX=/usr DESTDIR="$pkgdir"

  # Script and executable
  mv "$pkgdir/usr/bin/$pkgname" "$pkgdir/usr/bin/$pkgname.elf"
  install -Dm755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop shortcut
  install -Dm644 "gtk/res/$pkgname.xpm" \
    "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 "gtk/res/$pkgname-gtk.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # License
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
