# Maintainer: Robin Broda <robin@broda.me>
# Contributor: Michael Lass <bevan@bi-co.net>
# Contributor: Justin Dray <justin@dray.be>
# Contributor: Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools
pkgver=0.8.3
pkgrel=1
pkgdesc='Multipath tools for Linux (including kpartx)'
arch=('x86_64')
url='http://christophe.varoqui.free.fr/'
license=('GPL2')
depends=('libaio' 'device-mapper' 'json-c' 'liburcu' 'systemd-libs')
makedepends=('git')
source=("multipath-tools::git+https://git.opensvc.com/multipath-tools/.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd "${pkgname}"

  # Fix systemd version detection in Makefile
  sed -i 's/sed -n .*$/head -n1 | cut -d" " -f2\)/g' Makefile.inc

  # Fix bindir in Makefile
  sed -i 's|$(exec_prefix)/sbin|$(exec_prefix)/bin|g' Makefile.inc
}

build() {
  cd "${pkgname}"

  make
}

package() {
  cd "${pkgname}"

  make DESTDIR="${pkgdir}" \
       exec_prefix="/usr" \
       LIB="/usr/lib" \
       install
}
