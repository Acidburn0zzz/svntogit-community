# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hunter
pkgname=('python-hunter' 'python2-hunter')
pkgver=3.0.1
pkgrel=1
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
makedepends=('python-setuptools' 'python2-setuptools' 'python-manhole' 'python2-manhole'
             'python-colorama' 'python2-colorama' 'cython' 'cython2')
checkdepends=('python-pytest-benchmark' 'python2-pytest-benchmark' 'python-virtualenv'
              'python2-virtualenv' 'python-pytest-runner' 'python2-pytest-runner' 'gdb'
              'python-process-tests' 'python2-process-tests' 'python-aspectlib' 'python2-aspectlib')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('865ab939a4bda9dd0f86bf3f4bd403be0b2a1c1128965e6ea2ab219aa3ddbc6508b5e3611f44a5f37ada28f3b28057784762ba2117e004fe5bf74ca1f8f73596')

prepare() {
  rm python-hunter-$pkgver/src/hunter/*.c
  cp -a python-hunter-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build

  cd "$srcdir"/python-hunter-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  ) || warning "Tests failed"

  cd "$srcdir"/python-hunter-$pkgver-py2
  virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py pytest
  ) || warning "Tests failed"
}

package_python-hunter() {
  depends=('python-manhole' 'python-colorama')
  optdepends=('gdb: for remote support')

  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hunter() {
  depends=('python2-manhole' 'python2-colorama')
  optdepends=('gdb: for remote support')

  cd python-hunter-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/hunter-trace{,2}
}
