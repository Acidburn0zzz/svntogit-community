# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.0.1
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('cinnamon-desktop' 'ibus' 'libcanberra-pulse' 'librsvg' 'nss' 'pulseaudio-alsa' 'upower')
makedepends=('intltool' 'docbook-xsl' 'gnome-common')
options=('!emptydirs' '!libtool')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz"
        keyboard.patch
        automount-plugin.patch)
sha256sums=('f03f5ea46bec11a6a0ed02b232dab8cb99a84fd8a9b52a76a454a7ba74dc1168'
            '3c05b978cb414c8fc1530f211bab58627a64ac9c156630c9d486f5926a85109c'
            '0a6dfbea6f28e5107212a8e8091ded6a3b5b34b1710c11c2f04c4d02846f9398')

prepare() {
  cd $pkgname-$pkgver

  # Fix crasher
  patch -Np1 -i ../keyboard.patch

  # Turn automount helper into a plugin
  patch -Np1 -i ../automount-plugin.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static --enable-systemd

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
