# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=4.4.1
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gtk3' 'libxkbfile' 'libpulse')
makedepends=('meson' 'samurai' 'gobject-introspection' 'intltool')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "config.patch")
sha256sums=('b32fb974e49048e091e98a24e740b01e4e6f97a7947b53dd65f4552dd61a34cf'
            '45f403e6fe4ec701a0f3d16e990bb36598dd4a58fac5a374f85ccecdf74dd98a')
b2sums=('8bf770e1f2af67c32381c5ba23e88f259d9d6c6a0b722e61202922da8c8c5374bd297b3866846f28924d6d527e6122432682df98d4b3979eeaf026674b6e5027'
        '40efc9c24e1011239f68734c6c0a9459dab95d4330d8c712e674c7d1e99dd01b60833667b52d0ad8c58b31469eee349757ed4c44e4589b6fbfd7236c1499d28b')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    # Adjust configuration
    patch -Np1 -i ../config.patch
}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr --buildtype=plain ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
