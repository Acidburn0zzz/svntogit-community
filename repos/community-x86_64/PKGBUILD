# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=giac
pkgname=(xcas libgiac)
_pkgver=1.7.0-3
pkgver=${_pkgver//-/.}
pkgrel=2
pkgdesc="A free computer algebra system"
arch=(x86_64)
url="http://www-fourier.ujf-grenoble.fr/~parisse/giac.html"
license=(GPL3)
makedepends=(python mpfi gsl pari ntl lapack libpng libjpeg fltk texlive-core texlive-science hevea glpk libao gmp-ecm nauty libsamplerate)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgbase}_${_pkgver}.tar.gz
        giac-test-pari-2.11.patch
        giac-evalb.patch)
sha256sums=('181fec9db622571bb568d5c254aeaab29fe5c6310a4f3af5a207e7392f9171f0'
            'c5f091986f0a0807fc1ae5a3b7454132816e9bc3ed64411f0a7bdf90a8c8e624'
            '1c20d955192183f2b248c327b2270369dea2961a80b5cdca09083228920c7a07')

prepare() {
  cd $pkgbase-${pkgver%.*}
  patch -p1 < ../giac-test-pari-2.11.patch # Fix tests with pari 2.11
  patch -p1 < ../giac-evalb.patch # Fix evalb
}

build() {
  cd $pkgbase-${pkgver%.*}

  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgbase-${pkgver%.*}

  make check
}

package_xcas() {
  depends=(libgiac fltk)
  optdepends=('perl: for pgiac')
  conflicts=(giac)
  provides=(giac)
  replaces=(giac)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{lib,include}
}

package_libgiac() {
  pkgdesc="Giac shared library"
  depends=(mpfi gsl pari ntl lapack libpng curl glpk libao gmp-ecm libsamplerate)

  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{bin,share}
}
