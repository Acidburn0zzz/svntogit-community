# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ales Katona <almindor@gmail.com>

pkgname=etherwall
pkgver=2.2.1
_ewnodecommit=9d9fbbe18a44a36456b90f51d14c241228b53c30
_trezorcommit=4ac8e8cdffb04f83ba9f71c27513f8dced1aa3b0
pkgrel=1
pkgdesc="Ethereum GUI Wallet (QT5)"
arch=(i686 x86_64)
url="http://etherwall.com"
license=('GPL3')
depends=('qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols' 'qt5-websockets' 'geth' 'protobuf' 'hidapi' 'libsystemd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/almindor/${pkgname}/archive/v$pkgver.tar.gz"
        "ew-node-$_ewnodecommit.tar.gz::https://github.com/almindor/ew-node/archive/$_ewnodecommit.tar.gz"
        "trezor-common-$_trezorcommit.tar.gz::https://github.com/trezor/trezor-common/archive/$_trezorcommit.tar.gz"
        etherwall.desktop)
sha512sums=('cc0c1b707a5b284db7a4ecd40a80811a8fd85ae0c5786e20e57d4693e529f7eec0d05b55bbf807dff1d24341f0b5992db26ee257b337ff7bf5b06372dfdd9c22'
            '34ef9911961a64d2f56eeac47a10205189522c02391ea0d0febba7cccf62ba056a4777cbf221b9f7b621a3970368742b1ae4f57472ca3c64b24a1f89e6f8f73b'
            'a655df160b786934d50d7041de4b9b9b2d8d99107313173b8bf61f8f1a529fee1e328d0941454ab95d891ae67875a95b11a33aea469c24527ed1a550c6f7a6f3'
            '82ecee1f1d7a6fe7014b928e3ab79ed507c3c5795ac3e832e0be1aca8fc5659212c5a4323d5dec4571b9e9255c5c46615bd9865dc03eba85d8be9b969b534d57')

build() {
  cd "etherwall-$pkgver"
  rmdir "src/ew-node"
  ln -s "../../ew-node-$_ewnodecommit" "src/ew-node"

  SRCDIR="../trezor-common-$_trezorcommit/protob" ./generate_protobuf.sh
  qmake -config release && make
}

package() {
  mkdir -p "$pkgdir/usr/bin"
  mkdir -p "$pkgdir/usr/share/pixmaps"
  mkdir -p "$pkgdir/usr/share/applications"

  cd "etherwall-$pkgver"
  cp "$srcdir"/etherwall.desktop "$pkgdir"/usr/share/applications
  cp "./Etherwall" "$pkgdir"/usr/bin/etherwall
  cp "./qml/images/icon.png" "$pkgdir"/usr/share/pixmaps/etherwall.png
}
