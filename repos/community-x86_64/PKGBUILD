# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=mpfi
pkgver=1.5.4
pkgrel=1
pkgdesc="C library for interval arithmetic"
arch=(x86_64)
url="http://perso.ens-lyon.fr/nathalie.revol/software.html"
license=(GPL)
depends=(mpfr)
source=("https://gforge.inria.fr/frs/download.php/latestfile/181/mpfi-$pkgver.tar.gz" div_ext.c)
sha256sums=('76b01b9eed7150a44b3f6aea24ad4ac78b0f2078253be87ff6d1903d02dfa434'
            'aa280208c1168fbdfdac5cbb3fd8421904bc80d6a323de89a4b6b48a66446b01')

prepare() {
  cd $pkgname-$pkgver/mpfi
  ./autogen.sh
  cp "$srcdir"/div_ext.c src # Missing source file
}

build() {
  cd $pkgname-$pkgver/mpfi
  ./configure --prefix=/usr --enable-shared 
  make
}

package() {
  cd $pkgname-$pkgver/mpfi
  make DESTDIR="$pkgdir" install
}
