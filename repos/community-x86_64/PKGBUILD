# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=(gmic gimp-plugin-gmic)
pkgver=1.4.7.2
pkgrel=2
pkgdesc="GMIC image procession framework"
arch=('i686' 'x86_64')
license=('custom:CeCILL')
url="http://gmic.sourceforge.net"
makedepends=('gimp' 'fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
options=('docs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz"
	"build-fix.patch")
md5sums=('052e5e346f30608b0124d36c71dd03d2'
         'b54ace496079ea66163aa521ead1f8c7')

build() {
  cd "$srcdir/gmic-$pkgver"

  patch -Np1 -i "$srcdir/build-fix.patch"

  # Enable lapack optimizations
  CFLAGS+=" -Dcimg_use_lapack"
  LDFLAGS+=" -llapack"

  make -C src all
}

package_gmic() {
  depends=('fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
  replaces=('greycstoration')

  cd "$srcdir/gmic-$pkgver"
  make -C src install DESTDIR="$pkgdir" USR=/usr
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gmic/LICENSE"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the GMIC image procession framework"
  depends=('gimp' 'fftw' 'lapack')
  replaces=('gimp-plugin-greycstoration' 'gimp-plugin-gmic4gimp')

  cd "$srcdir/gmic-$pkgver"
  install -Dm755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gimp-plugin-gmic/LICENSE"
}
