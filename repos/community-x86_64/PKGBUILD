# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.9.2
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('i686' 'x86_64')
url="https://filezilla-project.org/"
license=('GPL')
depends=('gcc-libs')
checkdepends=('cppunit')
source=("http://download.filezilla-project.org/libfilezilla/libfilezilla-$pkgver.tar.bz2"
cppunit-pkgconfig.patch random_bytes.patch)

prepare() {
  cd "${srcdir}/$pkgname-$pkgver"

  patch -p1 -i "$srcdir/cppunit-pkgconfig.patch"
  patch -p1 -i "$srcdir/random_bytes.patch"
  autoreconf
}

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
md5sums=('d52a0e7229efb3c9fafc8a2b649d665d'
         '0e3c4b6bfe11f7f9dddbc033c23c8c12'
         '19beb164bda0f853678629b24865bdce')
