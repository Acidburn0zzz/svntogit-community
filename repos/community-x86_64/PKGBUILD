# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=gopass
pkgver=1.6.5
pkgrel=1
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/justwatchcom/gopass"
license=('MIT')
depends=('gnupg')
makedepends=('go-pie')
optdepends=('xdotool: for typing passwords selected by dmenu'
	    'xsel: clipboard support'
	    'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/justwatchcom/gopass/archive/v${pkgver}.tar.gz")
sha256sums=('bac8a09b44321a8bda8f4b2b98eeb0b547f845599c81119a93f8d4c0d9b6c529')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/justwatchcom
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/justwatchcom/gopass
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  make build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 bash.completion "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  install -Dm644 zsh.completion "${pkgdir}/usr/share/bash/completions/${pkgname}"
  install -Dm644 fish.completion "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}
