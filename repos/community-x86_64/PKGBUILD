# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-subtitles
pkgver=1.4.2
pkgrel=2
pkgdesc="Video subtitle editor for GNOME"
arch=('x86_64')
url="http://www.gnomesubtitles.org/"
license=('GPL')
depends=('gst-plugins-base' 'gtk-sharp-3' 'gtkspell3')
makedepends=('intltool' 'itstool')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('12847186070c8edbfc17d4aa1118a2ad9d8a9d40b806adf5b7041b71e4682641')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libenchant.so.1/libenchant-2.so.2/' src/GnomeSubtitles/Execution/gnome-subtitles.exe.config

  # Install icons under icons/hicolor/scalable/apps instead of pixmaps
  # https://gitlab.gnome.org/GNOME/gnome-subtitles/merge_requests/3
  sed -i 's|$(datadir)/pixmaps|$(datadir)/icons/hicolor/scalable/apps|' data/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
