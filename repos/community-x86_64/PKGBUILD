# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Wei√üschuh <thomas t-8ch de>
# Contributor: Nathan Owe <ndowens.aur at gmail dot com>

pkgname=batctl
pkgver=2020.2
_advver=$pkgver
pkgrel=1
pkgdesc='B.A.T.M.A.N. advanced control and management tool'
arch=('x86_64')
url='http://www.open-mesh.net/'
license=('GPL')
depends=('libnl')
source=("http://downloads.open-mesh.net/batman/releases/batman-adv-$_advver/batctl-$pkgver.tar.gz"{,.asc})
sha512sums=('4990eece371320107cd522f5dc6ff4ecd2867d8b77ddb8a09b8c8fc81c6cf2a88df6635f026b16a2c59fc05532a82ecafae206fb1581ac7e1f863043eb3afa93'
            'SKIP')
validpgpkeys=('2DE9541A85CC87D5D9836D5E0C8A47A2ABD72DF9')  # Simon Wunderlich

build(){
  cd batctl-$pkgver
  make
}

package() {
  cd batctl-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr SBINDIR=/usr/bin install
}
