# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-session-manager
pkgver=3.0.0
pkgrel=1
pkgdesc="Session manager of the UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-session-manager"
groups=('ukui')
depends=('kidletime' 'peony' 'qt5-multimedia' 'qt5-ukui-platformtheme' 'ukui-biometric-auth'
         'ukui-panel' 'ukui-settings-daemon' 'ukui-screensaver' 'ukwm' 'xtrans')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-session-manager/archive/v$pkgver.tar.gz")
sha512sums=('9873dd02a243f3fd7ef1b67b73625c4e183cc2fb8edd7acd2d00c33f15d28ec7452eaae3b27f388597c982a1ab6640fd818afe05642f3ef19918e02feccc8cb7')

prepare() {
  mkdir -p build
  sed -i '/#include <QString>/i #include <QFile>' $pkgname-$pkgver/tools/main.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
