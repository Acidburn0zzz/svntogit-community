# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=atom
pkgname=python-atom
pkgver=0.4.3
pkgrel=1
pkgdesc='Memory efficient Python objects'
arch=(x86_64)
url='https://github.com/nucleic/atom'
license=('BSD' 'custom')
depends=(python python-setuptools)
checkdepends=(python-pytest)
source=($pkgname-$pkgver.tar.gz::https://github.com/nucleic/atom/archive/$pkgver.tar.gz)
sha512sums=('7f38db91c62629fd1de16b981ef6bc77a691ac14aabbd46700e074beb8a60cc393ec62006b23405ee5aeca265ec1e793537d96de1f92ad5ab58e0ab8603e2990')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  cd ${_name}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}" pytest
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
