# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.3.2
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('i686' 'x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
changelog=$pkgname.changelog
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('695465b2aeb22dbb66ed1be0b03e743b5f23b854ffc9a085bacff9174a03660c')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
