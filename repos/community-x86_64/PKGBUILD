# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-terra-pinyin
pkgver=0.0.0.20201018
_commit=5a445e2953c4b8149daac9d4625782fd5f80d9f2
pkgrel=1
pkgdesc="Terra pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-terra-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-terra-pinyin/archive/$_commit/rime-terra-pinyin-$_commit.tar.gz")
sha512sums=('771acf1ba3271c7c006163437e30262da355ab13f13262854a7b5737075a7a73884c657c2fa82c445b05b9ebeadaf8def227c27ea913ed8a325e675e2183b95f')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
