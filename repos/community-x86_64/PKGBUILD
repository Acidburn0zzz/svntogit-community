# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.2.2
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'deepin-tool-kit' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::http://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz")
sha512sums=('890385dca9e5178743646cdc8d489c4952a29715a21e01bec8f9758a8e77dc8d7b2e2d1446e1884b62fbf32b8d4ec748d1e8da950126110b77dbcad0759b119d')

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
