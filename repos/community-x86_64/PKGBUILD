# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=cosign
pkgver=0.1.0
pkgrel=2
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('d3af55eb3d68b41dbefbbd00f18c4bed8a89d7839ff6c6f0da89c9b1a1214d3b')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

test() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/cosign "${pkgdir}/usr/bin/${pkgname}"
  install -Dm755 build/copasetic "${pkgdir}/usr/bin/copasetic"
}
