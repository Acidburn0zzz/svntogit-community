# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.21
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'lv2-host' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt6-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application'
            'qt6-wayland: for native wayland support')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0b8ba858dbe133312489e3c3de2a01d84e3ee16ce0161f75d8bc18bf7d03bef5b59ee685d4c74be6c32fae4172edb5c3f9547bb04d05b3df4e52f48394b93739')
b2sums=('1430b1f65cedc970e17642c3ee75ac8d2162f9e386fc7ed50186a7a59c5cdb8d136c390e77f6b5ea95a8ac546b0aac1f318de2cb22651a7a10db3a64580941d1')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
