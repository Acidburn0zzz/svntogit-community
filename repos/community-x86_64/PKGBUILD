# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.6.13
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz")
sha512sums=('ab867bc7f3f53d603b12e16bc778c6d7a49368503cc74deaef9c3f4793553cef3c9af5cff7401fe3ead8e4e5277506d5f4bcee0c2b89006234acdfc6fd8a2235')

prepare() {
  cd deepin-compressor-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' deepin-compressor/source/src/openwithdialog/openwithdialog.cpp deepin-compressor/source/src/logviewheaderview.cpp
}

build() {
  cd deepin-compressor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-compressor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
