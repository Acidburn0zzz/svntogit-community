# Maintainer: Morten Linderud <foxboron@archlinux.org>
pkgname=gopass
pkgver=1.6.11
pkgrel=1
pkgdesc="The slightly more awesome standard unix password manager for teams."
arch=('x86_64')
url="https://github.com/justwatchcom/gopass"
license=('MIT')
depends=('gnupg' 'git')
options=(!strip debug)
makedepends=('go-pie')
optdepends=('xdotool: for typing passwords selected by dmenu'
	    'xsel: clipboard support'
	    'xclip: clipboard support')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/justwatchcom/gopass/archive/v${pkgver}.tar.gz")
sha256sums=('2199532b6a5f51960e6b3412ea196efb5e2d187b7cb436ecbbcff1a0ec4f9fc1')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/justwatchcom
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/justwatchcom/gopass
  cd "${pkgname}-${pkgver}"
  sed -i -e "s/-extldflags '-static'//g" Makefile
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  make GO="go" build
  make completion
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
