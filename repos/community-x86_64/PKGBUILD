# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.2.0rc1
_realpkgver=1.2.0-RC1
pkgrel=1
pkgdesc='An engine for running Transport Tycoon Deluxe.'
arch=('i686' 'x86_64')
url='http://www.openttd.org'
license=('GPL')
depends=('libpng' 'sdl' 'icu' 'fontconfig' 'lzo2' 'hicolor-icon-theme' 'desktop-file-utils' 'xz')
install=openttd.install
optdepends=('openttd-opengfx: free graphics' 
            'openttd-opensfx: free soundset')
source=("http://binaries.openttd.org/releases/${_realpkgver}/${pkgname}-${_realpkgver}-source.tar.xz")
md5sums=('e2b32c0623a884e43e6d8b29cbb1febf')

build() {
  cd ${pkgname}-${_realpkgver} 

 ./configure \
    --prefix-dir=/usr \
    --binary-name=${pkgname} \
    --binary-dir=bin \
    --data-dir=share/${pkgname} \
    --install-dir=${pkgdir} \
    --doc-dir=share/doc/${pkgname} \
    --menu-name="OpenTTD" \
    --personal-dir=.${pkgname}    

  make
}

package() {
  cd ${pkgname}-${_realpkgver} 

  make install
}
