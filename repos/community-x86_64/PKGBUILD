# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Daniel Plaza <daniel.plaza.espi@gmail.com>

pkgname=pcsclite
pkgver=1.8.2
pkgrel=1
pkgdesc="PC/SC Architecture smartcard middleware library"
arch=('i686' 'x86_64')
url="https://alioth.debian.org/projects/pcsclite/"
license=('BSD')
depends=('udev')
makedepends=('pkg-config')
options=('!libtool' '!docs')
source=("https://alioth.debian.org/frs/download.php/3695/pcsc-lite-${pkgver}.tar.bz2"
        'pcscd')
md5sums=('cb7156346e9478c19085c918b605ed43'
         'c97b6bce695222efb0f003d8e8369010')

build() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  ./configure --prefix=/usr \
              --enable-libudev \
              --sysconfdir=/etc \
              --enable-ipcdir=/run/pcscd \
              --enable-usbdropdir=/usr/lib/pcsc/drivers \
              --with-systemdsystemunitdir=/lib/systemd/system

  make
}

package() {
  cd "${srcdir}/pcsc-lite-${pkgver}"

  make DESTDIR=${pkgdir} install

  install -D -m644 ${srcdir}/pcsc-lite-${pkgver}/COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
  install -D -m755 ${srcdir}/pcscd ${pkgdir}/etc/rc.d/pcscd
  install -d ${pkgdir}/usr/lib/pcsc/drivers
}
