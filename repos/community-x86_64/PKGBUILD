# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.36
pkgrel=3
pkgdesc="Qt5 Configuration Utility"
arch=(x86_64)
url="https://qt5ct.sourceforge.net"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2"
        qt5ct-qt5.12.patch)
sha256sums=('8fc3942f34d377af5533a0953cca633a7babc60c0476fd4318b617702183ee89'
            'ef2318d0003d33bf58e4bccfeed5d9697de68d752c0af9f7bedb70c031cb98ec')

prepare() {
  cd $pkgname-$pkgver
  patch -p3 -i ../qt5ct-qt5.12.patch # Fix color palette support with Qt 5.12
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 $pkgname.pro 
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

