# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Esclapion <esclapion at gmail.com>

pkgname=qastools
pkgver=0.22.0
pkgrel=1
pkgdesc="A collection of desktop applications for the Linux sound system ALSA."
arch=('x86_64')
url="https://gitlab.com/sebholt/qastools"
license=('GPL3')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libudev.so'
'qt5-base' 'qt5-svg')
makedepends=('cmake' 'qt5-tools')
source=("https://gitlab.com/sebholt/${pkgname}/-/archive/v${pkgver}/qastools-v${pkgver}.tar.gz")
sha512sums=('9656960095520a1bee58efa3985dda30fae1b6be666dfd65b50cb582be21a40d273a1c06b69e870bc2b467f5d644b22ea30c2fd73eee9057b50de5e85864d712')

prepare() {
  mv -v "${pkgname}-v${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
}

build() {
  cd "${pkgname}-${pkgver}"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX='/usr' \
          -DCMAKE_BUILD_TYPE=Release \
          -DUSE_QT5=ON \
          ..
  )
  make -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="${pkgdir}/" install
  install -vDm 644 {CHANGELOG,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

