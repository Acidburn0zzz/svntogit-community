# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=setconf
pkgver=0.3
pkgrel=1
pkgdesc="Utility to easily change settings in Makefiles and other configuration files"
arch=('x86_64' 'i686')
url="http://setconf.roboticoverlords.org/"
license=('GPL')
depends=('pcre' 'gc')
makedepends=('shedskin>=0.1.2')
source=("http://setconf.roboticoverlords.org/$pkgname-$pkgver.tbz2")
sha256sums=('94d37bcc74d59dfa60770308e2ab3b669dc2e0716a23fda584bf71cf037f1183')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  shedskin "$pkgname.py"
  make CPPFLAGS="-march=native -fomit-frame-pointer"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.1.gz" "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
