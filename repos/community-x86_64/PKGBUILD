# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: SpepS <dreamspepser at yahoo dot it>

pkgname=openimageio
pkgver=2.1.19.0
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications"
arch=(x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'openjpeg2' 'glew' 'libtiff' 'opencolorio' 'intel-tbb' 'libpng' 'libraw' 'libwebp')
makedepends=('cmake' 'qt5-base' 'python' 'boost' 'mesa' 'git' 'freetype2' 'fontconfig' 'libxrender' 'ninja')
optdepends=('qt5-base: iv image viewer'
            'python: bindings support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenImageIO/oiio/archive/Release-$pkgver.tar.gz")
sha512sums=('5bb7d0198e77bdd5821829bce7ed02be54316fa4ef1e87225eb0ab1108fe6b8163a3ea40549ca7e9d5ef2e7b8b0459bb30be3a761c6f17a558b3cb71eb888787')

build() {
  cd oiio-Release-$pkgver

  cmake \
      -Bbuild \
      -GNinja \
      -DUSE_PYTHON=ON \
      -DPYTHON_VERSION=3.8 \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DOIIO_BUILD_TESTS=ON \
      -DOIIO_BUILD_TOOLS=ON \
      -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd oiio-Release-$pkgver

  DESTDIR="$pkgdir" ninja -C build install

  # Remove vendored fonts
  rm -r "$pkgdir"/usr/share/fonts

  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE".md
}
