# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=ghc-lib-parser-ex
pkgname=haskell-ghc-lib-parser-ex
pkgver=8.8.5.6
pkgrel=1
pkgdesc="Algorithms on GHC parse trees"
url="https://github.com/shayne-fletcher/ghc-lib-parser-ex"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-uniplate' 'haskell-ghc')
makedepends=('ghc' 'haskell-extra' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('51957c9f8ea33d86138543d1e510dc5fdc19ff47a61313b98ca6be2bce336e20e5fcfdfb4d74884054e392ecfd65455b5d74193583865a6be58ea4ca1adf133f')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
