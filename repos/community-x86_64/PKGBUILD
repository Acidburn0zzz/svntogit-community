# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.4.1
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=('x86_64')
url="https://github.com/jacktrip/jacktrip"
license=('LGPL3' 'GPL3' 'MIT')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('help2man' 'meson' 'jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('5c6e8f5cc846d39473562a7ddd952e0b9ecdb247257829acb873aa02ba9a31c9f76f24998e56e71008dc09fcefd2860f421b07ba39c533c6d59a20cc42a54236')
b2sums=('4792b9f593ff09e2d23c82efecacd92e2c85e9d0bac080194a220982e3554546ce3a63b2ea18e4c55b9f8cc968a6cabaf0ebdbfb8eaf7e5d6ea145962c82fa16')

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -fv build
}

build() {
  cd "${pkgname}-${pkgver}"
  arch-meson -Drtaudio=disabled build
  ninja -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  ninja -C build test
}
package() {
  depends+=('libjack.so')

  cd "${pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" meson install -C build

  # licenses
  install -vDm 644 LICENSE.md LICENSES/MIT.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
