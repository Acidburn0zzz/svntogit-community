# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Jan Hicken <firstname [dot] lastname [at] posteo [dot] de>

pkgname=networkmanager-strongswan
_pkgname=NetworkManager-strongswan
pkgver=1.4.3
pkgrel=1
pkgdesc="Strongswan NetworkManager plugin"
arch=('x86_64')
url="http://wiki.strongswan.org/projects/strongswan/wiki/NetworkManager"
license=('GPL')
depends=('networkmanager' 'strongswan>=5.5.1' 'libnm-gtk' 'libsecret')
makedepends=('intltool')
source=("https://download.strongswan.org/NetworkManager/$_pkgname-$pkgver.tar.bz2"{,.sig})
sha512sums=('2437dcdd8f7989793b31a527afce5bbf224b179cff9bd9263fe707e3426b307bc7090d99883f47bbc995d71f45f4d0e023233481c1dc659b9dfa2dafa1a34539'
            'SKIP')
validpgpkeys=('12538F8F689B5F1F15F07BE1765FE26C6B467584') # Tobias Brunner

build() {
  cd "$_pkgname-$pkgver"
  ./configure --sysconfdir=/etc --prefix=/usr \
    --libexecdir=/usr/lib/networkmanager \
    --with-charon=/usr/lib/strongswan/charon-nm
  make
}

check() {
  cd "$_pkgname-$pkgver"
  make check
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
