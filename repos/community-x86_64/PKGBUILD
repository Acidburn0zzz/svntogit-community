# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=solidity
pkgver=0.4.8
pkgrel=1
pkgdesc='Contract-Oriented Programming Language'
arch=('i686' 'x86_64')
url='https://github.com/ethereum/solidity'
license=('GPL3')
depends=('boost-libs' 'jsoncpp')
makedepends=('boost' 'cmake')
source=("${url}/releases/download/v${pkgver}/solidity_${pkgver}.tar.gz")
sha256sums=('429c7c46fad256b714a25891dccbbeb90d3dfcdf431706b5816832374c094318')

prepare() {
    cd solidity_${pkgver}

    sed -e '/jsoncpp\.cmake/d' -i CMakeLists.txt
}

build() {
    cd solidity_${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DTESTS=OFF \
        ..
    make
}

package() {
    cd solidity_${pkgver}/build

    make DESTDIR="${pkgdir}" install
}
