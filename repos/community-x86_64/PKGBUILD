# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas at gmail dot com>
# Contributor: Laszlo Papp <djszapi2 at gmail dot com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Vladimir Cerny <blackvladimir@gmail.com>
# Contributor: 4javier <4javieregATgmailDOTcom>

_target=avr
pkgname=$_target-gdb
pkgver=9.1
pkgrel=1
pkgdesc='The GNU Debugger for AVR'
arch=(x86_64)
url='https://www.gnu.org/software/gdb/'
license=(GPL3)
depends=(python guile2.0 xz gdb-common mpfr)
options=(!emptydirs)
source=(https://ftp.gnu.org/gnu/gdb/gdb-$pkgver.tar.xz{,.sig})
sha256sums=('699e0ec832fdd2f21c8266171ea5bf44024bd05164fdf064e4d10cc4cf0d1737'
            'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker <brobecker@adacore.com>

prepare() {
  cd gdb-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd gdb-$pkgver

  mkdir -p build && cd build
  ../configure \
    --disable-nls \
    --enable-languages=c,c++ \
    --prefix=/usr \
    --target=$_target \
    --with-python=/usr/bin/python \
    --with-guile=guile-2.0 \
    --with-system-readline \
    --with-system-gdbinit=/etc/gdb/gdbinit

  make
}

package() {
  cd gdb-$pkgver/build

  make -C gdb DESTDIR=$pkgdir install

  # Following files conflict with 'gdb'/'gdb-common' packages
  rm -r $pkgdir/usr/include/gdb/
  rm -r $pkgdir/usr/share/gdb/
  rm -r $pkgdir/usr/share/info/
  rm -r $pkgdir/usr/share/man/man5/

}
