# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.33.20180720
_commit=5f2d72eadf596fbef23fa4944cbd4f381c406c01
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm' 'deepin-metacity' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'deepin-go-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go-pie' 'python2' 'git' 'jq' 'deepin-dbus-factory')
replaces=('deepin-wm-switcher')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/felixonmars/startdde/archive/$_commit.tar.gz")
sha512sums=('cc36ceb19b0c6541e41acf22f7fc6ab7bca907c97191b8f91c22260fe20653e0f148e1891f4ee4457b89c0dd1e0bee58dcb2b1c7981c5b880b89720485874d30')

prepare() {
  mv startdde-{$_commit,$pkgver}

  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/cryptix/wav golang.org/x/net/context github.com/linuxdeepin/go-x11-client

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install
}
