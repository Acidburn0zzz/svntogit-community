# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64.at.gmail.dot.com>

pkgname=mtpaint
pkgver=3.40
pkgrel=11
pkgdesc='Simple paint program for creating icons and pixel based artwork'
arch=('x86_64' 'i686')
url='http://mtpaint.sourceforge.net/'
license=('GPL3')
depends=('gtk2' 'giflib' 'libjpeg' 'openjpeg' 'desktop-file-utils' 'lcms2' 'jasper')
makedepends=('gtk2' 'giflib' 'libjpeg' 'openjpeg' 'lcms2' 'jasper')
install='mtpaint.install'
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('ef321d2b404839c7b909bdf5283eb22a37fbdd35b4cc9e380ddc400573d7c890')

prepare() {
  cd "$pkgname-$pkgver"

  sed -i 's:EGifOpenFileName(file_name, FALSE):EGifOpenFileName(file_name, FALSE, NULL):' src/png.c
  sed -i 's:DGifOpenFileName(file_name):DGifOpenFileName(file_name, NULL):g' src/png.c
  sed -i 's:MakeMapObject(:GifMakeMapObject(:g' src/png.c
  sed -i 's:FreeMapObject(:GifFreeMapObject(:g' src/png.c
}

build() {
  cd "$pkgname-$pkgver"

  export CFLAGS="-w `pkg-config libopenjpeg --cflags` $CFLAGS"
  ./configure --prefix=/usr --mandir=/usr/share/man man intl GIF jpeg tiff cflags lcms2 jp2
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:

