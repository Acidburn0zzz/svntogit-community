# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.9.0
pkgrel=2
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dde-dock/commit/5f11a0134aeb4e97fff9edd7d3b5d318491398b1.patch
        https://github.com/linuxdeepin/dde-dock/commit/4cd74405ce9bba827d70ec515b2b98d5b68dd69b.patch
        https://github.com/linuxdeepin/dde-dock/commit/b1d0fd4ad4af7e3fcf7a1ceb2728a3fc1b03b066.patch)
sha512sums=('ceb21c31cc59761b4536ed7d2ad2cf3c018142ba8d4c3aa405170cf6a6cf52bb19d9a824bf60d91fe28ebb0a5ea99533c11a684e55c987760f13962b91890b43'
            'eae7cbd50f437529ca065b611be90a16cd0453d1d7f1dc48942d254c025c087f1feec74957ef914255ccce006d21d324311c00c696d7b99fbc2ae7710b1639a4'
            '92a90fd063903aeae2f216bb535901f36fabfba7e6460c322d70b14743b1fd3ff0a6d19a278f8daf817fcf2b632d64ff6346aa2a441f76356125bf1f6d40b744'
            'e055882ed48e26795c788bba04186cbd1ffb73ba24d6331908ceb8e62f423d95b6916f068f390fb6674e4ba6fdb289f29f58b6d1413029c39d166f2643d33e0a')

prepare() {
  patch -d dde-dock-$pkgver -p1 -i ../5f11a0134aeb4e97fff9edd7d3b5d318491398b1.patch
  patch -d dde-dock-$pkgver -p1 -i ../4cd74405ce9bba827d70ec515b2b98d5b68dd69b.patch
  patch -d dde-dock-$pkgver -p1 -i ../b1d0fd4ad4af7e3fcf7a1ceb2728a3fc1b03b066.patch
}

build() {
  cd dde-dock-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  make
}

package() {
  cd dde-dock-$pkgver
  make DESTDIR="$pkgdir" install
}
