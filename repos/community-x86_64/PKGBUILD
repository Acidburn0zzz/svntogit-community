# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=2.0.12.1
pkgrel=1
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kio libxss vlc)
makedepends=(extra-cmake-modules kidletime kdoctools)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz")
sha256sums=('a19eb8262f1f35b013c3ea5aa66fd7cd99d624643db718d2e9d36d9dd7a796ca')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-${pkgver%.*} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
