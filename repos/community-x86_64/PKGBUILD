# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.2
pkgrel=2
epoch=1
pkgdesc='DTK core modules'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base' 'gsettings-qt')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dtkcore/commit/34fbe4b3386954c59382eccad5a1b19e63539547.patch)
sha512sums=('5884715aa1dfbdf6e5bffd7c1d8722553d94cf199cc75d2f6778b29a2806e66619965011cc4d3808d1729ad1ccf7f6a141459eb09ffac5a0854d0dce98698f61'
            'b36bef5a37d47e503a20829eff53b89aec4b3d207ebf34d929885fd02bc41216413456b0aaee8b4d574fb71bbddfd2ba2bae20106616eb2f1341a50d409704b0')

prepare() {
  cd dtkcore-$pkgver
  patch -p1 -i ../34fbe4b3386954c59382eccad5a1b19e63539547.patch
}

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
