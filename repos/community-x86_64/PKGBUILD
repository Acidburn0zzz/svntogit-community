# Maintainer: David Runge <dave@sleepmap.de>
pkgname=midi_matrix.lv2
pkgver=0.22.0
pkgrel=1
pkgdesc="A plugin for MIDI channel filtering, multiplication and rerouting"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/midi_matrix/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
md5sums=('2c1a8cd9f1cb429b9a1589de519c36e3')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

# check fails due to currently broken lv2_validate in lv2
# check() {
#   cd "$pkgname-$pkgver"
#   meson test -C build
# }


package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
