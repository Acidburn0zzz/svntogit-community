# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=clingo
pkgver=5.1.0
pkgrel=1
pkgdesc='Grounding tools for (disjunctive) logic programs'
arch=('x86_64' 'i686')
url='https://potassco.org/'
license=('GPL3')
depends=('python2')
makedepends=('bison' 're2c' 'scons')
source=("https://github.com/potassco/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('c1afa83142d727d18bdab68fd6d7e1341dcb25d2e9a1aea3abc672ecbae6c72a')

build() {
  cd "$pkgname-$pkgver"

  scons --build-dir=release
}

package() {
  cd "$pkgname-$pkgver/build/release"

  install -D gringo ${pkgdir}/usr/bin/gringo
  install -D clingo ${pkgdir}/usr/bin/clingo
  install -D reify ${pkgdir}/usr/bin/reify
  install -D lpconvert ${pkgdir}/usr/bin/lpconvert

  if [ -x "python/clingo.so" ]; then
      install -D "python/clingo.so" ${pkgdir}/usr/lib/libclingo_python.so
  fi

  if [ -x "lua/clingo.so" ]; then
      install -D "lua/clingo.so" ${pkgdir}/usr/lib/libclingo_lua.so
  fi
}

# vim:set ts=2 sw=2 et:
