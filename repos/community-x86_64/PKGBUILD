# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-1-1
_patchver=2
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=7
pkgdesc="Computer Algebra System for polynomial computations"
arch=(x86_64)
url="http://www.singular.uni-kl.de/"
license=(GPL)
depends=(flint cddlib) # polymake
makedepends=(doxygen python2)
optdepends=('python2: Python interface')
source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz"
        singular-ntl-error-handler.patch)
#source=("http://www.mathematik.uni-kl.de/ftp/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz")
sha256sums=('8e1f67c0a940ae772231dc5f700ab2a1b08199be26d33323558fd08d629ea91f'
            '2f318d9d712fc12e77c6d7e78a2cf6d4077f180c29789b4a3eb9f536056b8857')
options=(!zipman)

prepare() {
  cd singular-${_majver//-/.}
  patch -p1 -i ../singular-ntl-error-handler.patch # Move NTL error handler out of libsingular so it doesn't conflict with Sage's one
}

build() {
  cd singular-${_majver//-/.}
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  # Fix ownership
  chown -R root:root "$pkgdir"/usr/share/singular/html "$pkgdir"/usr/share/info
}
