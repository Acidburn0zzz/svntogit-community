# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-edit
pkgver=0.5.0
pkgrel=2
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('39c216f6e51f6d497ae3895d063b68476471c880d94603b6e0712891bb7c2229af93c9181bf06d98040ddabc71e052051457e4d8075c8dfe8aa5f17d1797d140')
b2sums=('3d4383eb87f25630fbcece3b5494694de089254c7b716527382f521af25a8152f12f3dea3196acd00f56450571413cfce323d7cd334e79bad323236738e4ff02')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  cargo test --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-add \
    target/release/cargo-rm \
    target/release/cargo-upgrade \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
