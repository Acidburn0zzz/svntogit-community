# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-font-manager
pkgver=5.8.0.21
pkgrel=1
pkgdesc='A font management tool for Deepin desktop'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-font-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-file-manager')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-font-manager/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('11155184240dcbc7ee64d5977355b96d5aea7882858bd5c40ab5b3e502ebaf99699ba648ede2835f084bf32c4fe4752ccc7f74df88a56eb8a8c2e6dd7775e48b')

build() {
  cd deepin-font-manager-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-font-manager-$pkgver
  ninja test || :
}

package() {
  cd deepin-font-manager-$pkgver
  DESTDIR="$pkgdir" ninja install
}
