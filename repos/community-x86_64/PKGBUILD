# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=gitlab-gitaly
pkgver=0.28.0
pkgrel=1
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=('glibc')
options=(!buildflags)
makedepends=('go' 'ruby2.3-bundler' 'cmake')
backup=("etc/gitlab-gitaly/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitaly/repository/archive.tar.gz?ref=v${pkgver}"
        "gitlab-gitaly.service")
sha512sums=('a54c39dc1113cc29b7de610dbdeb89b4a81a289162d483df7fa2dab2c5312d72e562b65ab631d44b4df1de8030781449aa647e12696b281b9ddac9f5ff7ca684'
            'b330c0f42592322ad2131079ca554a13a364007182dded8c556198caff2c9ec642acf5bb7dfecb05de5a3d89bffec6588b6d05c8c7c5c771a46df3d296deed28')

_homedir="/var/lib/gitlab"

prepare() {
  cd "gitaly-v${pkgver}-"*

  sed -i "s/bundle install/bundle-2.3 install/" Makefile
  sed -i "s/VERSION = .*/VERSION = ${pkgver}/" Makefile
}

package() {
  cd "gitaly-v${pkgver}-"*

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/etc/${pkgname}"
  sed \
    -e "s|^socket_path =.*|socket_path = \"${_homedir}/sockets/gitlab-gitaly.socket\"|" \
    -e "s|^path =.*|path = \"${_homedir}/repositories\"|" \
    config.toml.example > "${pkgdir}/etc/${pkgname}/config.toml"

  install -Dm644 config.toml.example "${pkgdir}/usr/share/${pkgname}/config.toml.example"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
