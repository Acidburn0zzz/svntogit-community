# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=1.13
pkgrel=3
pkgdesc="Tool that finds the software that makes your laptop use more power than necessary"
arch=('i686' 'x86_64')
url="http://www.lesswatts.org/projects/powertop/"
license=('GPL2')
depends=('ncurses')
source=(http://www.lesswatts.org/projects/powertop/download/$pkgname-$pkgver.tar.gz \
        powertop-1.13-with-3.0.6.patch)
md5sums=('78aa17c8f55178004223bf236654298e'
         '2b4c9d75fb0f5c96bcd549bcbbeb9c9d')

build() {
  cd "$srcdir/powertop-$pkgver"

  # fix the reported batery usage on recent kernels
  patch -Np1 < "$srcdir/powertop-1.13-with-3.0.6.patch" || true

  make
}

package() {
  cd "$srcdir/powertop-$pkgver"
  make DESTDIR="$pkgdir" install
}
