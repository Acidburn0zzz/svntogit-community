# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-polkit-agent
pkgver=1.0.2
pkgrel=1
pkgdesc='Pantheon Polkit Agent'
arch=(x86_64)
url=https://github.com/elementary/pantheon-agent-polkit
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgranite.so
  polkit
)
makedepends=(
  git
  meson
  intltool
  vala
)
source=(pantheon-polkit-agent::git+https://github.com/elementary/pantheon-agent-polkit.git#tag=dc7e3b1f7f7ac22b806129c1be40d11d7261821d)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-polkit-agent

  git describe --tags
}

build() {
  arch-meson pantheon-polkit-agent build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
