# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-web
pkgver=1.17.1
pkgrel=58
pkgdesc="Web interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'hledger' 'haskell-hledger-lib' 'haskell-aeson' 'haskell-blaze-html'
         'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-clientsession'
         'haskell-cmdargs' 'haskell-conduit' 'haskell-conduit-extra' 'haskell-data-default'
         'haskell-decimal' 'haskell-extra' 'haskell-hjsmin' 'haskell-http-conduit'
         'haskell-http-client' 'haskell-http-types' 'haskell-megaparsec' 'haskell-network'
         'haskell-shakespeare' 'haskell-unix-compat' 'haskell-utf8-string' 'haskell-wai'
         'haskell-wai-cors' 'haskell-wai-extra' 'haskell-wai-handler-launch' 'haskell-warp'
         'haskell-yesod' 'haskell-yesod-core' 'haskell-yesod-form' 'haskell-yesod-static')
makedepends=('ghc' 'haskell-hspec' 'haskell-yesod-test')
replaces=('hledger-api')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4221cd01387715efe6c17bbece72878a93acb6f1b153fac5eb2a28a3086676e9ebca84e1c6fbff86c49d2b6f383cbc2bfb38115301c10645bc9245c03c1f5b31')

prepare() {
    cd $pkgname-$pkgver
    sed -i -e '/semigroups/d' $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-dev -f-library-only -fthreaded
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $pkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
