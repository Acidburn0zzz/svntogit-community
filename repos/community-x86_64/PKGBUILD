# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: DonVla <donvla/users.sourceforge/net>
# Contributor: Herv√© Cauwelier <herve/oursours/net>

pkgname=python-xapian
_realname=xapian-bindings
pkgver=1.4.15
epoch=1
pkgrel=2
pkgdesc="Python bindings for Xapian"
arch=('x86_64')
url="https://xapian.org/docs/bindings/python/"
license=('GPL2')
depends=('python' "xapian-core=$epoch:$pkgver")
makedepends=('python-sphinx')
source=("https://oligarchy.co.uk/xapian/${pkgver}/${_realname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('dec8a0431c9070fc70d79b67bddc2e0adb10ef5366c4b9d7a135365ea6c6680d2e7682dbfd60c29eca51da48eaa0715509e30b13d5482d5a38bfd0d0156c43f9'
            'SKIP')
validpgpkeys=('08E2400FF7FE8FEDE3ACB52818147B073BAD2B07') # Olly Betts <olly@debian.org>

prepare() {
  cd "${_realname}-${pkgver}"
  autoreconf -vfi
  export XAPIAN_CONFIG=/usr/bin/xapian-config
}

build() {
  cd "${_realname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-python3
  make
}

package() {
  cd "${_realname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${_realname}" \
    -vDm644 {AUTHORS,ChangeLog,HACKING,NEWS,README,TODO}
}
# vim:set ts=2 sw=2 et:
