# $Id: PKGBUILD 73557 2012-07-09 19:15:21Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jared Casper <jaredcasper@gmail.com>
# Contributor: Paulo Matias <matias archlinux-br org>

pkgname=iverilog
pkgver=0.9.6
pkgrel=1
pkgdesc='Icarus Verilog compiler and simulation tool'
arch=('x86_64' 'i686')
url='http://iverilog.icarus.com/'
license=('GPL')
makedepends=('readline' 'gperf')
depends=('bzip2' 'sh' 'gcc-libs' 'zlib')
source=("ftp://ftp.icarus.com/pub/eda/verilog/v0.9/verilog-$pkgver.tar.gz")
sha256sums=('25304d5d58d6411fcd1ab94992a505215eea5a6bbd9779c2be2d9d19f38cd54a')

build() {
  cd "$srcdir/${pkgname/i}-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/${pkgname/i}-$pkgver"

  make -j1 prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
