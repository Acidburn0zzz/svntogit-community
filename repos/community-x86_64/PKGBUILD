# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.7.1.1
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs'
         'polkit-qt5' 'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'xdg-user-dirs'
         'file-roller' 'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal'
         'gst-plugins-good' 'mpv' 'deepin-anything' 'deepin-movie' 'kcodecs')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
optdepends=('deepin-manual: view the help manual')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$pkgver.tar.gz"
        fix-plugin-link.patch)
sha512sums=('991d44c74a094d56c36fa9720a2a600708a647441d1c2ab5587dad982d69380293c829650437777a5b6d51ddf45569d258d21641f5f518170550c4732da9091c'
            '8a1ed955a25ea2e8d477d3c70da6243c053e940af671211eb3e4c363783d5fd7864176f92c6f59d748c97f569825e3ef9c81e2f8b4d1454542193215605fb009')

prepare() {
  cd dde-file-manager-$pkgver
  patch -p1 -i ../fix-plugin-link.patch
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr DISABLE_JEMALLOC=1
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
