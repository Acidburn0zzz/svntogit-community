  # $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-qt
pkgver=0.0.0.20200730
_commit=cf6daf5e41c2d6cc77a082cbf872bc7163732d48
pkgrel=1
pkgdesc="Fcitx5 Qt Library"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-qt"
license=('GPL')
groups=('fcitx5-im')
depends=('fcitx5' 'qt5-x11extras')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://github.com/fcitx/fcitx5-qt/archive/$_commit/fcitx5-qt-$_commit.tar.gz")
sha512sums=('c2a31c56184d8c6fd14ee9d085fe7328392d5833da34feee5b1d5c280cf75a57bf90db425e38f9a4118fb23ea6dd8e28910c540813b11c3c5d0e16fdbdd2b3f7')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DENABLE_QT4=0 .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
