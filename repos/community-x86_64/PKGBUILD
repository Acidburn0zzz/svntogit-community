# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Leslie P. Polzer <polzerΘgnu·org>
# Contributor: erm67 <erm67Θyahoo·it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview4
pkgver=4.7
pkgrel=1
pkgdesc='Portable DjVu viewer and browser plugin'
arch=('i686' 'x86_64')
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
depends=('qt' 'djvulibre')
source=("http://downloads.sourceforge.net/djvu/djview-${pkgver}.tar.gz")
sha1sums=('6c0c9c285caaf6211454c36898801c2eda799c8b')
install=install

build() {
	cd "${srcdir}/djview-${pkgver}"
	QTDIR=/usr ./configure --prefix=/usr
	sed 's/netscape/mozilla/g' -i nsdejavu/Makefile
	make
}

package() {
	cd "${srcdir}/djview-${pkgver}"
	make DESTDIR="${pkgdir}" install-djview install-nsdejavu

	install -Dm644 desktopfiles/hi32-djview4.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/djvulibre-djview4.png
	install -Dm644 desktopfiles/hi64-djview4.png "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/djvulibre-djview4.png
	install -Dm644 desktopfiles/hi64-djview4.png "${pkgdir}"/usr/share/pixmaps/djvulibre-djview4.png
	install -Dm644 desktopfiles/djvulibre-djview4.desktop "${pkgdir}"/usr/share/applications/djvulibre-djview4.desktop
}
