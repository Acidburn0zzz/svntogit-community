# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Grigorios Bouzakis <grbzks@gmail.com>

pkgname=fdm
pkgver=1.6
pkgrel=3
pkgdesc='Program to fetch and deliver mail'
url='http://fdm.sourceforge.net/'
license=('BSD')
arch=('i686' 'x86_64')
options=('!makeflags')
depends=('openssl' 'tdb' 'zlib')
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"
	'GNUmakefile.patch')
sha1sums=('fb8042f2355062848ff142c9604069b6657e6a82'
          '0bddf0b0403ccee62190355f43fae84932c37436')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../GNUmakefile.patch
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	install -d "${pkgdir}"/usr/share/{doc,licenses}/"${pkgname}"
	sed '3,17p;d' fdm.c > "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	mv examples "${pkgdir}/usr/share/doc/${pkgname}"
}
