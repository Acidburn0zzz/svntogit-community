# Maintainer: Ainola <ainola@archlinux.org>

pkgname=slurp
pkgver=1.0.1
pkgrel=1
pkgdesc='Select a region in a Wayland compositor'
arch=('x86_64')
url="https://github.com/emersion/slurp"
license=('MIT')
depends=('cairo' 'wayland')
makedepends=('meson' 'scdoc' 'wayland-protocols')
source=("$pkgname-$pkgver.tar.gz::https://github.com/emersion/slurp/archive/v$pkgver.tar.gz")
sha256sums=('3e44401ff3ecf78bd147e1e3d40cc571973da7f4e0f26ce3cd70b60e2c270b67')

build() {
    meson --prefix=/usr --buildtype=plain "$pkgname-$pkgver" build
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    cd "$pkgname-$pkgver"
    install -Dm 0644 LICENSE -t "$pkgdir/usr/share/licenses/slurp/"
    install -Dm 0644 README.md -t "$pkgdir/usr/share/doc/slurp/"
}
