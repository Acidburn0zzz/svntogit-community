# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.8.2
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('84c7d5c059a450dde70e7bb2c6a9075bebac06b4e02a53dd03959d902f44483884e3111e2aa48bcef1fd747edc279f4a2fde8af25aa6dd859e32ebdb215ba12a'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

prepare() {
  mkdir .gopath
  ln -sf "$PWD/gost-$pkgver/vendor" .gopath/src
  ln -s "$PWD/gost-$pkgver" .gopath/src/github.com/ginuerzh/gost
}

build() {
  export GOPATH="$srcdir/.gopath"
  cd gost-$pkgver/cmd/gost
  go build -ldflags "-extldflags ${LDFLAGS}" # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
