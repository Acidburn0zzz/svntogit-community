# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=turtle
pkgname=haskell-turtle
pkgver=1.4.3
pkgrel=2
pkgdesc="Shell programming, Haskell-style"
url="https://github.com/Gabriel439/Haskell-Turtle-Library"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-ansi-wl-pprint' 'haskell-async' 'haskell-clock' 'haskell-foldl'
         'haskell-hostname' 'haskell-managed' 'haskell-optparse-applicative' 'haskell-optional-args'
         'haskell-semigroups' 'haskell-stm' 'haskell-system-fileio' 'haskell-system-filepath'
         'haskell-temporary' 'haskell-text' 'haskell-unix-compat')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('c00ff979fbda3442538cd7ee22bb87f6c3739a9d70c0d7971692a2928f310c38287d8bb6af7c94a5b4b4429c137c4b5d0bfb40710af1deff06a8516f2c16b3d7')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
