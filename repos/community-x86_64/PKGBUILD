# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-configtool
pkgver=0.0.0.20200803.1
_commit=e819708e1b7a12e8c43964a8a1972d91b9ef982b
pkgrel=1
pkgdesc="Configuration Tool for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-configtool"
license=('GPL')
groups=('fcitx5-im')
provides=('fcitx5-config-qt' 'kcm-fcitx5')
conflicts=('kcm-fcitx5')
replaces=('kcm-fcitx5')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews' 'qt5-x11extras')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'ninja' 'python')
source=("https://github.com/fcitx/fcitx5-configtool/archive/$_commit/fcitx5-configtool-$_commit.tar.gz")
sha512sums=('965c95f99d4d107f20db2795f97da9872b5f5695c114dad81782cb03df873c994e66b75896e7a3f62167b353f201af1a9735eefe6fb5df504bf1cfab9f6e4b64')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
