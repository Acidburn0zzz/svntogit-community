# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=mxml
pkgver=3.0
pkgrel=1
pkgdesc="A small XML parsing library"
arch=('x86_64')
url="https://www.msweet.org/mxml"
license=('LGPL')
depends=('zlib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/michaelrsweet/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('3c540348022226b122ce014f183368cda6631600a11d8f4d43c99c62fba2f3ca4b23dff4122082f63915b59a46a5475d1d12d0081f9315c65f5c044ac3c751d6')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-shared
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DSTROOT="${pkgdir}" install
}
