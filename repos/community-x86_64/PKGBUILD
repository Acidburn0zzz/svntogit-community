# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.4.3
pkgrel=3
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-cffi')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('721a00c42f591cfa62813b2e2948e139a29a9e555fbe0c2732588699e36c614ad993c6b11c88229ccf94e4729e2edf8195dd54ed635f4a2ecd6e6b86d90559d8')

build() {
  cd "$srcdir"/persistent-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/persistent-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" python -m zope.testrunner --test-path=.
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
