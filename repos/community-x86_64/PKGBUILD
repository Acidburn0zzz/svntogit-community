# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=4.2.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('dbus-glib' 'gtk3' 'gobject-introspection-runtime' 'js52')
makedepends=('autoconf-archive' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('b5dc46dc9fd3a25331bc5f74b7da7b0cbadbe022f0ae857a50df67fc6155dbcaa8930c8e904b128422a8e27f504a2f7d29868e3611de1d43c5f85d29ebae6ff0')
b2sums=('2268cc8d557e05f8ce3877f54e22d7ad6b256631ec74d3bcfc7be180dea9555f28619d5cee9b5a0dfc39559906c29e4714e59f87700c0bc8ecc3eca22eb138af')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --disable-static

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # Needs a display
    xvfb-run make -k check || :
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
