# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Lucas Hermann Negri <kkndrox@gmail.com>

pkgbase=luaexpat
pkgname=(lua-expat lua51-expat lua52-expat lua53-expat)
pkgver=1.4.0
pkgrel=1
_rockrel=1
pkgdesc='SAX XML parser based on the Expat library'
arch=('x86_64')
url="https://github.com/lunarmodules/$pkgbase"
license=('MIT')
depends=(expat)
options=(debug)
makedepends=(lua
             lua51
             lua52
             lua53
             luarocks)
_archive="$pkgbase-$pkgver"
_rock="$_archive-$_rockrel.linux-$CARCH.rock"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha512sums=('ba1a98267757ec5f2464b8d5e903f69102ca991c4034939d02d871d014463a8e29815d1294edc7fa3a1c4b679c5e42fdc1a19b2a1c1654ed1f382f779a213375')

build() {
	cd "$_archive"
	for LUAVER in 5.1 5.2 5.3 5.4; do
		luarocks --lua-version "$LUAVER" \
			CFLAGS="$CPPFLAGS $CFLAGS -fPIC" \
			LIBFLAG="$LDFLAGS -shared" \
			make --pack-binary-rock --deps-mode none -- "rockspecs/$_archive-$_rockrel.rockspec"
		install -Dm0644 -t "lua$LUAVER/" "$_rock"
	done
}

_package() {
	cd "$_archive"
	depends+=("${pkgname%-*}")
	luarocks --lua-version "$1" --tree "$pkgdir/usr/" \
		install --deps-mode none --no-manifest -- "lua$1/$_rock"
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_lua-expat() {
	_package 5.4
}

package_lua53-expat() {
	_package 5.3
}

package_lua52-expat() {
	_package 5.2
}

package_lua51-expat() {
	replaces=(luaexpat)
	conflicts=(luaexpat)
	_package 5.1
}
