# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=operator-sdk
pkgver=1.10.1
pkgrel=1
pkgdesc="SDK for building Kubernetes applications"
arch=('x86_64')
url="https://github.com/operator-framework/operator-sdk"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/operator-framework/operator-sdk/archive/v${pkgver}.tar.gz")
sha512sums=('57246fd6c7422f1b65b13ee16a68ce45f8aac895fd7579448cbabcd5323480806556c9b0750ee8176eca9ad4aa1e0708fefc624835a6dc1b50708cd281fa13d2')
b2sums=('79cffe2bee971e86bd466f2675c4d45cf191c3c5c847567bfb97f148033fdf26bb26687275eb4de0e603a12eb270fa5290de1e4f288e05b5912710d7d3986396')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/operator-framework "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/operator-framework/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/operator-framework/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=1
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-linkmode=external" -v -o . ./cmd/{operator-sdk,ansible-operator,helm-operator}
}

#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./operator-sdk "${pkgdir}/usr/bin/operator-sdk"
  install -Dsm755 ./ansible-operator "${pkgdir}/usr/bin/ansible-operator"
  install -Dsm755 ./helm-operator "${pkgdir}/usr/bin/helm-operator"
}
