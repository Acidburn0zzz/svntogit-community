# $Id$ 
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=gpicview
pkgver=0.2.3
pkgrel=2
pkgdesc="lightweight image viewer"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://lxde.org/"
groups=('lxde')
depends=('gtk2' 'desktop-file-utils')
install=$pkgname.install
makedepends=('pkgconfig' 'intltool')
optdepends=('librsvg: For SVG support')
source=(http://downloads.sourceforge.net/sourceforge/lxde/${pkgname}-${pkgver}.tar.gz
        fix-scrolling.patch)
md5sums=('bca9ffe1d283ed3dbb13bada7feb7e89'
         'f258a3e703c4daa2f79c0c9f5a625d86')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	# Fix scrolling (FS#34872)
	patch -Np1 -i "$srcdir/fix-scrolling.patch"

	# Apply one main category only (FS#34684)
	sed -i '/^Categories=/ s/Utility;//' gpicview.desktop.in
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --sysconfdir=/etc --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
}
