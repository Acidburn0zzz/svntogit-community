# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: "Amhairghin" Oscar Garcia Amor
# Contributors: bhushan, huma, adlaiff6, Invie, solarce

pkgname=tmate
pkgver=2.2.1
pkgrel=4
pkgdesc='Instant terminal sharing'
arch=('x86_64')
url='http://tmate.io/'
license=('BSD')
depends=('libevent' 'libssh' 'libutempter' 'msgpack-c')
makedepends=('cmake' 'ruby')
source=("https://github.com/tmate-io/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '0001-libssh.patch')
sha256sums=('d9c2ac59f42e65aac5f500f0548ea8056fd79c9c5285e5af324d833e2a84c305'
            'f8399462312eec47b4dca34d14fc2550173556438d81fbb936896d5114008a06')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 < ../0001-libssh.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/tmate/COPYING
}
