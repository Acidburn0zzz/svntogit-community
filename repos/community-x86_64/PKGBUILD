# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: "Amhairghin" Oscar Garcia Amor
# Contributors: bhushan, huma, adlaiff6, Invie, solarce

pkgname=tmate
pkgver=2.2.1
pkgrel=3
pkgdesc='Instant terminal sharing'
arch=('x86_64')
url='http://tmate.io/'
license=('BSD')
depends=('libevent' 'libssh' 'libutempter' 'msgpack-c')
makedepends=('cmake' 'ruby')
source=("https://github.com/tmate-io/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        '0001-add-new-channel-after-authentication.patch')
sha256sums=('d9c2ac59f42e65aac5f500f0548ea8056fd79c9c5285e5af324d833e2a84c305'
            '85029b3b32f6306fe44fb4a7c0a7307007436a69c4e5c2c00bfabec4db9f117c')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  patch -Np1 < ../0001-add-new-channel-after-authentication.patch
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m0644 COPYING ${pkgdir}/usr/share/licenses/tmate/COPYING
}
