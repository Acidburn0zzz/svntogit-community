# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jacek Poplawski <jacekpoplawski@gmail.com>

pkgname=rocksndiamonds
pkgver=4.0.0.1
pkgrel=1
pkgdesc="game similiar to Boulderdash with lots of levels"
arch=(i686 x86_64)
url="http://www.artsoft.org/rocksndiamonds/"
license=('GPL')
depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_net' 'smpeg')
makedepends=('unzip' 'p7zip')
source=(http://www.artsoft.org/RELEASES/unix/rocksndiamonds/rocksndiamonds-$pkgver.tar.gz
	rocksndiamonds.desktop)
sha256sums=('8649a450419d9cee2ffd9463cae65e7ebcbbf48c2cc945a85d91bbcdd9be3249'
            '90008d356dd30034287ca763a05feb482d7e0c4508572abb08b5215fcb4fe922')

prepare() {
  cd "$srcdir"/rocksndiamonds-$pkgver
  sed -i 's|inline void InitElementSmallImagesScaledUp|void InitElementSmallImagesScaledUp|g' src/init.c
}

build() {
  cd "$srcdir"/rocksndiamonds-$pkgver
  make clean
  make RO_GAME_DIR=/usr/share/rocksndiamonds RW_GAME_DIR=/usr/share/rocksndiamonds
}

package() {
  depends=('sdl' 'sdl_image' 'sdl_mixer' 'sdl_net' 'smpeg' 'rocksndiamonds-data')

  cd "$srcdir"/rocksndiamonds-$pkgver

  install -d "$pkgdir"/usr/bin
  cp "$srcdir"/rocksndiamonds-$pkgver/rocksndiamonds "$pkgdir"/usr/bin/rocksndiamonds

#  install -d "$pkgdir"/usr/share/man/man1
#  cp "$srcdir"/rocksndiamonds-$pkgver/rocksndiamonds.1 "$pkgdir"/usr/share/man/man1

  install -d "$pkgdir"/usr/share/doc/$pkgname/
  cp -a docs/elements "$pkgdir"/usr/share/doc/$pkgname/

  install -Dm0644 "$srcdir"/rocksndiamonds.desktop "$pkgdir"/usr/share/applications/rocksndiamonds.desktop
}
