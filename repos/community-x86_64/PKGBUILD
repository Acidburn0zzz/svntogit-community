# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.44.3
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=2
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('i686' 'x86_64')
url="http://crca.ucsd.edu/~msp/software.html"
license=('custom:BSD')
depends=('jack' 'fftw' 'tk')
provides=('puredata')
source=("http://crca.ucsd.edu/~msp/Software/pd-$_ver.src.tar.gz")
md5sums=('a3e8ca0eab039dc472a6533ef9344582')

build() {
  cd "$srcdir/pd-$_ver"
  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-jack \
              --enable-fftw \
              --disable-portaudio
  make
}

package() {
  cd "$srcdir/pd-$_ver"
  make DESTDIR="$pkgdir" install

  # license
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/pd/LICENSE"
}
