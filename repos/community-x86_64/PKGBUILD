# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: ChaosKid42 <christoph.scholz@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

_name=rugged
# ideally this should match $pkgver, as package() might fail otherwise. thanks, github
_libgit2_pkgver=1.0.0
pkgname=ruby-rugged
pkgver=1.0.0
pkgrel=1
pkgdesc='A Ruby binding to the libgit2 linkable library'
arch=('x86_64')
url="https://github.com/libgit2/rugged"
license=('MIT')
depends=('glibc' 'ruby')
makedepends=('libgit2' 'ruby-minitest' 'ruby-rake' 'ruby-rake-compiler' 'ruby-rdoc')
checkdepends=('git')
options=(!emptydirs)
source=("${_name}-${pkgver}.tar.gz::https://github.com/libgit2/${_name}/archive/v${pkgver}.tar.gz"
        "libgit2-${_libgit2_pkgver}.tar.gz::https://github.com/libgit2/libgit2/archive/v${_libgit2_pkgver}.tar.gz")
sha512sums=('69ed5aa9340854fc0bee58d8b872d0b91648be7a59b59b6bc3de87499f4d8c49353cc2edbb625cc779e0e2cdb697839654edfcba1e8cb849c6f2e38aa4be75fe'
            '7e3b6582dd1567fd5ebfc829a98e22acfd5566ba08f2fb9b7a6783fa87f5e8952ac67f1a2c13adb543e56753d858e73a03204bac17a096c72f5daf4ba376d904')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec
  # remove broken and useless libgit2 "version check":
  # https://github.com/libgit2/rugged/issues/698
  sed '68,90d' -i ext/rugged/extconf.rb
  # source tarball doesn't include vendored libgit2 version, which is only
  # needed for test fixtures (resources): https://github.com/libgit2/rugged/issues/801
  mv -v ../libgit2-${_libgit2_pkgver}/* vendor/libgit2
}

build() {
  cd "${pkgname}-${pkgver}"
  export CI_BUILD=true
  export RUGGED_USE_SYSTEM_LIBRARIES=true
  export CMAKE_FLAGS=" --use-system-libraries"
  rake compile
  rake gem
}

check(){
  cd "${pkgname}-${pkgver}"
  # ConfigTest#test_read_global_config_file fails, if no gitconfig is set for
  # user...
  # https://github.com/libgit2/rugged/issues/432
  git config --global user.name "Foo Bar"
  rake test
}

package() {
  depends+=('libgit2.so')
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              pkg/${_name}-${pkgver}.gem
  install -vDm 644 "$pkgdir/$_gemdir/gems/$_name-$pkgver/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md CHANGELOG.md -t \
    "${pkgdir}/usr/share/doc/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out} \
    gems/${_name}-${pkgver}/vendor
}
