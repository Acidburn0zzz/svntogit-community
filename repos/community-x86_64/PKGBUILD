# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rink
pkgver=0.5.0
pkgrel=1
pkgdesc='Unit conversion tool and library written in rust'
arch=('x86_64')
url=https://github.com/tiffany352/rink-rs
license=('GPL3' 'MPL2')
depends=('gcc-libs' 'gmp' 'openssl')
makedepends=('rust')
source=("rink-$pkgver.tar.gz::https://static.crates.io/crates/rink/rink-$pkgver.crate")
sha512sums=('dc6b7049fceaa7f9b4084d2d513bc2524bef90cf5eccdd8e75a7044d55633f108631cf6aea6f95ab95bf9e983b302bcc6becfe7b26796e062ca5dc83e7ea9549')

build() {
  cd rink-$pkgver
  cargo build --release --locked
}

check() {
  cd rink-$pkgver
  cargo test --release --locked
}

package() {
  cd rink-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rink
}

# vim:set ts=2 sw=2 et:
