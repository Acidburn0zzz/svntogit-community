# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Thanks to Moonprincessluna at DeviantArt for the default icon:
# http://mylittlepixel.deviantart.com/gallery/35405924#/d4pt23a

pkgname=gendesk
pkgver=0.2
pkgrel=1
pkgdesc="Utility to generate .desktop files and download icons"
arch=('x86_64' 'i686')
url="http://roboticoverlords.org/gendesk/"
license=('MIT')
depends=('glibc')
makedepends=('go')
conflicts=('go-hg')
source=("http://roboticoverlords.org/$pkgname/$pkgname-$pkgver.tar.gz"
        "http://roboticoverlords.org/images/default.png")
sha256sums=('51de3dd28ac38da82ca1db1428aad44348bc2edc491931ed0146044e781ca2e8'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')
options=(!strip)
if [ "$CARCH" = "x86_64" ]; then
  _go=6
else
  _go=8
fi

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Compiling..."
  "${_go}g" "$pkgname.go"
  "${_go}l" -o "$pkgname" "$pkgname.$_go"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$srcdir/default.png" "$pkgdir/usr/share/pixmaps/default.png"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
