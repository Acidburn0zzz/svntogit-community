# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=49
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-48-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('5ad0c0f411f60a91ef14bc21ff3447354c8d7c515aac96620045d37be8f35df9179d2551208eded9ed1f78e0108ee537fc00855d73bd8f539ba114bfaf15c82b'
            'bea57324b517605b793b15329b7e5b9ffc855b906b611800b38d8482829e467e462c28eb7ebf789ece2216e7ed677f47e29643f3bd09f67c6bf6510fc57d2cca')
b2sums=('5511fa54ee72a51018ee1ed1a2248163d4dd6580ddf953c5798cda6f9f7c571265adf39a166d919cae6639234532897026c0625ff6c6c67134f0ebc4b7221228'
        '3b8965fa7e6e71772b4228fbdaaab4ebb4e0024d0a4ba5ec0e416fb4276bc5b0d358f16cfa93083bf5648ddcfb7401e301f2220c6b1549f71dc48003cc1af832')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-48-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
