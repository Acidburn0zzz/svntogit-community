# Maintainer:
# Contributor: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=icewm
pkgver=2.3.4
pkgrel=1
pkgdesc='Window Manager designed for speed, usability, and consistency'
arch=(x86_64)
url='https://ice-wm.org/'
license=(LGPL)
depends=(alsa-lib imlib2 librsvg libsndfile libxcomposite libxdamage libxinerama libxpm libxrandr)
makedepends=(asciidoctor xorg-mkfontdir)
groups=(icewm)
optdepends=('perl: for icewm-menu-xrandr')
source=("https://github.com/ice-wm/icewm/releases/download/$pkgver/$pkgname-$pkgver.tar.lz")
sha256sums=('1368f7db61589b1309274a2fe7250961e21904a294b1cff2c13273abd73725f4')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}
