# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openxr
pkgver=1.0.16
pkgrel=1
pkgdesc='An open standard for virtual reality and augmented reality platforms and devices'
arch=('x86_64')
url='https://www.khronos.org/openxr/'
depends=('jsoncpp' 'libx11' 'vulkan-icd-loader' 'libglvnd' 'libxrandr' 'libxxf86vm')
makedepends=('cmake' 'vulkan-headers' 'python' 'ninja' 'wayland' 'mesa')
license=('APACHE')
source=("https://github.com/KhronosGroup/OpenXR-SDK-Source/releases/download/release-${pkgver}/OpenXR-SDK-Source-release-${pkgver}.tar.gz"{,.asc})
validpgpkeys=(F029455EAB70F520FF4A03BD7F534CD56F00321E)
sha512sums=('a5a8aa9d446ce1c1fbfc3965b5af22ee439a46f4884db2d9df7fcb99655062243f641d1308065d6f9bbeeffc60d2caa7a5827085f14e636ee0c478b649d7b094'
            'SKIP')

build() {
  cd OpenXR-SDK-Source-release-${pkgver}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DPRESENTATION_BACKEND=xlib \
    -DDYNAMIC_LOADER=ON
  ninja -C build
}

package() {
  cd OpenXR-SDK-Source-release-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
