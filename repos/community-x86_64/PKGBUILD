# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=4.15.3
pkgrel=2
pkgdesc="Lenses, Folds and Traversals"
url="http://github.com/ekmett/lens/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc" "haskell-base-orphans" "haskell-bifunctors" "haskell-comonad"
         "haskell-contravariant" "haskell-distributive" "haskell-exceptions" "haskell-free"
         "haskell-hashable" "haskell-kan-extensions" "haskell-mtl" "haskell-parallel"
         "haskell-profunctors" "haskell-reflection" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-tagged" "haskell-text" "haskell-th-abstraction" "haskell-transformers-compat"
         "haskell-unordered-containers" "haskell-vector" "haskell-void")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('0ccc13ee500712e1e36fe7284fe179f461f00c179756968aca27d570629322e1a17e13b6d006ee27e03c4aad4d6006b9e0397567af24333ebc3b7f8726a953ca')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit \
            -ftest-doctests -ftest-hlint -f-dump-splices -f-old-inline-pragmas -finlining \
            -f-benchmark-uniplate
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
