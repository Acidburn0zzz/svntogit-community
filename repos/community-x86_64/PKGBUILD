# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-session-manager
pkgver=2.0.0
pkgrel=1
pkgdesc="Session manager of the UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-session-manager"
groups=('ukui')
depends=('kidletime' 'peony' 'qt5-multimedia' 'ukui-biometric-auth' 'ukui-panel' 'ukui-settings-daemon' 'ukui-screensaver' 'ukwm' 'xtrans')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-session-manager/archive/v$pkgver.tar.gz")
sha512sums=('38840a72699194655003950ed65b675d91cb7f0015583718735f9142ff244b052e5f5aca4d496afc09eaf856c73f800253637d07462caebc2c64f9642e16aadf')

prepare() {
  mkdir -p build
  sed -i '/#include <QString>/i #include <QFile>' $pkgname-$pkgver/tools/main.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
