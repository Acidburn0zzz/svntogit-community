# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i@hexchain.org>

# Thanks Nicholas Guriev <guriev-ns@ya.ru> for the patches!
# https://github.com/mymedia2/tdesktop

pkgname=telegram-desktop
pkgver=1.9.8
pkgrel=1
pkgdesc='Official Telegram Desktop client'
arch=('x86_64')
url="https://desktop.telegram.org/"
license=('GPL3')
depends=(
    'enchant'
    'ffmpeg'
    'hicolor-icon-theme'
    'lz4'
    'minizip'
    'openal'
    'qt5-imageformats'
    'xxhash'
    'libappindicator-gtk3'
)
# for libappindicator-gtk3 see https://bugs.archlinux.org/task/65080

makedepends=(
    'cmake'
    'git'
    'ninja'
    'python'
    'quilt'
    'range-v3'
)
optdepends=(
    'ttf-opensans: default Open Sans font family'
)
source=(
    "https://github.com/telegramdesktop/tdesktop/releases/download/v${pkgver}/tdesktop-${pkgver}-full.tar.gz"
    telegram-desktop.sh
    0005-Use-system-wide-fonts.patch
    #"gcc9-cpp2a.patch::https://github.com/desktop-app/cmake_helpers/commit/67cf2a5abdb01658c1cf852b29e25808dcc02c56.patch"
    series
)
sha512sums=('5562eb99812a8faec74fe073323d6e04e36311c1e4ce984035212ecfed8bd5d12df92cd0f0022401201136315fb5556971b267b4bf47edf4eeddc9926c7969dc'
            '3c21c871e28bac365400f7bc439a16ad1a9a8d87590ad764ce262f1db968c10387caed372d4e064cb50f43da726cebaa9b24bcbcc7c6d5489515620f44dbf56b'
            'e12021af0c95c967f80240abd683bf197f3c4f8f21315b48c5dadeb399dc39cd1e352352daba1c30691c6d1c6e66078af95b9262e8fe0c2174709fcb1a8a3f5d'
            'bff9dea22b659f37a5e3508e5da381f2cffcc83f4f0aa85d0ef9a9ff5666acb36af70a6396aa9d9610626cf8c81fb4fefb63ee511f94bf58c1c9830d93853ffe')

prepare() {
    cd tdesktop-$pkgver-full

    QUILT_PATCHES=.. quilt --quiltrc=/dev/null push -a

    mkdir build

    #cd cmake
    #patch -Np1 -i "$srcdir/gcc9-cpp2a.patch"
}

build() {
    cd tdesktop-$pkgver-full

    export CXXFLAGS="$CXXFLAGS -ffile-prefix-map=$srcdir/tdesktop-$pkgver-full="
    cmake -B build -G Ninja . \
        -Ddisable_autoupdate=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DTDESKTOP_API_ID=17349 \
        -DTDESKTOP_API_HASH=344583e45741c457fe1862106095a5eb \
        -DDESKTOP_APP_USE_GLIBC_WRAPS=OFF \
        -DDESKTOP_APP_USE_PACKAGED=ON \
        -DDESKTOP_APP_USE_PACKAGED_RLOTTIE=OFF \
        -DDESKTOP_APP_DISABLE_CRASH_REPORTS=ON \
        -DTDESKTOP_DISABLE_REGISTER_CUSTOM_SCHEME=ON \
        -DTDESKTOP_DISABLE_DESKTOP_FILE_GENERATION=ON \
        -DTDESKTOP_USE_PACKAGED_TGVOIP=OFF \
        -DDESKTOP_APP_SPECIAL_TARGET="" \
        -DTDESKTOP_LAUNCHER_BASENAME="telegramdesktop"
    ninja -C build
}

package() {
    install -dm755 "$pkgdir/usr/bin"
    install -m755 telegram-desktop.sh "$pkgdir/usr/bin/telegram-desktop"

    cd tdesktop-$pkgver-full
    install -m755 build/bin/telegram-desktop "$pkgdir/usr/bin/telegram-desktop-bin"

    install -d "$pkgdir/usr/share/applications"
    install -m644 lib/xdg/telegramdesktop.desktop "$pkgdir/usr/share/applications/telegramdesktop.desktop"

    install -d "$pkgdir/usr/share/kservices5"
    install -m644 lib/xdg/tg.protocol "$pkgdir/usr/share/kservices5/tg.protocol"

    install -d "$pkgdir/usr/share/metainfo/"
    install -m644 lib/xdg/telegramdesktop.appdata.xml "$pkgdir/usr/share/metainfo/telegramdesktop.appdata.xml"

    local icon_size icon_dir
    for icon_size in 16 32 48 64 128 256 512; do
        icon_dir="$pkgdir/usr/share/icons/hicolor/${icon_size}x${icon_size}/apps"

        install -d "$icon_dir"
        install -m644 "Telegram/Resources/art/icon${icon_size}.png" "$icon_dir/telegram.png"
    done
}
