# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.11
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=('x86_64')
url="https://drumkv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libsndfile' 'qt5-base')
makedepends=('alsa-lib' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ea290c2e6143d2d56b32cc4dbdbf3ad3e942d1d02d4be99a2277bbe96ae7087fdf586747c9534ac474354c12f7c95a4e161c222454b03ab5e4301d1e1047cff5')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
