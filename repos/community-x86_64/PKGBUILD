# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=2.6
pkgrel=1
pkgdesc="A light-weight process isolation tool"
arch=('x86_64')
url="http://nsjail.com"
license=('Apache')
depends=('protobuf' 'libnl')
makedepends=('git')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/google/nsjail/archive/${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=9a8568c8f91a91d93893a61a48665dc329e075f1')
sha256sums=('866c181266e33f94e22e1bba72c04883d8bd5694929b19d8aa183a72acc8519b'
            'SKIP')

prepare() {
    mv kafel ${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
