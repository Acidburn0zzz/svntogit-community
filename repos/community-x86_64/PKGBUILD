# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20181024
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(x86_64)
license=('GPL' 'LGPL')
url="https://www.prevanders.net/dwarf.html"
depends=('elfutils')
options+=('staticlibs')
source=(https://www.prevanders.net/libdwarf-$pkgver.tar.gz)
sha512sums=('134f8639c4a2ddf012ab196551bcc9afc65c729b36299ebf807de94561fe878473ea58443d33daaa96f008b28dd169ab29f65616e9f1e44a6e7921cfde6e3f19')

build() {
  cd "$srcdir"/libdwarf-$pkgver
  ./configure --prefix=/usr --includedir=/usr/include/libdwarf --enable-shared
  make
}

check() {
  cd "$srcdir"/libdwarf-$pkgver
  make check
}

package() {
  cd "$srcdir"/libdwarf-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README NEWS "$pkgdir"/usr/share/doc/$pkgname/
}
