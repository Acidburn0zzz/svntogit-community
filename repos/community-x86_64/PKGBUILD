# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.3.1
pkgrel=1
pkgdesc="Light eye-candy fully themable animated dock"
arch=('i686' 'x86_64')
url="https://launchpad.net/cairo-dock-core"
license=('GPL')
depends=('curl' 'dbus-glib' 'gtk3' 'librsvg')
makedepends=('cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
options=('!libtool')
source=("http://launchpad.net/${pkgname}-core/${pkgver%.?}/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4d7c5c58606ac1e22dcbe6d80404e38d747c2c846102df09f25d5b97943f0459')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
