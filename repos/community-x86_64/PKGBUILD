# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
pkgname='vault'
pkgdesc='A tool for managing secrets'
pkgver='0.9.0'
pkgrel='3'
url='https://vaultproject.io/'
license=('MPL')
arch=('x86_64')
makedepends=('go-pie' 'git')
depends=('glibc')
install='vault.install'
backup=('etc/vault.hcl')
_vault_commit='bdac1854478538052ba5b7ec9a9ec688d35a3335'
source=("git+https://github.com/hashicorp/vault#commit=${_vault_commit}"
        'vault.service'
        'vault.hcl')
sha512sums=('SKIP'
            '6619cf57668e995cddb29fb6c388c18c21b251052a53832415e415bb4fe538361ef77b74536f5b082b9cda6cd71b598fc50d8b7f51092c4d60262052c5725af2'
            '46106cc76151eef2dd5e4b2caa6a96aae4d6ce1ecbf977dcc8667a3f6c829cbea95133622adafcb15cdfaa066ecc94c73c983e7613ee2f6573694981569729fe')

prepare () {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hashicorp/
  mv ${pkgname} src/github.com/hashicorp/
}

build () {
  cd src/github.com/hashicorp/${pkgname}
  go build -o vault-binary
}

package () {
  cd src/github.com/hashicorp/${pkgname}
	install -Dm755 vault-binary "${pkgdir}/usr/bin/vault"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	install -Dm644 "${srcdir}/vault.hcl" "${pkgdir}/etc/vault.hcl"
	install -Dm644 "${srcdir}/vault.service" "${pkgdir}/usr/lib/systemd/system/vault.service"
	for file in README.md CHANGELOG.md ; do
		install -Dm644 "${file}" "${pkgdir}/usr/share/doc/${pkgname}/${file}"
	done
}
