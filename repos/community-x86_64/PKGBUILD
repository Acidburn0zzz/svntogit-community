# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=1.2.1
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('3bd59322215caee34b9d7b26ec55cdda9bcd92208844bc46da0cae404181d451d4ae398a974e9662d3e9a774e2b37048f3f862696248dbcade9998750d48aa69')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
