# $Id$
# Maintainer: Antonio Rojas < nqn76sw@gmail.com >

pkgname=linbox
pkgver=1.5.1
pkgrel=1
pkgdesc="A template library for exact, high-performance linear algebra computation with dense, sparse, and structured matrices over the integers and over finite fields"
arch=(x86_64)
url="http://linalg.org/"
license=(LGPL)
depends=(m4rie flint fplll iml fflas-ffpack tinyxml2) 
source=("https://github.com/linbox-team/linbox/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz" linbox-config.patch)
sha256sums=('b2a4dbfe1e599f0ca5a5d3c63403ba7914463a0040cdd2a55e7b29aaab7d7589'
            '2f60b1b598d04168622ad1cccf0e8745b73efc5b7686398cb6f0f89cc2032ceb')

prepare() {
  cd $pkgname-$pkgver
  patch -Rp1 -i ../linbox-config.patch # Install missing config.h header
  autoreconf -vi
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-sage --with-fplll=/usr \
    --disable-sse3 --disable-ssse3 --disable-sse41 --disable-sse42 --disable-avx --disable-avx2 --disable-fma
  make
}

check() {
  cd $pkgname-$pkgver
# make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
