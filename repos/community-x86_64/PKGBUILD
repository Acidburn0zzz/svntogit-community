# $Id: PKGBUILD 162811 2016-02-20 15:24:54Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.1.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=('x86_64' 'i686')
url='http://duktape.org/'
makedepends=('setconf')
depends=('glibc')
license=('MIT')
source=("http://duktape.org/duktape-$pkgver.tar.xz" # no https available
        'duktape.pc')
sha256sums=('e02364cd72077e34e204b8375c8bd7c461eb13cb623c7dc1a74d253d84b1446b'
            '2b2b8c690f19587b2976617a03b60f172eb6f3612a6920eaedad61a4f93f33d2')

prepare() {
  cd "$pkgname-$pkgver"

  mv Makefile.sharedlibrary Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version "$pkgver"
}

build() {
  make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 "$srcdir/duktape.pc" "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim:set ts=2 sw=2 et:
