# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.2.2.5
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz")
sha512sums=('24c9ad46d4c3386193a357c3e78b629f154e599d12242d8b374e2ff24c76ac92eb19c520070c9035798a676dd10d92ea4686f819a62bbd50931106dd4992bdea')

prepare() {
  cd dtkwidget-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' src/util/dwidgetutil.cpp
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
