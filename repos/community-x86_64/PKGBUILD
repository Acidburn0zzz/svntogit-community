# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=soapysdr
_pkgname=SoapySDR
pkgver=0.7.1
_gitver=soapy-sdr-$pkgver
pkgrel=1
pkgdesc="Vendor and platform neutral SDR support library"
arch=('x86_64')
url="https://github.com/pothosware/SoapySDR"
license=('Boost')
depends=('python')
makedepends=('cmake' 'swig' 'doxygen')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha256sums=('5445fbeb92f1322448bca3647f8cf12cc53d31ec6e0f11e0a543bacf43c8236d')

build() {
  mkdir -p "$srcdir"/$_pkgname-$_gitver/build
  cd "$srcdir"/$_pkgname-$_gitver/build

  cmake .. \
    -DSOAPY_SDR_EXTVER=ARCH \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd "$srcdir"/$_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install
}
