# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cangjie
pkgver=0.0.0.20210222
_commit=f3a95bebbc019546b3ee9908f08fec239f9eba4c
pkgrel=1
pkgdesc="Cangjie input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cangjie"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-luna-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cangjie/archive/$_commit/rime-cangjie-$_commit.tar.gz")
sha512sums=('a41f34c0cd17b5261588cd360442555cd5ef57efc52611930769259634d80a7e2756b1040096dfed532fc622317143da771899e25b17b35cb4f2282957d60be2')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
