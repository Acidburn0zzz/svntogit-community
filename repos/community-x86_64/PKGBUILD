# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Johannes Sjolund <j.sjolund@gmail.com>

pkgname=mxml
pkgver=2.12
pkgrel=1
pkgdesc="A small XML parsing library"
arch=('x86_64')
url="https://www.msweet.org/mxml/"
license=('LGPL')
depends=('zlib')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/michaelrsweet/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('49233a0087f3ef73a01ef71bb79511af36bb72027e3d9f6df919385e9ff0b03a489a3ccc590941bc4af1f558f82b2ed9bf8ff641863300a7791ce6dddfd56e77')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-shared
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DSTROOT="${pkgdir}" install
}
