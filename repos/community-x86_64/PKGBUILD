# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: damir <damir@archlinux.org>

_pkg=netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=4.6.2.1
pkgrel=1
pkgdesc="network Common Data Form interface for array-oriented data access and corresponding library with parallel support (${_mpi} version)"
arch=('x86_64')
url="https://www.unidata.ucar.edu/software/netcdf/"
license=('custom')
depends=("hdf5-${_mpi}" "pnetcdf-${_mpi}" 'curl')
makedepends=('cmake')
optdepends=('netcdf-fortran: fortran bindings' 'netcdf-cxx: c++ bindings')
checkdepends=('inetutils')
provides=("${_pkg}")
conflicts=("${_pkg}")
options=('!makeflags')
source=(${_pkg}-${pkgver}.tar.gz::"https://github.com/Unidata/netcdf-c/archive/v${pkgver}.tar.gz")
sha256sums=('f5526423f11ec6c582025710d144438a707afca0499dc00f9fe6067aa8676aac')

prepare() {
    mkdir -p build
}

build() {
    cd build
    export CC=mpicc
    cmake ../${_pkg}-c-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=Release \
        -DENABLE_CDF5=ON \
        -DENABLE_PNETCDF=ON \
        -DENABLE_DAP_LONG_TESTS=ON \
        -DENABLE_EXAMPLE_TESTS=ON \
        -DENABLE_EXTRA_TESTS=ON \
        -DENABLE_FAILING_TESTS=ON \
        -DENABLE_FILTER_TESTING=ON \
        -DENABLE_LARGE_FILE_TESTS=ON
    make
}


check() {
    cd build

    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    # One test failure https://github.com/Unidata/netcdf-c/issues/808
    make test || warning "Test failure"
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install

    cd "${srcdir}"/${_pkg}-c-${pkgver}
    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}/
}
