#$Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>

pkgname=connman
pkgver=1.17
pkgrel=1
pkgdesc="Wireless LAN network manager"
url="http://connman.net/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('dbus-core' 'iptables' 'glib2' 'gnutls')
conflicts=('openresolv')
optdepends=('bluez: Support for Bluetooth devices'
            'wpa_supplicant: For WiFi devices')
makedepends=('bluez' 'wpa_supplicant' 'openconnect' 'openvpn')
options=('!libtool')
source=("http://www.kernel.org/pub/linux/network/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'allow_group_network.diff')
md5sums=('21ab8945f84b2ebf5f60bc8591fecdfd'
         'a8d22ee089fb0ed725130d16ad393047')

prepare(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/allow_group_network.diff"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
	  --bindir=/usr/bin \
	  --sbindir=/usr/bin \
      --with-systemdunitdir=/usr/lib/systemd/system \
      --enable-threads \
      --enable-openconnect \
      --enable-vpnc \
      --enable-openvpn \
      --enable-polkit \
      --enable-client

  make
}

package(){
  make -C "${srcdir}/${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
  install -Dm755 "$srcdir/$pkgname-$pkgver/client/${pkgname}ctl" "$pkgdir/usr/bin/${pkgname}ctl"
  find "$pkgdir/usr" -name \*.service -exec sed -i 's/s\(bin\)/\1/' {} +
}
