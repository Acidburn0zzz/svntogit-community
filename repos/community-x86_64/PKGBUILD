# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=2.8.0.post2
pkgrel=1
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-setuptools-scm python-wheel python-toml)
depends=(python-lxml python-pillow qpdf)
source=(https://pypi.python.org/packages/source/p/pikepdf/pikepdf-$pkgver.tar.gz)
sha256sums=('d703b9227bbf1d0ebb57e9ed0281d792198aa22d995169850fe6af40725c4979')

prepare() {
  sed -e '/setuptools_scm_git_archive/d' -i pikepdf-$pkgver/setup.py # Remove unused build dependency
}

build() {
  cd pikepdf-$pkgver
  python setup.py build
}

package() {
  cd pikepdf-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
