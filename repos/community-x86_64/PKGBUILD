# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-bls1
pkgver=0.1.0
pkgrel=6
pkgdesc="Blumlein Shuffler implementation converting binaural stereo signals into a form suitable for reproduction on convential stereo speakers."
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'clthreads' 'clxclient' 'jack' 'zita-convolver')
makedepends=('gendesk')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2"
        'zita-convolver.patch')
sha512sums=('a1b4bd7a7ca56afb627e9f095e5e1f67415a52ae316bdc6ce0eced191830f53eaf5ba3402a44fbbb97529e29a0b007d1f0f8e79c89c863207216bca1fb76435c'
            '2f91833217428d2c0104006cb741f74abf709d21e33d4ec6d1a802d1c1f7a69a2465b73e4a44349cf7bde471538963ecc24f15a4a5de7d85978dd31539c57e81')

prepare() {
  cd "${pkgname}-${pkgver}"
  # strip march=native
  sed -e '/native/d' \
      -e 's/freetype-config\ --cflags/pkgconf\ --cflags\ freetype2/' \
      -i source/Makefile
  patch -Np1 -i ../zita-convolver.patch
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name zita-bls1 \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir" install

  # XDG desktop integration
  install -vDm 644 "../${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../doc/html/* \
    -vDm 644 ../{AUTHORS,README}
}
