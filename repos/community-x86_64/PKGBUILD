# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  TDY <tdy@archlinux.info>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgbase=gap
pkgname=(gap gap-data gap-doc gap-packages)
pkgver=4.8.6
_gapver=4r8p6_2016_11_12-14_25
pkgrel=1
pkgdesc="Groups, Algorithms, Programming: a system for computational discrete algebra"
arch=(i686 x86_64)
url="http://www.gap-system.org/"
license=(GPL)
source=("http://www.gap-system.org/pub/gap/gap48/tar.gz/gap$_gapver.tar.gz" gap-no-packages-by-default.patch)
sha256sums=('a9df4c49b2feb5dd7b8c4f093f6558bdd7a6c1b3f5f64971c4d9e74828eb0cd4'
            'e8f19968d998172b7c289d0499b8b8a05bc31119a1ce073181d3842ece6fe3a2')
makedepends=(libxaw openmpi givaro mpfi fplll normaliz polymake git)

prepare() {
  cd gap4r8

# Use system normaliz
  sed -e '/build-normaliz.sh/d' -i bin/BuildPackages.sh
# Don't load any packages by default
  patch -p1 -i ../gap-no-packages-by-default.patch
}

build() {
  cd gap4r8
  CC=mpicc ./configure --prefix=/usr --with-gmp=system
  make

  cd pkg
  sh ../bin/BuildPackages.sh
# These packages fail to build:
# carat
# float (doesn't support fplll 5)
# fr https://github.com/gap-packages/fr/issues/16
# guava
# linboxing (doesn't support givaro 4)
# qaos 

  cd ../etc
  sh install-tools.sh
}

package_gap() {
  depends=(gmp openmpi)
  optdepends=('gap-packages: extra packages' 'gap-data: additional databases' 'gap-doc: documentation')
  replaces=(gap-math)
  conflicts=(gap-math)
  cd gap4r8
  
  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r bin etc grp lib CITATION "$pkgdir"/usr/lib/gap
  
  mkdir -p "$pkgdir"/usr/lib/gap/pkg
  cp -r pkg/GAPDoc-* "$pkgdir"/usr/lib/gap/pkg

  mkdir -p "$pkgdir"/usr/bin
  install -m755 "$pkgdir"/usr/lib/gap/bin/gap.sh "$pkgdir"/usr/bin/gap

# fix location
  sed -e 's|/build/gap/src/gap4r8|/usr/lib/gap|' -i "$pkgdir"/usr/bin/gap  
# remove unneeded stuff
  rm "$pkgdir"/usr/lib/gap/bin/*/*.o
  rm "$pkgdir"/usr/lib/gap/bin/gap.sh
  find "$pkgdir"/usr/lib/gap/bin/ -maxdepth 1 -type f -exec rm {} \;
}

package_gap-data() {
  depends=(gap)
  pkgdesc="Additional databases for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r prim small trans "$pkgdir"/usr/lib/gap
}

package_gap-doc() {
  depends=(gap)
  pkgdesc="Documentation for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r doc "$pkgdir"/usr/lib/gap
}

package_gap-packages() {
  depends=(gap)
  optdepends=('normaliz: Normaliz interface package' 'polymake: Polymake interface package' 'libxaw: xgap package')
  pkgdesc="Extra packages for GAP"
  cd gap4r8

  mkdir -p "$pkgdir"/usr/lib/gap
  cp -r pkg "$pkgdir"/usr/lib/gap

# GAPDoc is in main package
  rm -r "$pkgdir"/usr/lib/gap/pkg/GAPDoc-*
}
