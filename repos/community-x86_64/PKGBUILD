# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=lxc
pkgver=0.9.0
epoch=1
pkgrel=2
pkgdesc="Linux Containers"
arch=('i686' 'x86_64')
url="http://lxc.sourceforge.net/"
depends=('bash' 'perl' 'libseccomp')
license=('LGPL')
source=("http://lxc.sourceforge.net/download/lxc/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('8552a4479090616f4bc04d8473765fc9')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"
  ./configure \
	--prefix=/usr \
	--localstatedir=/var \
	--libexecdir=/usr/bin \
	--sysconfdir=/etc \
	--disable-apparmor \
	--disable-doc \
        --enable-seccomp
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir/var/lib/lxc"

  cd doc
  find . -type f -name '*.1' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man1/{}" \;
  find . -type f -name '*.5' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man5/{}" \;
  find . -type f -name '*.7' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man7/{}" \;
}
