# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=lv2file
pkgver=0.91
pkgrel=1
pkgdesc="A simple program which you can use to apply effects to your audio files"
arch=(x86_64)
url="https://github.com/jeremysalwen/lv2file"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(argtable glibc)
makedepends=(libsndfile lilv lv2)
# upstream didn't add the manpage to the release tarball:
# https://github.com/jeremysalwen/lv2file/issues/4
source=("$pkgname-$pkgver.tar.gz::https://github.com/jeremysalwen/${pkgname}/archive/upstream/${pkgver}.tar.gz"
        "https://github.com/jeremysalwen/${pkgname}/blob/9c6fa09f829afa4e4dd47841ec57959c958ddce3/debian/${pkgname}.1")
sha512sums=('9f0d102905ca66622bbd0a057acff38cf7929be6ac4bc1423637218ae7990b64a75d4042804f93662cc9d1e6509affc7bb804b6ff8ebc043cc1271efee4f9ecf'
            'd477981f1c2f7dc5937e8a061f3a000c08a4df656506e0677296cd6d7ffdb3055c629979e01fdded580054534a726a4b2bacd98b6643d25ac7659713b14e0a24')
b2sums=('a59fa01d611785000f886fc32ebce044fe24da69e485b8380afb094e9b943ebaa02c71c2c45f48091ed7fd48609bd60dcb2d4cfa93dce7ec916be5f8d6e4858f'
        '92497a36ad44e6d1f6d40f671269b397993bbfa124142cbebdb13928ead02741d3008746016b29e54d7aa854b6ed0395ef419a6499cb8aecbbb099db2d0b9207')

build() {
  make all -C "$pkgname-upstream-$pkgver"
}

package() {
  depends+=(liblilv-0.so libsndfile.so)

  make DESTDIR="$pkgdir/" install -C "$pkgname-upstream-$pkgver"
  install -vDm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644  "$pkgname-upstream-$pkgver/README" -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
