# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=50
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-48-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('58796353df9180dc6b12bfe71579fb3dd2d7ab029dc5f8d09b4f8a3cb6938db054702dc4c7344b8d8852926e052f9bdf304b9a71e3d8faa9c6a209ef0b94ff4d'
            'bea57324b517605b793b15329b7e5b9ffc855b906b611800b38d8482829e467e462c28eb7ebf789ece2216e7ed677f47e29643f3bd09f67c6bf6510fc57d2cca')
b2sums=('31efae8f031e41b04d0e1cb9dea011572b9516ed9962687d863b5cd2f77ec409a9dc6b449a299869a89abc554e673aa05bae1350a06cc850b898b878c02e8daa'
        '3b8965fa7e6e71772b4228fbdaaab4ebb4e0024d0a4ba5ec0e416fb4276bc5b0d358f16cfa93083bf5648ddcfb7401e301f2220c6b1549f71dc48003cc1af832')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-48-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
