# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=96
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc="Enterprise-class stacked cryptographic filesystem for Linux"
url="https://launchpad.net/ecryptfs"
license=('GPL')
makedepends=('swig' 'intltool' 'gettext')
depends=('keyutils' 'nss' 'python2')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz"
        symlinked_mtab.patch)
options=(!libtool)
md5sums=('4f92c9f6e8f62ac00ca7e2c4e480f1d7'
         '7740ed69601cfc75bb96c9ad072dfdb4')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-pamdir=/usr/lib/security PYTHON=python2
  # Apply patch to detect if /etc/mtab is a symlink.
  patch src/libecryptfs/main.c < "$srcdir/symlinked_mtab.patch"
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  chmod +s "$pkgdir"/sbin/mount.ecryptfs_private
}
