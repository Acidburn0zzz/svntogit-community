# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: RedTide <redtid3@gmail.com>

pkgname=sfizz
pkgver=1.1.0
pkgrel=1
pkgdesc="SFZ library and LV2 plugin"
arch=('x86_64')
url="https://github.com/sfztools/sfizz"
license=('BSD')
groups=(lv2-plugins pro-audio vst3-plugins)
depends=(abseil-cpp cairo gcc-libs glibc libx11 libxkbcommon libxkbcommon-x11
pugixml ttf-roboto xcb-util xcb-util-cursor zenity)
makedepends=(cmake cxxopts jack freetype2 lv2 xcb-util-keysyms)
checkdepends=(lv2lint)
optdepends=(
  'jack: for sfizz_jack'
  'lv2-host: for the LV2 plugin'
  'vst3-host: for the VST3 plugin'
)
provides=('libsfizz.so' 'soundfont-synthesizer')
source=("https://github.com/sfztools/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3ddebdebaf290416a478233e03dcc3d228ba0bfb68fd14564f9cc7b3f1dd82f19b8381f1f9a3aa825683cb41f2298fe5439fc15068befc942866e067b872b18f')
b2sums=('fcec77b47edcc06ef14b3f3dc2e358ef4dd18fa7081056ffa50909a5ee798aabc5e9b0dec127b5632a748c87f91c83c0de53783ecb1f4429ee3d82d34865921f')

prepare() {
  cd "$pkgname-$pkgver"
  # devendor lv2
  rm -rfv lv2/lv2
  sed -e 's|"lv2/core/lv2.h"|<lv2/core/lv2.h>|g' \
      -i plugins/lv2/external/ardour/ardour/lv2_extensions.h
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX="/usr" \
        -DCMAKE_BUILD_TYPE='None' \
        -DSFIZZ_PUREDATA=ON \
        -DSFIZZ_TESTS=ON \
        -DSFIZZ_USE_SYSTEM_ABSEIL=ON \
        -DSFIZZ_USE_SYSTEM_CXXOPTS=ON \
        -DSFIZZ_USE_SYSTEM_PUGIXML=ON \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  ./build/tests/${pkgname}_tests
  lv2lint -Mpack -I "build/${pkgname}.lv2" "http://sfztools.github.io/sfizz"
}

package() {
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  # devendor ttf-roboto
  ln -svf /usr/share/fonts/TTF/Roboto-Regular.ttf "${pkgdir}/usr/lib/vst3/${pkgname}.vst3/Contents/Resources/Fonts/"

  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CONTRIBUTING,GOVERNANCE,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
