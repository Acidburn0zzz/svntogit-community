# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Charles Mauch <cmauch@gmail.com>

pkgname=perl-crypt-blowfish
pkgver=2.12
pkgrel=6
pkgdesc="Perl/CPAN Module Crypt::Blowfish : XSbased implementation of Blowfish"
arch=("i686" "x86_64")
url="http://search.cpan.org/dist/Crypt-Blowfish"
license=("GPL" "PerlArtistic")
source=("http://search.cpan.org/CPAN/authors/id/D/DP/DPARIS/Crypt-Blowfish-$pkgver.tar.gz"
	"perl-5.18.patch")
md5sums=('a0eca17addc8bdaf38c044c365a8800c'
         '700129e690f239f9c1a318fffd3504c7')

prepare() {
  cd $srcdir/Crypt-Blowfish-$pkgver
  patch -p1 <$srcdir/perl-5.18.patch
}

build() {
  cd $srcdir/Crypt-Blowfish-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor 
  make
}

package() {
  cd $srcdir/Crypt-Blowfish-$pkgver
  make pure_install doc_install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
