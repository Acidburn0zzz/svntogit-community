# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Jokler <aur@jokler.xyz>

pkgname=bacon
pkgver=2.0.1
pkgrel=2
pkgdesc="A background rust code checker"
arch=('x86_64')
url="https://dystroy.org/bacon"
license=('AGPL3')
depends=('gcc-libs')
makedepends=('git' 'rust')
options=('!lto')
_commit='06149aa5f174cb646f5d43a1c2127162e3c619a6'
source=("$pkgname::git+https://github.com/Canop/bacon.git#commit=$_commit")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname"

  cargo test --frozen --all-features
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
}
