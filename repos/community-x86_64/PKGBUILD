# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-dolphin
pkgver=29760
pkgrel=1
pkgdesc='Nintendo GC/Wii core'
arch=(x86_64)
url=https://github.com/libretro/dolphin
license=(GPL2)
groups=(libretro)
depends=(
  bluez-libs
  hidapi
  libretro-core-info
  libudev.so
  libx11
  libxi
  libxrandr
  mbedtls
  lzo
  sfml
  systemd-libs
  zlib
)
makedepends=(
  cmake
  git
  mesa
  python
)
provides=(libretro-dolphin)
conflicts=(libretro-dolphin)
source=(libretro-dolphin::git+https://github.com/libretro/dolphin.git#commit=08efaa3b9d010101e95fc4f37f3564527c28421d)
sha256sums=(SKIP)

pkgver() {
  cd libretro-dolphin

  git rev-list --count HEAD
}

build() {
  cmake -S libretro-dolphin -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBRETRO=ON \
    -DLIBRETRO_STATIC=1
  make -C build
}

package() {
  install -Dm 644 build/dolphin_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
