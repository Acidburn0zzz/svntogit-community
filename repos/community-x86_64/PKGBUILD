# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=liquidsfz
pkgver=0.2.2
pkgrel=1
pkgdesc="SFZ Sampler"
arch=('x86_64')
url="https://github.com/swesterfeld/liquidsfz"
license=('LGPL2.1')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('jack' 'libsndfile' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
provides=('libliquidsfz.so' 'soundfont-synthesizer')
source=("$pkgname-$pkgver.tar.gz::https://github.com/swesterfeld/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('265275e641542b951da288c6bcaabbcb823e56039d0f981e521b715843078c42d6e00460c5ad0f3439f533cec8c752fc68498564a74fed8febcfd55d08d8d8e1')
b2sums=('994b0e5af28a497f6c781b362f5f2f807d9bf0a7e10f376ab01a348d6875e43663595a8801ed4c5e6e88caa6ba84e4fa74dfb6221122c34c0ea9785051936e42')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --disable-static-cxx
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
  LD_LIBRARY_PATH="lib/.libs/:$LD_LIBRARY_PATH" lv2lint -Mpack -I "${PWD}/lv2" "http://spectmorph.org/plugins/liquidsfz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {NEWS,TODO,{OPCODES,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
