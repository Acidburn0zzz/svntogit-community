# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.4.0
pkgrel=1
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'libchewing' 'libxtst')
makedepends=('swig' 'cmake' 'gob2')
install=ibus-chewing.install
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz)
md5sums=('dcbb3b2b6f4d57f4964c8b5cf3421bd3')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"

  sed  -i '0,/COMMAND ${CMAKE_COMMAND} -E remove -f ${CMAKE_CACHE_TXT}/! s/COMMAND ${CMAKE_COMMAND} -E remove -f ${CMAKE_CACHE_TXT}//' Modules/ManageVersion.cmake 

  cmake . -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    -DLIBEXEC_DIR=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-Source"
  make DESTDIR="${pkgdir}" install
}
