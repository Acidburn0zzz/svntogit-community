# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.40.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('atkmm' 'bluez-libs' 'cairo' 'cairomm' 'gcc-libs' 'gdk-pixbuf2'
'glibc' 'glibmm' 'gtkmm3' 'libsigc++' 'libx11' 'pangomm' 'ttf-roboto')
makedepends=('avahi' 'curl' 'boost' 'eigen' 'faust' 'fftw' 'glib2' 'gperf'
'gtk3' 'intltool' 'jack' 'ladspa' 'liblrdf' 'lilv' 'lv2' 'pango'
'sassc' 'waf' 'zita-convolver' 'zita-resampler')
provides=('guitarix2' 'libgxw.so' 'libgxwmm.so')
conflicts=('guitarix2')
replaces=('guitarix2')
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz")
sha512sums=('902e4d924cae68a477518cb87d7b63edb8716db93e11add921e733c315f2646652fb1b434d182e836e1e866035cabe22a41d8dc66df0b6156b5242cdd31d51e8')

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=('libavahi-common.so' 'libavahi-gobject.so' 'libboost_iostreams.so'
  'libcurl.so' 'libfftw3f.so' 'libgdk-3.so' 'libgio-2.0.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libjack.so' 'liblilv-0.so' 'liblrdf.so'
  'libpangocairo-1.0.so' 'libpango-1.0.so' 'libsndfile.so'
  'libzita-convolver.so' 'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
