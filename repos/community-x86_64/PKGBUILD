# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=1.0.13
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('dtkwidget')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('568f97d83653fab2876f6e46b33593313a8a7b2505379bb446d9bdb15c3a348d346fcdf3cdc1788ed0d55ef6c9f6ccce652c4ddc4a27d28c77e08ea508c895d7')

build() {
  cd dde-calendar-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-calendar-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
