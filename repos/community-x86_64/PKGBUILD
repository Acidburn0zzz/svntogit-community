# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=stylish-haskell
pkgver=0.6.1.0
pkgrel=2
pkgdesc="Haskell code prettifier"
url="https://github.com/jaspervdj/stylish-haskell"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=8.0.1" "haskell-aeson" "haskell-src-exts" "haskell-mtl"
             "haskell-optparse-applicative" "haskell-strict" "haskell-syb" "haskell-yaml")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        hse-1.18.patch)
sha256sums=('eef85fe3940779e092c3a3ffa26c17ae6c96625a5fa606f0c816a37fce357b0d'
            '97d15de9eb0d2848bc5f95bb84080c06e46344452ccf259f8157a13d0b21e15a')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../hse-1.18.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}

    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
