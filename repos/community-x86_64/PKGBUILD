# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200817
_pkgver=2020-08-17
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('6e5be5a6f8905519746f55cee457a9a3ac1fc78e43963a633bfc9a3a351c63594a25b7ff087537cafd07628fa622148eaf30648426511359e9a345aa9f2bb6b4')

prepare() {
  cd $pkgname-$_pkgver
  sed -i '/ensure_rustfmt()?;/d' xtask/src/lib.rs
}

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$_pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
