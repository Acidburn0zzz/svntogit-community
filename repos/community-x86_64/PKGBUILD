# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.3.2
pkgrel=1
pkgdesc='Korean input engine for IBus'
arch=('i686' 'x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul')
makedepends=('intltool')
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz ibus-1.4.0.patch)
md5sums=('b8132e8b11afadd803b2602c63294da5'
         'edf93824ab2eead1307088d3edc24dbc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}/ibus-1.4.0.patch"
  PYTHON=python2 ./configure --prefix=/usr --libexecdir=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make NO_INDEX=true DESTDIR="${pkgdir}" install
}
