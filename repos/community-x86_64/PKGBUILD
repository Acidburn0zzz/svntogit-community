# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-datastore
pkgver=0.114
pkgrel=1
pkgdesc="Sugar datastore service"
arch=('x86_64')
url="https://sugarlabs.org/"
license=('GPL')
depends=('python2-dbus' 'python2-gobject' 'python2-xapian')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('eb4256f1cbf90a9a7d5dd30446e0d0d1a240db02e3104a6339e2f4497dcda4c8')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/copy-to-journal
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
