# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Contributor: Marti Raudsepp <marti@juffo.org>

pkgname=libdwarf
pkgver=20190104
pkgrel=1
pkgdesc="A library for handling DWARF Debugging Information Format"
arch=(x86_64)
license=('GPL' 'LGPL')
url="https://www.prevanders.net/dwarf.html"
depends=('elfutils')
options+=('staticlibs')
source=(https://www.prevanders.net/libdwarf-$pkgver.tar.gz)
sha512sums=('6f6891df47cd76aa8ee5cbbad91c43de2c6a9b6cee27c8f5f64dc9df3e37c577718f8056e7a71fba6215c7751ccb834935f52532032eb16bae2c26b3a3450677')

build() {
  cd "$srcdir"/libdwarf-$pkgver
  ./configure --prefix=/usr --includedir=/usr/include/libdwarf --enable-shared
  make
}

check() {
  cd "$srcdir"/libdwarf-$pkgver
  make check
}

package() {
  cd "$srcdir"/libdwarf-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir"/usr/share/doc/$pkgname
  install -m644 README NEWS "$pkgdir"/usr/share/doc/$pkgname/
}
