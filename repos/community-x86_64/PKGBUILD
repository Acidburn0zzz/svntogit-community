# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-readme-renderer
pkgname=(python-readme-renderer python2-readme-renderer)
pkgver=23.0
pkgrel=1
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('x86_64')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bleach' 'python2-bleach'
             'python-cmarkgfm' 'python2-cmarkgfm' 'python-docutils' 'python2-docutils'
             'python-future' 'python2-future' 'python-pygments' 'python2-pygments' 'python-six'
             'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('4299cc064930de854f1b9bd110d105c88f335ecc416703cd61b8db6597f0f6e80753bbda74e26bb1942ad6c59fa3e3fef9011f92da6407588217522fd247aa2a')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || warning "Tests failed"
  python2 setup.py pytest || warning "Tests failed"
}

package_python-readme-renderer() {
  depends=('python-bleach' 'python-cmarkgfm' 'python-docutils' 'python-future' 'python-pygments'
           'python-six')

  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-readme-renderer() {
  depends=('python2-bleach' 'python2-cmarkgfm' 'python2-docutils' 'python2-future' 'python2-pygments'
           'python2-six')

  cd readme_renderer-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
