# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=1.4.0
pkgrel=2
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-tool-kit' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual')
makedepends=('git' 'qt5-tools')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-file-manager.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  sed -i 's/-0-2//g' dde-file-manager/*/*.pro
}

build() {
  cd dde-file-manager
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager
  make INSTALL_ROOT="$pkgdir" install

  rm "$pkgdir"/usr/share/dbus-1/interfaces/org.freedesktop.FileManager1.xml
}
