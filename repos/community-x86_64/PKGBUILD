# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.5
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('fluidsynth' 'glib2' 'jack' 'libsndfile' 'libutil-linux' 'ncurses'
'nuitka')
optdepends=('non-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('7446192ff8b7fc57446b636fa7c81fa65d70a4f71c1bb6fefadc64cf1e2c2ec3b4acd29b89460616f50a559bb84db49abb6c78e366ae2c9058e2c3e6f1e38b8b'
            'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so' 'libglib-2.0.so' 'libjack.so' 'libncursesw.so'
  'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
