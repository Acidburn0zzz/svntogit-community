# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
pkgver=2.3.0
pkgrel=1
pkgdesc="An open-source time-series database optimized for fast ingest and complex queries"
arch=('x86_64')
url="https://www.timescale.com/"
license=('Apache')
depends=('postgresql' 'postgresql-libs')
makedepends=('gcc' 'cmake')
install=timescaledb.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/timescale/timescaledb/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('1dac0969fb90840ccf5e3e458643903c56ded6028b6be32a6d0a02088333404f20d02509a22a80e0aad4903b61d7be9d4b9fe17cd4f60e8ba055620b15e8d6f3')

build() {
    cd "${pkgname}-${pkgver}"
    ./bootstrap -DWARNINGS_AS_ERRORS=OFF -DREGRESS_CHECKS=OFF
    cd build && make
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir/" install
}
