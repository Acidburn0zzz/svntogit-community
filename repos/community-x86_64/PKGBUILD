# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-subtitles
pkgver=1.4
pkgrel=1
pkgdesc="Video subtitle editor for GNOME"
arch=('x86_64')
url="http://www.gnomesubtitles.org/"
license=('GPL')
depends=('gst-plugins-base' 'gtk-sharp-3' 'gtkspell3')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('77d739dd392203f6d1d91f603b961f7412346c71799314c00081e9e4c680634e')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/libenchant.so.1/libenchant-2.so.2/' src/GnomeSubtitles/Execution/gnome-subtitles.exe.config
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make -j1
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
