# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kirigaya <kirigaya@mkacg.com>

pkgname=deepin-topbar
pkgver=0.6.6
pkgrel=3
pkgdesc="This is topbar for Deepin"
arch=('x86_64')
url="https://github.com/kirigayakazushin/deepin-topbar"
license=('GPL')
depends=('deepin-network-utils' 'deepin-qt5integration' 'deepin-qt-dbus-factory' 'libdbusmenu-qt5'
         'procps-ng')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kirigayakazushin/deepin-topbar/archive/$pkgver.tar.gz"
        $pkgname-dde-v20.patch)
sha512sums=('415f6e4eadbe78fe1ab4ef61c5f32f8c945fea56556458a88592da7094aeaf1a03cac2f8ac4a2b0f0d3506368ff3f9f3ae19b5683892f5b995024225f06a65dc'
            '4109ca2aea0c35d2c2c8a91dda43776f24420446ded2f41d0e6feb210fdf7130dfdba449d32d1ce3318389749ec242a815ac58f0b143e36788f31de835d24c8c')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-dde-v20.patch
}

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
