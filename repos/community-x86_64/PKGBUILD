# $Id$
# Maintainer: Stefan Husmann  <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=0.99.1.2
pkgrel=2
pkgdesc="A python plotting library, making publication quality plots"
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/"
license=('custom')
backup=(usr/lib/python2.6/site-packages/matplotlib/mpl-data/matplotlibrc)
depends=('python-dateutil' 'python-pytz' 'python-numpy')
optdepends=('tk: used by the TkAgg backend' 'pygtk: for use with the GTK or GTKAgg backend' 'wxpython: for use with the WXAgg backend' 'pygobject: for use with vala' 'qt:for use with the QT4 backend' 'qt3: for use with qt backend')
makedepends=('tk' 'wxpython' 'pygtk' 'qt' 'qt3')
provides=('matplotlib')
source=(license libpng-1.4.patch \
"http://downloads.sourceforge.net/matplotlib/matplotlib-$pkgver.tar.gz")
md5sums=('5e579927a4d338bba112471e52555d02' '0756cabf53b170e899557d1fb85b3d64'\
         '952e2c992e4a762b8538171f51c9140a')
sha1sums=('904d2075d7eaadd79e3bb670d6e6a1f15a78348a' 'bf77d3e87c5dc14749ad58683efd2a9af99a3fb8'\
         'c3f9053a8522e2541d93ee55e8b447165708ac15')

build() {
    cd $srcdir/matplotlib-0.99.1.1 # yes, not $pkgver
    patch -p1 < ../libpng-1.4.patch || return 1
    sed -i "s/rc =.*/rc = dict({'backend':'GTKAgg', 'numerix':'numpy'})/g" \
      setup.py || return 1
    sed -i "s/^BUILD_GTKAGG .*/BUILD_GTKAGG = 1/g" setup.py || return 1
    sed -i "s/^BUILD_GTK .*/BUILD_GTK = 1/g" setup.py || return 1
    sed -i "s/^BUILD_WXAGG .*/BUILD_WXAGG = 1/g" setup.py || return 1
    sed -i "s/^BUILD_TKAGG .*/BUILD_TKAGG = 1/g" setup.py || return 1
    sed -i "s/^BUILD_QT4 .*/BUILD_QT4 = 1/g" setup.py || return 1
    sed -i "s/^BUILD_QT .*/BUILD_QT = 1/g" setup.py || return 1
    sed -i 's/^macosx = True/macosx = False/' setup.py || return 1
    sed -i 's/^pytz = True/pytz = False/' setup.py || return 1
    sed -i 's/^dateutil = True/Dateutil = False/' setup.py || return 1

    python setup.py build || return 1
}
package() {
    cd $srcdir/matplotlib-0.99.1.1
    python setup.py install --root $pkgdir --prefix=/usr || return 1

    rm -rf $pkgdir/usr/lib/python2.6/site-packages/configobj.py* || return 1
    install -Dm644 $srcdir/license \
      $pkgdir/usr/share/licenses/$pkgname/license || return 1
}
