# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=lua-sdl2
pkgver=2.0.5.6.0.r16.g272a748
pkgrel=1
_lua=5.3
pkgdesc='Pure C binding of SDL 2.0 for Lua'
arch=(x86_64)
url='https://github.com/Tangent128/luasdl2'
license=(MIT)
depends=(lua sdl2_gfx sdl2_image sdl2_mixer sdl2_net sdl2_ttf)
makedepends=(lua git sdl2_gfx sdl2_image sdl2_mixer sdl2_net sdl2_ttf cmake ninja)
source=('git+https://github.com/Tangent128/luasdl2#commit=272a748d652c229e9d8c2c749ac74ced00e3ccb3')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname/-}
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  mkdir -p build
  cd build
  cmake ../${pkgname/-} \
    -DWITH_LUAVER=${_lua/./} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_DOCSDIR=share/doc/$pkgname \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 luasdl2/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
