# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.0.2
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('6e68f7c9fd848a3c8fc39341523d5900b2ea72b341fedc6153cf3700e9d29c188e57dce4dd19c6d2eb18edad23dfaf66e29ed659c6f969b9721e2277f95cdf7b')
b2sums=('3e467089b8cdc6bdf296c43e138cc71c316b71dc8a39c9ba78767624a6606db9cc1e9c14c1f12ffc7dbe98a0d9ccabacc5660a1b0a75cebe9ac429eef74f1c5b')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          -Dinstall_binfiles=false \
          ..

    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
