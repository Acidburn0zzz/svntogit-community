# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hunter
pkgname=('python-hunter' 'python2-hunter')
pkgver=2.2.1
pkgrel=1
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
makedepends=('python-setuptools' 'python2-setuptools' 'python-fields' 'python2-fields'
             'python-colorama' 'python2-colorama' 'python-six' 'python2-six' 'cython' 'cython2')
checkdepends=('python-pytest-benchmark' 'python2-pytest-benchmark' 'python-virtualenv'
              'python2-virtualenv' 'python-pytest-runner' 'python2-pytest-runner' 'gdb'
              'python-manhole' 'python2-manhole' 'python-process-tests' 'python2-process-tests')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('d8d6b9c4e909a91b528b8e8e2d35da266d82ebca98a5bf6697e518bac15bc50334912bd370258d4a18fc6514d8b65608679933f51491950e39caf1365b55ba88')

prepare() {
  rm python-hunter-$pkgver/src/hunter/*.c
  cp -a python-hunter-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build

  cd "$srcdir"/python-hunter-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  ) || warning "Tests failed"

  cd "$srcdir"/python-hunter-$pkgver-py2
  virtualenv2 "$srcdir/pyvenv-py2" --system-site-packages
  (
    . "$srcdir/pyvenv-py2/bin/activate"
    python2 setup.py install
    python2 setup.py pytest
  ) || warning "Tests failed"
}

package_python-hunter() {
  depends=('python-fields' 'python-colorama' 'python-six')
  optdepends=('python-manhole: for remote support'
              'gdb: for remote support')

  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hunter() {
  depends=('python2-fields' 'python2-colorama' 'python2-six')
  optdepends=('python2-manhole: for remote support'
              'gdb: for remote support')

  cd python-hunter-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/hunter-trace{,2}
}
