# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-echo
pkgver=0.60
pkgrel=2

_modname="${pkgname#nginx-mod-}"
_nginxver=1.12.1

pkgdesc='Nginx module that provides directives "echo", "sleep", "time" and more'
arch=('i686' 'x86_64')
depends=("nginx=$_nginxver")
url="https://github.com/openresty/echo-nginx-module"
license=('BSD')

source=(
	http://nginx.org/download/nginx-$_nginxver.tar.gz
	https://github.com/openresty/$_modname-nginx-module/archive/v$pkgver/$_modname-$pkgver.tar.gz
	$pkgname-nginx-1.12.patch::https://patch-diff.githubusercontent.com/raw/openresty/echo-nginx-module/pull/65.patch
)

sha256sums=('8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb'
            '1077da2229ac7d0a0215e9e6817e297c10697e095010d88f1adbd1add1ce9f4e'
            '1d2cb5f3977cd6ee0a5ae5958ac4cca79cf5ecc097e68b72cf03a2e9f48e51ed')

prepare() {
	cd $_modname-nginx-module-$pkgver
	patch -p1 -i "$srcdir"/$pkgname-nginx-1.12.patch
}

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --add-dynamic-module=../$_modname-nginx-module-$pkgver
	make modules
}

package() {
	install -Dm644 "$srcdir/"$_modname-nginx-module-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in *.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
