# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSEQuencer
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BSEQuencer"
pkgname=bsequencer
pkgver=1.8.6
pkgrel=1
pkgdesc="Multi channel MIDI step sequencer LV2 plugin with a variable matrix"
arch=('x86_64')
url="https://github.com/sjaehn/BSEQuencer"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('eddb25f3ae008f2497f3a5e98b1601e1a1d0d79bd3bc112cc0b463b92a9bc88dc33715915ce7bde529a9238b903c78fddab5cd6fe8e08570d2f75f1e667f11e2')
b2sums=('17f6a6c7f336a2e5ae2dd541f2e1e73d20f5a72c40a5afdf67ba00c5ee573442effd67f8ed8b1a32b2f2917d72aa43ea72fd5a1a03454be363186a2e0d61b2bd')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "${_plugin_uri}"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
