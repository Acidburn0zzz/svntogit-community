# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=myodbc
pkgver=5.1.11
pkgrel=2
pkgdesc="ODBC driver/connector for MySQL"
arch=(i686 x86_64)
url="http://dev.mysql.com/downloads/connector/odbc/"
depends=('unixodbc' 'libmysqlclient')
license=('GPL')
options=('libtool')
source=("http://mysql.skynet.be/Downloads/Connector-ODBC/5.1/mysql-connector-odbc-$pkgver-src.tar.gz"
	myconf.h.in)
md5sums=('1d868f404038bbce6623a28255c8a9f7'
         '6c99c83e309b18d15ef0f07f0b461241')

build() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  cp $srcdir/myconf.h.in driver/myconf.h.in
  patch driver/Makefile.am <<EOF
55c55,56
< driver.h
---
> driver.h \\
> my_stmt.c
EOF
  [ -x configure ] || { aclocal; libtoolize; automake --add-missing; autoreconf; }
  [ -f Makefile ] || CFLAGS="$CFLAGS -DTHREAD" \
	./configure --prefix=/usr --sysconfdir=/etc --with-unixODBC=/usr --with-odbc-ini=/etc/odbc.ini \
		--with-mysql-path=/usr --without-x --disable-gui --disable-test
  make
}

package() {
  cd $srcdir/mysql-connector-odbc-${pkgver}-src
  make DESTDIR=$pkgdir install
}
