# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.3.5
pkgrel=3
pkgdesc='Computer algebra system designed for fast computations in number theory'
arch=('i686' 'x86_64')
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
depends=('gmp' 'readline')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail'
            'libx11: high precision plots')
source=("${url}pub/pari/unix/$pkgname-$pkgver.tar.gz")
sha1sums=('f211ccfe42a4b18dbcde832dfd1ece6f477f4966')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./Configure --prefix=/usr --with-gmp --with-readline
	make all
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir/" install

	install -m 644 -t "$pkgdir/usr/include/$pkgname/" Olinux-$CARCH/*.h

	# symlink pointing to a symlink that is going to be moved by zipman
	rm "$pkgdir"/usr/share/man/man1/pari.1
	ln -s gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1.gz
}
