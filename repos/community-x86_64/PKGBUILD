# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=yubikey-touch-detector
pkgver=1.2.0
pkgrel=2
pkgdesc='A tool that can detect when your YubiKey is waiting for a touch'
arch=('x86_64')
url='https://github.com/maximbaz/yubikey-touch-detector'
license=('MIT')
depends=('glibc')
makedepends=('go-pie')
optdepends=('gnupg: for GPG'
            'openssh: for SSH'
            'pam-u2f: for U2F')
source=("${pkgname}-${pkgver}.tar.gz::${url}/releases/download/${pkgver}/${pkgname}-src.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::${url}/releases/download/${pkgver}/${pkgname}-src.tar.gz.sig")
sha256sums=('9f15f435bcb898f001e159ef107861d5c41dbe6828f1b22734abc30444e7c207'
            'SKIP')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

prepare() {
    mkdir -p "gopath/src/github.com/maximbaz"
    ln -rTsf "${pkgname}" "gopath/src/github.com/maximbaz/${pkgname}"
}

build() {
    export GOPATH="${srcdir}/gopath"
    cd "gopath/src/github.com/maximbaz/${pkgname}"
    make build
}

package() {
    cd "${pkgname}"
    install -Dm755 -t "${pkgdir}/usr/bin" "${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/lib/systemd/user" "${pkgname}.service"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
}

# vim:set ts=4 sw=4 et:
