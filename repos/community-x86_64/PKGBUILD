# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
pkgname=setconf
pkgver=0.2
pkgrel=8
pkgdesc="A utility to easily change settings in Makefiles and other configuration files"
arch=('x86_64' 'i686')
url="http://setconf.roboticoverlords.org/"
license=('GPL')
depends=('pcre' 'gc')
makedepends=('shedskin>=0.1.2')
md5sums=('ccd0ca9527d8406d5393d77a6a56686b')
source=("http://setconf.roboticoverlords.org/$pkgname-$pkgver.tbz2")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  shedskin "$pkgname.py"
  make CPPFLAGS="-march=native -fomit-frame-pointer"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname.1.gz" \
    "$pkgdir/usr/share/man/man1/$pkgname.1.gz"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
