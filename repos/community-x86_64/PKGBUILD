# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: gumper <gumper1034@gmail.com>

pkgname=libalkimia
pkgver=8.0.1
pkgrel=1
pkgdesc="A library with common classes and functionality used by finance applications for the KDE SC"
arch=('x86_64')
url="https://community.kde.org/Alkimia/libalkimia"
license=('LGPL')
depends=('qt5-webkit' 'knewstuff' 'kdelibs4support' 'hicolor-icon-theme')
makedepends=('cmake' 'doxygen' 'extra-cmake-modules' 'kdesignerplugin' 'plasma-framework' 'kpackage' 'kdoctools')
source=(https://ftp.acc.umu.se/mirror/kde.org/ftp/stable/alkimia/$pkgver/alkimia-$pkgver.tar.xz{,.sig})
validpgpkeys=('D69A745A55331F44F404D8258D4DE062AA2EB01C'  # Thomas Baumgart <thb@net-bembel.de>
              '8C13BC14185A65215B592A2F1243132CD9321771') # Ralf Habacker <ralf.habacker@freenet.de>
sha256sums=('f5601780f6ed95b5e14dd3a243ecd1afbdbdb522f0e32dec2fb86933ec363115'
            'SKIP')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../alkimia-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -Wno-dev
  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
