# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-notificationd
pkgver=0.14.1
pkgrel=1
pkgdesc="LXQt notification daemon and library."
arch=("x86_64")
groups=("lxqt")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base" "liblxqt" "kwindowsystem")
makedepends=("cmake" "lxqt-build-tools")
provides=("notification-daemon")
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
)
sha512sums=('dcd1892ff66d6beb5c8953c6731f798a59272365fb7439277732b145cc9e6479314415481d9e8aae9fd670d779011c3d7bb3874b05a67cfad2a722f467866795'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
