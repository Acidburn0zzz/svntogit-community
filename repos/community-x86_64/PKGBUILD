# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=2.0.3
pkgrel=1
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('i686' 'x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql>=9.2' 'postgresql<9.3' 'proj' 'geos' 'gdal' 'json-c' 'libxml2')
changelog=$pkgname.changelog
options=('!libtool')
source=(http://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7f865a6fdf19afed7d2f3e7178cd504f7254a9e96f2ce6a07d0ea19edc1668a0')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  
  ./configure --prefix=/usr
  make

  # Build utils (FS#25836)
  cd utils
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd utils
  make DESTDIR=${pkgdir} install
}
