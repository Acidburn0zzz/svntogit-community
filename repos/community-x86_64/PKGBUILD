# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.2
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libxcb' 'qt5-base' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'drumstick' 'qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a2357b41f6dda1187795c28174ff8377fffa3a69153b2d39302b515ba433362de25f8cee07dc82bc0e584685096e7fddad6dbe8f81ce336211bae81c7eb1085e')
b2sums=('bf99ad54eeb90b66d0f6dc24f8c5967da91623d582e258a0a3214f9d883b3e97f3252c89dafa5054e11af73e4a59fa47b247c2a58218e7b6d8bf504178f3be97')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libdrumstick-rt.so' 'libdrumstick-widgets.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
