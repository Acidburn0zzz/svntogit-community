# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=audacity
pkgver=2.3.3
pkgrel=1
epoch=1
pkgdesc="A program that lets you manipulate digital audio waveforms"
arch=('x86_64')
url="https://audacityteam.org"
license=('GPL2' 'CCPL')
groups=('pro-audio')
# upstream enquired via mail about:
# vendors/requires older version of portaudio
# vendors/doesn't detect system version of portmidi
depends=('expat' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'gtk3' 'libFLAC.so'
'libFLAC++.so' 'libid3tag' 'libjack.so' 'liblilv-0.so' 'libmad' 'libogg'
'libsndfile' 'libsoxr' 'libsuil-0.so' 'libtwolame.so' 'libvamp-hostsdk.so'
'libvorbis.so' 'libvorbisenc.so' 'libvorbisfile.so' 'portsmf' 'soundtouch'
'wxgtk3')
makedepends=('cmake' 'ffmpeg' 'lv2')
optdepends=('ffmpeg: additional import/export capabilities')
source=("https://github.com/${pkgname}/${pkgname}/archive/Audacity-${pkgver}.tar.gz")
sha512sums=('766cc6cf21ff79a74f02497c21d8d28e1d06479218a0064fa252b187cc655845da3615045fc3f1088111441121b40147378dbb5078a4bb480bdd2224d09db3ba')

prepare() {
  mv -v "${pkgname}-Audacity-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  WX_CONFIG=wx-config-gtk3 ./configure --prefix="/usr" \
    --with-libsndfile="system" \
    --with-ffmpeg="system" \
    --with-expat="system" \
    --with-lame="system" \
    --with-libsoxr="system" \
    --with-portaudio \
    --with-portmidi \
    --with-libflac \
    --with-libid3tag \
    --with-libsamplerate \
    --with-sbsms \
    --with-soundtouch \
    --with-libtwolame \
    --with-libvorbis \
    --with-lv2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
