# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libiio
pkgver=0.18
pkgrel=3
pkgdesc="Interface to the Linux Industrial Input/Output (IIO) Subsystem"
arch=('x86_64')
url="https://github.com/analogdevicesinc/libiio"
license=('LGPL2.1')
depends=('glibc' 'libaio' 'libserialport' 'libusb-1.0.so' 'libxml2')
makedepends=('cmake')
provides=('libiio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/analogdevicesinc/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('220154473fa33784ba2803936abda4872040ab6d86201c772574e4296b18d0fad470aa956bd13cebf1255e7072757fe01e5813f162aa0a304d0737a1a9196722')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_SBINDIR=bin \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DSYSTEMD_UNIT_INSTALL_DIR=/usr/lib/systemd/system \
        -DWITH_SYSTEMD=ON \
        -Bbuild \
        .
  make -C build VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
}
