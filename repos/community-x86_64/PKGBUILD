# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=5.1.0.12.20200429
_commit=d9f20a322c59a80d0bd90007fb564422ab410843
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('deepin-dock' 'cmake' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$_commit.tar.gz"
        $pkgname-build-fix.patch)
sha512sums=('91ff1f323471dc3c2493fe91e0df2fce304f5b910f9ca60c5e5ab72061214e01d5d60d9238c223a4fb67bc1045fcc6a9e0903f3a854d22c734f7e553bcce991b'
            'f19cbef03edb458b5dce941b9d4f25d4b0838490da37950ed6ab0c7b13b8ce236ff2115718f3fece230d7c5dbb2d74970695ac55d5f03b9ebf6bd7b8553e40aa')

prepare() {
  mv dde-control-center-{$_commit,$pkgver}
  patch -d dde-control-center-$pkgver -p1 < $pkgname-build-fix.patch
  mkdir -p build

  cd dde-control-center-$pkgver
  sed -i 's|/bin/restore-tool|/usr/bin/restore-tool|' src/frame/window/modules/systeminfo/backupandrestoreworker.cpp \
                                                      com.deepin.controlcenter.restore.policy
  sed -i 's| /bin| /usr/bin|' src/restore-tool/CMakeLists.txt

  # remove after they obey -DDISABLE_SYS_UPDATE properly
  sed -i '/new UpdateModule/d' src/frame/window/mainwindow.cpp
}

build() {
  cd build
  cmake -DDISABLE_SYS_UPDATE=YES -DCMAKE_INSTALL_PREFIX=/usr \
    ../dde-control-center-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
