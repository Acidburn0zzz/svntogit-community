# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-mupen64plus-next
pkgver=192
pkgrel=1
epoch=1
pkgdesc='Nintendo 64 core'
arch=(x86_64)
url=https://github.com/libretro/mupen64plus-libretro-nx
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libpng
  libretro-core-info
)
makedepends=(
  git
  mesa
  nasm
)
replaces=(libretro-mupen64plus)
source=(libretro-mupen64plus-next::git+https://github.com/libretro/mupen64plus-libretro-nx.git#commit=39048f20d0a28e906095a744f1b1553dfb16c7df)
sha256sums=(SKIP)

pkgver() {
  cd libretro-mupen64plus-next

  git rev-list --count HEAD
}

build() {
  make -C libretro-mupen64plus-next WITH_DYNAREC=x86_64
}

package() {
  install -Dm 644 libretro-mupen64plus-next/mupen64plus_next_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
