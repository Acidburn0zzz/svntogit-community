# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Adri√† Arrufat <swiftscythe@gmail.com>
# Contributor: Mark Lee <mark@markelee.com>

pkgname=mediastreamer
pkgver=2.15.1
pkgrel=2
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=('i686' 'x86_64')
url="https://github.com/BelledonneCommunications/mediastreamer2"
license=('GPL')
depends=('ortp' 'ffmpeg' 'libxv' 'libupnp' 'bzrtp' 'glew' 'libsrtp' 'mbedtls' 'bctoolbox')
# xxd from Vim is needed to build
makedepends=('intltool' 'vim' 'bcunit')
source=("$pkgname-$pkgver.tar.gz::https://github.com/BelledonneCommunications/mediastreamer2/archive/$pkgver.tar.gz")
validpgpkeys=('9774BC1725758EB16D639F8B3ECD52DEE2F56985')
sha256sums=('5e7d3eefc3ca807711da2cc279f1a05d7a24e0aa6e9dfe912fc203419880c852')

build() {
  cd ${pkgname}2-$pkgver
#  export CFLAGS="$CFLAGS -Dbctbx_list_remove_link=bctbx_list_unlink -Dbctbx_list_delete_link=bctbx_list_erase_link"
  [ -x ./configure ] || ./autogen.sh
  ./configure --prefix=/usr \
    --enable-glx --enable-xv --enable-zrtp --enable-external-ortp \
    --disable-strict
  make
}

package() {
  cd ${pkgname}2-$pkgver
  make DESTDIR="${pkgdir}" install
}
