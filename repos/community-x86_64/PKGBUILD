# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.6.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'glew' 'glibc' 'glu' 'libevdev' 'libfaust.so' 'libfreetype.so'
'libinput' 'libglvnd' 'libvterm' 'libx11' 'ttf-fira-code')
makedepends=('lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('4a6f5169da579ec2c55d9b839c836e6d31fc0cf770841a88491c68bc207cb811c9e114f61b5a72a3d3064a25014e6bdf8dc170b240c5bc42b1ae37039e323c93')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  # devendoring ttf-fira-code
  rm -vf "${pkgdir}/usr/lib/lv2/${pkgname}/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf "/usr/share/fonts/TTF/FiraCode-${font_type}.ttf" \
      "${pkgdir}/usr/lib/lv2/${pkgname}/"
  done
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
