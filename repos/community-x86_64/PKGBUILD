# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Elrondo46 TuxnVape <elrond94@hotmail.com>
# Contributor: Ivan Fonseca <ivanfon@riseup.net>
# Contributor: Alfredo Ramos <alfredo dot ramos at yandex dot com>
# Contributor: Giacomo <giacomogiorgianni at gmail dot com>

pkgname=vokoscreen
_pkgname=vokoscreenNG
pkgver=3.0.4
pkgrel=3
pkgdesc='Easy to use screencast creator'
arch=(x86_64)
url='https://linuxecke.volkoh.de/vokoscreen/vokoscreen.html'
license=(GPL2)
depends=(gst-plugins-good qt5-multimedia qt5-x11extras)
optdepends=('gst-plugins-bad: record audio into AAC format'
            'gst-plugins-ugly: record screen into H.264 format')
makedepends=(qt5-tools)
source=(https://github.com/vkohaupt/$_pkgname/archive/$pkgver/$_pkgname-$pkgver.tar.gz
        qt-5.15.patch
        0001-Add-better-integration-for-Linux.patch)
sha256sums=('f404f2a1b6d342fe2d8c97849dcd54bd2070bebf840bdb247e634229f47b9871'
            '41b2e88669aa2e347298f0d2bf4b4bedffa0fba8a19cb1d6a6d50067fe76ee31'
            '15c20c70cfd515aba6bb758d7e7fadf1e414553ce42303846bc642dc39241785')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../qt-5.15.patch
  patch -Np1 -i ../0001-Add-better-integration-for-Linux.patch
}

build() {
  cd $_pkgname-$pkgver
  qmake PREFIX=/usr src/vokoscreenNG.pro
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
