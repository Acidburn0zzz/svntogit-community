# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.6
pkgrel=1
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('MIT' 'GPL2')
depends=('libmng' 'librsvg' 'curl' 'lcms' 'desktop-file-utils'
         'libnsbmp>=0.1.4' 'libnsgif>=0.1.4' 'libcss>=0.6.1'
         'libdom>=0.3.1' 'libnsutils>=0.0.3' 'libutf8proc>=1.3.1'
         'gtk3' 'duktape')
makedepends=('re2c' 'netsurf-buildsystem' 'perl-html-parser' 'nsgenbind>=0.3'
             'inetutils' 'libutf8proc>1.3.1' 'git' 'setconf' 'check' 'gtk3'
             'duktape')
source=("git://git.netsurf-browser.org/netsurf.git#tag=release/$pkgver"
        netsurf.sh)
sha256sums=('SKIP'
            'f2e2f61f1864da57cafa74ffdbefac2f9e1d55d9627e82ef923d4931684c5c49')

prepare() {
  cd "$pkgname"

  sed 's:netsurf-gtk:netsurf:' -i frontends/gtk/res/netsurf-gtk.desktop
  sed 's:libutf8proc/::' -i utils/idna.c
  sed 's:UTF8PROC_CCC_VIRAMA:UTF8PROC_BIDI_CLASS_EN:' -i utils/idna.c
  sed 's:utf8proc_normalise:utf8proc_reencode:' -i utils/idna.c

  setconf Makefile.defaults NETSURF_UA_FORMAT_STRING '"NetSurf/%d.%d (%s; Arch Linux)"'
  setconf Makefile.defaults NETSURF_USE_DUKTAPE YES
}

build() {
  make -C "$pkgname" \
    NETSURF_GTK_MAJOR=3 \
    TARGET=gtk \
    LIBDIR=lib \
    INCLUDEDIR=include \
    PREFIX=/usr
}

package() {
  cd "$pkgname"

  make install LIBDIR=lib INCLUDEDIR=include PREFIX=/usr TARGET=gtk \
    NETSURF_GTK_MAJOR=3 DESTDIR="$pkgdir"

  # Script
  install -Dm755 "../$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop shortcut
  install -Dm644 "frontends/gtk/res/$pkgname.xpm" \
    "$pkgdir/usr/share/pixmaps/$pkgname.xpm"
  install -Dm644 "frontends/gtk/res/$pkgname-gtk.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"

  # License
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  # Fix path
  #mv "$pkgdir/usr/share/netsurf/:./gtk/res/"* "$pkgdir/usr/share/netsurf/"
  #rmdir "$pkgdir/usr/share/netsurf/"{:./gtk/res,:./gtk,:.}
}

# vim:set ts=2 sw=2 et:
