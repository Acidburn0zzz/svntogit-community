# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=openimagedenoise
pkgver=1.2.0
pkgrel=5
pkgdesc='Intel(R) Open Image Denoise library'
arch=('x86_64')
url='https://openimagedenoise.github.io'
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ninja' 'ispc' 'python')
source=("https://github.com/OpenImageDenoise/oidn/releases/download/v${pkgver}/oidn-${pkgver}.src.tar.gz"
        'fix-ispc.patch::https://github.com/OpenImageDenoise/oidn/commit/e321d7c90a2c706a521a3afd8913af36b121dc9e.patch'
        'fix-unwanted-simd.patch')
sha512sums=('e8386fecdf60424c89f0d08ded88e166f72131e626e7c1c23b510fd4fa46530b7bcd934e5d2c2516ff1c32fe30f0ebd403fe46767a181d7e77023380d53ff6ae'
            'b03916098e771fb0467d32d60aa687c804da1d184956b392489e5943c8d71e439c05d05d809fc8e616a45a1cdcc9425cd29e73bd2222f4b6a2093c3eb09fadb2'
            'e180338d0b1fbfd52c3c7fe29cabcb482a6ec80f6459e7096b7773bd7f3ae77035168cec62d20103d25d14eeb809e769166a9d0f645c4dba8f1a59cdf1d2157b')

prepare() {
    cd oidn-$pkgver

    patch -p1 -i ../fix-ispc.patch
    patch -p1 -i ../fix-unwanted-simd.patch
}

build() {
    cd oidn-$pkgver

    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    ninja -C build
}

package() {
    cd oidn-$pkgver
    DESTDIR="$pkgdir" ninja -C build install
    rm "$pkgdir/usr/bin/tests"
}

