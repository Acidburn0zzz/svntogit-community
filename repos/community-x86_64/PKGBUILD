# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=tokei
pkgver=10.0.1
pkgrel=2
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('e3bc7dd3d1baffc7be0c42541b715ebe5f7357269904589d7f424b299ccb5bc3a1d5aa8c3635c83d82d4cb7f342cd02a68223190491c56bd8574adfe925c3c29')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # --locked won't work until https://github.com/XAMPPRocky/tokei/issues/360 is fixed.
    cargo build --release --features all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
