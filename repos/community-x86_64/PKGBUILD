# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Kuba Serafinowski <zizzfizzix(at)gmail(dot)com>

pkgname=owncloud-client
pkgver=2.9.0.5150
pkgrel=2
pkgdesc='ownCloud client based on mirall'
arch=(x86_64)
url='http://owncloud.org/'
license=(GPL2)
depends=(qtkeychain hicolor-icon-theme libcloudproviders)
makedepends=(extra-cmake-modules python-sphinx qt5-tools doxygen kio)
optdepends=('python-nautilus: integration with Nautilus' 'nemo-python: integration with Nemo' 'python-caja: integration with Caja')
backup=('etc/ownCloud/sync-exclude.lst')
source=(https://download.owncloud.com/desktop/ownCloud/stable/$pkgver/source/ownCloud-$pkgver.tar.xz{,.asc}
        https://github.com/owncloud/client/commit/608c1434.patch
        https://github.com/owncloud/client/commit/a004a91c.patch)
sha256sums=('5acac2639097b486fe9d9a82cd66061f9087a3a73640479ef5605c405047c659'
            'SKIP'
            'e53ce85f65f70b3eaae450892ea5e00bc79d24f469f17112c4b08ccf6749a8ab'
            'acfa37c08b1f6fa5edcaf267fc679534f32268b593b78bde0f86eabb82f50f85')
validpgpkeys=(F05F7DD7953A07DF36579DAA498C45EBE94E7B37) # ownCloud Client Team (Signing Key) <info@owncloud.com>

prepare() {
  patch -d ownCloud-$pkgver -p1 < 608c1434.patch # Fix build with libcloudproviders
  patch -d ownCloud-$pkgver -p1 < a004a91c.patch # Fix build with libcloudproviders
}

build() {
  cmake -B build -S ownCloud-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDATADIR=share
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
