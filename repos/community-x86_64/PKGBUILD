# Maintainer: David Runge <dave@sleepmap.de>
pkgname=spectmorph
pkgver=0.5.0
pkgrel=1
pkgdesc="Analyze samples of musical instruments and combine them (morphing)"
arch=('x86_64')
url="http://spectmorph.org/"
license=('LGPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'fftw' 'jack' 'libao' 'qt5-base')
makedepends=('lv2')
optdepends=('python2: For sminstbuilder')
source=("http://www.spectmorph.org/files/releases/${pkgname}-${pkgver}.tar.bz2"
        "http://spectmorph.org/files/instruments/${pkgname}-instruments-${pkgver}.tar.xz")
sha512sums=('588c4d2cf15720569f6dae2559da1d5039e894e8e9804ac2959075818be6c50cc3c89f25ade004f6501bfb9bca849bd62b069ca52acd0f438fdf44340d4894a3'
            'a7c909bf653ba206480b15208991b308b279a12a31dda2a61ee612c3c71524804195f9d2a69f0ca8e819650ca1982e5461a4222621447ce991ac63e5ee9254b9')

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --with-lv2 \
              --with-jack \
              --with-qt
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # standard instruments
  install -vDm 644 ../instruments/standard/* \
    -t "${pkgdir}/usr/share/${pkgname}/instruments/standard/"
  # docs
  install -vDm 644 {AUTHORS,DESIGN,NEWS,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
