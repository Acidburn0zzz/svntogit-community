# Maintainer: David Runge <dave@sleepmap.de>
pkgname=geonkick
pkgver=1.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'jack' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('cffb5569efeb6d2680ea091c410ebe90bdfaf8369d188e40f963dd01b2873b898ac647b265713dcab0c1ffde0134034d743efcfa1ea1975fac659755b3909b67')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -v build
  # fix ignored CMAKE_INSTALL_PREFIX:
  # https://gitlab.com/iurie/geonkick/issues/83
  sed -e 's|/usr/local|${CMAKE_INSTALL_PREFIX}|' \
      -i standalone/CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 ../examples/* \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -vDm 644 ../README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
