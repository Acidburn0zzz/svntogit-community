# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=deepin-metacity
pkgver=3.22.23
pkgrel=1
pkgdesc="2D window manager for Deepin"
arch=('x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'deepin-desktop-schemas' 'gtk3' 'startup-notification' 'zenity' 'bamf')
makedepends=('intltool' 'itstool' 'autoconf-archive' 'yelp-tools' 'python')
url="https://github.com/linuxdeepin/deepin-metacity"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-metacity/archive/$pkgver.tar.gz")
sha512sums=('ed41a1bb818352caa3c80c376fb0403daba217bb249765c3cd5e7877ec3b14c633b87118d5a1f2c27834a4006ad68a48c4eef7049482d8e22dc18bedcd0a0cc6')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool  

  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/ui/*.c
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
