# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.4.0.20171106
_commit=53db4247f4b5eaca1781624dcdd0107db43cc612
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('dtkwidget' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs' 'polkit-qt5'
         'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual' 'file-roller'
         'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal' 'gst-plugins-good' 'mpv')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dde-file-manager/archive/$_commit.tar.gz")
sha512sums=('9438af84fdfd6ec068526e88591afba12c5cb09838aba9374bd7bd7fdaef242c371bfb32f32dcb61eef4b90f063df2a9f6f3f7dec57f9cbe34a6d1d2b0ace30c')

prepare() {
  mv dde-file-manager-{$_commit,$pkgver}
}

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
