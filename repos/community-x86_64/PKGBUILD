# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=2.2
pkgrel=1
pkgdesc="A light-weight process isolation tool"
arch=('x86_64')
url="http://nsjail.com"
license=('Apache')
depends=('protobuf' 'libnl')
makedepends=('git')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/google/nsjail/archive/${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=33d7970ba0d5a1939371f3448e13ede833b32afd')
sha256sums=('36433dddff9fe3433574f2c53be1bec5873c4bc3ef4e4934597d30da90ec0c4d'
            'SKIP')

prepare() {
    mv kafel ${pkgname}-${pkgver}
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
