# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgname=spring
pkgver=0.89.0
_pkgver=89.0
pkgrel=1
pkgdesc='A free 3D real-time-strategy (RTS) game engine'
arch=('i686' 'x86_64')
url="http://springrts.com/"
license=('GPL')
depends=('openal' 'glew' 'boost-libs' 'freetype2' 'devil' 'libvorbis' 'sdl'
         'libxcursor')
makedepends=('boost' 'cmake' 'zip' 'xz' 'p7zip' 'python2' 'java-environment')
optdepends=('python2: python-based bots'
            'java-runtime: java-based bots')
source=(http://downloads.sourceforge.net/sourceforge/springrts/${pkgname}_${_pkgver}_src.tar.lzma
        boost-1.50.patch)
md5sums=('36fc266e925bc3790a68bf1bc7ebb315'
         'a3c3b4a53eeb499090b027fd6b3cf848')

build() {
  bsdtar -xf ${pkgname}_${_pkgver}_src.tar.lzma
  
  cd spring_${_pkgver}

  sed "s/TIME_UTC/TIME_UTC_/g" -i AI/Skirmish/E323AI/CScopedTimer.h
  patch -Np1 < $srcdir/boost-1.50.patch
 
  cmake	. \
    -DCMAKE_INSTALL_PREFIX=/usr \
	-DDATADIR=share/spring
  make
}

package() {
  cd spring_${_pkgver}

  make DESTDIR=$pkgdir install

  install -d $pkgdir/etc/spring
  echo '$HOME/.spring' > $pkgdir/etc/spring/datadir
}

# vim sw=2:ts=2 et:
