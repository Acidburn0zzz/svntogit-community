# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=timescaledb-tune
pkgver=0.11.3
pkgrel=1
pkgdesc="A tool for tuning TimescaleDB for better performance"
arch=('x86_64')
url="https://github.com/timescale/timescaledb-tune"
license=('Apache')
depends=('glibc' 'postgresql')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('142272f1447ad6f0123d9e90cbbe9bb59387f3a4609e756e4d01440765f282bde3bf63ba6374706e7ab9f7c0af9e26995e6128aa537e61547bc0e12e037ded49')
b2sums=('aa44f185ddcc2a581639115da5a2db25dd04f9a084dd0103c12e18b3f68caa06377b511ccfd9b4550ef5dda70a5cfc2d4eaa0d1f990f97e8d34f5772a6720440')

prepare() {
  cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod vendor

  # fix version string
  sed -i "s/^\tVersion =.*$/\tVersion = \"$pkgver\"/" pkg/tstune/tuner.go
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -buildmode=pie \
    -trimpath \
    -mod=vendor \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS}" \
    -o build \
    ./cmd/...
}

package() {
  install -Dm755 -t "$pkgdir/usr/bin" "$pkgname-$pkgver/build/$pkgname"
}
