# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=lib3mf-1
pkgver=1.8.1
pkgrel=1
pkgdesc='Implementation of the 3D Manufacturing Format file standard (legacy version)'
arch=('x86_64')
url='https://3mf.io/'
license=('BSD')
depends=('gcc-libs' 'libutil-linux') 
makedepends=('cmake')
checkdepends=('gtest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/3MFConsortium/lib3mf/archive/v$pkgver.tar.gz"
        'lib3mf-1_use-system-gtest.patch')
sha512sums=('f7fd75bcb6472de1595a018e5add516d0d78ab0aee92462b686b77f8d2bef05270e7b737cb7e1d31fc5850815056e753874c2f9ec456a455e8461c4010fe914a'
            'b6bd55163b1532ce1b0fa19b1963e063ba49b3930079a781ad5a796648baa00e800e75fe6198aaf97395a5c540e947dff7dbee2bc7bd95679eb9f72cd7270414')

prepare() {
	mkdir build
	cd "${pkgname%-1}-$pkgver"
	patch --strip=1 --input="$srcdir/lib3mf-1_use-system-gtest.patch"
}

build() {
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_INCLUDEDIR="include/$pkgname" \
	      $( ((CHECKFUNC)) || echo -DLIB3MF_TESTS=OFF) \
	      "$srcdir/${pkgname%-1}-$pkgver"
	make
}

check() {
	cd build
	ctest
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/${pkgname%-1}-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
