# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=keepassxc
pkgver=2.2.1
pkgrel=1
pkgdesc="Cross-platform community-driven port of Keepass password manager"
arch=(x86_64 i686)
url="https://keepassxc.org/"
license=(GPL)
depends=(hicolor-icon-theme libxtst qt5-x11extras yubikey-personalization)
makedepends=(cmake qt5-tools)
source=("https://github.com/keepassxreboot/$pkgname/releases/download/$pkgver/$pkgname-$pkgver-src.tar.xz"{,.sig})
sha256sums=('6761cdcef482941d958557466bb113be2c514b9847b0d0a65c7f5ef2c6d67dbe'
            'SKIP')
validpgpkeys=('BF5A669F2272CF4324C1FDA8CFB4C2166397D0D2') # KeePassXC Release <release@keepassxc.org>

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../$pkgname-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_XC_AUTOTYPE=ON \
		-DWITH_XC_HTTP=ON \
                -DWITH_XC_YUBIKEY=ON
	make
}

check() {
	cd build
	# Fails on i686 - https://github.com/keepassxreboot/keepassxc/issues/327
	if [ $CARCH == x86_64 ]; then
		make test
	fi
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
