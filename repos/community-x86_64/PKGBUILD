# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: ML <neldoreth>

pkgname=zathura
pkgver=0.3.4
pkgrel=1

pkgdesc="Minimalistic document viewer"
url="http://pwmt.org/projects/zathura/"
arch=('i686' 'x86_64')
license=('custom')

depends=('girara>=0.2.4' 'sqlite' 'desktop-file-utils' 'file')
makedepends=('python2-docutils')
optdepends=('zathura-djvu: DjVu support'
            'zathura-pdf-poppler: PDF support using Poppler'
            'zathura-pdf-mupdf: PDF support using MuPDF'
            'zathura-ps: PostScript support')

install=zathura.install
source=(http://pwmt.org/projects/zathura/download/zathura-$pkgver.tar.gz
        bash-completion
        PATCH-Build-even-without-SyncTeX.patch)

sha1sums=('d8142ffdd9df8f04619cc823da07afb1f7694270'
          '94a8236c483626a7f3acee053a1ea885aed45a82'
          'f8d8a0a9c699eeb5fc64bf56194dd0cb12f65ab1')

prepare() {
  cd zathura-$pkgver
  patch -p1 <../PATCH-Build-even-without-SyncTeX.patch
}
build() {
  cd zathura-$pkgver
  make
}

package() {
  cd zathura-$pkgver

  make install DESTDIR="$pkgdir"
  install -D -m664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm0644 "$srcdir"/bash-completion "$pkgdir"/usr/share/bash-completion/completions/zathura
}
