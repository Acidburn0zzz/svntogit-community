# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-hangul
pkgver=5.0.0
pkgrel=1
pkgdesc="Hangul Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-hangul"
license=('GPL')
depends=('fcitx5' 'libhangul')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('716733b6c070abbffe7cc63cf1e42ae435c8318d52cdc1b42c818a2d6261ce9c0f21ae875ef1776021ea856095ae699f2758f5c61a47e1b7c118c49853de4721'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
