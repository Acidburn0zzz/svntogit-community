# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.2.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('2f019fea811a1a8e995b8fb2ae77ea4f52c0ff82f3b049f22730b5aba8f7a580cf046cb93bc779999805edbc6b9b7c1ab81cdc800bc3068be5631dd947849770')

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"
  go build .
}

check() {
  cd "${srcdir}/sh-${pkgver}/"
  go test ./...

}

package() {
  cd "${srcdir}/sh-${pkgver}/"
  install -Dm755 "cmd/shfmt/shfmt" "${pkgdir}/usr/bin/shfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
