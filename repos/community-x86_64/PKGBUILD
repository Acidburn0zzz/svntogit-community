# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.262
pkgrel=1
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk3' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer')
makedepends=('boost' 'cmake')
source=(http://springlobby.info/tarballs/springlobby-${pkgver}.tar.bz2)
sha512sums=('54e93fc9c2f05e53c576cbe94828667a3e7e9fdb1619639165f28830cdf4f0720ca96377c23ee4fee7fda55c71e60ba12c7c50424b69ceb596d419c51571aa42')

build() {
  cd $srcdir/${pkgname}-${pkgver}

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver}

  make DESTDIR=$pkgdir install
}

# vim: sw=2:ts=2 et:
