# Maintainer: Robin Broda <coderobe @ archlinux.org>
pkgname=nebula
pkgver=1.0.0
pkgrel=1
pkgdesc='A scalable overlay networking tool with a focus on performance, simplicity and security'
arch=('x86_64')
url='https://github.com/slackhq/nebula'
license=('MIT')
depends=()
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('e0585ef37fae1f8db18cdea20648d4087e586b20ff0961ab7eac59a6c9bdafa2')

build() {
  cd "${pkgname}-${pkgver}"

  go build \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS} -X main.Build=$(date -u '+%Y%m%d%H%M%S' -d @${SOURCE_DATE_EPOCH})" \
    -o ./nebula ./cmd/nebula

  go build \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS} -X main.Build=$(date -u '+%Y%m%d%H%M%S' -d @${SOURCE_DATE_EPOCH})" \
    -o ./nebula-cert ./cmd/nebula-cert
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 nebula "${pkgdir}/usr/bin/nebula"
  install -Dm755 nebula-cert "${pkgdir}/usr/bin/nebula-cert"
}
