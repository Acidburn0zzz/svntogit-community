# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=libdom
pkgver=0.3.3
pkgrel=2
pkgdesc='Implementation of the W3C DOM'
arch=('x86_64')
url='http://www.netsurf-browser.org/projects/libdom/' # no https available
license=('MIT')
depends=('expat' 'libhubbub>=0.3.5' 'libwapcaplet>=0.4.1')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('e06a987d61d0783b81170ddb920d80802a1616aa7f1a7a458c6fd0564f3929a4')

prepare() {
  sed -i 's:_BSD_SOURCE:_DEFAULT_SOURCE:' "$pkgname-$pkgver/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -shared -fPIC -w"
  export LDFLAGS="$LDFLAGS -shared -z,now"
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared"
}

package() {
  make -C "$pkgname-$pkgver" \
    DESTDIR="$pkgdir" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE="lib-shared" \
    install
  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: source.netsurf-browser.org/libdom.git
# vim: ts=2 sw=2 et:
