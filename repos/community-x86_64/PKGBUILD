# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=darkstat
pkgver=3.0.714
pkgrel=3
pkgdesc="Network statistics gatherer (packet sniffer)."
url="http://dmr.ath.cx/net/darkstat/"
license=("GPL")
arch=(i686 x86_64)
depends=('libpcap' 'zlib')
source=(http://dmr.ath.cx/net/darkstat/$pkgname-$pkgver.tar.bz2
	$pkgname
	$pkgname.conf)
install=$pkgname.install
md5sums=('eef385fadc8dbb611d3d4c4d8fa94817'
         '1021f95fabe16cfab4c95f6264d535e5'
         'aa9a9effd1e8f08860afcd6439fc94c5')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # darkstat script
  install -D -m755 "$srcdir"/darkstat "$pkgdir"/etc/rc.d/darkstat
  install -D -m644 "$srcdir"/darkstat.conf "$pkgdir"/etc/conf.d/darkstat.conf
}
