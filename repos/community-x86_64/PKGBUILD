# Maintainer: David Runge <dave@sleepmap.de>
pkgname=moony.lv2
pkgver=0.26.0
pkgrel=1
pkgdesc="Easily add realtime programmable logic glue in LV2 plugin graphs"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/moony/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('4825fc90a04660a34266bf4dddb51b6c19c173278a0e27653fd5b7c1b84b636891bf3d215bf8479a6fd9637c0a16aa2058c8ab1c3124dbd090fc82750d129d86')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

# check fails due to currently broken lv2_validate in lv2
# check() {
#   cd "$pkgname-$pkgver"
#   meson test -C build
# }

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
