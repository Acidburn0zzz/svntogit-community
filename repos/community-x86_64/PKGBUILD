# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-mupen64plus-next
pkgver=169
pkgrel=1
epoch=1
pkgdesc='Nintendo 64 core'
arch=(x86_64)
url=https://github.com/libretro/mupen64plus-libretro-nx
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libpng
  libretro-core-info
)
makedepends=(
  git
  mesa
  nasm
)
source=(libretro-mupen64plus-next::git+https://github.com/libretro/mupen64plus-libretro-nx.git#commit=f818ff22fb7549f08b74ffe3974ce844e11d9679)
sha256sums=(SKIP)

pkgver() {
  cd libretro-mupen64plus-next

  git rev-list --count HEAD
}

build() {
  cd libretro-mupen64plus-next

  make WITH_DYNAREC='x86_64'
}

package() {
  cd libretro-mupen64plus-next

  install -Dm 644 mupen64plus_next_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
