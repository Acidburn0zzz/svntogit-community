# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200601
_pkgver=2020-06-01
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('67b19f430726aefb2e7c6c1d4cb7705b0dde9e4fe52bddebead6f38e6ababc61265fdfd3791b956e9ea7a8d81997728739c5c4442c67da248e6e9c7a11bc5e10')

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

# https://github.com/rust-analyzer/rust-analyzer/issues/4218#issuecomment-625612149
# check() {
#   cd $pkgname-$_pkgver
#   cargo test --release --locked
# }

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
