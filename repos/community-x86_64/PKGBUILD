# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.7.3
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'zlib')
makedepends=('alsa-lib' 'cmake' 'fftw' 'fltk' 'jack' 'lv2' 'mxml' 'ncurses'
'readline')
checkdepends=('kxstudio-lv2-extensions' 'lv2lint')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'readline: for standalone application'
            'ncurses: for standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('646ddbdf7804c2109124e97383a4e4e4f7cc295e27da7f0690c960d39d185a6648c38d033f2311535fa98529167e3c6c2b38a8f009d5b38d49961937cb4affae')
b2sums=('7f2618b209caae9f957066ba939b71cd87a92f94aeda00c5cbbaeaea90e9b33069d6d373dec6047949e1d15cc8bd29cf4d6e494ae004c0e0e0e92bfd71425f99')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

# # lv2 plugin requires banks to be in some absolute location to be tested
# # https://github.com/Yoshimi/yoshimi/issues/110
# check() {
#   cd "${pkgname}-${pkgver}"
#   cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
#   lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
#   rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
# }

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfltk.so' 'libfltk_images.so'
  'libjack.so' 'libmxml.so' 'libncursesw.so' 'libreadline.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
