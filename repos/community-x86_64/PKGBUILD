# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.2.5
pkgrel=1
pkgdesc="Japanese input method Anthy IMEngine for IBus Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://ibus.googlecode.com"
depends=('ibus' 'anthy' 'python2')
makedepends=('swig' 'intltool')
provides=('ibus-anthy')
options=('!libtool')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('00b44c874616d797a0117d087cf33341')

build() {
  cd ${pkgname}-${pkgver}

  # python2 fix
  for file in setup/ibus-setup-anthy.in engine/ibus-engine-anthy.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --libexec=/usr/lib/ibus

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
