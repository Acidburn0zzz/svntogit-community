# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.6.6
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('9a7738b87f103d2086607125d58a0efacacf63b9cb2e7ee37650d270a2e99aa2408df1691ae22b6ae9017fcf0be489a14a513c00bf018c73e821e40f399a8e2a')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core github.com/sun8911879/shadowsocksR
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
