# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Wilhem Barbier <wilhem.b@free.fr>

pkgname=raylib
pkgver=2.0.0
pkgrel=1
pkgdesc='Well designed and easy-to-use game programming library'
arch=(x86_64)
url='https://www.raylib.com'
license=(ZLIB)
depends=(libx11)
makedepends=(cmake git glfw libxcursor libxinerama libxrandr ninja vulkan-headers xorg-server-devel xorg-xinput)
source=("git+https://github.com/raysan5/raylib#tag=$pkgver")
md5sums=(SKIP)

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_GAMES=OFF \
    -DSHARED=ON \
    -DSTATIC=OFF \
    -DOpenGL_GL_PREFERENCE=GLVND \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  # Also package header files
  for f in build/release/*.h; do
    install -Dm644 "$f" "$pkgdir/usr/include/$(basename $f)"
  done
  # License
  install -Dm644 "$pkgname/LICENSE.md" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# vim: ts=2 sw=2 et:
