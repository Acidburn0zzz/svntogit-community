# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Philip Nilsson <pnilsson@nullref.se>

pkgname=alex
pkgver=3.0.2
pkgrel=4
pkgdesc='Lexical analyser generator for Haskell'
arch=('x86_64' 'i686')
url='http://hackage.haskell.org/package/alex'
license=('custom:BSD3')
depends=('gmp')
makedepends=('ghc=7.6.2-1' 'haskell-quickcheck>=2')
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('997e33342c3c6e0edfff862f051fc286bd661f442d09d1bc02cd6e9d22083164')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  runghc Setup.lhs configure --prefix=/usr --datasubdir="$pkgname"
  runghc Setup.lhs build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  runghc Setup.lhs copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/alex/BSD3"
  # doc only contains the license
  rm -rf $pkgdir/usr/share/doc
}

# vim:set ts=2 sw=2 et:
