# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: TJ Vanderpoel <tj@rubyists>

pkgname=musl
pkgver=1.0.4
pkgrel=1
pkgdesc="An implementation of the C/POSIX standard library intended for use on Linux-based systems"
arch=('i686' 'x86_64')
url="http://www.musl-libc.org/"
license=('MIT')
makedepends=('make')
provides=('musl')
conflicts=('musl-mainline')
options=('staticlibs' '!buildflags' '!strip')
source=(http://www.musl-libc.org/releases/$pkgname-$pkgver.tar.gz)
md5sums=('f502c98800dfe02d063ca66a1aea5a3e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if [ "$CC" != "clang" ];then
    _my_flags="-fno-toplevel-reorder -fno-stack-protector"
  fi
  CFLAGS="${CFLAGS} $_my_flags"  ./configure --prefix=/usr/lib/musl --exec-prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -d -m0755 "$pkgdir/usr/lib/"
  mv "$pkgdir/lib/"ld-musl*.so* "$pkgdir/usr/lib/"
  rmdir "$pkgdir"/lib

  install -Dm0644 README "$pkgdir"/usr/share/doc/musl/README
  install -Dm0644 COPYRIGHT "$pkgdir"/usr/share/licenses/musl/COPYRIGHT
}
