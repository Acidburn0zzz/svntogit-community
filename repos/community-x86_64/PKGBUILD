# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=(qpid-proton python2-qpid-proton)
pkgver=0.28.0
pkgrel=1
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
makedepends=('cmake' 'doxygen' 'python' 'python2' 'swig')
source=("http://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('98c82db9a13543f81ccf0b5d6e4d2f18cb8446016969f3582fb7a50604a189b27c124eb9899887ba10405cce5d9ccb65fa385d57c60a511e5e067b77d74a3fa1')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        ..
    make

    cd python/dist
    python2 setup.py build
}

package_qpid-proton() {
    depends=('jsoncpp' 'python' 'ruby')

    cd ${pkgname}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_python2-qpid-proton() {
    depends=('python2')

    cd qpid-proton-${pkgver}/build/python/dist
    python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
