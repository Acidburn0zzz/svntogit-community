# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-system-monitor
pkgver=1.22.1
pkgrel=1
pkgdesc="A system monitor for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtkmm3' 'libgtop' 'libwnck3')
makedepends=('intltool' 'itstool' 'python')
conflicts=('mate-system-monitor-gtk3')
replaces=('mate-system-monitor-gtk3')
groups=('mate-extra')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('75b1dd52b1146568a59f187e08639144c5a3835d39510321dbf134ab0bd4017a')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--localstatedir=/var \
        	--enable-systemd
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
