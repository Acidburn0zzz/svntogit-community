# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.5+35+ga023c778
pkgrel=1
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://budgie-desktop.org/"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=a023c7781435d71e463184124a5cc8b26985d961  # master
source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "desktop-override.patch")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            'baffb1d64715c20c1ae444a5b8ea0f8e9bf69cccf30e5e2c6729fe6d0c7491d2')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
	cd $pkgname

	# Override settings only for Budgie
	patch -Np1 -i ../desktop-override.patch

	# Don't use systemd session management, it's not supported by Budgie
	sed -i 's/exec gnome-session/exec gnome-session --builtin/' src/session/budgie-desktop.in

	git submodule init
	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update
}

build() {
	arch-meson $pkgname build -D with-desktop-icons=none
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
