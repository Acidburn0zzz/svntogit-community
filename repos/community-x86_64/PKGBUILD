# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=texworks
pkgver=0.6.4
pkgrel=1
pkgdesc='Unicode-based, TeX-aware editor with integrated PDF viewer'
arch=('x86_64')
url='https://www.tug.org/texworks/'
license=('GPL')
depends=('hunspell' 'libsynctex' 'poppler-qt5' 'qt5-script')
makedepends=('cmake' 'lua' 'python' 'qt5-tools')
checkdepends=('gsfonts' 'poppler-data' 'xorg-server-xvfb')
optdepends=('gsfonts: correctly display PDF files without embedded fonts'
            'lua: Lua scripts support'
            'poppler-data: correctly display certain CJK documents'
            'python: Python scripts support'
            'texlive-core: typeset TeX documents')
source=("$pkgname-$pkgver.tar.gz::https://github.com/TeXworks/texworks/archive/release-$pkgver.tar.gz"
        'Disable-mouse-tracking-for-CompletingEdit.patch')
sha512sums=('8fa3e16e6ee19d46dba30707e7a83340c1d68f6cb855cc2838c5c168dc580f8973cb7438bdfce93e51962653694d369c51d7a82a07ad6bcf5eb89030fa3640ea'
            'd151dff96a7c7b280235b5d533c4e0874c600ef4eae9119dd5a841aaf3853a4c22163da5d8719e4adc3f83b51ba08c829e8adff054b1356cee81296832f9a988')

prepare() {
	mkdir build

	# Temporary workaround for https://bugreports.qt.io/browse/QTBUG-80831
	cd "texworks-release-$pkgver"
	patch --strip=1 --input="$srcdir/Disable-mouse-tracking-for-CompletingEdit.patch"
}

build() {
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr \
	      -DTW_BUILD_ID='Arch Linux' \
	      -DWITH_PYTHON=ON \
	      "$srcdir/texworks-release-$pkgver"
	make
}

check() {
	cd build
	xvfb-run ctest --output-on-failure
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
