# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=gmsynth.lv2
pkgver=0.4.1
pkgrel=1
pkgdesc="General MIDI LV2 Synth"
arch=('x86_64')
url="https://x42-plugins.com/x42/x42-gmsynth"
license=('GPL2')
groups=('pro-audio' 'lv2-plugins')
depends=('glibc' 'glib2' 'libfluidsynth.so')
makedepends=('lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz"
        "$pkgname-$pkgver-use-system-fluidsynth.patch")
sha512sums=('3b83d6376c297edc9f88bcaffb888eda730b70d239202d887f80b7df2f0ae316d95af5937e6387b3ff09c351f06f828a965e4fbac7a5af059aab0b89ad7191b3'
            '5f871b75c8e2e31dac6f780e0e40825d1984e8d6da679a5472f7c402f7cfaf08732b339d4ae23fbb333d4ff76324ee4fc45805caa59464302256c2f4a5006b66')

prepare() {
  cd "$pkgname-$pkgver"
  # use system fluidsynth
  patch -Np1 -i "../$pkgname-$pkgver-use-system-fluidsynth.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
