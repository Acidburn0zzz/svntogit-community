# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: rilian-la-te <ria.freelander@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=appmenu-gtk-module
pkgver=0.7.3.2
pkgrel=1
pkgdesc="Application Menu GTK+ Module"
depends=()
makedepends=(cmake gtk3 gtk2)
optdepends=('gtk3: GTK3 module' 'gtk2: GTK2 module')
url="https://gitlab.com/vala-panel-project/vala-panel-appmenu/"
arch=(x86_64)
license=(LGPL3)
source=("https://gitlab.com/vala-panel-project/vala-panel-appmenu/-/archive/$pkgver/vala-panel-appmenu-$pkgver.tar.gz"
        80-appmenu-gtk-module)
sha256sums=('42311dad372e03236567815e8ef54be92802020ecdfc6d7dfed46858582ff702'
            '4c006c4ea7b8556070ad6d35529d3a9e23da8033429e34d1824c25942d969fbc')

build() {
  cmake -B build -S vala-panel-appmenu-$pkgver/subprojects/$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm755 80-appmenu-gtk-module -t "$pkgdir"/etc/X11/xinit/xinitrc.d/
}
