# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

pkgname=lxqt-archiver
pkgver=0.1.1
pkgrel=2
pkgdesc="A simple & lightweight desktop-agnostic Qt file archiver"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
license=("GPL2")
depends=('libfm-qt' 'qt5-x11extras' 'glib2' 'json-glib' 'libfm-qt.so')
makedepends=('cmake' 'qt5-tools' 'lxqt-build-tools')
source=("https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
        $pkgname-gcc10.patch::https://github.com/lxqt/lxqt-archiver/commit/caefefb3547b256ead7d7d35820082ec2ab5ceb5.patch)
sha256sums=('32b3e2bead037159b99b4069a53034abb43d31728693b5fa3c8ccbddfaffd316'
            'SKIP'
            '1e576b9b242a6e77e15e77bf60406cf7167009e42c65db16909bb8d2bf4f521e')
validpgpkeys=(
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-gcc10.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib

  make -C build
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
