# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: DuGi <dugi@irc.pl>

pkgname=springlobby
pkgver=0.269
pkgrel=2
pkgdesc="A free cross-platform lobby client for the Spring RTS project"
arch=('x86_64')
url="http://springlobby.info/"
license=('GPL2')
depends=('hicolor-icon-theme' 'wxgtk3' 'curl' 'libtorrent-rasterbar' 'boost-libs'
         'alure' 'libsm')
optdepends=('sdl' 'sdl_sound' 'sdl_mixer')
makedepends=('boost' 'cmake' 'git')
source=("git+https://github.com/springlobby/springlobby.git#tag=$pkgver"
        "git+https://github.com/spring/pr-downloader.git"
        "springlobby-wxgtk-3.1.patch")
sha512sums=('SKIP'
            'SKIP'
            'bf70703c5f001bfc92699d4c21e6ca63b35994319f1096e50b07dba4ceb48cd7d67798fa1668c3c654b0372465a5ad383d9cbae9488f08dea6d78dee8901d418')

prepare() {
  cd $pkgname
  patch -p1 -i ../springlobby-wxgtk-3.1.patch # Fix build with wxgtk 3.1

  git submodule init
  git config submodule."src/downloader/lib".url "$srcdir"/pr-downloader
  git submodule update

  mkdir build
}

build() {
  cd $pkgname/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname/build

  DESTDIR="$pkgdir" make install
}

# vim: sw=2:ts=2 et:
