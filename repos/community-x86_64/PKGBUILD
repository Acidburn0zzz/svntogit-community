# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lsp-plugins
pkgver=1.1.14
pkgrel=1
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=('x86_64')
url="https://lsp-plug.in"
license=('LGPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('gendesk' 'imagemagick' 'ladspa' 'jack' 'libsndfile' 'lv2' 'php')
optdepends=('jack: for standalone applications')
source=("https://github.com/sadko4u/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1e955f989563aa8021340ee9ea70b1e1062bf918afc396ea606f4b11eedf1fab65155de029cbde7cbaa4fbafc07e25d998d666d26760bf0eb3ae9d14e20d66b4')

_sizes=('16' '22' '24' '26' '32' '36' '48' '64' '72' '96' '128' '192' '256'
'384' '512' '1024')

prepare() {
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # generate xdg desktop icons
  for size in "${_sizes[@]}"; do
    convert res/art/logo-large.png -resize "${size}x${size}" "${size}x${size}.png"
  done
}

build() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  make
  make OBJDIR="${_test_path}" clean
  make OBJDIR="${_test_path}" test
}

check() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  "${_test_path}/lsp-plugins-test" utest \
    --nofork \
    --debug \
    --verbose || echo "Known flaky test: https://github.com/sadko4u/lsp-plugins/issues/19"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' \
       DESTDIR="$pkgdir/" \
       install

  # non-scalable at scalable location:
  # https://github.com/sadko4u/lsp-plugins/issues/83
  rm -rvf "${pkgdir}/usr/share/icons/hicolor/scalable/"
  for size in "${_sizes[@]}"; do
    install -vDm 644 "${size}x${size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
  done
}
