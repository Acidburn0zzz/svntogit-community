# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=bzlib
pkgname=haskell-bzlib
pkgver=0.5.0.5
pkgrel=16
pkgdesc="Compression and decompression in the bzip2 format"
url="https://hackage.haskell.org/package/${_hkgname}"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'bzip2')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        ghc-8.8.patch)
sha512sums=('fe4b6e1a4d67573a9ff72808cfd0bf47a127885593e98debcaa505c488c76af364bed31cdb8ee9308295514f154536e958ff5e2a1c3157eeb5b16f8011424819'
            'ab5344deb9667b73ed3387d58abab5ee2fd6887fd78327d15b40ac8dc81165b6060517c29e8c7860e6e4e21119622182659d8f0e4491be2fbc1f5cf12aeab885')

prepare() {
    cd $_hkgname-$pkgver
    patch -p0 -i ../ghc-8.8.patch
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
