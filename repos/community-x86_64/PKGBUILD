# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.13.0
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "git")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha512sums=('051d2c55f7809fbecf2b13121f03a60c0820d4b0ea6a37a3c1901a4e52d2ed2c27ee1adea6f3e86138e2d1ad2670e8075c227fe49ca2ac62f97e58b6fc5bbf2a')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
