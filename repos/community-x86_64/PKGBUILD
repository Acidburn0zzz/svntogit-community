# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: FigoDaPaura <ffigoDaPaura>
# Contributor: Andres Jimenez

pkgname=virtualgl
pkgver=2.6.3
pkgrel=1
pkgdesc="Redirects 3D commands from an OpenGL application onto a server-side 3D graphics card"
arch=('x86_64')
url="https://virtualgl.org"
license=('LGPL')
depends=('libxv' 'glu' 'libjpeg-turbo' 'xcb-util-keysyms' 'opencl-driver' 'ocl-icd')
makedepends=('cmake' 'mesa' 'libxtst' 'opencl-headers')
source=("https://downloads.sourceforge.net/project/virtualgl/${pkgver}/VirtualGL-${pkgver}.tar.gz"{,.sig}
        faedcc1e36b4ed89a325e01822447900840a0b77.patch)
sha512sums=('c01c602335d51c94840c32021a5ce4f982939d67efbe1458fce67582c5a76ac42f5b733998a8c8f04a77d57d7c2de01377f7cabcf848c01bfd9b2d8e0b318fea'
            'SKIP'
            '6845a60071aac333ac1c2ad5c3861f9fbe8cdf9f28425817a18c2b6f8c280b5469ce3be4831a0bf8a6600955bf5177217ff684c61f728783f2d09be6885f2815')
validpgpkeys=('ECF01671D05E2A105FF84DC46BBEFA1972FEB9CE') # The VirtualGL Project (Signing key for official binaries) <information@VirtualGL.org>

prepare() {
  cd VirtualGL-$pkgver

  patch -Np1 -i "$srcdir"/faedcc1e36b4ed89a325e01822447900840a0b77.patch

  # FS#48403
  export LDFLAGS="${LDFLAGS/--as-needed,}"

  mkdir build
}

build() {
  cd "VirtualGL-$pkgver"/build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DTJPEG_INCLUDE_DIR=/usr/include \
      -DTJPEG_LIBRARY=/usr/lib/libturbojpeg.so \
      -DCMAKE_INSTALL_LIBDIR=/usr/lib \
      -DCMAKE_INSTALL_BINDIR=/usr/bin \
      -DCMAKE_INSTALL_DOCDIR=/usr/share/doc/${pkgname} \
      -DVGL_FAKEXCB=1
  make
}

package() {
  cd "VirtualGL-$pkgver/build"
  make install DESTDIR="$pkgdir"

  mv "$pkgdir"/usr/bin/glxinfo "$pkgdir"/usr/bin/vglxinfo
}
