# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.1.8
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        use-system-qt5-headers.patch)
sha512sums=('bb3d9f0b406ec97b9eee371485fcc5d6cb7371342c3e1e454bd8e2949ca428475d371bd330a2c395ad5e0537f210e7ddcd839262f9ae79160b9038dd7687f780'
            '29152204a36efc5fc2376b74be2ebe1d9cb253f9371ca8b0e9bf95c415c903f956d0d400eaffb3f708f81b19acd0a5383647af3a5a10cf6a61dfa586eb1bd57b')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../use-system-qt5-headers.patch
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
