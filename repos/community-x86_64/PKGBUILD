# $Id$
# Contributor: Giorgio Lando <lando at imap dot cc>
# Contributor: Sergej Pupykin
# Contributor: Thomas Bächler
# Contributor: Sébastien Luttringer
# Maintainer: Thorsten Töpper <atsutane-tu@freethoughts.de>

pkgname=fcron
pkgver=3.1.2
pkgrel=5
pkgdesc='Feature-rich cron implementation'
arch=(i686 x86_64)
url='http://fcron.free.fr'
license=('GPL')
depends=('pam')
makedepends=('smtp-server' 'vi')
optdepends=('smtp-server: to receive mails from cron jobs'
            'vi: default editor for fcrontab')
provides=('cron')
conflicts=('dcron')
backup=('etc/fcron/fcron.conf'
        'etc/fcron/fcron.allow'
        'etc/fcron/fcron.deny'
        'var/spool/fcron/systab'
        'var/spool/fcron/systab.orig')
options=('emptydirs' '!makeflags')
source=("http://fcron.free.fr/archives/$pkgname-$pkgver.src.tar.gz"
        'systab'
        'systab.orig'
        'run-cron')
md5sums=('36bf213e15f3a480f2274f8e46cced0a'
         '55be3e80fb2545608feae6f2e0eebece'
         '5384c607d842ca3d5cbb612ac1dceb15'
         '524eba827447a6b7ef7515eedf305698')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --sysconfdir=/etc/fcron \
              --with-answer-all=no \
              --with-boot-install=no \
              --with-username=root \
              --with-groupname=root \
              --datarootdir=/usr/share \
              --datadir=/usr/share \
              --with-docdir=/usr/share/doc \
              --localstatedir=/var \
              --with-systemdsystemunitdir=/usr/lib/systemd/system \
              --with-piddir=/run \
              --with-editor=/usr/bin/vi \
              --with-sendmail=/usr/sbin/sendmail
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir/" install
  install -D -m644 "$srcdir/$pkgname-$pkgver/files/fcron.pam" "$pkgdir/etc/pam.d/fcron"

  install -D -m644 "$srcdir/$pkgname-$pkgver/files/fcrontab.pam" "$pkgdir/etc/pam.d/fcrontab"
  # Install default fcrontab so that fcron can completely replace dcron

  install -D -m640 "$srcdir/systab" "$pkgdir/var/spool/fcron/systab"
  # In order to preserve the systab crontab in any case it is better to have
  # it in non-binary form too
  install -D -m640 "$srcdir/systab.orig" "$pkgdir/var/spool/fcron/systab.orig"

  # Add cron.* directories
  install -d -m755 "$pkgdir/etc/cron.daily"
  install -d -m755 "$pkgdir/etc/cron.hourly"
  install -d -m755 "$pkgdir/etc/cron.monthly"
  install -d -m755 "$pkgdir/etc/cron.weekly"

  # Install run-cron script to make fcron run without dcron
  install -D -m755 "$srcdir/run-cron" "$pkgdir/usr/bin/run-cron"

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir"/{var/,}run

  # avoid conflict with libbsd - #31259
  rm "$pkgdir/usr/share/man/man3/bitstring.3" \
    "$pkgdir/usr/share/man/fr/man3/bitstring.3"
  rmdir --ignore-fail-on-non-empty "$pkgdir/usr/share/man/man3" \
    "$pkgdir/usr/share/man/fr/man3"
}

# vim:set ts=2 sw=2 et:
