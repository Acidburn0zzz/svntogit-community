# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=waybar
pkgver=0.8.0
pkgrel=5
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'gtkmm3'
    'jsoncpp'
    'libjsoncpp.so'
    'libdbusmenu-gtk3'
    'libpulse'
    'libmpdclient'
    'spdlog'
)
# Sway is needed for building support for workspace and window title modules
makedepends=(
    'meson'
    'scdoc'  # For generating manpages
    'sway'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
    "include_cassert.patch"
)
sha256sums=('2de2f0cec243da0d9ff2255ceb9dac9d95e8be06d6ea13926fae460d72e4b8aa'
            'ca77d1ba64d0852bbf0aaa0b8c648e88f8712ed51fb39862b636eb0b33c6e7e1')

prepare() {
    cd "Waybar-$pkgver"
    patch -p1 < ../include_cassert.patch
}

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
