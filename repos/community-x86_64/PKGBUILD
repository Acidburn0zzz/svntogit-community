# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=traefik
pkgver=2.1.4
pkgrel=2
pkgdesc="Modern reverse proxy written in Go"
arch=('x86_64')
url="https://containo.us/traefik/"
license=('MIT')
makedepends=('go-pie' 'git' 'go-bindata')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/traefik/releases/download/v${pkgver}/traefik-v${pkgver}.src.tar.gz")
sha512sums=('c45d8b90bc1fc437f38102c03f7d5383b94e5ee362d2c2c837085d80a5007d0432989abc5698267260fb9ba8b22561c68012b47ad0367bc221d501b776ed8992')

build() {
  go generate
  cd cmd/traefik
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}"
}

check() {
  go test ./...
}

package() {
  install -Dm755 cmd/traefik/"${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 contrib/systemd/traefik.service "${pkgdir}/usr/lib/systemd/system/traefik.service"
}
