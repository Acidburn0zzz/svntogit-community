# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=24.0
pkgrel=2
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('i686' 'x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'libgnome-keyring' 'libpeas' 'libnotify' 'python2-gobject2' 'dconf' 'xdg-utils')
makedepends=('vala' 'intltool' 'itstool' 'nautilus')
optdepends=('nautilus: backup extension'
            'ubuntuone-client: backup into Ubuntu One storage')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz
        new-u1backend-support.patch
        not-check-ubuntuone-couch.patch)
md5sums=('335131d1f3c85615339fa8b212db50ae'
         '944a4c62f042b240feba98f8d9a893b2'
         '8d59798638982c2bdb55b2e415087245')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/\"python\", \"-c\"/\"python2\", \"-c\"/' common/PythonChecker.vala

  # Add support for the new Ubuntu One backend
  patch -Np0 -i "$srcdir/new-u1backend-support.patch"

  # Now we don't need ubuntuone-couch for Ubuntu One support
  patch -Np1 -i "$srcdir/not-check-ubuntuone-couch.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --without-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
