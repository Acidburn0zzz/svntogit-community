# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2607
pkgname=sonic-visualiser
pkgver=4.0.1
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'bzip2' 'desktop-file-utils' 'fftw' 'gcc-libs' 'glibc'
'hicolor-icon-theme' 'libcapnp.so' 'libfishsound.so' 'libid3tag' 'libkj.so'
'liblo.so' 'liblrdf.so' 'libmad' 'liboggz.so' 'libportaudio.so' 'libpulse'
'librubberband.so' 'libsamplerate' 'libserd-0.so' 'libsndfile' 'libsord-0.so'
'opusfile' 'qt5-base' 'qt5-svg')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-4.0.1-remove-werror.patch"
)
sha512sums=('6557d7c90618f7f2c18f8bb0058b8cd74a084f26376999074b9d5f23f04e1548b7e82ccbb73a301fa68c1d2f047810d312e568a85c9c7f3f25da9ec9b00c4220'
            '1b5873953ec8635a1e10f924cfc2aa8dcdfa856dfcf2b4a06e8f1ab8ed947697e64f3c20147464dcc95b1d836e92e848c57c6480bd04a9d2086547044df606da')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all declarations of -Werror
  # https://sourceforge.net/p/sv1/bugs/277/
  patch -Np1 -i "../${pkgname}-4.0.1-remove-werror.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  # INSTALL_ROOT used in favor of DESTDIR
  # https://sourceforge.net/p/sv1/bugs/254
  make INSTALL_ROOT="${pkgdir}/" install
  # xdg desktop file
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  # samples
  install -vDm 644 samples/* -t "${pkgdir}/usr/share/${pkgname}/samples"
  # templates
  install -vDm 644 templates/* -t "${pkgdir}/usr/share/${pkgname}/templates"
  # docs
  install -vDm 644 {CHANGELOG,CITATION,README.{md,OSC}} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
