# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.9.6
pkgrel=2
pkgdesc='New control center for linux deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde' 'networkmanager-qt'
         'deepin-qt-dbus-factory' 'deepin-qt5integration' 'deepin-network-utils')
makedepends=('deepin-dock' 'cmake' 'qt5-tools')
optdepends=('redshift: automatic color temperature support'
            'networkmanager-openconnect: for OpenConnect support'
            'networkmanager-openvpn: for OpenVPN support'
            'networkmanager-pptp: for PPTP support'
            'networkmanager-strongswan: for StrongSwan support'
            'networkmanager-vpnc: for VPNC support'
            'network-manager-sstp: for SSTP support')
# Not packaged: network-manager-l2tp
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dde-control-center/commit/c3b3f492519897a5a54e4d3883da0db85397c076.patch)
sha512sums=('d9eb95abc827c1ecb393f3495de1eaca7f42df7c00ea0d1b785ca4c3cef30569e1d30e521cca90947b7126948eb6034c295f6ee115ee14e0e56ea1ca54c16f69'
            'f8f28c2a4a5d7566cfe3d9a95eff9a8b654473fe8fb2a56f521512a4cd186184ce8269df217a64c3e44c10efeddefddf2882d91462211baa3e7b183a8e241539')

prepare() {
  patch -d dde-control-center-$pkgver -p1 -i ../c3b3f492519897a5a54e4d3883da0db85397c076.patch
  mkdir -p build
}

build() {
  cd build
  cmake -DDISABLE_SYS_UPDATE=YES -DCMAKE_INSTALL_PREFIX=/usr \
    ../dde-control-center-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
