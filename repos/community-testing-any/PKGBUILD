# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

# TODO: move optional python-requests-cache

pkgname='python-pandas-datareader'
pkgver=0.8.1
pkgrel=2
pkgdesc="Data readers extracted from the pandas codebase"
arch=('any')
url="https://github.com/pydata/pandas-datareader"
license=('BSD')
depends=('python-pandas' 'python-lxml' 'python-requests' 'python-wrapt')
makedepends=('python-setuptools')
source=("https://github.com/pydata/pandas-datareader/archive/v${pkgver}.tar.gz")
sha256sums=('a7d8343c69d1fb76f80e3bc6d1280cb0525ead41f54da53746ad0205bfd6bd31')

build() {
  cd "$srcdir"/pandas-datareader-${pkgver}
  python setup.py build_ext --inplace
  python setup.py build
}

package() {
  cd "$srcdir"/pandas-datareader-${pkgver}
  python setup.py install --skip-build --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

