# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tldr
pkgver=1.1.0
pkgrel=2
pkgdesc="Command line client for tldr, a collection of simplified and community-driven man pages."
arch=('any')
url="https://github.com/tldr-pages/tldr-python-client"
license=('MIT')
depends=('python-argcomplete' 'python-colorama' 'python-setuptools' 'python-termcolor')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tldr-pages/tldr-python-client/archive/$pkgver.tar.gz")
sha512sums=('bf697916b78360093970498b3ea8852b9e210a4ca39eb2bb95783687c619029e9bd9881fbbb29f5f4f5647489a4731d25045a97dfa93c32e05a8d0dd8bb9ea4f')

build() {
  cd tldr-python-client-$pkgver
  python setup.py build
}

check() {
  cd tldr-python-client-$pkgver
  python -m pytest
}

package() {
  cd tldr-python-client-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
  install -Dm644 LICENSE.md -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
