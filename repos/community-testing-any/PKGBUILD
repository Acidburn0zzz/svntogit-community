# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-readme-renderer
pkgver=24.0
pkgrel=5
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('any')
depends=('python-bleach' 'python-docutils' 'python-pygments' 'python-six')
optdepends=('python-cmarkgfm: for Markdown support')
makedepends=('python-setuptools' 'python-cmarkgfm')
checkdepends=('python-pytest-runner' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('d5e98215469f2335da9ee52d29e3d7e12d2b5a481274587ae8c0286a807004749ef19f57aa4475cd6e56e782bc2c6926fcc2c9bc4250aa2b6549700bf29f989b')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package_python-readme-renderer() {
  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
