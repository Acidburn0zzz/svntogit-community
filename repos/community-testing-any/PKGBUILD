# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tzinfo
pkgname=ruby-tzinfo
pkgver=2.0.1
pkgrel=1
pkgdesc='Daylight savings aware transformations between times in different time zones'
arch=('any')
url="https://github.com/tzinfo/tzinfo"
license=('MIT')
depends=('ruby' 'ruby-concurrent')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-simplecov
# checkdepends=('ruby-minitest' 'ruby-simplecov')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('9cfd0a076aba5e7ae2f519374a6154a62e3ec71eaeb2d5a31bb07e3e26b44c4e01fce8cb385e0da9c565e0e408a0a7f204ac666baa7a9d6f3a592f2218ef99b6')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec Gemfile
}

build() {
  cd ${pkgname}-${pkgver}
  gem build "${_name}.gemspec"
}

# check(){
#   cd "${pkgname}-${pkgver}"
#   rake test
# }

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
