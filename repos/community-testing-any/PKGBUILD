# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-redbaron
pkgver=0.9.1
pkgrel=1
pkgdesc="Abstraction on top of baron, a FST for python to make writing refactoring code a realistic task"
url="https://github.com/PyCQA/redbaron"
license=('LGPL')
arch=('any')
depends=('python-baron')
makedepends=('python-setuptools' 'python-pypandoc')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/redbaron/archive/$pkgver.tar.gz"
        https://github.com/PyCQA/redbaron/commit/812081b7bd8774ceb61016ac37da43107c26eea2.patch)
sha512sums=('90381a8009563fa121569cf8e7ed4ee3dbddaa04e7e6de56cf626ed7ea26381bd40c4ef9b4c35b42f001def9b240acefa1104514a487f7c8e4811963822f19ee'
            'ba829d260ae1994ef12faaa3dd10531046ca754627de44a79f0b5ae9a27eed7a8b304ee5cac709586969b8ad2129b6a68b59631f88a4c616a65ca91937d6b45f')

prepare() {
  patch -d redbaron-$pkgver -p1 -i ../812081b7bd8774ceb61016ac37da43107c26eea2.patch
}

build() {
  cd redbaron-$pkgver
  python setup.py build
}

check() {
  cd redbaron-$pkgver
  pytest tests
}

package() {
  cd redbaron-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
