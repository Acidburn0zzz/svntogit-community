# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.14
pkgrel=1
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-yaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint python-setuptools python-sshpubkeys)
checkdepends=(python-flask python-freezegun python-nose python-parameterized
              python-sure tk)
optdepends=(
  'python-flask: for moto_server'
)
source=("https://files.pythonhosted.org/packages/source/m/moto/moto-$pkgver.tar.gz"
        $pkgname-dynamodb-regions.patch::https://github.com/yan12125/moto/commit/cbd531584cda892b0c022c53fc00c3c28a274abc.patch)
sha256sums=('79aeaeed1592a24d3c488840065a3fcb3f4fa7ba40259e112482454c0e48a03a'
            '5fac5e72223682b4c0c186983a307bf8aa82fef7a4049ec93f067e5f13add4db')

prepare() {
  cd $_pkgname-$pkgver

  patch -Np1 -i ../$pkgname-dynamodb-regions.patch

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#,?<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_lambda tests moto.awslambda, which requires a running docker.service
  TZ=UTC nosetests -sv ./tests/ --exclude='test_lambda.*'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
