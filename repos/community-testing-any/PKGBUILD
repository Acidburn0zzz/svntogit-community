# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-voluptuous
pkgname=(python-voluptuous python2-voluptuous)
pkgver=0.11.3
pkgrel=1
pkgdesc="A Python data validation library"
url="https://github.com/alecthomas/voluptuous"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pypandoc' 'python2-pypandoc')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/v/voluptuous/voluptuous-$pkgver.tar.gz")
sha512sums=('db197d3d30b303f4849d160419185074526dc3b01c1408f9bcd0e5bd02ca8c28c498ab15e99e671eed04f54bc36eb9ae1b43e042b3c00f9ac0666331cf2caecc')

prepare() {
  # https://github.com/alecthomas/voluptuous/issues/333
  rm -r voluptuous-$pkgver/*.egg-info

  # https://github.com/alecthomas/voluptuous/pull/332
  sed -e "s/'w'/'wb'/" -e "s/.encode('utf-8')//" -e "s/f.write(long_description)/f.write(long_description.encode('utf-8'))/" -i voluptuous-$pkgver/setup.py

  # Python 3.7
  sed -i '/raise StopIteration/d' voluptuous-$pkgver/voluptuous/schema_builder.py

  cp -a voluptuous-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py build

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/voluptuous-$pkgver
  python setup.py nosetests

  cd "$srcdir"/voluptuous-$pkgver-py2
  python2 setup.py nosetests
}

package_python-voluptuous() {
  depends=('python')

  cd voluptuous-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-voluptuous() {
  depends=('python2')

  cd voluptuous-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
