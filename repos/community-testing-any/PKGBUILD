# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.6.9
pkgrel=2
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock')
source=("git+https://github.com/python-cmd2/cmd2.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a cmd2{,-py2}
}

build() {
  cd "$srcdir"/cmd2
  python setup.py build

  cd "$srcdir"/cmd2-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cmd2
  python setup.py ptr

  cd "$srcdir"/cmd2-py2
  python2 setup.py ptr
}

package_python-cmd2() {
  depends=('python-pyparsing')

  cd "$srcdir"/cmd2
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-pyparsing')

  cd "$srcdir"/cmd2-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
