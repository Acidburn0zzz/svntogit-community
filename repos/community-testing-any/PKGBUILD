# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=pacman-contrib
pkgver=4.0.0
pkgrel=1
pkgdesc="Utilities for use with the pacman package manager"
arch=('any')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz)
md5sums=('8c97f4804d2a0847956c45888b0ea517')

_scripts=(
  bacman
  paccache
  pacdiff
  paclist
  paclog-pkglist
  pacscripts
  pacsearch
)

build() {
  cd "$srcdir/pacman-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc

  make -C contrib
}

package() {
  cd "$srcdir/pacman-$pkgver/contrib"

  install -dm755 "$pkgdir/usr/bin" "$pkgdir/usr/share/vim/vimfiles/ftdetect"

  install -m755 -t "$pkgdir/usr/bin" "${_scripts[@]}"

  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    > "$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"
}
