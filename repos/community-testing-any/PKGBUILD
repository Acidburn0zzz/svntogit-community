# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgname=(python-texttable python2-texttable)
pkgver=1.3.0
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python-cjkwrap' 'python2-cjkwrap')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/t/texttable/texttable-$pkgver.tar.gz")
sha512sums=('9ccdaa8944b8dfe93ed459c5a18715a2ffedc484157f44de7a68cbfdacae88044b71ed89e14761e474a227c664d29d4903d4f80fa54c6e577f508c77391305ce')

prepare() {
  cp -a texttable-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/texttable-$pkgver
  python setup.py build

  cd "$srcdir"/texttable-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/texttable-$pkgver
  py.test tests.py

  cd "$srcdir"/texttable-$pkgver-py2
  py.test2 tests.py
}

package_python-texttable() {
  depends=('python')
  optdepends=('python-cjkwrap: for better CJK support')

  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-texttable() {
  depends=('python2')
  optdepends=('python2-cjkwrap: for better CJK support')

  cd texttable-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
