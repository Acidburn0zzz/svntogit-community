# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=sagemath-doc
pkgver=8.5
pkgrel=1
pkgdesc="HTML documentation and inline help for SageMath"
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
depends=(python2-sphinx gap-doc thebe)
makedepends=(sage-notebook python2-pyzmq python2-docutils python2-jupyter_client python2-pkgconfig cython2 python2-ipywidgets)
source=("$pkgname-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        docbuild_main.patch sagemath-doc-sphinx-1.8.patch sagemath-doc-segfault.patch sagemath-gap-4.10.patch)
sha256sums=('369810fac9ad7fb6cb1eb2a82d3e3679edee015778de34c56670d4ee80bde690'
            'ea5e17bb7a7cb36a22e5e3872fcc2585852bc971c4b139b0b2cd69a36c1b009b'
            '17aae8d9c76a4e14cf3a4f5c7c9470e5312b18976bdd60f970ee736ca03a8cb3'
            '141ac064305526785fb6d9507896876cbf893cd41ec1e42d86fc836f766c200f'
            '224f8d1db783d7ae25408912c4891687d6a8a1dacc8c5b3a00b725a28d951b6c')

prepare() {
  cd sage-$pkgver

# fix relative imports
  patch -p0 -i ../docbuild_main.patch
# use sage_setup imports from sagemath source, but use system sage ones (which include compiled modules)
  mkdir -p local-python
  ln -sr src/sage_setup local-python
# fix build with sphinx 1.8
  patch -p1 -i ../sagemath-doc-sphinx-1.8.patch
# Partially revert https://trac.sagemath.org/ticket/24655 to workaround a segfault
  patch -Rp1 -i ../sagemath-doc-segfault.patch
# Port to GAP 4.10
  patch -p1 -i ../sagemath-gap-4.10.patch
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_ROOT="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_DOC_SRC"
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="$srcdir"/sage-$pkgver/local-python
  python2 sage_setup/docbuild --no-pdf-links all html
}

package() {
  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
        rm -r $_dir
        ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}
