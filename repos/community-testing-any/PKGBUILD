# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=etl
pkgver=0.04.22
pkgrel=2
pkgdesc="VoriaETL: C++ STL complementory multiplatform template library"
arch=(any)
url="http://synfig.org"
license=('GPL2')
depends=()
conflicts=('synfig-etl')
replaces=('synfig-etl')
source=(https://downloads.sourceforge.net/project/synfig/releases/1.2.0/source/ETL-${pkgver}.tar.gz ETL-swap-type.patch)
sha256sums=('ac5aeb9b8ec2c69dd455c6c72e104ff20c43e1e5dde6c0cd87f3f7d1b41d2746'
            '50ef24805f2e90d02fc2b94e2a578f78eaa266b56c57c198b3b947a65237e9c7')

prepare() {
  cd ETL-$pkgver
  # fix synfig build with GCC 7 (Fedora)
  patch -p0 -i ../ETL-swap-type.patch
}

build() {
  cd "$srcdir"/ETL-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/ETL-$pkgver
  make DESTDIR="$pkgdir" install
}
