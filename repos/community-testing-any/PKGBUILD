# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-digitalocean
pkgname=('python-digitalocean' 'python2-digitalocean')
pkgver=1.14.0
pkgrel=1
pkgdesc='digitalocean.com API to manage Droplets and Images'
arch=('any')
license=('LGPL')
url='https://github.com/koalalorenzo/python-digitalocean'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-jsonpickle' 'python2-jsonpickle')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-responses' 'python2-responses')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/koalalorenzo/python-digitalocean/archive/v$pkgver.tar.gz")
sha512sums=('821116dfdfa2191af409f3433d1edee4b6d2f97700b1b8ba6d7ca1cd6e38dd555e8dcf83e0b631d1a4e958abd0e989e3001d774916f94b2244a9d9f6bcffbf4e')

prepare() {
  cp -a python-digitalocean-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py build

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-digitalocean-$pkgver
  python setup.py pytest || warning "https://github.com/koalalorenzo/python-digitalocean/issues/258"

  cd "$srcdir"/python-digitalocean-$pkgver-py2
  python2 setup.py pytest || warning "https://github.com/koalalorenzo/python-digitalocean/issues/258"
}

package_python-digitalocean() {
  depends=('python-requests' 'python-jsonpickle')

  cd python-digitalocean-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-digitalocean() {
  depends=('python2-requests' 'python2-jsonpickle')

  cd python-digitalocean-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
