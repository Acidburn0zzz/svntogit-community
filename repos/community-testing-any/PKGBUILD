# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgbase=python-progressbar
pkgname=('python-progressbar' 'python2-progressbar')
pkgver=2.3
pkgrel=8
arch=('any')
url='https://pypi.python.org/pypi/progressbar'
license=('LGPL')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/progressbar/progressbar-$pkgver.tar.gz"
        '01-python33.patch')
sha1sums=('5b4b11fc0421855314d666b08c069d2ed0349bcc'
          '5dc63611e364e90441cbc90fe2530b9fab67e780')

prepare() {
  cd progressbar-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch $filename"
      patch -p1 -N -i "$srcdir/$filename"
    fi
  done
  # fix egg-info permissions
  chmod -R ug=rwX,o=rX .
}

build() {
  cd progressbar-$pkgver
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2

  msg2 'Fix python2 sheebang'
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

package_python-progressbar() {
  pkgdesc='Text progressbar library for python'
  depends=('python')

  cd progressbar-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
}

package_python2-progressbar() {
  pkgdesc='Text progressbar library for python2'
  depends=('python2')

  cd progressbar-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
