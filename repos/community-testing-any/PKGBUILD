# Maintainer: David Runge <dvzrv@archlinux.org>

_name=natsort
pkgname=python-natsort
pkgver=8.0.1
pkgrel=2
pkgdesc="Simple yet flexible natural sorting in Python"
arch=(any)
url="https://github.com/SethMMorton/natsort"
license=(MIT)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-hypothesis python-mock python-pytest-mock)
optdepends=('python-fastnumbers: more efficient sorting'
            'python-pyicu: sort in locale dependant manner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('a1db383c466b8605006d880cb19f284902ae7384bca839877cc7baa0d60b5ab369126ae7efe0b93e1b5a0c399ad902fdd818beed2c34bb209b56e3c960896d4f')
b2sums=('81aaeebb02b0a0e45d9291c4efd341eadff7be9e8b50a966e21b0a22b92eb05cee99148f6e2b93ce792bda2a223f1a64e43d2569dd1cbc4b421e11f2652722f8')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
