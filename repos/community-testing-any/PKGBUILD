# Maintainer: David Runge <dave@sleepmap.de>

pkgname=gandi.cli
pkgver=1.5
pkgrel=3
pkgdesc="Gandi command line interface"
arch=('any')
url="https://cli.gandi.net"
license=('GPL3')
depends=('python-click' 'python-ipy' 'python-requests' 'python-setuptools' 'python-yaml')
makedepends=('python-docutils')
checkdepends=('docker' 'openssh' 'openssl' 'python-pytest' 'python-pytest-cov')
optdepends=('docker: docker integration'
            'openssh: ssh integration'
            'openssl: certificate creation')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5681d7e84db4efadfd35874de080a719a97330bea5732d7dc271dfc50e6075a1392deb30b6b0adb5456cdf5d70dc963088c153bf56829e678b97d5b7a5579233')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  rst2man --no-generator gandicli.man.rst > gandi.1
}

check() {
  cd "$pkgname-$pkgver"
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES.rst,{CONTRIBUTING,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 gandi.1 -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 gandi.completion \
    -t "${pkgdir}/usr/share/bash-completion/completions/gandi"
}
