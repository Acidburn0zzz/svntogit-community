# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.2.2
pkgrel=3
pkgdesc="An asyncio based SMTP server"
arch=('any')
url="https://github.com/aio-libs/aiosmtpd"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-1.2.2-exclude_examples.patch::https://github.com/aio-libs/aiosmtpd/pull/205/commits/2d3d6b4a6f70bdc83f613dc3665a612392b38244.patch")
sha512sums=('7ffc279a73685601496dbfffef30a78cc254a76f8c8fccd0c7b7efe41da66672f0d8662f302eb928e90eb8977b859cb7e713415d0aa5e93f369ef23bdff405fc'
            'a6a81bc2f61bf07fd3f048159ab18b50c4a7f7ebbf219b83db1970533e0a02879b6077e409ebf2afaa4038206a54e0775643ddbe75f500299862fc252e12bf8c')
b2sums=('abf178c28a21c25e8a7c7ed920c154f037aaa21da5d2dfa0ee8cae7d239d00d5a979eedfb3865e3de7e54e7da94ea8a1e6b5194681a2137ac7d8f0f69d82e3a6'
        '1902889225801dd3cec83dc255ec510b9b94301e8367c6496353a4042145904e7941dbb94a22e12f6300cf988c8576b8d8c96b27169f19e57cfa54ddf7ec039a')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not install examples to site-packages:
  # https://github.com/aio-libs/aiosmtpd/issues/181
  patch -Np1 -i "../${pkgname}-1.2.2-exclude_examples.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.py -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}
