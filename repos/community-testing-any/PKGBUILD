# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-augeas
pkgver=1.0.2
pkgrel=1
pkgdesc="Python bindings for Augeas"
arch=('any')
license=('LGPL')
url="http://augeas.net"
depends=('augeas' 'python-cffi')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/p/python-augeas/python-augeas-$pkgver.tar.gz")
sha512sums=('c24c8f01420ca474be2903d3da35a246fa74f5ae848bb9267bf4a2166d3a4c64fa4d29a1c5aad6dac3c43251cda90673d6a5a72e6ebdd192e147baa03281e727')

build() {
  cd python-augeas-$pkgver
  python setup.py build
}

check() {
  cd python-augeas-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" make check
}

package() {
  cd python-augeas-$pkgver
  python setup.py install --skip-build -O1 --root="$pkgdir"
}
