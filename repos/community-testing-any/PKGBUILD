# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=catfish
pkgver=0.4.0.2
pkgrel=1
pkgdesc="A versatile file searching tool"
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install=$pkgname.install 
depends=('gtk3' 'python2-gobject' 'pyxdg' 'xdg-utils' 'hicolor-icon-theme')
optdepends=('zeitgeist: Zeitgeist integration'
            'findutils: filename search'
            'locate:    filename search'
            'tracker:   fulltext search'
            'strigi:    fulltext search'
            'pinot:     fulltext search')
source=(https://launchpad.net/$pkgname-search/0.4/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('d80eb544a12dbee2b2c84b63c4903eea')

build() {
  cd $srcdir/$pkgname-$pkgver

  # python2 build fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr

  # Correct symlinks
  sed -i 's|ln -sf $(DESTDIR)/$(PREFIX)/share|ln -sf ..|' Makefile
  make
}

package () {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  # python2 run fix
  sed -i 's|python|python2|' $pkgdir/usr/bin/$pkgname

  # icon file
  install -d $pkgdir/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/scalable/apps/$pkgname.svg $pkgdir/usr/share/pixmaps/$pkgname.svg
}
