# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=acorn
pkgver=8.0.0
epoch=1
pkgrel=1
pkgdesc='A tiny, fast JavaScript parser, written completely in JavaScript.'
arch=('any')
url='https://github.com/ternjs/acorn'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c2e26606f28ff699233ddda6fbab2435c80ae92e3775fe5c45b513590967b3caee91cbb96dc4afc7d7998ef3574ed15ee3ea02b2502f221342bd805effaf7133')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
