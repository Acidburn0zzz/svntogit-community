# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=avr-libc
pkgver=1.7.0
pkgrel=1
pkgdesc="The C runtime library for the AVR family of microcontrollers"
arch=('any')
url="http://savannah.nongnu.org/projects/avr-libc/"
license=('custom')
depends=('gcc-avr')
source=(http://savannah.nongnu.org/download/avr-libc/${pkgname}-${pkgver}.tar.bz2)
md5sums=('3eff7b779bd950400cb0a45f3c2fb377')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
}

# vim:set ts=2 sw=2 et:
