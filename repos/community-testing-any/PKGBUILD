# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2020.04.13
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('ad10b52cbb8242a2629569daa0ccfa55319a3032285c6688906515e4b6a97fc91dd25e735151b17af2ecab244a216c5e8d0c0c4a00c035b4b98976e9ead0a96d'
            'de3365adc47d824ae8f527db7b68f568d623bf5f96ab3f511146594aee57db8fd587dc6d2c5639283f620f6f19fa8dd5b789c703dfa7eaf571bd766e397cba23')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
