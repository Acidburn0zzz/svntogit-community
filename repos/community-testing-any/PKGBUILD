# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Arnaud Taffanel <dev@taffanel.org>
# Contributor: Victor HÃ¤ggqvist <aur a snilius d com>

_name=Solaar
pkgname=solaar
pkgver=1.0.1
pkgrel=3
pkgdesc="Linux device manager for a wide range of Logitech devices"
url="https://pwr-solaar.github.io/Solaar/"
license=('GPL2')
arch=('any')
depends=('gtk3' 'libnotify' 'python-dbus' 'python-gobject' 'python-pyudev')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pwr/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('e0b2bbfd97d9650d94f1a4b47e4e70f09c75f684e7e1d3e67d2cf6dd197109084e941b98333e173fb205d5c9464dd07e17390d9c2d5b46393fee2efdb20429ac')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # udev
  install -vDm 644 rules.d/42-logitech-unify-permissions.rules \
    "${pkgdir}/usr/lib/udev/rules.d/42-logitech-unify-permissions.rules"
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # install fix
  mv "${pkgdir}/usr/etc" "${pkgdir}/etc"
}
# vim:set ts=2 sw=2 et:
