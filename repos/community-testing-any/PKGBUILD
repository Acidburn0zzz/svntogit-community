# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>

pkgname=python-aiohttp-socks
pkgver=0.6.0
pkgrel=1
pkgdesc="SOCKS proxy connector for aiohttp. SOCKS4(a) and SOCKS5 are supported."
arch=('any')
url="https://github.com/romis2012/aiohttp-socks/"
license=('Apache')
depends=('python-aiohttp' 'python-python-socks')
makedepends=('git' 'python' 'python-setuptools')
checkdepends=('python-flask' 'python-pytest' 'python-pytest-asyncio')
source=("git+https://github.com/romis2012/aiohttp-socks?signed#tag=v${pkgver}"
        "python-aiohttp-socks-0.6.0_fix_test_suite.patch::$url/commit/266505841d1c70b9b7ead36a98d3da28c429e33f.patch")
sha256sums=('SKIP'
            '8605ac8062d75e206f4a80bf66c42806f567955c4ca610dd2f20b7cc2fcdcac8')
validpgpkeys=('706A8935FED55F3BFB7C188B5EEAD7400A5D80ED') # Roman Snegirev (maintainer)

prepare() {
    cd "${pkgname//python-}"
    # Fix test suite failures due to expired certificates
    patch --forward --strip=1 --input="$srcdir/python-aiohttp-socks-0.6.0_fix_test_suite.patch"
}

check() {
    cd "${pkgname//python-}"
    pytest
}

build() {
    cd "${pkgname//python-}"
    python setup.py build

}

package() {
    cd "${pkgname//python-}"
    python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}
