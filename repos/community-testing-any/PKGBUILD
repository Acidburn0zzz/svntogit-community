# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jose
pkgname=('python-jose' 'python2-jose')
pkgver=2.0.2
pkgrel=1
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pycryptodome' 'python2-pycryptodome'
             'python-six' 'python2-six' 'python-ecdsa' 'python2-ecdsa' 'python-future'
             'python2-future')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-cryptography'
              'python2-cryptography')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mpdavis/python-jose/archive/$pkgver.tar.gz")
sha512sums=('b81114eb8d44cb8427607ae6801d18eabc4d37791474b0d22f8d3ce19430588e2bb7e2fa7026098f7b04eee1c71963615a47d65d7007cf85f44126cd308fafde')

prepare() {
  cp -a python-jose-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py build

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py pytest
}

package_python-jose() {
  depends=('python-pycryptodome' 'python-six' 'python-ecdsa' 'python-future')

  cd python-jose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jose() {
  depends=('python2-pycryptodome' 'python2-six' 'python2-ecdsa' 'python2-future')

  cd python-jose-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
