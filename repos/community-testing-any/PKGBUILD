# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: enderst <enderst@gmail.com>

pkgname=gpodder
pkgver=2.9
pkgrel=1
pkgdesc='A podcast receiver/catcher'
license=('GPL3')
arch=('any')
url='http://gpodder.berlios.de/'
depends=('pygtk' 'python-feedparser' 'dbus-python' 'python-mygpoclient')
makedepends=('intltool' 'imagemagick' 'help2man')
optdepends=('libgpod: for ipod support'
            'pymad: for ipod support'
            'mplayer: for ipod video support'
            'python-imaging: rockbox cover art support'
            'vorbis-tools: for ogg tag updating and converting to mp3'
            'lame: for converting ogg to mp3'
            'gnome-bluetooth: transfer podcast via bluethooth'
            'pywebkitgtk: render shownotes'
            'python-eyed3: for ipod support')
            # pymtp: for mtp support (AUR only)
source=(http://download.berlios.de/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('6eb02a01c7848c056c96d7be36ec54bd')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  sed -i 's_PYTHON ?= python_PYTHON ?= python2_' Makefile
  for file in $(find . -name '*.py' -print); do
    sed -i 's_#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir}/ install
}
