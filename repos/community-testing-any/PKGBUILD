# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

_pyname=pylint
pkgname=python-$_pyname
pkgver=2.9.6
pkgrel=2
pkgdesc='Analyzes Python code looking for bugs and signs of poor quality'
arch=(any)
url=https://pylint.org
license=(GPL)
_pydeps=(appdirs
         astroid
         isort
         mccabe
         toml)
depends=(python
         "${_pydeps[@]/#/python-}")
makedepends=(python-setuptools
             python-sphinx)
checkdepends=(mpdecimal
              python-pytest)
optdepends=('graphviz: Enable output formats other than dot or vcg'
            'tk: Pylint GUI')
_archive="$_pyname-$pkgver"
source=("https://github.com/PyCQA/pylint/archive/v$pkgver/pylint-$pkgver.tar.gz")
sha256sums=('e6335c810ee3ff4b59e3990db49cf0d1e263e122b766853755436d88133c9496')

prepare() {
	cd "$_archive"
	# Remove overly restrictive dependency version pinning that ends up in runtime checks
	sed -i -e '/>=/{s/>.*//;p}' setup.cfg
}

build() {
	cd "$_archive"
	python setup.py build
	make PYTHONPATH="$PWD/build/lib" -C doc man
}

check() {
	cd "$_archive"
	PYTHONPATH="$PWD/build/lib" pytest \
		--deselect tests/benchmark/test_baseline_benchmarks.py \
		--deselect tests/test_functional.py::test_functional[arguments_renamed]
}

package() {
	cd "$_archive"
	export PYTHONHASHSEED=0
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/_build/man/*
}
