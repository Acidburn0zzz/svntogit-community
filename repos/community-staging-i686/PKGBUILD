# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-api
pkgver=1.3.1
pkgrel=11
pkgdesc="Web API server for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc-libs' 'hledger' "haskell-hledger-lib" "haskell-aeson" "haskell-decimal" "haskell-docopt"
         "haskell-either" "haskell-microlens" "haskell-microlens-platform" "haskell-safe"
         "haskell-servant-server" "haskell-servant-swagger" "haskell-swagger2" "haskell-text"
         "haskell-wai" "haskell-wai-extra" "haskell-warp")
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('4425cfa786b6d6b7db9a48c1d816cff313bd1258c83b882aadd2fd53fe9fa3986158a6254761a787dd993528454f04f31aaad53bbed6a347c6f048d96ded0996')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed -i 's/instance ToSchema Price/instance ToSchema Price where\n  declareNamedSchema = genericDeclareNamedSchemaUnrestricted defaultSchemaOptions/' hledger-api.hs
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup configure -O --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
