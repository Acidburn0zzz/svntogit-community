# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.3.2
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="http://code.google.com/p/openshadinglanguage/"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'llvm' 'intel-tbb' 'freetype2' 'libpng' 'libtiff')
makedepends=('boost' 'cmake' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        https://github.com/imageworks/OpenShadingLanguage/pull/273.patch)
md5sums=('985f1c0908368e9acfeff76120ab1b9e'
         '9fe2b863a4b69dc5be489fd4eef8606c')

build() {
  cd OpenShadingLanguage-Release-$pkgver

  patch -Np1 < $srcdir/273.patch

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    ..
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  mkdir -p "$pkgdir"/usr/share/OSL/
  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  mv "$pkgdir"/usr/LICENSE "$pkgdir"/usr/share/licenses/$pkgname # TODO: Tell upstream about this shit
  mv "$pkgdir"/usr/{CHANGES,README.md,INSTALL} "$pkgdir"/usr/share/OSL/
  mv "$pkgdir"/usr/doc "$pkgdir"/usr/share/OSL/doc
  mv "$pkgdir"/usr/shaders "$pkgdir"/usr/share/OSL/shaders
}

# vim:set ts=2 sw=2 et:
