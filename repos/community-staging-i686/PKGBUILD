# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=onboard
pkgver=0.98.1
pkgrel=1
pkgdesc="On-screen keyboard useful on tablet PCs or for mobility impaired users"
arch=('i686' 'x86_64')
url="https://launchpad.net/onboard"
license=('GPL')
depends=('python-dbus' 'python-gobject' 'gtk3' 'python-virtkey' 'librsvg' 'gsettings-desktop-schemas' 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('python-distutils-extra')
optdepends=('at-spi2-atk: auto-show when editing text'
            'mousetweaks: hover click with mouse')
options=('!emptydirs')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('107e031b429329b71deb67c9b90195d6')

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Do not set getpreferredencoding, because it breaks getstatusoutput() in the setup script
  sed -i '/getpreferredencoding =/ d' setup.py

  python3 setup.py install --root=$pkgdir/ --optimize=1
}
