# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=geos
pkgver=3.6.1
pkgrel=1
pkgdesc="C++ port of the Java Topology Suite"
arch=('x86_64' 'i686')
url="http://trac.osgeo.org/geos/"
license=('LGPL')
depends=('gcc-libs' 'bash')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(http://download.osgeo.org/$pkgname/$pkgname-$pkgver.tar.bz2)
sha256sums=('4a2e4e3a7a09a7cfda3211d0f4a235d9fd3176ddf64bd8db14b4ead266189fc5')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/$pkgname-$pkgver

  make check
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
