# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hledger-ui
pkgver=1.2
pkgrel=7
pkgdesc="Curses-style user interface for the hledger accounting tool"
url="http://hledger.org"
license=("GPL")
arch=('i686' 'x86_64')
depends=('ghc' 'hledger' "haskell-hledger-lib" "haskell-ansi-terminal" "haskell-async"
         "haskell-base-compat" "haskell-cmdargs" "haskell-data-default" "haskell-fsnotify"
         "haskell-hunit" "haskell-microlens" "haskell-microlens-platform" "haskell-megaparsec"
         "haskell-pretty-show" "haskell-safe" "haskell-split" "haskell-text" "haskell-text-zipper"
         "haskell-vector" "haskell-brick" "haskell-vty")
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        brick-0.19.patch)
sha512sums=('07f571a66e94a34a20cd6145196cd3333137a6281c33c5f917facc3c70d424b01c0f2a3d4d0d0c543b877338a350b5add4d3a5d48bbe28009d5cc42a2c3d3e29'
            '91cd273625fe4d5863bfa5602b9147ede04b6e83e125f26974877231cc02418e7e76236ba2cdc687dd20bb3b67cf769d34f10d8296c2a8f035a79f3695187627')

prepare() {
    # It does nothing
    sed -e '/mtl-compat/d' \
        -e 's/megaparsec >=5.0 && < 5.3/megaparsec >=5.0 \&\& < 5.4/' \
        -e 's/brick >= 0.12 && < 0.18/brick >= 0.12 \&\& < 0.19/' \
        -i ${pkgname}-${pkgver}/${pkgname}.cabal
    (cd ${pkgname}-${pkgver}; patch -p2 -i ../brick-0.19.patch)
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -f-oldtime -fthreaded
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
