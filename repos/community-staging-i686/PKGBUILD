# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.1.0
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(i686 x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(python2 m4ri)
makedepends=(boost)
source=("https://github.com/BRiAl/BRiAl/releases/download/$pkgver/brial-$pkgver.tar.bz2")
sha256sums=('56d3eecd6fdfdf0c06e20f573ccf929d35ca62e9b2173fddf519b41e25f00ce8')

build() {
  cd $pkgname-$pkgver 

  export PYTHON=python2
  ./configure --prefix=/usr --enable-shared
  make

  cd sage-brial
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
