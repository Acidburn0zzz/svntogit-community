# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.41.0
pkgrel=2
pkgdesc="An open-source engine reimplementation for the role-playing game Morrowind"
arch=('i686' 'x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz")
sha512sums=('a47a7af3c6d3252e22206eb776069ef87f698acbe133b0e0294db307486c28cb300085fcf45b436af541dbb22f6d45f2ee3998e50a1c711f667f8a738813255d')

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DDESIRED_QT_VERSION=5
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
