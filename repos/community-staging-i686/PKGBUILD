# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libparserutils
pkgver=0.1.0
pkgrel=2
pkgdesc="Library for building efficient parsers"
arch=('i686' 'x86_64')
url="http://www.netsurf-browser.org/projects/libparserutils"
license=('MIT')
source=("http://www.netsurf-browser.org/projects/releases/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('eb415317925a0fb12a4ff8f849d2fe92')

build() {
	cd "${srcdir}"/${pkgname}-${pkgver}-src

	sed 's/-Wall/ /' -i Makefile
	make PREFIX=/usr COMPONENT_TYPE="lib-shared"
	make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
	cd "${srcdir}"/${pkgname}-${pkgver}-src

	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-shared" 
	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-static"
	install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING

}
