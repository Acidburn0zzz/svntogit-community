# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.0.6
pkgrel=2
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('git' 'xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("git+https://cr.deepin.io/qt5integration.git#tag=$pkgver"
        "libqt5xcbqpa-dev-felix::git+https://github.com/felixonmars/libqt5xcbqpa-dev.git"
        qt-5.8.patch)
sha256sums=('SKIP'
            'SKIP'
            '5a282c109ba27da5747b8417b248cacc5331a72300389a560e35f10df6a6feac')

prepare() {
  cd qt5integration
  patch -p1 -i ../qt-5.8.patch
  ln -s ../../libqt5xcbqpa-dev-felix ./platformplugin/libqt5xcbqpa-dev
}

build() {
  cd qt5integration
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration
  make INSTALL_ROOT="$pkgdir" install
}
