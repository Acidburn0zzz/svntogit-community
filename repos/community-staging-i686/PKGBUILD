# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=projectm
pkgver=2.0.1
pkgrel=6
pkgdesc='A music visualizer which uses 3D accelerated iterative image based rendering.'
arch=('i686' 'x86_64')
url='http://projectm.sourceforge.net/'
license=('LGPL')
depends=('gcc-libs' 'ftgl' 'glew' 'gtkglext')
makedepends=('pkg-config' 'cmake')
source=("http://downloads.sourceforge.net/${pkgname}/projectM-${pkgver}-Source.tar.gz"
        'libprojectM-2.0.1-pkg-config.patch'
        'libprojectM-2.0.1-fix-linking.patch')
md5sums=('f8bf795878cdbbef54784cf2390b4c89'
         '4f258f4e7c2ab612931936bfd7f1f0fb'
         '5c178cadf1e00ee4baf32cd9ccbc818c')

build() {
  cd "${srcdir}/projectM-${pkgver}-Source"

  patch -Np1 -i "${srcdir}/libprojectM-2.0.1-pkg-config.patch"
  patch -Np1 -i "${srcdir}/libprojectM-2.0.1-fix-linking.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/projectM-${pkgver}-Source"
  make DESTDIR="${pkgdir}" install
}
