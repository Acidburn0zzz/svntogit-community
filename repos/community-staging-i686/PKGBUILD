# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-primitive
pkgver=0.6.2.0
pkgrel=1
pkgdesc="Wrappers for primitive operations"
url="https://hackage.haskell.org/package/primitive"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc")
source=("https://hackage.haskell.org/packages/archive/primitive/${pkgver}/primitive-${pkgver}.tar.gz")
sha256sums=('b8e8d70213e22b3fab0e0d11525c02627489618988fdc636052ca0adce282ae1')

build() {
  cd "$srcdir"/primitive-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/${pkgname} \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  _ghcver=`pacman -Q ghc | cut -f2 -d\  | cut -f1 -d-`
  depends=("ghc=${_ghcver}")

  cd "$srcdir"/primitive-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -dm755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/${pkgname}/html "$pkgdir"/usr/share/doc/ghc/html/libraries/primitive
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE
  rm -f "$pkgdir"/usr/share/doc/${pkgname}/LICENSE
}
