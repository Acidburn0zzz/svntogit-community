# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=ncrack
pkgver=0.5
pkgrel=2
pkgdesc='A high-speed network authentication cracking tool'
arch=(i686 x86_64)
url='https://nmap.org/ncrack/'
license=(GPL)
depends=(openssl-1.0)
source=("https://nmap.org/ncrack/dist/ncrack-$pkgver.tar.gz")
md5sums=('c264bced401db826852014aef22927cd')

build() {
  cd $pkgname-$pkgver
  CFLAGS+=" -I/usr/include/openssl-1.0" \
  CPPFLAGS+=" -I/usr/include/openssl-1.0" \
  LDFLAGS+=" -L/usr/lib/openssl-1.0" \
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
