# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=hedgewars
pkgver=0.9.17
pkgrel=3
pkgdesc="Free Worms-like turn based strategy game"
arch=('i686' 'x86_64')
url="http://hedgewars.org"
license=('GPL' 'custom')
depends=('qt' 'sdl' 'sdl_mixer' 'sdl_image' 'sdl_net' 'sdl_ttf' 'lua')
makedepends=('fpc' 'cmake'
             # haskell specific
             'ghc=7.4.2-1'
	     'haskell-network=2.3.0.14-1' 'haskell-bytestring-show=0.3.5.1-3'
	     'haskell-utf8-string=0.3.7-2' 'haskell-dataenc=0.14.0.3-2' 
	     'haskell-hslogger=1.1.5-7')
source=(http://download.gna.org/hedgewars/hedgewars-src-$pkgver.tar.bz2
	hedgewars.desktop
	hedgewars.png)
md5sums=('77c0bc4bc664604adbba67fab606d8f1'
         '48c66a3204d4fa0a69e9aa46f7c31e0c'
         'eeb14d50df39063549ac5eca9dbc65d1')

build() {
  cd $pkgname-src-$pkgver

#  sed -i 's|.*misc/liblua.*||' CMakeLists.txt

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DDATA_INSTALL_DIR=/usr/share \
	-DQT_QMAKE_EXECUTABLE=/usr/bin/qmake \
	-DQT_MOC_EXECUTABLE=/usr/bin/moc \
	-DWITH_SERVER=1 .

  make
}

package() {
  cd $pkgname-src-$pkgver

  make DESTDIR=$pkgdir install

  install -D -m644 Fonts_LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/Fonts_LICENSE.txt
  install -D -m644 $srcdir/hedgewars.png $pkgdir/usr/share/pixmaps/hedgewars.png
  install -D -m644 $srcdir/hedgewars.desktop $pkgdir/usr/share/applications/hedgewars.desktop
}
