# $Id$
#Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
#Contributor: BlackEagle <ike.devolder@gmail.com>
#Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine
pkgver=1.1.1
pkgrel=8
pkgdesc="A music player and library organizer"
url="http://www.clementine-player.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gstreamer0.10-base' 'taglib' 'glew' 'liblastfm' 'libgpod' 'projectm'
         'libmtp' 'libplist' 'hicolor-icon-theme' 'qt4' 'libimobiledevice' 'chromaprint'
         'sparsehash' 'qjson' 'libcdio-paranoia' 'protobuf' 'qca' 'qca-ossl' 'gvfs')
makedepends=('cmake' 'boost' 'mesa')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
source=(http://clementine-player.googlecode.com/files/${pkgname}-${pkgver}.tar.gz
        'clementine-1.1.1-libimobiledevice-fix.patch')
install=clementine.install

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"

   patch -Np1 < "$srcdir"/clementine-1.1.1-libimobiledevice-fix.patch

   cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUSE_SYSTEM_PROJECTM=1 -DBUILD_WERROR=0
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
md5sums=('28e4afb822388bd337a761db8f86febf'
         '736df6cd828a3172701f63c8dc978cb9')
