# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=keepalived
pkgver=1.3.4
pkgrel=2
pkgdesc='Failover and monitoring daemon for LVS clusters'
arch=('i686' 'x86_64')
url='http://www.keepalived.org/'
license=('GPL2')
backup=('etc/keepalived/keepalived.conf' 'etc/sysconfig/keepalived')
depends=('openssl' 'popt' 'libnl1' 'ipset' 'iptables')
optdepends=('ipset: ipset support')
makedepends=('libnfnetlink' 'ipset' 'systemd')
options=('!emptydirs')
source=("http://www.keepalived.org/software/$pkgname-$pkgver.tar.gz")
md5sums=('b5283fe3f3d03f9866344732961ddf69')

build() {
#  # trick broken ./configure systemctl test
#  printf "#!/bin/sh\necho -.mount\n" > systemctl
#  chmod +x systemctl
#  PATH=$PWD:$PATH
#
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # move examples to /usr/share
  install -d -m 755 "$pkgdir/usr/share/$pkgname"
  mv "$pkgdir/etc/keepalived/samples" "$pkgdir/usr/share/$pkgname/samples"
}

# vim:set ts=2 sw=2 et:
