# $Id$
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openimageio
pkgver=1.1.2
pkgrel=2
pkgdesc="A library for reading and writing images, including classes, utilities, and applications."
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew')
makedepends=('cmake' 'qt' 'python2' 'boost' 'mesa')
optdepends=('qt: iv image viewer'
            'python2: bindings support')
source=(https://github.com/OpenImageIO/oiio/tarball/Release-$pkgver
        fix32bit-fail.patch)
md5sums=('8a5fd88f87fbf1826a4782041cf9e7d0'
         '64403604d0b74bd6c50fe8638207116a')

build() {
  cd "$srcdir"/$_pkgname*

  patch -Np1 < $srcdir/fix32bit-fail.patch

  cd src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages ..
  make
}

package() {
  cd "$srcdir"/$_pkgname*/src/build

  make DESTDIR="$pkgdir" install

  # license
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
