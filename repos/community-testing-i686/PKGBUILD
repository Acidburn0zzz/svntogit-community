# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=myodbc
pkgver=5.1.9
pkgrel=2
pkgdesc="ODBC driver/connector for MySQL"
arch=(i686 x86_64)
url="http://dev.mysql.com/downloads/connector/odbc/"
depends=('unixodbc' 'libmysqlclient')
license=('GPL')
options=('libtool')
source=("http://mysql.cce.usp.br/Downloads/Connector-ODBC/5.1/mysql-connector-odbc-${pkgver}.tar.gz"
	myconf.h.in)
md5sums=('80f146bd384a9011d968aa67df27e68e'
         '6c99c83e309b18d15ef0f07f0b461241')

build() {
  cd $srcdir/mysql-connector-odbc-${pkgver}
  cp $srcdir/myconf.h.in driver/myconf.h.in
  [ -x configure ] || { aclocal; libtoolize; automake --add-missing; autoreconf; }
  [ -f Makefile ] || CFLAGS="$CFLAGS -DTHREAD" \
	./configure --prefix=/usr --sysconfdir=/etc --with-unixODBC=/usr --with-odbc-ini=/etc/odbc.ini \
		--with-mysql-path=/usr --without-x --disable-gui --disable-test
  make
}

package() {
  cd $srcdir/mysql-connector-odbc-${pkgver}
  make DESTDIR=$pkgdir install
}
