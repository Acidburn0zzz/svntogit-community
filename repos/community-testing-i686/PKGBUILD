# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.1.9
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        use-system-qt5-headers.patch qt5.9.patch)
sha512sums=('f6936cc6a9a1d51c24d4be634cf38c97a42a926c7c92af74517a952163ad41ecf0ab105461d239035794651b303a71e1f85913242d698842b72dc66e3793f916'
            '29152204a36efc5fc2376b74be2ebe1d9cb253f9371ca8b0e9bf95c415c903f956d0d400eaffb3f708f81b19acd0a5383647af3a5a10cf6a61dfa586eb1bd57b'
            '83695d051d100ab6e8fa0aa73f9387f320f6bbaab39c8db835b27f5b24b1c38c993535ed3c5864975e5881231d0f3151d6e4eacb8cc9364f67febc6328bf5b01')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../qt5.9.patch
  patch -p1 -i ../use-system-qt5-headers.patch
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
