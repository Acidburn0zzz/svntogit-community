# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Eivind Uggedal <eivind@uggedal.com>

pkgname=mpv
pkgver=0.1.7
pkgrel=2
pkgdesc='Video player based on MPlayer/mplayer2'
arch=('i686' 'x86_64')
license=('GPL')
url='http://mpv.io'
depends=('portaudio' 'ffmpeg' 'lcms2' 'libxinerama' 'libquvi' 'mpg123' 'libxv' 'libxkbcommon' 'libva')
makedepends=('mesa' 'python-docutils')
options=(!emptydirs)
source=("https://github.com/mpv-player/$pkgname/archive/v$pkgver.tar.gz")
md5sums=('69185e255fe6e8b6b13dcf6ea9b2fefb')

build() {
  unset CFLAGS
  unset LDFLAGS

  cd $pkgname-$pkgver

  ./configure --prefix=/usr \
    --confdir=/etc/mpv \
    --disable-lircc \
    --disable-joystick \
    --disable-radio \
    --disable-radio-capture \
    --disable-radio-v4l2 \
    --disable-rpath \
    --disable-libbs2b \
    --disable-direct3d \
    --disable-corevideo \
    --disable-cocoa \
    --disable-coreaudio
  make
}

package() {
  cd $pkgname-$pkgver
  make -j1 DESTDIR="$pkgdir" install

  install -d "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 etc/{input,example,encoding-example-profiles}.conf \
    "$pkgdir"/usr/share/doc/mpv/examples
  install -m644 DOCS/{encoding.rst,tech-overview.txt} \
    "$pkgdir"/usr/share/doc/mpv
  install -m755 TOOLS/mpv_identify.sh "$pkgdir"/usr/bin
}
