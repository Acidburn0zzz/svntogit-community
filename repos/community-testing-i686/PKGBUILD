# $Id$
# Contributor: Paulo Matias <matias.archlinux-br.org>
# Contributor: Leslie P. Polzer <polzer.gnu.org>
# Contributor: erm67 <erm67.yahoo.it>
# Contributor: Daniel J Griffiths
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=djview4
pkgver=4.8
pkgrel=2
pkgdesc='Portable DjVu viewer and browser plugin'
url='http://djvu.sourceforge.net/djview4.html'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qt' 'djvulibre')
source=("http://downloads.sourceforge.net/djvu/djview-${pkgver}.tar.gz")
sha1sums=('266d207afb63a1ee63eed054190bf88888fda572')

install=install

build() {
	cd "${srcdir}/djview-${pkgver}"
	QTDIR=/usr ./configure --prefix=/usr
	sed 's/netscape/mozilla/g' -i nsdejavu/Makefile
	sed 's/swap/rofl_swap/g' -i src/qdjvuwidget.cpp
	make
}

package() {
	cd "${srcdir}/djview-${pkgver}"
	make DESTDIR="${pkgdir}" install-djview install-nsdejavu

	cd desktopfiles
	install -Dm644 hi32-djview4.png "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/icons/hicolor/64x64/apps/djvulibre-djview4.png
	install -Dm644 hi64-djview4.png "${pkgdir}"/usr/share/pixmaps/djvulibre-djview4.png
	install -Dm644 djvulibre-djview4.desktop "${pkgdir}"/usr/share/applications/djvulibre-djview4.desktop
}
