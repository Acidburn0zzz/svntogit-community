# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=2.0.0
_modelver=14
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('kyotocabinet' 'glib2')
makedepends=('gnome-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz"
        "$pkgname-model$_modelver.text.tar.gz::http://downloads.sourceforge.net/libpinyin/model$_modelver.text.tar.gz")
noextract=("$pkgname-model$_modelver.text.tar.gz")
sha512sums=('6a64c7f8378da1f1e364738f9cc9caaeb47b8bdc617ba916c807ff9336272d2d473697216bfc2820f515d6a9784d927f000914cb2004c339747ec33c58083f2a'
            '792d3cf7ab7fdea701737cea66d18f697015f19d18b6d26273db2da0ae28291f38b3d8a5717f595da63fbe573921c552bdce10c4bec9a0834947dfec92265da9')

prepare() {
  cd $pkgname-$pkgver

  ln -s "$srcdir"/$pkgname-model$_modelver.text.tar.gz data/model$_modelver.text.tar.gz
  sed -i "/wget.*model$_modelver\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --with-dbm=KyotoCabinet
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
