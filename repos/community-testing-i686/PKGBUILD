# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Dwayne Bent <dbb.1@liqd.org>
# Contributor: Paul-Sebastian Manole <brokenthorn@gmail.com>

pkgbase=luasec
pkgname=(lua-sec lua51-sec)
pkgver=0.4.1
pkgrel=1
pkgdesc='Lua bindings for OpenSSL library to provide TLS/SSL communication.'
arch=('i686' 'x86_64')
url='http://luasec.luaforge.net/'
license=('MIT')
makedepends=('openssl' 'lua' 'lua-socket' 'lua51' 'lua51-socket')
conflicts=('luasec')
replaces=('luasec')
source=("http://www.inf.puc-rio.br/~brunoos/luasec/download/$pkgbase-$pkgver.tar.gz")
md5sums=('b8a5fde3b3fdb6174f54cd51d7f53e12')

build() {
    cp -a $pkgbase-$pkgver $pkgbase-$pkgver-51
    msg2 'Building with lua 5.2'
    cd $pkgbase-$pkgver
    make linux
    msg2 'Building with lua 5.1'
    cd ../$pkgbase-$pkgver-51
    make INCDIR=-I/usr/include/lua5.1 linux
}

package_lua-sec() {
    depends=('openssl' 'lua' 'lua-socket')

    install -dm755 "$pkgdir/usr/lib/lua/5.2"
    cd $pkgbase-$pkgver
    make LUACPATH="$pkgdir/usr/lib/lua/5.2" LUAPATH="$pkgdir/usr/share/lua/5.2" install
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_lua51-sec() {
    depends=('openssl' 'lua51' 'lua51-socket')

    install -dm755 "$pkgdir/usr/lib/lua/5.1"
    cd $pkgbase-$pkgver-51
    make LUACPATH="$pkgdir/usr/lib/lua/5.1" LUAPATH="$pkgdir/usr/share/lua/5.1" install
    install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=4 sw=4 et:
