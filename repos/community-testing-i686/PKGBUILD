# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-shortcut-viewer
pkgver=1.3.1
pkgrel=2
pkgdesc='Deepin Shortcut Viewer'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-shortcut-viewer"
license=('GPL3')
depends=('deepin-tool-kit')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-shortcut-viewer/archive/$pkgver.tar.gz")
sha512sums=('0bd464e878a9b06cd5689605c04ff011af0e53d3c2d48cc3baa4c49f5f5eadec6b173d4878f7abe395457877b071e5ad221b4c456c40fef79b835305bb44fd59')

prepare() {
  cd deepin-shortcut-viewer-$pkgver
  sed -e 's/dtkbase/dtkbase1/' -e 's/dtkwidget/dtkwidget1/' -e 's/dtkutil/dtkutil1/' -i *.pro
}

build(){
  cd deepin-shortcut-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-shortcut-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
