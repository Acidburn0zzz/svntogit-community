# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=stlink
pkgver=1.3.0
pkgrel=1
pkgdesc='Firmware programmer for STM32 STLINK v1/v2 protocol'
arch=(i686 x86_64)
url='https://github.com/texane/stlink'
license=(BSD)
# Add support for gtk3-based GUI
depends=(libusb)
makedepends=(cmake)
install=stlink.install
source=($pkgname-$pkgver.zip::https://github.com/texane/stlink/archive/$pkgver.zip)
sha256sums=('b96a41fcdcf82d196d31953ae653c211650059c9a15f3d5bd7682a7b573241ab')

build() {
  cd stlink-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr \
        -DSTLINK_UDEV_RULES_DIR=/usr/lib/udev/rules.d \
        -DSTLINK_MODPROBED_DIR=/usr/lib/modprobe.d
  make
}

package() {
  cd stlink-$pkgver

  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm "$pkgdir"/usr/lib/libstlink.a
}
