# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Jan Steffens <heftig@archlinux.org>
# Contributor: Remi Gacogne <rgacogne[at]archlinux[dot]org>

pkgname=powerdns-recursor
pkgver=4.0.2
pkgrel=1
pkgdesc='Resolving DNS server'
url='https://www.powerdns.com/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gcc-libs' 'boost-libs' 'libsystemd' 'lua' 'openssl' 'protobuf')
makedepends=('boost' 'inetutils' 'pkg-config')
provides=('pdns-recursor')
conflicts=('pdns-recursor')
backup=('etc/powerdns/recursor.conf')
source=(http://downloads.powerdns.com/releases/pdns-recursor-${pkgver}.tar.bz2{,.asc})
sha512sums=('74dea771deb0bcce6209c256f4c2c82a76d34a80a4a67497b58bf8889166a51864c2649527475aa6e6355af8bba86a68d599828274eaa7521006384223f2e937'
            'SKIP')
validpgpkeys=('B76CD4671C0968BAA87DE61C5E50715BF2FFE1A7') # Pieter Lexis <pieter.lexis@powerdns.com>

build() {
  cd pdns-recursor-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc/powerdns \
    --disable-silent-rules \
    --enable-reproducible
  make
}

package() {
  cd pdns-recursor-${pkgver}
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/powerdns/recursor.conf"{-dist,}
}

# vim: ts=2 sw=2 et:
