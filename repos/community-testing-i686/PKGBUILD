# $Id$
# Maintainer: Sergej Pupykin <pupykin.s@gmail.com>
# Contributor: damir <damir@archlinux.org>

pkgname=libextractor
pkgver=0.6.3
pkgrel=6
pkgdesc="A library used to extract meta-data from files of arbitrary type"
arch=("i686" "x86_64")
license=('GPL')
url="http://gnunet.org/libextractor/"
depends=('libltdl')
makedepends=('flac' 'poppler' 'exiv2' 'qt' 'libvorbis' 'gtk2' 'libgsf' 'libmpeg2')
optdepends=('flac: flac support'
	    'poppler: pdf support'
	    'exiv2: metadata support'
	    'qt: the Qt GUI interface/thumbnails'
	    'gtk2: gtk2 GUI interface/thumbnails'
	    'libgsf: (optional) for ODF support'
	    'libmpeg2: mpeg support'
	    'libvorbis: ogg/vorbis support')
options=('!libtool' '!makeflags')
install=libextractor.install
source=("http://ftpmirror.gnu.org/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
md5sums=('7004ff6be06e400050f0ad7016140981'
         '81f95c31831dde62428a98ae32464c51')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  ./configure --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/share/info/dir
}
