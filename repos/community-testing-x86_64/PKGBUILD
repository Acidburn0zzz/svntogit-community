# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Tyler Dence <tyzoid@archlinux32.org>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgname=nextcloud-client
pkgver=2.5.1
pkgrel=2
pkgdesc='Nextcloud desktop client'
arch=(x86_64)
url='https://nextcloud.com/'
license=(GPL)
depends=(sqlite qtkeychain qt5-svg qt5-webengine qt5-webkit xdg-utils)
makedepends=(doxygen extra-cmake-modules kio python-sphinx qt5-tools)
optdepends=(
  'kio: integration with Dolphin'
  'nemo-python: integration with Nemo'
  'python-nautilus: integration with Nautilus'
  'python2-caja: integration with Caja'
)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/nextcloud/desktop/archive/v$pkgver.tar.gz"
        fix-build-with-qt5.12.patch::https://github.com/nextcloud/desktop/commit/129588442041c32b0d98f2361a69e0e350592f70.patch
        fix-login-crash.patch::https://github.com/yan12125/desktop/commit/09b0055dc3fe8ffae64dae7af9f522ed12ebdea8.patch)
sha256sums=('48be2556f4fddb956ad31e954be5f9c73482e936a80ec0efaeb30549dcd81f19'
            '69182f7f75428420385cde58a9e038c6c326105c3bccbe55f9a7bb1814e3f3fd'
            '3443f76b1480bb66b71bd94284ae91f8d689e525e55a399b878bf48ad80b5beb')
backup=('etc/Nextcloud/sync-exclude.lst')

prepare() {
  cd desktop-$pkgver

  patch -Np1 -i ../fix-build-with-qt5.12.patch

  # https://github.com/nextcloud/desktop/issues/941
  # https://bugs.archlinux.org/task/61106
  patch -Np1 -i ../fix-login-crash.patch

  mkdir -p build
}

build() {
  cd desktop-$pkgver/build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    ..

  make
  make doc-man
}

package() {
  cd desktop-$pkgver/build

  make DESTDIR="$pkgdir" install
}
