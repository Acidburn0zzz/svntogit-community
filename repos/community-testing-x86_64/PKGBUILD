# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=0.1.4
_commit=e27df5e5756868ae48e4a7f389cf788b544b13c6
#_golibver=0.4.4
_golibrev=c276a48ce147db8cba1951da6164f8979d021cb4
#_deepinapiver=2.92.4
_deepinapirev=9d927e1a74a684edea6d7d80bca096952355c75b
#_dbusfactoryver=2.92.3
_dbusfactoryrev=bef8c791e07beb144312b0eee83b00d1c7f68618
#_girgeneratorver=0.9.1
_girgeneratorrev=6e53a3b74f2076b2f472920cd2bc1ae4752e0608
pkgrel=1
pkgdesc='deepin file manager backend'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-file-manager"
license=('GPL3')
depends=('deepin-metacity' 'libcanberra' 'poppler-glib' 'gcc-libs>=5.3.0-3')
makedepends=('git' 'deepin-dbus-generator' 'deepin-gir-generator' 'gcc-go')
replaces=('deepin-file-manager-backend')
install="${pkgname}.install"
source=("git+https://github.com/linuxdeepin/deepin-file-manager.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_deepinapirev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_dbusfactoryrev"
        "git+https://github.com/linuxdeepin/go-gir-generator.git#commit=$_girgeneratorrev")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  sed -i "s|\"\${GOLDFLAGS}\"|\"\${GOLDFLAGS} $CFLAGS $LDFLAGS\"|" deepin-file-manager/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/

  go get github.com/howeyc/fsnotify github.com/mattn/go-sqlite3
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/$pkgname"
  make USE_GCCGO=1
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" do-install
}
