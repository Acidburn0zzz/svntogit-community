# $Id$
# Maintainer:
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2015.11.22
pkgrel=2
pkgdesc="console-based frontend for Pandora"
url="http://6xq.net/0017"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libao' 'ffmpeg' 'curl')
source=(http://6xq.net/projects/pianobar/$pkgname-$pkgver.tar.bz2)
sha256sums=('23fbc9e6f55b3277dba7a0f68ff721bad7f1eeea504c616ba008841686de322b')

build() {
    make -C ${pkgname}-${pkgver}
}

package() {
    make -C ${pkgname}-${pkgver} DESTDIR="$pkgdir" PREFIX=/usr install
    install -Dm644 $pkgname-$pkgver/COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm755 $pkgname-$pkgver/contrib/headless_pianobar "$pkgdir/usr/bin/pianoctl"
}
