# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=1.4.0
pkgrel=1
pkgdesc="GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support"
arch=('x86_64')
url="https://libosinfo.org/"
license=('GPL' 'LGPL')
depends=('glib2' 'libxslt' 'osinfo-db')
makedepends=('check' 'gobject-introspection' 'libsoup' 'vala' 'gtk-doc')
source=(https://releases.pagure.org/libosinfo/$pkgname-$pkgver.tar.gz{,.asc})
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF'  # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B') # Fabiano Fidêncio <fidencio@redhat.com>
sha256sums=('a29e61a82b1a5727b145403914812273dfa033a0c07a15500078726bb3b84165'
            'SKIP')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/export LC_ALL = C/d' maint.mk
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids \
              --enable-gtk-doc
  make
}

check() {
  cd $pkgname-$pkgver
  make check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
