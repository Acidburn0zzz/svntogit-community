# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=brial
pkgver=1.0.1
pkgrel=1
pkgdesc="Library for polynomials over boolean rings"
arch=(i686 x86_64)
url="https://github.com/BRiAl/BRiAl"
license=(GPL)
depends=(python2 m4ri)
makedepends=(boost)
source=("$pkgname-$pkgver.tar.gz::https://github.com/BRiAl/BRiAl/archive/$pkgver.tar.gz")
sha256sums=('1ed86e761c850d8febf41ee3f77412be091d48d528d5541dcb9fc412ae5d6220')

build() {
  cd BRiAl-$pkgver 

  export PYTHON=python2
  export CXXFLAGS="$CXXFLAGS -std=c++98"
  autoreconf -vi
  ./configure --prefix=/usr --enable-shared
  make

  cd sage-brial
  python2 setup.py build
}

package() {
  cd BRiAl-$pkgver

  make DESTDIR="$pkgdir" install

  cd sage-brial
  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1
}
