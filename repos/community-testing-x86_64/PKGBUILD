# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-xattr
pkgname=('python-xattr' 'python2-xattr')
pkgver=0.9.4
pkgrel=1
pkgdesc='Python wrapper for extended filesystem attributes'
arch=('x86_64')
license=('MIT')
url='https://github.com/xattr/xattr'
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/xattr/xattr/archive/v$pkgver.tar.gz")
sha512sums=('fb70d0f07189215558fc3e1565fc87b3307475703c6807b9bbecbeb0a575826a1f488107c8d66c99c96a36850598021ac58f722cbf90af63dde0b05b55b79957')

prepare() {
  cp -a xattr-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/xattr-$pkgver
  python setup.py build

  cd "$srcdir"/xattr-$pkgver-py2
  python2 setup.py build
}

check() { (
  # Build env doesn't have permission for those xattrs
  cd "$srcdir"/xattr-$pkgver
  python setup.py test

  cd "$srcdir"/xattr-$pkgver-py2
  python2 setup.py test
  ) || warning "Tests failed"
}

package_python-xattr() {
  depends=('python-cffi' 'python-setuptools')

  cd xattr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-xattr() {
  depends=('python2-cffi' 'python2-setuptools')

  cd xattr-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  mv "$pkgdir"/usr/bin/xattr{,2}
}

# vim:set ts=2 sw=2 et:
