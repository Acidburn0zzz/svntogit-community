# Maintainer: David Runge <dave@sleepmap.de>
_name=zopfli
pkgname=python-zopfli
pkgver=0.1.6
pkgrel=2
pkgdesc="Python bindings to zopfli"
arch=('x86_64')
url="https://github.com/obp/py-zopfli"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('168267dfbc9c33d85dfab2c004aa8f89e3d9d0619ef3bbde6d9a0bee1b302bb07acfd85c61dabb4fc6492df2d735147de8ea0cfe38a173b7434cacc34300e024')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
