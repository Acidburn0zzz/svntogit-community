# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server
pkgver=4.4.2.0
_ffmpeg_ver=2020_02_24
pkgrel=2
pkgdesc='Bring together your videos, music, photos, and live television'
arch=(x86_64)
url=https://emby.media
license=(custom)
depends=(
  alsa-lib
  aom
  bzip2
  dotnet-runtime
  expat
  fontconfig
  fribidi
  glibc
  gmp
  gnutls
  lame
  libass.so
  libdav1d.so
  libdrm
  libfreetype.so
  libjpeg-turbo
  libmfx
  libpng
  libtheora
  libva-drm.so
  libva.so
  libvorbisenc.so
  libvorbis.so
  libwebp
  libx264.so
  opus
  skia-sharp
  sqlite
  zlib
  zvbi
)
makedepends=(
  ffnvcodec-headers8.1
  nasm
)
optdepends=(
  'intel-media-sdk: Intel QuickSync support (requires Emby Premiere)'
  'nvidia-utils: Nvidia NVDEC/NVENC support (requires Emby Premiere)'
)
source=(
  https://github.com/MediaBrowser/Emby.Releases/releases/download/${pkgver}/embyserver-netcore_${pkgver}.zip
  https://mediabrowser.github.io/embytools/ffmpeg-${_ffmpeg_ver}.tar.gz
  https://mediabrowser.github.io/embytools/ffdetect-${_ffmpeg_ver}-x64.tar.xz
  emby-server
  emby-server.conf
  emby-server.service
  license.docx
  Permission_to_distribute.mbox
)
noextract=(license.docx)
backup=(etc/conf.d/emby-server)
install=emby-server.install
sha256sums=('36c65cfbd41ec83d415c098384032cd414da4f670e77013da7eb901bd16afe27'
            '6d115633eeb1d48a80f07f53c2e8c43093bd56a4a3d7d8b3d6207c4f473a61e0'
            '50ed2c4b4a8bc62baf92ac6192c8fe21a00dd77b0079d5a971958f412bc2bf9e'
            'ee6ed36a13fcd8fd0b2b818eac742cc2cc2513d7f8f6cd867fa8d1c45ef3dd22'
            'd1a805284d8a1329f6f6e736423be747d8951c5a9600fbb79c13a6f9be7149c0'
            '54bf6a38fcea32fae8468e217a0b1a66a50f5f1a5674fce35fefb6c3881bb827'
            'a6d7ea65dcb06392479a85e1a10a7aeb872d803da6f784f6935fcd4ee63008c6'
            '716cc88f47b829937884e765add9fe54ed27f0f4d1cec1a18045feb138fb66c9')

prepare() {
  rm -rf system/{electron,runtimes/{osx,tizen*,win*}}
}

build() {
  cd ffmpeg-${_ffmpeg_ver}_public

  export PKG_CONFIG_PATH=/usr/lib/ffnvcodec8.1/pkgconfig

  ./configure \
    --disable-doc \
    --disable-ffplay \
    --disable-shared \
    --disable-vdpau \
    --disable-libxcb \
    --disable-lzma \
    --disable-sdl2 \
    --disable-xlib \
    --enable-fontconfig \
    --enable-gmp \
    --enable-gnutls \
    --enable-gpl \
    --enable-libaom \
    --enable-libass \
    --enable-libdav1d \
    --enable-libdrm \
    --enable-libfreetype \
    --enable-libfribidi \
    --enable-libmfx \
    --enable-libmp3lame \
    --enable-libopus \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libwebp \
    --enable-libx264 \
    --enable-libzvbi \
    --enable-version3 \
    --enable-nvdec \
    --enable-nvenc \
    --enable-static \
    --enable-vaapi
  make
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve='ownership' system "${pkgdir}"/usr/lib/emby-server
  ln -s ../libSkiaSharp.so.68.0.0 "${pkgdir}"/usr/lib/emby-server/libSkiaSharp.so
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 755 bin/ffdetect "${pkgdir}"/usr/bin/ffdetect-emby
  install -Dm 755 ffmpeg-${_ffmpeg_ver}_public/ffmpeg "${pkgdir}"/usr/bin/ffmpeg-emby
  install -Dm 755 ffmpeg-${_ffmpeg_ver}_public/ffprobe "${pkgdir}"/usr/bin/ffprobe-emby
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

# vim: ts=2 sw=2 et:
