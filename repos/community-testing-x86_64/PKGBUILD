# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
_rtmidiver=3.0.0
pkgname=csoundqt
pkgver=0.9.5
pkgrel=3
pkgdesc="A frontend for Csound featuring a highlighting editor with autocomplete, interactive widgets and integrated help"
arch=('x86_64')
url="https://csoundqt.github.io/"
license=('CCPL' 'FDL' 'GPL' 'LGPL2.1')
# somehow include pythonqt
depends=('csound' 'csound-doc' 'qt5-declarative')
makedepends=('shared-mime-info')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "rtmidi-${_rtmidiver}.tar.gz::https://github.com/thestk/rtmidi/archive/v${_rtmidiver}.tar.gz")
sha512sums=('136fb395b955e21c153ccde3642362d4451bd458af78e235808a8b911a87e24fc5614c72b0e09f3dcd6ab1f49d563eb547dc1371b8ebd75cb8e051a2d629fb74'
            '8975a63e7be9102af70401cef48c702597b87efe2d8fa30a978fe280e26da1dfa90d6f30cfbd3df587462f0dd085d0f29e1c014e67d7fcd3a36960b6bcfb3e55')

prepare() {
  cd "${_name}-${pkgver}"
  # fix system-wide locations for html, examples and scripts
  sed -e 's,csdocdir = \"\",csdocdir = \"/usr/share/doc/csound/html\",' \
    -i src/csoundoptions.cpp
  sed -e 's@/usr/share/qutecsound/Examples/@/usr/share/csoundqt/Examples/@g' \
    -i src/qutecsound.cpp
  sed 's@../../csoundqt/src/Scripts@/usr/share/csoundqt/Scripts@' \
    -i src/types.h
  # fix icon in desktop file
  sed -e "s/qtcs/${pkgname}/" -i ${_name}.desktop
  # move rtmidi sources
  mv ${srcdir}/rtmidi-${_rtmidiver} .
}

build() {
  cd "${_name}-${pkgver}"
  qmake-qt5 qcs.pro CONFIG+=rtmidi "RTMIDI_DIR=rtmidi-${_rtmidiver}/"
  make
}

package() {
  cd "${_name}-${pkgver}"
  # executable
  install -vDm755 "bin/${_name}-d-cs6" "${pkgdir}/usr/bin/${pkgname}"
  # docs
  install -vDm644 doc/*.pdf  -t "${pkgdir}/usr/share/doc/${pkgname}"
  # mime types and icon
  install -vDm644 mime-types/*.xml -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm644 mime-types/csound-light-128.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/mimetypes/csound.png"
  # desktop file and icon
  install -vDm644 "${_name}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -vDm644 images/qtcs.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"

  # examples
  find examples -type f -print0 | xargs -0 -n1 -i{} install -Dm644 '{}' \
    "$pkgdir/usr/share/doc/${pkgname}/"'{}'
  cd src
  find Examples -type f -print0 | xargs -0 -n1 -i{} install -Dm644 '{}' \
    "$pkgdir/usr/share/${pkgname}/"'{}'

#  # pythonqt scripts (disabled until pythonqt is included properly)
#  find Scripts -type f -print0 | xargs -0 -n1 -i{} install -Dm644 '{}' \
#    "$pkgdir/usr/share/${pkgname}/"'{}'
}

# vim:set ts=2 sw=2 et:
