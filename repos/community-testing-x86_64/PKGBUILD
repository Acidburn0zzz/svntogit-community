# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-gevent
pkgname=(python-gevent python2-gevent)
pkgver=1.5.0
pkgrel=1
arch=('x86_64')
pkgdesc="Python network library that uses greenlet and libev for easy and scalable concurrency"
license=("MIT")
url="http://www.gevent.org/"
makedepends=('python-greenlet' 'python2-greenlet' 'cython' 'cython2' 'c-ares' 'libev')
checkdepends=('lsof' 'python-perf' 'python2-perf' 'python2-virtualenv' 'python-objgraph'
              'python2-objgraph' 'python-zope-interface' 'python2-zope-interface' 'python-dnspython'
              'python2-dnspython' 'python-requests' 'python2-requests' 'python-zope-event'
              'python2-zope-event' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/gevent/gevent/archive/$pkgver.tar.gz")
sha512sums=('20460c2ff5822b4ccff27d616e94897f4edc19372d19aaf8cb00877449d1c899481bc0312839ee5b77a7166a61584e0f4f779dc81e437ebbd75e042db68d50ae')

prepare() {
  cp -a gevent-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/gevent-$pkgver

  EMBED=0 \
  CYTHON=cython \
  PYTHON=python \
  python setup.py build

  cd "$srcdir"/gevent-$pkgver-py2

  EMBED=0 \
  CYTHON=cython2 \
  PYTHON=python2 \
  python2 setup.py build
}

check() {
  (
    cd "$srcdir"/gevent-$pkgver
    pyvenv "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python3.6/site-packages"
    python setup.py install
    make bench alltest
  ) || warning "Tests failed"

  (
    cd "$srcdir"/gevent-$pkgver-py2
    virtualenv2 "$srcdir/pyvenv" --system-site-packages
    . "$srcdir/pyvenv/bin/activate"
    export PYTHONPATH="$PYTHONPATH:/usr/lib/python2.7/site-packages"
    python setup.py install
    PYTHON=python2 make bench alltest
  ) || warning "Tests failed"
}

package_python-gevent() {
  depends=('c-ares' 'libev' 'python-greenlet')

  cd gevent-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-gevent() {
  depends=('c-ares' 'libev' 'python2-greenlet')

  cd gevent-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
