# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alex <mail.avatar@gmail.com>

pkgname=exfat-utils
pkgver=1.2.3
pkgrel=1
pkgdesc="Utilities for exFAT file system"
arch=('i686' 'x86_64')
url="https://github.com/relan/exfat"
license=('GPL2')
depends=('glibc' 'fuse')
source=("$pkgname-$pkgver.zip::https://github.com/relan/exfat/archive/v$pkgver.zip")
md5sums=('fd5c1ef6507290f2ff1802043deaab78')

build() {
  cd exfat-$pkgver

  autoreconf -i
  ./configure --prefix=/usr \
              --sbindir=/usr/bin

  make CCFLAGS="${CFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}

package() {
  cd exfat-$pkgver

  make DESTDIR="${pkgdir}" install

  # Install man pages
  install -Dm444 dump/dumpexfat.8 "${pkgdir}/usr/share/man/man8/dumpexfat.8"
  install -m444 fsck/exfatfsck.8 "${pkgdir}/usr/share/man/man8"
  install -m444 mkfs/mkexfatfs.8 "${pkgdir}/usr/share/man/man8"
  install -m444 label/exfatlabel.8 "${pkgdir}/usr/share/man/man8"
}
