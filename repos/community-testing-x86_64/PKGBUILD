# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.2.1
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
         dtkwidget-qt5.15.patch)
sha512sums=('28b3719961c826f08cc225ccd6327dbe4154a1a4776018dfec6b92c768c51e0b4c498de18ded4b070de36119fee0f15c7f95e8397840ada453f1a61864eefb01'
            'df6ad678c1dc198516692ff230bfda59a9e31146edcac03be7e5f7b05d67c038435ce544216edec7449cf1971a68275dd4d8a897333a9dc723f16403895c419a')

prepare() {
  patch -d dtkwidget-$pkgver -p1 -i ../dtkwidget-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
