# $Id$
# Maintainer : Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openvdb
pkgver=5.0.0
pkgrel=1
pkgdesc='A large suite of tools for the efficient storage and manipulation of sparse volumetric data discretized on three-dimensional grids'
url='https://github.com/dreamworksanimation/openvdb'
arch=('x86_64')
license=('MPL')
depends=('openexr' 'boost-libs' 'intel-tbb' 'zlib' 'jemalloc' 'blosc')
makedepends=('doxygen' 'boost' 'cmake' 'mesa' 'cppunit' 'glfw-x11' 'glu' 'python-numpy')
optdepends=('glfw: for tools'
            'glu: for tools'
            'python-numpy: python module')
source=("https://github.com/dreamworksanimation/openvdb/archive/v${pkgver}.tar.gz")
sha512sums=('8916d54683d81144114e57f8332be43b7547e6da5d194f6147bcefd4ee9e8e7ec817f27b65adb129dfd149e6b308f4bab30591ee953ee2c319636491bf051a2b')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  sed -i "s|DESTINATION docs|DESTINATION share/doc/$pkgname|" openvdb/CMakeLists.txt
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  export CXXFLAGS="${CXXFLAGS} -DPY_OPENVDB_USE_NUMPY"
  cmake .. \
    -DBLOSC_LOCATION=/usr/ \
    -DTBB_LOCATION=/usr/ \
    -DUSE_GLFW3=ON \
    -DGLFW3_LOCATION=/usr/ \
    -DILMBASE_NAMESPACE_VERSIONING=OFF \
    -DOPENEXR_NAMESPACE_VERSIONING=OFF \
    -DILMBASE_LOCATION=/usr/ \
    -DOPENEXR_LOCATION=/usr/ \
    -DCPPUNIT_LOCATION=/usr/include/cppunit \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOPENVDB_BUILD_PYTHON_MODULE=ON \
    -DOPENVDB_BUILD_DOCS=ON \
    -DOPENVDB_ENABLE_3_ABI_COMPATIBLE=ON

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}

# vim:set sw=2 sts=2 et:
