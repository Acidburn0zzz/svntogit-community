# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=sassc
pkgver=3.4.7
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
depends=("libsass")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('f515241260557f17932ed2fc205375169c40d5b83c7a24577487426e33be607a5a8a90e58512a4e6d4ed3796f391b09ef6e148f9b293a76c73cea267dcf9440b')


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
