# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Redistribution information:
# https://software.intel.com/en-us/mkl
# https://software.intel.com/en-us/articles/intel-math-kernel-library-license-faq
pkgname=intel-mkl
pkgver=2020.0.166
_pkgver=${pkgver/%./-/}
pkgrel=1
pkgdesc="Intel Math Kernel Library"
arch=(x86_64)
url="https://software.intel.com/en-us/mkl"
license=("custom:ISSL")
source=("http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/16318/l_mkl_$pkgver.tgz")
sha512sums=('470bdb6b435ad938a520185fcd35ab4ce1656da0884c32506575077f398b3db84d5eeb4ac9f1af5a4d29dc9c30d473c4ff82effe8015c4f2dd8e0e4038155127')

prepare() {
  cd l_mkl_$pkgver/rpm
  for rpm in *.rpm; do
    bsdtar -xf $rpm
  done
}

package() {
  cd l_mkl_$pkgver/rpm

  mkdir -p "${pkgdir}"/usr/include
  mkdir -p "${pkgdir}"/usr/lib

  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.{a,so} "${pkgdir}"/usr/lib/
  cp -r opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/include "${pkgdir}"/usr/include/mkl
  install -Dm644 opt/intel/compilers_and_libraries_${pkgver}/licensing/mkl/en/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
# vim:set ts=2 sw=2 et:
