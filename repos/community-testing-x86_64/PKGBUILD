# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpac
pkgver=0.4.5
pkgrel=9
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
depends=('wxgtk' 'libmad' 'faad2' 'ffmpeg' 'freeglut')
license=('LGPL')
options=('!makeflags')
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libpng14-infopp-null.patch openjpeg14.patch)
sha1sums=('2ec03c1d7dc3f4d10c0f7b47696cc1e753a56fc8'
          '00ba08808ec46ef6b8301ede26e500b3449253c1'
          '95747ca98cdf0efc33ec776764401f5a83818ab5')

build() {
  cd "${srcdir}/${pkgname}"
  chmod +x configure
  sed -i 's|--warn-common||' configure
  sed -i 's#lib64#lib#g' configure
  patch -p1 -i "${srcdir}/libpng14-infopp-null.patch"
  patch -p1 -i "${srcdir}/openjpeg14.patch"
	
  export LDFLAGS=${LDFLAGS/,--as-needed/}
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no
  make
}

package() {
  cd "${srcdir}/${pkgname}"
  make DESTDIR="${pkgdir}" install install-lib
}
