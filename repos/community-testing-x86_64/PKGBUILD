# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.6.8
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libspectre' 'poppler-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-reader/archive/$pkgver.tar.gz")
sha512sums=('c0eae2da526346da28a7dd9c5dc0328cfa035e646abb8d183a130aa19d1ae799988f9820bc96e5f8cc00290cbaf2d8356e77c458a01c3143133958fc2881a800')

prepare() {
  cd deepin-reader-$pkgver/src
  sed -i '/include <QMetaType>/a #include <QRectF>' pdfControl/docview/commonstruct.h
  sed -i '/include <QPainter>/a #include <QPainterPath>' CustomControl/RoundColorWidget.cpp lpreviewControl/bookmark/bookmarkdelegate.cpp \
                                                         utils/utils.cpp lpreviewControl/thumbnail/ThumbnailDelegate.cpp lpreviewControl/note/notesdelegate.cpp \
                                                         lpreviewControl/search/searchresdelegate.cpp
  sed -i '/include <QRect>/a #include <QPainterPath>' document/model.h
  sed -i '/include <QMap>/a #include <QSet>' uiframe/DocSheet.h
  sed -i '/include <QBitmap>/a #include <QMouseEvent>' djvuControl/SheetBrowserDJVU.cpp
  sed -i '/include <QDesktopWidget>/a #include <QMouseEvent>' widgets/SlideWidget.cpp
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
