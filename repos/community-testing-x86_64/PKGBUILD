# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.5.2+15+gc0939ab6
pkgrel=1
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://github.com/solus-project/budgie-desktop"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter' 'graphene')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=c0939ab629c0719370da25ce2b63e7c1aebb92d7  # master
source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "desktop-override.patch")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            'baffb1d64715c20c1ae444a5b8ea0f8e9bf69cccf30e5e2c6729fe6d0c7491d2')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Override settings only for Budgie
  patch -Np1 -i ../desktop-override.patch

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule update
}

build() {
  arch-meson $pkgname build -D with-desktop-icons=none
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
