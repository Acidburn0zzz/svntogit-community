# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

_pkgbase=pybox2d
pkgbase=python-pybox2d
pkgname=('python-pybox2d' 'python2-pybox2d')
pkgver=2.3.2
pkgrel=4
epoch=1
pkgdesc='Python wrapper for Box2D'
arch=('x86_64')
url='https://github.com/pybox2d/pybox2d'
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools' 'swig')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pybox2d/$_pkgbase/archive/$pkgver.tar.gz")
sha256sums=('ca7111557df3ecda3cb48b6c5fb3290fa8b45b6226a34867d2a85cbee5747266')

prepare() {
	# Generate py library properly
	rm $_pkgbase-$pkgver/library/Box2D/Box2D.py
	cp -a $_pkgbase-$pkgver{,-python2}
}

build() {
	cd $_pkgbase-$pkgver
	swig -python -c++ -IBox2D -small -O -includeall -ignoremissing -w201 -globals b2Globals -outdir library/Box2D -keyword -w511 -D_SWIG_KWARGS -o Box2D/Box2D_wrap.cpp Box2D/Box2D.i
	python setup.py build

	cd ../$_pkgbase-$pkgver-python2
	swig -python -c++ -IBox2D -small -O -includeall -ignoremissing -w201 -globals b2Globals -outdir library/Box2D -keyword -w511 -D_SWIG_KWARGS -o Box2D/Box2D_wrap.cpp Box2D/Box2D.i
	python2 setup.py build
}

check() {
	cd $_pkgbase-$pkgver
	python setup.py test

	cd ../$_pkgbase-$pkgver-python2
	python2 setup.py test || warning "Tests failed"
}

package_python-pybox2d() {
	depends=('python')

	cd $_pkgbase-$pkgver
	python setup.py install --root "$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pybox2d() {
	depends=('python2')

	cd $_pkgbase-$pkgver-python2
	python2 setup.py install --root "$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
