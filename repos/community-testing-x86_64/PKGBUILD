# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Contributor: Isaac Dupree <antispam@idupree.com>

pkgname=libsass
pkgver=3.4.9
pkgrel=1
pkgdesc="C implementation of Sass CSS preprocessor (library)."
arch=("x86_64")
url="http://libsass.org/"
license=("MIT")
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('f81c6cea533090f7d94395792fa5b9dad8f8e912673f764e75d2350ba0bb51c80ad22f64d9bff6a20d04be21b7a7fb802f75a745a0fceca9c2ecc5ca7ace0988')


build() {
	cd "$srcdir/$pkgname-$pkgver"

	autoreconf -i
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir/" install
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
