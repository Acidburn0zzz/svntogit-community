# Maintainer: SÃ©bastien "Seblu" Luttringer
# Contributor: Brian Bidulock <bidulock@openss7.org>

pkgname=lldpd
pkgver=1.0.1
pkgrel=2
pkgdesc='802.1ab implementation (LLDP) to help you locate neighbors'
arch=('x86_64')
url='https://vincentbernat.github.io/lldpd/'
license=('custom:ISC' 'GPL')
depends=('glibc' 'libevent' 'libbsd' 'zlib' 'openssl' 'pciutils' 'perl'
         'libxml2' 'net-snmp' 'jansson')
backup=('etc/lldpd.conf')
validpgpkeys=('AEF2348766F371C689A7360095A42FE8353525F9') # Vincent Bernat <bernat@luffy.cx>
source=("https://media.luffy.cx/files/lldpd/lldpd-$pkgver.tar.gz"
        "lldpd-$pkgver.tar.gz.sig::https://media.luffy.cx/files/lldpd/lldpd-$pkgver.tar.gz.gpg"
        'LICENSE'
        'lldpd.service'
        'lldpd.sysusers'
        'lldpd.tmpfiles')
sha512sums=('61b2a7a3a78276c613c6d0b34eb625e69e5c8a206b6658a167d6a09ac6d0e1c943e46c0ff6fc3538a791d9947f193185b65848da581b3d6ecb9bc0befadde8dd'
            'SKIP'
            'e8a218f0b8a9eca4e67b2e614c07375e3fc0489c88300cb323ca02250f6ffa12ca5b4accba8efd45dff577b5b42ac31aa4252389d5471b8fccad7163ebcc0bae'
            '4ef437d38315ddcc594a6f58590830baf00647b2076cdee13186f8b176c92b180b6dcc744d6a364eeb593c3e255892f866145f70a21bb189be54b156432d585e'
            '51d044dfc8d1b88184a16c3ec9ffa7da6d631a6a442eaf9170fa17f37e4d10bee444860cd388343c59420ddbbc3878d8c657e0a19a0fd15aa325bcb269f2a2a6'
            '3e8b1dcf9843c6f902684df8512a34b04d3c8df59d1c0962cea20d4205ad12e1e5cac764e3244944316271a0f099d3982e6740939b69fbe993ae88c29fd88d33')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-snmp \
    --with-xml \
    --with-readline \
    --with-privsep-user=lldpd \
    --with-privsep-group=lldpd \
    --with-privsep-chroot=/run/lldpd/chroot \
    --with-lldpd-ctl-socket=/run/lldpd/socket \
    --with-lldpd-pid-file=/run/lldpd/pid
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  # config stuff
  install -D -m 644 /dev/null "$pkgdir/etc/lldpd.conf"
  install -d -m 755  "$pkgdir/etc/lldpd.d"
  # systemd stuff
  install -D -m 644 lldpd.service "$pkgdir/usr/lib/systemd/system/lldpd.service"
  install -D -m 644 lldpd.sysusers "$pkgdir/usr/lib/sysusers.d/lldpd.conf"
  install -D -m 644 lldpd.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/lldpd.conf"
  # license
  install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
