# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Frederik Schwan <frederik dot schwan at linux dot com>

pkgname=gitea
pkgver=1.5.1
pkgrel=2
pkgdesc="Painless self-hosted Git service. Community managed fork of Gogs."
arch=('x86_64')
url="https://gitea.io"
license=('MIT')
depends=('git')
makedepends=('go-pie' 'go-bindata')
optdepends=(
  'mariadb: MariaDB support'
  'memcached: MemCached support'
  'openssh: GIT over SSH support'
  'pam: Authentication via PAM support'
  'postgresql: PostgreSQL support'
  'redis: Redis support'
  'sqlite: SQLite support'
)
backup=('etc/gitea/app.ini')
_commit=38d8b8cf49ce62dd7cfc806f1995d00e6fecc707  # tags/1.5.1^0
source=("git+https://github.com/go-gitea/gitea.git#commit=${_commit}"
        gitea.tmpfiles
        gitea.service
        gitea-repos.patch
        gitea-ldflags.patch
        gitea-disable-u2f.patch)
sha256sums=('SKIP'
            'da04ca25b76bcfc443c49da056393240d94d8c7e3b4bd63d4cf10e5aea001239'
            '49c6da593d9c463d39bec47aa2496a636944d5c673c2983be731466d4dbbf578'
            '66c5a84fbbe56dd8b3bca3a779aaaf5d4855573f0dd2b273018a8983d7951a72'
            'f062d395b3122edcbcb51f6bac59da70ef57bfcf4693c17bde14ee35b1ef5e92'
            'd6842efd13b6971b77d233ff3ac2bd3d81c5d26c348ae1f2a2b19b0e7237726e')

prepare() {
  mkdir -p gopath/src/code.gitea.io
  ln -rTsf ${pkgname} gopath/src/code.gitea.io/${pkgname}

  cd ${pkgname}
  # Change default repos path for ArchLinux
  patch -Np1 -i ../gitea-repos.patch
  # Fix LDFLAGS not being respected by Go
  patch -Np1 -i ../gitea-ldflags.patch
  # https://github.com/go-gitea/gitea/issues/4692
  patch -Np1 -i ../gitea-disable-u2f.patch
}

build() {
  export GOPATH="${srcdir}"/gopath
  cd gopath/src/code.gitea.io/gitea
  make generate
  EXTRA_GOFLAGS="-gcflags all=-trimpath=${GOPATH} -asmflags all=-trimpath=${GOPATH}" \
  make GOFLAGS="-v" TAGS="bindata sqlite tidb pam" build
}

package() {
  install -Dm755 ${pkgname}/${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 ${pkgname}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
  install -dm755 -o root -g git "${pkgdir}"/etc/gitea
  install -m 660 -o root -g git ${pkgname}/custom/conf/app.ini.sample "${pkgdir}"/etc/gitea/app.ini
}
