# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=(python-cysignals python2-cysignals)
pkgver=1.4.0
pkgrel=3
pkgdesc="Interrupt and signal handling for Cython"
arch=(i686 x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(cython2 cython pari-sage)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgbase-$pkgver.tar.bz2")
md5sums=('c2d216ae3676c670c68dfb07e7ed25bc')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cysignals() {
  depends=(python pari-sage)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"

# Keep scripts in python2 package
  rm -r "$pkgdir"/usr/bin
}

package_python2-cysignals() {
  depends=(python2 pari-sage)
  conflicts=(cysignals)
  provides=(cysignals)
  replaces=(cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"
}
