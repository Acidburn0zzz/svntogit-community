# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com
# Based on gnome-control-center:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-control-center
pkgver=1.8.2
pkgrel=3
pkgdesc="The Control Center for Cinnamon"
arch=('i686' 'x86_64')
depends=('cinnamon' 'cheese' 'libgnomekbd' 'network-manager-applet')
optdepends=('gnome-color-manager: for color management tasks')
makedepends=('intltool' 'gnome-common')
url="https://github.com/linuxmint/cinnamon-control-center"
install=cinnamon-control-center.install
license=('GPL')
options=('!libtool' '!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/cinnamon-control-center/archive/$pkgver.tar.gz"
        "gnomerrlabeler.patch"
        "idle-delay.patch"
        "remove_obex.patch")
sha256sums=('78effed26a8fdce481cfe467889c9afdb6db78a961aec61b918c83c9781ccf4f'
            'a01db243251c3da59c969d4538a35a63020d1e20866ff700ef273debd05456db'
            '98227484162071744b5a336a5b3915c211f5eb1559034b3d40e4a9d790fe0e31'
            '3df65b1f5e691d4a4634c05698da6df79cb23a0ebf71b201f23c48742b75b6e4')

prepare() {
  cd $pkgname-$pkgver

  # Build fix for GNOME 3.8
  patch -Np1 -i "$srcdir/gnomerrlabeler.patch"

  # Runtime fix for GNOME 3.8
  patch -Np1 -i "$srcdir/idle-delay.patch"

  # Remove "Browse Files..." option as it's gone from gnome-bluetooth 3.8
  patch -Np1 -i "$srcdir/remove_obex.patch"
}

build() {
  cd $pkgname-$pkgver

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --enable-systemd \
      --disable-update-mimedb

  #https://bugzilla.gnome.org/show_bug.cgi?id=656229
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove unused stuff
  make -C panels/sound DESTDIR="$pkgdir" uninstall
  make -C panels/sound/data DESTDIR="$pkgdir" install
  make -C shell DESTDIR="$pkgdir" uninstall
  make -C shell DESTDIR="$pkgdir" install-libLTLIBRARIES

  # Remove broken desktop files
  rm -R "$pkgdir/usr/share/applications"
  rm -R "$pkgdir/etc"

  # Fix a warning
  chown 102:0 "$pkgdir/usr/share/polkit-1/rules.d"
  chmod 700 "$pkgdir/usr/share/polkit-1/rules.d"
}
