# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=python-cypari2
pkgver=2.1.1
pkgrel=5
pkgdesc="Cython bindings for PARI"
arch=(x86_64)
url="https://www.sagemath.org"
license=(LGPL)
depends=(python-cysignals)
makedepends=(cython)
source=($pkgname-$pkgver.tar.gz::"https://github.com/defeo/cypari2/archive/$pkgver.tar.gz"
        cypari2-pari-2.13.patch::"https://github.com/sagemath/cypari2/commit/1a4be244.patch")
sha256sums=('8b707cba6f5ed249105348d3e29ce55c12c7ed5556ff6e0ae03f7aa792321e10'
            'ec9aa1e50480900883cbbb9b334a412717cfc3f624f5bc66459265de3848416e')

prepare() {
  patch -d $pkgbase-$pkgver -p1 -i ../cypari2-pari-2.13.patch # Port to PARI 2.13 API changes
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build
}

package_python-cypari2() {
  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"
}
