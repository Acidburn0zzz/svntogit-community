# $Id$
# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=0.1.28
pkgrel=1
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/projectatomic/skopeo'
license=(APACHE)
depends=(gpgme device-mapper ostree)
makedepends=(go-pie go-md2man btrfs-progs)
source=($pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz
        0001-Revert-ostree-add-selinux-label-for-each-file.patch)
sha256sums=('f97ce19e7f88718e7ead012dcf5e72808c87ba2f7ca71226ebbc0c02c57c3786'
            '4810389fbf2ecfb6d8c762c4de6387bac173e422cf03841bfcbee1af8045649a')

prepare() {
  pushd $pkgname-$pkgver/vendor/github.com/containers/image
  patch -p1 -i "$srcdir/0001-Revert-ostree-add-selinux-label-for-each-file.patch"
  popd

  export GOPATH="$srcdir"
  mkdir -p src/github.com/projectatomic
  cp -r $pkgname-$pkgver src/github.com/projectatomic/skopeo
}

build() {
  cd src/github.com/projectatomic/skopeo
  go build -v -o skopeo ./cmd/skopeo
  make docs
}

package() {
  cd src/github.com/projectatomic/skopeo
  make DESTDIR="$pkgdir" install
}
