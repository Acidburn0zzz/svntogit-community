# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-workhorse
pkgver=8.32.1
pkgrel=1
pkgdesc="HTTP server to unload Git HTTP traffic from GitLab Rails app (Unicorn)"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-workhorse"
license=('MIT')
depends=('glibc' 'perl-image-exiftool')
makedepends=('go')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-workhorse/repository/archive?sha=v${pkgver}"
        "gitlab-workhorse.service")
sha512sums=('894f704e0b5ea2cb2b896db3e79600b880e7029a473b8db580c6c2fa10b641561334d72a722ef260d783674c5a4445bd615542f4a4a08e8e72711d2b3419ce63'
            '5cec3ac883759ebb0e0c1e6074610b9cdc0d79370b1228acb3a96081a6d843c5a8bfbeb912ce1599058ee3842577c09d147ebf04ce7a760032ef485c4ba8f87a')

build() {
  cd "${pkgname}-v${pkgver}-"*

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  make
}

package() {
  cd "${pkgname}-v${pkgver}-"*

  install -Dm755 "gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
  install -Dm755 "gitlab-zip-cat" "${pkgdir}/usr/bin/gitlab-zip-cat"
  install -Dm755 "gitlab-zip-metadata" "${pkgdir}/usr/bin/gitlab-zip-metadata"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-workhorse.service" "${pkgdir}/usr/lib/systemd/system/gitlab-workhorse.service"
}

# vim:set ts=2 sw=2 et:
