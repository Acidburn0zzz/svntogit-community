# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.12.0
pkgrel=1
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=(x86_64)
url="https://ispc.github.io/"
license=(BSD)
depends=(ncurses zlib llvm-libs clang)
makedepends=(llvm python lib32-glibc cmake)
source=("https://github.com/ispc/ispc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9ebc29adcdf477659b45155d0f91e61120a12084e42113d0e9f4ce5cfdfbdcab')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DISPC_INCLUDE_EXAMPLES=OFF \
    -DISPC_NO_DUMPS=ON 
  make
}

check() {
  cd build
  make check-all
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
  cd ../${pkgname}-${pkgver}
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
