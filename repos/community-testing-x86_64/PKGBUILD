# Maintainer: Ray Rashif <schiv@archlinux.org>
# Maintainer: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
epoch=1
pkgver=0.8.2
pkgrel=2
pkgdesc="A BibTeX editor for KDE"
arch=(x86_64)
url='https://userbase.kde.org/KBibTeX'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qt5-xmlpatterns qoauth kparts)
optdepends=('okular: Document preview')
makedepends=(extra-cmake-modules kdoctools qca-qt5)
source=("https://download.kde.org/stable/KBibTeX/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('081c61c4bb55cf3c88d59877684956df7a235fe80a40a37af92224cdf5d677d6'
            'SKIP')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
