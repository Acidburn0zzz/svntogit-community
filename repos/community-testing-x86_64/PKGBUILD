# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qmidiarp
pkgver=0.6.4
pkgrel=2
pkgdesc="A MIDI arpeggiator, phrase generator and controller LFO for the ALSA sequencer."
arch=(x86_64)
url="https://sourceforge.net/projects/qmidiarp/"
license=('GPL2')
groups=('lv2-plugins')
depends=('jack' 'hicolor-icon-theme' 'liblo' 'lv2' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('06f3dc52c6673f452af4d5c5f4d2efc50fbe16b8fe792cb9d115f9efa124224e0131e763d31ee60cee68b78d952061e69976a09fa3118b23362ea3cea76260d0')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr --enable-qt5
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
}
