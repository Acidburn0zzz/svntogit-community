# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=0.0.9
_extramodules=extramodules-ARCH
pkgrel=2
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('linux')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz")
sha512sums=('31f7507221c995bcf2667ff59765c14eea0e0d2739debbe85be785f1290f3e84614e9e691c84434798f5dc427deaafe116171ea72e7b14b862c90e8107545bee')

build() {
  cd deepin-anything-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package() {
  cd deepin-anything-$pkgver/kernelmod
  install -Dm644 vfs_monitor.ko "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
  gzip "$pkgdir"/usr/lib/modules/$_extramodules/vfs_monitor.ko
}
