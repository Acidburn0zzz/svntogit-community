# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.8.5
pkgrel=2
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('pro-audio' 'vee-one' 'lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info' 'fftw')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('1f7753bc40e2ac80a2bdb1f9393b917e49dc6dbb7967e79956bdb9002fd95880e62c818c5d016bac16fff4a7ee52c803b9f3caf3f04664c7e8387c18668e2415')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
