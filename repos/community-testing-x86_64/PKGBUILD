# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=stubby
pkgver=0.4.0
pkgrel=1
pkgdesc="DNS Privacy stub resolver"
arch=(x86_64)
url="https://dnsprivacy.org/wiki/display/DP/DNS+Privacy+Daemon+-+Stubby"
license=(BSD)
backup=(etc/stubby/stubby.yml)
depends=(getdns libsystemd libyaml)
makedepends=(cmake)
source=(https://github.com/getdnsapi/stubby/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('8e6a4ba76f04b23612d58813c4998141b0cc6194432d87f8653f3ba5cf64152a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's/^# appdata_dir:/appdata_dir:/g' -e 's:/etc/unbound/getdns-root.key:/etc/trusted-key.key:g' -i stubby.yml.example
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_BUILD_TYPE=Release
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  rmdir "${pkgdir}"/var{/run,}
  cd ${pkgname}-${pkgver}
  install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
  install -Dm644 systemd/${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
