# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
pkgname=opendht
pkgver=1.7.3
pkgrel=2
epoch=1
pkgdesc="A C++11 implementation of the Kademlia DHT (Distributed Hash Table)"
arch=('x86_64')
depends=('gnutls' 'nettle' 'readline' 'argon2' 'jsoncpp' 'restbed')
makedepends=('msgpack-c' 'cmake' 'cython' 'systemd' 'cppunit')
optdepends=('python: to use the Python bindings')
url="https://github.com/savoirfairelinux/opendht"
license=('GPL3')
source=("https://github.com/savoirfairelinux/opendht/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('957ddfd4acd366625bd0d1a22fceaad42e3768cdb391134f4dc100946f477227')
sha512sums=('ee7af8aec5749965b4e46eb5ef9b4b4aac98b22930de6b239c51ec629abca67af017a99f3b282286a10dab1ef623ff761069566cc9fe1973d1cd06cbd2dee8a7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 'Building...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_BUILD_TYPE=None \
    -DOPENDHT_TESTS=ON \
    -DOPENDHT_DOCUMENTATION=OFF \
    -DOPENDHT_STATIC=OFF \
    -DOPENDHT_SYSTEMD=ON \
    -DOPENDHT_LTO=ON \
    -DOPENDHT_TOOLS=ON \
    -DOPENDHT_PYTHON=ON \
    -DOPENDHT_PROXY_SERVER=ON \
    -DOPENDHT_PROXY_CLIENT=ON \
    -DOPENDHT_PUSH_NOTIFICATIONS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cd build
  make test
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  msg2 'Installing...'
  cd build
  make DESTDIR="$pkgdir" install
  cd ..

  msg2 'Installing documentation...'
  install -D -m644 README.md "${pkgdir}/usr/share/doc/opendht/README.md"
}

# vim:set ts=2 sw=2 et:
