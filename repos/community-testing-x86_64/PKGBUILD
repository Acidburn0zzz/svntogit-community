# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.26.0+33+g78fd6a1
pkgrel=1
pkgdesc="GNOME Flashback session"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
depends=('gnome-bluetooth' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'gnome-themes-standard' 'libibus' 'metacity')
makedepends=('autoconf-archive' 'git' 'intltool' 'python')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=78fd6a1d90f54d1241256076adf441249d842438  # master
source=("git+https://git.gnome.org/browse/gnome-flashback#commit=$_commit"
        "git+https://git.gnome.org/browse/libgnome-volume-control")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git config --local submodule.gnome-flashback/libsound-applet/gvc.url "$srcdir/libgnome-volume-control"
  git submodule update --init

  NOCONFIGURE=1 ./autogen.sh
}

check() {
  cd $pkgname
  make check
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --enable-compile-warnings=minimum --enable-gtk-doc
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
