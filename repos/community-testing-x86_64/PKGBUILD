# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.4.11
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url='http://pawsa.fedorapeople.org/balsa/'
depends=('gmime' 'libwebkit' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview2' 'gtkspell' 'gnome-icon-theme' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils' 'intltool')
install=balsa.install
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2
        gmime26.patch)
md5sums=('915c622b6385aa4f83d5eee8f31ee8e8'
         '108d33f533558a371189441edce7d7e6')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch -Np1 -i "${srcdir}/gmime26.patch"
    autoreconf -fi

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-ssl \
        --with-gpgme=gpgme-config \
        --with-gss \
        --with-ldap \
        --with-gtksourceview \
        --with-gtkspell \
        --with-rubrica \
        --with-sqlite \
        --without-nm \
        --without-gnome \
        --with-html-widget=webkit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
