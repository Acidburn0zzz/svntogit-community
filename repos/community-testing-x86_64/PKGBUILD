# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.8.2
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(go)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/v$pkgver.zip)
sha512sums=('d53470b095d07aeb24319b3d7ba7d4079499df9ed10182088bb999bea5c02bd047eaadf95b094ef23093b02421165e6681b5aa17495cc13d6f3bb97864eec5cd')

build() {
  cd vegeta-$pkgver
  BUILDDATE=`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +'%FT%TZ%z'`
  go build -ldflags "-X main.Version=$pkgver -X main.Date='$BUILDDATE'"
}

check() {
  cd vegeta-$pkgver
  go test ./...
}

package() {
  cd vegeta-$pkgver
  install -Dm755 vegeta "$pkgdir/usr/bin/vegeta"
}
