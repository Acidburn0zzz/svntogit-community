# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=0.0.8
pkgrel=1
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'kwin')
makedepends=('cmake' 'expac' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz")
sha512sums=('3d1653865e972342a55e9c3894ece3c0ae8b6b36b2a3463b883ed9309a3af2e118dbc7b41490f6a97d4705aadee0adbd2fb878544f30d0999e61439287cfdc28')

build() {
  cd dde-kwin-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DKWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  make
}

package() {
  cd dde-kwin-$pkgver
  make DESTDIR="$pkgdir" install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
