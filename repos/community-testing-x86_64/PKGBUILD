# Maintainer: David Runge <dave@sleepmap.de>

pkgname=hexter
pkgver=1.1.0
pkgrel=3
pkgdesc="A Yamaha DX7 modeling software synthesizer for the DSSI Soft Synth Interface"
arch=('x86_64')
url="https://github.com/smbolton/hexter"
license=('GPL2')
groups=('dssi-plugins' 'pro-audio')
depends=('alsa-lib' 'gtk2' 'liblo')
makedepends=('dssi' 'ladspa')
optdepends=('dssi: for jack-dssi-host as plugin host'
            'qtractor: plugin host')
source=("$pkgname-$pkgver.tar.gz::https://github.com/smbolton/${pkgname}/archive/version_${pkgver}.tar.gz")
sha512sums=('4e779bf386d79e4e4869e84d0ab600976b3b8f40ad96d79cde6315f378554a86ebf78f4e85e777ed7820842a840b508afdd19a62b42a7d2f65d16c26dfa146f2')

prepare() {
  mv -v "${pkgname}-version_${pkgver}" "${pkgname}-${pkgver}"
  cd "$pkgname-$pkgver"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
