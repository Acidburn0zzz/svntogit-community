# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2017.08.30
pkgrel=4
pkgdesc="Console-based frontend for Pandora"
arch=('x86_64')
url="https://6xq.net/pianobar/"
license=('MIT')
depends=('libao' 'ffmpeg' 'curl' 'json-c')
source=(https://6xq.net/pianobar/$pkgname-$pkgver.tar.bz2{,.asc}
        pianobar-ffmpeg4.patch::"https://github.com/PromyLOPh/pianobar/commit/38b16f99.patch")
sha256sums=('ec14db6cf1a7dbc1d8190b5ca0d256021e970587bcdaeb23904d4bca71a04674'
            'SKIP'
            'cc90b0f0cd7b02a1a5470af2cb60e5dc6f45bcbc2302faba6502f2f6c9e73bf0')
validpgpkeys=('017D74E27F5856963801781DF663943E08D8092A') # Lars-Dominik Braun <lars@6xq.net>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../pianobar-ffmpeg4.patch # Fix build with ffmpeg 4
}

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm644 $pkgname-"${pkgver}"/COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 $pkgname-"${pkgver}"/contrib/headless_pianobar "${pkgdir}"/usr/bin/pianoctl
}
