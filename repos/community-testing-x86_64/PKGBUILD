# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=2.1.2
pkgrel=1
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=(x86_64)
url="https://github.com/ocaml/dune"
license=(Apache)
depends=(glibc ocaml ocaml-findlib)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ede9fe430ebe1404a6d39cdf8c45cb8b08864fbe2c988545b7fb5469dcc80df3edd3d96e32d761d1ddf7a7b4e06e4e804ccddf03d7fb4a6231a8c6b767e2eca1')

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" INSTALL_ARGS="--prefix=/usr --libdir='$(ocamlfind printconf destdir)'" install

    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
}
