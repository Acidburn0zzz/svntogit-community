# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Daniel Wallace < danielwallace at gtmanfred dot com>
# Contributor: Mitch Bigelow <ipha00@gmail.com>
# Contributor: Patrick Palka <patrick@parcs.ath.cx>

pkgname=pianobar
pkgver=2017.08.30
pkgrel=5
pkgdesc="Console-based frontend for Pandora"
arch=('x86_64')
url="https://6xq.net/pianobar/"
license=('MIT')
depends=('libao' 'ffmpeg' 'curl' 'json-c')
source=(https://6xq.net/pianobar/$pkgname-$pkgver.tar.bz2{,.asc}
        pianobar-ffmpeg4.patch::"https://github.com/PromyLOPh/pianobar/commit/38b16f99.patch")
sha512sums=('35019bd387a323c85928e8979c075c7b2a8d88115066a55068dd62279760200fcadf022f7000a40e198bebc3cfc5507913da7c9c06ae220cba6fe3910a6bca54'
            'SKIP'
            'fd707292e8e1ef32b520e7ae3ab80197b86b89e80c661385ca488eb256571ce6ec2b636c78ff786b6743cd73f1ddd37ab417507bf7829e0d6340262ba7f3fafe')
validpgpkeys=('017D74E27F5856963801781DF663943E08D8092A') # Lars-Dominik Braun <lars@6xq.net>

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../pianobar-ffmpeg4.patch # Fix build with ffmpeg 4
}

build() {
  make -C ${pkgname}-${pkgver}
}

package() {
  make -C ${pkgname}-${pkgver} DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm644 $pkgname-"${pkgver}"/COPYING "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
  install -Dm755 $pkgname-"${pkgver}"/contrib/headless_pianobar "${pkgdir}"/usr/bin/pianoctl
}
