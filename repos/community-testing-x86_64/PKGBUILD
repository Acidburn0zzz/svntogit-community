# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-workhorse
pkgver=8.28.0
pkgrel=1
pkgdesc="HTTP server to unload Git HTTP traffic from GitLab Rails app (Unicorn)"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-workhorse"
license=('MIT')
depends=('glibc' 'perl-image-exiftool')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-workhorse/repository/archive?sha=v${pkgver}"
        "gitlab-workhorse.service")
sha512sums=('7ef783e612226455d3b20b6c51f89ae502813e50c64de986ca15a82600bde940cc381d12ccdfe34cbc486a1d13bf9acf8812de0ef3dfc75ccd0054b429a7ed99'
            'f45ea8f0dedaefc547fdf855d36cdc56e75e90c45ad6c7c848ff225995e57258cb727df0c3065300d1300f86d48f5d4912f854ddf4e38feda460c785634a61de')

build() {
  cd "${pkgname}-v${pkgver}-"*

  make
}

package() {
  cd "${pkgname}-v${pkgver}-"*

  install -Dm755 "gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
  install -Dm755 "gitlab-zip-cat" "${pkgdir}/usr/bin/gitlab-zip-cat"
  install -Dm755 "gitlab-zip-metadata" "${pkgdir}/usr/bin/gitlab-zip-metadata"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-workhorse.service" "${pkgdir}/usr/lib/systemd/system/gitlab-workhorse.service"
}

# vim:set ts=2 sw=2 et:
