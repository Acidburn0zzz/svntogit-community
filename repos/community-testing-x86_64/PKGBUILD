# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: quininer <quininer@live.com>
pkgbase='alacritty'
pkgname=('alacritty' 'alacritty-terminfo')
pkgdesc="A cross-platform, GPU-accelerated terminal emulator"
_pkgver=0.2.2
pkgver=0.2.3
pkgrel=1
arch=('x86_64')
url="https://github.com/jwilm/alacritty"
license=('Apache')
makedepends=('rust' 'cargo' 'cmake' 'fontconfig' 'ncurses' 'desktop-file-utils')
source=("${pkgbase}-${_pkgver}.tar.gz::https://github.com/jwilm/${pkgbase}/archive/v${_pkgver}.tar.gz"
        "diff0.2.3.patch::https://github.com/jwilm/alacritty/compare/v0.2.2...f5e2f39979e2983399dd867cb6f844256fd14163.patch")
sha256sums=('028e67c3ea005c8222aabe2c95e6d819ce0aca220efe37120b89c01e9afdc7a1'
            '7ad42729a2e81a450a7ecbf96fdf6efdd74ab579789e5e18bd88e24668252115')

prepare(){
  cd $pkgbase-$_pkgver
  patch -Np1 -i $srcdir/diff0.2.3.patch
}

build(){
  cd $pkgbase-$_pkgver
  env CARGO_INCREMENTAL=0 cargo build --release
}

check(){
  cd $pkgbase-$_pkgver
  env CARGO_INCREMENTAL=0 cargo test --release
}

package_alacritty() {
  depends=('freetype2' 'fontconfig' 'xclip' 'libxi' 'libxcursor')
  
  cd $pkgbase-$_pkgver
  desktop-file-install -m 644 --dir "$pkgdir/usr/share/applications/" "alacritty.desktop"
  
  install -D -m755 "target/release/alacritty" "$pkgdir/usr/bin/alacritty"
  install -D -m644 "alacritty.man" "$pkgdir/usr/share/man/man1/alacritty.1"
  install -D -m644 "alacritty-completions.bash" "$pkgdir/usr/share/bash-completion/completions/alacritty"
  install -D -m644 "alacritty-completions.zsh" "$pkgdir/usr/share/zsh/site-functions/_alacritty"
  install -D -m644 "alacritty-completions.fish" "$pkgdir/usr/share/fish/completions/alacritty.fish"
}

package_alacritty-terminfo() {
  pkgdesc="Terminfo files for the alacritty terminal emulator"
  depends=('ncurses')
  
  cd $pkgbase-$_pkgver
  
  install -dm 755 "$pkgdir/usr/share/terminfo/a/"
  tic -o "$pkgdir/usr/share/terminfo" alacritty.info
}
