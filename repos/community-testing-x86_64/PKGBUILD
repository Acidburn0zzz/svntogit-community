# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=4.6.2
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('i686' 'x86_64')
url="http://kmymoney2.sourceforge.net/"
license=('GPL')
depends=('kdebase-workspace' 'shared-mime-info' 'aqbanking' 'libofx' 'libalkimia' 'boost-libs')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost')
replaces=('kmymoney2')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/kmymoney2/KMyMoney-KDE4/$pkgver/$pkgname-$pkgver.tar.bz2
qt4.8.patch::https://projects.kde.org/projects/extragear/office/kmymoney/repository/revisions/c736b6f08e77df72d58c5b836b7b04f6098737df/diff/libkdchart/src/KDChartBackgroundAttributes.cpp?format=diff)
sha256sums=('add8acba2f0d4c212ac1c7fbbdad2fd43a24caceed586b13d90dcd2a20a12b75'
            '7612b490400dbd4f80ff27bd328f8603a22279c81bb3004e53f48545dbd359ae')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Qt 4.8 fix - fixed upstream
  patch -Np1 -i ../qt4.8.patch

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make VERBOSE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}
