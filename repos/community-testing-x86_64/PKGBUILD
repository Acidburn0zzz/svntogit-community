# Contributor: Martin Wimpress <code@flexion.org>

pkgname=libmateweather
pkgver=1.18.0
pkgrel=1
pkgdesc="Provides access to weather information from the Internet."
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk3' 'libsoup')
makedepends=('intltool')
conflicts=('libmateweather-gtk3')
replaces=('libmateweather-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('b0f158847c02a22ca2fc802a855a681f5970ce9a')

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-python \
        --enable-locations-compression

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
