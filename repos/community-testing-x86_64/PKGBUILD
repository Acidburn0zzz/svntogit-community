# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.2.7
_helper_gitrev=3b52434b6d1cfe00c5bac1046d323813ce531a55
pkgrel=2
pkgdesc="Modern desktop environment from the Solus Project"
arch=('i686' 'x86_64')
url="https://solus-project.com/budgie"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
source=("https://github.com/solus-project/budgie-desktop/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev"
        "0001-Raven-Trigger-use-standard-icons.patch")
sha256sums=('abfd35666764fabc39960dd298834e52d5cd7d7f2f3e1b5de77cfe1fe9090bad'
            '3a4d7dd7c95ccba4e2916adf4a14769ffe54e8f86ed302d0268cd312b2a85c0e'
            'a1ef238cc9ae1763a15d59ae7bceb2f76f3c5966b7f99e347a55dd38e2e92538')

prepare() {
	cd $pkgname-$pkgver

	# Raven Trigger: use standard icons
	# https://github.com/solus-project/budgie-desktop/pull/637
	patch -Np1 -i ../0001-Raven-Trigger-use-standard-icons.patch

	# Fix theme for GTK+ 3.22
	sed -i 's/font:1;//' theme/3.20/theme.css

	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
