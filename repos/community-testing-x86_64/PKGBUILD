# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=synthv1
pkgver=0.8.5
pkgrel=1
pkgdesc="An old-school all-digital 4-oscillator subtractive polyphonic synthesizer with stereo fx"
arch=('x86_64')
url="https://synthv1.sourceforge.io/synthv1-index.html"
license=('GPL')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5d305aa6087b6b29d3e2ae285a0ec5cfc9debf758e2a81613dc1075630667ca3a39500e0637809fd250e51ab7954e787fa19b269f87d320c739c847fe434b3b2')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
