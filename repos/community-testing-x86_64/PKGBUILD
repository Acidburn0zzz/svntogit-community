# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.13.0
pkgrel=2
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://bitbucket.org/maproom/qmapshack/'
license=('GPL3')
depends=('hicolor-icon-theme' 'gdal' 'routino>=3.1' 'qt5-script'
         'qt5-webkit' 'proj' 'quazip' 'qt5-webengine' 'desktop-file-utils')
makedepends=('cmake' 'qt5-tools' 'patch')
source=(https://bitbucket.org/maproom/qmapshack/downloads/$pkgname-$pkgver.tar.gz
        FindPROJ4.patch
        FindQuaZip5.patch)
sha256sums=('6a658f9188cd96ec53bd051cf475256b4bacc4a76a7043c5f478a38cc27161b3'
            '1ffb847b098fedf01e347971da33c58b3fbf1a0f27205d48f6eb52206e228046'
            '23f32e58daa7c69072ea59465f0541e86dcff3085e4e73a948713a02ae421889')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "${srcdir}"/FindPROJ4.patch
  patch -Np1 -i "${srcdir}"/FindQuaZip5.patch
}

build() {
  cd "$pkgname-$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
