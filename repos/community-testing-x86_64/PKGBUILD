# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.5353
pkgrel=2
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang llvm python)
options=(!emptydirs)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-llvm11.patch
        git+https://github.com/intel/vc-intrinsics.git#commit=eabcd2022cf868a658b257b8ea6ad62acbbe7dc5
        git+https://github.com/KhronosGroup/SPIRV-LLVM-Translator.git#commit=d6dc999eee381158a26f048a333467c9ce7e77f2)
sha256sums=('8a7ebe4498b4777c55837ffbea75d2cff367fb31e1eeb44fbbe307fdb7cf26d6'
            '5bdfb2a71b388e1b24387e07aa7e1906e9dbdcb52e5e4d8a1a417f7360b6a0bb'
            'SKIP'
            'SKIP')

prepare() {
    cd ${pkgname}-igc-${pkgver}
    patch -Np1 -i ../${pkgname}-llvm11.patch
}

build() {
    cmake -B build -S ${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='11.0.0' \
        -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
        -DSPIRV_SRC="${srcdir}/SPIRV-LLVM-Translator" \
        -DINSTALL_GENX_IR=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
    mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
