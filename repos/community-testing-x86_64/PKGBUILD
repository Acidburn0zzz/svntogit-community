# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>
# Contributor: Francisco Magalhães <franmagneto gmail com>

pkgname=code
pkgdesc='The Open Source build of Visual Studio Code (vscode) editor'
pkgver=1.27.1
pkgrel=7
arch=('x86_64')
url='https://github.com/Microsoft/vscode'
license=('MIT')
depends=('electron' 'libsecret' 'libxkbfile')
makedepends=('git' 'gulp' 'npm' 'python2' 'yarn')
source=("$pkgname::git+https://github.com/Microsoft/vscode.git#tag=$pkgver"
        'code.js'
        'product_json.patch'
        'code-liveshare.patch')
sha512sums=('SKIP'
            '6f729b01e81d5cba31ee9e7e0275810410bcdbd3514b3956e9c19f4cbe849056fdc6267680cae6f9b28bf85b6b5b09130cf7bdf0db9ae651701efd3b480a22aa'
            'dcf52fa373a787cd94ce7ad216a269de3ca90b3f02cea1d635016d980e765a9cc040776ce4b4ef83721ab6f0d74efb7a3a9ca02fbdf3daa06653c766d36cde08'
            '6080b9d30b2c852a831d4fa98be94e42eb7d94dfd5813bbe28410d031191c20563e4288d6b0062c74b635a962c3eba3533fcebeff1f67b4a8005a167c2f7fbf2')

# Even though we don't officially support other archs, let's
# allow the user to use this PKGBUILD to compile the package
# for his architecture
case "$CARCH" in
  i686)
    _vscode_arch=ia32
    ;;
  x86_64)
    _vscode_arch=x64
    ;;
  armv7h)
    _vscode_arch=arm
    ;;
  *)
    # Needed for mksrcinfo
    _vscode_arch=DUMMY
    ;;
esac

prepare() {
  cd $pkgname

  # This patch no longer contains proprietary modifications.
  # See https://github.com/Microsoft/vscode/issues/31168 for details.
  patch -p1 < ../product_json.patch

  # Set the commit and build date
  local _commit=$(git rev-parse HEAD)
  local _datestamp=$(date -u -Is | sed 's/\+00:00/Z/')
  sed -e "s/@COMMIT@/$_commit/" -e "s/@DATE@/$_datestamp/" -i product.json

  # Prepare appdata and desktop file
  sed -i 's|/usr/share/@@NAME@@/@@NAME@@|@@NAME@@|g
          s|@@NAME_SHORT@@|Code - OSS|g
          s|@@NAME_LONG@@|Code|g
          s|@@NAME@@|code|g
          s|@@ICON@@|code|g
          s|@@LICENSE@@|MIT|g
          s|inode/directory;||' resources/linux/code.{appdata.xml,desktop}

  # See https://github.com/MicrosoftDocs/live-share/issues/262 for details
  patch -p1 < ../code-liveshare.patch

  # Build native modules for system electron
  local _target=$(</usr/lib/electron/version)
  sed -i "s/^target .*/target \"${_target//v/}\"/" .yarnrc
}

build() {
  cd $pkgname

  yarn install --arch=$_vscode_arch
  node --max_old_space_size=4096 /usr/bin/gulp vscode-linux-$_vscode_arch-min
}

package() {
  # Install resource files
  install -dm 755 "$pkgdir"/usr/lib/$pkgname
  cp -r --no-preserve=ownership --preserve=mode VSCode-linux-$_vscode_arch/resources/app/* "$pkgdir"/usr/lib/$pkgname/

  # Install binary
  install -Dm 755 code.js "$pkgdir"/usr/bin/code

  # Install appdata and desktop file
  install -Dm 644 code/resources/linux/code.appdata.xml "$pkgdir"/usr/share/metainfo/code.appdata.xml
  install -Dm 644 code/resources/linux/code.desktop "$pkgdir"/usr/share/applications/code.desktop
  install -Dm 644 VSCode-linux-$_vscode_arch/resources/app/resources/linux/code.png "$pkgdir"/usr/share/pixmaps/code.png

  # Install license files
  install -Dm 644 VSCode-linux-$_vscode_arch/resources/app/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm 644 VSCode-linux-$_vscode_arch/resources/app/ThirdPartyNotices.txt "$pkgdir"/usr/share/licenses/$pkgname/ThirdPartyNotices.txt
}

