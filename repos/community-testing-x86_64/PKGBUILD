# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Albert Graef <aggraef at gmail.com>
# Contributor: Bernardo Barros <bernardobarros at gmail dot com>
# Contributor: bjoern lindig (bjoern _dot_ lindig _at_ google.com)

pkgname=faust
pkgver=2.5.17
pkgrel=1
pkgdesc="A functional programming language for realtime audio signal processing."
arch=('x86_64')
url="https://faust.grame.fr/"
license=('GPL2')
depends=('bash' 'llvm-libs' 'libsndfile' 'libmicrohttpd' 'python2' 'ruby')
makedepends=('llvm' 'xxd')
optdepends=('clang: needed for sound2reader'
            'java-runtime=8: needed for faust2android')
provides=('faust')
conflicts=('faust-git')
options=('staticlibs')
source=("https://github.com/grame-cncm/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        'python2-fix.patch')
sha512sums=('84d7d8d9f6e7e351525870b4c2ba96a013d81e920ae4c2a514bb196c5f2207699c4a615fd19945e6564ad09a062787c03cb95ad75b9b3065012c5e2e8812555a'
            'aefee8c8bfe1115f8b2a576e6021bd0da3eeced9403878dc124fe7fbcb819d3910f1f119662ae09c52e60dff6ebfc76f0f63e07df3705e0849149fa540104482')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix scripts requiring python2
  patch -Np1 < "${srcdir}/python2-fix.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr world
}

package() {
  cd "${pkgname}-${pkgver}"
  make install PREFIX=/usr DESTDIR="$pkgdir"

  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 documentation/{,misc/}*.pdf

  # examples
  install -vd "${pkgdir}/usr/share/${pkgname}/examples"
  cp -vR "examples/"* "${pkgdir}/usr/share/${pkgname}/examples/"

  cd syntax-highlighting
  # atom
  install -vDm644 "atom/language-${pkgname}/package.json" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/package.json"
  install -vDm644 "atom/language-${pkgname}/grammars/${pkgname}.cson" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/grammars/${pkgname}.cson"
  install -vDm644 "atom/language-${pkgname}/settings/language-${pkgname}.cson" \
    "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/settings/language-${pkgname}.cson"
  install -t "${pkgdir}/usr/lib/atom/dot-atom/packages/language-${pkgname}/snippets/" \
    -vDm644 "atom/language-${pkgname}/snippets/"*
  install -vDm644 "atom/language-${pkgname}/process-palette.json.linux" \
    "${pkgdir}/usr/share/doc/${pkgname}/process-palette.json"
  # kate
  install -vDm644 "${pkgname}.xml" \
    "${pkgdir}/usr/share/apps/katepart/syntax/${pkgname}.xml"
  # gedit
  install -vDm644 "${pkgname}.lang" \
    "${pkgdir}/usr/share/gtksourceview-2.0/language-specs/${pkgname}.lang"
  install -vDm644 "${pkgname}.lang" \
    "${pkgdir}/usr/share/gtksourceview-3.0/language-specs/${pkgname}.lang"
  # highlight
  install -vDm644 dsp.lang "$pkgdir/usr/share/highlight/langDefs/dsp.lang"
  # nano
  install -vDm644 "${pkgname}.nanorc" "$pkgdir/usr/share/nano/${pkgname}.nanorc"
  # vim
  install -vDm644 "${pkgname}.vim" \
    "${pkgdir}/usr/share/vim/vimfiles/syntax/${pkgname}.vim"
  # emacs
  install -vDm644 "${pkgname}-mode.el" \
    "${pkgdir}/usr/share/emacs/site-lisp/${pkgname}-mode.el"
}

