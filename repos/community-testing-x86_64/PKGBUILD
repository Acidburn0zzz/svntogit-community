# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Alad Wenter <alad@archlinux.org>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Chris Salzberg <chris@dejimata.com>
# Contributor: Leonidas Spyropoulos <artafinde@gmail.com>
pkgname=neomutt
pkgver=20211022
pkgrel=2
pkgdesc='A version of mutt with added features'
url='https://neomutt.org/'
license=('GPL')
arch=('x86_64')
depends=('glibc' 'gpgme' 'lua53' 'notmuch-runtime' 'krb5' 'gnutls' 'sqlite'
         'libsasl' 'ncurses' 'libidn2' 'lmdb' 'gdbm' 'kyotocabinet'
         'lz4' 'zlib' 'zstd' 'db')
optdepends=('python: keybase.py'
            'perl: smime_keys'
            'ca-certificates: default CA certificates')
makedepends=('git' 'gnupg' 'libxslt' 'docbook-xsl' 'w3m')
_github='https://github.com/neomutt/neomutt'
source=("$pkgname-$pkgver.tar.gz::$_github/archive/$pkgver.tar.gz"
        "$pkgname-$pkgver.tar.gz.sig::$_github/releases/download/$pkgver/$pkgver.tar.gz.sig"
        "default-ca-certificates.patch"
        "neomutt-20211022_fix-fs-72525.patch::$_github/commit/a4a02a4afde1ec38dbbcfd6c4015634ca5d96138.patch")
sha256sums=('49aa5029665c6819e708276b9efa1ca71ec5afe870eb9f08e656107d234941e6'
            'SKIP'
            '571716b8979e9b43328416c3c56eff228b6c58355d7c080b8987ea89c6360776'
            '14bc9137451c4fa4248ce28a4c809689d66ae8d99c52ce22bf031c837956c70a')
validpgpkeys=('86C2397270DD7A561263CA4E5FAF0A6EE7371805') # Richard Russon (flatcap) <rich@flatcap.org>

prepare() {
    cd "$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/default-ca-certificates.patch"

    # Fix crash when composing emails with PGP enabled (FS#72525, https://github.com/neomutt/neomutt/pull/3090)
    patch -Np1 -i "$srcdir/neomutt-20211022_fix-fs-72525.patch"
}

build() {
    cd "$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib \
        --gpgme \
        --sqlite \
        --autocrypt \
        --lua \
        --notmuch \
        --gss \
        --gnutls \
        --sasl \
        --with-ui=ncurses \
        --with-idn2=/usr \
        --disable-idn \
        --idn2 \
        --bdb \
        --lmdb \
        --kyotocabinet \
        --gdbm \
        --lz4 \
        --zlib \
        --zstd
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
