# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: A.T.W.A. <arch.atwa@gmail.com>

pkgname=gxkb
pkgver=0.8.1
pkgrel=2
pkgdesc="X11 keyboard indicator and switcher"
arch=('x86_64')
url="https://zen-tools.github.io/gxkb"
license=('GPL2')
depends=('libxklavier' 'libwnck')
source=("https://github.com/zen-tools/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('aa8a69bbdce94e3cd17c7c12f5773c8e4bb7f2a5b706aacdce545e82063ff35e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./autogen.sh
  ./configure
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
