# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=jupyterlab
_pipname=jupyterlab
pkgver=3.0.13
pkgrel=1
pkgdesc="JupyterLab computational environment"
arch=(any)
url="https://github.com/jupyterlab/jupyterlab"
license=(custom)
makedepends=(python-setuptools)
depends=(python-jupyterlab_server python-jupyter_packaging jupyter-nbclassic nodejs)
optdepends=('npm: to install extensions' 'jupyterlab-widgets: ipywidgets support')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('6aefaf11251309ffdd66ef20025f5165aeec26d59a83c76e4d02cc8f593bc9dc')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|jupyter_packaging~=0.7.3|jupyter_packaging|' -i setup.py
}

build() {
  cd $pkgname-$pkgver
  python setup.py build 
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/etc "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
