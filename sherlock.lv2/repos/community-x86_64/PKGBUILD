# Maintainer: David Runge <dave@sleepmap.de>
pkgname=sherlock.lv2
pkgver=0.20.0
pkgrel=1
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd' 'libx11' 'serd' 'sratom')
makedepends=('flex' 'lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
sha512sums=('9085a8822a6d96945df3bc759d12cca79a5dd4b403e886a54fa809fed5f3f5c8d9c6f803a0501da1035def3dc3f266d50cf82ee693f4225c892933535700ac03')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
