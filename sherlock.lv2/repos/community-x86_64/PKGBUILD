# Maintainer: David Runge <dave@sleepmap.de>
pkgname=sherlock.lv2
pkgver=0.18.0
pkgrel=1
pkgdesc="An investigative plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/sherlock/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl' 'sratom')
makedepends=('flex' 'lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
sha512sums=('3f0fa46fb14568b727ca727d03b1b9bc2cf1e85f7deaaa1d607ec4ebe4f28ac3ef0a6cb6487b3f6fbd0cc3dfe99c315d456813ede2df4487f4147d06b9213496')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
