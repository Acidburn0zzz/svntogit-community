# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=kresus
pkgver=0.14.0
pkgrel=1
pkgdesc="Self-hosted personal finance manager"
arch=('x86_64')
url="https://kresus.org"
license=('MIT')
backup=('etc/webapps/kresus/config.ini')
depends=('nodejs' 'weboob-headless')
makedepends=('yarn' 'npm' 'libpng' 'python2')
optdepends=('python2-pdfminer: For IBAN extraction from PDF RIB')
source=("https://framagit.org/kresusapp/kresus/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2"
        'config.ini'
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('741a996db2b121e360c0dcf72633d56d010fca22dbf230a99a8be91be3a642bc'
            'e9ea978f2edb56651471fcfb7689318dbeba737e2bf3f9847536526ca698a9e3'
            '3d4b07c2c029be80597e84d9fbbf67c479c6231347b83cf429af4441ec5a491e'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'ba8ad7d9eb5d2b47fde5f6a3ab98596e5c679141b78d76d54b44830604b67632')

build() {
    cd ${pkgname}-${pkgver}
    npm install && npm run build:prod
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # Fix python2 name
    sed -i 's:#!/usr/bin/env python:#!/usr/bin/env python2:' "${pkgdir}"/usr/lib/node_modules/kresus/build/server/weboob/main.py
    # Fix npm crazyness
    chmod -R go-w "${pkgdir}"/usr
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
