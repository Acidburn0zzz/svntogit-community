# $Id$
# Maintainer: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=2.0.1
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'cinnamon-desktop' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'libcanberra-pulse' 'libnotify'
         'libsystemd' 'libwacom' 'pulseaudio' 'pulseaudio-alsa' 'upower' 'ibus' 'librsvg' 'libgweather')
makedepends=('intltool' 'xf86-input-wacom' 'libxslt' 'docbook-xsl' 'gnome-common')
options=('!emptydirs' '!libtool')
install=cinnamon-settings-daemon.install
url="https://github.com/linuxmint/cinnamon-settings-daemon"
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-settings-daemon/archive/${pkgver}.tar.gz")
sha256sums=('f03f5ea46bec11a6a0ed02b232dab8cb99a84fd8a9b52a76a454a7ba74dc1168')

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/lib/$pkgname --disable-static

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
