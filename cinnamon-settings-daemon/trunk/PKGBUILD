# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=5.0.1
pkgrel=1
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('81f618587576cc0e7a65e2658b6a4ff941ab0d87d0db2629427a79de7e82cc58ef1301568f2ed232aed0edf2e79def4a7ccb8f76becec1fe50b4b528d01c3c58')
b2sums=('d481dbde1cb1c39298296f8bde506921ce75ce0e70fe443b7c9101c62a4163b0457f4df5127fd676ebf535a0d83792d83a95b3b230a46e62b978ba65c6af2fee')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          -Dinstall_binfiles=false \
          ..

    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
