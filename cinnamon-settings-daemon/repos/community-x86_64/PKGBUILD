# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=4.8.4
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "https://github.com/linuxmint/cinnamon-settings-daemon/pull/321/commits/e7d59b47217073f1e411e07135b3fd3833b0a54a.patch")
sha512sums=('b92af7c7cc0f4bfa6043f8db011838de6b3e165f4cea926cf5c11774c0aca0bc3a74cdb4a3590209f7adc4539c26520ef5dbdf3e6678440092dc926f090e0275'
            '49df180676d03aef22017f634809e85938510ca0bad082675765165fff2f6fb881c8dfd7804e6395c79f5442aad7df7b05634a30df6a462bb6028b4a3480af4d')
b2sums=('6354ee6ece519b50cf59a35e26f7c34a19be0f923c2b22ac41addfcd5302d37ccb0ec319aaf1a2f6450c4e1a573d3696257fa87a7849ba0a8ca6b8202d2c9b07'
        '1d2ef73924f19df9d481d4211ebf43ac36a240842a5a5e117c6b76e01f210401bfe6ba3e51429190335a2075c85b8b4f4b8ac813cc7cf49ce0e9d595b7a0b4e6')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # FS#69309 -- version 4.8.4 has no changes other than deleting lots of
    # programs and replacing them with broken symlinks. Fix via
    # https://github.com/linuxmint/cinnamon-settings-daemon/pull/321
    # do not delete programs and replace them with broken symlinks to themselves
    patch -p1 -i ../e7d59b47217073f1e411e07135b3fd3833b0a54a.patch
}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir="/usr/lib/${pkgname}" \
          --buildtype=plain \
          -Dinstall_binfiles=false \
          ..

    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

     DESTDIR="${pkgdir}" samu install
}
