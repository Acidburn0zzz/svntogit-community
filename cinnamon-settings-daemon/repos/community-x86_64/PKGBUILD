# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira (faidoc) <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-settings-daemon
pkgver=4.4.0
pkgrel=2
pkgdesc="The Cinnamon Settings daemon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'colord' 'dbus-glib' 'libcanberra-pulse' 'libcups'
         'libgnomekbd' 'libgudev' 'libnotify' 'librsvg' 'libwacom' 'nss' 'polkit'
         'pulseaudio-alsa' 'upower')
optdepends=('cinnamon-translations: i18n')
makedepends=('autoconf-archive' 'intltool' 'python' 'xf86-input-wacom')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        '0001-dbus-sys-dir.patch')
sha256sums=('6797a0bb73ea2345580a43fe8c75c70f48634b82b0a3518f48cc603f084bf012'
            'a075aa599f1b0a4718723fcd5ee550da9e7a8d980efc31a8261240956fc8cdd5')
b2sums=('ef0666864a4735ff32d57a584004176c74d0f0ad4b077d3439e135c1d24c8d0606221a1b1693a0189972b462055478680b03917a6acfcdfdc80649f7ee0fc224'
        'ce13cf885dda6ed3839c4e33770144abb482b49dc991a71276a097695ed3184e1a1a4559d73e9f2ba464f9ccfe1bfbcf33e64fa3b128dfa0a4c5a69b93a604d1')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    patch -Np1 < ../0001-dbus-sys-dir.patch

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libexecdir="/usr/lib/${pkgname}" \
                --with-dbus-sys="/usr/share/dbus-1/system.d" \
                --enable-systemd \
                --enable-polkit

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="$pkgdir" install
}
