# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
pkgname=openvas-libraries
pkgver=7.0.3
pkgrel=1
pkgdesc='The OpenVAS libraries'
arch=('i686' 'x86_64')
url="http://www.openvas.org/"
license=('GPL')
depends=('gnutls' 'libpcap' 'gpgme' 'libssh' 'glib2')
makedepends=('cmake' 'doxygen')
options=('!makeflags')
# This URL needs to be manually updated on every pkgver upgrade.
# http://www.openvas.org/install-source.html
source=("http://wald.intevation.org/frs/download.php/1682/openvas-libraries-${pkgver}.tar.gz")
md5sums=('d3a494ebe19eb67aecf716c8f75fd013')

prepare() {
  cd openvas-libraries-$pkgver
  find . -name 'CMakeLists.txt' -exec sed -i 's/-Werror//' {} \;
}

build() {
  cd openvas-libraries-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DLIBDIR=/usr/lib -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make CFLAGS=-Wno-error CC=--disable-werror
}

package() {
  cd openvas-libraries-$pkgver
  make DESTDIR="$pkgdir/" install
}
