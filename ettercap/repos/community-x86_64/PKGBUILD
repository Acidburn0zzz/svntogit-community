# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=0.7.5.3
pkgrel=1
arch=('i686' 'x86_64')
url="http://ettercap.github.com/ettercap/"
license=('GPL')
makedepends=('ghostscript' 'cmake' 'libpcap' 'libltdl' 'libnet' 'gtk2')
options=('!libtool' '!makeflags' '!emptydirs')
source=("http://sourceforge.net/projects/ettercap/files/ettercap/0.7.5-Assimilation/$pkgname-$pkgver.tar.gz")

md5sums=('784b5cf2493246e95943179b21f70645'
         'f9281fe1735ceba59753bfe442715bb6')
build() {
  cd "${srcdir}"

  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-gtk

  cd ${pkgbase}-${pkgver}
  mkdir build
  cd build
  cmake -D ENABLE_GTK=OFF -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make

  cd "$srcdir"/${pkgbase}-${pkgver}-gtk
  mkdir build
  cd build
  cmake -D ENABLE_GTK=ON -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFIX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  depends=('openssl' 'libpcap' 'pcre' 'libltdl' 'libnet')
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap-gtk')

  cd "${srcdir}"/${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK frontend"
  depends=('libpcap' 'pcre' 'libltdl' 'libnet'  'openssl' 'gtk2')
  backup=('etc/ettercap/etter.conf')
  conflicts=('ettercap')

  cd "${srcdir}"/${pkgbase}-${pkgver}-gtk/build
#install -Dm755 src/ettercap "${pkgdir}"/usr/bin/ettercap-gtk
  make DESTDIR="${pkgdir}" install
}
