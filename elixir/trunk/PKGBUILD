# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Old maintainer: Gilbert Kennen <gilbert firewatcher org>

pkgname=elixir
pkgver=1.2.5
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elixir-lang/elixir/tarball/v${pkgver}")
md5sums=('f56d2f68ab89244144b496cfa7ee5c61')

build() {
  cd "${srcdir}/"elixir-lang*
  make
}

check() {
  cd "${srcdir}/"elixir-lang*
  make test_erlang
}

package() {
  cd "${srcdir}/"elixir-lang*
  mkdir -p "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}"
  make DESTDIR=$pkgdir PREFIX=/usr install
}
