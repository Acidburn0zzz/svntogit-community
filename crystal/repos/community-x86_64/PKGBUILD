# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=crystal
pkgver=0.17.2
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
depends=(gc libatomic_ops pcre libevent libffi)
# depend on previous llvm version until https://github.com/crystal-lang/crystal/issues/1614 is resolved
makedepends=(libxml2 llvm35)
checkdepends=(libyaml libxml2 gmp git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/crystal-lang/crystal/archive/$pkgver.tar.gz)
source_i686+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('b7a0b46fa8838665f834fda080d8f5861fde0211038d997031c073513e3ca15a')
sha256sums_i686=('0dbde9687ce1721a4f389c96923af9fa27636a4657077beb59eb871098f9b6fb')
sha256sums_x86_64=('fe4fbd8528b0ba6a81a3851bb25276f937cdcf1fca11edde88d68d803ded9b0d')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr
}

build() {
  cd $pkgname-$pkgver

  make release=1 \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal" \
       CRYSTAL_CACHE_DIR="/tmp/crystal"
  make doc
}

check() {
  cd $pkgname-$pkgver

  make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver" \
            CRYSTAL_CACHE_DIR="/tmp/crystal"
}

package() {
  cd $pkgname-$pkgver

  # /usr/bin/crystal                compiled executable
  # /usr/lib/crystal/               compiler src & core libs
  # /usr/share/doc/crystal/api      api docs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"

  install -dm755 "$pkgdir/usr/lib"
  cp -r src "$pkgdir/usr/lib/crystal"

  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -r doc "$pkgdir/usr/share/doc/crystal/api"
  cp -r samples "$pkgdir/usr/share/doc/crystal/"

  install -Dm644 etc/completion.bash "$pkgdir/usr/share/bash-completion/completions/crystal"
  install -Dm644 etc/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
