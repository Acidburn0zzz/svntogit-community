# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=crystal
pkgver=0.19.0
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
depends=(gc libatomic_ops pcre libevent libffi)
makedepends=(libxml2 llvm)
checkdepends=(libyaml libxml2 gmp inetutils git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/crystal-lang/crystal/archive/$pkgver.tar.gz)
source_i686+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('4d7d9770891bd8bd835251e2654316412a3f44074db0adeca28357c0993eb2d8')
sha256sums_i686=('6b58af00442c677f0adf635bd1c8de304914bda706c0e69f3e03f03e2a749892')
sha256sums_x86_64=('17bb8fa1c1771efead7d88b8571027675588ec3b35e4b453e7cdc952e53826cf')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr
}

build() {
  cd $pkgname-$pkgver

  make release=1 \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal" \
       CRYSTAL_CACHE_DIR="/tmp/crystal"
  make doc CRYSTAL_CACHE_DIR="/tmp/crystal"
}

check() {
  cd $pkgname-$pkgver

  make all_spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver" \
            CRYSTAL_CACHE_DIR="/tmp/crystal"
}

package() {
  cd $pkgname-$pkgver

  # /usr/bin/crystal                compiled executable
  # /usr/lib/crystal/               compiler src & core libs
  # /usr/share/doc/crystal/api      api docs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"

  install -dm755 "$pkgdir/usr/lib"
  cp -r src "$pkgdir/usr/lib/crystal"

  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -r doc "$pkgdir/usr/share/doc/crystal/api"
  cp -r samples "$pkgdir/usr/share/doc/crystal/"

  install -Dm644 etc/completion.bash "$pkgdir/usr/share/bash-completion/completions/crystal"
  install -Dm644 etc/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
