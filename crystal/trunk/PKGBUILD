# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=crystal
pkgver=0.21.1
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
depends=(gc libatomic_ops pcre libevent llvm-libs)
makedepends=(libxml2 llvm)
checkdepends=(libyaml libxml2 gmp inetutils git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/crystal-lang/crystal/archive/$pkgver.tar.gz
        remove_failing_socket_test.patch
        openssl11_1.patch::https://github.com/crystal-lang/crystal/commit/1658c312d8b26c559ce5410254f0c75182529abe.patch
        openssl11_2.patch::https://github.com/crystal-lang/crystal/commit/b67a118e7a438009e5d44d0701c21d030c92aa94.patch
        openssl11_3.patch::https://github.com/crystal-lang/crystal/commit/58b84812ed531acc528086a08ce843baa6188a7a.patch
        openssl11_4.patch::https://github.com/crystal-lang/crystal/commit/3119e5989ee0dbdb9b1eff45bf5a0e2fdf665115.patch
)
source_i686+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/crystal-lang/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('eb93de3b388641fd2a09a2d10ab9fe40a58148d888fc48e4583a86ee1ddb1085'
            'e258c64886b678cfc6bde6a7e1eb04cd79a4a0d4057b318404d3d5f495fa3a7d'
            '77e9b34c9e4ccb9c168344396cb249d8b91f9e0c5428c67918f40641654d0240'
            '7c6458aeeb663674ccf642a2223c2e4ba73b6a6f2c30d3c56810af34456e2273'
            'c7118f72e5179b3c2a95576966e17c520fab6023479a5e61e59ea250ee4f9273'
            'c0f5ee92aac8acbee85fc0d3124112262538f2dfe30019e80823b0e6ba78f52c')
sha256sums_i686=('8ab875309aa71d4501995e834850d40538fb66dd9890024ed812777f83085f48')
sha256sums_x86_64=('e8a7489aba888cc0deec61e9bda5dd27ab600edb102afc17fa11582b90960875')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr

  patch -p1 < ../remove_failing_socket_test.patch

  # https://github.com/crystal-lang/crystal/issues/4204
  patch -p1 < ../openssl11_1.patch
  patch -p1 < ../openssl11_2.patch
  patch -p1 < ../openssl11_3.patch
  patch -p1 < ../openssl11_4.patch
}

build() {
  cd $pkgname-$pkgver

  make release=1 \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="lib:/usr/lib/crystal" \
       CRYSTAL_CACHE_DIR="/tmp/crystal"
  make doc CRYSTAL_CACHE_DIR="/tmp/crystal"
}

check() {
  cd $pkgname-$pkgver

  make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver" \
            CRYSTAL_CACHE_DIR="/tmp/crystal" \
            PATH=".build:$PATH"
}

package() {
  cd $pkgname-$pkgver

  # /usr/bin/crystal                compiled executable
  # /usr/lib/crystal/               compiler src & core libs
  # /usr/share/doc/crystal/api      api docs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"

  install -dm755 "$pkgdir/usr/lib"
  cp -r src "$pkgdir/usr/lib/crystal"

  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -r doc "$pkgdir/usr/share/doc/crystal/api"
  cp -r samples "$pkgdir/usr/share/doc/crystal/"

  install -Dm644 etc/completion.bash "$pkgdir/usr/share/bash-completion/completions/crystal"
  install -Dm644 etc/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
