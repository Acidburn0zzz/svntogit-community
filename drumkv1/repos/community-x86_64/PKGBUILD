# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.1
pkgrel=2
pkgdesc="An old-school drum-kit sampler"
arch=('x86_64')
url="https://drumkv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vee-one')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'jack' 'liblo' 'qt5-base')
makedepends=('lv2' 'qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7a6a43d4da83461153455ca00dfeee7aa4699445975a8bf0a708700500cdd696d71b35dd3f34ce6b00375fb849bfdc4065f598f0684705bbfb16877cde422e69')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}

# vim:set ts=2 sw=2 et:
