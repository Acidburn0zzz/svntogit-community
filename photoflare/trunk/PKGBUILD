# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=photoflare
pkgver=1.6.2
pkgrel=1
pkgdesc='Quick, simple but powerful Qt-based image editor inspired by PhotoFiltre'
arch=(x86_64)
url='https://photoflare.io/'
license=(GPL3)
depends=(desktop-file-utils graphicsmagick hicolor-icon-theme qt5-base qt5-imageformats)
makedepends=(qt5-tools)
source=("$pkgname-$pkgver.tar.gz::https://github.com/PhotoFlare/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('686cc4109f9d068835502ff8129f37774efbcdf701357e11080c63f18b1a7570')

build() {
  cd $pkgname-$pkgver
  qmake Photoflare.pro PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  rm -r "$pkgdir/usr/share/pixmaps"
}
