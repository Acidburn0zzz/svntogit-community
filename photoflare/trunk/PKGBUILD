# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=photoflare
pkgver=1.6.4
pkgrel=2
pkgdesc='Quick, simple but powerful Qt-based image editor inspired by PhotoFiltre'
arch=(x86_64)
url='https://photoflare.io/'
license=(GPL3)
depends=(graphicsmagick hicolor-icon-theme qt5-base qt5-imageformats)
makedepends=(qt5-tools)
source=("$pkgname-$pkgver.tar.gz::https://github.com/PhotoFlare/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('b48fdd56feb68a1bc688fe10892658fdc29a200830774c500aed9b819d988b09')

prepare() {
  cd $pkgname-$pkgver
  # Fix metainfo file
  # https://github.com/PhotoFlare/photoflare/issues/263
  sed -i 's/io.photoflare.photoflare.desktop/photoflare.desktop/' io.photoflare.photoflare.appdata.xml
}

build() {
  cd $pkgname-$pkgver
  qmake Photoflare.pro PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  rm -r "$pkgdir/usr/share/pixmaps"
}
