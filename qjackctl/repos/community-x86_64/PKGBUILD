# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.9.0
pkgrel=4
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'qt5-tools' 'portaudio')
optdepends=('jack2-dbus: for JACK dbus support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5aa6f3e86b6a5c528daa5fdb71b5400d5882fea6569d585a86bb6b9fa66b62699472fd82999a6f4eb1a375c70e684a20214240c1fc3b44239c22d182e3ae1b1c')
b2sums=('3be89091b6efb34d2dffee92fdfda5dabadc8b6e9f9a248cc5cd60df423482dc65342e9758a98f34c5c01bfba74eac64039e3ec2de157df0e03f8bf09a7a14fc')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('libasound.so' 'libjack.so' 'libportaudio.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
