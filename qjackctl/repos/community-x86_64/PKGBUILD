# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.6.2
pkgrel=1
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'jack' 'qt5-tools' 'portaudio')
optdepends=('portaudio: Portaudio support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a05f514d749b4f2344c7f0d57a2a0dd1ac6a97f35d0227827e78d3bd7abfbddc3201e2fc7e23f878cd318e408f169a4006a150dc0e43101086e9ba32004e5520')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libasound.so' 'libjack.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
