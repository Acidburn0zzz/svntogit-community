# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-opentracing
pkgver=2.3.0
pkgrel=3
pkgdesc="OpenTracing API for Python"
url="https://github.com/opentracing/opentracing-python"
license=('Apache')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-gevent' 'python-mock' 'python-pytest' 'python-tornado')
source=("https://github.com/opentracing/opentracing-python/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-py39.patch::https://github.com/opentracing/opentracing-python/commit/4feff50d7c96.patch)
sha512sums=('59e9459be56018e54d6e33d42fd2c0e1eb6cd3eaffdaddd5371cae055bf2a710d9c2ce74f4ab77e24808d4b0237a88150c02d5583dfffc6a107a2fcd5479bac9'
            'bf33e5398ea33887a68f6a8005c92108b22df371c8c246790b9660dfc97e2f0998356832bee456c3f1fc8627318f30100877178e1324d6d6a7f62b936a1c27d7')

prepare() {
  cd opentracing-python-$pkgver
  # https://github.com/opentracing/opentracing-python/pull/135
  patch -Np1 -i ../$pkgname-py39.patch
}

build() {
  cd opentracing-python-$pkgver
  python setup.py build
}

check() {
  cd opentracing-python-$pkgver
  pytest tests
  python -m testbed
}

package() {
  cd opentracing-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
