# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-security
pkgver=5.0
pkgrel=1
pkgdesc="Zope Security Framework"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.security"
license=('ZPL')
depends=('python-setuptools' 'python-zope-component' 'python-zope-i18nmessageid'
         'python-zope-interface' 'python-zope-location' 'python-zope-proxy' 'python-zope-schema')
checkdepends=('python-btrees' 'python-nose' 'python-zope-configuration' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.security/archive/$pkgver.tar.gz")
sha512sums=('a620d8a1de3d891772144be58741051821d98cb274259e4abf56e9f7b12f7fd7c4b83b3410e380fa6662c7ef07684181ed48224c844ea7c32e637595f9c084b3')

build() {
  cd "$srcdir"/zope.security-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.security-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.security-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
