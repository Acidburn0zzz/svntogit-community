# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=1.8.5
pkgrel=1
pkgdesc='IPv6 Router Advertisement Daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc')
arch=('i686' 'x86_64')
backup=('etc/radvd.conf' 'etc/conf.d/radvd')
source=("http://www.litech.org/radvd/dist/$pkgname-$pkgver.tar.gz"
        "$pkgname.rc"
        "$pkgname.conf"
        "$pkgname.service")
sha1sums=('344c145bd29938b7eefb0210cf92bf18df3e191d'
          '75d7ae9e1a64aea59ceb9ebe130e72a40dbe23e4'
          '0c4cfe8f171d59a647062d2cbcf7a4b3b3af75d1'
          'd95dc4d650a086641d62bf42806669e0a407d1df')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-pidfile=/run/radvd.pid \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install

  # install default config
  install -D -m 644 radvd.conf.example "$pkgdir/etc/radvd.conf"

  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"

  # install initscripts & systemd files
  install -D -m 755 ../$pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 ../$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 ft=sh et:
