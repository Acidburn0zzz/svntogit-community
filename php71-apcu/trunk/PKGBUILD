# $Id$

pkgname=php71-apcu
pkgver=5.1.10
pkgrel=1
arch=('x86_64')
pkgdesc='A userland caching module for PHP'
url='https://pecl.php.net/package/APCu'
depends=('php71')
license=('PHP')
conflicts=("${pkgname/71/}")
provides=("${pkgname/71/}=${pkgver}")
source=("https://pecl.php.net/get/apcu-$pkgver.tgz"
        'apcu.ini')
backup=('etc/php/conf.d/apcu.ini')
sha256sums=('08b6c0879b105036d4e79fbd1fa00cac25b77512420cacdfcaae10ffb23b1b1c'
            'd15339e3faa07a3279d47c2c0ec53059f3d0555f6979c6ea56368d795215fc1c')

build() {
	cd $srcdir/apcu-$pkgver

	phpize
	./configure --prefix=/usr
	make
}

check() {
	cd $srcdir/apcu-$pkgver

	export REPORT_EXIT_STATUS=1
	export NO_INTERACTION=1
	export SKIP_ONLINE_TESTS=1
	export SKIP_SLOW_TESTS=1
	make test
}

package() {
	cd $srcdir/apcu-$pkgver

	make INSTALL_ROOT=$pkgdir install

	install -D -m644 $srcdir/apcu.ini $pkgdir/etc/php/conf.d/apcu.ini
	install -D -m644 apc.php $pkgdir/usr/share/webapps/php-apcu/apc.php
	install -D -m644 INSTALL $pkgdir/usr/share/doc/php-apcu/install.txt
}
