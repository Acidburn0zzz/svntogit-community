# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.6.2
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=('x86_64')
url="https://qsynth.sourceforge.net/"
license=('GPL')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('fluidsynth' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6e6f5dc64c39ab18ad1a9e791376e0dcf6eea4882986b1723a5a2f7c53309ce166d443a198c62b33ac31ed3fc17100c3a4f8e3b1e474b70afcec0ffa7f1ffc08')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
