# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=wkhtmltopdf
pkgver=0.12.3
pkgrel=2
pkgdesc="Command line tools to render HTML into PDF and various image formats"
arch=('i686' 'x86_64')
url="http://wkhtmltopdf.org/"
license=('LGPL3')
depends=('qt5-webkit' 'qt5-svg')
optdepends=('xorg-server: wkhtmltopdf needs X or Xvfb to operate')
source=($pkgname-$pkgver.tar.gz::https://github.com/wkhtmltopdf/wkhtmltopdf/archive/$pkgver.tar.gz)
sha256sums=('0d55ad5d0582a8a62f3f108c41be071c9762ee970bdc6ed47a22067350e9d2ed')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake-qt5 wkhtmltopdf.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/usr" install

  # Generate and install man page
  install -d "$pkgdir/usr/share/man/man1"
  LD_LIBRARY_PATH=bin ./bin/wkhtmltopdf --manpage \
    >"$pkgdir/usr/share/man/man1/wkhtmltopdf.1"
  LD_LIBRARY_PATH=bin ./bin/wkhtmltoimage --manpage \
    >"$pkgdir/usr/share/man/man1/wkhtmltoimage.1"
}

# vim:set ts=2 sw=2 et:
