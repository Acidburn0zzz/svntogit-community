# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=wkhtmltopdf
pkgver=0.9.9
pkgrel=3
pkgdesc="Simple shell utility to convert html to pdf using the webkit rendering engine, and qt"
arch=('i686' 'x86_64')
url="http://code.google.com/p/wkhtmltopdf/"
license=('GPL3')
depends=('qt4')
optdepends=('xorg-server: wkhtmltopdf needs X or Xvfb to operate')
source=(http://wkhtmltopdf.googlecode.com/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('e6311c0d398d50c757d43d34f1f63c4b33010441e97d07e92647542419ab1a8b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  qmake wkhtmltopdf.pro
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir/usr" install

  # Generate and install man page
  install -d "$pkgdir/usr/share/man/man1"
  ./wkhtmltopdf --manpage >"$pkgdir/usr/share/man/man1/wkhtmltopdf.1"
}

# vim:set ts=2 sw=2 et:
