# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-manual
pkgver=2.0.7.6
pkgrel=1
pkgdesc='Deepin User Manual'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-manual"
license=('GPL3')
depends=('deepin-qt5integration' 'qcef')
makedepends=('cmake' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-manual/archive/$pkgver.tar.gz")
sha256sums=('bc7fad2cc61bd6765a0795abdf5879e58dda7e1fa2da0e422e3ff6df742669f2')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
