# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-manual
pkgver=2.0.10
pkgrel=1
pkgdesc='Deepin User Manual'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-manual"
license=('GPL3')
depends=('deepin-qt5integration' 'qcef')
makedepends=('cmake' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-manual/archive/$pkgver.tar.gz")
sha512sums=('1871d39999f48c1ff86cac5e4001e59a375687ea2c15ef53b4400bd3b758c57525963a241ec962a1aaaff9fcc18cf025ebfd998df5b353038e91fdc3e7a7f5dd')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
