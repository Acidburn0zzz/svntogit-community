# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-manual
pkgver=2.0.18
pkgrel=1
pkgdesc='Deepin User Manual'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-manual"
license=('GPL3')
depends=('deepin-qt5integration' 'qcef')
makedepends=('cmake' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-manual/archive/$pkgver.tar.gz")
sha512sums=('88853a9d78f87db2c6a6f1a3d57f76267dfa82a57f068e2ee7c03a9b882e0f408a2c56f86b9c894fe902a368058199d979a0416f3081d5f3f6454fd2233a8494')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
}
