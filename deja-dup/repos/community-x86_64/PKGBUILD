# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=40.6
pkgrel=3
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/DejaDup"
license=('GPL')
depends=('duplicity' 'org.freedesktop.secrets' 'gnome-online-accounts' 'gvfs' 'python-gobject' 'python-pydrive')
makedepends=('appstream-glib' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'nautilus' 'vala' git)
optdepends=('nautilus: backup extension')
_commit=bf75273ea0c4812ee7adec5a482c397d8f123ee2  # tags/40.6
source=("git+https://gitlab.gnome.org/World/deja-dup.git#commit=$_commit")
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  git cherry-pick -n 05422eab47e444525427b4108c64a00721139712
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
