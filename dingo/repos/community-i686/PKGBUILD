# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=dingo
pkgver=0.13
pkgrel=1
pkgdesc="A DNS client in Go that supports Google DNS over HTTPS"
arch=('i686' 'x86_64')
url="https://github.com/pforemski/dingo"
license=('GPL3')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pforemski/dingo/archive/$pkgver.tar.gz"
        dingo.service)
sha512sums=('24748a726aea598f6f8785bfe0bc349608e7106860fa73f4e3da12936035c249e8bdfee0e2d162d876beecf60b5cdfbce6cd0f09b040ce7b1db2d901bc0fd350'
            '98ced7a22f73af141d6f2a5e09d62c93f1ba18b9c334eae86a1ea832e5f4ae0acbeb82d20b80c82f266c5e7a1e5a78f7241280a2b1a630a2fc5aba6af756973a')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  go get github.com/miekg/dns github.com/patrickmn/go-cache golang.org/x/net/http2
}

build() {
  cd dingo-$pkgver
  go build -o dingo # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd dingo-$pkgver
  install -Dm755 dingo "$pkgdir"/usr/bin/dingo
  install -Dm644 ../dingo.service "$pkgdir"/usr/lib/systemd/system/dingo.service
}
