# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.59
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha512sums=('96a5116b5c606d2ca7b022eb28291f40d04d06531bf398d4bd43ee3916e726df5d3271f5c158d509f4bb25b4fe044e8df8eb860d2911bed6f6927c4e92c9d310')

prepare() {
  # They are moving files around without merging patches. Wow.
  cd deepin-image-viewer-$pkgver/viewer
  sed -i '/#include <QDebug>/a #include <QPainterPath>' frame/toptoolbar.cpp module/view/contents/ttbcontent.cpp widgets/popupmenustyle.cpp widgets/thumbnaillistview.cpp widgets/thumbnaildelegate.cpp
  sed -i '/#include <QtDebug>/a #include <QPainterPath>' module/view/contents/imageinfowidget.cpp

  # Our build of freeimage disabled support for these formats
  #sed -i '/FIF_FAXG3/d' utils/unionimage.cpp
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
