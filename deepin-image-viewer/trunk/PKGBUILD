# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.6
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz"
        deepin-image-viewer-qt5.15.patch)
sha512sums=('f95d5742e5f6a2407d80179f121d6fa7278ed253328b77dbd8024066791d930c807bb9830fd63435408c5200e5932994aafcc17a980f94ee8fd7b08ba9b42cea'
            'e802fa93972bf3d32c3672603e41bc662866aaf7e458b0cd7eeef786e22348c57161d3292754df5357beae6bb3b475bd45dfc6912c969200e4bf079c00b77763')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../deepin-image-viewer-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
