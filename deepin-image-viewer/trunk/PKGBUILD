# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.15
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha512sums=('77a93ca932f1e0112417a5c883513f5c2f5c9f973799d2b8d7027462a8182ced516702352f2be5a16a7c8d35ad69f6f20db710e57129e8f30f9d4d8bbb5b6df0')

prepare() {
  # They are moving files around without merging patches. Wow.
  cd deepin-image-viewer-$pkgver/viewer
  sed -i '/#include <QDebug>/a #include <QPainterPath>' module/view/contents/ttbcontent.cpp widgets/thumbnaillistview.cpp widgets/popupmenustyle.cpp widgets/thumbnaildelegate.cpp
  sed -i '/#include <QtDebug>/a #include <QPainterPath>' module/view/contents/imageinfowidget.cpp
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
