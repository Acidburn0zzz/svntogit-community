# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.8
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha512sums=('29c83b0832230da6a5d5ea32cb36afb504a9f91477ce5fb93ea1f00f37d07358cbc378515d676b651b3b20415ced593f389f13ac5daa0a13a5a7db20c11b553e')

prepare() {
  # They are moving files around without merging patches. Wow.
  cd deepin-image-viewer-$pkgver/src/viewer
  sed -i '/#include <QDebug>/a #include <QPainterPath>' module/view/contents/ttbcontent.cpp widgets/thumbnaillistview.cpp widgets/popupmenustyle.cpp widgets/thumbnaildelegate.cpp
  sed -i '/#include <QtDebug>/a #include <QPainterPath>' module/view/contents/imageinfowidget.cpp
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
