# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.32
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha512sums=('b40247dd6af44d4d6c18a0a1ed369c5aed08b7802130c6aa9ce15b8e66393119a41731914c3dc3fd3ba23e900a83a0439173c61eae2744a670b17d8f7db571f9')

prepare() {
  # They are moving files around without merging patches. Wow.
  cd deepin-image-viewer-$pkgver/viewer
  sed -i '/#include <QDebug>/a #include <QPainterPath>' module/view/contents/ttbcontent.cpp widgets/popupmenustyle.cpp
  sed -i '/#include <QtDebug>/a #include <QPainterPath>' module/view/contents/imageinfowidget.cpp

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' utils/unionimage.cpp
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
