# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=5.6.3.13
pkgrel=1
pkgdesc='An image viewing tool with fashion interface and smooth performance'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'libexif' 'libraw' 'qt5-svg' 'udisks2-qt5')
makedepends=('qt5-tools')
optdepends=('deepin-manual: for help menual'
            'deepin-shortcut-viewer: for shortcut display')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha512sums=('c048e7248331de9574363f37adf6f20ff21f3cb629d5b110d88c024f2db8556d87e212b25493a53b4419f79d61c03181f73d8ea556f0304ba8416c25badd9e2a')

prepare() {
  # They are moving files around without merging patches. Wow.
  cd deepin-image-viewer-$pkgver/viewer
  sed -i '/#include <QDebug>/a #include <QPainterPath>' module/view/contents/ttbcontent.cpp widgets/thumbnaillistview.cpp widgets/popupmenustyle.cpp widgets/thumbnaildelegate.cpp
  sed -i '/#include <QtDebug>/a #include <QPainterPath>' module/view/contents/imageinfowidget.cpp
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install -j1
}
