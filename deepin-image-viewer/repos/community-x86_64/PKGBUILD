# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=1.0.3
pkgrel=1
pkgdesc='Deepin Image Viewer'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-tool-kit' 'libexif' 'qt5-svg')
makedepends=('git' 'qt5-tools')
groups=('deepin')
source=("git+https://cr.deepin.io/deepin-image-viewer.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  sed -i '/vendor\/prebuild/d' deepin-image-viewer/deepin-image-viewer.pro

  cat > deepin-image-viewer/vendor/vendor.pri << EOF
QMAKE_CXXFLAGS += \$\$system(pkg-config --cflags dtkwidget)
LIBS +=  \$\$system(pkg-config --libs dtkwidget)
QMAKE_CXXFLAGS += \$\$system(pkg-config --cflags dtkutil)
LIBS +=  \$\$system(pkg-config --libs dtkutil)
QMAKE_CXXFLAGS += \$\$system(pkg-config --cflags dtkbase)
LIBS += \$\$system(pkg-config --libs dtkbase)
EOF
}

build() {
  cd deepin-image-viewer
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd deepin-image-viewer
  make INSTALL_ROOT="$pkgdir" install
}
