# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Felix Rohrbach <fxrh@gmx.de>

pkgname=eric5
pkgver=5.1.0
pkgrel=2
pkgdesc="A full-featured Python IDE that is written in PyQt4 using the QScintilla2 editor widget"
arch=('any')
url="http://eric-ide.python-projects.org/index.html"
license=('GPL3')
depends=('python-qscintilla' 'python-qt' 'python>=3.2')
source=(http://downloads.sourceforge.net/eric-ide/${pkgname}/stable/${pkgver}/${pkgname}-${pkgver}.tar.gz)
md5sums=('60eb0285ddf222411dc56c1ea032d2fe')

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

   	_pybasever=3.2

	python install.py -c -b /usr/bin -d /usr/lib/python${_pybasever}/site-packages -i ${pkgdir}

	# Fix icon name so it doesn't conflict with eric
	mv ${pkgdir}/usr/share/pixmaps/eric.png \
		${pkgdir}/usr/share/pixmaps/eric5.png

	# Fix .desktop file to point to new icon
	sed "s/icon=eric/icon=eric5/" -i \
		${pkgdir}/usr/share/applications/eric5.desktop	

	# Remove files conflicting with eric4
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-2.10.7.api
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-2.11.2.api
	rm ${pkgdir}/usr/share/qt/qsci/api/python/zope-3.3.1.api
	rm ${pkgdir}/usr/share/qt/qsci/api/ruby/Ruby-1.8.7.api
}
