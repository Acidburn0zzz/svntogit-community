# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=slirp4netns
pkgdesc='User-mode networking for unprivileged network namespaces'
pkgver=0.4.1
pkgrel=2
arch=(x86_64)
url="https://github.com/rootless-containers/$pkgname"
license=(GPL2)
depends=(glibc glib2 libcap libseccomp)
source=($pkgname-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz
        pr153.patch)
sha256sums=('75d2a7411cc2b3e341d8530228750bb1db06077b349d10fbdddbb582c27f8cfc'
            '7ce56f88cdd941d786117c388a31669018b6c37fd27f3afe97f2c4369cd92fac')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i "$srcdir/pr153.patch"
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
