# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Sven Kauber, <celeon@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=encfs
pkgver=1.9
pkgrel=1
pkgdesc='Encrypted filesystem in user-space'
arch=('i686' 'x86_64')
url='https://vgough.github.io/encfs/'
license=('LGPL')
depends=('openssl' 'fuse' 'tinyxml2')
makedepends=('cmake')
source=($pkgname-$pkgver.tar.gz::https://github.com/vgough/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('30d2db1555ec359082046748d278018b8a246dc49c0442291c5671da0486f4bf')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
      -DBUILD_SHARED_LIBS=ON \
      -DUSE_INTERNAL_TINYXML=OFF \
      -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${srcdir}"/build

  make test
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
