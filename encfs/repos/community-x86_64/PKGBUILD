# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Sven Kauber, <celeon@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=encfs
pkgver=1.9.1
pkgrel=1
pkgdesc='Encrypted filesystem in user-space'
arch=('i686' 'x86_64')
url='https://vgough.github.io/encfs/'
license=('LGPL')
depends=('openssl' 'fuse' 'tinyxml2')
makedepends=('cmake')
source=(https://github.com/vgough/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('67203aeff7a06ce7be83df4948db296be89a00cffe1108a0a41c96d7481106a4')

build() {
  cd "${srcdir}"

  mkdir build
  cd build

  cmake ../$pkgname-$pkgver \
      -DUSE_INTERNAL_TINYXML=OFF \
      -DINSTALL_LIBENCFS=ON \
      -DBUILD_SHARED_LIBS=ON \
      -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${srcdir}"/build

  make test
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install
}
