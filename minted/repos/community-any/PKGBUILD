# $Id$
# Maintainer: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Markus Härer <markus.haerer@gmx.net>
# Contributor: robotanarchy
# Contributor: Philipp Trommler <aur@philipp-trommler.me>

pkgname=minted
pkgver=2.4
pkgrel=2
pkgdesc='Syntax highlighted source code for LaTeX'
url='https://github.com/gpoore/minted'
arch=('any')
license=('LPPL')
depends=('texlive-latexextra' 'pygmentize' 'fvextra')
source=("$pkgname-$pkgver.tgz::https://github.com/gpoore/minted/archive/v${pkgver}.tar.gz"
        'minted.install')
install=minted.install
sha256sums=('fc991510f400d2cac284347768eb5a938a659aff9865d211c74f206ef5758234'
            '4e54230bc50a4efaebd03e0d59518dcb5e0695e77af97ce0eb5468bfa4ff8719')

build() {
  cd "$pkgname-$pkgver/source"

  yes | tex "$pkgname.ins"
}

package() {
  cd "$pkgname-$pkgver/source"

  install -Dm644 "$pkgname.sty" "$pkgdir/usr/share/texmf/tex/latex/$pkgname/$pkgname.sty"
  install -Dm644 "$pkgname.pdf" "$pkgdir/usr/share/doc/$pkgname/$pkgname.pdf"
}

# vim:set ts=2 sw=2 et:
