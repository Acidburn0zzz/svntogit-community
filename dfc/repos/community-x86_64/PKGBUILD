# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Rolinh <robinDOThahlingATgw-computingDOTnet>

pkgname=dfc
pkgver=3.1.0
pkgrel=1
pkgdesc="Display file system space usage using graphs and colors"
arch=('x86_64' 'i686')
url="http://projects.gw-computing.net/projects/dfc"
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'gettext')
conflicts=('dfc-git')
source=("https://github.com/Rolinh/dfc/archive/v${pkgver}.tar.gz")
install='dfc.install'
sha512sums=('704ced1e9120635ddcfb59d09ddc47464dac212fdeda2e27d499f0f8c8b97471b60f351783cc59736d9a8d37a03a85bb4f09fa3a4f05d9ee088e0395cdb0edec')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DPREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
