# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=clipper
pkgver=6.4.2
pkgrel=1
url="https://sourceforge.net/projects/polyclipping/"
arch=('x86_64')
license=('custom')
pkgdesc='Polygon and line clipping and offsetting library'
depends=(gcc-libs)
makedepends=(cmake)
source=(https://sourceforge.net/projects/polyclipping/files/clipper_ver$pkgver.zip)
sha512sums=('ffc88818c44a38aa278d5010db6cfd505796f39664919f1e48c7fa9267563f62135868993e88f7246dcd688241d1172878e4a008a390648acb99738452e3e5dd')

prepare() {
  mkdir cpp/build
}

build() {
  cd cpp/build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
  ..

  make 
}

package() {
  install -Dm644 License.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd cpp/build
  make DESTDIR="${pkgdir}" install
}
