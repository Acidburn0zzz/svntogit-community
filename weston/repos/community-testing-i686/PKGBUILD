# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Joel Teichroeb <joel@teichroeb.net>

pkgname=weston
pkgver=1.2.0
pkgrel=1
pkgdesc='Reference implementation of a Wayland compositor'
arch=('i686' 'x86_64')
url='http://wayland.freedesktop.org'
license=('MIT')
options=(!libtool)
depends=('libxkbcommon' 'libunwind' 'poppler-glib' 'mtdev' 'libxcursor' 'glu' 'pango' 'colord')
source=("http://wayland.freedesktop.org/releases/$pkgname-$pkgver.tar.xz")
sha1sums=('23201d31e71768e5ec3de0d10fa6de988dd273c8')

build() {
	cd $pkgname-$pkgver
	./configure \
		--prefix=/usr \
		--libexecdir=/usr/lib/weston
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	# license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	# embed more clients
	for _c in calibrator clickdot cliptest dnd editor eventdemo flower fullscreen gears \
         image resizor simple-egl simple-shm simple-touch smoke transformed view; do
		install -Dm755 "clients/$_c" "$pkgdir/usr/bin/weston-$_c"
	done
	for _c in desktop-shell info keyboard screensaver screenshooter simple-im tablet-shell \
	 terminal; do
		install -Dm755 "clients/weston-$_c" "$pkgdir/usr/bin/weston-$_c"
	done
}
