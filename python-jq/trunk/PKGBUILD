# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Mubashshir <ahmubashshir@gmail.com>

_name=jq.py
pkgname=python-jq
pkgver=1.1.3
pkgrel=3
pkgdesc='Python bindings for jq'
arch=('x86_64')
url=https://github.com/mwilliamson/jq.py
license=('BSD')
depends=('jq' 'python')
makedepends=('cython' 'python-build' 'python-install' 'python-setuptools'
             'python-wheel')
checkdepends=('python-nose')
source=("$url/archive/$pkgver/$_name-$pkgver.tar.gz"
        'jq-py-setup.patch')
sha256sums=('b0f4de47d6ae4fb221b539463eca777d723f2e7b290af6e132d51d7ef89c824c'
            '81abf958b5b7987a0e90a217ed62e1f62a53d19bc3354f8cdf15cd863bcc72d5')
b2sums=('31fe4744e18f01c96d0a6df614ad5122c7fbd07e00f666c4b1d63b25e3b075c2ada533784d73f01fea6eeaaaabf907ef8b8d4eb9af5dd1c9066e4fadfa9f1b35'
        '3424a91e0e6330e8d25e167b862554e466f469cdeb5672d4d4e5ff4d4475bfe9c7a575f3c93cd03687e10cc6afac2bb94294c0fef50feec03eaa55684e2907bc')

# Devendor
prepare() {
  cd $_name-$pkgver
  sed -i '/certifi\|requests/d' pyproject.toml
  patch --forward --strip=1 --input=../jq-py-setup.patch
}

build() {
  cd $_name-$pkgver
  cython jq.pyx
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python -m install --optimize=1 --destdir=temp dist/*.whl
  PYTHONPATH="$PWD/temp/$site_packages" nosetests tests
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl

  # Symlink license file
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s $site_packages/jq-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
