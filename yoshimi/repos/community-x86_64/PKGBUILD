# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.7.1
pkgrel=2
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'fftw' 'fltk' 'gcc-libs' 'glibc'
'libmxml.so' 'zlib')
makedepends=('cmake' 'libasound.so' 'libjack.so' 'libreadline.so'
'libncursesw.so' 'lv2')
# checkdepends=('lv2lint')
optdepends=('libasound.so: for standalone application'
            'libjack.so: for standalone application'
            'libreadline.so: for standalone application'
            'libncursesw.so: for standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-1.7.1-lv2-1.8.0.patch::https://github.com/Yoshimi/yoshimi/commit/4bb8bfe9f9f40dd53f9a3098f8f87ad993147935.patch")
sha512sums=('d422c08caa6bf20665442b2d88fc4ccc41b881d719d5150d1cd3d2617f2216b18c112107c5723cb1077da0ba2278a3eb2db0a8dda4ce3c6315e3875cbfaf421c'
            '206e0302d45dd36102adda295f533777913c76e7e53c7151ac796185590ebbd010a23f1b97d5a23d09b59d4ff207eda46234f3bd3e348992267b6e9e1234e1d3')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix compatibility with lv2 >= 1.18.0
  patch -Np1 -i "../${pkgname}-1.7.1-lv2-1.8.0.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

# lv2 plugin requires banks to be in some absolute location to be tested
# check() {
#   cd "${pkgname}-${pkgver}"
#   cp -v build/LV2_Plugin/${pkgname}_lv2.so src/LV2_Plugin/
#   LV2_PATH="${PWD}/src/" lv2lint "http://yoshimi.sourceforge.net/lv2_plugin"
# }

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
