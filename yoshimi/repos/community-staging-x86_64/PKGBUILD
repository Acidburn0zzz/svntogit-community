# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.5.10
pkgrel=2
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL')
groups=('pro-audio')
depends=('cairo' 'fftw' 'fltk' 'jack' 'mxml')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('8c15631ea41a32dece62fffd329866204688439e7dccaf442af3cab7eeafcceabee923bbea76c87f892a69bbba4b60ec2a4920fa354b504a37248ef0dbf316de')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake . \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}

# vim:set ts=2 sw=2 et:
