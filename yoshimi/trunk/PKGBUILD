# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.6.0.1
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'fltk' 'gcc-libs' 'glibc' 'jack' 'mxml' 'ncurses' 'zlib')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('29b9cd0b624e7a6bcf3dcdcbd63b004270bea8240313fb8cf9c0264ffaf6b4f657d21033df26b9bb593208ca5d5a6633ba0af59c0a8598717ff135f2b809d1c9')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake . \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}
# vim:set ts=2 sw=2 et:
