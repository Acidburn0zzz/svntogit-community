# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.1.2
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(pro-audio)
depends=(cairo gcc-libs glibc zlib)
makedepends=(alsa-lib cmake fftw fltk jack lv2 mxml ncurses readline)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
  "${pkgname}-2.1.2-cmake.patch::https://github.com/Yoshimi/yoshimi/pull/155/commits/63c3f1d8c2266b3c1f1769ab9475203d252d6604.patch"
)
sha512sums=('3c26d79181c03a0771a83e7c9d8e7c5ab7506a9a3ce26ffb4d536d5b2e8272cb7d6880cc048639447572b274e9ce0c31f09e9fc23a44db755bbb2d9a836eaa0b'
            '2c62ce1374091b88801ad667445e628d52f256bb61cfdd4b635c82f5c9f45406128bfb7fa1e2efc0bb3401671c58f9d45ca2fe7a4ecd4240eb177e70504cd02e')
b2sums=('efb769227ecc955098e4ad69fd86fc3e3c83ca3552db2d0df5ee53c59f3accbfe369ebf25f8a92681f506ddb2ba96c9058d1f2775d8396221a22af1e4f007213'
        '2e320bf53d14432c1a6a57a6a02fb829459b8729219ccb88d0074a03ae4936ac9d8a1b5bfacc604392708b65c7cb00a40584198d3f47a0504ff96fc9a21f2af2')

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix missing link against threads library
  # https://github.com/Yoshimi/yoshimi/issues/154
  patch -Np1 -i ../"${pkgname}-2.1.2-cmake.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=(libasound.so libfftw3f.so libfltk.so libfltk_images.so libjack.so
  libmxml.so libncursesw.so libreadline.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
