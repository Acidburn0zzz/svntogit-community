# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.0
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'zlib')
makedepends=('alsa-lib' 'cmake' 'fftw' 'fltk' 'jack' 'lv2' 'mxml' 'ncurses'
'readline')
checkdepends=('kxstudio-lv2-extensions' 'lv2lint')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'readline: for standalone application'
            'ncurses: for standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('6601d1f6248649fa8d66f86b787cf01a97fe34e98b5b0463eab829c4f1895a50c8113cae234970f243c8eaefcd1082bc7ff1d666a90550d6a195ebef2f378448')
b2sums=('56647275166aebb82783157ace163b3215ac4752d3813b4c0a21afa7c034a8cb9bd4adbcc2c53c057a1408f9e7b457bc1eadf7e3be9e67c92536f95b4cb0e1c7')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=('libasound.so' 'libfftw3f.so' 'libfltk.so' 'libfltk_images.so'
  'libjack.so' 'libmxml.so' 'libncursesw.so' 'libreadline.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
