# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.6.0.3
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'fltk' 'gcc-libs' 'glibc' 'libjack.so'
'libncursesw.so' 'libreadline.so' 'mxml' 'zlib')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('cd4d6a996b19682642c2b508551eaa3d7678b4ea8d1cf67d55e8e398119b0514a79b4b3f41ff140c90a019dcf0d779218b863be745d08113a507cf62be5cb529')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build\
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        src
  make -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="${pkgdir}" install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
