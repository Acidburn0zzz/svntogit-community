# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filter-rspamd
pkgname=opensmtpd-${_pkg}
pkgver=0.1.4
pkgrel=1
pkgdesc="OpenSMTPD filter integration for Rspamd"
arch=(x86_64)
url=https://github.com/poolpOrg/filter-rspamd
license=(ISC)
depends=(glibc rspamd)
makedepends=(go-pie)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('7a4932f611696b0e345b4f7e36515a9d66eb4dc148da2167c24f9cbd18234983')

build() {
  cd ${_pkg}-${pkgver}
  go build -v \
    -trimpath \
    -ldflags "-extldflags $LDFLAGS" \
    -o ${_pkg}
}

package() {
  cd ${_pkg}-$pkgver
  install -D filter-rspamd -t "${pkgdir}"/usr/lib/smtpd/opensmtpd
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
