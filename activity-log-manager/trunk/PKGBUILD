# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ner0

pkgname=activity-log-manager
pkgver=0.9.4
pkgrel=2
pkgdesc='Graphical user interface which lets you easily control what gets logged by Zeitgeist'
arch=('i686' 'x86_64')
url="https://launchpad.net/activity-log-manager"
license=('GPL2')
depends=('gtk3' 'libgee' 'libzeitgeist' 'zeitgeist' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala' 'intltool')
install=$pkgname.install
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20121021.tar.gz)
md5sums=('cf85aecdaa6c0188a53d659709e1f8cd'
         '6167df74ce44659aa94a89f31ff4d351')

build() {
  cd $pkgname-$pkgver

  # Fix docdir location
  sed -i 's/almdocdir =.*/almdocdir = ${docdir}/' Makefile.{am,in}

  # Update language files
  echo 'af ar ast be bg bn bs ca ca@valencia cs da de el eo es et eu fi fr gd gl he hi hr hu id it ja km ko ku lt lv mi ms my nb nl oc pl pt ro ru se si sk sl sr sv ta te ug uk uz vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # We don't have Whoopsie on Arch Linux
  rm -r "$pkgdir/etc" "$pkgdir"/usr/share/{applications/activity-log-manager-ccpanel.desktop,dbus-1,gnome-control-center,polkit-1}
}
