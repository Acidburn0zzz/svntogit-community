# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=camlp5-transitional
pkgver=5.15
pkgrel=1
pkgdesc='Camlp5 is a preprocessor-pretty-printer of OCaml.'
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
conflicts=('camlp5')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/camlp5-${pkgver}.tgz")
md5sums=('67ccbf37ffe33dec137ee71ca6189ea2')
options=('!makeflags')

build() {
  cd camlp5-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -transitional

  make world.opt
}

package() {
  cd camlp5-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
