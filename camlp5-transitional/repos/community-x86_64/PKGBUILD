# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>
# Contributor: Massimiliano Brocchini <brocchini@netseven.it>

pkgname=camlp5-transitional
pkgver=6.02.2
pkgrel=1
pkgdesc='Camlp5 is a preprocessor-pretty-printer of OCaml.'
arch=('i686' 'x86_64')
url='http://pauillac.inria.fr/~ddr/camlp5/'
license=('BSD')
depends=('sh')
makedepends=('ocaml')
conflicts=('camlp5')
options=('!makeflags')
source=("http://pauillac.inria.fr/~ddr/camlp5/distrib/src/camlp5-${pkgver}.tgz"
        'http://pauillac.inria.fr/~ddr/camlp5/distrib/src/patch-6.02.2-1'
        'http://pauillac.inria.fr/~ddr/camlp5/distrib/src/patch-6.02.2-2'
        'http://pauillac.inria.fr/~ddr/camlp5/distrib/src/patch-6.02.2-3')
md5sums=('b495bf26355451186c6725ee01add0da'
         '3acab547f3c0acb3c09d1d707e6c5ee3'
         '2e911c01e7da967bf68deee13e9712c8'
         '6fa59ec7c684919b6c8e2eb2bcabcf93')

build() {
  cd camlp5-${pkgver}

  patch -Np0 -i ${srcdir}/patch-6.02.2-1
  patch -Np0 -i ${srcdir}/patch-6.02.2-2
  patch -Np0 -i ${srcdir}/patch-6.02.2-3

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -transitional

  make world.opt
}

package() {
  cd camlp5-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
