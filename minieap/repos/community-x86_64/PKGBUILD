# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=minieap
pkgver=0.92.1
pkgrel=1
pkgdesc="Extensible 802.1x client with Ruijie v3/v4 support"
arch=('x86_64')
url="https://github.com/updateing/minieap"
license=('GPL')
depends=('glibc' 'coreutils')
backup=(etc/minieap.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/updateing/minieap/archive/v$pkgver.tar.gz"
        minieap.conf
        minieap.service)
sha512sums=('986ae4b031fe40ea4aafcb1fbc2a95f50f5fb7fa45a8a4092aef7275b8befd98ad702764f76749a2d5b350ed7c673b51de52306d0a8bdf19794afdf523f44869'
            '308fd102807e0054536f7e01cf411d9ab59f05f1ab20ff36cd43eeca342ee48c3415ade604186fcc7a13b49b52983eb9362857cb126fc3580bb14f4ad6c0c3d5'
            '62fabfd58fc4274c86ba73a12f0f344e87f649fffc793d927db104a4459271948060a9d6e0f3b7e9407de24ed614f1aa1061b494984298b50826d6558daaaafb')

build() {
  cd minieap-$pkgver
  make
}

package() {
  cd minieap-$pkgver
  install -Dm755 minieap "$pkgdir"/usr/bin/minieap
  install -Dm600 "$srcdir"/minieap.conf "$pkgdir"/etc/minieap.conf
  install -Dm644 "$srcdir"/minieap.service "$pkgdir"/usr/lib/systemd/system/minieap.service
}
