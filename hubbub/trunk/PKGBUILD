# $Id$
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=hubbub
pkgver=0.1.0
pkgrel=1
pkgdesc="HTML5 compliant parsing library"
arch=('i686' 'x86_64')
url="http://www.netsurf-browser.org/projects/hubbub"
license=('MIT')
depends=('glibc' 'libparserutils')
source=("http://www.netsurf-browser.org/projects/releases/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('9d7bb287bd5f993902c577ce3503f6d5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  sed -i 's/-Werror//' Makefile
  make PREFIX=/usr COMPONENT_TYPE="lib-shared"
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}-src"
  make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-shared"   
  make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
