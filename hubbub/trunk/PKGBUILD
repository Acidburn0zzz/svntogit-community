# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
pkgname=hubbub
pkgver=0.1.1
pkgrel=2
pkgdesc="HTML5 compliant parsing library"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/projects/hubbub/"
license=('MIT')
depends=('libparserutils')
makedepends=('curl')
source=("http://www.netsurf-browser.org/projects/releases/$pkgname-$pkgver-src.tar.gz")
md5sums=('98f49be58e228010c88381bc39f39e19')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE="lib-shared"
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"   
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
