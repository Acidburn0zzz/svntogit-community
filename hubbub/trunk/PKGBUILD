# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>

pkgname=hubbub
pkgver=0.2.0
pkgrel=1
pkgdesc='HTML5 compliant parsing library'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/projects/hubbub/'
license=('MIT')
depends=('libparserutils>=0.1.2')
makedepends=('netsurf-buildsystem')
source=("http://download.netsurf-browser.org/libs/releases/lib$pkgname-$pkgver-src.tar.gz")
sha256sums=('91c90965fdbbfb9137c7881b33736a03fc80bd16f5cd7808e1efff3d63000eaf')

build() {
  cd "$srcdir/lib$pkgname-$pkgver"

  sed -i 's:-Werror::' Makefile
  make PREFIX=/usr COMPONENT_TYPE='lib-shared'
  make PREFIX=/usr COMPONENT_TYPE='lib-static'
}

package() {
  cd "$srcdir/lib$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-shared'   
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE='lib-static'
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
