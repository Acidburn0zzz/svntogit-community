# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=clash-lib
pkgname=haskell-clash-lib
pkgver=1.2.5
pkgrel=1
pkgdesc="CAES Language for Synchronous Hardware - As a Library"
url="https://clash-lang.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-attoparsec'
         'haskell-clash-prelude' 'haskell-concurrent-supply' 'haskell-data-binary-ieee754'
         'haskell-data-default' 'haskell-dlist' 'haskell-errors' 'haskell-extra' 'haskell-ghc'
         'haskell-hashable' 'haskell-src-meta' 'haskell-hint' 'haskell-interpolate' 'haskell-lens'
         'haskell-ordered-containers' 'haskell-parsers' 'haskell-prettyprinter' 'haskell-primitive'
         'haskell-reducers' 'haskell-temporary' 'haskell-terminal-size' 'haskell-text-show'
         'haskell-trifecta' 'haskell-unordered-containers' 'haskell-utf8-string' 'haskell-vector'
         'haskell-vector-binary-instances')
makedepends=('ghc' 'haskell-ghc-typelits-knownnat' 'haskell-src-exts' 'haskell-tasty'
             'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha256sums=('239b15e4502cf7e92bcf26efd973c7b3bd67423b0673c768f5c399fa2905b193')

build() {
  cd $_hkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
