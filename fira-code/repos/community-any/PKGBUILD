# Maintainer: Jiachen YANG <farseerfc@gmail.com>
# Contributor: Henrik Hodne <henrik@hodne.io>
# Contributor: Ariel AxionL <axionl@aosc.io>
# AUR Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=fira-code
pkgname=('ttf-fira-code' 'woff-fira-code' 'woff2-fira-code')
pkgver=6.1
pkgrel=1
pkgdesc="Monospaced font with programming ligatures"
arch=('any')
url='https://github.com/tonsky/FiraCode'
license=('custom:OFL')
depends=()
source=("$pkgbase-$pkgver.zip::https://github.com/tonsky/FiraCode/releases/download/${pkgver}/Fira_Code_v${pkgver}.zip"
        "LICENSE::https://raw.githubusercontent.com/tonsky/FiraCode/${pkgver}/LICENSE")
sha512sums=('262027cb970c5c8bcf0f10e54f2e8a96ebd85d580ba300d2c5e42cc55a27f44f3e6398bb268f7017f5b6ef83527b985674296446e5af30bd25f9aa24dcc4ea6f'
            'bee6615aa2c8771ea9685a6de630eceb554c22920fd56379be460f461b27081de2d12e04298e4aecc76b733465730f3f3b602867abbc2df53105eb8ccf5475d2')

function _package {
    local ext="${1%%-*}"
    cd "$ext"
    local fonts=(FiraCode*."$ext")
    local installdir="${ext^^}"

    # Prepare destination directory
    install -dm755 "$pkgdir/usr/share/fonts/$installdir"

    # Install fonts
    local font
    for font in "${fonts[@]}"; do
        install -m644 "$font" "$pkgdir/usr/share/fonts/$installdir"
    done

    # Install LICENSE
    install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

for _pkgname in ${pkgname[@]}; do
    eval "function package_$_pkgname() { _package $_pkgname; }"
done

# vim:set ts=8 sw=4 et:
