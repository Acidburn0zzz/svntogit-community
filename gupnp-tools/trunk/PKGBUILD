# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gupnp-tools
pkgver=0.8.15
pkgrel=2
pkgdesc="Utilities and demos to work with UPnP"
arch=('x86_64')
url="https://wiki.gnome.org/Projects/GUPnP"
license=('GPL')
depends=('gtksourceview4' 'gupnp-av')
makedepends=('meson')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('336ef4a09b9fc83444a1594c8215e2bed55fbea5b6d1bf6b54c63104b4c497ab')

prepare() {
  cd $pkgname-$pkgver

  # Fix icons installation
  # https://gitlab.gnome.org/GNOME/gupnp-tools/merge_requests/2
  sed -i "s/'256x256'))/'256x256', 'apps'))/" data/meson.build
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
