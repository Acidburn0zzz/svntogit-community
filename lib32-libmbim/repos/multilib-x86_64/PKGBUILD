# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-libmbim
pkgver=1.16.0
pkgrel=1
pkgdesc='MBIM modem protocol helper library'
arch=('x86_64')
url='https://www.freedesktop.org/wiki/Software/libmbim/'
license=('GPL2')
depends=('lib32-glib2' 'lib32-glibc' 'lib32-libgudev' 'libmbim')
makedepends=('gcc-multilib' 'python')
source=("https://www.freedesktop.org/software/libmbim/libmbim-${pkgver}.tar.xz")
sha256sums=('c8ca50beeddd4b43309df5b698917268303bf176cea58fe4fe53d5bf0e93fac2')

build() {
  cd libmbim-${pkgver}

  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/libmbim' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --disable-gtk-doc-html \
    --disable-static
  make
}

package() {
  cd libmbim-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
