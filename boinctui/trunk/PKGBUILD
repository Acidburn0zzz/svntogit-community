# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.4.3
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('i686' 'x86_64')
url="https://sourceforge.net/projects/boinctui/"
license=('GPL')
depends=('expat' 'gcc-libs' 'openssl')
makedepends=("cmake")
source=("http://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('1517466246cd57b96d496cfb90d9296e2cf87a4abf10ca6d40e5b9ed5a95aa11adb3f739ea143d18921859ffccd39666e02877177e85d4144ca68b614e9178c2')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's_#include <ncursesw/curses.h>_#include <cursesw.h>_' src/tui-main.cpp
  sed -i 's_#include <ncursesw/_#include <_g' src/*
  sed -i 's_ncursesw/curses.h_cursesw.h_' configure.in
  sed -i 's_ncursesw/__g' configure.in
}

build() {
  cd $pkgname-$pkgver
  autoconf
  ./configure --prefix=/usr --without-gnutls
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
