# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=boinctui
pkgver=2.4.2
pkgrel=1
pkgdesc="Curses based fullscreen BOINC manager"
arch=('i686' 'x86_64')
url="http://boinctui.googlecode.com/"
license=('GPL')
depends=('expat' 'gcc-libs' 'openssl')
makedepends=("cmake")
source=("http://sourceforge.net/projects/boinctui/files/${pkgname}_${pkgver}.tar.gz")
sha512sums=('be4c3ba61d389814a7cd91d239e6766140c7257e2d3271cba54c1cffac2a593d0959b23abcf763838d1f8b02a790fc5537bc2f0f82d4a615348344548e5ea485')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's_#include <ncursesw/curses.h>_#include <cursesw.h>_' src/tui-main.cpp
  sed -i 's_#include <ncursesw/_#include <_g' src/*
  sed -i 's_ncursesw/curses.h_cursesw.h_' configure.in
  sed -i 's_ncursesw/__g' configure.in
}

build() {
  cd $pkgname-$pkgver
  autoconf
  ./configure --prefix=/usr --without-gnutls
  make 
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
