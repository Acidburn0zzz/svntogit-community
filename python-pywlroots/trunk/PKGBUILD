# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywlroots
pkgname=python-pywlroots
pkgver=0.14.8
pkgrel=1
pkgdesc="Python binding to the wlroots library using cffi"
arch=('x86_64')
url="https://github.com/flacjacket/pywlroots"
license=('custom:UIUC')
depends=('glibc' 'python-cffi' 'python-pywayland' 'python-xkbcommon' 'wlroots'
'libwlroots.so')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
provides=('pywlroots')
conflicts=('pywlroots')
replaces=('pywlroots')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ae22a6cd7db8ce9706aed21167d53c449e3f61b09656ff9cc54307b0548d9c9ccf0bd591b77662a7b0abcf8db2428f370197e562d8fffb77fc371c8c274dde99')
b2sums=('84cddb9d71ed98cd0f17b47c856477a2543043283f33e24d5b6e27795689337f3703fbed3f53e6a6ef74c36b05948aa0a3f758f02fde136f1a1e6dc328a560d0')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python wlroots/ffi_build.py
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
