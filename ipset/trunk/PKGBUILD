# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=ipset
pkgver=6.12.1
pkgrel=1.1
pkgdesc='Administration tool for IP sets'
arch=('i686' 'x86_64')
url='http://ipset.netfilter.org'
license=('GPL2')
depends=('libmnl')
backup=("etc/$pkgname.conf" "etc/conf.d/$pkgname")
options=('!libtool')
source=("http://ipset.netfilter.org/$pkgname-$pkgver.tar.bz2"
        "$pkgname.conf"
        "$pkgname.rc"
        "$pkgname.systemd"
        "$pkgname.service")
md5sums=('80df500a4d93cb14e1373ae290716cda'
         '3e606a2823a700b4e9c5f17511a6f6a5'
         'de89b054d967036e08b13bc6a7235286'
         'a723a42c75be3c6fd53ab177ff784c8f'
         'b032241b96b5802975fe4321cc511c6b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-kmod=no
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install doc
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README UPGRADE ChangeLog "$pkgdir/usr/share/doc/$pkgname"
  popd
  # install initscripts
  install -Dm 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -Dm 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd service
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
