# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=ipset
pkgver=6.20
pkgrel=1
pkgdesc='Administration tool for IP sets'
arch=('i686' 'x86_64')
url='http://ipset.netfilter.org'
license=('GPL2')
depends=('libmnl')
backup=("etc/$pkgname.conf")
options=('!libtool')
source=("http://ipset.netfilter.org/$pkgname-$pkgver.tar.bz2"
        '01-Fix-configure-failure-when-with-kmod.patch'
        "$pkgname.service")
md5sums=('af28b3af356fff3e4a8e321ff7145678'
         '5569fd4ee93acac4ae01d091002cc2f5'
         '6be6a2b34c1f38fc8ad60828403e2077')

prepare() {
  patch -p1 -d $pkgname-$pkgver < 01-Fix-configure-failure-when-with-kmod.patch
  cd $pkgname-$pkgver && ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin --with-kmod=no
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install doc
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
  install -m644 README UPGRADE ChangeLog "$pkgdir/usr/share/doc/$pkgname"
  # install pkgconfig file
  install -Dm644 lib/libipset.pc "$pkgdir/usr/lib/pkgconfig/libipset.pc"
  popd
  # systemd
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
