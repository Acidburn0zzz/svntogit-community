# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-gtk
pkgver=5.0.2
pkgrel=1
pkgdesc="Fcitx5 gtk im module and glib based dbus client library"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-gtk"
license=('GPL')
groups=('fcitx5-im')
depends=('fcitx5')
makedepends=('extra-cmake-modules' 'gobject-introspection' 'gtk2' 'gtk3' 'gtk4' 'ninja' 'vulkan-headers')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('b8d6418abedc27ff9c229cff000b0ec27b30fae79eecde9889f1d204253c0e768a6b9a6431347cfd643de2b17cc2cddd5a9e781a0567d3381a1f44ba61016f26'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
