# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cldr-emoji-annotation
pkgver=33.0.0.2
_tag=33.0.0_2
pkgrel=1
pkgdesc="Emoji annotation files in CLDR"
arch=('any')
url="https://github.com/fujiwarat/cldr-emoji-annotation"
license=('custom')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fujiwarat/cldr-emoji-annotation/archive/$_tag.tar.gz")
sha512sums=('493db9156226d1a170b037eb1ab33a6e0332550b0a97b339aefd826c40746280c6983b1c2bf305a0a65d963be6c1e93cea6ab8cc42b3ac30dac3d443e5018456')

build() {
  cd cldr-emoji-annotation-$_tag
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd cldr-emoji-annotation-$_tag
  make DESTDIR="$pkgdir" install

  install -Dm644 unicode-license.txt "$pkgdir"/usr/share/licenses/$pkgname/unicode-license.txt
}
