# Maintainer: David Runge <dvzrv@archlinux.org>

_name=toolz
pkgname=python-toolz
pkgver=0.10.0
pkgrel=2
pkgdesc="A functional standard library for Python"
arch=('any')
url="https://github.com/pytoolz/toolz/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4ed40c165462440c6004974d2515067f211c625bebcb69ea854fd8837e1fe05febc546215355a6bb9de5916c8ba98a4d297f8aa20e40ffbc8a730b2bb3831a71')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
