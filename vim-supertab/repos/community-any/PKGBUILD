# $Id$
# Maintainer: Aaron Schaefer <aaron@elasticdog.com>

pkgname=vim-supertab
pkgver=1.1
_sourceid=13928
pkgrel=1
pkgdesc='A Vim plugin that allows you to use the tab key to do all insert completion'
arch=('any')
url='http://www.vim.org/scripts/script.php?script_id=1643'
license=('BSD')
depends=('vim')
install=vimdoc.install
source=("supertab.vba::http://www.vim.org/scripts/download_script.php?src_id=$_sourceid"
        'LICENSE')
md5sums=('c24c0df19e8d7e788b562d14cd10a6ea'
         'a232f3fa724b2e52e1678adcc84f67bb')

build() {
  cd "$srcdir"

  # extract the vimball
  /usr/bin/vim -n -i NONE -c ":UseVimball $srcdir" -c ":q" ./supertab.vba

  installpath="$pkgdir/usr/share/vim/vimfiles"

  install -D -m644 doc/supertab.txt "$installpath/doc/supertab.txt"
  install -D -m644 plugin/supertab.vim "$installpath/plugin/supertab.vim"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
