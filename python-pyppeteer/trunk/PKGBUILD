# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: robertfoster
# Contributor: Ista Zahn <istazahn[at]gmail[dot]com>

_name=pyppeteer
pkgname=python-pyppeteer
pkgver=0.2.3
pkgrel=1
pkgdesc='Headless chrome/chromium automation library'
arch=('any')
url=https://github.com/pyppeteer/pyppeteer
license=('MIT')
depends=('python-appdirs' 'python-pyee' 'python-tqdm' 'python-urllib3'
         'python-websockets')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('bda1f17bd6693c60421ea3c78e91e7461b42551df5a32f9bcfe2f86c8ba29274')
b2sums=('28862bdbb04733fe4191166fbf1f754182771f3fdc11a2a1fe327802ccf87ff7ce14a886fef9149e2a9a4dc061ac2733d022298eea7ae00a28c9876c21e33f8c')

prepare() {
  cd $_name-$pkgver
  sed -i '/^include/,/]/d' pyproject.toml
}

build() {
  cd $_name-$pkgver
  poetry build --format wheel
}

package() {
  cd $_name-$pkgver
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
