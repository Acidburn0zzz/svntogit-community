# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=('gmic' 'gimp-plugin-gmic')
pkgver=1.5.1.1
pkgrel=1
pkgdesc="image procession framework"
arch=('i686' 'x86_64')
license=('custom:CeCILL')
url="http://gmic.sourceforge.net"
makedepends=('gimp' 'fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr' 'imagemagick')
options=('docs' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz"
	opencv-buildfix.patch)
md5sums=('858a1d8d06d19a1903feb5314a58c805'
         '62515a0e477e71e11ee24f66b5799937')

build() {
  cd "$srcdir/gmic-$pkgver"
#  CFLAGS+=" -Dcimg_use_lapack"
#  LDFLAGS="-llapack"
  patch -p1 <$srcdir/opencv-buildfix.patch
  make -C src all
}

package_gmic() {
  depends=('fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
  replaces=('greycstoration')

  cd "$srcdir/gmic-$pkgver"
  make -C src install DESTDIR="$pkgdir" USR=/usr
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gmic/LICENSE"
  mkdir -p $pkgdir/usr/share/bash-completion/completions/
  mv $pkgdir/etc/bash_completion.d/gmic $pkgdir/usr/share/bash-completion/completions/gmic
  rm -rf $pkgdir/etc/bash_completion.d
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the GMIC image procession framework"
  depends=('gimp' 'fftw' 'lapack')
  replaces=('gimp-plugin-greycstoration' 'gimp-plugin-gmic4gimp')

  cd "$srcdir/gmic-$pkgver"
  install -Dm755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gimp-plugin-gmic/LICENSE"
}
