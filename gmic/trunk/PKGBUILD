# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=("gmic" "gimp-plugin-gmic" "zart")
pkgver=1.5.1.8
pkgrel=4
arch=("i686" "x86_64")
url="http://gmic.sourceforge.net"
license=("custom:CeCILL")
makedepends=("gimp" "qt" "fftw" "openexr" "opencv")
#options=('!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_${pkgver}.tar.gz"
        "opencv-buildfix.patch")
md5sums=('1fc38346269bdd829dd7c4864a27fbc3'
         '8d1cf5000239099255d25c7d3dbe5728')

build() {
  cd "${srcdir}/gmic-${pkgver}"
  patch -p1 -i "${srcdir}/opencv-buildfix.patch"
  find "${srcdir}/gmic-${pkgver}/zart" -type f -execdir chmod 644 '{}' \;
  find "${srcdir}/gmic-${pkgver}/zart" -type d -execdir chmod 755 '{}' \;
  make -C src all
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=("fftw" "libtiff" "libjpeg" "libpng" "openexr" "libx11")
  replaces=("greycstoration")

  cd "${srcdir}/gmic-${pkgver}"
  make -C src install DESTDIR="$pkgdir" USR="/usr"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/"{bin/zart,lib/gimp,share/zart}
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=("opencv" "fftw" "qt")

  cd "${srcdir}/gmic-${pkgver}"
  make -C src install DESTDIR="$pkgdir" USR="/usr"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  rm -rf "${pkgdir}/usr/"{bin/gmic,include,lib,share/{man,bash-completion}}
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=("gimp" "fftw")
  replaces=("gimp-plugin-greycstoration" "gimp-plugin-gmic4gimp")

  cd "${srcdir}/gmic-${pkgver}"
  install -Dm755 src/gmic_gimp "${pkgdir}/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
