# Maintainer: Muflone http://www.muflone.com/contacts/english/

pkgname=perl-gtk3-simplelist
_perl_module=Gtk3-SimpleList
pkgver=0.19
pkgrel=1
pkgdesc="Perl interface to the Gtk3's complex MVC list widget"
arch=('any')
url="https://metacpan.org/release/${_perl_module}"
license=('LGPL')
depends=('perl-gtk3')
source=("https://cpan.metacpan.org/authors/id/T/TV/TVIGNAUD/${_perl_module}-${pkgver}.tar.gz")
sha512sums=('0c6875caaf353c36a559cb0fa1bf6cf243366751737af8851b822bc21a9b770a7ccaca2c6831fe83d7f2eabe57a42eac327efb118ca33abdf24a52bdedcee03e')
options=('!emptydirs')

build() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}

check() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}

package() {
  cd "${_perl_module}-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  make pure_install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
  # Delete unuseful files
  find "${pkgdir}" -name '.packlist' -delete
}
