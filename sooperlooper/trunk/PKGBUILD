# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sooperlooper
pkgver=1.7.7
pkgrel=1
pkgdesc="Live looping sampler capable of immediate loop recording"
arch=('x86_64')
url="http://essej.net/sooperlooper/"
license=('GPL2')
depends=('alsa-lib' 'libasound.so' 'gcc-libs' 'glibc' 'jack' 'libjack.so'
'liblo' 'liblo.so' 'libsigc++' 'ncurses' 'libncursesw.so' 'rubberband'
'librubberband.so' 'libsamplerate' 'libsamplerate.so' 'libsndfile'
'libsndfile.so' 'wxgtk2')
makedepends=('gendesk' 'imagemagick' 'libxml2')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/essej/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('eea3c8549c177a9fb15c4848220515f4534ca05f0b863155e0744d3e6871002a2365f090b01003d06a0f5cc21ca58aacfb4cf81622b731e5a538677e5aa751cd')
b2sums=('927cdc0940f3caf907f25d5b1547cbcb025d394fd117a72910d6536c23191aa2808e8bb1b93ade1b771493db49b819bb3f8d43165af6ef63649311dbcc55966b')

prepare() {
  cd "${pkgname}-${pkgver}"

  ./autogen.sh

  convert doc/html/sl_web_logo_black.png \
    -resize 128x128\> \
    -size 128x128 xc:black +swap \
    -gravity center \
    -composite net.essej.sooperlooper.png

  gendesk -n \
          --pkgname "net.essej.sooperlooper" \
          --name "SooperLooper" \
          --pkgdesc "Live Looping Sampler" \
          --exec "slgui" \
          --icon "net.essej.sooperlooper" \
          --genericname "Live Looping Sampler" \
          --categories "AudioVideo;Audio;"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=()
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 net.essej.sooperlooper.png -t "${pkgdir}/usr/share/icons/hicolor/128x128/apps/"
}
