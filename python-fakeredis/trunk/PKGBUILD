# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=fakeredis
pkgname=python-fakeredis
pkgver=1.0.5
pkgrel=1
pkgdesc="Fake implementation of redis API (redis-py) for testing purposes"
arch=('any')
url="https://github.com/jamesls/${_pkgname}"
license=('BSD' 'MIT')
depends=('python-redis' 'python-six' 'python-sortedcontainers')
makedepends=('python-setuptools' 'python-lupa')
checkdepends=('python-nose' 'python-hypothesis')
optdepends=('python-lupa: for lua scripting support')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('9194c481526e09e5d572559a097d36af282f1c725c615036f8a1bb3fb07596a9223d6ffd362b67fd30cf280f768d4bf75623b64849adcc6d351683d87f9f5dea')
b2sums=('3747e5065f465f9aaf98331e7e5175924fe2300877a15d7728cf0342f29e9bcf5caf13873e1a489ca86b1e5a4bd5a9d178ad8c3baa0883f3ed2d277b7969e462')

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    nosetests
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
