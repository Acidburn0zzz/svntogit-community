# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: ajs124 < aur AT ajs124 DOT de >

pkgname=firejail
pkgver=0.9.64rc1
pkgrel=2
pkgdesc="Linux namespaces sandbox program"
arch=(x86_64)
license=(GPL2)
url="https://github.com/netblue30/firejail"
backup=('etc/firejail/login.users'
	'etc/apparmor.d/local/firejail-local'
	'etc/firejail/firejail.config')
depends=('apparmor')
validpgpkeys=('F951164995F5C4006A73411E2CCB36ADFC5849A7')
install=firejail.install
#source=(${pkgname}-${pkgver}.tar.gz::https://github.com/netblue30/${pkgname}/archive/$pkgver.tar.gz)
source=(https://sourceforge.net/projects/firejail/files/firejail/firejail-${pkgver}.tar.xz{,.asc}
	https://github.com/netblue30/firejail/commit/bba750c73469ea315d859464ddd19e495d830a72.patch)
sha256sums=('47ac6805199f34ebc5b46d56d2b0dfde535c5ba9cdc57ffd56165a9a6db57377'
            'SKIP'
            '8b90c00d4eb54fa5ad15d14cc260761a382c7adaed03d4c59f05ab9bef891193')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 <../bba750c73469ea315d859464ddd19e495d830a72.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr --enable-apparmor
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
#	install -c -m 0644 etc/firejail-local ${pkgdir}/etc/apparmor.d/local/firejail-local
}
