# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Loui Chang <louipc dot ist at gmail company>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=tig
pkgver=1.1
pkgrel=1
pkgdesc='Text-mode interface for Git.'
depends=('git' 'ncurses')
url='http://jonas.nitro.dk/tig/'
license=('GPL')
arch=('i686' 'x86_64')
source=("http://jonas.nitro.dk/${pkgname}/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('adeb797a8320962eeb345a615257cbac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install install-doc-man
  install -Dm0644 contrib/tigrc "$pkgdir/etc/tigrc.sample"
  install -Dm0644 contrib/tig-completion.bash "$pkgdir/usr/share/bash-completion/completions/tig"
}
