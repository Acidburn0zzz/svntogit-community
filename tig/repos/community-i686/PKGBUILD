#  Maintainer: Loui Chang <louipc dot ist at gmail company>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Nathan Jones <nathanj@insightbb.com>

pkgname=tig
pkgver=0.16.2
pkgrel=2
pkgdesc="Text-mode interface for git"
depends=(git ncurses)
url="http://jonas.nitro.dk/tig/"
license=('GPL')
arch=('i686' 'x86_64')
source=("http://jonas.nitro.dk/${pkgname}/releases/${pkgname}-${pkgver}.tar.gz"
        "ncurses-5.8-fix.patch")
md5sums=('d72b5d3437dbc538ea6f66c74988d75e'
         '3406e82c4018b5b8bda5abab3915cf50')

build() {
  cd "$srcdir"/$pkgname-$pkgver

  # fix newwin() calls for ncurses 5.8
  patch -Np0 < "$srcdir/ncurses-5.8-fix.patch"

  ./configure --prefix=/usr
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-doc-man
  install -D -m644 contrib/tigrc "$pkgdir"/etc/tigrc.sample
  install -D -m644 contrib/tig-completion.bash "$pkgdir"/etc/bash_completion.d/tig
}
