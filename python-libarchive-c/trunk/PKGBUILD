# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-libarchive-c
pkgname=('python-libarchive-c' 'python2-libarchive-c')
pkgver=2.9
pkgrel=0
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('CC0')
makedepends=('python-setuptools' 'python2-setuptools' 'libarchive')
checkdepends=('python-pytest-cov' 'python-mock' 'python2-pytest-cov' 'python2-mock')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/Changaco/python-libarchive-c/archive/${pkgver}.tar.gz)
sha512sums=('282791cef6ff67aefc238ce1b40783e3952b366fc5c464fe99557e81d91e8057b9ec339c52535917c251f9a9bffdd3ec2773c91cbb6c9881d9ecb8572cf0fc5b')

prepare() {
  sed '/os.umask(2)/d' -i ${pkgbase}-${pkgver}/setup.py
  cp -a ${pkgbase}-${pkgver}{,-py2}
}

check() {
  (cd ${pkgbase}-${pkgver}
    LC_CTYPE=en_US.UTF-8 py.test
  )
  (cd ${pkgbase}-${pkgver}-py2
    LC_CTYPE=en_US.UTF-8 py.test2
  )
}

build() {
  (cd ${pkgbase}-${pkgver}
    python setup.py build
  )
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

package_python-libarchive-c() {
  depends=('python' 'libarchive')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python2-libarchive-c() {
  depends=('python2' 'libarchive')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
