# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-libarchive-c
pkgname=('python-libarchive-c' 'python2-libarchive-c')
pkgver=2.5
pkgrel=1
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('CC0')
makedepends=('python-setuptools' 'python2-setuptools' 'libarchive')
checkdepends=('python-pytest-cov' 'python2-pytest-cov')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/Changaco/python-libarchive-c/archive/${pkgver}.tar.gz)
sha512sums=('b451d4be2271f0f9c6ffafd349f6bbbeeeb8b92007c97cd6042379dafa237afcacfbf63034b5932cf6a62c4eaf65eb5c089b61050ab23ce88e49a53556abae08')

prepare() {
  cd ${pkgbase}-${pkgver}
  sed '/os.umask(2)/d' -i setup.py
}

check() {
  cd ${pkgbase}-${pkgver}
  LC_CTYPE=en_US.UTF-8 py.test
  LC_CTYPE=en_US.UTF-8 py.test2
}

package_python-libarchive-c() {
  depends=('python' 'libarchive')
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-libarchive-c() {
  depends=('python2' 'libarchive')
  cd ${pkgbase}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
