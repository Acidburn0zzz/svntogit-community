# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-tornado-redis
pkgname=(python-tornado-redis python2-tornado-redis)
pkgver=2.5.0
_commit=64f52be0e2004a0871e24ac61598c9dd760a873d
pkgrel=1
pkgdesc="Asynchronous Redis client for the Tornado Web Server"
url="https://github.com/kmike/tornado-redis"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-tornado' 'python2-tornado')
checkdepends=('pifpaf' 'redis')
source=("$pkgbase-$_commit.tar.gz::https://github.com/leporo/tornado-redis/archive/$_commit.tar.gz")
sha512sums=('eb2b76671130efb6d1135fd32b728f908e1b86f5cd285444eb289f0991ee5ed93f2f110e33f69b9e10c58baae41a768b5fc7b4532b04e2bb396acff332f881b8')

prepare() {
  mv tornado-redis-{$_commit,$pkgver}
  cp -a tornado-redis-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/tornado-redis-$pkgver
  python setup.py build

  cd "$srcdir"/tornado-redis-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/tornado-redis-$pkgver
  pifpaf run redis -- python -m tornado.testing tornadoredis.tests

  cd "$srcdir"/tornado-redis-$pkgver-py2
  pifpaf run redis -- python2 -m tornado.testing tornadoredis.tests
}

package_python-tornado-redis() {
  depends=('python')

  cd tornado-redis-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-tornado-redis() {
  depends=('python2')

  cd tornado-redis-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
