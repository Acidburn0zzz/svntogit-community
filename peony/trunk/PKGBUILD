# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=1.1.5
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('atril' 'gtksourceview3' 'mate-desktop' 'libnotify' 'libsm' 'udisks2' 'webkit2gtk')
makedepends=('gobject-introspection' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('2cead31ac82b38f34fc5c3ae2b75dc32431864726d4a15e99a6122c43cd6678441d4656c97da436ebf571a456811c46c3de85bf2e2de4e16568337e30872bf37')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --enable-introspection --disable-update-mimedb
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
