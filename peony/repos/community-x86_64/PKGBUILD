# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=2.2.0
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gsettings-qt' 'kwindowsystem' 'poppler-qt5' 'qt5-x11extras')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('bb248404eea19cd5cffee1bfdc92230c61f4e195dc12ba138aa5daae6b31a21ac5e21e97852bf2099207e54edb5ca331e735f7e164b68e51f99e7fb87b2ccadc')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
