# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=2.2.0
pkgrel=2
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gsettings-qt' 'kwindowsystem' 'poppler-qt5' 'qt5-x11extras')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz"
        qm-install.patch)
sha512sums=('4d441a0acefb6f46aa763d8b05b7f7d15c5d1f5790d270db2cf0179fde7b94fe77e03ca3cf29e2762d96aa714530a86e84d470297f01ba5ae0d4aa4c9945d0df'
            'c7dbbe00cedebbe65afafb70b76053e5db30482672db0141b467df91139e0ada31f9a6080283f96e7b5be98b0ac48e81c72d3deff461750000ae0881176767bc')

build() {
  cd peony-$pkgver
  patch -p1 -i ../qm-install.patch
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/control/tab-status-bar.cpp
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
