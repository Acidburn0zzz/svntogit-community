# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.0.3
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gtk2' 'gsettings-qt' 'kwindowsystem' 'libnotify' 'poppler-qt5' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('75adcb2b0eb455c290cd81f24f48e2a6f564ee036c63bfea8d812997f38db99c21e6f32bf0ef2c8bdafe47a3a5e598306f42c3c6a26567dbd34eced5e563420f')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
