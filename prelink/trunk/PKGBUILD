# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Rouslan Solomakhin <rouslan@localnet.com>

pkgname=prelink
pkgver=20130503
pkgrel=2
pkgdesc='ELF prelinking utility to speed up dynamic linking'
arch=('x86_64' 'i686')
url='http://people.redhat.com/jakub/prelink/'
license=('GPL')
depends=('elfutils')
backup=('etc/prelink.conf')
source=("http://people.redhat.com/jakub/$pkgname/$pkgname-$pkgver.tar.bz2"
        "$pkgname.conf")
sha256sums=('6339c7605e9b6f414d1be32530c9c8011f38820d36431c8a62e8674ca37140f0'
            'c486ee7d2a39fad6b4ed49607eff5a81c9d07316b67b08b61af6008d010c4ad4')

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --mandir=/usr/share/man --sbin=/usr/bin
  make
}

check() {
  make -C "$pkgname" check
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
  install -Dm644 prelink.conf "$pkgdir/etc/prelink.conf"
}

# vim:set ts=2 sw=2 et:
