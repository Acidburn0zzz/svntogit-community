# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Fabian Schoelzel <myfirstname.mylastname@googlemail.com>
# Contributor: funkyou <spamopfer@nickname.berlin.de>
# Contributor: tardo <tardo@nagi-fanboi.net>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgbase=mlt
pkgname=('mlt' 'mlt-python-bindings')
pkgver=0.6.2
pkgrel=2
pkgdesc="An open source multimedia framework"
arch=('i686' 'x86_64')
url="http://www.mltframework.org"
license=('GPL')
makedepends=('sdl_image' 'libsamplerate' 'libdv' 'qt' 'sox' 'libxml2' 'gtk2' 'ffmpeg'
	     'frei0r-plugins' 'swig' 'python2')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('7b3a866790381a227d278bb050d87590')

build() {
  # mlt
  cd $srcdir/mlt-$pkgver
  [ $CARCH == "i686" ] && SSE2=--disable-sse2 || SSE2=
  msg "SSE2=$SSE2"
  ./configure --prefix=/usr --enable-gpl \
	--qimage-libdir=/usr/lib/ --qimage-includedir=/usr/include/Qt \
	--avformat-swscale $SSE2
  make

  # mlt python bindings
  cd $srcdir/mlt-$pkgver/src/swig/python
  sed -i 's_path=`which python_path=`which python2_' build
  sed -i 's_`python -c_`python2 -c_' build
  ./build
}

package_mlt() {
  depends=('sdl_image' 'libsamplerate' 'libdv' 'sox' 'libxml2' 'ffmpeg' 'frei0r-plugins')
  optdepends=('jack' 'libexif' 'qt' 'gtk2')
  conflicts=('mlt++<=0.3.8')

  cd $srcdir/mlt-$pkgver
  make DESTDIR=$pkgdir install
}

package_mlt-python-bindings() {
  depends=('python2' 'mlt')

  cd $srcdir/mlt-$pkgver/src/swig/python
  mkdir -p $pkgdir/usr/lib/python2.7/
  install -m755 mlt.py $pkgdir/usr/lib/python2.7/
  install -m755 _mlt.so $pkgdir/usr/lib/python2.7/
  install -m755 mlt_wrap.o $pkgdir/usr/lib/python2.7/
}
