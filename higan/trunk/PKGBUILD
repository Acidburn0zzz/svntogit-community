# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=higan
pkgver=097
pkgrel=1
pkgdesc='Nintendo multi-system emulator'
arch=('i686' 'x86_64')
url='http://byuu.org/'
license=('GPL3')
depends=('alsa-lib' 'cairo' 'gcc-libs' 'gdk-pixbuf2' 'glib2' 'glibc' 'gtk2'
         'gtksourceview2' 'libao' 'libgl' 'libpulse' 'libx11' 'libxext' 'libxv'
         'openal' 'pango' 'sdl' 'zenity'
         'libudev.so')
makedepends=('mesa')
conflicts=('higan-gtk' 'higan-qt')
replaces=('higan-gtk' 'higan-qt')
install='higan.install'
source=("http://download.byuu.org/higan_v${pkgver}-source.7z"
        'higan-flags.patch'
        'higan')
sha256sums=('6dea7084f0b786e42d1911df05de2d7f7f18c58f426e3e61c3a122b17481e22b'
            '6ee3489a3cf3e8d09beaa5c1f809824593aa08b61dc9a8c9ddc12a09c89ae6cb'
            '1abd4fd30e48e48dddb3cdce95f62be0b1b28c66f85a3b6ce95d29d7bfc94659')

prepare() {
  cd higan_v${pkgver}-source

  patch -Np1 -i ../higan-flags.patch
}

build() {
  cd higan_v${pkgver}-source/icarus

  make

  cd ../higan

  for p in {accuracy,balanced,performance}; do
    make profile=$p
    mv out/higan higan-$p
    make clean
  done
}

package() {
  cd higan_v${pkgver}-source

  install -dm 755 "${pkgdir}"/usr/{bin,share/{applications,higan,pixmaps}}
  install -m 755 ../higan "${pkgdir}"/usr/bin/
  install -m 644 higan/data/higan.desktop "${pkgdir}"/usr/share/applications/
  install -m 644 higan/data/higan.png "${pkgdir}"/usr/share/pixmaps/
  cp -dr --no-preserve='ownership' higan/profile/* higan/data/cheats.bml "${pkgdir}"/usr/share/higan/

  install -m 755 icarus/out/icarus "${pkgdir}"/usr/bin/

  for p in {accuracy,balanced,performance}; do
    install -m 755 higan/higan-$p "${pkgdir}"/usr/bin/
  done
}

# vim: ts=2 sw=2 et:
