# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=sigal
pkgver=2.1
pkgrel=1
pkgdesc="Yet another simple static gallery generator"
arch=('any')
url="http://sigal.saimon.org/en/latest/"
license=('MIT')
depends=('ffmpeg' 'python-blinker' 'python-click' 'python-jinja'
'python-markdown' 'python-natsort' 'python-pillow' 'python-pilkit'
'python-setuptools')
checkdepends=('python-pytest-cov' 'python-boto' 'python-brotli'
'python-cryptography' 'python-feedgenerator' 'python-zopfli')
optdepends=('python-boto: upload to S3 plugin'
            'python-brotli: compress assets plugin'
            'python-cryptography: encrypt plugin'
            'python-feedgenerator: feed plugin'
            'python-zopfli: compress assets plugin')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('951755295ad971360a0d3b219a2649f5d0c7d1dbdec73a2178e1802ddd3a6693ffd50a42edef80b0188554b49fa21952e9b12738321ec23c48f3a24761385eaf')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  # disabling failing test: https://github.com/saimn/sigal/issues/390
  pytest -v -k 'not test_generate_thumbnail'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,{CONTRIBUTING,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
