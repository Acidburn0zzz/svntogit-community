# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Florian Bruhin (The Compiler) <archlinux.org@the-compiler.org>

pkgname=qutebrowser
pkgver=0.6.1
pkgrel=2
pkgdesc="A keyboard-driven, vim-like browser based on PyQt5 and QtWebKit"
arch=("any")
url="http://www.qutebrowser.org/"
license=("GPL")
depends=("desktop-file-utils" "hicolor-icon-theme" "libxkbcommon-x11"
	"python-jinja" "python-pygments" "python-pypeg2" "python-pyqt5>=5.2"
	"python-yaml" "qt5-base>=5.2" "qt5-webkit>=5.2" "xdg-utils")
makedepends=("asciidoc")
optdepends=("python-colorlog: colored logging output"
	"gst-libav: media playback"
	"gst-plugins-base: media playback"
	"gst-plugins-good: media playback"
	"gst-plugins-bad: media playback"
	"gst-plugins-ugly: media playback"
	"pdfjs: Displaying PDF in-browser")
options=(!emptydirs)
install="qutebrowser.install"
source=("http://qutebrowser.org/releases/v$pkgver/qutebrowser-$pkgver.tar.gz"
	"qutebrowser.install"
	"pyqt56-1.patch"
	"pyqt56-2.patch"
	"pyqt56-3.patch")
sha256sums=('a0ca51617170ca5ad14942d325feab8af188451153065a8e3b10a4c1a42f69f5'
            'a8a464c45ca26a88b5f7bf5d714d1038a364d2846c6820201cc4f443fd791acc'
            '7838fe6753b55a0d325fa1927cca9b880826b532234fc9e4ff7163a8336865a9'
            '59f97519bbfe67d3c0a9f43a635fc10674e6681913a6c56b213823faebd5aa18'
            'b9bf35ae5b92e6b6692097f85b2cce216d2bd2a83f7f467a538ecfe9c17d1765')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"

	msg2 "Backporting PyQt 5.6 compatibility fixes..."
	patch -p1 < "$srcdir/pyqt56-1.patch"
	patch -p1 < "$srcdir/pyqt56-2.patch"
	patch -p1 < "$srcdir/pyqt56-3.patch"
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	a2x -f manpage doc/qutebrowser.1.asciidoc
	python setup.py build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	python setup.py install --root="$pkgdir/" --optimize=1
	install -Dm644 doc/qutebrowser.1 "$pkgdir/usr/share/man/man1/qutebrowser.1"
	install -Dm644 qutebrowser.desktop \
		"$pkgdir/usr/share/applications/qutebrowser.desktop"
	for i in 16 24 32 48 64 128 256 512; do
		install -Dm644 "icons/qutebrowser-${i}x$i.png" \
			"$pkgdir/usr/share/icons/hicolor/${i}x$i/apps/qutebrowser.png"
	done
	install -Dm644 icons/qutebrowser.svg \
		"$pkgdir/usr/share/icons/hicolor/scalable/apps/qutebrowser.svg"
	install -Dm755 -t "$pkgdir/usr/share/qutebrowser/userscripts/" misc/userscripts/*
}
