# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Sebastian Nowicki <sebnow@gmail.com>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Tobias Kieslich <tobias@justdreams.de>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=('llvm' 'llvm-ocaml' 'clang' 'clang-analyzer')
pkgver=2.6
pkgrel=6.1
arch=('i686' 'x86_64')
license=('custom:University of Illinois/NCSA Open Source License')
makedepends=('gcc-libs' 'libffi' 'python' 'ocaml')
source=(http://llvm.org/releases/$pkgver/$pkgname-$pkgver.tar.gz
        http://llvm.org/releases/$pkgver/clang-$pkgver.tar.gz)
md5sums=('34a11e807add0f4555f691944e1a404a'
         '09d696bf23bb4a3cf6af3c7341cdd946')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # At the present, clang must reside inside the LLVM source code tree to build
  # See http://llvm.org/bugs/show_bug.cgi?id=4840
  rm -rf tools/clang
  cp -r "$srcdir/clang-$pkgver" tools/clang

  # Leave clang-cc in /usr/bin
  sed -i '/^PROJ_bindir/d' \
         tools/clang/tools/clang-cc/Makefile || return 1

  # Fix symbolic links from OCaml bindings to LLVM libraries
  sed -i 's:\$(PROJ_libdir):/usr/lib/llvm:' \
         bindings/ocaml/Makefile.ocaml || return 1

  # Fix installation directories, ./configure doesn't seem to set them right
  sed -i -e 's:\$(PROJ_prefix)/etc/llvm:/etc/llvm:' \
         -e 's:\$(PROJ_prefix)/lib:$(PROJ_prefix)/lib/llvm:' \
         -e 's:\$(PROJ_prefix)/docs/llvm:$(PROJ_prefix)/share/doc/llvm:' \
         Makefile.config.in || return 1

  # Fix insecure rpath (http://bugs.archlinux.org/task/14017)
  sed -i 's:$(RPATH) -Wl,$(\(ToolDir\|LibDir\|ExmplDir\))::g' \
         Makefile.rules || return 1

  # Fix path that points to the build directory
  sed -i 's:^TOOLDIR.*:TOOLDIR=/usr/bin:' \
         tools/{gccas/gccas,gccld/gccld}.sh || return 1

  # Apply strip option to configure
  _optimize_flag="--enable-optimize"
  [ "$(check_option strip)" = "n" ] && _optimize_flag="--disable-optimize"

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

  ./configure --prefix=/usr --libdir=/usr/lib/llvm --sysconfdir=/etc \
              --enable-bindings=ocaml --enable-targets=all \
              --disable-expensive-checks --disable-debug-runtime \
              --disable-assertions $_optimize_flag
  make || return 1
}

package_llvm() {
  pkgdesc="Low Level Virtual Machine"
  url="http://llvm.org/"
  depends=('perl' 'libffi')

  cd "$srcdir/$pkgname-$pkgver"

  # We move the clang directory out of the tree so it won't get installed and
  # then we bring it back in for the clang package
  mv tools/clang "$srcdir"
  # -j1 is due to race conditions during the installation of the OCaml bindings
  make -j1 DESTDIR="$pkgdir" install || return 1
  mv "$srcdir/clang" tools

  # OCaml bindings go to a separate package
  rm -rf "$srcdir"/{ocaml,ocamldoc}
  mv "$pkgdir"/usr/{lib/ocaml,share/doc/llvm/ocamldoc} "$srcdir"

  # Remove duplicate files installed by the OCaml bindings
  rm "$pkgdir"/usr/{lib/llvm/libllvm*,share/doc/llvm/ocamldoc.tar.gz}

  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/lib/llvm/*.a

  # Fix libdir in llvm-config (http://bugs.archlinux.org/task/14487)
  sed -i 's:\(ABS_RUN_DIR/lib\):\1/llvm:' \
         "$pkgdir/usr/bin/llvm-config" || return 1

  # Get rid of example Hello transformation
  rm "$pkgdir"/usr/lib/llvm/*LLVMHello.*

  install -D -m644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_llvm-ocaml() {
  pkgdesc="OCaml bindings for LLVM"
  depends=("llvm=$pkgver" 'ocaml')

  cd "$srcdir/llvm-$pkgver"

  install -d "$pkgdir"/{usr/lib,usr/share/doc/llvm}
  mv "$srcdir/ocaml" "$pkgdir/usr/lib"
  mv "$srcdir/ocamldoc" "$pkgdir/usr/share/doc/llvm"

  # Remove execute bit from static libraries
  chmod -x "$pkgdir"/usr/lib/ocaml/libllvm*.a

  install -D -m644 LICENSE.TXT "$pkgdir/usr/share/licenses/llvm-ocaml/LICENSE"
}

package_clang() {
  pkgdesc="C language family frontend for LLVM"
  url="http://clang.llvm.org/"
  depends=('gcc-libs')

  # Fix installation path for clang docs
  sed -i 's:$(PROJ_prefix)/share/doc/llvm:$(PROJ_prefix)/share/doc/clang:' \
         "$srcdir/llvm-$pkgver/Makefile.config" || return 1

  cd "$srcdir/llvm-$pkgver/tools/clang"
  make DESTDIR="$pkgdir" install || return 1

  # Revert the path change in case we want to do a repackage later
  sed -i 's:$(PROJ_prefix)/share/doc/clang:$(PROJ_prefix)/share/doc/llvm:' \
         "$srcdir/llvm-$pkgver/Makefile.config" || return 1

  install -D -m644 LICENSE.TXT "$pkgdir/usr/share/licenses/clang/LICENSE"
}

package_clang-analyzer() {
  pkgdesc="A source code analysis framework"
  url="http://clang-analyzer.llvm.org/"
  depends=("clang=$pkgver" 'python')

  cd "$srcdir/llvm-$pkgver/tools/clang"

  install -d "$pkgdir"/usr/{bin,lib/clang-analyzer}
  install utils/{scan-build,ccc-analyzer} "$pkgdir/usr/lib/clang-analyzer"
  install -m644 utils/{scanview.css,sorttable.js} "$pkgdir/usr/lib/clang-analyzer"
  ln -s /usr/lib/clang-analyzer/scan-build "$pkgdir/usr/bin"
  ln -s /usr/lib/clang-analyzer/scan-view "$pkgdir/usr/bin"
  cp -r tools/scan-view/* "$pkgdir/usr/lib/clang-analyzer"

  # Compile Python scripts
  python -m compileall "$pkgdir/usr/lib/clang-analyzer" || return 1
  python -O -m compileall "$pkgdir/usr/lib/clang-analyzer" || return 1

  install -D -m644 LICENSE.TXT "$pkgdir/usr/share/licenses/clang-analyzer/LICENSE"
}

# vim:set ts=2 sw=2 et:
