# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-eventlet
pkgname=('python-eventlet' 'python2-eventlet')
_pyname=eventlet
pkgver=0.18.1
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='http://eventlet.net'
arch=('any')
license=('MIT')
makedepends=('python-greenlet' 'python-setuptools' 'python-sphinx' 'python2-greenlet' 'python2-setuptools' 'python2-sphinx')
#TODO: add 'python-dnspython' if test is fixed
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq'
              'python2-psycopg2' 'python2-nose' 'python2-pyopenssl' 'python2-httplib2' 'python2-mock' 'python2-pyzmq' 'python2-dnspython')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/eventlet/eventlet/archive/v${pkgver}.tar.gz)
sha512sums=('8772ed2c8ce8b9e6b380f4093b622d9bdf134a5b68e1ceae194e5fa37083e9c7b13ec6c38fdaddac8c435e613ca26969f9d0ad569e924d2766f6da36a6ca93c7')

prepare() {
  (cd ${_pyname}-${pkgver}
    sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i tests/*_test.py
  )
  cp -a ${_pyname}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${_pyname}-${pkgver}-py2/examples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C doc text
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C doc text
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests -sv tests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2 -sv tests
  )
}

package_python-eventlet() {
  depends=('python' 'python-greenlet')
  optdepends=('python-psycopg2: non-blocking PostgreSQL support'
              'python-pyopenssl: non-blocking SSL support'
              'python-httplib2: non-blocking HTTP support'
              'python-pyzmq: non-blocking ZeroMQ support'
              'python-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-eventlet() {
  depends=('python2' 'python2-greenlet')
  optdepends=('python2-psycopg2: non-blocking PostgreSQL support'
              'python2-pyopenssl: non-blocking SSL support'
              'python2-httplib2: non-blocking HTTP support'
              'python2-pyzmq: non-blocking ZeroMQ support'
              'python2-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
