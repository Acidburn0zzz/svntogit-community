# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Marco Ferragina <marco.ferragina@gmail.com>

pkgname=cgmail
pkgver=0.6.2
pkgrel=2
pkgdesc="An email checker and notifier applet for gmail"
arch=('any')
url="http://cgmail.tuxfamily.org/"
license=('GPL')
depends=('dbus-python' 'gnome-python-desktop' 'libwnck' 'python2' 'python-feedparser' 'python-notify' 'pyxdg')
makedepends=('setuptools' 'python-distutils-extra' 'libcanberra')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/0.6/$pkgver/+download/$pkgname-$pkgver.tar.gz)
sha256sums=('eda5059d8e29ccd6d0967499c031bc86ddab74a26e11a6f9b90403214e01f2de')

build() {
  cd "$srcdir/$pkgname-$pkgver"

# python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done
  sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' waf

  ./waf configure --prefix=/usr
  ./waf build ${MAKEFLAGS}
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  ./waf install --destdir="${pkgdir}"

  #chmod 644 "$pkgdir"/usr/share/cGmail/cgmailservice.desktop
}
