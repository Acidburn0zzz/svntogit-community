# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=conmon
pkgver=1.13.0+9+8fba206
pkgrel=1
pkgdesc='OCI container runtime monitor'
arch=(x86_64)
url='https://github.com/containers/conmon'
license=(APACHE)
depends=(glibc glib2)
makedepends=(git)
_commit=8fba206232c249a8fc4e2fac1469fb2fddbf5cf7  # master
source=("git+https://github.com/containers/conmon#commit=${_commit}")
md5sums=('SKIP')

pkgver() {
  cd conmon
  printf "1.13.0+%s+%s" $(git rev-list --count @) $(git rev-parse --short HEAD)
}

build() {
  cd conmon
  make PREFIX=/usr LIBEXECDIR=/usr/lib DESTDIR="$pkgdir"
}

package() {
  cd conmon
  make PREFIX="$pkgdir/usr" LIBEXECDIR="$pkgdir/usr/lib" install
}
