# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Remi Gacogne <rgacogne@archlinux.org>

pkgname=getdns
pkgver=1.2.0
pkgrel=1
pkgdesc="A modern asynchronous DNS API"
arch=('i686' 'x86_64')
url="http://getdnsapi.net"
license=('BSD')
depends=('libbsd' 'libev' 'libevent' 'libidn' 'libuv' 'libyaml' 'unbound')
source=("${url}/dist/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('06e6494b5d8b9404f439d5a98a3ab8f1f4b3557fb7aa3db005b021a6289b4229' 'SKIP')
validpgpkeys=('DC34EE5DB2417BCC151E5100E5F8F8212F77A498') # Willem Toorop <willem@nlnetlabs.nl>

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-rpath \
        --enable-static=no \
        --with-libev \
        --with-libevent \
        --with-libuv
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    install -d "${pkgdir}"/etc/unbound/
    ln -sf /etc/trusted-key.key "${pkgdir}"/etc/unbound/getdns-root.key
}
