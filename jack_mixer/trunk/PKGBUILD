# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=15
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject' 'python-xdg')
makedepends=('cython' 'glib2' 'jack2' 'meson' 'python-docutils')
optdepends=('new-session-manager: for session management')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver}.tar.gz")
sha512sums=('58e9d218902b6deb8cf6a023cbaa2b5dc3f492eaadb619f7002eb8db8eff5a20a530e932c1482e35d1d6b2c53646161a9968a62c6f9c8d47beaa3254716232d1')
b2sums=('e8260066459a7ef1e8743a915e370fe398f0f74f7ecfdae920cdd020630e4881ec507be8fa884fbfb84d88373ab8e726cc27c7b9fe275a00b133671396644916')

prepare() {
  mv -v "$pkgname-release-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
