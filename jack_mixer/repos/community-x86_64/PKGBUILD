# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=15.1
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject' 'python-xdg')
makedepends=('cython' 'glib2' 'jack2' 'meson' 'python-docutils')
optdepends=('new-session-manager: for session management')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver//./-}.tar.gz")
sha512sums=('c94f3b9baad57dce7ab952127d7f527d963e7fec3efc3fa12d21c22822578c11f602db86d0a11da1a51f8457bb15bc1d75846903bf8703d56a052c76ab9add03')
b2sums=('a5f4abc597ffb275658fab850549f0f702c6286cd4f24dd240ce7d244575889641aa73bab91ba6c77cc6289cdce00be69be78df1c5136fd51414084f024d9446')

prepare() {
  mv -v "$pkgname-release-${pkgver//./-}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  meson test -C build
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
