# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>

pkgname=mailnag-gnome-shell
pkgver=3.38.1
pkgrel=2
pkgdesc='Mailnag GNOME Shell extension'
arch=('x86_64')
url='https://github.com/pulb/mailnag-gnome-shell'
license=('GPL')
depends=('folks' 'gnome-shell' 'mailnag')
makedepends=('vala')
source=("https://github.com/pulb/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('6d3c9318270f2e1b397c648306cdbe6d5041b69a9de5db7155d3ec31ecef40d2')

prepare() {
  cd $pkgname-$pkgver
  # Pass flags
  sed -i 's/CC = valac/CC = valac $(foreach w,$(CFLAGS) $(LDFLAGS),-X $(w))/' Makefile
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir"/usr install
}
