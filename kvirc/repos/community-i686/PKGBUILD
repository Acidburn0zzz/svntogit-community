# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=kvirc
pkgver=4.0.4
pkgrel=5
pkgdesc="Qt-based IRC client"
url="http://www.kvirc.net"
depends=('python2' 'qt' 'libxss' 'qtwebkit')
arch=(i686 x86_64)
makedepends=('cmake')
license=('GPL')
options=("!libtool")
install=kvirc.install
source=(ftp://ftp.kvirc.de/pub/kvirc/$pkgver/source/$pkgname-$pkgver.tar.bz2
        kvirc-4.0.4-gcc-4.7.0.patch)
md5sums=('26fd24979ae4a29bac9309b3b0c98788'
         '863e33f5b38f0b5124144e9a8b640e42')

build() {
  cd $srcdir

  # Fix build with GCC 4.7
  patch -d "$pkgname-$pkgver" -Np4 -i "$srcdir/kvirc-4.0.4-gcc-4.7.0.patch"

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DWANT_KDE4=OFF \
    -DWANT_PERL=ON \
    -DWANT_PHONON=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=$pkgdir install
}
