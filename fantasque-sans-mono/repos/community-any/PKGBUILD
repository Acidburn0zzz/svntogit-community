# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: George Rawlinson <george@rawlinson.net.nz>

pkgbase=fantasque-sans-mono
pkgname=('otf-fantasque-sans-mono' 'ttf-fantasque-sans-mono')
pkgver=1.7.2
pkgrel=2
pkgdesc='Font family with a great monospaced variant for programmers'
arch=('any')
url=https://github.com/belluzj/fantasque-sans
license=('custom:OFL')
depends=('fontconfig' 'xorg-font-utils')
source=("$pkgbase-$pkgver.tar.gz::$url/releases/download/v$pkgver/FantasqueSansMono-Normal.tar.gz")
sha512sums=('f854de5a5e0464d7f69b484c4ae0f59cfdcaa65d357b9935eda1df8cb90781f78c6b6ab0a96ac5099a6464a52de14cf26630d3db5dad8228e86e44033b32c228')

package_otf-fantasque-sans-mono() {
  pkgdesc='OTF font family with a great monospaced variant for programmers'
  install -Dm644 -t "$pkgdir"/usr/share/fonts/OTF OTF/*.otf
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname[0]}/LICENSE
}

package_ttf-fantasque-sans-mono() {
  pkgdesc='TTF font family with a great monospaced variant for programmers'
  install -Dm644 -t "$pkgdir"/usr/share/fonts/TTF TTF/*.ttf
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname[1]}/LICENSE
}
