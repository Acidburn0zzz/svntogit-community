# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-digitalocean
pkgver=0.40.1
pkgrel=1
pkgdesc="DigitalOcean DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-digitalocean' 'python-mock' 'python-setuptools'
         'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('f763423113fbc4d0a9b5ed0dcccfaaefda0f194df22f7a131b3c6f36133e20dc197a2736e6edf4ec4f9a3614e0e280b17350b6e052be0cbdeaf6704b88ece4b2'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
