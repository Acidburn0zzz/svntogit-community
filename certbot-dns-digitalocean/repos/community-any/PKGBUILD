# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-digitalocean
pkgver=0.15.0
pkgrel=1
pkgdesc="DigitalOcean DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" "python-acme=$pkgver" 'python-digitalocean' 'python-mock'
         'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc}
        python3.patch)
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('5ebb5c1f291a03afb85fb5f45ec53f5795af8f5f28954be4e8c25624e28089c178f8dba26e5d1a8cabdffcd85629cb4b2410721b0aa4846835e52a9bc2f53699'
            'SKIP'
            '7d61ce1ac31939ce5f0ea78ef45f74e6e22bc2a5c1348c98a7b1c9f7083eddd0a3448dec9c2d6ef9ea14e763d3fc6672e882bf1460e97770fba0e928ebc52266')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p2 -i ../python3.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
