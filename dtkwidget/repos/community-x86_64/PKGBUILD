# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.9.1
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz")
sha512sums=('18c0a5e1cf10396f67d0058db26f39691d8c8282b5d2f714908d63ef52c76681dbb64dd54e4cb5bd4b7620c2add7ab635eabf3f5db0ee6c6852e05f558f7de39')

build() {
  cd dtkwidget-$pkgver
  LDFLAGS=${LDFLAGS/,-z,now/}
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
