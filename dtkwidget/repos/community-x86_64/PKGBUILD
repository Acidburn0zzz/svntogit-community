# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.8.1.20180709
_commit=870cf29cea6af3bd9fdfab6ccf2553714679a413
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$_commit.tar.gz")
sha512sums=('7dc8ac7e1adb89864825e1b8a9b20fa3128278ec260f4986030b0d598f190db0c23a3a9a859a254b8978f1e73013dfc54ce4537763dea47941b0e17663f0dd61')

prepare() {
  mv dtkwidget-{$_commit,$pkgver}
}

build() {
  cd dtkwidget-$pkgver
  LDFLAGS=${LDFLAGS/,-z,now/}
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
