# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.8
pkgrel=3
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
options=('debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dtkwidget/commit/02f18f91d0582d1df1cdc47167d65baee01332c2.patch
        https://github.com/linuxdeepin/dtkwidget/commit/5b0aee6d108309a4aba12ffd7c61218ae05ff005.patch)
sha512sums=('31bfd871ea275254e2a36955c0719b33dc0c0f8c4778aaa9cfa79319c39a2c576929f708d952f4b0174b741630c70514f4027ca4e72ea797dd588ee40edf6ab3'
            '9120a74957d30733b495ec23c02cbe22cc0f31cd9ddd35da56f4259335662e320e75737a6f30ab45a5a73040fb62c67d8bbc55285011af38ad000740cfcd73cb'
            '4a95a8338366887ee268677cb366a416b9b68c4595a0ae697b55939915b8a16a5ac3e8eb6e5bacecb037dc1eee1fd548f833c20c48a2edf1d0158119b5686927')

prepare() {
  cd dtkwidget-$pkgver
  patch -p1 -i ../02f18f91d0582d1df1cdc47167d65baee01332c2.patch
  patch -p1 -i ../5b0aee6d108309a4aba12ffd7c61218ae05ff005.patch
  sed -i 's/x11/x11 xi/' src/widgets/private/keyboardmonitor/keyboardmonitor.pri
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
