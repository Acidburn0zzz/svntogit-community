# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.2.2
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
         dtkwidget-qt5.15.patch)
sha512sums=('f99baf8f6e9fbde675a7c0a57537ff48b183947e8cc2e199cdd3592250931bfdf67ab601dacdac9656525b0f2d7f8fc921153efc121351ce5d0afab561c5e8d2'
            'df6ad678c1dc198516692ff230bfda59a9e31146edcac03be7e5f7b05d67c038435ce544216edec7449cf1971a68275dd4d8a897333a9dc723f16403895c419a')

prepare() {
  patch -d dtkwidget-$pkgver -p1 -i ../dtkwidget-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
