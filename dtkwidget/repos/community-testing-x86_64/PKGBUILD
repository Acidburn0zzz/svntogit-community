# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.5.3
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz")
sha512sums=('275b9c902874dbf34505d2789e800787020dcd0808fa5256b45c58b32e9ab933eb28261660238751743694e82e771206d8d1084cc9fdd56d5b3d5042dc634f7d')

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr DBUS_VERSION_0_4_2=YES
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
