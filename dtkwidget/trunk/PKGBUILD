# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.14
pkgrel=3
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'qt5-multimedia' 'qt5-svg' 'qt5-x11extras'
         'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
        dtkwidget-qt5.13.patch)
sha512sums=('0250ee77234bb0487420a1176e08c27b25f674137913e656048b66502810a20bbacd3180272400a5bb3fb30bd969bc6d4236d3178edc42e2fd1270b5e194a713'
            '4b2f4373bb55ed38ebeee1345213a9c3b3e1f97a762b955a4b96baae41ed0da35c901d65157113f914ef808af370d0490c83e2e595b6b44010b5297063c8eeaa')

prepare() {
  cd dtkwidget-$pkgver
  patch -p1 -i ../dtkwidget-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd dtkwidget-$pkgver
  LDFLAGS=${LDFLAGS/,-z,now/}
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
