# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: minder

pkgbase=python-pillow
pkgname=(python-pillow python2-pillow)
_appname=Pillow
pkgver=2.1.0
pkgrel=2
pkgdesc="Python Imaging Library (PIL) fork. Python3 version."
arch=('i686' 'x86_64')
url="http://python-imaging.github.io/"
license=('BSD')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/P/$_appname/$_appname-$pkgver.zip")
md5sums=('ec630d8ae15d4a3c4ae7b7efdeac8200')

build() {
  cd "$srcdir"
  cp -r "$srcdir/$_appname-$pkgver" "$srcdir/${_appname}2-$pkgver"
}  

package_python-pillow() {
  depends=('python')
  makedepends=('python-setuptools')
  cd "$srcdir/$_appname-$pkgver"
  python3 setup.py install --root="$pkgdir/" --optimize=0
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}"
  done
}

package_python2-pillow() {
  pkgdesc="Python Imaging Library (PIL) fork. Python2 version."
  depends=('python2')
  makedepends=('python2-setuptools')
  provides=('python-imaging' 'python2-imaging')
  conflicts=('python-imaging' 'python2-imaging')
  replaces=('python2-imaging')
  cd "$srcdir/${_appname}2-$pkgver"
  python2 setup.py install --root="$pkgdir/" --optimize=0
  install -Dm644 docs/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # clean up bins
  cd "$pkgdir/usr/bin"
  for f in *.py; do
    mv "$f" "${f%.py}2"
  done
}

