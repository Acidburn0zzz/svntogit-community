# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-deck
_name=deck
pkgver=1.3.2
pkgrel=1
# upstream doesn't adhere to semver:
# https://github.com/nextcloud/deck/issues/1197
epoch=1
pkgdesc="Kanban style organization tool aimed at personal planning and project organization"
arch=('any')
url="https://github.com/nextcloud/deck"
license=('AGPL3')
groups=('nextcloud-apps')
depends=('nextcloud')
makedepends=('xmlstarlet')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}.tar.gz")
sha512sums=('8986f40966c612ad71c422b099dad60776efeae93a9310ead2d585e84025410c2a5dcc71e322fe041cca3f374e0fb1f834440b431f15159cc68e29e9c149a03c')
b2sums=('babbec1dd61d157c58a096b7e6eef269ff467c11213782ed60e1ce76ed3e96e6c2b1a9376c3f5f34ad3b32cd06bd6c28fbaf592a9ea3f34c2e282305013d9c3f')

package() {
  depends=("nextcloud>="$(xmlstarlet sel -t -v '/info/dependencies/nextcloud/@min-version' deck/appinfo/info.xml)
           "nextcloud<="$(xmlstarlet sel -t -v '/info/dependencies/nextcloud/@min-version' deck/appinfo/info.xml))
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
