# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-deck
_name=deck
pkgver=1.5.0
pkgrel=1
# upstream doesn't adhere to semver:
# https://github.com/nextcloud/deck/issues/1197
epoch=1
pkgdesc="Kanban style organization tool aimed at personal planning and project organization"
arch=('any')
url="https://github.com/nextcloud/deck"
license=('AGPL3')
groups=('nextcloud-apps')
depends=('nextcloud>21' 'nextcloud<23')
makedepends=('ripgrep' 'yq')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}.tar.gz")
sha512sums=('152f032f8d9049e812f29ebd1e5b7a539fe729d8075a22555955fd8656fb4b4981d893d0f2aafc32080e32d4b299b27479717a0ceeabf0546b57b54ecf01c931')
b2sums=('519c40837dcbcf382faf691d6b551f20ea36d8b310386500548e21e7e64545d0192b48a3265ba32b71de5b45295df8994799fc0e1064eefdc9a9d7c67d8ebb00')

check() {
  local _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
