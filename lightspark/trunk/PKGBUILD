# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.4.5.1
pkgrel=6
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('LGPL3')
conflicts=('lightspark-git')
#depends=('mesa' 'ftgl' 'sdl' 'gtk2' 'curl' 'zlib' 'ffmpeg' 'glew' 'pcre' 'libpulse' 'libffi' 'libxml++' 'boost-libs')
depends=('mesa' 'ftgl' 'sdl' 'gtk2' 'curl' 'zlib' 'ffmpeg' 'glew' 'pcre' 'libpulse' 'libffi' 'boost-libs')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost')
# add libxml++ makedepends
makedepends+=('pkgconfig' 'glibmm-docs' 'libxml2>=2.7.7' 'glibmm>=2.24.2')
optdepends=('gnash-gtk: fallback support')
install="${pkgname}.install"
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver:0:5}/+download/${pkgname}-${pkgver}.tar.gz"
        'http://ftp.gnome.org/pub/GNOME/sources/libxml++/2.33/libxml++-2.33.1.tar.bz2')
md5sums=('3ef25cdf4fecd9b7c1820c138893ce4f'
         '90919b5a5e92381722ef004898eba343')

build() {
  # statically link against libxml++ 2.33.1 because of constant crashes
  cd libxml++-2.33.1
  ./configure --prefix=${srcdir}/iamsorry --enable-static --disable-shared --disable-documentation --disable-examples
  make
  make install

  export PKG_CONFIG_PATH=${srcdir}/iamsorry/lib/pkgconfig/
  
  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    ../${pkgname}-${pkgver}
    #-DXMLPP_INCLUDE_DIRS=${srcdir}/iamsorry/include/libxml++-2.6/ -I/usr/include/glibmm-2.4/ \
    #-DGLEW_INCLUDE_DIR=/usr/include/glibmm-2.4/ \
    #-DFTGL_INCLUDE_DIR=/usr/lib/glibmm-2.4/include/ \
    #-DPCRECPP_INCLUDE_DIR=${srcdir}/iamsorry/lib/libxml++-2.6/include/ \

  make VERBOSE=1
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
