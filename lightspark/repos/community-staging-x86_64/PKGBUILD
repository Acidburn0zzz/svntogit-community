# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.5.4.1
pkgrel=1
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('LGPL3')
conflicts=('lightspark-git')
depends=('ftgl' 'ffmpeg' 'glew' 'curl' 'boost-libs' 'gtkglext' 'libxml++' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost' 'fontconfig')
optdepends=('gnash-gtk: fallback support')
install="lightspark.install"
source=(http://launchpad.net/lightspark/trunk/lightspark-${pkgver}/+download/lightspark-${pkgver}.tar.gz{,.asc})
md5sums=('cf24336e42ad356f1026b7071b110a88'
         '9bd295cd6d56bfee6370bbaeef034d4c')

build() {
  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    ../lightspark-${pkgver}

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
