# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.5.0rc1
pkgrel=2
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('LGPL3')
conflicts=('lightspark-git')
depends=('mesa' 'sdl' 'gtk2' 'curl' 'zlib' 'ffmpeg' 'glew' 'pcre' 'libpulse' 'libffi' 'boost-libs' 'glibmm' 'gtkglext' 'desktop-file-utils' 'libxml++')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost')
optdepends=('gnash-gtk: fallback support')
install='lightspark.install'
source=("http://launchpad.net/lightspark/trunk/lightspark-0.5.0/+download/lightspark-0.5.0~rc1.tar.gz"
        'git-fixes-set-sys-in-destroystream-class.diff')
md5sums=('bf668cf6178fbbb75a7898bd6c6dd1e5'
         '57b1b5a975f5e2518c2a96d61b925f41')
_pkgver='0.5.0~rc1'

build() {
  cd lightspark-${_pkgver}
  patch -Np1 -i ${srcdir}/git-fixes-set-sys-in-destroystream-class.diff
  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    ../lightspark-${_pkgver}

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
