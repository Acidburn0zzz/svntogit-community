# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.5.2.1
pkgrel=2
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('LGPL3')
conflicts=('lightspark-git')
depends=('mesa' 'ftgl' 'sdl' 'gtk2' 'curl' 'zlib' 'ffmpeg' 'glew' 'pcre' 'libpulse' 'libffi' 'boost-libs' 'glibmm' 'gtkglext' 'desktop-file-utils' 'libxml++' 'libxml2')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost' 'fontconfig')
optdepends=('gnash-gtk: fallback support')
install="lightspark.install"
source=("http://launchpad.net/lightspark/trunk/lightspark-${pkgver}/+download/lightspark-${pkgver}.tar.gz"
        'llvm29.diff')
md5sums=('ac2233a83e8bf0b305563bbce322cd76'
         '5f717d1d9ead478184ca68f6ac068099')

build() {
  cd lightspark-${pkgver}
  patch -Np0 -i ${srcdir}/llvm29.diff
  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    ../lightspark-${pkgver}

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
