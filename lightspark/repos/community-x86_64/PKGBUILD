# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lightspark
pkgver=0.4.7.1
pkgrel=1
pkgdesc='An alternative Flash Player for Linux.'
arch=('i686' 'x86_64')
url='http://lightspark.sourceforge.net'
license=('LGPL3')
conflicts=('lightspark-git')
# add libxml++ back after repos update it
depends=('mesa' 'ftgl' 'sdl' 'gtk2' 'curl' 'zlib' 'ffmpeg' 'glew' 'pcre' 'libpulse' 'libffi' 'boost-libs' 'glibmm' 'gtkglext' 'desktop-file-utils')
makedepends=('cmake' 'nasm' 'xulrunner' 'llvm' 'glproto' 'boost' 'pkgconfig' 'libxml2>=2.7.7' 'fontconfig')
optdepends=('gnash-gtk: fallback support')
install="${pkgname}.install"
source=("http://launchpad.net/${pkgname}/trunk/${pkgname}-${pkgver:0:5}/+download/${pkgname}-${pkgver}.tar.gz"
        'http://ftp.gnome.org/pub/GNOME/sources/libxml++/2.33/libxml++-2.33.1.tar.bz2')
md5sums=('fee5cd52a8a23e858979a8f4ec778266'
         '90919b5a5e92381722ef004898eba343')

build() {
  # statically link against libxml++ 2.33.1 because of constant crashes
  # DIRTEH HACK -- reading below code may cause eye leakage
  export CFLAGS="-fPIC ${CFLAGS}"
  export CXXFLAGS="-fPIC ${CXXFLAGS}"
  # need to add fPIC since liblightspark itself is a shared object
  cd libxml++-2.33.1
  ./configure \
    --prefix=${srcdir}/iamsorry \
    --enable-static \
    --disable-shared \
    --disable-documentation \
    --disable-examples
  make
  make install

  export PKG_CONFIG_PATH=${srcdir}/iamsorry/lib/pkgconfig/
  
  cd ${srcdir}

  sed -i '213iLINK_DIRECTORIES(${LIBXMLPP_LIB_DIR})' ${srcdir}/${pkgname}-${pkgver}/CMakeLists.txt

  rm -rf build
  mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCOMPILE_PLUGIN=1 \
    -DCMAKE_BUILD_TYPE=Release \
    -DGNASH_EXE_PATH=/usr/bin/gtk-gnash \
    -DLIBXMLPP_LIB_DIR=${srcdir}/iamsorry/lib \
    ../${pkgname}-${pkgver}

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install
}
