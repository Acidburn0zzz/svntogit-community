# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-go-dbus-factory
pkgver=0.1.0
pkgrel=1
pkgdesc='GO DBus factory for DDE'
arch=('any')
url="https://github.com/linuxdeepin/go-dbus-factory"
license=('GPL3')
depends=('deepin-go-lib')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-dbus-factory/archive/$pkgver.tar.gz")
sha512sums=('9870cd57be54a4b8e9a7cb4d67cabfac917c1e4793d4949b69997dedcbc0a3f9160495383e49183ee75864198ba0ae16006ab3c02d8238c79861a7b24ff8c0c6')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  ln -sf "$srcdir"/go-dbus-factory-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-dbus-factory

  go get golang.org/x/net/context
}

build() {
  cd go-dbus-factory-$pkgver
  make bin
  ./gen.sh
}

package() {
  cd go-dbus-factory-$pkgver
  install -dm755 "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory
  cp -a com.* org.* net.* object_manager "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory/
}
