# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-go-dbus-factory
pkgver=0.0.3
pkgrel=1
pkgdesc='GO DBus factory for DDE'
arch=('any')
url="https://github.com/linuxdeepin/go-dbus-factory"
license=('GPL3')
depends=('deepin-go-lib')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-dbus-factory/archive/$pkgver.tar.gz")
sha512sums=('29fdd954e2c7f8e58166f8ab47fb8d84c790deb6797cd3e9c7ebd1a81836d640a04e38f7ffefa7f3fcab1dad5a71e9475d825d6d6be7723f94afde8c1d764764')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  ln -sf "$srcdir"/go-dbus-factory-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-dbus-factory
}

build() {
  cd go-dbus-factory-$pkgver
  make bin
  ./gen.sh
}

package() {
  cd go-dbus-factory-$pkgver
  install -dm755 "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory
  cp -a com.* org.* net.* object_manager "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory/
}
