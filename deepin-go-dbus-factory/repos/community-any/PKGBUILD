# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-go-dbus-factory
pkgver=0.0.6
pkgrel=1
pkgdesc='GO DBus factory for DDE'
arch=('any')
url="https://github.com/linuxdeepin/go-dbus-factory"
license=('GPL3')
depends=('deepin-go-lib')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-dbus-factory/archive/$pkgver.tar.gz")
sha512sums=('49cab31854ee6303fb388e362084920e40c25c9b2307e7ee77524934cf383248ef72dbcadb7de73c49243a3d4bbf35580df472619e816f79b83a488f2cbae58c')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  ln -sf "$srcdir"/go-dbus-factory-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-dbus-factory

  go get golang.org/x/net/context
}

build() {
  cd go-dbus-factory-$pkgver
  make bin
  ./gen.sh
}

package() {
  cd go-dbus-factory-$pkgver
  install -dm755 "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory
  cp -a com.* org.* net.* object_manager "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory/
}
