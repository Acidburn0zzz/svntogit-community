# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.3.0
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(go-pie dep git)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/cli/v$pkgver.zip)
sha256sums=('39ad8ebd1e7c3f30db0bf754a2ef3bafa7f0cd47c1c30906ec7d689632d61daa')

prepare() {
  export GOPATH="$srcdir/gopath"
  mkdir -p gopath/src/github.com/tsenart
  cp -r vegeta-cli-v$pkgver/ gopath/src/github.com/tsenart/vegeta

  cd gopath/src/github.com/tsenart/vegeta
  dep ensure -v
}

build() {
  cd gopath/src/github.com/tsenart/vegeta
  #go build -v -X main.Version=$pkgver ./...
  CGO_ENABLED=0 go install -v -ldflags "-X main.Version=$pkgver -X main.Date=`date +'%FT%TZ%z'`" ./...
}

check() {
  cd gopath/src/github.com/tsenart/vegeta
  # currently tests fail due to https://github.com/tsenart/vegeta/issues/396
  # go test ./...
}

package() {
  install -Dm755 "gopath/bin/vegeta" "$pkgdir/usr/bin/vegeta"
}
