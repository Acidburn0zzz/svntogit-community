# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=12.8.1
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
depends=(glibc)
makedepends=(go)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/v$pkgver.zip)
sha512sums=('28697a28f56b1ee7e5b353eb43eac6b504a3c11624a032d2b574f1e0a168bb5380704a2b9c50f97f33c5f07a6e056b4adf29b492c9ebe607d259a882207be4f4')

build() {
  cd vegeta-$pkgver
  BUILDDATE=`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}" +'%FT%TZ%z'`
  go build -ldflags "-X main.Version=$pkgver -X main.Date='$BUILDDATE'"
}

check() {
  cd vegeta-$pkgver
  go test ./...
}

package() {
  cd vegeta-$pkgver
  install -Dm755 vegeta "$pkgdir/usr/bin/vegeta"
}
