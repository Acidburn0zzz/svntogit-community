# Maintainer: Anatol Pomozov

pkgname=vegeta
pkgver=11.3.0
pkgrel=1
pkgdesc="HTTP load testing tool"
arch=(x86_64)
url="https://github.com/tsenart/vegeta"
license=(MIT)
makedepends=(go-pie dep git)
source=(vegeta-$pkgver.zip::https://github.com/tsenart/vegeta/archive/cli/v$pkgver.zip)
sha256sums=('ce2018ee047e3fdd9a0f01b7fc144d24e20db6386be5671385d84383e59c97c5')

prepare() {
  export GOPATH="$srcdir/gopath"
  mkdir -p gopath/src/github.com/tsenart
  cp -r vegeta-cli-v$pkgver/ gopath/src/github.com/tsenart/vegeta

  cd gopath/src/github.com/tsenart/vegeta
  dep ensure -v
}

build() {
  cd gopath/src/github.com/tsenart/vegeta
  #go build -v -X main.Version=$pkgver ./...
  CGO_ENABLED=0 go install -v -ldflags "-X main.Version=$pkgver -X main.Date=`date +'%FT%TZ%z'`" ./...
}

check() {
  cd gopath/src/github.com/tsenart/vegeta
  go test ./...
}

package() {
  install -Dm755 "gopath/bin/vegeta" "$pkgdir/usr/bin/vegeta"
}
