# $Id: PKGBUILD 72029 2012-06-06 11:58:40Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=lib32-libphobos-ldc
pkgver=20121225
pkgrel=1
pkgdesc="libphobos for ldc (32-bit)"
arch=('x86_64')
url="http://www.dsource.org/projects/ldc"
license=('BSD')
depends=('llvm' 'libconfig')
makedepends=('git' 'cmake')
source=(fix-llvm-IRBuilder.h-include-when-using-LLVM-3.2.patch)
md5sums=('0dacd8e4b8f88030c107dbb2a35bcc42')

_gitroot=git://github.com/ldc-developers/ldc.git
_gitname=ldc
_gitbranch=master

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone --branch "$_gitbranch" "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone --recursive "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  # llvm/Support/IRBuilder.h was relocated in LLVM 3.2
  patch -Np1 -i "$srcdir/fix-llvm-IRBuilder.h-include-when-using-LLVM-3.2.patch"

  mkdir build && cd build
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINCLUDE_INSTALL_DIR=/usr/include/d/std-ldc \
      -DMULTILIB=ON \
      ..
  make
}

package() {
  cd "$srcdir/$_gitname-build"

  cd build
  make DESTDIR=$pkgdir install
  cd ..

  # We don't want anything but the 32-bit libs
  rm -rf $pkgdir/usr/{share,lib,include,bin} $pkgdir/etc

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
