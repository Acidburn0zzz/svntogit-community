# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.14
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt4 lua51 boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('a0bf796e952f5e1ccf061704cd774047'
         'SKIP')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make QMAKE=qmake-qt4
  make QMAKE=qmake-qt4 gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua51')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (QT version)"
  depends=('qt4' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" QMAKE=qmake-qt4 install-gui
}

# vim:set ts=2 sw=2 et:
