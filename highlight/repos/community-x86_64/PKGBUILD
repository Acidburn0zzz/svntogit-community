# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Jan Fader <jan.fader@web.de>
pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.13
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('i686' 'x86_64')
makedepends=(qt lua51 boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
md5sums=('2b5e217c80c441853360926dbb7777e9'
         'c68f6dd20af61ca1061d910879d910a8')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make
  make gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua51')
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (QT version)"
  depends=('qt' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" install-gui
}

# vim:set ts=2 sw=2 et:
