# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Johannes Wienke <languitar@semipol.de>
pkgname=xsecurelock
pkgver=1.3.1
pkgrel=1
pkgdesc="X11 screen lock utility with security in mind"
arch=('x86_64')
url="https://github.com/google/xsecurelock"
license=('APACHE')
depends=('pam' 'libxcomposite' 'libxmu' 'libxft' 'libxrandr' 'libxss')
optdepends=('mplayer: for the saver_mplayer module'\
            'mpv: for the saver_mpv module'\
            'imagemagick: for the auth_pamtester module'\
            'pamtester: for the auth_pamtester module'\
            'xorg-xset: for the saver_blank module'\
            'xscreensaver: for the saver_xscreensaver module')
source=("https://github.com/google/xsecurelock/archive/v${pkgver}.tar.gz")
sha512sums=('f2981e757b4acf6a3c02e09d523aee655ca3a7142de9ec6136195d6d6bb6f9d8459cc42c4b817d98bd72076e71ef6929fd39022d37a61f17581fe1a2db2698ab')

prepare() {
    cd "${pkgname}-${pkgver}"
    # TODO remove with the next release (https://github.com/google/xsecurelock/issues/59)
    echo 'const char *const git_version = "1.3";' > version.c
}

build() {
    cd "${pkgname}-${pkgver}"
    sh autogen.sh
    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --with-pam-service-name=system-auth
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
