# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=6.1.1
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-prawcore' 'python2-prawcore'
             'python-update-checker' 'python2-update-checker' 'python-pytest-runner'
             'python2-pytest-runner' 'python-websocket-client' 'python2-websocket-client')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-betamax-matchers' 'python2-betamax-matchers')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('280eca18a22a90f2b6e852c19c2d6d7667a902c1ab1aab473e6e1db04dad5e71bc959c86bd93c3f361e2af0b48e6b140b184d57cae4134cf9625eae2e054a09e')

prepare() {
  cp -a praw-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/praw-$pkgver
  python setup.py build

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/praw-$pkgver
  python setup.py pytest

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py pytest
}

package_python-praw() {
  depends=('python-prawcore' 'python-update-checker' 'python-websocket-client')

  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-update-checker' 'python2-websocket-client')

  cd praw-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
