# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-praw
pkgver=6.5.1
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="https://praw.readthedocs.org"
depends=('python-prawcore' 'python-update-checker' 'python-websocket-client')
makedepends=('python-setuptools' 'python-pytest-runner')
checkdepends=('python-mock' 'python-betamax-serializers' 'python-betamax-matchers')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('b1e3ac1ced80093db601d7bd26a3105732afbe2855169e8bdfe7ca34eeebdde0c6b793f1886c58f3ab08cbc112259e9e1e20d83eac7e84e015b43e284415c991')

build() {
  cd praw-$pkgver
  python setup.py build
}

check() {
  cd praw-$pkgver
  python setup.py pytest
}

package() {
  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
