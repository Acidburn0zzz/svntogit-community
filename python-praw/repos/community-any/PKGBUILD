# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=4.5.0
pkgrel=1
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="http://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-prawcore' 'python2-prawcore'
             'python-update-checker' 'python2-update-checker' 'python-pytest-runner'
             'python2-pytest-runner')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-betamax-matchers' 'python2-betamax-matchers')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/praw/archive/v$pkgver.tar.gz")
sha512sums=('5cb28ff562de9d9eecc95c71fa5af4577c014bc0fd56fbd657b2927c731a7fa6557896adaaa170efc81d4f78f85e2044caba055577b0cc55377b220804dc6246')

prepare() {
  sed -e 's/betamax-matchers >=0.3.0, <0.4/betamax-matchers >=0.3.0/' \
      -i praw-$pkgver/setup.py
  cp -a praw-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/praw-$pkgver
  python setup.py pytest

  cd "$srcdir"/praw-$pkgver-py2
  python2 setup.py pytest
}

package_python-praw() {
  depends=('python-prawcore' 'python-update-checker')

  cd praw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-update-checker')

  cd praw-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
