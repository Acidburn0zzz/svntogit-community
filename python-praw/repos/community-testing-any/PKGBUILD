# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-praw
pkgname=('python-praw' 'python2-praw')
pkgver=4.1.0
pkgrel=3
pkgdesc="Python Reddit API Wrapper that allows for simple access to reddit's API"
arch=('any')
license=('GPL')
url="http://praw.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-requests' 'python2-requests' 'python-six' 'python2-six' 'python-prawcore'
             'python2-prawcore' 'python-update-checker' 'python2-update-checker' 'git')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-serializers'
              'python2-betamax-serializers' 'python-pytest' 'python2-pytest'
              'python-betamax-matchers' 'python2-betamax-matchers')
source=("git+https://github.com/praw-dev/praw.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  sed -e 's/mock ==1.0.1/mock/' \
      -e 's/six ==1.10/six >=1.10/' \
      -e 's/pytest ==2.8.7/pytest >=2.8.7/' \
      -e 's/prawcore >=0.5.0, <0.6/prawcore >=0.5.0/' \
      -e 's/betamax-matchers >=0.2.0, <0.3/betamax-matchers >=0.2.0/' \
      -i praw/setup.py
  cp -a praw{,-py2}
}

check() {
  cd "$srcdir"/praw
  python setup.py test

  cd "$srcdir"/praw-py2
  python2 setup.py test
}

package_python-praw() {
  depends=('python-prawcore' 'python-decorator' 'python-requests' 'python-six' 'python-update-checker')

  cd praw
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-praw() {
  depends=('python2-prawcore' 'python2-decorator' 'python2-requests' 'python2-six' 'python2-update-checker')

  cd praw-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
