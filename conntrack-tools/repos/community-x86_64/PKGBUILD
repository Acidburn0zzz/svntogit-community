# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=1.2.1
pkgrel=1
pkgdesc='Userspace tools to interact with the Netfilter connection tracking system'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/conntrack-tools'
license=('GPL2')
depends=('libnetfilter_conntrack' 'libnetfilter_cttimeout')
backup=('etc/conntrackd.conf' 'etc/conf.d/conntrackd')
install=$pkgname.install
source=("http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"
        'conntrackd.rc'
        'conntrackd.conf'
        'conntrackd.service'
        '01-config-file-path.patch')
md5sums=('48d414cb7a354001babf184ae6c16db9'
         '478fe76da0202fcb5249bf65968d1c26'
         'd3e73e81b54e3e8166d357d434680684'
         'bd135eaefb47923bb8e8d26f084d3e9d'
         'e71ff7b8c4997b797cab3eaaccb693f4')

build() {
  cd $pkgname-$pkgver
  for i in "$srcdir"/*.patch; do
    patch -p1 -i "$i"
  done
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  # install binaries
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd
  # initscripts & systemd
  install -D -m 755 conntrackd.rc "$pkgdir/etc/rc.d/conntrackd"
  install -D -m 644 conntrackd.conf "$pkgdir/etc/conf.d/conntrackd"
  install -D -m 644 conntrackd.service \
    "$pkgdir/usr/lib/systemd/system/conntrackd.service"
  # install empty configuration files
  install -D -m 644 /dev/null "$pkgdir/etc/conntrackd.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
