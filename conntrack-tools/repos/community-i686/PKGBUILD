# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=1.4.1
pkgrel=2
pkgdesc='Userspace tools to interact with the Netfilter connection tracking system'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/conntrack-tools'
license=('GPL2')
depends=('libnetfilter_conntrack'
         'libnetfilter_cttimeout'
         'libnetfilter_cthelper'
         'libnetfilter_queue')
backup=('etc/conntrackd.conf')
options=('!libtool')
source=("http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"
        'conntrackd.service'
        '01-config-file-path.patch')
install=$pkgname.install
sha1sums=('6fda497ef1ebd5e4afe01830ece072d2df2fe50f'
          'ab888d59b98b673709d2d89554c668e2bea0cb86'
          'e7607a9ac14dd839f3716ac68d46e19ab26ef112')

build() {
  cd $pkgname-$pkgver
  for i in "$srcdir"/*.patch; do
    patch -p1 -i "$i"
  done
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin
  make
}

package() {
  # install binaries
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd
  # systemd
  install -D -m 644 conntrackd.service \
    "$pkgdir/usr/lib/systemd/system/conntrackd.service"
  # install empty configuration files
  install -D -m 644 /dev/null "$pkgdir/etc/conntrackd.conf"
}

# vim:set ts=2 sw=2 et:
