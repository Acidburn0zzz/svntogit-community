# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Valere Monseur <valere_monseur@hotmail.com>
# Contributor: Alessandro Sagratini <ale_sagra@hotmail.com>
# Contributor: Kevin Edmonds <edmondskevin@hotmail.com>

pkgname=conntrack-tools
pkgver=1.0.1
pkgrel=2
pkgdesc='Userspace tools to interact with the Netfilter connection tracking system'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/conntrack-tools'
license=('GPL2')
depends=('libnetfilter_conntrack')
backup=('etc/conntrackd/conntrackd.conf')
source=("http://www.netfilter.org/projects/$pkgname/files/$pkgname-$pkgver.tar.bz2"
        'conntrackd.rc'
        'conntrackd.conf'
        'conntrackd.service')
md5sums=('8a60f02a177fc31fe40cc992c4de90e2'
         '977dfb8fc58df0c74120c8ac92026a6a'
         '92a7fc6ab5cba5e48cde4cac0ac403b1'
         'bd135eaefb47923bb8e8d26f084d3e9d')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  # install binaries
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd
  # initscripts & systemd
  install -D -m 755 conntrackd.rc "$pkgdir/etc/rc.d/conntrackd"
  install -D -m 644 conntrackd.conf "$pkgdir/etc/conf.d/conntrackd"
  install -D -m 644 conntrackd.service \
    "$pkgdir/usr/lib/systemd/system/conntrackd.service"
  # install empty configuration files
  install -D -m 644 /dev/null "$pkgdir/etc/conntrackd/conntrackd.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
