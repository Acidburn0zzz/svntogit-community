# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=copr-cli
pkgver=1.91
pkgrel=1
pkgdesc="CLI tool to run copr"
url="https://pagure.io/copr/copr"
license=('GPL')
arch=('any')
depends=('python-copr' 'python-humanize' 'python-simplejson' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://pagure.io/copr/copr/archive/copr-cli-$pkgver-1/copr-copr-cli-$pkgver-1.tar.gz")
sha512sums=('614a0fb5adc5b27abeed6957d5c705b2a2a868212275a82203423b8218009250de88ef03ecee306232a588c701faa79e15c32dbe160eea86d9acf3c2408152f4')

build() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py build
}

check() {
  cd copr-copr-cli-$pkgver-1/cli
  python -m pytest
}

package() {
  cd copr-copr-cli-$pkgver-1/cli
  python setup.py install --root="$pkgdir" --optimize=1
}
