# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Aram Drevekenin <aram@poor.dev>

pkgname=bandwhich
pkgver=0.11.0
pkgrel=1
pkgdesc='Terminal bandwidth utilization tool'
url='https://github.com/imsnif/bandwhich'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=(https://github.com/imsnif/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('590308361e131f0675babc7080225bb6a0f75847ce90d7db12fc3d46ed5fc70bc25809668164ab5e2f0f3d60453433b4f551c672aa92e0dad014ed2ecc8e45cd')
b2sums=('1ccc22e5aaea2838d3dc9fcda07bda4806bb95ed8ee83c5513bbc16cbc18fa94360c090099a3378f3878d3afaa1bd2c5d6de469b69dc1b883ad227976e0c656d')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "docs/bandwhich.1" -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
