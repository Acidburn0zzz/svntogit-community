# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.30.3
pkgrel=1
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('c942cdef100d9c23418449d642133a50529595338ecc3c226763d068d0f64b8c')
b2sums=('b18d61e5d067d52a3c14ec05f402218dc3532e02e99ed4904c18f5728635f9b06cfe90d3ee6ab61e4ffaa8318a241dc659cefc3b592070132db9f8daed02bd5f')

prepare() {
  cd $pkgname-$pkgver
  # Avoid installing files directly under site-packages/
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  poetry build --format wheel
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --no-deps \
              --root="$pkgdir" \
              --ignore-installed \
              --no-warn-script-location dist/*.whl

  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s $site_packages/${pkgname//-/_}-$pkgver.dist-info/LICENSE \
    "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
