# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=jedi-language-server
pkgver=0.27.2
pkgrel=1
pkgdesc='Language server for Jedi'
arch=('any')
url=https://github.com/pappasam/jedi-language-server
license=('MIT')
depends=('python-click' 'python-docstring-to-markdown' 'python-jedi' 'python-pygls')
makedepends=('python-pip' 'python-poetry')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('f0f192e2eefd1573f59a9078378f2cddff5788ec177c64701f072b489be74438')
b2sums=('041e68ed0da9e21b2dd775e6d65c093ab349409538e873db3dd91079f5c7879a9673ab8096a27a5e6b556482aad2143da78c1b5f8b62d0c8c74878d4c1697d40')

prepare() {
  cd $pkgname-$pkgver
  # Avoid installing files directly under site-packages/
  sed -i '/include = \["README.md"\]/d' pyproject.toml
}

build() {
  cd $pkgname-$pkgver
  poetry build --format wheel
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps dist/*.whl
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
