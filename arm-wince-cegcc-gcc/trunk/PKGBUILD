# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arm-wince-cegcc-gcc
pkgver=0.59.1
pkgrel=3
_prefix=/opt/cegcc
pkgdesc="CE GCC bundle - cross compilation tools for WinCE"
arch=(i686 x86_64)
license=('GPL')
options=(!libtool)
url="http://cegcc.sf.net"
depends=(arm-wince-cegcc-binutils)
makedepends=(arm-wince-cegcc-newlib arm-wince-cegcc-w32api arm-wince-cegcc-importlibs arm-wince-cegcc-gcc-base)
conflicts=(arm-wince-cegcc-gcc-base)
groups=('cegcc')
source=(http://arch.p5n.pp.ru/~sergej/dl/cegcc-$pkgver.tar.bz2)
md5sums=('5e3c1098abdf8e5db2a3518ee3578b4b')

build() {
  cd $srcdir/cegcc/src

  export PATH=$_prefix/bin:$PATH
  export TARGET=arm-wince-cegcc
  unset CFLAGS
  unset CXXFLAGS
  unset LDFLAGS
  unset MAKEFLAGS

  mkdir -p build-gcc
  cd build-gcc

  [ -f Makefile ] || ../gcc-4.4.0/configure \
	--prefix=${_prefix}      \
	--exec-prefix=${_prefix} \
	--bindir=${_prefix}/bin  \
	--target=$TARGET         \
	--build=$CARCH           \
	--host=$CARCH            \
	--disable-nls            \
	--includedir=${_prefix}/include \
	--with-gcc               \
	--with-ld=/opt/cegcc/bin/arm-wince-cegcc-ld \
	--with-as=/opt/cegcc/bin/arm-wince-cegcc-as \
	--enable-threads=win32   \
	--enable-languages=c,c++ \
	--disable-win32-registry \
	--disable-multilib       \
	--disable-interwork      \
	--without-newlib         \
	--enable-checking        \
	--with-headers           \
	--disable-libssp

  make
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/$_prefix/lib/libiberty.a
  find $pkgdir/${_prefix} -type f -name \*.a -exec arm-wince-cegcc-ranlib {} \;
}
