# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgname=python-gmpy2
pkgver=2.1.0b1
pkgrel=3
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
depends=(python libmpc)
makedepends=(python-setuptools)
source=("$pkgname-$pkgver.tar.gz::https://github.com/aleaxit/gmpy/archive/gmpy2-$pkgver.tar.gz")
sha256sums=('888d3c34a91c2ac068ac5033431c765e04f125e85920bd69d8739fbb62f4279d')

build() {
  cd gmpy-gmpy2-$pkgver
  python setup.py build
}

package() {
  cd gmpy-gmpy2-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}
