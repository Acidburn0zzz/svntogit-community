# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.8.1
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'libxcb' 'qt5-base' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'drumstick' 'qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('320102e7e64b22486153524bfdea2a9898622e1a3e816af553ec4cc69a804748141dfbc291dc80244316bf9caba99d4bbdaaa2cb2b4d201e809cea5a4cbd2a28')
b2sums=('f7617a477c499d4e5ca02d12cf8e481ea55f3cfe2f9cfce071f66842f6ddb1b0296e41a49361aad9b9dce6b5a4ec091a83305a41716c3c96ff2d7694bc06aeea')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libdrumstick-rt.so' 'libdrumstick-widgets.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
