# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Radim Hvizd√°k <hvizdakr at gmail dot com>

pkgname=ibus-hangul
pkgver=1.3.1
pkgrel=1
pkgdesc='Korean input engine for IBus'
arch=('i686' 'x86_64')
url='http://ibus.googlecode.com'
license=('GPL')
depends=('ibus' 'libhangul')
makedepends=('intltool')
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('b71565bba3a1439a47212611b774ecf7')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make NO_INDEX=true DESTDIR="${pkgdir}" install
}
