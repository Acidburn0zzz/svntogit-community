# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwm
pkgver=0.3.3
pkgrel=1
pkgdesc='Deepin Tool Kit Wm'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkwm"
license=('GPL3')
depends=('dtkcore' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwm/archive/$pkgver.tar.gz")
sha512sums=('dbb385e79b320998410e8bdcfab967db6248bceaf56d38935093daf9db33f8a0e2ec3adf634d0d1e50a3080bff933aefe4f5008c5ebee1808c2845298f19f4a7')

prepare() {
  sed -i '/platformsupport-private/d' dtkwm-$pkgver/{src/src.pro,examples/examples.pro}
  sed -i 's/-lXtst//' dtkwm-$pkgver/src/src.pro
}

build() {
  cd dtkwm-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwm-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
