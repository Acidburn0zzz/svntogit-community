# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.2.9.20180409
_commit=b318a6fe0715e22236daa9c5b939128be6f84a90
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-icon-theme' 'deepin-sound-theme')
makedepends=('python')
conflicts=('deepin-artwork-themes')
replaces=('deepin-artwork-themes')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$_commit.tar.gz")
sha512sums=('370e83892c187825ecaec9198ccba7f886004ecce1c095134f18639116cb1acff4a005e1a45c3b79292c5cd626fb3cec38c81c3421bd6a42178ff2b92a0e50e3')

prepare() {
  cd deepin-desktop-schemas-$_commit
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/common/com.deepin.wrap.gnome.desktop.override
}

build() {
  cd deepin-desktop-schemas-$_commit
  make ARCH=x86
}

check() {
  cd deepin-desktop-schemas-$_commit
  make test
}

package() {
  cd deepin-desktop-schemas-$_commit
  make DESTDIR="$pkgdir" install
}
