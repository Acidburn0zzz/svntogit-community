# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.1.6
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
replaces=('deepin-default-gsettings')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz")
sha512sums=('47ad8e3187b11a74d008fe20ff10fe991bd85a44d239269ab8b7d01fe09324a8a61f9eb9dc154321a1e80e66ad116980bb3fd34d36c57ecf75e94dde389c36d2')

prepare() {
  cd deepin-desktop-schemas-$pkgver
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/x86/com.deepin.wrap.gnome.desktop.override
  # don't override GNOME defaults
  rm overrides/x86/{org.gnome.desktop,other}.override
}

build() {
  cd deepin-desktop-schemas-$pkgver
  make
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="${pkgdir}" install
}
