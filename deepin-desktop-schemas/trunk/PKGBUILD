# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.1.13
pkgrel=1
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
makedepends=('python')
replaces=('deepin-default-gsettings')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz")
sha512sums=('6384617c00659f607bb39a53aef4c03b22c0b0b7f501187f5c35a3a483d48ffa3542256931cc1e6c51a2ed4c90aa73d0d7038acf5660726388182b6e83c7d022')

prepare() {
  cd deepin-desktop-schemas-$pkgver
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/common/com.deepin.wrap.gnome.desktop.override
}

build() {
  cd deepin-desktop-schemas-$pkgver
  make ARCH=x86
}

check() {
  cd deepin-desktop-schemas-$pkgver
  make test
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="$pkgdir" install
}
