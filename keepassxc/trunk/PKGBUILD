# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=keepassxc
pkgver=2.1.2
pkgrel=2
pkgdesc="Cross-platform community-driven port of Keepass password manager"
arch=(x86_64 i686)
url="https://keepassxc.org/"
license=(GPL)
depends=(hicolor-icon-theme libxtst qt5-x11extras)
makedepends=(cmake qt5-tools)
source=("https://github.com/keepassxreboot/$pkgname/releases/download/$pkgver/$pkgname-$pkgver-src.tar.xz"{,.sig})
sha256sums=('b971888746158b935a0aa267671deb7c18859707df0cf26287c9bcd25b970bc0'
            'SKIP')
validpgpkeys=('BF5A669F2272CF4324C1FDA8CFB4C2166397D0D2') # KeePassXC Release <release@keepassxc.org>

prepare() {
	mkdir -p build
}

build() {
	cd build
	cmake ../$pkgname-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_XC_AUTOTYPE=ON \
		-DWITH_XC_HTTP=ON
	make
}

check() {
	cd build
	make test
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
