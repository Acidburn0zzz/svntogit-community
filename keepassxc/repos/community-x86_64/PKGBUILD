# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=keepassxc
pkgver=2.3.3
pkgrel=1
pkgdesc="Cross-platform community-driven port of Keepass password manager"
arch=(x86_64)
url="https://keepassxc.org/"
license=(GPL)
depends=(hicolor-icon-theme libgcrypt argon2 libsodium libxtst qt5-x11extras yubikey-personalization curl)
makedepends=(cmake qt5-tools)
source=("https://github.com/keepassxreboot/$pkgname/releases/download/$pkgver/$pkgname-$pkgver-src.tar.xz"{,.sig}
        "keepassxc-2.3.3-qt-5.11-edit-entry-widget-includes.patch")
sha256sums=('cfff85ef89ba590aec798c59bea4aa3db00626d7bff8cdde0f62ee34aea60ad5'
            'SKIP'
            '89729d7f83b49583ca073fd913123b3b7ee399c4ac9f2ec32886604c03bd1408')
validpgpkeys=('BF5A669F2272CF4324C1FDA8CFB4C2166397D0D2') # KeePassXC Release <release@keepassxc.org>

prepare() {
	mkdir -p build

	cd $pkgname-$pkgver
	# Fix build with Qt 5.11
	patch -Np1 -i ../keepassxc-2.3.3-qt-5.11-edit-entry-widget-includes.patch
}

build() {
	cd build
	cmake ../$pkgname-$pkgver \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib \
		-DWITH_XC_ALL=ON
	make
}

check() {
	cd build
	make test
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
