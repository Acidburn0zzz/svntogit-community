# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=chrono-date
pkgver=2.4.1+134+g9a0ee25
pkgrel=1
pkgdesc="A date and time library based on the C++11/14/17 <chrono> header"
arch=(x86_64)
url="https://howardhinnant.github.io/date/date.html"
license=('MIT')
makedepends=(
    'cmake'
    'git'
)
# It's easiest to pin this commit and wait for a new release rather than patch
# as a number of issues with tests/dependent packages (e.g.
# https://github.com/Alexays/Waybar/issues/565) are present with the tagged
# stable version.
source=("$pkgname::git+https://github.com/HowardHinnant/date#commit=9a0ee2542848ab8625984fc8cdbfb9b5414c0082"
        "538-output-date-pc-for-pkg-config.patch::https://patch-diff.githubusercontent.com/raw/HowardHinnant/date/pull/538.patch")
sha256sums=('SKIP'
            'f39ce7f1f738ebdc02948ed78be68cca10c01f9e22a4243670304eeda53a03fd')

pkgver() {
    cd "$pkgname"
    git describe --always --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
    cd "$pkgname"
    mkdir build
    # https://github.com/HowardHinnant/date/issues/537
    patch -p1 < ../538-output-date-pc-for-pkg-config.patch
}

build() {
    cd "$pkgname/build"
    cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_SHARED_LIBS=true \
        -DBUILD_TZ_LIB=true \
        -DUSE_SYSTEM_TZ_DB=true \
        -DENABLE_DATE_TESTING=true
}

check() {
    cd "$pkgname/build"
    make testit
}

package() {
    cd "$pkgname"
    make -C build DESTDIR="$pkgdir/" install
    install -Dm644 LICENSE.txt -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}
