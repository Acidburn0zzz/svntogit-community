# $Id$
# Maintainer: Alexander F RÃ¸dseth <rodseth@gmail.com>

pkgname=nimble
pkgver=0.7.0
epoch=1
pkgrel=1
pkgdesc='Package manager for the Nim programming language'
arch=('x86_64' 'i686')
url='https://github.com/nim-lang/nimble'
license=('BSD')
makedepends=('nim' 'git')
depends=('glibc')
source=("git://github.com/nim-lang/nimble.git#commit=670bcec")
#source=("git://github.com/nim-lang/nimble.git#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cd "$pkgname"

  git clone -b v0.13.0 --depth 1 git://github.com/nim-lang/nim vendor/nim
}

build() {
  cd "$pkgname"

  nim c -d:release src/nimble
}

package() {
  cd "$pkgname"

  install -Dm755 "src/nimble" "$pkgdir/usr/bin/nimble"
  install -Dm644 "license.txt" "$pkgdir/usr/share/licenses/$pkgname/license.txt"
}

# getver: github.com/nim-lang/nimble
# vim:set ts=2 sw=2 et:
