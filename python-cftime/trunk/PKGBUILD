# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=cftime
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=1.0.4
pkgrel=1
pkgdesc="Time-handling functionality from python-netcdf4"
arch=(x86_64)
url="https://unidata.github.io/cftime"
license=(MIT)
makedepends=(cython python-setuptools cython2 python2-setuptools python-numpy python2-numpy)
source=("https://files.pythonhosted.org/packages/source/c/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('f8f40f701b4ef2a4bf4c8e827b8acc7886bb9dd93cf2204b4bc81d9352c69312')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

package_python-cftime() {
    depends=(python-numpy)

    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-cftime() {
    depends=(python2-numpy)

    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
