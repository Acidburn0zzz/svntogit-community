# $Id: PKGBUILD,v 1.28 2009/05/16 16:53:39 daenyth Exp $
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent
pkgver=0.8.2
pkgrel=3
pkgdesc="Ncurses BitTorrent client based on libTorrent"
arch=('i686' 'x86_64')
url="http://libtorrent.rakshasa.no"
license=('GPL')
depends=('libtorrent=0.12.2' 'curl>=7.14.3' 'xmlrpc-c')
source=(http://libtorrent.rakshasa.no/downloads/$pkgname-$pkgver.tar.gz gcc43.patch gcc44.patch)
install=$pkgname.install
md5sums=('a2456182e1767e5aed7341dbbd058f60'
         'fcf2bb287bb25086a86bdfec2fd90556'
         '889373df7a96eae7496a4c4b1bb73f93')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/gcc43.patch" || return 1
  patch -Np1 -i "$srcdir/gcc44.patch" || return 1

  CXXFLAGS="$CXXFLAGS -fno-strict-aliasing" \
    ./configure --prefix=/usr --disable-debug --with-xmlrpc-c || return 1
  make || return 1
  make DESTDIR="$pkgdir" install
}


# vim:set ts=2 sw=2 et:
