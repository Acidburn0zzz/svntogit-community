# $Id$
# Maintainer: Jeff Mickey <jeff@archlinux.org>
# Contributor: sh__

pkgname=rtorrent
pkgver=0.8.4
pkgrel=1
pkgdesc="Ncurses BitTorrent client based on libTorrent"
arch=('i686' 'x86_64')
url="http://libtorrent.rakshasa.no"
license=('GPL')
depends=('libtorrent=0.12.4' 'curl>=7.14.3' 'xmlrpc-c')
source=(http://libtorrent.rakshasa.no/downloads/$pkgname-$pkgver.tar.gz gcc44.patch)
install=$pkgname.install
md5sums=('dc0f37d933b0b6c713ad617e09441f3b'
         'cdd0860dc25c9dedcc7d09ed9df5f527')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/gcc44.patch" || return 1

  CXXFLAGS="$CXXFLAGS -fno-strict-aliasing" \
    ./configure --prefix=/usr --disable-debug --with-xmlrpc-c || return 1
  make || return 1
  make DESTDIR="$pkgdir" install
}


# vim:set ts=2 sw=2 et:
