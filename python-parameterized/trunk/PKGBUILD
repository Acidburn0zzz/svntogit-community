# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.6.1
_commit=974d527d71c986e1ac89fa897b0aa18f03ffc058
pkgrel=1
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$_commit.tar.gz::https://github.com/wolever/parameterized/archive/$_commit.tar.gz")
sha512sums=('ec9f25bffd75dfc1dba041b4cca2cb50f832033d4b86338d7778897e9e1518d485dc25a7f3aafa3625bc40fe8f2328e737f8341b5ffba3d55258892234ed6a17')

prepare() {
  mv parameterized-{$_commit,$pkgver}
}

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
