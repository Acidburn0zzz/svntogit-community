# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.6.3
pkgrel=1
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
source=("https://pypi.io/packages/source/p/parameterized/parameterized-$pkgver.tar.gz")
sha512sums=('27568b6a7faa41a3153c85782ba1390767f606b8b1a781c74c79c31f9e04422ed0695aa54f6ecdb0d2e1d4cfcface57efb5480f1c4a87b1dd39f25d7e621b7b8')

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
