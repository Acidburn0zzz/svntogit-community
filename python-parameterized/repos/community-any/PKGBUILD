# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-parameterized
pkgver=0.7.0
pkgrel=3
pkgdesc="Parameterized testing with any Python test framework"
url="https://github.com/wolever/parameterized"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock')
source=("https://pypi.io/packages/source/p/parameterized/parameterized-$pkgver.tar.gz"
         parameterized-python-3.8.patch::"https://patch-diff.githubusercontent.com/raw/wolever/parameterized/pull/75.patch")
sha512sums=('b76e18a7eee62fcab4385fd5d053253c096d4c3fc3a6300e8539afc4d8decc995e533085297c52f8a26411d2591a326033a740cc30a250d510aa1c11ee904246'
            'a6b36cf4883990ed89db81effebcdc4630f645d91b0538dde3a6ed9d383c311668c0c23429b5693f34270bba338c011148e0dab4d064a6821a5fd82486f73232')

prepare() {
  cd parameterized-$pkgver
  patch -p1 -i ../parameterized-python-3.8.patch # Fix tests with python 3.8
}

build() {
  cd parameterized-$pkgver
  python setup.py build
}

check() {
  cd parameterized-$pkgver
  python setup.py nosetests
}

package() {
  cd parameterized-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
