# $Id$
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.2.7
pkgrel=1
pkgdesc='Japanese input method Anthy IMEngine for IBus Framework'
arch=('i686' 'x86_64')
license=('LGPL')
url="http://ibus.googlecode.com"
depends=('ibus' 'anthy' 'python2')
makedepends=('swig' 'intltool')
options=('!libtool')
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('091a13ff950171981768463993ffb683')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
# python2 fix
  for file in setup/ibus-setup-anthy.in engine/ibus-engine-anthy.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
