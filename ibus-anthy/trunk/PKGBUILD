# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars at gmail dot com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-anthy
pkgver=1.2.1
pkgrel=2
pkgdesc="Japanese input method Anthy IMEngine for IBus Framework"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://ibus.googlecode.com"
depends=('ibus' 'anthy' 'python2')
makedepends=('swig' 'intltool')
provides=('ibus-anthy=1.2.0')
options=('!libtool')
source=(http://ibus.googlecode.com/files/${pkgname}-${pkgver}.tar.gz)
md5sums=('a1b8adc10d4afd84f113da6999cd5f05')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # python2 fix
  for file in setup/ibus-setup-anthy.in engine/ibus-engine-anthy.in; do
    sed -i 's_exec python_exec python2_' $file
  done

  ./configure --prefix=/usr --libexec=/usr/lib/ibus
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
# vim:set ts=2 sw=2 et:
