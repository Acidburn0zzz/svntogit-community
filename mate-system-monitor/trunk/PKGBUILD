# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
pkgname=mate-system-monitor
pkgver=${_ver}.1
pkgrel=1
pkgdesc="A system monitor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('gtkmm3' 'libgtop' 'libwnck3')
makedepends=('intltool' 'itstool')
conflicts=("${pkgname}-gtk3")
replaces=("${pkgname}-gtk3")
groups=('mate-extra' 'mate-extra-gtk3')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('10c1f769f2c511adcdbdd63785de787e4c68723b')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --localstatedir=/var
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
