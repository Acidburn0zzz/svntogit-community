# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-chewing
pkgver=5.0.2
pkgrel=1
pkgdesc="Chewing Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-chewing"
license=('GPL')
depends=('fcitx5' 'libchewing')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/fcitx5-chewing/fcitx5-chewing-$pkgver.tar.xz"{,.sig})
sha512sums=('d89ffc24bd58fc2a2752ae8713bc00de35bc137242a9a4dd4e638949681ded88fc72b45eb83f600b4bee74166adb177a930a4c079460333c9b06506674c1a4de'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
