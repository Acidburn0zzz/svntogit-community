# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Adria Arrufat <adria.arrufat AT protonmail+aur DOT com>
# Contributor: Sami B. <spidermario@free.fr>
# Contributor: Yunhui Fu <yhfdev@gmail.com>

pkgname=cudnn
pkgver=7.4.2.24
_cudaver=10
pkgrel=1
pkgdesc="NVIDIA CUDA Deep Neural Network library"
arch=('x86_64')
url="https://developer.nvidia.com/cuDNN"
license=('custom')
depends=('cuda>=10')
# To figure out these URLs, check out the Dockerfiles at
# https://gitlab.com/nvidia/cuda/tree/centos7 for the appropriate cuda version.
source=("https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}.0_amd64.deb"
        "https://developer.download.nvidia.com/compute/machine-learning/repos/ubuntu1804/x86_64/libcudnn7_${pkgver}-1+cuda${_cudaver}.0_amd64.deb"
        "NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf")
sha512sums=('d99b3de0f063462dafe60c43dd2407778eb5ab1ccd2f082d5d1d4c0650754b5e136f0f3b4c2525a49381eba01e771727a61f781138e9a13eaf1abdedceafea37'
            '5e504bfaea9185759c012689e9679dff2ae98074b1b204623147baf38bf2ad2dc5f348d8b0437bcefe2c73668519d9a5ad05f2db5054d12b34a8589753cb2951'
            '38bbb7eb287914e4d7ba79da20a47222382687832d4ba0290715199a129c08715a23681800cf8d381f40e24202470a7b20505a31c8cea51a78762a740860251b')
noextract=("libcudnn7-dev_${pkgver}-1+cuda${_cudaver}.0_amd64.deb"
           "libcudnn7_${pkgver}-1+cuda${_cudaver}.0_amd64.deb")

package() {
  mkdir -p "${pkgdir}"/usr/{lib,include}

  mkdir libcudnn7-dev
  cd libcudnn7-dev
  ar xv "${srcdir}"/libcudnn7-dev_${pkgver}-1+cuda${_cudaver}.0_amd64.deb
  tar xf data.tar.xz
  cp -a usr/include/x86_64-linux-gnu/* "${pkgdir}"/usr/include/
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  mkdir libcudnn7
  cd libcudnn7
  ar xv "${srcdir}"/libcudnn7_${pkgver}-1+cuda${_cudaver}.0_amd64.deb
  tar xf data.tar.xz
  cp -a usr/lib/x86_64-linux-gnu/* "${pkgdir}"/usr/lib/

  # Get rid of some Ubuntu-isms but at the same time still allow for them to
  # work because nobody is ever quite sure what the right names are.
  ln -s cudnn_v7.h "${pkgdir}"/usr/include/cudnn.h
  ln -s libcudnn.so.7 "${pkgdir}"/usr/lib/libcudnn.so

  install -Dm644 \
      ${srcdir}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf \
      ${pkgdir}/usr/share/licenses/${pkgname}/NVIDIA_SLA+cuDNN_Supp_Feb2017_release.pdf
}

# vim: ft=sh syn=sh et
