# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Xilon <xilonmu@gmail.com>

pkgname=powertop
pkgver=2.12
pkgrel=1
pkgdesc='A tool to diagnose issues with power consumption and power management'
arch=('x86_64')
url='https://01.org/powertop/'
license=('GPL2')
depends=('gcc-libs' 'libnl' 'ncurses' 'pciutils')
makedepends=('autoconf-archive')
optdepends=('xorg-xset: for the --calibrate function')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/fenrus75/powertop/archive/v$pkgver.tar.gz)
sha256sums=('35fbdbf2a02bf6f3558c013d83f4a0e96c2af9a29d0f764b843339f7c39bd1fb')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

# https://bugzilla.redhat.com/show_bug.cgi?id=1826935
  autoreconf -fi || autoreconf -fi

#   ./autogen.sh
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
