# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=ring-daemon
pkgname=jami-daemon
pkgver=20190412
pkgrel=1
_pjprojectver=2.8
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(opendht yaml-cpp alsa-lib libpulse jack jsoncpp dbus-c++ ffmpeg gnutls
         expat libupnp libnatpmp libva libvdpau restbed libsecp256k1 util-linux
         opus)
makedepends=(git boost msgpack-c autoconf-archive)
#checkdepends=(cppunit)
provides=(${_pkgname})
conflicts=(${_pkgname})
replaces=(${_pkgname})
_commit=008c78225bd12615dc1fdd0a3b258b88ce944a0d
source=("git+https://git.jami.net/savoirfairelinux/ring-daemon.git#commit=${_commit}"
        "https://github.com/pjsip/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz")
noextract=("pjproject-${_pjprojectver}.tar.gz")
sha256sums=('SKIP'
            '1fcdf6e295e3b8d99c9e738d3e980d8e7da34c76bd5e773c8ed9ad26b119d0eb')

prepare() {
  cd ${_pkgname}
  # Fixes w.r.t. OpenDHT
  git cherry-pick -n 57adfd4f41e34479d8b2027f2250aee9c28aec0e
  git cherry-pick -n f40eb59a1ae964fa74d4f4eca2508cc4a076bb10
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd ${_pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd ${_pkgname}
#  make -k check
#}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
}
