# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=ring-daemon
pkgname=jami-daemon
pkgver=20190503
pkgrel=1
_pjprojectver=6b9212dcb4b3f781c1e922ae544b063880bc46ac
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=(x86_64)
url="https://jami.net"
license=(GPL3)
groups=(jami)
depends=(opendht yaml-cpp alsa-lib libpulse jack jsoncpp dbus-c++ ffmpeg gnutls
         expat libupnp libnatpmp libva libvdpau restbed libsecp256k1 util-linux
         opus)
makedepends=(git boost msgpack-c autoconf-archive)
#checkdepends=(cppunit)
provides=(${_pkgname})
conflicts=(${_pkgname})
replaces=(${_pkgname})
_commit=69ffe0b284a1868997b054ed7db0b2fc15c14edb
source=("git+https://git.jami.net/savoirfairelinux/ring-daemon.git#commit=${_commit}"
        "https://github.com/pjsip/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz")
noextract=("pjproject-${_pjprojectver}.tar.gz")
sha512sums=('SKIP'
            'e203f3846db1a380b2bcb2e18fb73f6296a87c25d5b0b38d44df88a398c597bcb31c85a0e72b476908e4b56fdeb6fc5c586cf595f9723091d6a09659b75d1d5c')

prepare() {
  cd ${_pkgname}
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd ${_pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd ${_pkgname}
#  make -k check
#}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}" install
}
