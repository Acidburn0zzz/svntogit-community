# $Id$
# Maintainer: Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Sébastien Luttringer

pkgbase=virtualbox-modules-lts
pkgname=('virtualbox-host-modules-lts' 'virtualbox-guest-modules-lts')
pkgver=4.2.14
pkgrel=1
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL')
makedepends=('linux-lts'
             'linux-lts-headers'
             "virtualbox-host-dkms>=$pkgver"
             "virtualbox-guest-dkms>=$pkgver"
             'expac'
             'sed'
             'bc')

build() {
  _kernmaj=$(expac -Q '%v' linux-lts|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-lts/version)
  # dkms need modification to be run as user
  cp -r /var/lib/dkms .
  echo "dkms_tree='$srcdir/dkms'" > dkms.conf
  # build host modules
  msg2 'Host modules'
  dkms --dkmsframework dkms.conf build "vboxhost/$pkgver" -k "$_kernver"
  # build guest modules
  msg2 'Guest modules'
  dkms --dkmsframework dkms.conf build "vboxguest/$pkgver" -k "$_kernver"
}

package_virtualbox-host-modules-lts(){
  _kernmaj=$(expac -Q '%v' linux-lts|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-lts/version)
  pkgdesc='Host kernel modules for VirtualBox'
  depends=("linux-lts>=$_kernmaj" "linux-lts<$(echo "$_kernmaj+0.1"|bc)")
  replaces=('virtualbox-modules-lts')
  conflicts=('virtualbox-modules-lts')
  provides=("virtualbox-host-modules=$pkgver")
  install=virtualbox-host-modules-lts.install

  cd "dkms/vboxhost/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-lts/"
  install -m644 * "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-lts/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/VERSION='.*'/VERSION='$_kernver'/" "$startdir/virtualbox-host-modules-lts.install"
}

package_virtualbox-guest-modules-lts(){
  _kernmaj=$(expac -Q '%v' linux-lts|sed -r 's/([[:digit:]]+\.[[:digit:]]+).*/\1/')
  _kernver=$(< /usr/lib/modules/extramodules-$_kernmaj-lts/version)
  pkgdesc='Guest kernel modules for VirtualBox'
  license=('GPL')
  depends=("linux-lts>=$_kernmaj" "linux-lts<$(echo "$_kernmaj+0.1"|bc)")
  replaces=('virtualbox-archlinux-modules-lts')
  conflicts=('virtualbox-archlinux-modules-lts')
  provides=("virtualbox-host-modules=$pkgver")
  install=virtualbox-guest-modules-lts.install

  cd "dkms/vboxguest/$pkgver/$_kernver/$CARCH/module"
  install -dm755 "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-lts/"
  install -m644 * "$pkgdir/usr/lib/modules/extramodules-$_kernmaj-lts/"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -ie "s/VERSION='.*'/VERSION='$_kernver'/" "$startdir/virtualbox-guest-modules-lts.install"
}

# vim:set ts=2 sw=2 et:
