# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgbase=himalaya
pkgname=('himalaya' 'vim-himalaya')
pkgver=0.5.8
pkgrel=1
pkgdesc="A CLI email client"
arch=('x86_64')
url="https://github.com/soywod/himalaya"
license=('BSD')
depends=('gcc-libs' 'openssl')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('99b5f4c4b6b3977e6c57124203b42d4a9229ee0cf5a404a164b534b5eed63d8da31649ce1067a3deabee246038d0aa8b5b6304c1ca89e571f39ac9546e005ede')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen -- --skip "test_imap_backend"
}

package_himalaya() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "assets/$pkgname.desktop" -t "$pkgdir/usr/share/applications"
}

package_vim-himalaya() {
  depends=('vim-plugin-runtime' 'himalaya')
  pkgdesc+=' - Vim UI plugin'

  cd "$pkgbase-$pkgver/vim/"
  find autoload doc ftplugin lua plugin syntax \
    -type f -exec install -Dm 644 '{}' "$pkgdir/usr/share/vim/vimfiles/{}" \;
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
  install -Dm 644 ../LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set ts=2 sw=2 et:
