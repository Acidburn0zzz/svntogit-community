# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Cedric Girard <girard.cedric@gmail.com>
# Contributor: foxbunny <bg.branko@gmail.com>

_pkgname=DisplayCAL
pkgname=displaycal
pkgver=3.1.2.0
pkgrel=1
pkgdesc="Open Source Display Calibration and Characterization powered by Argyll CMS (Formerly known as dispcalGUI)"
arch=('i686' 'x86_64')
url="http://displaycal.net/"
license=('GPL3')
makedepends=('libx11' 'libxrandr' 'libxinerama' 'libxxf86vm' 'python2-setuptools' 'xdg-utils')
depends=('argyllcms' 'wxpython' 'hicolor-icon-theme' 'desktop-file-utils' 'python2-numpy')
optdepends=('gksu: For running as root')
replaces=('dispcalgui')
install=${pkgname}.install
source=("http://downloads.sourceforge.net/project/dispcalgui/release/${pkgver}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('c2ce3ac78ba2877cf4adafa0f5fa3705')

prepare() {
  cd ${_pkgname}-${pkgver}
  find . -name "*.py" -exec sed -i 's!/usr/bin/env python!/usr/bin/env python2!g' {} +
}

package() {
  cd ${_pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1
  #udev rules are shipped with argyllcms
  rm -rf "${pkgdir}"/etc/udev/
}

# vim:set ts=2 sw=2 et:
