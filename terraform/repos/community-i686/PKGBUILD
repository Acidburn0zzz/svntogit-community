# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=terraform
pkgver=0.9.11
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="http://www.terraform.io/"
arch=("i686" "x86_64")
license=("MPL")
makedepends=("go" "godep" "git" "mercurial")
conflicts=("terraform-bin")
_gourl="github.com/hashicorp"
source=("https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha256sums=("400fbf7c65a5f4b599d1b3d20db8ad9d45930a80ce3abf14aa7ed56eac22b3fb")


prepare() {
	mkdir -p "$srcdir/src/$_gourl"
	rm -rf "${srcdir}/src/$_gourl/$pkgname"
	mv -f "terraform-$pkgver" "$srcdir/src/$_gourl/$pkgname"
	msg2 "Fetching dependencies"
	cd "$srcdir/src/$_gourl/$pkgname"
	GOPATH="$srcdir" go get -u github.com/mitchellh/gox
	GOPATH="$srcdir" go get -u golang.org/x/tools/cmd/stringer
}


build() {
	msg2 "Build program"
	cd "$srcdir/src/$_gourl/$pkgname"
	GOPATH="$srcdir" PATH="$srcdir/bin:$PATH" TF_DEV=1 TF_QUICKDEV=1 make bin
}


package() {
	cd "$srcdir/bin"
	install -Dm755 terraform "$pkgdir/usr/bin/terraform"

	cd "$srcdir/src/$_gourl/$pkgname"
	# Zsh completion
	install -D contrib/zsh-completion/_terraform "$pkgdir/usr/share/zsh/site-functions/_terraform"
}
