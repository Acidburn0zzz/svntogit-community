# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.18
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('b75b9318a10285b445e97ab3b6a4b8b166d60a0036098e1e46421b6c67f87c0bc8c3a91d781b015264f6b6265f233396ac7735a758648e5f721229c5de172472')
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath"
	go build -o terraform-binary
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
