# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.17
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('6311fdbf6764e585126a1e177dc498634a799ba1a68ba98682e2be29d14b40c09d0a780555f10bbde0587445d31a1e0d9810226f9e6bd3fcb248428ceb864c87')
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath"
	go build -o terraform-binary
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
