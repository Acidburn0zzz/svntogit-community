# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=terraform
pkgver=0.14.4
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go")
depends=('glibc')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('b39d081e03a9a2272ba979ae236049d1b5825110e0cae35961de42464e98d531a8be8b4c1fae150c10d148583cada30ff820f8a09cd71e2ea7bae43e8367f2c4')
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
	go build -o terraform-binary
}

check() {
	cd "${pkgname}-${pkgver}"
	go test -mod=readonly ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
