# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.0
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha256sums=("0e13fec7d1ecde21369f2ae9eeb06405c078d392cb373a43e8fe34a6a373417d")


prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p src/github.com/hashicorp/
	mv "${pkgname}-${pkgver}" src/github.com/hashicorp/${pkgname}
}


build() {
	cd src/github.com/hashicorp/"${pkgname}"
	go build -o terraform-binary
}


package() {
	cd src/github.com/hashicorp/"${pkgname}"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
