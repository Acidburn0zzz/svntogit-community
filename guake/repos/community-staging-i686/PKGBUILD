# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto JÃºnior (N3RD3X) <n3rd3x@linuxmail.org>
pkgname=guake
pkgver=0.4.2
pkgrel=5
pkgdesc='is a drop-down terminal for Gnome Desktop Environment'
arch=('i686' 'x86_64')
url='http://guake.org'
license=('GPL')
depends=('python-notify' 'vte' 'gconf' 'python2-gconf' 'dbus-python' 'notification-daemon')
makedepends=('libx11' 'pkgconfig' 'perlxml' 'intltool')
install=guake.install
source=(http://guake.org/files/guake-$pkgver.tar.gz
        http://guake.org/raw-attachment/ticket/246/0001-Retrieve-port-as-int.2.patch)
md5sums=('1f0feff3bfc15c998147dbf07d9d8a8e'
         '9aca6ba8e46ae1b69958cda0e03320ff')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Point Python scripts to the python2 binary
  sed -i 's|bin/python"|bin/python2"|' src/guake{,-prefs}.in

  # Patch to import proxy port setting as int (http://guake.org/ticket/246)
  patch -Np1 -i "$srcdir/0001-Retrieve-port-as-int.2.patch"

  ./configure --sysconfdir=/usr/share --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
