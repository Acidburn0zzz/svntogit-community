# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>

pkgname=guake
pkgver=0.4.2
pkgrel=7
pkgdesc='Drop-down terminal for GNOME'
arch=('i686' 'x86_64')
url="http://guake.org/"
license=('GPL')
depends=('python-notify' 'vte' 'python2-gconf' 'dbus-python')
makedepends=('intltool')
options=('!libtool')
install=$pkgname.install
source=(http://guake.org/files/$pkgname-$pkgver.tar.gz
        http://guake.org/raw-attachment/ticket/246/0001-Retrieve-port-as-int.2.patch
        guake-glib2.32.patch)
md5sums=('1f0feff3bfc15c998147dbf07d9d8a8e'
         '9aca6ba8e46ae1b69958cda0e03320ff'
         'ac692391e2fa171f6f96e5ceb2284c2d')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|schemadir = $(sysconfdir)/gconf/schemas|schemadir = $(GCONF_SCHEMA_FILE_DIR)|' data/Makefile.in
  sed -i 's|PYTHON="/usr/bin/python"|PYTHON="/usr/bin/python2"|' src/guake{,-prefs}.in
  patch -Np1 -i "$srcdir/guake-glib2.32.patch"

  # Patch to import proxy port setting as int (http://guake.org/ticket/246)
  patch -Np1 -i "$srcdir/0001-Retrieve-port-as-int.2.patch"

  # Do not disable autostart in GNOME
  sed -i '/X-GNOME-Autostart-enabled/ d' data/guake.desktop.in

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Remove autostart file
  rm -r "$pkgdir/etc"
}
