# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=3.2.0
pkgrel=1
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('libkeybinder3' 'libnotify' 'libutempter' 'python-cairo' 'python-dbus' 'python-gobject' 'python-pbr' 'vte3')
makedepends=('git')
_commit=20f3aed4c95f7af7dd8742d7b620748328c19e8a  # tags/3.2.0
source=("git+https://github.com/Guake/guake#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  sed -i '/@pip3/d' Makefile
  make DESTDIR="$pkgdir" prefix='/usr' COMPILE_SCHEMA=0 install
}
