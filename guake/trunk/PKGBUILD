# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Wilson Pinto Júnior (N3RD3X) <n3rd3x@linuxmail.org>
# Contributor: Attila Bukor <r1pp3rj4ck@w4it.eu>

pkgname=guake
pkgver=3.4.0
pkgrel=2
pkgdesc='Drop-down terminal for GNOME'
arch=('any')
url="http://guake-project.org/"
license=('GPL')
depends=('libkeybinder3' 'libnotify' 'libutempter' 'python-cairo' 'python-dbus' 'python-gobject' 'python-pbr' 'vte3')
makedepends=('git')
_commit=2a497694efb8ce1f6e1500eb3a71864276eb0b28  # tags/3.4.0
source=("git+https://github.com/Guake/guake#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # Revert use_mouse code to previous behavior (FS#61243)
  git cherry-pick -n 6918843b6d110f70a372e01eff09d4fe7280969a
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname
  sed -i '/-m pip install -r requirements.txt/d' Makefile
  make DESTDIR="$pkgdir" prefix='/usr' COMPILE_SCHEMA=0 install
}
