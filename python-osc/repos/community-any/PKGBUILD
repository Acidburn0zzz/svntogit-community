# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=python-osc
pkgver=1.7.6
pkgrel=1
pkgdesc='Open Sound Control server and client implementations in pure Python'
arch=('any')
url="https://github.com/attwad/python-osc"
license=('Unlicense')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('fa8492bd96ac736651ea1810874de6f7a728f43a4ad2f97fcb687c3af7dc8b724e902de7b3537d08a970afe04c7031f89ffbf83dc5400484676275beeb9aa56a')
b2sums=('d606b1951e11b1f10d7e2b86f5a35fe51a0fa27c2611f3c5ac1cb2b0693014d27e8e500828d1066cc11474877950bc29fb42247dec00ef2bfc5b2f7954c3d8ae')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # exclude flaky test: https://github.com/attwad/python-osc/issues/140
  pytest -v -k "not test_nto_to_system_time"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
