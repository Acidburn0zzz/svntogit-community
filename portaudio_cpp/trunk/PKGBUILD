# $Id$
# Maintainer: Bob Finch <w9ya@qrparci.net>

pkgname=portaudio_cpp
pkgver=19_20110326
pkgrel=1
pkgdesc="PortAudio c++ bindings"
arch=('i686' 'x86_64')
url="http://www.portaudio.com/"
license=('custom')
depends=("portaudio=${pkgver}" 'gcc-libs')
options=('!libtool')
source=(http://www.portaudio.com/archives/pa_stable_v$pkgver.tgz)
md5sums=('8f266ce03638419ef46e4efcb0fabde6')

build() {
  cd "$srcdir/portaudio"
  ./configure --prefix=/usr --enable-cxx
  make 
}

package() {
  cd "$srcdir/portaudio/bindings/cpp"
  make DESTDIR=$pkgdir install
  install -D -m644 ../../LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}
