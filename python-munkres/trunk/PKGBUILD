# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgbase=python-munkres
pkgname=('python-munkres' 'python2-munkres')
pkgver=1.0.9
pkgrel=1
pkgdesc='Munkres algorithm for Python'
arch=('any')
url='https://github.com/bmc/munkres'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-munkres-${pkgver}.tar.gz::https://github.com/bmc/munkres/archive/release-${pkgver}.tar.gz")
sha256sums=('1dae30b882f409bab8d913af7bbf41877359b1ece3fa0ea31d6486a454f3493b')

prepare() {
  mv {,python-}munkres-release-${pkgver}
  cp -r python{,2}-munkres-release-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-munkres-release-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-munkres() {
  depends=('python')

  cd python-munkres-release-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-munkres() {
  depends=('python2')

  cd python2-munkres-release-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
