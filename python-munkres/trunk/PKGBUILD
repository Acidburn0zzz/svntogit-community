# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Adrian Sampson <adrian@radbox.org>

pkgbase=python-munkres
pkgname=('python-munkres' 'python2-munkres')
pkgver=1.0.10
pkgrel=1
pkgdesc='Munkres algorithm for Python'
arch=('any')
url='https://github.com/bmc/munkres'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
source=("python-munkres-${pkgver}.tar.gz::https://github.com/bmc/munkres/archive/release-${pkgver}.tar.gz")
sha256sums=('cc00ca9b1bae6b574797fbefc3a200b65d1317e599055f3edf9643c28cf4ecca')

prepare() {
  mv {,python-}munkres-release-${pkgver}
  cp -r python{,2}-munkres-release-${pkgver}

  cd python2-munkres-release-${pkgver}

  sed 's|#!.*python$|#!/usr/bin/env python2|' -i $(grep -rl '#!.*python$')
}

build() {
  for py in python{,2}; do
    pushd ${py}-munkres-release-${pkgver}
    ${py} setup.py build
    popd
  done
}

package_python-munkres() {
  depends=('python')

  cd python-munkres-release-${pkgver}

  python setup.py install --root="${pkgdir}" --optimize='1'
}

package_python2-munkres() {
  depends=('python2')

  cd python2-munkres-release-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize='1'
}

# vim: ts=2 sw=2 et:
