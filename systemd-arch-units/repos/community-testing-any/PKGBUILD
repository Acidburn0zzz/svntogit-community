# $Id$
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=systemd-arch-units
pkgver=20120612
pkgrel=6
pkgdesc="Arch specific Systemd unit files"
arch=('any')
url="http://github.com/falconindy/systemd-arch-units"
license=('MIT')
depends=('systemd')
install=systemd-arch-units.install
source=("$pkgname-$pkgver.tar.gz::https://www.github.com/falconindy/$pkgname/tarball/$pkgver")
md5sums=('a7adf1a2510be04cbb956cb1212e70e0')

package() {
  dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q)
  make -C "$srcdir/$dirname" DESTDIR="$pkgdir" install

  rm -v "$pkgdir"/usr/lib/systemd/system/oidentd{@.service,.socket}
  rm -v "$pkgdir/usr/lib/systemd/system/ip"*
  rm -v "$pkgdir/usr/lib/systemd/system/lxdm.service"
  rm -v "$pkgdir/usr/lib/systemd/system/nginx.service" 
  rm -v "$pkgdir/usr/lib/systemd/system/wpa_supplicant.service" 

  # error: failed to commit transaction (conflicting files)
  # /usr/lib/systemd/scripts/iptables-flush exists in both 'iptables' and 'systemd-arch-units'
  # Errors occurred, no packages were upgraded.
  rm -v "$pkgdir/usr/lib/systemd/scripts/iptables-flush"
}
