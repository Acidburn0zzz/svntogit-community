# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk2hs-buildtools
pkgver=0.13.2.2
pkgrel=8
pkgdesc="Tools to build the Gtk2Hs suite of User Interface libraries."
url="https://hackage.haskell.org/package/gtk2hs-buildtools"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-random' 'haskell-hashtables')
makedepends=('alex' 'happy')
provides=('gtk2hs-buildtools')
conflicts=('gtk2hs-buildtools')
replaces=('gtk2hs-buildtools')
source=("https://hackage.haskell.org/packages/archive/gtk2hs-buildtools/$pkgver/gtk2hs-buildtools-$pkgver.tar.gz"
        float128.patch)
md5sums=('d39564d3da1c4225e3758ddb50de5eed'
         '0ef88deea74635cb30498fd468981454')

prepare() {
    cd gtk2hs-buildtools-$pkgver
    # https://github.com/gtk2hs/gtk2hs/issues/200
    patch -p2 -i ../float128.patch
}

build() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"
    
    runhaskell Setup configure -O --enable-executable-dynamic --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/gtk2hs-buildtools"
    runhaskell Setup copy --destdir="${pkgdir}"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
