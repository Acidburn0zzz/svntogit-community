# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.90
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
depends=(lib32-glibc)
makedepends=(gcc-multilib)
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('2ae23e500e236a87f9f49f4f6e7af73e6d17b7f9853d9a0786f361209ea9cfab'
            'a41fda6529cd113356f6b18da5e90ff5313aa5fd5f61688023ccbb2a34eedfb6'
            'a72dccc2c8f9c07de802953a00b0e1895fe8ccd97adab24b0d62df6890e0f53f'
            'da0b549d9cee7d5471e69c9bb17a94f144bada51a223db4d2ac456ae2ff6436d'
            'd83bc54171760e5bb8ad9b7ed5e83d10b9c94bdf5ab0732ce48f827c657747dd'
            '2631c7fe0af05b01e2f91ee5b1cc7ba235806d11f4d31a334299279ec4e7857b'
            '973e5c3cd03ace58506cd4a361075f75a34cf1544273bfaed024fa242d64b23b'
            'edba8ba2be823f8650d3beffe6f5b296755ae325c6e339c004761173e25772c6'
            '9f1ee0cb753eacdd8e562b931dedda5828a6501ae9127f8c32368f8aab1d37ad'
            'b996b5f4332d7a4f5fa202b49b5cd205490c23c31f8a7fefac24a7a4bea7590b'
            '755db2705caddcb84e733127bb0cfbe98a882c2fc296a951dc3d2d24e7bfbe63'
            'b7c414379c5be723e5e7022e09fa7ce4a583b233c8795a06082e3859956affeb'
            '3d4403260f54169196258f619ab7b44f4e501f644b09056a163d9ebd33bc73b2'
            '7856a02f0ae0f38a65d98e072fe78ac93a8f70810d6775aea5b56d123b058c6c'
            'dbd1b3fb0d232da10acce2f753737fd9e0d343a75bb7fce76d303b32792b45ba'
            'b4d4bfb95ded86a0c6b5e0ad8cae97372c7b3c67e5da2a2505ce2ad8d1f7c23e'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
