# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.97
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
depends=(lib32-glibc)
provides=(sml)
install=smlnj.install
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.amd64-unix.tgz::${_url}boot.amd64-unix.tgz"
  "$pkgname-$pkgver-boot.x64-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('7ddc99b51ab54c7b6fe5094939f64dbfa11dcb551a907c4723e8f7d39b3c0314'
            '3f97df4dc592cf21df3f2b65a51b66ebb56277d051a05589ecdbf86b884b18df'
            '919482e661cb53afd193830a3395d42d786ff4f48baf69f951b75546073ba6b2'
            'b9f9b02d4f6af7fb0665d8444f685c7cf3074a446eef1dcd1a53e9e472467a9b'
            'f30a04a427d0ce2dfd902fe06cb178df45d3a0d8b5eb432a6bb8b7691f96f3cd'
            'e457c3f2a62935ba1af074360e3e457aaea539c17d88aef730aac0254e4da4f0'
            'd7f964083ab6d4a477654a15a1305147f83d754ff66076d34bffc834b8c79b3d'
            '09ffa9d07758c603afe1fc88dd78ee3eabd9090a0c6233683ba83730747d5219'
            '7213b67bba1b1a28fff56f38931f0365202f9722fa4864a065c4228562ddc10f'
            '049b9a48101bca898fad91768605a487add248408c765cb6f1c1cfa56b50bc10'
            '29ee0e891a2e0cd2fe39b32ea327ba57741bb581853215ff96196dfd5c00ba14'
            'd925d60976afc6e50040276f229f1156523721753f5d05683af50ad89a157af0'
            'e67b21f1c60e3eeeac9f4a1c8eee2afbbf27374d7aaf66f2d23f6dbef99da41f'
            '14375b117ab393eb7abc6421a298a0418fbd9cc1ee7b859924bf0b10a2f38f80'
            '47b0dc7e01d250ca8ab24a74915fb6480148236a2ac45710291adf1155ca7c8d'
            '6aaec72c95b4d54ccbb3f9a8dc1f020687e281d1614c71cb74b8aae2c1712327'
            '6cf597545c72e330040d9031ece199b8cda99fe8a78f68b337f80f8087a9f6f7'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

build() {
  mkdir -p install
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  export CFLAGS="$CFLAGS -Wl,-z,relro,-z-now"
  export LDFLAGS="$LDFLAGS -Wl,-z,relro,-z-now"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh -default 64
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
