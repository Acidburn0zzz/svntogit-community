# $Id$
# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.83
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=('BSD')
arch=('x86_64')
provides=('sml')
install=smlnj.install
depends=('lib32-glibc')
makedepends=('gcc-multilib')
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  'urlgetter.sh'
  'profile.d-smlnj.sh'
  'smlnj.sh'
)
sha256sums=('5830522e5981ba9fb6d01201db53f26285e6a251bedaf9dd87db0b9edf540b09'
            '997d74d1a3e7e6b22c3002bf69a6003173c856d59baba79e93073f9c7f3eacaa'
            '89ccb520252097d83b8bdd70acc6f8bfbb662880f71bd0e516518c390454b2ca'
            '4887d767566a2dc5276315ae51c84ae4d2317523c0683f91c7df7e8a4300b463'
            '0254a24e4438a4b19e4d97a44306d0aa3063dfc5ab2606c7efaa664778c47173'
            '1d30c9e7ff386b7f09b98863c3778e2a008c9404a50d20f105f3d3dfb950f091'
            '1842f9c6db8aa3e685af5b3fa73ab1162b3ce4aefb28a9d1b46f070564152455'
            'eabb1eee5a4ca09bc5d244625c1a58ad51199df682c572a322b296921d3b2364'
            '6ab5db28b154c925e538b6cde886d7e0eee0bff24a3e01b950a2bfbdc8866921'
            'eabcb899e9d16720ce2f44dda074236691aa5dacac05af8741a288e0d2c2dd5b'
            '28e9bff3598dfa0bc58b7aa4d9031509109fbb3b2bb17fc389f4f7c669d531d9'
            'ec9f407659fca997fb0714edafd4079e8d356463f9e6b7cdf787b6bf8b35fdec'
            'beb1ef366db2034966eb9832bf6f8168513f58f18f34b38a6b7ab92f960b2e7e'
            '2789f4f7b1e1b6ac0874d2232ea4d7aa44adccb655934227058b3153f9be2607'
            '11e079d7ac5dde5e67457480053cd0e37dac343cb35fb0a7135df2bbb48426c5'
            '1cb5559445805017f16f56df348e7e5c75352e060a8a43ea600e4300cab59a14'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
