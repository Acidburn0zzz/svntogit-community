# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.95
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.amd64-unix.tgz::${_url}boot.amd64-unix.tgz"
  "$pkgname-$pkgver-boot.x64-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('0632c2d321ea23962ef878483213ef585454d6d6d2792484b1cbac399032c9fe'
            'aeb74291c8f41c4829eb950be41c24d3ad1a1ac4acfa259f0f367a78e5ce6c1d'
            '7c45198ca0ce026a2239cb6077fb054ce9ab021be83743cd57d120da67c75927'
            'fea76cd393d6b6fddfce20975698b2a88ad23dbf1123ded549e51fb0b28d083e'
            'a7bb8e76f0de47edb463feb9f26a906cbfca18cfc88e186da1c108e63f9766ce'
            'ecde990ed2d033d0c10a9eafa456ec99f2b189a3d68ffe9f0e816c566032209e'
            '1a15c6991d2c065ca842488a418d6a732329bbcb31bfdc79ae59deeef8901169'
            'bffe180cf3618e0a4ef46e00cee6556a84eb2ce00cd3577a49ecb1dce51b4dec'
            '77190889ab93f6eb03caac2cb0d59fff0f10a16c609947c7f747a78de20c2112'
            '57d5c0af43cc79214c8efec38aed577401535539219fc92a4bf81586e4d0aca2'
            '93cfed2449059d186c3e7891416b6bb9a5ae6a9113fda1baa44fbf8555a1b99a'
            'b6e6fd4c9d1f0850f4e26f0b22f86604759ca2116aef31c92826deb02d41a703'
            '3fc853b1e1a3d5ff3292377da2ea9ddccece04375a4d6a7a2329239f134c9e9e'
            'fd0ac13ae7511c44f006938b2819e8eab822228e8d57541829e7041ca7a043c9'
            '4e0e2fa72bc709a9e472f79336fe5f64d4a22ce21446a9d378fe20bef9b488d4'
            '927c527752f176b3d6e7a38f661a4f86de2d02a981c447aa7cc8ae03d5dacb8e'
            '7d83408b10a6002082bc087536795e6dca5b8b23cf59524acd62e934d9eb00b1'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

build() {
  mkdir -p install
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  export CFLAGS="$CFLAGS -Wl,-z,relro,-z-now"
  export LDFLAGS="$LDFLAGS -Wl,-z,relro,-z-now"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh -default 64
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
