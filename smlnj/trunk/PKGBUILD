# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.94
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
depends_x86_64=(lib32-glibc)
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('71c7bc2f7763d52211a7f0188619dbd175b12bb18f97b30cf392ccfdea1fe762'
            '6a29d26f4a4aa11e5fe7c50399fa940d27a01ae2caa412d1ad65b33555e6d69b'
            '77765f396304c0081022637afc2c6038cf5f86b6be1d6642ffb9db1766b6e0f4'
            '430d725645fe9dec0a41c0a5b90060994170b5ea6ceb4b320c6f257d0290bdba'
            'e9938960302d4f628fa889ddb4d85fb4f9237506aa5acd73883832725b4bb823'
            'b052852206fe418cdca119a4eafcb5d06822c3dbb9e2afa06e79b9d4eb00b152'
            '3b2e5f8f674b9670ad5b3f702ec8f994fc0fb296b50592d400d6e1fb9d0b6e78'
            'e78571bc578668850daa653a15f10d3dc6d50bde53390e87d9a3ac79347f74ef'
            'e25b55ea2b05e8b15c705f342d56bd87766452aacef37f9d71f392c6f9380995'
            '1482e387ea24d2949f034cdb5178b335eea36c2aa351e4bdefb72fb18229638d'
            '74e0e50e8e13214d07a0aef66e9f11f40c928fd7afa6efa76f7b0c8c75b50060'
            'd27f80d2be7c87cc389ed7f40850b16d778f213b9dfc0750fbf714b728416714'
            '31ccbc5598063a6ea11073f78737c764a63e272462824a287e326c8026335c14'
            'd5c09318c754e13695df916ef83f2c83bb4a3f668970f82d5bfbf4ef9c2143ee'
            'fdd38446833c9ddf720bd02daaa1a5065e32c97bd01785e4b30612d866271c0b'
            '91c425663df0cfacee5b4d6bd8341e9169bb1c4cf25fc6fd1946da0701ba11fd'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

build() {
  mkdir -p install
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
