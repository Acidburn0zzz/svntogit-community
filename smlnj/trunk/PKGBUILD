# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.87
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
depends=(lib32-glibc)
makedepends=(gcc-multilib)
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('f8aaa8161a4004903dbf9143840e3b4ec15f022ef1bdd41bdd3a88ceb69a5301'
            'bf1a1e99b407da3bbc170b327c7a2b305891bde4165e6419b2a0d37d59a845e3'
            'a22c1b50543a5c03e5d6dd6d1aa19871c7230f937c661e2d82bd8f971c87f69a'
            'dc1898d6c350c1a47e03864d71716db2fdc03e1bed01984e15226a6c136a29bd'
            '1b06916d3645ab7b35ef2e2d0a5456169566994a5bfbfa44248d546e80495347'
            'fe933c725e089c5ecf62ef67c75810efc2ef58b13ac83da820cd87ed3959a9ac'
            '04520cf231f4d63d9c4fa546a7196119a20442cd55c3f2ecd7bbb7229858ad65'
            '5f06a33a729795e85320c9ac4a4a655a1092a164f6430c1eeb88a2ab5dda6e7b'
            '8a777fd2399e444b3370d4fbac3ea479ffc7c5e205d219a4b9799c1773c210cf'
            '1bf141ea377d09f74fe00fc096866f80edacb47bf5671a2c1427c1dab6a2abb7'
            'cab6c57c54b33b40ca16a16ab6e9c6592c793592e88aaea6c2d898b36aa47a7f'
            '5d4345085ef6a263c465cdee649a5fb58849e565519f737ee8f26f2dca3b56cc'
            'b7d8f85ae38330ba10cdd19243f6464660b5d2cb40d5e4fdb132686874c5327c'
            '4092be8a6d7b22e16935ee731dd4c77627cf92aab6b2acd45a993c6587fc1e20'
            '879d202431b10a02facdd35d0754e16e18aba9c8a3e291612db2910703c3e533'
            '8bcd31b0f54fbaf7f77d0e141ed85ed695ddc2758370d31022a763d4cb8c5f6b'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
