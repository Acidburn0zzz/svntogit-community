# $Id$
# Maintainer: M0Rf30
# Contributor: marlock

pkgname=android-udev
pkgver=r182
pkgrel=1
pkgdesc='Udev rules to connect Android devices to your linux box'
arch=(any)
url='https://github.com/M0Rf30/android-udev-rules'
license=(GPL3)
depends=(udev libmtp)
makedepends=(git)
install='android-udev.install'

# instead of using HEAD version let's depend on some known revision
_git_revision=f276ea1be761acb4a5987591a7a9e103e49afe80
source=(git+https://github.com/M0Rf30/android-udev-rules.git#commit=$_git_revision)
md5sums=('SKIP')

pkgver() {
  cd android-udev-rules
  printf "r%s" "$(git rev-list --count HEAD)"
}

package() {
  install -Dm 644 android-udev-rules/51-android.rules "$pkgdir"/usr/lib/udev/rules.d/51-android.rules
}
