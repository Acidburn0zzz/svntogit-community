# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pip-api
pkgver=0.0.11
pkgrel=1
pkgdesc="An unofficial, importable pip API"
url="https://github.com/di/pip-api"
license=('Apache')
arch=('any')
depends=('python-packaging' 'python-pip')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pretend' 'python-virtualenv')
source=("$pkgname-$pkgver.tar.gz::https://github.com/di/pip-api/archive/$pkgver.tar.gz")
sha512sums=('efbf11dd458c31bb88ad604aaeadf6c5d6c6658607f15b8fab7848d4bcb093aff5dc543056cdd2539b1949246318a8d9147f7c65b3edcb298b967fe79ca9d15c')

build() {
  cd pip-api-$pkgver
  python setup.py build
}

check() {
  cd pip-api-$pkgver
  python setup.py pytest
}

package() {
  cd pip-api-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
