# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Michal Soltys <soltys@ziu.info>

pkgname=arptables
pkgver=0.0.3_4
pkgrel=3.1
pkgdesc='ARP filtering utility'
arch=('i686' 'x86_64')
url='http://ebtables.sourceforge.net/'
license=('GPL')
backup=("etc/$pkgname.conf")
install=$pkgname.install
source=("http://downloads.sourceforge.net/ebtables/$pkgname-v${pkgver//_/-}.tar.gz"
        "$pkgname.systemd"
        "$pkgname.service")
md5sums=('1d4ab05761f063b0751645d8f2b8f8e5'
         'e54342a833c52b2bc67e7985e8f8fdfd'
         'b26771191e52905d8aea6333c26cb1c3')

build() {
  cd $pkgname-v${pkgver//_/-}
  make
}

package() {
  pushd $pkgname-v${pkgver//_/-}
  make install \
    DESTDIR="$pkgdir" \
    PREFIX=/usr \
    LIBDIR=/usr/lib/arptables \
    BINDIR=/usr/sbin \
    MANDIR=/usr/share/man \
    INITDIR=/etc/rc.d \
    SYSCONFIGDIR=/etc
  popd
  # systemd
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
