# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=6.0.1
pkgrel=2
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=16c20c516791fb07d2a4ebd38bbcbbd7250a899f
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
sha256sums=('SKIP')

prepare() {
  cd gala

  # mutter 9
  git cherry-pick -n 87d51c50fbd5 605aa10ea2a7 9a137bd7071c
}

pkgver() {
  cd gala
  git describe --tags
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
