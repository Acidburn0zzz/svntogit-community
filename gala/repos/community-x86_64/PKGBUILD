# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=6.2.1.r86.78a5f881
pkgrel=1
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_commit=78a5f881ba329ea28ade0c3c91fe18521135877a
source=(git+https://github.com/elementary/gala.git#commit=${_commit})
sha256sums=('SKIP')

prepare() {
  cd gala
}

pkgver() {
  cd gala
  git describe --tags | sed 's/^v//; s/-/.r/; s/-g/./'
}

build() {
  arch-meson gala build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}

# vim: ts=2 sw=2 et:
