# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gala
pkgver=6.0.0
pkgrel=4
pkgdesc='The Pantheon Window Manager'
arch=(x86_64)
url=https://github.com/elementary/gala
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  bamf
  cairo
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  json-glib
  libdrm
  libgee
  libgexiv2
  libgl
  libgranite.so
  libx11
  libxcomposite
  libxdamage
  libxext
  libxfixes
  libxi
  libxrandr
  libxtst
  mesa
  mutter
  pango
  plank
  wayland
)
makedepends=(
  cmake
  git
  gnome-common
  intltool
  meson
  vala
)
provides=(libgala.so)
_tag=70408457186a03d61d975386ce7a25beb86191af
source=(git+https://github.com/elementary/gala.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd gala
  git cherry-pick -n 22e85fe1c8909906e726eafcc3ad4f69b23ee15d
}

pkgver() {
  cd gala
  git describe --tags
}

build() {
  arch-meson gala build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
