# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=3.00
pkgrel=2
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('i686' 'x86_64')
depends=('gmp' 'libcl')
optdepends=('libxnvctrl: NVIDIA X driver configuration support'
            'opencl-nvidia: OpenCL support for NVIDIA'
            'opencl-mesa: OpenCL support for AMD/ATI Radeon mesa drivers')
makedepends=('opencl-headers')
license=('MIT')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/hashcat/hashcat/archive/v${pkgver}.tar.gz
        ${pkgname}-${pkgver}.tar.gz.asc::https://hashcat.net/files/v${pkgver}.tar.gz.asc
        makefile.patch)
sha512sums=('cdaf00830a1f4b52b008c5c6499cd6246eb328a1711be1446ea7b8b3d6ad9afeb9f3626cf9cef58a04b4a414d10e09e5a8429bc47684a9d6dbf511effbc2eedb'
            'SKIP'
            'be389cce5f325437b965c755819c09d6b997c534caebd74a947659f8d950e9b3cee365c993928c5522188e53412156eb64218dd844952125de96e7b764716a7d')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/makefile.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
