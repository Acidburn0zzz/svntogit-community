# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=3.10
pkgrel=1
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('i686' 'x86_64')
depends=('gmp' 'libcl')
optdepends=('libxnvctrl: NVIDIA X driver configuration support'
            'opencl-nvidia: OpenCL support for NVIDIA'
            'opencl-mesa: OpenCL support for AMD/ATI Radeon mesa drivers')
makedepends=('opencl-headers')
license=('MIT')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/hashcat/hashcat/archive/v${pkgver}.tar.gz
${pkgname}-${pkgver}.tar.gz.asc::https://hashcat.net/files/hashcat-${pkgver}.tar.gz.asc)
sha512sums=('2adf16513118b91085fe587c53b15f142abb7673f659b2721dcba8aacdac6f024b65e7b50b916f86c161c21e98eb4758f187d25ad1ca4c66cdb67a259b07ae04'
            'SKIP')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -Dm 644 docs/license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
