# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=seahorse-nautilus
pkgver=3.6.0
pkgrel=1
pkgdesc='PGP encryption and signing for nautilus'
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/seahorse-nautilus/"
license=('GPL')
depends=('libcryptui' 'gcr' 'nautilus')
makedepends=('intltool')
replaces=('seahorse-plugins')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix-schema.patch)
sha256sums=('edae97cf7bab38ed928e25a7805913977f892ec9b9120783d874cda07912519d'
            '3c5a3fe557fc56d5d5cda12d4b4909f513260da9b439d8bbaca2bf42b4fd114c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix-schema.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
