# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=libparserutils
pkgver=0.1.1
pkgrel=1
pkgdesc="Library for building efficient parsers"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/projects/libparserutils"
license=('MIT')
source=("http://www.netsurf-browser.org/projects/releases/${pkgname}-${pkgver}-src.tar.gz")
depends=('glibc')
md5sums=('4f228516863de33f33f7f4258315867e')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	sed 's:-Wall::' -i Makefile
	make PREFIX=/usr COMPONENT_TYPE="lib-shared"
	make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-shared" 
	make install PREFIX=/usr DESTDIR="${pkgdir}" COMPONENT_TYPE="lib-static"
	install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}

# vim:set ts=2 sw=2 et:
