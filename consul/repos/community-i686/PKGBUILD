# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=consul
pkgver=0.8.0
pkgrel=1
pkgdesc="A tool for service discovery, monitoring and configuration."
arch=('i686' 'x86_64')
url="https://www.consul.io"
license=('MPL')
depends=('glibc')
makedepends=('git' 'go')
source=("git+https://github.com/hashicorp/consul.git#tag=v$pkgver"
        consul.service)
sha512sums=('SKIP'
            '0f39434e73a3f5d79919e3fad28de834688765edebfce8793aa471f272af61d9cebd13237a7aeb4f8b89f1749a891a63cd710881d58a9590990deedd43dffdba')

prepare() {
  cd consul

  mkdir build
  mkdir -p .gopath/src/github.com/hashicorp
  ln -sf "$PWD" .gopath/src/github.com/hashicorp/consul
  export GOPATH="$PWD/.gopath"
}

build() {
  cd consul

  go get github.com/mitchellh/cli
  go build -o build/consul # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd consul
  
  install -Dm755 build/consul "$pkgdir"/usr/bin/consul
  install -Dm644 "$srcdir"/consul.service "$pkgdir"/usr/lib/systemd/system/consul.service
  install -d "$pkgdir"/etc/consul.d
}
