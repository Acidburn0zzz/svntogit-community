# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=consul
pkgver=0.8.2
pkgrel=1
pkgdesc="A tool for service discovery, monitoring and configuration."
arch=('i686' 'x86_64')
url="https://www.consul.io"
license=('MPL')
depends=('glibc')
makedepends=('git' 'go')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/consul/archive/v$pkgver.tar.gz"
        consul.service)
sha512sums=('13bd28f0da488ee8fb3068515e2fddc7fe2d663c8848357b689332c44d757c1f3a017c604a68958f1fa22b5d6b58e3b09574a8fff9ab41d74ab6aec347b356d0'
            '0f39434e73a3f5d79919e3fad28de834688765edebfce8793aa471f272af61d9cebd13237a7aeb4f8b89f1749a891a63cd710881d58a9590990deedd43dffdba')

prepare() {
  cd consul-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/hashicorp
  ln -sf "$PWD" .gopath/src/github.com/hashicorp/consul
  export GOPATH="$PWD/.gopath"

  sed -e "s/Version           = \"unknown\"/Version           = \"$pkgver\"/" \
      -e "s/VersionPrerelease = \"unknown\"/VersionPrerelease = \"\"/" \
      -e "s/release = \"dev\"/release = \"\"/" \
      -i version/version.go
}

build() {
  cd consul-$pkgver

  go get github.com/mitchellh/cli
  go build -o build/consul # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd consul-$pkgver
  
  install -Dm755 build/consul "$pkgdir"/usr/bin/consul
  install -Dm644 "$srcdir"/consul.service "$pkgdir"/usr/lib/systemd/system/consul.service
  install -d "$pkgdir"/etc/consul.d
}
