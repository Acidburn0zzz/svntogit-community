# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vm.lv2
pkgver=0.8.0
pkgrel=1
pkgdesc="A virtual machine LV2 plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/vm/#"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'libglvnd')
makedepends=('glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2//$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
sha512sums=('343be1c9f48f2a10995bc10cc774eaf89228ef7b5e76f5da75cd8d9fe396cf583db039e432e919fab3d39f37732282e46dd998b448baf06950ea4e36ec69fe74')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('atom' 'audio' 'control' 'cv' 'midi')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/vm#${_plugin}"
  done
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
