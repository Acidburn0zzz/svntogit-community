# Maintainer: David Runge <dave@sleepmap.de>
pkgname=vm.lv2
pkgver=0.6.0
pkgrel=1
pkgdesc="A virtual machine LV2 plugin bundle"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/vm/#"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('libgl')
makedepends=('lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lv2//$pkgname/snapshot/$pkgname-$pkgver.tar.xz")
md5sums=('7313588b1e5b9794fc70c1b7acb31134')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

# check fails due to currently broken lv2_validate in lv2
# check() {
#   cd "$pkgname-$pkgver"
#   meson test -C build
# }

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
