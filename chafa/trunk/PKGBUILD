# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=chafa
pkgver=1.0.1
pkgrel=1
pkgdesc="Image-to-text converter supporting a wide range of symbols and palettes, transparency, animations, etc."
arch=("x86_64")
url="https://hpjansson.org/chafa/"
depends=('libmagick' 'libxslt')
makedepends=('gtk-doc')
license=("LGPL")
source=(https://github.com/hpjansson/chafa/releases/download/$pkgver/chafa-$pkgver.tar.xz{,.asc})
sha512sums=('fe0dc8ecec77c7b1b4c694d6dd3f4de895a07faf39519f47c2284d76261e2c6298943fa5309cdca6d41ef0a22fe0321a2ef890f319903596bdfaad10394e7ec2'
            'SKIP')
validpgpkeys=('C01EDE5BB0D91E26D003662EC76BB9FEEAD12EA7')  # Hans Petter Jansson

prepare() {
  cd $pkgname-$pkgver
  sed -i -e 's|wand/MagickWand.h|MagickWand/MagickWand.h|' \
         -e 's|$LDFLAGS $MAGICKWAND_LIBS|$MAGICKWAND_LIBS $LDFLAGS|' \
         configure chafa/chafa.c
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --enable-man
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
