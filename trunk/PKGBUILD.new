# $Id: PKGBUILD 176067 2016-05-21 17:05:53Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.4
pkgrel=10
pkgdesc='Pixelart-oriented painting program'
arch=('x86_64' 'i686')
url='https://gitlab.com/GrafX2/grafx2'
license=('GPL2')
depends=('lua' 'fontconfig' 'sdl2_image' 'sdl2_ttf')
makedepends=('gendesk' 'git')
options=('!emptydirs')
source=("git+https://gitlab.com/GrafX2/grafX2.git#commit=e60d0179") # sdl2 branch
sha256sums=('SKIP')

prepare() {
  ln -sf "grafX2" "$pkgname"
  gendesk -f -n \
    --pkgname "$pkgname" \
    --pkgdesc "$pkgdesc" \
    --name 'GrafX2' \
    --genericname 'Drawing program' --exec 'grafx2 %f' \
    --mimetypes 'image/bmp;image/gif;image/png;image/x-tga;image/tiff' \
    --categories 'Graphics;2DGraphics;RasterGraphics'
}

build() {
  make -C "$pkgname/src"
}

package() {
  make -C "$pkgname/src" prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/readme.txt"
}

# vim:set ts=2 sw=2 et:
