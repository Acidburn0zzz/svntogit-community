# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.11.0
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libevent gc)
makedepends=(crystal)
checkdepends=(git)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('d1b58ec81c6acc2bf7ecad4e2868e05f74a57647318a167c7324f706b568bfff')

build() {
  cd shards-$pkgver
  CRYSTAL_OPTS="--release" make
}

check() {
  cd shards-$pkgver
  # tests are currently failing. TODO: fix it
  # make test -j1 # https://github.com/crystal-lang/shards/issues/347
}

package() {
  cd shards-$pkgver
  PREFIX=/usr DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
