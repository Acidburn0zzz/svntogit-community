# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" "${pkgbase/-/2-}")
pkgver=4.55
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.python.org/packages/8f/af/3a235696acd1d4e211e2546d5c417358de9aac59e45bd6a8ad83f410e60d/Pyro4-4.55.tar.gz#md5=0400a4d260dbeaa4e7e725c4ba310ead")
md5sums=('0400a4d260dbeaa4e7e725c4ba310ead')

package_python-pyro() {
  depends=('python')

  cd "Pyro4-$pkgver"

  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')

  cd "Pyro4-$pkgver"

  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
