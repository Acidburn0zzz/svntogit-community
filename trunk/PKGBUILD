# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cliff
pkgname=(python-cliff python2-cliff)
pkgver=2.6.0
pkgrel=1
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="http://docs.openstack.org/developer/cliff"
license=('Apache')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-cmd2' 'python2-cmd2' 'python-prettytable' 'python2-prettytable'
             'python-pyparsing' 'python2-pyparsing' 'python-stevedore'
             'python2-stevedore' 'python-unicodecsv' 'python2-unicodecsv' 'python-yaml'
             'python2-yaml')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-docutils'
              'python2-docutils')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('0925fbc0b7e80c6da72994d04d1d3ab545db17a235e2bd3d78b761503cbd291e74c3649073d3bb8158aa60e96cede4a36fba88fff862056c3eb3dbb08cfb1a54')

prepare() {
  cp -a cliff-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/cliff-$pkgver
  python setup.py build

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cliff-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests3 -d

  cd "$srcdir"/cliff-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" nosetests2 -d
}

package_python-cliff() {
  depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
           'python-stevedore' 'python-unicodecsv' 'python-yaml')

  cd "$srcdir"/cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cliff() {
  depends=('python2-six' 'python2-pbr' 'python2-cmd2' 'python2-prettytable' 'python2-pyparsing'
           'python2-stevedore' 'python2-unicodecsv' 'python2-yaml')

  cd "$srcdir"/cliff-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
