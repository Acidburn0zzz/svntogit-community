# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Eric Bélanger <eric@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Contributor: kritoke <kritoke@nospam.gmail.com>

pkgbase=ruby-gtk2
pkgname=('ruby-atk' 'ruby-gdkpixbuf2' 'ruby-gio2' 'ruby-glib2' 'ruby-gtk2'
         'ruby-pango')
pkgver=1.2.1
pkgrel=4
arch=('x86_64' 'i686')
url='http://ruby-gnome2.sourceforge.jp/'
license=('LGPL')
makedepends=('ruby-pkgconfig' 'ruby-cairo' 'gtk2' 'glib2')
source=("http://downloads.sourceforge.net/ruby-gnome2/ruby-gtk2-$pkgver.tar.gz"
        '0001-backport-https-github.com-ruby-gnome2-ruby-gnome2-co.patch')
sha256sums=('cb6e329f64042c8d24248204e11bb4652f9b2984ac2965d82dab49634deb1125'
            '9180b6b8d146b815de63bd2c4f0e215ab7591e3ebaca913120ae962695f3c388')

build() {
  cd "$srcdir/ruby-gtk2-$pkgver"
  patch -Np1 -i ${srcdir}/0001-backport-https-github.com-ruby-gnome2-ruby-gnome2-co.patch
  #find . -name extconf.rb -exec sed -i 's/glib2/glib-2.0/' {} \;
  #sed -i 's/glib2/glib-2.0/' extconf.rb
  LANG="en_US.UTF-8" \
    ruby extconf.rb atk gdk_pixbuf2 gio2 glib2 gtk2 pango --vendor
  mkdir -p "$pkgdir/ruby-gdkpixbuf2/usr/lib/ruby/site_ruby/2.0.0/i686-linux"
  mkdir -p "$pkgdir/ruby-gio2/usr/lib/ruby/site_ruby/2.0.0/x86_64-linux"
  export CFLAGS="$CFLAGS `pkg-config --libs glib-2.0`"
  export CXXFLAGS="$CXXFLAGS `pkg-config --libs glib-2.0`"
  make
}

package_ruby-atk() {
  pkgdesc='Ruby bindings for atk'
  depends=("ruby-glib2=$pkgver" 'atk')

  cd "$srcdir/ruby-gtk2-$pkgver/atk"
  export CFLAGS="$CFLAGS `pkg-config --libs glib-2.0`"
  export CXXFLAGS="$CXXFLAGS `pkg-config --libs glib-2.0`"
  make includedir="/usr/include" DESTDIR="$pkgdir" install
}

package_ruby-gdkpixbuf2() {
  pkgdesc='Ruby bindings for gdkpixbuf2'
  depends=("ruby-glib2=$pkgver" 'gdk-pixbuf2')

  cd "$srcdir/ruby-gtk2-$pkgver/gdk_pixbuf2"
  export CFLAGS="$CFLAGS `pkg-config --libs glib-2.0`"
  export CXXFLAGS="$CXXFLAGS `pkg-config --libs glib-2.0`"
  make includedir="/usr/include" DESTDIR="$pkgdir" install
}

package_ruby-gio2() {
  pkgdesc='Ruby bindings for gio2'
  depends=("ruby-glib2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gio2"
  export CXXFLAGS="$CXXFLAGS -lglib-2.0"
  export CFLAGS="$CFLAGS -lglib-2.0"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install
}

package_ruby-glib2() {
  pkgdesc='Ruby bindings for glib2'
  depends=('glib2' 'ruby')

  cd "$srcdir/ruby-gtk2-$pkgver/glib2"
  export CXXFLAGS="$CXXFLAGS -lglib-2.0"
  export CFLAGS="$CFLAGS -lglib-2.0"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install
}

package_ruby-gtk2() {
  pkgdesc='Ruby bindings for gtk2'
  depends=('gtk2' "ruby-glib2=$pkgver" "ruby-pango=$pkgver" "ruby-atk=$pkgver"
           "ruby-gdkpixbuf2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gtk2"
  export CFLAGS="$CFLAGS `pkg-config --libs glib-2.0`"
  export CXXFLAGS="$CXXFLAGS `pkg-config --libs glib-2.0`"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install
}

package_ruby-pango() {
  pkgdesc='Ruby bindings for pango'
  depends=("ruby-glib2=$pkgver" 'ruby-cairo' 'pango')

  cd "$srcdir/ruby-gtk2-$pkgver/pango"
  export CFLAGS="$CFLAGS `pkg-config --libs glib-2.0`"
  export CXXFLAGS="$CXXFLAGS `pkg-config --libs glib-2.0`"
  make includedir="/usr/include" DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
