# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcached
pkgver=3.0.4
pkgrel=3
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('x86_64')
url="https://github.com/php-memcached-dev/php-memcached"
license=('PHP')
depends=('php' 'libmemcached')
makedepends=('git')
backup=('etc/php/conf.d/memcached.ini')
source=("git+https://github.com/php-memcached-dev/php-memcached.git#tag=v$pkgver"
        'php-7.3.patch')
sha256sums=('SKIP'
            '14f68fdb7352fedc4a086acd8fa68eda32299facfc56a89ee01bf331828d4aef')

prepare() {
  cd "$srcdir/$pkgname"

  patch -p1 -i "$srcdir/php-7.3.patch"
}

build() {
  cd "$srcdir/$pkgname"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname"

  make INSTALL_ROOT="$pkgdir" install

  install -d "$pkgdir/etc/php/conf.d"
  echo ';extension=memcached.so' >"$pkgdir/etc/php/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
