# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cozy-stack
pkgver=2018M2S1
pkgrel=1
pkgdesc="Digital home: brings all your web services in the same private space â€“ Stack component"
arch=('x86_64')
url="https://cozy.io"
license=('AGPL3')
backup=('etc/cozy/cozy.yml')
depends=('couchdb' 'imagemagick' 'git')
makedepends=('go-pie')
optdepends=('nodejs: konnectors without isolation'
            'nsjail: isolated konnectors'
            'smtp-forwarder: to allow sending mail to users')
source=("https://apt.cozy.io/debian/pool/testing/c/${pkgname}/${pkgname}_${pkgver}.orig.tar.xz"
        "cozy.yml"
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('eb0eb9591e8faf236468a72243261fb174825e0fbe540e2b2fc00591e963958a'
            '66b2c6be56f70896308974de35d94cedf0124736acd2bdd0b64f0f46d26fd034'
            'f0a8cc43c51daeba92b36b449537eb6fa5d3fb84ef1428dc586266749ed742e0'
            'a6bea52350e85163c3141509a52903223fa0f6e7390b1b1f9336c326a8fff984'
            '2851ae1dbe60d83a2ee919b783be8c6bd39516c0290f43270605bebe7c9b22f2')

build() {
    cd cozy-stack/src/github.com/cozy/cozy-stack
    GOPATH="${srcdir}"/cozy-stack go build -o "${srcdir}"/bin/cozy-stack -ldflags "\
		-X github.com/cozy/cozy-stack/pkg/config.Version=${pkgver}) \
		-X github.com/cozy/cozy-stack/pkg/config.BuildTime=$(date -u +"%Y-%m-%dT%H:%M:%SZ") \
        -X github.com/cozy/cozy-stack/pkg/config.BuildMode=production"
}

package() {
    install -Dm755 bin/cozy-stack -t "${pkgdir}"/usr/bin/
    install -Dm644 cozy.yml -t "${pkgdir}"/etc/cozy/
    install -Dm644 cozy-stack/src/github.com/cozy/cozy-stack/cozy.example.yaml -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 cozy-stack/src/github.com/cozy/cozy-stack/scripts/konnector-node-run.sh -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 cozy-stack/src/github.com/cozy/cozy-stack/scripts/konnector-nsjail-run.sh -t "${pkgdir}"/usr/share/cozy/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
