# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=retroarch-assets
pkgname=(
  retroarch-assets-ozone
  retroarch-assets-xmb
)
pkgver=319
pkgrel=1
epoch=1
pkgdesc='XMB menu assets for RetroArch'
arch=(any)
url=http://www.libretro.com/
license=(GPL)
groups=(libretro)
depends=(retroarch)
makedepends=(git)
_commit=0ac74a9b2503f5a19e0a9ab1985e5fab8c64993d
source=(git+https://github.com/libretro/retroarch-assets.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd retroarch-assets

  git rev-list --count HEAD
}

package_retroarch-assets-ozone() {
  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership ozone "${pkgdir}"/usr/share/retroarch/assets/
}

package_retroarch-assets-xmb() {
  cd retroarch-assets

  install -dm 755 "${pkgdir}"/usr/share/retroarch/assets
  cp -dr --no-preserve=ownership xmb "${pkgdir}"/usr/share/retroarch/assets/
}

# vim: ts=2 sw=2 et:
