# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.18.18
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=('x86_64')
url="https://github.com/pymupdf/PyMuPDF"
license=('AGPL3')
depends=('glibc' 'gumbo-parser' 'jbig2dec' 'libmupdf' 'openjpeg2' 'python')
makedepends=('freetype2' 'libjpeg-turbo' 'python-distro' 'python-setuptools' 'swig' 'systemd')
checkdepends=('python-pillow' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('16c933684c6164abeef4035b8edb3ae016bd8c02d484c4c74fef87f42be4ce1643d43deb5766d78f0c65aafc345097daaaad49cbcff7f1cf7e20bb8d73399571')
b2sums=('87bd12fc65413dfc7fc963183a2c81e63ab1e7e18338dfaec1a1950bab3eb874fbfcd034fd40abd0ef5b59ec981b59954f94b713ead2a923630a6c3b240243ab')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"
  cd "$pkgname-$pkgver"
  mkdir -vp "${_test_dir}"
  # install to test dir for testing
  python setup.py install --optimize=1 --root="${_test_dir}"

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  pytest -v -k "not test_font1 and not test_pageids and not test_textbox3"
}

package() {
  depends+=('libfreetype.so' 'libjpeg.so')

  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
