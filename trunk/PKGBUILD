# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: James Zhu <jameszhu@berkeley.edu>

pkgname=python-black
pkgver=21.5b0
pkgrel=1
pkgdesc='Uncompromising Python code formatter'
arch=('any')
url=https://github.com/psf/black
license=('MIT')
depends=('python' 'python-appdirs' 'python-click' 'python-mypy_extensions'
         'python-pathspec' 'python-regex' 'python-toml')
makedepends=('python-setuptools-scm')
checkdepends=('python-aiohttp' 'python-aiohttp-cors' 'python-parameterized'
              'python-pytest' 'python-typed-ast')
optdepends=('python-aiohttp: for the blackd HTTP server'
            'python-aiohttp-cors: for the blackd HTTP server'
            'python-colorama: for colored diffs'
            'python-typed-ast: to format Python 2 code')
source=("https://files.pythonhosted.org/packages/source/b/black/black-$pkgver.tar.gz")
sha256sums=('9dc2042018ca10735366d944c2c12d9cad6dec74a3d5f679d09384ea185d9943')
b2sums=('1f6f18f56cfe948c8efe9589b357b7ef957bf4bf79417e539ee52e9ce42a7056b482b1599f39e719cab1e35a48d43ecea33396ae52c2d341689e884513cc4867')

build() {
  cd "black-$pkgver"
  python setup.py build
}

check() {
  cd "black-$pkgver"
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --skip-build --root=temp
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" pytest tests -m "not without_python2"
}

package() {
  cd "black-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}

# vim:set ts=2 sw=2 et:
