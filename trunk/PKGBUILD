# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.9.2
pkgrel=1
pkgdesc="Command-line YAML processor - jq wrapper for YAML documents"
url="https://github.com/kislyuk/yq"
depends=('jq' 'python-yaml' 'python-xmltodict' 'python-toml')
makedepends=('python-setuptools')
checkdepends=('flake8' 'python-coverage' 'python-wheel')
license=('Apache')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('9a169c2cbdabf2e3151e7e289ac9aa94ee4284c135eacb630703102d6e321edd')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
