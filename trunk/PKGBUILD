# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.2.4
_helper_gitrev=4b68fcf6d26326d80d41bf4d889d156312bf231a
pkgrel=3
pkgdesc="Modern desktop environment from the Solus Project"
arch=('i686' 'x86_64')
url="https://solus-project.com/budgie"
license=('GPL' 'LGPL')
depends=('gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
install=$pkgname.install
source=("https://github.com/solus-project/budgie-desktop/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev"
        "0001-panel-Fix-missing-panel-background.patch"
        "0001-Port-CSS-files-to-GTK-3.20.patch")
sha256sums=('b01e7be9349cbc26cd77db74a0c138d21c31d8bad21a02b31828014b416256f7'
            'd558ce2b9978bd383572ac29939bebaa8d7271382686035d4a8373701afe85df'
            '7212d6f08c7ceaeefc0f97ad125b8a04b6f689a31a7059677067f9e111f54eb6'
            'cc7d7d7c0be0e445c349c6a0eb60f2bbcf7e76f26f56cf3dec6e0618d9b0ac26')

prepare() {
	cd $pkgname-$pkgver
	# Disable workspaces applet as it's broken
	# https://github.com/solus-project/budgie-desktop/issues/302
	sed -i ':a;N;$!ba;s/ \\\n\tworkspaces//' panel/applets/Makefile.am
	autoreconf -fi

	# Remove preferences button from rundialog as it's broken
	# https://github.com/solus-project/budgie-desktop/issues/360
        sed -i '/box.add(preferences);/d' rundialog/RunDialog.vala

	# panel: Fix missing panel background
	# https://github.com/solus-project/budgie-desktop/pull/358
	patch -Np1 -i ../0001-panel-Fix-missing-panel-background.patch

	# Port CSS files to GTK+ 3.20 (prepared for upgrade)
	# https://github.com/solus-project/budgie-desktop/issues/359
	# patch -Np1 -i ../0001-Port-CSS-files-to-GTK-3.20.patch

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile

	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

	make

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
