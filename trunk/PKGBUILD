# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.4.1
pkgrel=2
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools'
             'python2-setuptools' 'git')
checkdepends=('python-nose' 'python2-nose')
source=("git+https://github.com/gabrielfalcao/sure.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a sure{,-py2}
}

check() {
  cd "$srcdir"/sure
  python setup.py test

  cd "$srcdir"/sure-py2
  python2 setup.py test
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="$pkgdir" --optimize=1
}
