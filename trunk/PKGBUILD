# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

_pkgbasename=libva-intel-driver
pkgname=lib32-libva-intel-driver
pkgver=2.3.0
pkgrel=1
pkgdesc='VA-API implementation for Intel G45 and HD Graphics family (32-bit)'
arch=('x86_64')
url='https://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libva')
source=("https://github.com/01org/intel-vaapi-driver/releases/download/$pkgver/intel-vaapi-driver-$pkgver.tar.bz2")
sha512sums=('60f37c09269e6ae07e55ecfc2c046ae7b6321abe71647625310ea7c75627ea875b94ed8e14813a36ec068bbd605c6d447dfa2c3bd692eb7953a58a871064fd5b')

prepare() {
  cd intel-vaapi-driver-${pkgver}

  # Only relevant if intel-gpu-tools is installed,
  # since then the shaders will be recompiled
  sed -i '1s/python$/&2/' src/shaders/gpp.py
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd intel-vaapi-driver-${pkgver}

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make 
}

package() {
  cd intel-vaapi-driver-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/$pkgname/COPYING
}
