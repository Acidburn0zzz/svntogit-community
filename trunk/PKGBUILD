# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Thore Bödecker <foxxx0@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian Köhler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.3.3
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.5.3
pkgrel=1

pkgdesc='Sieve implementation for Dovecot'
url='http://pigeonhole.dovecot.org/'
arch=('x86_64')
license=('LGPL')

depends=("dovecot=${_dcpkgver}")

conflicts=('dovecot-sieve' 'pigeonhole-hg')

source=("https://pigeonhole.dovecot.org/releases/2.3/dovecot-2.3-pigeonhole-${pkgver}.tar.gz"{,.sig})

sha256sums=('e02f2741c05f9e2af1c5f46da35efb74997f9d4b941ba68936206d310ddf2622'
            'SKIP')
validpgpkeys=('42F3CD50D4F25A41833BEE3704D62B1E3DFBB4F4') # Stephan Bosch <stephan@rename-it.nl>

build() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"

  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot \
    --with-moduledir=/usr/lib/dovecot/modules \
    --disable-static
  make
}

check() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"
  make check
}

package() {
  cd "dovecot-2.3-pigeonhole-${pkgver}"

  make DESTDIR="$pkgdir" install
}
