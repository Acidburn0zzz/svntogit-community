# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Rolinh <robinDOThahlingATgw-computingDOTnet>

pkgname=dfc
pkgver=3.0.5
pkgrel=1
pkgdesc="Display file system space usage using graphs and colors"
arch=('x86_64' 'i686')
url="http://projects.gw-computing.net/projects/dfc"
license=('BSD')
depends=('glibc')
makedepends=('cmake' 'gettext')
conflicts=('dfc-git')
source=("https://projects.gw-computing.net/attachments/download/467/${pkgname}-${pkgver}.tar.gz")
install='dfc.install'
sha512sums=('f828ede8aee0a496518d1ee9583ba71495cdd01ee0d22833e3b46aeb5f5f870ce7de629923d129a7bf795b458feec10f9a16882134dae34dd2ace7ea9eebb134')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cmake . -DPREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
