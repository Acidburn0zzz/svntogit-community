# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.10.0
pkgrel=1
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Almanah_Diary"
license=('GPL')
depends=('evolution-data-server>=3.6.0' 'gtkspell3' 'libcryptui' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('fd213b53ca2a9832df868ee44ed7c446cd6b320651e2f2d10e0a0c98aa3bfced')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --disable-schemas-compile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
