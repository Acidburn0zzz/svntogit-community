# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Jon Bergli Heier <snakebite@jvnv.net>

pkgname=('python2-pyxattr' 'python-pyxattr')
pkgbase=python-pyxattr
_pkgname=pyxattr
pkgver=0.6.1
pkgrel=2
pkgdesc='A python extension module that allows you to manipulate the extended attributes.'
arch=('x86_64')
url='https://pyxattr.k1024.org/'
license=('GPL')
makedepends=('python2' 'python' 'python2-setuptools' 'python-setuptools')
source=("https://pyxattr.k1024.org/downloads/pyxattr-$pkgver.tar.gz"{,.asc})
md5sums=('523e9d90f9801141c785d93e6197cc33'
         'SKIP')
validpgpkeys=('CB94E3AA3B1755D61EBB19A5F66E3E419F84F4DE') # Iustin Pop <iustin@k1024.org>

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py test
  python2 setup.py test
}

package_python2-pyxattr() {
  depends=('python2')
  provides=('pyxattr')
  conflicts=('pyxattr')
  replaces=('pyxattr')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}

package_python-pyxattr() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}"
}
