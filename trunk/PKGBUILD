# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-webcolors
pkgver=1.11
pkgrel=1
pkgdesc="A library for working with color names and color value formats defined by the HTML and CSS specifications for use in documents on the Web"
url="https://github.com/ubernostrum/webcolors"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/w/webcolors/webcolors-$pkgver.tar.gz")
sha512sums=('7b46a4a17794184a61f10cc6c3017fd9108ba7ae688eaeea508754318d05513e9a3ab9376331ccecb1e617f417c9cb5e6ae3ffd794865e7402942680c763c94d')

build() {
  cd webcolors-$pkgver
  python setup.py build
}

check() {
  cd webcolors-$pkgver
  python setup.py pytest
}

package() {
  cd webcolors-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
