# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Radek Podgorny <radek@podgorny.cz>
# Contributor: Dan Anderson <dan-anderson at cox dptnet>

pkgname=mosquitto
pkgver=1.4.11
pkgrel=2
pkgdesc="An Open Source MQTT v3.1/v3.1.1 Broker"
arch=('i686' 'x86_64')
url="http://mosquitto.org/"
depends=('openssl' 'c-ares' 'libwebsockets')
makedepends=('docbook-xsl' 'c-ares')
license=('BSD')
source=(https://mosquitto.org/files/source/$pkgname-$pkgver.tar.gz{,.asc} "$pkgname.service" "$pkgname.install" "sysusers_mosquitto.conf")
install=$pkgname.install
backup=("etc/$pkgname/$pkgname.conf")
sha256sums=('ca47533bbc1b7c5e15d6e5d96d3efc59677f2515b6692263c34b7c48f33280c5'
            'SKIP'
            '3480aa1a9da543230fbbb0e7e11cde9db2736e486cd024fbd505f927188fa3a2'
            '7aa6edfc36c5a1d5bd89be4c28721b3f802a1eb67c042f5cfd74366cc4a4cae6'
            'e648d0ce5428c0199ac75c623c8a27e40495a98aa1bf4a22179ed582492f748f')
validpgpkeys=('A0D6EEA1DCAE49A635A3B2F0779B22DFB3E717B7')

build() {
  cd "$pkgname-$pkgver"
  # FIX upstream by making SBINDIR=foo or use CMAKE
  sed -i 's|/sbin|/bin|g' src/Makefile
  make WITH_WEBSOCKETS=yes
}

package() {
  cd "$pkgname-$pkgver"

  make prefix=/usr DESTDIR="$pkgdir/" install

  # Shipped in git.
  install -Dm644 "$srcdir/$pkgname.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  mv "$pkgdir/etc/$pkgname/$pkgname.conf.example" "$pkgdir/etc/$pkgname/$pkgname.conf"

  install -D -m644 "${srcdir}"/sysusers_mosquitto.conf "${pkgdir}"/usr/lib/sysusers.d/mosquitto.conf
}
