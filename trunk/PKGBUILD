# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.4
pkgrel=2
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('python2-docutils')
source=("http://cloud.github.com/downloads/spaetz/offlineimap/offlineimap-v$pkgver.tar.gz"
        '00-fix-readonly.patch')
sha1sums=('a9ad5f32f8bc0ec042f8059ea9d34282bb8b682a'
          'a9faa6d857b0d46a485f7551a3e56f44546370f8')

prepare() {
    cd spaetz-offlineimap-*
    patch -p1 -i "$srcdir/00-fix-readonly.patch"
    find . -type f -exec \
        sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
    sed -i 's/\^/=/g' docs/MANUAL.rst
}

build() {
    cd spaetz-offlineimap-*
    python2 setup.py build
    rst2man2 docs/MANUAL.rst offlineimap.1
}

package() {
    cd spaetz-offlineimap-*
    python2 setup.py install --root="$pkgdir" --optimize=1
    install -Dm644 offlineimap.1 "$pkgdir"/usr/share/man/man1/offlineimap.1
    install -Dm644 offlineimap.conf "$pkgdir"/usr/share/offlineimap/offlineimap.conf
    install -Dm644 offlineimap.conf.minimal "$pkgdir"/usr/share/offlineimap/offlineimap.conf.minimal
}

# vim:set ts=4 sw=4 et:
