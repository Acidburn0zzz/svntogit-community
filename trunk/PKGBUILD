# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Florian Pritz <bluewind@xinu.at>

pkgbase=lib32-harfbuzz
pkgname=(lib32-harfbuzz lib32-harfbuzz-icu)
pkgver=2.6.8
pkgrel=2
pkgdesc="OpenType text shaping engine (32-bit)"
url="https://www.freedesktop.org/wiki/Software/HarfBuzz"
arch=(x86_64)
license=(MIT)
makedepends=(lib32-glib2 lib32-freetype2 lib32-cairo lib32-icu ragel git python
             meson harfbuzz)
checkdepends=(python-fonttools python-setuptools)
_commit=e4203c14699cc0903a2b4611167a7b99532cfdb0  # tags/2.6.8^0
source=("git+https://github.com/harfbuzz/harfbuzz#commit=$_commit"
        0001-meson-Make-Requires-in-.pc-files-match-autotools.patch)
sha256sums=('SKIP'
            'ebf92aed7613da18817fedccab0962e50308b93f8218e79aae36b6587453cbba')

pkgver() {
  cd harfbuzz
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd harfbuzz
  git apply -3 ../0001-meson-Make-Requires-in-.pc-files-match-autotools.patch
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson harfbuzz build \
    --libdir=/usr/lib32 \
    -D introspection=disabled \
    -D gtk_doc=disabled
  meson compile -C build
}

check() {
  TMPDIR="$srcdir" meson test -C build --print-errorlogs
  rm -rf "$srcdir"/tmp*
}

package_lib32-harfbuzz() {
  depends=(lib32-glib2 lib32-freetype2 libglib-2.0.so libfreetype.so
           libgobject-2.0.so harfbuzz)
  provides=(libharfbuzz.so libharfbuzz-subset.so libharfbuzz-gobject.so)

  DESTDIR="$pkgdir" meson install -C build
  rm -rf "${pkgdir}"/usr/{include,share,bin}

  # Split harfbuzz-icu
  mkdir -p hb-icu/usr/lib32/pkgconfig
  mv -t hb-icu/usr/lib32 "$pkgdir"/usr/lib32/libharfbuzz-icu*
  mv -t hb-icu/usr/lib32/pkgconfig "$pkgdir"/usr/lib32/pkgconfig/harfbuzz-icu.pc

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}

package_lib32-harfbuzz-icu() {
  pkgdesc="$pkgdesc (ICU integration)"
  depends=("lib32-harfbuzz=$pkgver-$pkgrel" lib32-icu libharfbuzz.so harfbuzz-icu)
  provides=(libharfbuzz-icu.so)

  mv hb-icu/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 harfbuzz/COPYING
}
