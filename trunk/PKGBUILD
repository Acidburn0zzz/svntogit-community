# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: flu

pkgname=sqlcipher
pkgver=3.4.0
pkgrel=2
pkgdesc="SQLite extension that provides transparent 256-bit AES encryption of database files"
arch=('i686' 'x86_64')
url="https://www.zetetic.net/sqlcipher/"
license=('BSD')
makedepends=('openssl' 'sqlite' 'tcl')
source=($pkgname-$pkgver.zip::https://github.com/$pkgname/$pkgname/archive/v$pkgver.zip
        https://www.zetetic.net/$pkgname/verify/$pkgver/$pkgname-$pkgver.zip.sig)
validpgpkeys=('A4EA79E649E745B92117A56F0CB99EE267FD0322') # Zetetic LLC <support@zetetic.net>
sha256sums=('cb558ff8fc3fb49d37817d42d1da6bb85f6310c28186f8e3570783a0f850366e'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-tempstore=yes \
                CFLAGS="$CFLAGS -DSQLITE_HAS_CODEC" LDFLAGS="-lcrypto"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

#license
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
