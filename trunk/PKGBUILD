# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=deepin-anything
pkgname=(deepin-anything deepin-anything-dkms)
pkgver=0.0.4
_extramodules=extramodules-ARCH
pkgrel=1
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
makedepends=('qt5-base')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        deepin-anything-server.sysusers
        linux-4.20.patch)
sha512sums=('fcb7683ce3bef818ac06dd62f14b5624669f9c09b53c5f08e319b724f5408bc221154428a6436d9e2a68aa6444e966c494c8a7611dfed0c93b6b9e71a3da0f55'
            '0ff6a6de1fbfb0c33eaac511b989da321a9e43ece92708af88aee34ad1a05e55572713b1290bc2705d70b91dc7bec4fb4abd3dc664a0abe01de27d88bd9e9c85'
            'dc3c85e9535cc589fdf56d265c4330519c92b5bcc406153e3162ba6ef5e799b702c394e1961132770f4f3ddd288dbbfe74a9d8056329e4585eb5a2094ffe0155')

prepare() {
  cd deepin-anything-$pkgver
  patch -p1 -i ../linux-4.20.patch
  sed -i 's|^systemd_service.path.*|systemd_service.path = /usr/lib/systemd/system|' server/src/src.pro
}

build() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver
}

package_deepin-anything-dkms() {
  depends=('dkms')
  provides=('DEEPIN-ANYTHING-MODULE')
  conflicts=('DEEPIN-ANYTHING-MODULE')

  cd deepin-anything-$pkgver
  install -dm 755 "$pkgdir"/usr/src
  cp -r kernelmod "$pkgdir"/usr/src/deepin-anything-$pkgver
  install -m644 debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
}

package_deepin-anything() {
  depends=('DEEPIN-ANYTHING-MODULE' 'qt5-base')
  groups=('deepin')

  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/src

  install -Dm644 ../deepin-anything-server.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-anything-server.conf"
}
