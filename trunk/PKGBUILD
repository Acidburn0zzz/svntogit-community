# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=frei0r-plugins
pkgver=1.3
pkgrel=5
pkgdesc="A minimalistic plugin API for video sources and filters"
arch=(i686 x86_64)
url="http://www.piksel.org/frei0r"
license=('GPL')
depends=('gcc-libs' 'gavl>=1.0.0' 'opencv>=2.4.3')
makedepends=('graphviz')
options=('!emptydirs')
source=(http://ftp.dyne.org/frei0r/releases/frei0r-plugins-$pkgver.tar.gz)
md5sums=('a2eb63feeeb0c5cf439ccca276cbf70c')

build() {
  cd "$srcdir/frei0r-$pkgver"
  ./configure --prefix=/usr --docdir=/usr/share/doc
  make
}

package() {
  cd "$srcdir/frei0r-$pkgver"
  make DESTDIR="$pkgdir" install \
	htmldocsdir=$pkgdir/usr/share/doc/frei0r-plugins-$pkgver \
	docsdir=$pkgdir/usr/share/doc/frei0r-plugins-$pkgver
  mkdir -p $pkgdir/usr/share/doc/frei0r-plugins-$pkgver/
  mv $pkgdir/$pkgdir/usr/share/doc/frei0r-plugins-$pkgver/* $pkgdir/usr/share/doc/frei0r-plugins-$pkgver/
}
