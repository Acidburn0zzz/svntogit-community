# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=embree
pkgver=3.6.0
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels"
arch=(x86_64)
url="https://embree.github.io/"
license=(Apache)
depends=(intel-tbb)
makedepends=(cmake ispc freeglut libxmu openexr)
source=("https://github.com/embree/embree/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3c179aa09efe1f591f277cd8835e37726998f657730ca456d08f584fc8780559')

build() {
    cd ${pkgname}-${pkgver}
    cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DEMBREE_TUTORIALS=OFF \
      -DEMBREE_MAX_ISA="AVX512SKX"
    # Embree detects actual ISA at runtime
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}

