# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-material-color
pkgver=0.0.0.20200410
_commit=0fb2ef1f7a8a7d7591e2d83222d9d8ab3ed4f774
pkgrel=1
pkgdesc="Material color theme for fcitx5"
arch=('any')
url="https://github.com/hosxy/Fcitx5-Material-Color"
license=('GPL')
source=("https://github.com/hosxy/Fcitx5-Material-Color/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('03f3d1ecc22487a6e563b75bf017284e02a911275b8efc3d7d9d994bd1023ea21ce97f6d51de89d7f6e33f745509ee283312462cae431cd5f62992cf91527370')

package() {
  cd Fcitx5-Material-Color-$_commit
  install -Dm644 arrow.png radio.png -t "$pkgdir"/usr/share/$pkgname/
  for _variant in blue brown deepPurple indigo pink red teal; do
    _variant_name=Material-Color-${_variant^}
    install -Dm644 highlight-$_variant.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/highlight.png
    install -Dm644 panel-$_variant.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/panel.png
    ln -s ../../../$pkgname/arrow.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/radio.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    install -Dm644 theme.conf -t "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    sed -i "s/^Name=.*/Name=$_variant_name/" "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
  done
}
