# $Id$
# Maintainer: judd <jvinet@zeroflux.org>

_pkgbasename=util-linux
pkgname=lib32-$_pkgbasename
pkgver=2.20.1
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('x86_64')
depends=('lib32-glibc' "$_pkgbasename")
makedepends=('gcc-multilib')
provides=('lib32-util-linux-ng')
conflicts=('lib32-util-linux-ng')
replaces=('lib32-util-linux-ng')
license=('GPL2')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.infradead.org/pub/$_pkgbasename/v2.20/$_pkgbasename-$pkgver.tar.bz2)
md5sums=('079b37517fd4e002a2e6e992e8b4e361')

shopt -s extglob

build() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh
  ./configure --without-ncurses --libdir=/usr/lib32

  for lib in lib{mount,blkid,uuid}; do
    make -C "$lib"
  done
}

package() {
  cd "$srcdir/$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir" install

  # remove everything but libs
  rm -rf "$pkgdir"/!(usr) "$pkgdir"/usr/!(lib32)
}
