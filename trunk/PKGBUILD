# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbformat
pkgver=5.2.0
pkgrel=1
pkgdesc="The base implementation of the Jupyter Notebook format and Python APIs for working with notebooks"
arch=('any')
url="https://pypi.python.org/pypi/nbformat"
license=('BSD')
depends=('python-traitlets' 'python-jupyter_core' 'python-jsonschema')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-testpath' 'python-fastjsonschema')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/nbformat/archive/$pkgver.tar.gz")
md5sums=('835f4d20224a3327b570c2c8ba1ad941')

build() {
  cd nbformat-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbformat-$pkgver
  pytest
}

package() {
  cd nbformat-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
