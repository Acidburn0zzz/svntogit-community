# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-daiquiri
pkgname=('python-daiquiri' 'python2-daiquiri')
pkgver=1.2.0
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
makedepends=('python-pbr' 'python2-pbr')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-testtools'
              'python2-testtools' 'python-json-logger' 'python2-json-logger')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('ea092428f82b7ce2347c619ebb97d1ea7672ceef5dd3eba065019fc69be2164543d30e9099e45e8f7cc6ad66f8dabb9b454eaa6b1092d6d1ba427d62ea77b4d0')

prepare() {
  cp -a daiquiri-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py build

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py testr

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py testr
}

package_python-daiquiri() {
  depends=('python')
  optdepends=('python-json-logger: For JSON support')

  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-daiquiri() {
  depends=('python2')
  optdepends=('python2-json-logger: For JSON support')

  cd daiquiri-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
