# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.9.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-yaml python-mock python-parameterized)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/awslabs/serverless-application-model/archive/v$pkgver.tar.gz")
sha256sums=('2d5a8447a32692ef22f0d1cdd1c22df2c2d2a667b41d984f4704b947a014136a')

prepare() {
  cd serverless-application-model-$pkgver

  # remove coverage.py options - running coverage as a non-developer does not make much sense
  sed -i 's#addopts = .*##' pytest.ini
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver

  # PYTHONPATH=build/lib pytest -v tests
  pytest -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
