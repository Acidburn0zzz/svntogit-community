# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Mark Smith <markzzzsmith@yahoo.com.au>

pkgname=radvd
pkgver=1.9.2
pkgrel=2
pkgdesc='IPv6 Router Advertisement Daemon'
url='http://www.litech.org/radvd/'
license=('custom')
depends=('glibc' 'libdaemon')
arch=('i686' 'x86_64')
backup=('etc/radvd.conf')
source=("http://www.litech.org/radvd/dist/$pkgname-$pkgver.tar.gz"
        "$pkgname.service")
sha1sums=('5bc39b7bec0d73ffa443634c340e75b54867766c'
          '9cb6b2eaaf445fe8d17dfc50fde15709d4de7d1d')
build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc --with-pidfile=/run/radvd.pid \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  # default config
  install -D -m 644 radvd.conf.example "$pkgdir/etc/radvd.conf"
  # license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/COPYRIGHT"
  # systemd
  install -D -m 644 ../$pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 et:
