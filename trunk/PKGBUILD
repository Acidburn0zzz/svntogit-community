# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: mosra <mosra@centrum.cz>

pkgname=magnum
pkgver=2018.02
pkgrel=1
pkgdesc='C++11/C++14 graphics middleware for games and data visualization'
arch=('x86_64')
url='http://magnum.graphics'
license=('MIT')
depends=('corrade' 'openal' 'sdl2')
optdepends=('magnum-plugins: import and export features')
makedepends=('cmake' 'git' 'ninja')
source=("git+https://github.com/mosra/magnum.git#tag=v$pkgver")
sha1sums=('SKIP')

build() {
  mkdir -p build
  cd build
  cmake ../magnum \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DWITH_AUDIO=ON \
    -DWITH_SDL2APPLICATION=ON \
    -DWITH_GLXAPPLICATION=ON \
    -DWITH_WINDOWLESSGLXAPPLICATION=ON \
    -DWITH_GLXCONTEXT=ON \
    -DWITH_MAGNUMFONT=ON \
    -DWITH_MAGNUMFONTCONVERTER=ON \
    -DWITH_OBJIMPORTER=ON \
    -DWITH_TGAIMAGECONVERTER=ON \
    -DWITH_TGAIMPORTER=ON \
    -DWITH_WAVAUDIOIMPORTER=ON \
    -DWITH_DISTANCEFIELDCONVERTER=ON \
    -DWITH_FONTCONVERTER=ON \
    -DWITH_MAGNUMINFO=ON \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "magnum/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# getver: no
# vim: ts=2 sw=2 et:
