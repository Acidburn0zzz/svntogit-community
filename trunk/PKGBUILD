# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.1.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('ff6aed85f8b6959ea34a5029a584f5f1083ca9e09eca4c571a10dda05a65109a2307fd80564a1ddf28fee4dddd668986c407dfbed4fa361f8325399c2a3ae917')

build() {
  cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" .
}

check() {
  cd "${srcdir}/sh-${pkgver}/"
  go test ./...

}

package() {
  cd "${srcdir}/sh-${pkgver}/"
  install -Dm755 "cmd/shfmt/shfmt" "${pkgdir}/usr/bin/shfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
