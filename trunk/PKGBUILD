# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Corey Mwamba <contact.me@coreymwamba.co.uk>

pkgname=rtirq
pkgver=20210319
pkgrel=1
pkgdesc="Realtime IRQ thread system tuning."
arch=('any')
url="https://www.rncbc.org/archive/#rtirq"
license=('GPL2')
groups=('realtime')
depends=('bash')
backup=('etc/rtirq.conf')
source=("https://www.rncbc.org/archive/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('1b518224d4aa598673e71829e1eae52b84526e80f1782a5189911a2803f4ecf8c4e9fa7476def1676d783eb5ebd79c05f7a2600540a3ee2667f7e49ff2e5e668'
            'SKIP')
b2sums=('7e2e248973ced44d1451dabd5f02db0ad4fc9a2909cd3317acc5fe24683c45b620fb68e04ad132a81b9a77809647a24c9dfc7ed9d0b22ec8f8e130f66ffb3264'
        'SKIP')
validpgpkeys=('0E89694D1D4F7883B98DEFA19D0514152A8E5EC4') # Rui Nuno Capela <rncbc@rncbc.org>

prepare() {
  cd "${pkgname}-${pkgver}"
  # fix executable location in systemd services
  sed -e "s|etc/init.d|usr/bin|" -i *".service"
}

package() {
  cd "${pkgname}-${pkgver}"
  install -vDm 755 "${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 "${pkgname}.conf" -t "${pkgdir}/etc/"
  install -vDm 644 *".service" -t "${pkgdir}/usr/lib/systemd/system/"
}
