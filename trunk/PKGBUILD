# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: xyzzy <628208@gmail.com>

pkgname=flameshot
pkgver=0.5.1
pkgrel=5
pkgdesc="Powerful yet simple to use screenshot software"
arch=('x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-base')
makedepends=('make' 'qt5-tools')
source=("https://github.com/lupoDharkael/flameshot/archive/v${pkgver}.tar.gz"
	"pr174.patch::https://github.com/lupoDharkael/flameshot/commit/986b7741428e1f00a5b0688e254665eab90c025a.patch"
	"pr149.patch::https://github.com/lupoDharkael/flameshot/commit/1bf282921744d3b3332d626c3ca00367ac7e6a44.patch"
	"pr171.patch::https://github.com/lupoDharkael/flameshot/commit/b4ba65588df33ead8bd0e9898a2f0b48be9f979d.patch"
	)
sha256sums=('35220eabb8c4203abb0732c63e0b4d955e17ddd0c03c3d2c7a5beb875728e44e'
            'ad8d85d72e0729fb7629f04b1250fab354340987efd58f3fdd09ae6a7f075b27'
            '2f5b165f43831f1447b28df57cb3853a0eeaa9a52b72faddc06764e4e8715ac2'
            'e6479eb2a6e489e33a57d74f25c00adead799d4bd2af9e013f92b2f22d59f50d')
install="${pkgname}.install"

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 <../pr174.patch
  patch -Np1 <../pr149.patch
  patch -Np1 <../pr171.patch

  ## add a version number from pkgver
  sed -i "s|TAG_VERSION = .*|TAG_VERSION = v${pkgver}|" flameshot.pro 
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake CONFIG+=packaging BASEDIR="${pkgdir}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install
}
