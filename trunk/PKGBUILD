# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Jared Casper <jaredcasper@gmail.com>

pkgname=gtkwave
pkgver=3.3.24
pkgrel=1
pkgdesc='A wave viewer which reads LXT, LXT2, VZT, GHW and VCD/EVCD files'
arch=('i686' 'x86_64')
url='http://gtkwave.sourceforge.net'
license=('GPL' 'MIT')
depends=('gtk2')
makedepends=('gperf')
install='gtkwave.install'
source=("http://gtkwave.sourceforge.net/gtkwave-${pkgver}.tar.gz")
md5sums=('792cdc0b7c3e4583f407800020877d05')

build() {
  cd gtkwave-${pkgver}

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-tcl

  make CFLAGS=-D_LARGEFILE64_SOURCE
}

package() {
  cd gtkwave-${pkgver}

  make -j1 prefix=${pkgdir}/usr mandir=${pkgdir}/usr/share/man install

  install -D -m644 ${srcdir}/gtkwave-${pkgver}/.gtkwaverc \
    ${pkgdir}/usr/share/gtkwave/sample.gtkwaverc
  install -D -m644 ${srcdir}/gtkwave-${pkgver}/MIT.TXT \
    ${pkgdir}/usr/share/licenses/gtkwave/MIT.TXT
}
