# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk2hs-buildtools
pkgver=0.13.2.2
pkgrel=11
pkgdesc="Tools to build the Gtk2Hs suite of User Interface libraries."
url="https://hackage.haskell.org/package/gtk2hs-buildtools"
license=('GPL2')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-random' 'haskell-hashtables')
makedepends=('alex' 'happy' 'ghc')
provides=('gtk2hs-buildtools')
conflicts=('gtk2hs-buildtools')
replaces=('gtk2hs-buildtools')
source=("https://github.com/gtk2hs/gtk2hs/archive/843d83548bd076975f1869e6768768b5ed24b268.tar.gz")
sha512sums=('610a080ddf8ad8511176a7fd5ed5457aa26b5ce48539b7eb46563d86db1ffab6501f8b4deac4152f7d9cea84093de34e1bd1bb9467f558f1c202026337e1c708')

prepare() {
    mv gtk2hs-843d83548bd076975f1869e6768768b5ed24b268/tools gtk2hs-buildtools-$pkgver

    cd gtk2hs-buildtools-$pkgver

    sed -i 's/Cabal >= 1.24.0.0 && < 1.25,/Cabal >= 1.24.0.0,/' gtk2hs-buildtools.cabal
    sed -e '/import Distribution.Simple.LocalBuildInfo/i import Distribution.Types.LocalBuildInfo as TLBI' \
        -e 's/LBI.componentsConfigs/TLBI.componentsConfigs/' \
        -i src/Gtk2HsSetup.hs
}

build() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/gtk2hs-buildtools-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
}
