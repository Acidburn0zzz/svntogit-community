# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.10.0
pkgrel=1
pkgdesc="Command-line YAML/XML processor - jq wrapper for YAML/XML documents"
url="https://github.com/kislyuk/yq"
depends=('jq' 'python-setuptools' 'python-yaml' 'python-xmltodict'
         'python-argcomplete')
checkdepends=('python-toml')
optdepends=('python-toml')
license=('Apache')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz"
        0001-Fix-test_datestimes-for-PyYAML-5.3.patch)
sha256sums=('abaf2c0728f1c38dee852e976b0a6def5ab660d67430ee5af76b7a37072eba46'
            '4323f882c110ad4936892e295e634785086839b97730d74f924bc98e547a66cb')

prepare() {
  cd $pkgname-$pkgver
  patch --forward --strip=1 --input=../0001-Fix-test_datestimes-for-PyYAML-5.3.patch
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
