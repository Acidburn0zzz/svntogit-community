# Maintainer: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-devel-patchperl
pkgver=0.58
pkgrel=1
pkgdesc="Patch perl source a la Devel::PPPort's buildperl.pl"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0' 'perl-file-pushd>=1.00')
url=http://search.cpan.org/dist/Devel-PatchPerl
source=('http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Devel-PatchPerl-0.58.tar.gz')
md5sums=(0bd1861dc148d3c5b4fea91af474cabe)
sha512sums=(6107e7ac2385ebd1004f6aa8759624e07835b6cfdce6f3576a96a74849df32479dde39c2515d0a8dd03b20634dd5dec1b8e03089bf6adf829c96d21820b75566)
_distdir="${srcdir}/Devel-PatchPerl-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
