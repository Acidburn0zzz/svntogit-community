# $Id$
# Maintainer: Dan Printzell <arch@vild.io>

pkgname=('dfmt')
pkgver=0.5.2
pkgrel=1
_pkgcommit=0b7189107276163b250c311f35b9121b34d11808
pkgdesc="Dfmt is a formatter for D source code"
arch=('x86_64')
url="https://github.com/dlang-community/dfmt"
license=("custom")
groups=('dlang')
makedepends=('dmd' 'git')
depends=('libphobos')

source=(
	"git+https://github.com/dlang-community/dfmt#commit=$_pkgcommit"
	"git+https://github.com/dlang-community/libdparse"
	"git+https://github.com/sociomantic-tsunami/makd"
	"git+https://github.com/sociomantic-tsunami/beaver"
)
sha256sums=(
	'SKIP'
	'SKIP'
	'SKIP'
	'SKIP'
)

prepare() {
	cd "$srcdir/dfmt"

	git submodule init
	git config submodule.libdparse.url "$srcdir/libdparse"
	git config submodule.makd.url "$srcdir/makd"
	git config submodule.beaver.url "$srcdir/beaver"
	git submodule update
}

build() {
	cd "$srcdir/dfmt"
	make
}

package() {
	# binaries
	install -Dm755 "$srcdir/dfmt/bin/dfmt" "$pkgdir/usr/bin/dfmt"

	# license
	install -Dm644 "$srcdir/dfmt/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
