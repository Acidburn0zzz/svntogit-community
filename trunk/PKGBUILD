# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=17.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="http://www.rosegardenmusic.com/"
license=('GPL2')
depends=('liblrdf' 'dssi' 'fftw' 'lirc'
         'hicolor-icon-theme' 'qt5-base' 'shared-mime-info' 'liblo')
makedepends=('imake' 'cmake' 'qt5-tools')
optdepends=('lilypond: notation display'
            'cups: printing support'
            'okular: print preview, or any other PDF viewer'
            'timidity++: MIDI playback, or any other softsynth'
            'flac'
            'wavpack')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('109261e1a4153319eb8aea167a0fe4fdaff66c3464c0652dee30bf4a61d0bc1b4690f1885607af7dceffa34becb80596ee97e7eaa84aace573d54c92df588ea9')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DBUILD_TESTING=OFF -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
