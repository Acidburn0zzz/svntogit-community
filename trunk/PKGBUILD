# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-package-deprecationmanager
pkgver=0.11
pkgrel=1
pkgdesc="Manage deprecation warnings for your distribution"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-carp' 'perl-list-moreutils' 'perl-params-util'
         'perl-sub-install')
makedepends=('perl-extutils-makemaker>=6.30')
checkdepends=('perl-test-fatal' 'perl-test-requires' 'perl-test-simple')
url=https://metacpan.org/release/Package-DeprecationManager
source=("http://search.cpan.org/CPAN/authors/id/D/DR/DROLSKY/Package-DeprecationManager-${pkgver}.tar.gz")
md5sums=(d09664839b730997c591a5c891a2972b)
sha512sums=(2f1e7f4443d3e897753edaa3a8de9e83995277d938a0f886225ba4134c780c262684a2f284c0b758e6a3a0842ac1e23c5938a2963742710edaa8e7e62272250f)
_distdir="${srcdir}/Package-DeprecationManager-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
