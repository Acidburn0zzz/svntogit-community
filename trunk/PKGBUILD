# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Chris Down <chris@chrisdown.name>

pkgname=clipmenu
pkgver=6.0.0
pkgrel=1
pkgdesc='Clipboard management using dmenu'
url='https://github.com/cdown/clipmenu'
arch=('any')
license=('custom:Public Domain')
depends=('dmenu' 'xsel' 'clipnotify')
optdepends=(
  'rofi: dmenu replacement (use with "export CM_LAUNCHER=rofi")'
)

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cdown/clipmenu/archive/${pkgver}.tar.gz")
sha512sums=('59b758cae7584c878781b7d51fecaa42ea520451d8dcf6d20725c5d1d37d2279eed9364dc08513de3dc38923f87e6e25788288b1615b49768c0129d749c27ac1')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -D -m755 clipmenu "${pkgdir}/usr/bin/clipmenu"
  install -D -m755 clipmenud "${pkgdir}/usr/bin/clipmenud"
  install -D -m755 clipdel "${pkgdir}/usr/bin/clipdel"
  install -D -m644 init/clipmenud.service "${pkgdir}/usr/lib/systemd/user/clipmenud.service"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
