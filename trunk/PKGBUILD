# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
pkgver=2.2.0
pkgrel=1
pkgdesc="An open-source time-series database optimized for fast ingest and complex queries"
arch=('x86_64')
url="https://www.timescale.com/"
license=('Apache')
depends=('postgresql' 'postgresql-libs')
makedepends=('gcc' 'cmake')
install=timescaledb.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/timescale/timescaledb/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('041064f8c780add809deaeabbca762beb5b7b29803427107f02c39f385bfa71bac81abd43ba8abdcf9bd9d83725857e5ed37fa3f6de2cc1668f97b19fa5a792e')

build() {
    cd "${pkgname}-${pkgver}"
    ./bootstrap -DWARNINGS_AS_ERRORS=OFF -DREGRESS_CHECKS=OFF
    cd build && make
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir/" install
}
