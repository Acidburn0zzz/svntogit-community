# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=colordiff
pkgver=1.0.10
pkgrel=1
pkgdesc="A Perl script wrapper for 'diff' that produces the same output but with pretty 'syntax' highlighting"
arch=('any')
url="http://colordiff.sourceforge.net/"
license=('GPL')
depends=('perl' 'diffutils')
backup=('etc/colordiffrc' 'etc/colordiffrc-lightbg')
source=(http://colordiff.sourceforge.net/${pkgname}-${pkgver}.tar.gz{,.sig})
sha1sums=('eeedbe025b9f250134d91fd68fd3940748bbe102'
          '931bbcc180aa97bb172ef296e2779d5c065b448e')

package() {       
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" INSTALL_DIR=/usr/bin MAN_DIR=/usr/share/man/man1 install
  install -m644 colordiffrc-lightbg "${pkgdir}/etc/colordiffrc-lightbg"
}
