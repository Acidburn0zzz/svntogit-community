# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>
pkgname=haskell-tar
pkgver=0.3.1.0
pkgrel=5
pkgdesc="Reading, writing and manipulating \".tar\" archive files."
url="http://hackage.haskell.org/package/tar"
license=('custom:BSD3')
arch=('x86_64' 'i686')
depends=('ghc=7.0.3-2' sh)
options=('strip')
source=("http://hackage.haskell.org/packages/archive/tar/$pkgver/tar-$pkgver.tar.gz")
install=haskell-tar.install
md5sums=('10b54856495f9ef0a42aa270085c7d5e')

build() {
  cd "$srcdir/tar-$pkgver"

  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir="/usr/share/doc/$pkgname" \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd "$srcdir/tar-$pkgver"

  install -Dm 744 register.sh \
	  "$pkgdir/usr/share/haskell/$pkgname/register.sh"
  install -m 744 unregister.sh \
	  "$pkgdir/usr/share/haskell/$pkgname/unregister.sh"
  install -dm 755 "$pkgdir/usr/share/doc/ghc/html/libraries"
  ln -s "/usr/share/doc/$pkgname/html" \
	  "$pkgdir/usr/share/doc/ghc/html/libraries/tar"
  runhaskell Setup copy --destdir="$pkgdir"
  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -f "$pkgdir/usr/share/doc/$pkgname/LICENSE"
}
# vim:set ts=2 sw=2 et:
