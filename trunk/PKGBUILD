# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: earnestly

pkgname=llpp
pkgver=26b.r109.ge27b9bb
_commit=e27b9bb90449cbb48fb11de0ada03cbc8226009e
pkgrel=1
pkgdesc='opengl accelerated pdf viewer based on mupdf'
arch=('x86_64')
url="https://github.com/moosotc/${pkgname}"
license=('GPL')
depends=('glu' 'openjpeg2' 'jbig2dec' 'libjpeg-turbo' 'freetype2' 'desktop-file-utils')
optdepends=('djvulibre: llppac djvu conversion'
            'ghostscript: llppac ps, dvi, and djvu conversion'
            'princexml: llppac html conversion'
            'unoconv: llppac office conversion'
            'librsvg: llppac svg conversion'
            'imagemagick: llppac image conversion'
            'llpp-gc: clean nonextant history items')
makedepends=('git' 'ocaml' 'ocaml-lablgl' 'libmupdf' 'camlp4')

# llpp seems to go through build systems like fasion, holomorph has provided a
# very nice Makefile for building this software which has served well for
# months.  It was contributed, or offered, to the author malc, but he concluded
# that make was bad and needed constant effort to prevent "bitrot" as
# justification for using a terrible shell script that downloaded arbitrary
# commits of bundled libraries.  Now he's on to shake, a haskell NIH
# implementation of make requiring that you now also have ghc and the shake
# library to build this software.  Before this he used ninja.

# For obvious reasons I'm sticking with the custom makefile which greatly
# reduces maintainence burden and unecessary dependency bloat.
source=("git+${url}#commit=${_commit}"
        'no-quit-on-escape.patch'
        'Makefile')
sha256sums=('SKIP'
            '29a9550c6502c9de7db14f2f61edc6b77c7ebee9f1de8b865d4e1b219b9ae377'
            '8c4df5ee65db53e87e01b81628f1dba7673d4ce9a2a5ccc5328f0e9053ac63ce')

pkgver() {
    cd "${srcdir}"/${pkgname}

    git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}"/${pkgname}

    patch -Np1 -i "${srcdir}"/no-quit-on-escape.patch
}

build() {
    cd "${srcdir}"/${pkgname}

    make -f "${srcdir}"/Makefile
}

package() {
    cd "${srcdir}"/${pkgname}

    make -f "${srcdir}"/Makefile PREFIX=/usr DESTDIR="${pkgdir}" install

    cd misc/completions
    make SHELL=/bin/bash PREFIX=/usr DESTDIR="${pkgdir}" install
}
