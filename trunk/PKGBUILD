# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.5
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base' 'gsettings-qt')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz")
sha512sums=('0beaecdce8a287fab36f4247d009739822a843ff4e3d564df6f61c18fa689a50647e73b3f78f7df42793c552aa87aaa6db2c57f058ce4f239a6c66905bb9ecf0')

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  rm -r "$pkgdir"/usr/tests
}
