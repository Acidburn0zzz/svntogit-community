# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsbeautifier
pkgver=1.10.1
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-six' 'python-editorconfig')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('a09e3c59cbb33ca95ef79d3e8341a7de1941f4da8f05c6fc06df70627a0b77e8ad7939b8ee659e1876212db34940a82b9b3caf6f91fea373e15562b5838696ec')

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
}

check() {
  cd js-beautify-$pkgver/python
  python setup.py test
}

package() {
  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
