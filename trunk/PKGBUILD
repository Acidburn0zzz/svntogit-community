# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.3.3
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="https://pwmt.org/projects/girara"
arch=('x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-c' 'pango')
makedepends=('meson' 'ninja' 'doxygen' 'check')

source=(https://pwmt.org/projects/girara/download/girara-$pkgver.tar.xz)
source=(girara-$pkgver.tar.gz::https://github.com/pwmt/girara/archive/$pkgver.tar.gz)

sha512sums=('2764f339c4b5744330c60fa5c8c902a06952d6c5a38589d236fe290f9934b9128527658d8aa99d42806255403b90f55ea1a85e5bd09da4b3a8f7c48be4d94f51')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

check() {
  cd girara-$pkgver/build

  ninja test
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
