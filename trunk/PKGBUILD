# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=polymake
_pkgver=3.4
_patchver=
#pkgver=$_pkgver.r$_patchver
pkgver=$_pkgver
pkgrel=3
pkgdesc="Open source software for research in polyhedral geometry"
arch=(x86_64)
url="https://polymake.org/"
license=(GPL)
depends=(normaliz singular ppl lrs perl-xml-writer perl-xml-libxml perl-xml-libxslt perl-term-readline-gnu perl-term-readkey perl-json)
makedepends=(ninja cddlib ant boost bliss libnsl jdk8-openjdk)
optdepends=('java-runtime: java module')
source=("https://polymake.org/lib/exe/fetch.php/download/polymake-${_pkgver}-minimal.tar.bz2")
sha256sums=('f4ddfd8275b5b9fbf953e6b24f707d00081a1a6d32ac0e6a52165d016609b1e8')

build() {
  cd polymake-$_pkgver
  export CXXFLAGS+=' -DENFNORMALIZ' # Workaround https://github.com/Normaliz/Normaliz/issues/222
  ./configure --prefix=/usr
  make
}

package() {
  cd polymake-$_pkgver
  make DESTDIR="$pkgdir" install
}
