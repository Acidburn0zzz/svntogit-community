# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=jeepney-objects
pkgname=python-$_pkgname
pkgver=0.1.0
pkgrel=1
pkgdesc='Create and publish pure python DBus objects using jeepney'
arch=('any')
url='https://github.com/ocaballeror/jeepney-objects'
license=('GPL')
depends=('python-jeepney')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'dbus')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('b8b3f40291ca949ad3902c80f05a97544577c227ef28e4391ba3d54fbbb8144a4137003b79344444ee5864de06b658ebce83755c111b48a5635531c58d6b51ff')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  eval $(dbus-launch --sh-syntax)

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

