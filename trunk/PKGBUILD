# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.5.3
pkgrel=1
pkgdesc="The batch image converter and resizer"
arch=('i686' 'x86_64')
url="http://converseen.sourceforge.net/"
license=('GPL3')
depends=('imagemagick' 'qt' 'libwmf' 'openexr')
makedepends=('cmake')
install=converseen.install
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('8064020c56a531bb190967369c907cec')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DImageMagick_Magick++_LIBRARY:FILEPATH=/usr/lib/libMagick++-Q16.so \
          -DImageMagick_MagickWand_LIBRARY:FILEPATH=/usr/lib/libMagickWand-Q16.so \
          -DImageMagick_MagickCore_LIBRARY:FILEPATH=/usr/lib/libMagickCore-Q16.so
  make
}

package() {
  cd "${srcdir}/build"

  make DESTDIR="${pkgdir}" install
}
