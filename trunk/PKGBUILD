# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-config
pkgname=(python-oslo-config python2-oslo-config)
pkgver=3.8.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('GPL')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-netaddr' 'python2-netaddr'
             'python-stevedore' 'python2-stevedore' 'python-debtcollector' 'python2-debtcollector')
checkdepends=('python-mock' 'python2-mock' 'python-fixtures' 'python2-fixtures')
options=('!emptydirs')
source=("git+https://git.openstack.org/openstack/oslo.config#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a oslo.config{,-py2}
  find oslo.config-py2 -name \*.py -exec sed -i '1s/python$/&2/' {} +
  sed -i '/argparse/d' "oslo.config"{,-py2}/requirements.txt
}

build() {
  cd "$srcdir"/oslo.config
  python setup.py build

  cd "$srcdir"/oslo.config-py2
  python2 setup.py build
}

check() {
  # Disabled for now, as it brings in too many dependencies

  cd "$srcdir"/oslo.config
  # py.test

  cd "$srcdir"/oslo.config-py2
  # py.test2
}

package_python-oslo-config() {
  depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector')

  cd "$srcdir"/oslo.config
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-config() {
  depends=('python2-six' 'python2-netaddr' 'python2-stevedore' 'python2-debtcollector')

  cd "$srcdir"/oslo.config-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  mv $pkgdir/usr/bin/oslo-config-generator $pkgdir/usr/bin/oslo-config-generator2
}

# vim:set ts=2 sw=2 et:
