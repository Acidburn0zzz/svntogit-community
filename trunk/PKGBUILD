# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hunter
pkgver=3.4.1
pkgrel=1
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
depends=('python-colorama' 'python-cymem' 'python-manhole')
optdepends=('gdb: for remote support')
makedepends=('python-pip' 'python-wheel' 'cython')
checkdepends=('python-pytest-benchmark' 'python-virtualenv' 'python-pytest' 'gdb'
              'python-process-tests' 'python-aspectlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('f6c0cdb152ead0ab2e19472e59fe3133d77f79ad76ad39c2aac71cec9a0c3954e3035bf77e15a0d64a7a22b29881c90424974318ff7b4481fa8a54a8adb5c6f3')

prepare() {
  cd python-hunter-$pkgver
  rm src/hunter/*.c
  rm -r src/hunter/vendor

  sed -i 's/from .vendor.colorama/from colorama/' src/hunter/{actions,util}.py
  sed -i 's/from .vendor._cymem.cymem/from cymem.cymem/' src/hunter/_event.pyx
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  python tests/setup.py build_ext --inplace
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.10" PATH="$srcdir/pyvenv/bin:$PATH" pytest
  ) || :

  # Clean up to prevent it from being installed
  rm build/lib.linux-$CARCH-3.10/*.so
}

package() {
  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
