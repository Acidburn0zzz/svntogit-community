# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: sebikul <sebikul@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=3.34.0
pkgrel=1
epoch=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=(x86_64)
url=https://wiki.gnome.org/Apps/Geary
license=(GPL3)
depends=(
  cairo
  enchant
  folks
  gcr
  gdk-pixbuf2
  glib2
  gmime
  gnome-keyring
  gnome-online-accounts
  gspell
  gtk3
  iso-codes
  libcanberra
  libgee
  libhandy
  libnotify
  libsecret
  libsoup
  libxml2
  libytnef
  pango
  sqlite
  webkit2gtk
)
makedepends=(
  appstream-glib
  cmake
  git
  gnome-doc-utils
  gobject-introspection
  intltool
  itstool
  meson
  vala
)
source=(git+https://gitlab.gnome.org/GNOME/geary.git#tag=e3e24255270358962b64e54bb8fa9beab2de41a6)
sha256sums=(SKIP)

pkgver() {
  cd geary

  git describe --tags
}

build() {
  arch-meson geary build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
