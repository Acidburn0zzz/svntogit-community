# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-cmarkgfm
pkgname=(python-cmarkgfm python2-cmarkgfm)
pkgver=0.4.1
pkgrel=1
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
makedepends=('cmark-gfm' 'python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/theacodes/cmarkgfm/archive/$pkgver.tar.gz")
sha512sums=('a10d417ca515024df8d0d7734381d0bdd0f35da332e0bba40a88362f53a0c1b84f799011294ad2b6509a9656351e0886ba2d1dcb0edc50775e0661dbeab95caf')

prepare() {
  # Will commit this to upstream some time later
  cd cmarkgfm-$pkgver
  sed -e 's/cmark.h/cmark-gfm.h/' -e 's/cmark_extension_api.h/cmark-gfm-extension_api.h/' \
      -e 's/core-extensions.h/cmark-gfm-core-extensions.h/' \
      -i src/cmarkgfm/cmark_module.h
  sed -e 's/core_extensions_ensure_registered/cmark_gfm_core_extensions_ensure_registered/' \
      -i src/cmarkgfm/cmark.cffi.h
  sed -e 's/lib.core_extensions_ensure_registered/lib.cmark_gfm_core_extensions_ensure_registered/' \
      -i src/cmarkgfm/cmark.py
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
  python2 setup.py pytest
}

package_python-cmarkgfm() {
  depends=('cmark-gfm' 'python-cffi')

  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-cmarkgfm() {
  depends=('cmark-gfm' 'python2-cffi')

  cd cmarkgfm-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
