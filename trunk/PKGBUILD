# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-dkim
pkgver=0.9.2
_branch=${pkgver%.*}
pkgrel=1
pkgdesc='Python library that implements DKIM and ARC email signing and verification'
arch=('any')
url=https://launchpad.net/dkimpy
license=('ZLIB')
depends=('python-authres' 'python-dnspython' 'python-pynacl' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/d/dkimpy/dkimpy-$pkgver.tar.gz"
        "LICENSE-dkimpy-$pkgver::https://git.launchpad.net/dkimpy/plain/LICENSE?id=$pkgver")
sha512sums=('a1e7ac778f8cf0897793f296061ef67a6945b136db1a7aee18498ce33111d1a0d485b0ce32f52c22c0866be644065e000de57131301000a83a1b55b6a875bfce'
            '20a36385eabb3f758fee79d66033db77601ee352ea6f28c16395b1a7798e7804e1e9c3bc6326d939ece8f30e047d4ac95df25755f00a69e95de5f7528c16d5dd')

build() {
  cd dkimpy-$pkgver
  python setup.py build
}

check() {
  cd dkimpy-$pkgver
  python -m unittest dkim.tests.test_suite
}

package() {
  cd dkimpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 ../LICENSE-dkimpy-$pkgver "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
