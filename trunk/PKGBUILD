# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>

pkgbase=python-pygit2
pkgname=('python-pygit2' 'python2-pygit2')
pkgver=0.24.0
pkgrel=3
pkgdesc='Python bindings for libgit2'
arch=('i686' 'x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.24.0')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'git' 'python-six' 'python2-six')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz")
sha256sums=('3648c59c62bbde683eec65ef3fb1d3db58e10b5dee15517461778ecf205971fb')

package_python-pygit2() {
  depends+=('python-cffi' 'python2-six')
  replaces=('python-libgit2')
  conflicts=('python-libgit2')

  cd "pygit2-$pkgver"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  depends+=('python2-cffi' 'python2-six')
  replaces=('python2-libgit2')
  conflicts=('python2-libgit2')

  cd "pygit2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
