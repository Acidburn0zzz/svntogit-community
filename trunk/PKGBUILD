# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-pproxy
pkgver=2.4.7
# The commit that bumps version. Upstream does not use git tags [1], and the
# source tarball on PyPI does not include LICENSE
# https://github.com/qwj/python-proxy/issues/91
_commit=c8704c2ff65646efcc02debd344f18891e6bac2d
pkgrel=1
pkgdesc="Versatile TCP/UDP asynchronous tunnel proxy implemented in Python 3 asyncio"
arch=('any')
url="https://github.com/qwj/python-proxy"
license=('MIT')
depends=(python)
makedepends=(python-setuptools)
optdepends=(
  'python-pycryptodome: accelerated cipher operations'
  'python-uvloop: faster asynchronous operations'
  'python-daemon: run the server as a daemon'
)
source=(https://github.com/qwj/python-proxy/archive/$_commit/pproxy-$_commit.tar.gz)
sha256sums=('7971f9b48c6fa542110f22873df0e998439b26f0a711caf67a92bc1dbf6b6e94')

build() {
  cd python-proxy-$_commit
  python setup.py build
}

package() {
  cd python-proxy-$_commit
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
