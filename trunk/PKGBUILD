# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-go-dbus-factory
pkgver=0.2.0
pkgrel=1
pkgdesc='GO DBus factory for DDE'
arch=('any')
url="https://github.com/linuxdeepin/go-dbus-factory"
license=('GPL3')
depends=('deepin-go-lib')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-dbus-factory/archive/$pkgver.tar.gz")
sha512sums=('553409311960c67042f83e6204b1d9da312a7447a943b0a10653eb35d180d190617a4fde29d0c6322b9380e055948a97c836e650781cb4c8eac333bb60c0f307')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  ln -sf "$srcdir"/go-dbus-factory-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-dbus-factory

  go get golang.org/x/net/context
}

build() {
  cd go-dbus-factory-$pkgver
  make bin
  ./gen.sh
}

package() {
  cd go-dbus-factory-$pkgver
  install -dm755 "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory
  cp -a com.* org.* net.* object_manager "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-dbus-factory/
}
