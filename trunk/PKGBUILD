# $Id$
# Maintainer:  Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

_extramodules=extramodules-3.1-ARCH
_kver="$(cat /lib/modules/${_extramodules}/version)"
pkgname=ndiswrapper
pkgver=1.56
pkgrel=17
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
install=ndiswrapper.install
depends=('linux>=3.1' 'linux<3.2' 'wireless_tools' 'perl')
makedepends=('linux-headers')
provides=("$pkgname-utils" "$pkgname-bin")
replaces=("$pkgname-utils" "$pkgname-bin")
source=("http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz"
        "kernel-2.6.35.patch"
        "kernel-2.6.36.patch"
        "kernel-2.6.38.patch"
        "kernel-3.1.patch")
options=('!strip')
md5sums=('1431f7ed5f8e92e752d330bbb3aed333'
         '0a03d613b1fd545a75c5dd1a7c2aaec4'
         'cc16ed13449f17e90865df688b180b2c'
         '0fd9b0f8ae210d59cfe6b79dd2b86da9'
         'fb3a3e202f723923f5d0bdd9b6db8980')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # kernel patches
  patch -Np1 -i "$srcdir/kernel-2.6.35.patch"
  patch -Np1 -i "$srcdir/kernel-2.6.36.patch"
  patch -Np2 -i "$srcdir/kernel-2.6.38.patch"
  patch -Np1 -i "$srcdir/kernel-3.1.patch"

  # fix regex to accept kernel version 3.x
  #sed -i "/uname/s/\/.*\//\/^(\\\d+\\\.){1,2}\\\d+$\//" utils/$pkgname
  sed -i "/uname/s/\/.*\//\/(\\\d+)\\\.(\\\d+)\//" utils/$pkgname

  # fix module dir
  sed -i "s|misc|kernel/drivers/net/wireless/$pkgname|" driver/Makefile

  make KVERS=$_kver
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" KVERS=$_kver install

  # remove useless files
  rm -rf "$pkgdir/lib/modules/$_kver/"

  # gzip -9 modules
  find ./ -name '*.ko' -exec gzip -9 {} \;

  install -Dm 644 driver/ndiswrapper.ko.gz \
    "$pkgdir/lib/modules/$_extramodules/kernel/drivers/net/wireless/ndiswrapper/ndiswrapper.ko.gz"
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" "$startdir/ndiswrapper.install"
}

# vim:set ts=2 sw=2 et:
