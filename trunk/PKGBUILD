# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: emersion <contact emersion.fr>

_name=browserpass-native
pkgname=browserpass
pkgver=3.0.6
pkgrel=5
pkgdesc="Native host app for Browserpass, browser extension for zx2c4's pass (password manager)"
arch=('x86_64')
url="https://github.com/${pkgname}/${_name}"
license=('ISC')
depends=('gnupg')
makedepends=('go' 'git')
optdepends=('browserpass-chromium: Chromium extension for Browserpass'
            'browserpass-firefox: Firefox extension for Browserpass')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::${url}/releases/download/${pkgver}/${_name}-${pkgver}.tar.gz.asc")
sha256sums=('6a2442192aad8fae3642b925d1ff04368eef3a390043e6164c8eb2fd2a438ceb'
            'SKIP')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

prepare() {
    cd "${_name}-${pkgver}"
    make configure
}

build() {
    cd "${_name}-${pkgver}"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

    make "${pkgname}"
}

check() {
    cd "${_name}-${pkgver}"
    make test
}

package() {
    cd "${_name}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
