# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=('python-jsbeautifier' 'python-cssbeautifier')
pkgbase=python-jsbeautifier
pkgver=1.12.0
pkgrel=1
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-setuptools' 'python-six' 'python-editorconfig')
source=("https://github.com/beautify-web/js-beautify/archive/v$pkgver/js-beautify-v$pkgver.tar.gz")
sha512sums=('ebbcc5d2487145df05965115a87da3ed61209b4a226fae61f63378cee6266e0f3d22eedb61aa4aa2b9ebe46e723d0a2b7edcb006af008b3b97391c56dafbe5a0')

prepare() {
  cd js-beautify-$pkgver
  cp -a python python-css
}

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
  cd ../python-css
  python setup-css.py build
}

check() {
  cd js-beautify-$pkgver
  # Use python-css to avoid rebuilding cssbeautifier
  python python-css/js-beautify-test.py
}

package_python-jsbeautifier() {
  pkgdesc='JavaScript unobfuscator and beautifier'
  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" ../LICENSE
}

package_python-cssbeautifier() {
  pkgdesc='CSS unobfuscator and beautifier'
  depends+=('python-jsbeautifier')
  cd js-beautify-$pkgver/python-css
  python setup-css.py install --root="$pkgdir" --optimize=1 --skip-build
  rm -r "$pkgdir"/usr/{bin/js-beautify,lib/python3.8/site-packages/jsbeautifier{,-$pkgver-py3.8.egg-info}}
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" ../LICENSE
}
