# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.7.1
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agronholm/typeguard/archive/$pkgver.tar.gz")
sha512sums=('d446e86e93292c7a88fea5daa607d6d9ed26b60daf43c79feac7ed524073d3ca123e63f8fb27eeab828d5989f29f9800eec9c077c24d7ed1b1c4a183ceed0d94')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  python setup.py pytest
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
