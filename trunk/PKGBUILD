# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=tribler
_gitver=7.1.0-rc2
_dispersy=23d0b84663138b58fb5c16ac75042b2de0a68ef5
_pymdht=bcf6f44e8e8bfb103aad7fe6c0f8b0c0db2a6868
_pyipv8=1.0
pkgver=${_gitver//-/_}
pkgrel=1
pkgdesc='Privacy enhanced BitTorrent client with P2P content discovery'
url='https://www.tribler.org/'
arch=('any')
license=('LGPL3')
#depends=('python2-cryptography' 'python2-feedparser' 'python2-apsw' 'python2-cherrypy' 'python2-plyvel'
#         'python2-pillow' 'python2-pyqt5' 'qt5-svg' 'phonon-qt5-vlc' 'python2-feedparser' 'python2-chardet'
#         'python2-psutil' 'python2-meliae' 'python2-decorator' 'python2-netifaces' 'python2-requests'
#         'python2-twisted' 'libsodium' 'libtorrent-rasterbar' 'python2-m2crypto' 'python2-configobj'
#         'python2-matplotlib' 'python2-service-identity' 'python2-keyring' 'python2-keyrings-alt'
#         'python2-libnacl' 'python2-contextlib2' 'python2-zc.lockfile' 'python2-datrie' 'python2-networkx')
depends=('libsodium' 'libtorrent-rasterbar' 'python2-pyqt5' 'qt5-svg' 'phonon-qt5-vlc' 'python2-apsw'
         'python2-cherrypy' 'python2-cryptography' 'python2-decorator' 'python2-feedparser' 'python2-chardet'
         'python2-m2crypto' 'python2-netifaces' 'python2-plyvel' 'python2-twisted' 'python2-configobj'
         'python2-matplotlib' 'python2-networkx' 'python2-psutil' 'python2-scipy' 'python2-libnacl')
optdepends=('vlc: for internal video player')
makedepends=('python2-setuptools' 'git')
provides=('python2-pyipv8')
conflicts=('python2-pyipv8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Tribler/tribler/archive/v$_gitver.tar.gz"
        "dispersy-$_dispersy.tar.gz::https://github.com/Tribler/dispersy/archive/$_dispersy.tar.gz"
        "pymdht-$_pymdht.tar.gz::https://github.com/devos50/pymdht/archive/$_pymdht.tar.gz"
        "py-ipv8-$_pyipv8.tar.gz::https://github.com/Tribler/py-ipv8/archive/v$_pyipv8.tar.gz")
sha512sums=('29b085362b4e509f041817bc4b5bf38389576c84eceae3c6cd3a0ff04465811a3e8412197919c131a16e9faf6184d5fec8a21101278a267e8d06bce02674455d'
            '5b0492acc707094b51762aed5aca4ed4baac89775939ad45ff50dd1f0f42f067cd3a84fff7b6746af0bebbf16bc0fffce5943a72feaa4142e303d4ad638cce32'
            '7ec49381cb065281ba1c94975b197495196405a496f38275404a157329c6c911147d7efd6e5b5350a003b47acd9ed4ad1a54e3665e2d5fb51ac21c674ff0b327'
            '09d3ce9e9639c25cec593fc74289e2d27a03d3d173b7aea0dac8d79c561df8d3f1744f7f7674c27a852a070882a632dd630e9aa67c313ea2c55af68d6765a8b7')

prepare() {
  cd $pkgname-$_gitver

  # Setup submodules
  cp -rT "$srcdir"/dispersy-$_dispersy Tribler/dispersy
  cp -rT "$srcdir"/pymdht-$_pymdht Tribler/Core/DecentralizedTracking/pymdht
  cp -rT "$srcdir"/py-ipv8-$_pyipv8 Tribler/pyipv8

  # Fix tribler path
  sed -i 's|/opt/tribler|/usr/share/tribler|g' systemd/anontunnel_helper@.service
  sed -i 's|/opt/tribler|/usr/share/tribler|g' systemd/tribler.service

  # Fix version info
  sed -e "s|version_id =.*|version_id = \"${pkgver%_*}\"|g" \
      -e "s|build_date =.*|build_date = \"$(date)\"|g" \
        -i Tribler/Core/version.py
}

build () {
  cd $pkgname-$_gitver

  python2 setup.py build
}

package() {
  cd $pkgname-$_gitver

  # Install python modules
  python2 setup.py install --root="$pkgdir" --optimize=1

  # Install binary files/assets
  install -dm 755 "$pkgdir"/usr/{bin,share/tribler}
  cp -dr --no-preserve=ownership Tribler "$pkgdir"/usr/share/tribler
  cp -dr --no-preserve=ownership TriblerGUI "$pkgdir"/usr/share/tribler
  ln -s Tribler/Core/CacheDB/schema_sdb_v*.sql "$pkgdir"/usr/share/tribler/Tribler

  install -dm 644 "$pkgdir"/usr/share/{applications,pixmaps}
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler.desktop "$pkgdir"/usr/share/applications
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler.xpm "$pkgdir"/usr/share/pixmaps
  install -Dm 644 Tribler/Main/Build/Ubuntu/tribler_big.xpm "$pkgdir"/usr/share/pixmaps
  install -Dm 755 debian/bin/tribler "$pkgdir"/usr/bin
  install -Dm 644 logger.conf "$pkgdir"/usr/share/tribler/
  install -Dm 644 run_tribler.py "$pkgdir"/usr/share/tribler/
  install -Dm 644 check_os.py "$pkgdir"/usr/share/tribler/

  cp -dr --no-preserve=ownership twisted "$pkgdir"/usr/share/tribler

  # Remove test folders
  find "$pkgdir" -type d -name "test" -name "tests" -exec rm -rf {} \;

  # Install systemd files
  install -Dm 644 systemd/anontunnel_helper@.service "$pkgdir"/usr/lib/systemd/system/anontunnel_helper@.service
  install -Dm 644 systemd/tribler.service "$pkgdir"/usr/lib/systemd/system/tribler.service
}

