# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Jan Hicken <firstname [dot] lastname [at] posteo [dot] de>

pkgname=networkmanager-strongswan
_pkgname=NetworkManager-strongswan
pkgver=1.4.4
pkgrel=2
pkgdesc="Strongswan NetworkManager plugin"
arch=('x86_64')
url="http://wiki.strongswan.org/projects/strongswan/wiki/NetworkManager"
license=('GPL')
depends=('networkmanager' 'strongswan>=5.5.1' 'libsecret')
makedepends=('intltool' 'libnm-gtk')
optdepends=('libnm-gtk: GUI configuration plugin')
source=("https://download.strongswan.org/NetworkManager/$_pkgname-$pkgver.tar.bz2"{,.sig})
sha512sums=('e3e2dd9f5b961255f897ca1912f9deb34ac62628ff40289ff0f8f3900746482f2f32d390c64b88c10f10b8a99388e27099abedb7157d77c173524dc763a32155'
            'SKIP')
validpgpkeys=('12538F8F689B5F1F15F07BE1765FE26C6B467584') # Tobias Brunner

build() {
  cd "$_pkgname-$pkgver"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib --disable-static --disable-more-warnings \
    --with-charon=/usr/lib/strongswan/charon-nm
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd "$_pkgname-$pkgver"
  make check
}

package() {
  cd "$_pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
