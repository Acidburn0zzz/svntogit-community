# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=tree-sitter
pkgver=0.19.4
pkgrel=1
pkgdesc='An incremental parsing system for programming tools'
arch=('x86_64')
url='https://github.com/tree-sitter/tree-sitter'
license=('MIT')
provides=('libtree-sitter.so')
makedepends=('git' 'cargo' 'npm' 'emscripten')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('cc6adba489f4bbf3348b24f6d0d4039ae8bd80164d6e6bd5ee313bea064ef978d43aa78f7294601f65a95501b4fa0a914af879fdc362e54f429ca2255f70ae2b')

build() {
  cd $pkgname-$pkgver

  make

  ./script/build-wasm

  cd cli

  cargo build --release --locked --all-features
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 755 target/release/$pkgname -t "$pkgdir"/usr/bin

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

