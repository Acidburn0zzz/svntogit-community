# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=kcov
pkgver=33
pkgrel=1
pkgdesc="Code coverage tester for compiled programs, Python scripts and shell scripts"
arch=('i686' 'x86_64')
url="http://simonkagstrom.github.com/kcov/index.html"
license=('GPL2')
depends=('elfutils' 'curl')
makedepends=('cmake' 'python2')
source=("https://github.com/SimonKagstrom/kcov/archive/v${pkgver}.tar.gz")
sha512sums=('e35ba3c4dc320cef0cde097cfa2c4067ec6bf86a5ee797d3ec111facffa036da4fc4d403e63bd3767d03eac511987592e77ed61810605f8e2da77c26570e245c')

build() {
  cd $pkgname-$pkgver

  sed -i 's/env python$/env python2/' src/bin-to-c-source.py
  sed -i 's/env python$/env python2/' src/engines/python-helper.py


  mkdir -p build && cd build
  # Strip --as-needed, because dl is linked before dw,
  # but is actually used by dw, so we can't strip it before
  export LDFLAGS="${LDFLAGS/--as-needed,/}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr DCMAKE_C_FLAGS:STRING="-ldl -D__FOO_BAR_BAZ__" ..
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install
  make clean
}

# vim:set ts=2 sw=2 et:
