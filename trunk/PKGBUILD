# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=0.3.0
pkgrel=2
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/stevemao/diff-so-fancy"
license=('MIT')
install="${pkgname}.install"
depends=('git')
checkdepends=('bash-bats')
source=("git+https://github.com/so-fancy/diff-so-fancy.git#tag=v$pkgver"
        "git+https://github.com/ztombol/bats-core.git"
        "git+https://github.com/ztombol/bats-assert.git"
        system-diff_highlight.patch)
sha384sums=('SKIP'
            'SKIP'
            'SKIP'
            '586f722ae89c9d0d2ab1ce987052c7c0f4929efaad13c1c395cc15a72a47efe4214a958fdb973a17a6c761d9f7662d01')

prepare() {
  cd "${srcdir}/${pkgname}"
  git config submodule."test/test_helper/bats-core".url "${srcdir}"/bats-core
  git config submodule."test/test_helper/bats-assert".url "${srcdir}"/bats-assert
  git submodule update --init

  patch -p1 -i ../system-diff_highlight.patch
}

check() {
  cd "${srcdir}/${pkgname}"
  bats test/*.bats
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 'diff-so-fancy' "${pkgdir}/usr/bin/diff-so-fancy"
}
