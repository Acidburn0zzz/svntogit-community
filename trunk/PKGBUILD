# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Dan Serban
# Contributor: Richard Murri

pkgname=python-werkzeug
pkgver=0.9.1
pkgrel=1
pkgdesc='The Swiss Army knife of Python web development'
arch=('any')
url='http://werkzeug.pocoo.org/'
license=('custom:BSD')
depends=('python')
makedepends=('python-setuptools')
source=("http://pypi.python.org/packages/source/W/Werkzeug/Werkzeug-${pkgver}.tar.gz")
sha256sums=('d892523bb6110a8ff8bad51982bb76a9c168659b2d58369806fd9bf049b65669')

build() {
  cd "Werkzeug-$pkgver"

  python setup.py build
}

check() {
  cd "Werkzeug-$pkgver"

  python setup.py test
}

package() {
  cd "Werkzeug-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
