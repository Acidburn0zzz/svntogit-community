# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario at finel dot li>
# Contributor: Artem Vorotnikov <artem at vorotnikov dot me>

pkgname=ruby-addressable
pkgver=2.5.1
pkgrel=1
pkgdesc='URI Implementation'
arch=('any')
url='https://github.com/sporkmonger/addressable'
license=('Apache')
depends=('ruby' 'ruby-public_suffix')
options=('!emptydirs')
source=("https://rubygems.org/downloads/addressable-$pkgver.gem")
noextract=("addressable-$pkgver.gem")
sha512sums=('ae7232dc6cbd1aa6405135c7916697a93286fcb223b14e2c30b5897aed6fe32529c94bfb84154e0cead5af9a282cc89442cbd3249b68abd7ffd67b6c4599bff3')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "$pkgdir/$_gemdir" -n "$pkgdir"/usr/bin addressable-$pkgver.gem
  rm "$pkgdir/$_gemdir"/cache/addressable-$pkgver.gem
  install -D -m644 "$pkgdir/$_gemdir"/gems/addressable-$pkgver/LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
