# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Daenyth <Daenyth+Arch [AT] gmail [DOT] com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: rabyte <rabyte__gmail>

pkgname=supertuxkart
pkgver=0.8
pkgrel=3
pkgdesc='Kart racing game featuring Tux and his friends'
arch=('i686' 'x86_64')
url='http://supertuxkart.sourceforge.net/'
license=('GPL2')
depends=('openal' 'libvorbis' 'libgl' 'fribidi' 'curl')
makedepends=('cmake' 'subversion' 'mesa' 'imagemagick' 'setconf')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-src.tar.bz2")
md5sums=('0b939ce601374758938119e0b0dd1fec')

build() {
  cd ${srcdir}/SuperTuxKart-${pkgver}

  convert "data/${pkgname}_64.xpm" "data/$pkgname.png"
  _fn="data/${pkgname}_desktop.template"
  setconf "$_fn" Exec "$pkgname --log=file"
  setconf "$_fn" TryExec "$pkgname"
  setconf "$_fn" Icon "$pkgname"

  cd lib/irrlicht/source/Irrlicht
  NDEBUG=1 make

  cd ${srcdir}/SuperTuxKart-${pkgver}
  #sed -i "s|share/games|share|g" CMakeLists.txt

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake \
    -DIRRLICHT_DIR="$srcdir/irrlicht" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_CXX_FLAGS="-lpthread -lm -ldl" \
    ..

  make
}

package() {
  cd ${srcdir}/SuperTuxKart-${pkgver}

  cd build
  make DESTDIR=${pkgdir} install

  cd ../data
  install -Dm644 "$pkgname.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
}

# vim:set ts=2 sw=2 et:
