# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason Chu <jason@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=xboard
pkgver=4.9.0
pkgrel=1
pkgdesc="Graphical user interfaces for chess"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/xboard/"
license=('GPL3')
depends=('texinfo' 'xdg-utils' 'gtk2' 'cairo' 'librsvg')
optdepends=('gnuchess: chess engine'
	    'perl')
validpgpkeys=('47D20669A1ED486C0257C0362A0C7CF1F51BC536')
source=("https://ftp.gnu.org/gnu/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha256sums=('4b964ba1ef41124548140ec21e89cb3e0b42449e37c4c486e394f95df4c866ab'
            'SKIP')

build() {
  cd "${pkgname}-${pkgver}"
  sed -i 's/fairymax/gnuchess/g' {xaw,gtk}/xboard.h xboard.conf
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --with-gtk \
    --enable-zippy
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -Dm755 "$srcdir"/${pkgname}-${pkgver}/cmail \
    "$pkgdir"/usr/bin/cmail
}
