# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=0.0.5.1
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('dtkwidget' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('e5eb042e0eed77c43113f46835675601a638648159de02a4b5a5f991ffc127ba55a35dc594605f1eae57bb09184baa83c0b9e2abd033d721eedc042fd2757757')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' deepin-clone.pro deepin-clone-ionice deepin-clone-pkexec com.deepin.pkexec.deepin-clone.policy{,.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
