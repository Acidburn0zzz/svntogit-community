# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.8.5
pkgrel=1
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing'
             'python-pyperclip' 'python2-pyperclip' 'python-colorama' 'python2-colorama'
             'python2-contextlib2' 'python2-typing' 'python-wcwidth' 'python2-wcwidth')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-mock' 'python2-mock' 'vi')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz"
        https://github.com/python-cmd2/cmd2/commit/127a3b3e0b1ef0cc42f43970f666a2193360d57b.patch)
sha512sums=('452be32b4f873ece9864540772fcba47afcea5e5277a35e9e88ac071b40453df45683508510a632028e968c761c5b57ab46ded88e26f04f6a89230fde1380179'
            '110146871c6ab84a5ddc58572608ddf1f6116613329aabf403d9d4a94481ba15f3bfe3a21ca01ac45b4964da9f22e7653af95ad9c2714e1542ade7bbdb6b5859')

prepare() {
  (cd cmd2-$pkgver; patch -p1 -i ../127a3b3e0b1ef0cc42f43970f666a2193360d57b.patch)
  cp -a cmd2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py build

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py pytest

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py pytest
}

package_python-cmd2() {
  depends=('python-colorama' 'python-pyparsing' 'python-pyperclip' 'python-wcwidth')

  cd "$srcdir"/cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-colorama' 'python2-pyparsing' 'python2-pyperclip' 'python2-wcwidth'
           'python2-contextlib2' 'python2-typing')

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
