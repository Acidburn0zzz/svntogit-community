# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: Patrice Peterson <runiq at archlinux dot us>
# Contributor: Vivien Didelot <vivien+aur@didelot.org>

pkgname=i3blocks
pkgver=1.5
pkgrel=1
pkgdesc='Define blocks for your i3bar status line'
arch=('x86_64')
groups=('i3')
url="https://github.com/vivien/i3blocks"
license=('GPL3')
makedepends=('git')
depends=('glibc')
optdepends=('acpi: For battery script'
            'bc: For bandwidth script'
            'lm_sensors: For temperature script'
            'openvpn: For openvpn script'
            'playerctl: For mediaplayer script'
            'alsa-utils: For volume script'
            'sysstat: For cpu_usage script')
source=("${pkgname}-${pkgver}::git+https://github.com/vivien/${pkgname}#tag=${pkgver}?signed")
sha512sums=('SKIP')
backup=('etc/i3blocks.conf')
validpgpkeys=('44C919BDF206CFDC49C7101A66C63FBDFD79670A')

build () {
  cd "${pkgname}-${pkgver}"
  ./autogen.sh
  ./configure --prefix=/usr --libexecdir=/usr/lib --sysconfdir=/etc
  make VERSION="${pkgver}"
}

package () {
  cd "${pkgname}-${pkgver}"
  make VERSION="${pkgver}" DESTDIR="${pkgdir}" install
}
