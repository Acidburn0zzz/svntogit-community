# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=metacity
pkgver=3.30.1
pkgrel=2
pkgdesc="Window manager of GNOME Flashback"
arch=('x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'gsettings-desktop-schemas' 'gtk3' 'startup-notification' 'zenity')
makedepends=('intltool')
url="https://wiki.gnome.org/Projects/Metacity"
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        metacity-ggo04.patch)
sha256sums=('0f1311b7aa38de71c66080bd2b744de2cd9580afcaee9c6097a7d3f5c421798a'
            '611b9d91cad84626495390a6a7d7c5a4eed00c49f90debad0aec14d86b0fbb50')

prepare() {
  cd $pkgname-$pkgver

  # Add upstream fix for Sugar regression
  # https://gitlab.gnome.org/GNOME/metacity/issues/4
  patch -Np1 -i ../metacity-ggo04.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
