# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=prettier
pkgver=1.16.0
pkgrel=1
pkgdesc="An opinionated code formatter for JS, JSON, CSS, YAML and much more"
arch=("any")
url="https://prettier.io/"
license=("MIT")
depends=("nodejs")
makedepends=("yarn")
source=("$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('01fbbcf1480559ff57490596ffd8d6c8260550ae3b03d323243759904dd55c36')


build() {
	cd "$pkgname-$pkgver"
	yarn
	yarn build
}


package() {
	install -d "$pkgdir/usr/lib/$pkgname"
	cd "$pkgdir/usr/lib/$pkgname"
	cp -a "$srcdir/$pkgname-$pkgver/dist/"* .
	install -d "$pkgdir/usr/bin"
	ln -s "/usr/lib/$pkgname/bin-$pkgname.js" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$srcdir/$pkgname-$pkgver/LICENSE"
}
