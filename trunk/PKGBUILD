# $Id$
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=1.91
pkgrel=1
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('any')
url='https://gnometerminator.blogspot.com/p/introduction.html'
license=('GPL2')
depends=('libkeybinder3' 'libnotify' 'python2-cairo' 'python2-dbus' 'python2-psutil' 'python2-gobject' 'vte3' 'xdg-utils')
makedepends=('intltool')
source=("https://launchpad.net/terminator/gtk3/${pkgver}/+download/${pkgname^}-${pkgver}.tar.gz"
        "https://launchpad.net/terminator/gtk3/${pkgver}/+download/${pkgname^}-${pkgver}.tar.gz.asc")
md5sums=('44e6be581abc9b52fe2e665cbb572201'
         'SKIP')
sha512sums=('7037077680475164011e19bb5ff16b55a5b4f0b71941d6bfb2ba00af8289eff6f9bf62dded3a6f4f21f199e9e070d0731b19a4ac60fdd429815d708e7fdc3edd'
            'SKIP')
validpgpkeys=('FE67C001C72200A71DE9EFA5DB69E1780B95EF32')

prepare() {
    cd "${srcdir}/${pkgname^}-${pkgver}"

    # python2 fix
    for file in terminatorlib/{,plugins/}*.py; do
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

    # fix run without gconf
    sed -i 's/except ImportError:/except ValueError:/' terminatorlib/config.py
}

build() {
    cd "${srcdir}/${pkgname^}-${pkgver}"

    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname^}-${pkgver}"

    python2 setup.py install --skip-build --root="${pkgdir}"

    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
}
