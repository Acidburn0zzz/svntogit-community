# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=4.4.2
pkgrel=1
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url=https://github.com/elementary/files
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libcloudproviders
  libgee
  libgit2-glib
  libgranite.so
  libnotify
  pango
  sqlite
  zeitgeist
)
makedepends=(
  git
  gnome-common
  meson
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'gvfs: Cleaner devices section'
  'tumbler: Thumbnails generation'
)
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=39221eef3af5c9c20061f575c8729a773af5375d)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-files

  git describe --tags
}

build() {
  arch-meson pantheon-files build \
    -D with-unity=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
