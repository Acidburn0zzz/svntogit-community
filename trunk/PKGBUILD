# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.6.17
pkgrel=4
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm'
arch=('i686' 'x86_64')
url='http://www.nongnu.org/duplicity/'
license=('GPL')
depends=('librsync' 'ncftp' 'python2' 'python-boto' 'python-gnupginterface')
source=("http://savannah.nongnu.org/download/$pkgname/$pkgname-$pkgver.tar.gz"
        'botobackend.patch'
        'duplicity-0.6.17-fix-memleak.patch')
md5sums=('36423ab4e3b9aa90c5c44d9fa93fba0f'
         '49bbdc327230fe5985d18eaae37eec0c'
         'b77e98d006842414fdda9662c8081d3b')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # python2 fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  # add hotfix issued by upstream (FS#27390)
  patch -p0 -i ../botobackend.patch

  # fix a memory leak (upstream bug #908228)
  patch -p0 -i ../duplicity-0.6.17-fix-memleak.patch

  python2 setup.py install --root="$pkgdir" --optimize=1

  # fix broken documentation permissions until upstream does (FS#27301)
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
  chmod 644 "$pkgdir/usr/share/man/man1/rdiffdir.1"
  chmod 644 "$pkgdir/usr/share/doc/$pkgname-$pkgver"/*
}
