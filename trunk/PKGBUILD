# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=3.2.6
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'poppler-glib'
         'rfkill' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'deepin-polkit-agent' 'udisks2' 'upower' # 'gcc-libs>=5.3.0-3'
         'libxkbfile' 'accountsservice' 'deepin-desktop-base' 'bamf' 'pulseaudio'
         'gnome-keyring' 'noto-fonts')
makedepends=('deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib' 'deepin-api' 'sqlite'
             'deepin-gettext-tools' 'git' 'mercurial' 'python-gobject' 'networkmanager' 'go-pie')
             # 'gcc-go')
optdepends=('networkmanager: for network management support'
            'deepin-grub2-themes: theme for grub module'
            'iw: for miracast module'
            'proxychains-ng: for proxy configuration module')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-daemon/archive/$pkgver.tar.gz"
        https://raw.githubusercontent.com/jouyouyun/tap-gesture-patches/master/patches/dde-daemon_3.2.3.patch
        'deepin-daemon.sysusers' 'nm-1.10.patch')
sha512sums=('7f2e95d08721bf53fc0fabf3831cba9f779170607d288936f50f4d3c271e4ade4f9451b9bac74df2ddf2d7e7c1cf7333d07df07da1170dd732f25d128f32ffc3'
            '29874d6b316bb678b6656827e696d65eef174a53bb8226ed4c2d3bc89e42d5edb466df3c2ac647a1f2eb05d6e2a199b8d07dcaa9302b9227a0692fde0d50bb52'
            '808c02d4fec4cbbb01119bbb10499090199e738b7dd72c28a57dde098eef6132723f3434c151f79e21d9f788c7f7bae8046573ac93ba917afe0e803fbffa6d5a'
            'd4d152a0dc86ac8acc8131b00b52e8895367119ee08931f42f1bfcadac4e1bf766070db33f97f29f99cba3a68b56c7bbe04269f431121ff7df8b6bfa517c21b4')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/linuxdeepin/go-x11-client \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         github.com/BurntSushi/freetype-go/freetype github.com/BurntSushi/freetype-go/freetype/truetype \
         github.com/BurntSushi/graphics-go/graphics github.com/fsnotify/fsnotify golang.org/x/sys/unix \
         gopkg.in/yaml.v2 github.com/axgle/mahonia github.com/msteinert/pam github.com/nfnt/resize \
         golang.org/x/text/encoding/charmap

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' dde-daemon-$pkgver/accounts/user.go

  cd dde-daemon-$pkgver
  patch -p1 -i ../dde-daemon_3.2.3.patch
  patch -p1 -i ../nm-1.10.patch
}

build() {
  cd dde-daemon-$pkgver
  make -C network/nm_generator gen-nm-code
  make # USE_GCCGO=1
}

package() {
  cd dde-daemon-$pkgver
  make DESTDIR="$pkgdir" install # USE_GCCGO=1

  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
}
