# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.5
pkgrel=1
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('x86_64')
url='https://goaccess.io'
license=('GPL')
depends=('ncurses' 'libmaxminddb' 'openssl')
optdepends=('geoip2-database: for geoip support')
backup=('etc/goaccess/goaccess.conf')
source=("https://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('7404a487dc1eb52d83caf01775ad0c9bb5c962da27b3ec9a6d613306e19a6109f7337410a812781bdf238ba9efcbe5419a897bc10b5b11e3c3ac38be176c18b4')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip=mmdb --enable-utf8 --with-openssl
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}
