# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: NOGISAKA Sadata <ngsksdt@gmail.com>
# Contributor: Bennett Piater <bennett at piater dot name>

pkgname=doctl
pkgver=1.72.0
pkgrel=1
pkgdesc='The official command line interface for the DigitalOcean API'
url='https://github.com/digitalocean/doctl'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go')
source=(https://github.com/digitalocean/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('12e3aa209193f73054387b389de407e990fc4e2bc7a6466f1cc2b6bbbb0330ca43dc7ce7faba793df82a6a733ecd19964e54df24493b938473faffbb6a391421')
b2sums=('97ad78273d8779b31e1b322d01897cbccb3d886f1d030d55d6763673cd7c873cc7e37f771c1e5f8b1b6119d958139f7f055b44f8a7ab19306c9d68593c452583')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/...
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 "${pkgname}" -t "${pkgdir}/usr/bin"

  # setup completions
  install -dm 755 "${pkgdir}/usr/share/bash-completion/completions" \
                  "${pkgdir}/usr/share/zsh/site-functions" \
                 "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/${pkgname}" completion bash > "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
