# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Evaggelos Balaskas <eblaskas _AT_ ebalaskas _DOT_ gr>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor: Nikos Skalkotos <skalkoto (at) Gmail.com>

pkgname=libguestfs
pkgver=1.40.2
_pkgver_short=${pkgver%.*}
pkgrel=4
pkgdesc="Access and modify virtual machine disk image"
arch=("x86_64")
url="http://libguestfs.org"
license=("GPL2" "LGPL2.1")
backup=("etc/libguestfs-tools.conf"
        "etc/xdg/virt-builder/repos.d/libguestfs.conf"
        "etc/xdg/virt-builder/repos.d/libguestfs.gpg"
        "etc/php.d/guestfs_php.ini")
_pythonver=2
depends=("qemu-headless"
         "augeas"
         "hivex>=1.3.2"
         "libconfig"
         "libvirt"
         "fuse"
         "file"
         "cpio"
         "wget"
         "perl"
         "perl-string-shellquote"
         "perl-libintl-perl"
         "perl-sys-virt"
         "jansson"
         "supermin")
makedepends=("pcre"
             "cdrkit"
             "libxml2"
             "gperf"
             "python${_pythonver}"
             "lua"
#             "ghc"
#             "ruby"
             "erlang"
             "gjs"
             "php"
             "perl-module-build"
             "gobject-introspection"
             "ocaml-findlib"
             "ocaml"
             "go-pie"
             "bash-completion"
             "jdk8-openjdk"
             "yara"
             "sleuthkit")
optdepends=("python${_pythonver}: Python bindings"
#            "ruby: Ruby Bindings"
            "gjs: Javascript Bindings for GNOME"
            "php: PHP bindings"
            "erlang: Erlang Bindings"
#            "ghc: Haskell Bindings"
            "lua: Lua Bindings"
            "ocaml: Ocaml libs")
provides=("guestfish=${pkgver}")
source=("http://libguestfs.org/download/${_pkgver_short}-stable/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('d288f15d5a70b4b87a4271ea2eb7dc0ec4f48fdb136b8f62003352a069f75573de873e1063763889f410fb1b99951252e9a48dd73bf0233047f21aae43ac6de2'
            'SKIP')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    PYTHON=python$_pythonver \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libdir=/usr/lib \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --disable-static \
    --disable-haskell \
    --disable-ruby
  # ^ haskell & ruby bindings do not build

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
