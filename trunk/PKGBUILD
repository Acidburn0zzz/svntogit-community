# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlschema
pkgver=1.0.17
pkgrel=1
pkgdesc="An XML Schema validator and decoder"
url="https://github.com/sissaschool/xmlschema"
license=('MIT')
arch=('any')
depends=('python-elementpath')
makedepends=('python-setuptools')
checkdepends=('python-lxml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sissaschool/xmlschema/archive/v$pkgver.tar.gz")
sha512sums=('58ddf91d204ac06ca6cd92257021611a707186144877d28d4261b25df7293de429d338a4633708e8cc73f2e92ffd1f25e78fee0c803a6087e9a6ac8cf1ffcb32')

prepare() {
  sed -i 's/~=/>=/' xmlschema-$pkgver/setup.py
}

build() {
  cd xmlschema-$pkgver
  python setup.py build
}

check() {
  cd xmlschema-$pkgver
  python -c "import xmlschema.codepoints; xmlschema.codepoints.save_unicode_categories()"
  cp xmlschema/unicode_categories.json build/lib/xmlschema/
  PYTHONPATH="$PWD/build/lib" python xmlschema/tests/test_all.py
}

package() {
  cd xmlschema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
