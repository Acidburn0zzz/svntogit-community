# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=yaz
pkgver=4.2.43
pkgrel=1
pkgdesc='Toolkit supporting the development of Z39.50/SRW/SRU clients and servers'
arch=('i686' 'x86_64')
url="http://www.indexdata.dk/yaz"
license=('BSD')
depends=('openssl' 'libxslt' 'icu')
options=('!libtool')
source=(http://ftp.indexdata.dk/pub/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('849419a3e647568f2cc9a6097e009f0be8f379759d6d13274eb5d19314b1bfc2')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr \
              --enable-shared=yaz \
              --with-openssl=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
