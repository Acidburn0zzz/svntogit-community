# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=(gmic zart gimp-plugin-gmic krita-plugin-gmic digikam-plugin-gmic)
pkgver=2.9.1
pkgrel=1
arch=(x86_64)
url="https://gmic.eu/"
license=(custom:CeCILL)
makedepends=(cmake gimp qt5-base fftw openexr opencv eigen graphicsmagick qt5-tools digikam)
source=(https://gmic.eu/files/source/gmic_$pkgver.tar.gz
        gmic-source-$pkgver.tar.gz::https://github.com/dtschump/gmic/archive/v.2.9.1.tar.gz)
sha256sums=('55b1f6514f3e4f8861ef61b66e061b509abd82f87fb72bad0be3b9401dd20a00'
            '18a38c175b32340508fdc3282087505d056ab113088003141bb85ae6483f4c19')

prepare() {
  mkdir -p build{,-qt,-krita,-gimp,-digikam,-zart}

# Move cmake modules
  cp -r gmic-v.$pkgver/cmake gmic-$pkgver

  sed -e 's|opencv|opencv4|' -i gmic-$pkgver/zart/zart.pro
}

build() {
  cmake -B build -S gmic-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON
  cmake --build build

  export LDFLAGS="$LDFLAGS -L../build"
  cmake -B build-qt -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=none
  cmake --build build-qt

  cmake -B build-gimp -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=gimp
  cmake --build build-gimp

  cmake -B build-krita -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=krita
  cmake --build build-krita

  cmake -B build-digikam -S gmic-$pkgver/gmic-qt \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_DYNAMIC_LINKING=ON \
    -DGMIC_PATH="$srcdir"/gmic-$pkgver/src \
    -DGMIC_QT_HOST=digikam
  cmake --build build-digikam

  cd build-zart
  unset CXXFLAGS # https://github.com/c-koi/zart/issues/14
  qmake ../gmic-$pkgver/zart \
    GMIC_PATH="$srcdir"/gmic-$pkgver/src GMIC_DYNAMIC_LINKING=on QMAKE_CXXFLAGS+=" -DOPENCV2_HEADERS"
  make
}

package_gmic() {
  pkgdesc="GREYC's Magic Image Converter: image processing framework"
  depends=(graphicsmagick opencv fftw)
  optdepends=('qt5-base: for the Qt UI')

  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  DESTDIR="$pkgdir" cmake --install build-qt

  install -Dm644 gmic-$pkgver/resources/gmic_cluts.gmz -t "$pkgdir"/usr/share/gmic
}

package_zart() {
  pkgdesc="A GUI for G'MIC real-time manipulations on the output of a webcam"
  depends=(gmic qt5-base)

  cd build-zart
  install -Dm755 zart -t "$pkgdir"/usr/bin
  install -Dm644 ../gmic-$pkgver/zart/Licence_CeCILL_V2-en.html -t "$pkgdir"/usr/share/licenses/$pkgname
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the G'MIC image processing framework"
  depends=(gimp gmic qt5-base)

  DESTDIR="$pkgdir" cmake --install build-gimp
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_krita-plugin-gmic() {
  pkgdesc="Krita plugin for the G'MIC image processing framework"
  depends=(krita gmic)

  DESTDIR="$pkgdir" cmake --install build-krita
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_digikam-plugin-gmic() {
  pkgdesc="DigiKam plugin for the G'MIC image processing framework"
  depends=(digikam gmic)

  DESTDIR="$pkgdir" cmake --install build-digikam
  install -Dm644 gmic-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
