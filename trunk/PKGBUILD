# $Id$
# Maintainer: Anatol Pomozov

pkgname=codespell
pkgver=1.9.1
pkgrel=2
pkgdesc='Fix common misspellings in source code'
arch=(any)
url='https://github.com/lucasdemarchi/codespell'
license=(GPL2)
depends=(python)
checkdepends=(python-nose)
source=(https://pypi.io/packages/source/c/codespell/codespell-$pkgver.tar.gz
        default_arg.patch::https://github.com/Eric89GXL/codespell/commit/e9d8629cd950fb92f2d951a1517e1d5a09d2d74b.patch)
sha1sums=('633f23643a740bbb7157d910e400c863942c317a'
          'f4c61625cc20d61a15e2e9ac5c720c5a65ca3118')

prepare() {
  cd codespell-$pkgver
  patch -p1 < ../default_arg.patch
}

build() {
  cd codespell-$pkgver
  python setup.py build
}

check() {
  cd codespell-$pkgver
  python setup.py test
}

package() {
  cd codespell-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
