# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2021.06.09
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation-2021.06.09.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('0aa7b9c76ee7d9b3dbbfca0961d66c47324bbeec5c30f7658c1c99dc1d7898eb3a7dd68339dbc27a857aedf36a5dc2d3178a0dfdc0f429a3ae576eea23eece11'
            'b45426d9eb2ccc95fb2e06c13db28c28ae8d38b671decbebaaa8195a47cd5c3f83ff69a4b0a2b6378b30b431c5aeaedeac9ab3bc5fd70b8c42da809d25c41664')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation-2021.06.09.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links

  # Broken filenames are not dealt since reported in June 2020. Let's clean them up ourselves.
  find $pkgname-$pkgver -name "* 2.svg" -delete
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
