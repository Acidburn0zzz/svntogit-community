# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nextcloud
pkgver=21.0.0
[[ "$pkgver" == *.0 ]] && { echo "wait for .1 version" ; exit 1; }
pkgrel=4
pkgdesc="A cloud server to store your files centrally on a hardware controlled by you"
arch=('any')
url="https://nextcloud.com"
license=('AGPL3')
depends=('php>=8.0.0' 'php<8.1.0' 'php-gd')
optdepends=(
  'ffmpeg: for preview generation'
  'mariadb: to use a local MariaDB server'
  'memcached: to use a local memcached server'
  'php-apcu: for caching with APCU'
  'php-apache: to use the Apache web server'
  'php-fpm: to run in fastCGI process manager'
  'php-imagick: for preview generation'
  'php-imap: for external user authentication'
  'php-intl: to increase language translation performance and fix sorting'
  'php-ldap: for LDAP authentication'
  'php-memcached: for caching with memcached'
  'php-pgsql: to use the PostgreSQL database backend'
  'php-redis: for caching with redis'
  'php-sqlite: to use the SQLite database backend'
  'postgresql: to use a local PostgreSQL server'
  'redis: to use a local Redis server'
  'smbclient: for SMB/CIFS integration'
  'sudo: for occ command'
  'libreoffice: for preview generation'
  'uwsgi-plugin-php: run as application container - php7 only'
)
backup=(
  "etc/webapps/${pkgname}/config.php"
  "etc/uwsgi/${pkgname}.ini"
)
# there are no elf files, no need to strip anything
options=(!strip)
install="${pkgname}.install"
source=(
  "https://download.nextcloud.com/server/releases/nextcloud-${pkgver}.tar.bz2"{,.asc}
  "${pkgname}-21.0.0-config.patch"
  'apache.example.conf'
  "${pkgname}.hook"
  "${pkgname}.occ.sh"
  "${pkgname}.sysusers"
  "${pkgname}.tmpfiles"
  "${pkgname}.uwsgi"
)
sha512sums=('a1ae3400f0fb5997e54b0cee821c50d13e4ba0e27c9d11731c8b5233cba564666121a58aed848ccd2593a9783e27db1ed98ff559588a76a1acaeaf083ef4404a'
            'SKIP'
            '7e5f72591e7a8d2dde9d1cca2a3cff2bd5e48a8e938540dd94dbdc2eb35056a5d4f60f0c0bdbfdd080474e078b4f36b61650d9ed38927e50b29a29613506fd39'
            '6f9f284d78d414a3bb7c159a812b105e31e8cd2393afee44465701e6f18169709f6d492d790e403e1e66f308c51b6b6496d29ddce29e4fac2c73a3c84a10c7fe'
            '7b18b73ee369607b8ee3d00d2a054262a7b8f3033fc4873eb333c9b9333d7338870c035910ae123b564a65d7d245730178137311048f0713d5bc524de3654923'
            'cd86397e08459c9f06cfd44ff27a35879157166f5e8aab95d51bc7c4bb11f93e7c414a08ec1cbe9e81f1b4edfbf5eb5eaba7c6904da80a2c92c765bcb546aa66'
            '1d06f339c43d57e5f5406c2698b815241fd9e39039a9e55f777face510c9a5ddae5ccd4e051393e8f16f391301a6aa03fff2462837a2d3441d969ea7195e7d84'
            '4f951e7cad06940d983035c3dc51531bbbcbc0c74296af63f2fab1705b413f340ff67300506377fade0ed7ddecee8552b3da028711751c120f1fad4b1c33e8d5'
            'a3ebcabcdb8c2d2653f44a248abd213bc4dbb317766b82c8f3e93b31e72ace350d7e8037bd4e22a632ab33f739e1941467b0e9d2cf7d0a7433227b4169030051')
b2sums=('7c583c150f6b315830191329310993582439e7aef57f306b93681d654b8fa7d4d5dafee082befd493aafd2bf6efd940d52ca0ca05b8e42b79a1599ff3e809550'
        'SKIP'
        '2b84b21e40c9493869670e851b545692d3a53705a0ee3564dd002be135c553b73ad124f27e91f0c2bda1ce110e8bd5b62c0678f5fead4598826cb41dbc2dc3d3'
        'a7e1aa1d8cb2b0ff7832bb7d6d857987733253317c0f3727e744502aeaa3b2008351b47ecc24e348fe7b252cefaffe2418797523ee288dfd55e128917e8fbe2c'
        '772a9571fe7d88dbcbeb46351a188354e237dfe8e020131f2752b3a36cff0ff177423b4cfd6a1a5f28946f86a8581f3e24ee06ebda7260c5f862ca4071908e43'
        '6a90659bf0ab7163fe0cf591abed9b58b9ff09c548698e7f8844ecc710917cf05561bad754f6d7543593d13b41e9ec630042b4fc3fbad43e1011b0e45868e4ed'
        '1a61a89531636b65dade03cd7edca8747b3e1e880f9bbd4d5a53cffa452d70fe17e345cfed739ae92e99e336d4c1f2633c84a5c84a4ee6da518762bf1396b1db'
        'a644617d2b42ab357da546a5c9e3182fa9d2d8ae36ee77b24a04ed036e837212c9cbbd2f40d6e0c6f075a38aa58c7a8c01807ff820c9ee4f6556c661be81e0cb'
        'd3c66e22c3cc1b8de1c0b60cd32701080d599dc9853af41d9d9d493ffe3a8d9a2573455d80dd0de019dfe9992187cb23b9df90c18f0ca5962912566014c6779c')
validpgpkeys=('28806A878AE423A28372792ED75899B9A724937A') # Nextcloud Security <security@nextcloud.com>

prepare() {
  mv -v "${pkgname}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # patch config.sample.php to use FHS compliant paths for state and logs
  patch -Np1 -i ../"${pkgname}-21.0.0-config.patch"
}

package() {
  cd "${pkgname}-${pkgver}"
  # sysusers.d integration
  install -vDm 644 ../${pkgname}.sysusers \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # tmpfiles.d integration
  install -vDm 644 ../${pkgname}.tmpfiles \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # state dir for data and writable apps
  install -vdm 750 "${pkgdir}/var/lib/${pkgname}/"
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/"{apps,data}
  # log dir
  install -vdm 750 "${pkgdir}/var/log/${pkgname}/"
  # uwsgi integration
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # config
  install -vDm 640 config/config.sample.php "${pkgdir}/etc/webapps/${pkgname}/config.php"
  # occ script
  install -vDm 755 "../${pkgname}.occ.sh" "${pkgdir}/usr/bin/occ"
  # optional pacman hook
  install -vDm 644 "../${pkgname}.hook" -t "${pkgdir}/usr/share/doc/${pkgname}/"

  # webapp
  find . -type f \( \
    -iname "*.php" \
    -or -iname "*.bcmap" \
    -or -iname "*.crl" \
    -or -iname "*.crt" \
    -or -iname "*.css" \
    -or -iname "*.dist" \
    -or -iname "*.docx" \
    -or -iname "*.handlebars" \
    -or -iname "*.gif" \
    -or -iname "*.ico" \
    -or -iname "*.ini" \
    -or -iname "*.jpg" \
    -or -iname "*.js" \
    -or -iname "*.json" \
    -or -iname "*.lock" \
    -or -iname "*.map" \
    -or -iname "*.md" \
    -or -iname "*.odp" \
    -or -iname "*.ods" \
    -or -iname "*.odt" \
    -or -iname "*.pdf" \
    -or -iname "*.png" \
    -or -iname "*.properties" \
    -or -iname "*.scss" \
    -or -iname "*.svg" \
    -or -iname "*.ttf" \
    -or -iname "*.txt" \
    -or -iname "*.whiteboard" \
    -or -iname "*.woff" \
    -or -iname "*.yml" \
    -or -iname "*.xml" \
    -or -iname "*.xsd" \
    -or -iname "*COPYING*" \
    -or -iname "*LICENSE*" \
    -or -iname "*CAN_INSTALL" \
    -or -iname "*.htaccess" \
    \) -exec install -vDm 644 {} "${pkgdir}/usr/share/webapps/${pkgname}/"{} \;

  # config symlink
  ln -sv "/etc/webapps/${pkgname}/config.php" "${pkgdir}/usr/share/webapps/${pkgname}/config/config.php"
  # occ
  install -vDm 755 occ -t "${pkgdir}/usr/share/webapps/${pkgname}"
  # apache example conf
  install -vDm 644 ../apache.example.conf -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # add symlink for default writable apps directory
  ln -sv "/var/lib/${pkgname}/apps" "${pkgdir}/usr/share/webapps/${pkgname}/wapps"
}
