# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: mexus <gilaldpellaeon@gmail.com>
pkgname=dua-cli
pkgver=2.3.9
pkgrel=2
pkgdesc="A tool to conveniently learn about the disk usage of directories, fast!"
arch=('x86_64')
url="https://github.com/Byron/dua-cli"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Byron/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('6bba3407893162ca8db9e6573740aa0c9e03f70a0a8a21cee9e1e12c2680bdc9812fe539407ef515a635dce09c255305cedb536e4f6f2df40b7671545eab30b7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  cargo build --release --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 755 "target/release/dua" "$pkgdir/usr/bin/dua"
}
