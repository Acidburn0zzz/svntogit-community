# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.5.2pre1
pkgrel=1
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='http://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
source=("${url}pub/pari/unix/${pkgname}-${pkgver}.tar.gz")
source=("${url}pub/pari/snapshots/pari-2.5.2.pre1.tar.gz")
sha1sums=('3efe590e885a7dde521b8553762d3207b919e69f')

build() {
	cd "${srcdir}/${pkgname}-${pkgver/pre1/}"
	./Configure --prefix=/usr --with-gmp --with-readline
	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver/pre1/}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver/pre1/}"
	make DESTDIR="${pkgdir}" install

	# symlink pointing to a symlink that is going to be moved by zipman
	rm "${pkgdir}"/usr/share/man/man1/pari.1
	ln -s gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1.gz
}
