# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=1.8.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/cinnamon-screensaver"
backup=(etc/pam.d/cinnamon-screensaver)
depends=('cinnamon' 'gnome-screensaver')
makedepends=('intltool' 'gnome-common')
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::https://github.com/linuxmint/cinnamon-screensaver/archive/$pkgver.tar.gz
       cinnamon-screensaver.pam
       fix-dbus-compatibility.patch)
sha256sums=('e85722d5af2ecbed98f50909285b335d7635214bf8cada49964f60bb5d1339c1'
            'b6ea9e2eb586d94bcabb617a8f1c2958111df87afdbb51f645882bccdc15cbda'
            'eebd1d056c21d2d3f60fb954677b9214fd1d575ed1c2bbb56326cedf41a39eb6')

build() {
  cd $pkgname-$pkgver


  # Fix compatibility with gnome-screensaver's D-Bus interface and various fixes
  patch -Np1 -i ${srcdir}/fix-dbus-compatibility.patch

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon-screensaver \
               --localstatedir=/var \
               --with-mit-ext --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../cinnamon-screensaver.pam "$pkgdir/etc/pam.d/cinnamon-screensaver"

  # Remove conflicts with gnome-screensaver
  rm "$pkgdir"/usr/bin/gnome-screensaver{,-command}

  # Autostart only in Cinnamon
  cp "$pkgdir/etc/xdg/autostart/cinnamon-screensaver.desktop" \
    "$pkgdir/etc/xdg/autostart/cinnamon2d-screensaver.desktop"
  sed -i 's/^OnlyShowIn=.*/OnlyShowIn=GNOME;/' \
    "$pkgdir"/etc/xdg/autostart/cinnamon{,2d}-screensaver.desktop
  echo 'AutostartCondition=GNOME3 if-session cinnamon' \
    >> "$pkgdir/etc/xdg/autostart/cinnamon-screensaver.desktop"
  echo 'AutostartCondition=GNOME3 if-session cinnamon2d' \
    >> "$pkgdir/etc/xdg/autostart/cinnamon2d-screensaver.desktop"
}
