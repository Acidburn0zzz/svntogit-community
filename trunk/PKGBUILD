# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=2.4.0
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='https://cisofy.com/lynis/'
arch=('any')
license=('GPL3')
backup=('etc/lynis/default.prf')
depends=('sh' 'awk')
optdepends=('net-tools: networking tests'
            'bind-tools: nameserver tests'
            'iptables: firewall tests'
            'bash-completion: completion for bash')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CISOfy/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('e17c3f071aebeaf8a91a3d7e1515bdb8c36548e9f732728fdc8c7a7acf61cbcf5fed6765f2f5788218f6a778f139fd2004f2b10bc4a17d5ce5427c77ffa49a4a')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
}

package() {
  cd ${pkgname}-${pkgver}

  # binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -ra db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -Dm 644 README INSTALL CHANGELOG.md FAQ \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.8" -t "${pkgdir}/usr/share/man/man8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} \
    -t "${pkgdir}/usr/share/bash-completion/completions"

  # systemd
  install -Dm 644 extras/systemd/{lynis.service,lynis.timer} \
    -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
