# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

_pkg=h5py
pkgbase=python-$_pkg
pkgname=(python-$_pkg python2-$_pkg)
pkgver=2.9.0
pkgrel=3
pkgdesc="General-purpose Python bindings for the HDF5 library"
url="https://www.h5py.org/"
arch=('x86_64')
license=('BSD')
makedepends=('hdf5' 'cython' 'cython2' 'python-numpy' 'python2-numpy' 'python-six' 'python2-six'
             'python-pkgconfig' 'python2-pkgconfig')
checkdepends=('python2-unittest2')
source=("https://files.pythonhosted.org/packages/source/h/$_pkg/$_pkg-$pkgver.tar.gz"{,.asc}
        h5py-hdf5-1.10.5.patch::"https://github.com/h5py/h5py/commit/141eafa531c6c09a06efe6a694251a1eea84908d.patch")
sha256sums=('9d41ca62daf36d6b6515ab8765e4c8c4388ee18e2a665701fef2b41563821002' 'SKIP'
            '01fff5338ea37458856660cc5cfee5263b7d20d32fadd31ae45bd1e10d085e11')
validpgpkeys=('AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7' # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>
              '96B7334D7610EE3E68AFFE589E027116943D6A8B') # Thomas A Caswell <tcaswell@bnl.gov> (new key)

prepare() {
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" $_pkg-$pkgver/setup_build.py
  (cd $_pkg-$pkgver && patch -p1 -i ../h5py-hdf5-1.10.5.patch)
  cp -a $_pkg-$pkgver{,-py2}
}

build() {
  cd $_pkg-$pkgver
  python setup.py build

  cd ../$_pkg-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkg-$pkgver
  python setup.py test

  cd ../$_pkg-$pkgver-py2
  python2 setup.py test
}

package_python-h5py() {
  depends=('hdf5' 'python-numpy' 'python-six')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-h5py() {
  depends=('hdf5' 'python2-numpy' 'python2-six')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
