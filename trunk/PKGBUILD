# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=5.2.0.2
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'polkit-qt5')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-polkit-agent/archive/$pkgver.tar.gz")
sha512sums=('3e17bb7d2b34547fc5afdd95b670b842806eb3e508d4afe7dd509d85edf1713162957bccba3955ece0faa8141fb60191b60969958ddf17656e3211f43368c4a1')

prepare() {
  cd dde-polkit-agent-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/1721
  sed -i 's/bool is_deepin = true/bool is_deepin = false/' policykitlistener.cpp
}

build() {
  cd dde-polkit-agent-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-polkit-agent-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
