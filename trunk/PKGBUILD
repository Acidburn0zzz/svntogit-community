# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph W <c w e g e n e r at gmail dot com>

pkgbase=('python-ntlm-auth')
pkgname=('python-ntlm-auth' 'python2-ntlm-auth')
_module='ntlm-auth'
pkgver='1.0.2'
pkgrel=2
pkgdesc="Creates NTLM authentication structures"
url="https://github.com/jborean93/ntlm-auth"
makedepends=('python-setuptools' 'python2-setuptools')
license=('LGPL-3.0')
arch=('any')
source=("https://pypi.python.org/packages/ac/99/5183cbb714537e0bd31dd266f2bad894f17112f862bb5c65fd6a5367dd2b/ntlm-auth-${pkgver}.zip")
sha512sums=('62e1f5c6a8936c3c79cf3827bdd33c2a0d3cdc0b1e617d7fb4d8e4a1d5ce36e02809200a1a1e1cc41303e99a5ea5fb1cf601f345b88d19f679afb4c4517ba8fe')

prepare() {
    cp -a "${srcdir}/${_module}-${pkgver}"{,-python2}
}

build() {
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py build

    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py build
}

package_python-ntlm-auth() {
    depends+=('python' 'python-six' 'python-ordereddict')
    cd "${srcdir}/${_module}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-ntlm-auth() {
    depends+=('python2' 'python2-six' 'python2-ordereddict')
    cd "${srcdir}/${_module}-${pkgver}-python2"
    python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
