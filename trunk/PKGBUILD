# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=1.3
pkgrel=1
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose-show-skipped' 'python-numpy' 'python-feedparser' 'python-coverage'
              'python-simplejson' 'python-pymongo' 'python-ujson')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jsonpickle/jsonpickle/archive/v$pkgver.tar.gz"
         jsonpickle-python-3.8.patch)
sha512sums=('569c428687f71c8b7b6fa043d573b2454e351fe5564e1934b92e33ae8c50a952ad10daabf633436ae0ea3a5ff03f07a4a95dc17be7b587e9178f775c6b2d7931'
            'a07bda01508bbbe36f31c9f3be46507bff0d3f81cfb0df5eb6702690db1ba5d853aab1065b7a79e89bc9c8abaad01dd9fc6f71e59194f819addf99b0c28f0c1b')

prepare() {
  cd jsonpickle-$pkgver
# Fix tests with python 3.8 - file descriptors no longer have __getstate__
  patch -p1 -i ../jsonpickle-python-3.8.patch
}

build() {
  cd "$srcdir"/jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/jsonpickle-$pkgver
  nosetests3 --show-skipped
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
