# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=3.2
pkgrel=3
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://flufllock.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
replaces=(python-flufl.lock)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('496195e1c52f628bc6c0e8fc3dcd5373ed01cfee35b6165cae37ab4b90d70265c6a0aea565f1e0663277c2b9e9bafde8af1420133a15e6254275fad2f28a56d0')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
