# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=intel-media-driver
pkgver=19.2.1
pkgrel=2
pkgdesc="Intel Media Driver for VAAPI â€” Broadwell+ iGPUs"
arch=(x86_64)
url="https://github.com/intel/media-driver/"
license=(MIT BSD)
depends=(gcc-libs intel-gmmlib libva libpciaccess)
makedepends=(cmake)
source=("https://github.com/${pkgname/-//}/archive/${pkgname%-*}-${pkgver}.tar.gz")
sha256sums=('79279c991d918deaa52239dac55787b9c7562e520cd58744171b468d28b8af40')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname#*-}-${pkgname%-*}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_DRIVER_SYSCONF=OFF \
        -Wno-dev
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 ../${pkgname#*-}-${pkgname%-*}-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
