# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=buildah
pkgver=1.6
pkgrel=1
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(ostree runc skopeo)
makedepends=(go-pie git bash-bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo)
_commit=5f95bd9a350a2dcff776914c19192df1cf272b97  # tags/v1.6
source=(git://github.com/containers/buildah.git#commit=$_commit
        0001-Revert-SELinux-support.patch)
md5sums=('SKIP'
         '19fc0a2a56c4c5e8257bbe7bc5af08c0')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

prepare() {
  pushd buildah/vendor/github.com/containers/image/
  patch -p1 -i "$srcdir/0001-Revert-SELinux-support.patch"
  popd

  mkdir -p src/github.com/containers
  cp -r $pkgname src/github.com/containers
}

build() {
  export GOPATH="${srcdir}"
  cd src/github.com/containers/buildah
  make PREFIX=/usr
}

package() {
  cd src/github.com/containers/buildah
  make DESTDIR="$pkgdir" PREFIX=usr install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
