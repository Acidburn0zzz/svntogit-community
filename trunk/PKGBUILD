# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Campbell <davekong@archlinux.us>
# Contributor: Cilyan Olowen <gaknar@gmail.com>

pkgbase=python-paste
pkgname=(python-paste python2-paste)
pkgver=3.0.2
pkgrel=1
pkgdesc="Tools for using a Web Server Gateway Interface stack"
arch=('any')
url="http://pythonpaste.org/index.html"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
optdepends=('python2-flup: WSGI utilities'
            'python-openid: Support for OpenID')
source=("https://files.pythonhosted.org/packages/source/P/Paste/Paste-${pkgver}.tar.gz")
sha512sums=('cd825a4c9bbadf6d6f69eb2de18dd5b4ec8dd433981bed2432f784a58b4991155184afd20e9f15719ed22ae54760c0f75ac919c99fa3ed46ade34136207547ed')

prepare() {
  # https://github.com/cdent/paste/pull/13
  sed -i 's/nose/pytest/' Paste-${pkgver}/setup.py

  cp -a Paste-${pkgver}{,-py2}

  cd Paste-${pkgver}-py2
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
         -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      $(find . -name '*.py')
}

build() {
  cd "$srcdir"/Paste-$pkgver
  python setup.py build

  cd "$srcdir"/Paste-$pkgver-py2
  python2 setup.py build
}

check() {
  # Excluding 'test_paste_website' because pythonpaste.org
  # redirects to web.archive.org, and thus breaks the test

  cd "$srcdir"/Paste-$pkgver
  python setup.py pytest

  cd "$srcdir"/Paste-$pkgver-py2
  python2 setup.py pytest
}

package_python-paste() {
  depends=('python-six')

  cd Paste-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-paste() {
  depends=('python2-six')

  cd Paste-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
