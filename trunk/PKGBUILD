# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: rilian-la-te <ria.freelander@gmail.com>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=appmenu-gtk-module
pkgver=0.7.1
pkgrel=1
pkgdesc="Application Menu GTK+ Module"
depends=()
makedepends=(cmake gtk3 gtk2)
optdepends=('gtk3: GTK3 module' 'gtk2: GTK2 module')
url="https://gitlab.com/vala-panel-project/vala-panel-appmenu/"
arch=(x86_64)
license=(LGPL3)
source=("https://gitlab.com/vala-panel-project/vala-panel-appmenu/uploads/0315cbb6b1b9b9c4e4d573d23d3eaa1d/appmenu-gtk-module-$pkgver.tar.xz"
        80-appmenu-gtk-module)
sha256sums=('85c165ae1be49aac8188f9bdeb76e84aaa379786e52dcbe6c4df1fb9e091c1f7'
            '4c006c4ea7b8556070ad6d35529d3a9e23da8033429e34d1824c25942d969fbc')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/etc/X11/xinit/xinitrc.d/
  install -m755 "$srcdir"/80-appmenu-gtk-module "$pkgdir"/etc/X11/xinit/xinitrc.d/
}
