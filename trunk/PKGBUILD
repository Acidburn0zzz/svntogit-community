# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=btfs
pkgver=2.14
pkgrel=1
pkgdesc="A bittorrent filesystem based on FUSE"
arch=('i686' 'x86_64')
url="https://github.com/johang/btfs"
license=('GPL')
depends=('fuse2' 'libtorrent-rasterbar' 'curl')
makedepends=('boost')
optdepends=('python2: for btplay')
source=("$pkgname-$pkgver.tar.gz::https://github.com/johang/btfs/archive/v$pkgver.tar.gz")
sha512sums=('f5034e1f8c247324ebb612656d3d06eb87ba468e95dba2421108bb7e91ac5ddd19588bc1263d80be8af55e30acdb65e3ff07b5562f97e4bb8d595ac9f3d2746b')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python$|#!/usr/bin/env python2|' scripts/btplay
  autoreconf -i
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
