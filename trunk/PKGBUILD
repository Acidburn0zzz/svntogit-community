# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=suil
pkgver=0.10.2
pkgrel=1
pkgdesc="A lightweight C library for loading and wrapping LV2 plugin UIs"
arch=('x86_64')
url="https://drobilla.net/software/suil/"
license=('custom:ISC')
makedepends=('gtk2' 'gtk3' 'lv2' 'python' 'qt5-base')
optdepends=('qt5-base: Qt5 UI wrapping support'
            'gtk2: GTK+ 2.x UI wrapping support'
            'gtk3: GTK+ 3.x UI wrapping support')
source=("https://download.drobilla.net/${pkgname}-${pkgver}.tar.bz2"{,.sig})
sha512sums=('8f5b4d772e58dfc4563a5102d189d886218fdc56e74c9d8c118f7bbbcc5d056904c2c8d132f993187416145383d3d8cb4787e2f8c113e57ad47e5d4def7f2fba'
            'SKIP')
validpgpkeys=('907D226E7E13FA337F014A083672782A9BF368F3') # David Robillard

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove local call to ldconfig
  sed -i "/ldconfig/d" wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  python waf configure --prefix=/usr
  python waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  python waf install --destdir="${pkgdir}"
  # license
  install -vDm 644 COPYING \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -t "$pkgdir/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,NEWS,PACKAGING,README.md}
}

# vim:set ts=2 sw=2 et:
