# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.5.4
pkgrel=2
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unzip' 'p7zip' 'curl' 'xorg-xrandr'
         'python-pillow' 'python-requests' 'gnome-desktop' 'webkit2gtk' 'gvfs' 'mesa-demos')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'python-nose-cover3' 'wine' 'xterm')
optdepends=('wine: Windows support'
            'vulkan-icd-loader: Vulkan support'
            'lib32-vulkan-icd-loader: Vulkan support')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lutris/lutris/archive/v$pkgver.tar.gz"
        'sort_new_with_model_fix.patch')
sha512sums=('758c2a854912e29ac579c2030ec118e406db97aa0ecb9be394b2910e48e19c87a774972299b0c83e73fb57e8db3c98a2c52d1545e8071e378d3aff263d325cc4'
            '4c80024c47ae7165a57d94b4421bf8d61b926b5957b231570b0b2b3489d88ac08b1b16ba337ce9bc50174aa057987764363a42465af643286e5dd9cdce16e5d9')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../sort_new_with_model_fix.patch
}

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

check() {
  cd $pkgname-$pkgver

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

