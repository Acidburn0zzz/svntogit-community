# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=csfml
pkgver=2.5
pkgrel=2
pkgdesc='C bindings for sfml'
arch=('x86_64')
url='http://www.sfml-dev.org/'
license=('zlib')
depends=("sfml")
makedepends=('cmake' 'doxygen')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/SFML/CSFML/archive/${pkgver}.tar.gz")
sha512sums=('186ed87d8b925cfd51cc4aa0ba440407d18adc875da760576b5fafdf3e8c7e01ca0f594d9950495eb3e607c65bff12341a81df24af81043dce89d25d7cc24626')

build() {
  cd CSFML-*

  mkdir build && cd build
  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCSFML_BUILD_DOC=true
  make
  make doc
}

package() {
  cd CSFML-*/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 ../license.txt ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

