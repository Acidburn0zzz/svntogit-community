# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukas Spies <lukas (at) photoqt _dot_ org>

pkgname=python-symengine
_pkgname=symengine.py
pkgver=0.7.2
pkgrel=2
arch=(x86_64)
pkgdesc='Python wrappers for SymEngine'
url='https://github.com/symengine/symengine.py'
depends=(python symengine)
makedepends=(cmake cython)
license=(MIT)
source=(https://github.com/symengine/symengine.py/archive/v$pkgver/$_pkgname-$pkgver.tar.gz
        https://github.com/symengine/symengine.py/commit/9ea40f34.patch)
sha256sums=('2147e4bc6b616482dc86f332c7009a6a39de0b66c8f4acebc9ad10b5415b4e53'
            'a49aa42e0acddbe4ef4d9d46aacac98e85e178f56ef7909319a4edc550f51cb2')

prepare() {
  patch -d $_pkgname-$pkgver -p1 < 9ea40f34.patch # Fix build with symengine 0.8
}

build() {
  cmake -B build -S $_pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $_pkgname-$pkgver/LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
