# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=0.9.90
pkgrel=1
pkgdesc="A Twitter/identi.ca/laconica client for KDE"
url="http://choqok.gnufolks.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('kdebase-runtime' 'qjson' 'qoauth' 'qca-ossl')
makedepends=('cmake' 'automoc4')
install="${pkgname}.install"
source=("http://ftp.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('d3b9751791a08370de2c3fab3bf501ad')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
