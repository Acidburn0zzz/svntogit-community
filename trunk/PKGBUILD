# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=libime
pkgver=0.0.0.20200626
_commit=2c856684d6d7be0fe0e6cc514a09b2a68c8d1e68
_kenlmcommit=96d303cfb1a0c21b8f060dbad640d7ab301c019a
_lm_sc_ver=20140820
_dict_ver=20200613
pkgrel=1
pkgdesc="A library to support generic input method implementation"
arch=('x86_64')
url="https://github.com/fcitx/libime"
license=('GPL')
depends=('boost-libs' 'fcitx5')
makedepends=('boost' 'extra-cmake-modules' 'ninja' 'python')
source=($pkgname-$_commit.tar.gz::https://github.com/fcitx/libime/archive/$_commit.tar.gz
        kenlm-$_kenlmcommit.tar.gz::https://github.com/kpu/kenlm/archive/$_kenlmcommit.tar.gz
        https://download.fcitx-im.org/data/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
        https://download.fcitx-im.org/data/dict.utf8-$_dict_ver.tar.xz
        https://download.fcitx-im.org/data/table.tar.gz)
noextract=(lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
           dict.utf8-$_dict_ver.tar.xz
           table.tar.gz)
sha512sums=('e900168340dbfcbe0a54f363c251c51ec4e7293365bb9a12cf2ffef84adde7500f9101df0b63546ea515097e80716566695214fbc8fa0aac86c2179db618dceb'
            'c39210f5a52512b23ab20ecd396def4780ea78eef25ed935dec2667ad68ae822c0e53556725374fed6ccb427732a9101e76467b487eb729539f063b28ca17d3e'
            'ea3ede62eb993c4d21ca7cf339635f4686493a0cefd57df4a31f00a001e67502a13ff9612ff667b941b82b47de00be9f3c9689677d04195a7ef8716bac087519'
            '5b062625361810b50d7e7ce2d29c37615c6c0ea8358bddc0159d14870e0bfeb868670c98a4c7ffd2f72b97b5e6449bef4a439ff9eb11659f505d8ca4b5a554bc'
            '99e1d69c679355fcf3e117a1ef39b131cbd3af841387d2335757247d840b90ff6b7323e16fca98cf7a043033faf250d2e9c6d47b96845d18c278a0cf6f30fec9')

prepare() {
  cd $pkgname-$_commit/src/libime/core
  rmdir kenlm
  ln -s "$srcdir"/kenlm-$_kenlmcommit ./kenlm

  cd "$srcdir"/$pkgname-$_commit/data
  ln -s "$srcdir"/lm_sc.3gm.arpa-$_lm_sc_ver.tar.bz2
  ln -s "$srcdir"/dict.utf8-$_dict_ver.tar.xz
  ln -s "$srcdir"/table.tar.gz
}

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$_commit
  ninja test
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
