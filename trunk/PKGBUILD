# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>

pkgname=qgis
pkgver=1.6.0
pkgrel=2
pkgdesc='A Geographic Information System (GIS) that supports vector, raster & database formats.'
url='http://qgis.org/'
license=('GPL')
arch=('i686' 'x86_64')
# update to http://www.qgis.org/wiki/Building_QGIS_from_Source#Overview
depends=('jasper' 'curl' 'qt' 'gsl' 'grass' 'python2' 'pyqt' 'giflib' 'xerces-c' 'cfitsio' 'qwt')
makedepends=('postgis' 'netcdf' 'cmake' 'fcgi')
optdepends=('postgis: postgis support and SPIT plugin'
            'fcgi: qgis mapserver')
options=('!makeflags')
source=("http://qgis.org/downloads/${pkgname}-${pkgver}.tar.bz2"
        'qgis.desktop'
        'fix-compilation.patch'
        'sip.patch')
md5sums=('9ea8a784d970f5009b0e6271820248f6'
         '8ab66039f2aba519b92f52272ec3c13e'
         '3dd026c1491a3515f45854c2908fc7c9'
         'c1b75ba7b9423a557b0febbe39066286')

build() {
  # Fix insecure RPATH is weird, but just works ;)
#        echo "os.system(\"sed -i '/^LFLAGS/s|-Wl,-rpath,.\+ ||g' gui/Makefile core/Makefile\")" >> python/configure.py.in

  cd ${pkgname}-${pkgver}
  patch -Np1 -i ${srcdir}/fix-compilation.patch
  cd ${srcdir}

  rm -rf build
  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGRASS_PREFIX=/opt/grass \
    -DQGIS_MANUAL_SUBDIR=share/man

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  # install some freedesktop.org compatibility
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
