# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-connections
pkgver=40.0.1
pkgrel=2
pkgdesc='Remote desktop client for the GNOME desktop environment'
arch=('x86_64')
url='https://gitlab.gnome.org/GNOME/connections'
license=('GPL3')
depends=('freerdp' 'gtk-vnc')
makedepends=('appstream-glib' 'gobject-introspection' 'itstool' 'meson' 'vala')
groups=('gnome-extra')
options=('!emptydirs')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('be9bcba07cf3faf5ace0ce54cd22f8609b4dc7766e25ee5fd9c180c3959b4d11')

prepare() {
  cd $pkgname-$pkgver
  # Don't use legacy path for metainfo file
  sed -i "s/datadir'), 'appdata/datadir'), 'metainfo/" data/meson.build
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build

  # Remove unneeded development files
  rm -r "$pkgdir"/usr/include/gnome-connections/gtk-frdp \
        "$pkgdir"/usr/lib/gnome-connections/{girepository-1.0,pkgconfig} \
        "$pkgdir"/usr/share/gnome-connections/{gir-1.0,vapi}
}
