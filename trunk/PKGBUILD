# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=3.0.0
pkgrel=1
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz")
sha512sums=('13459ab66eb32a996cabd46ca3c5550df8ffb81e521bb92fc1576705dfe9ab41d512416ee6116ab899342f7943fadb02f4f741032f70ca6a638d779efab70f0d')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
