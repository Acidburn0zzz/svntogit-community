# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=osbuild
pkgdesc='Build Pipelines for Operating System Artifacts'
pkgver=42
pkgrel=1
url="https://www.osbuild.org"
arch=(any)
license=(Apache)
depends=(python)
makedepends=(make python-docutils python-setuptools)
source=($pkgname-$pkgver.tar.gz::https://github.com/osbuild/osbuild/archive/refs/tags/v${pkgver}.tar.gz)
sha256sums=('SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  make man
  python setup.py install --skip-build --root="$pkgdir"

  install -v -m 0755 -d "$pkgdir/usr/lib/osbuild"

  for group in assemblers runners schemas sources stages ; do
    cp -vdr --no-preserve=ownership "$group" "$pkgdir/usr/lib/osbuild/"
  done
}
