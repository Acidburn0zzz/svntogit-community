# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-configtool
pkgver=0.0.0.20200803
_commit=dceb551bf71d018e14c400a78b43c8c04f10d657
pkgrel=1
pkgdesc="Configuration Tool for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-configtool"
license=('GPL')
groups=('fcitx5-im')
provides=('fcitx5-config-qt' 'kcm-fcitx5')
conflicts=('kcm-fcitx5')
replaces=('kcm-fcitx5')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews' 'qt5-x11extras')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'ninja' 'python')
source=("https://github.com/fcitx/fcitx5-configtool/archive/$_commit/fcitx5-configtool-$_commit.tar.gz")
sha512sums=('36dcb738d57ff7c0cbaf72445498eaeafaccea9ae685c56fc6fb1b48b1776eb12c09ea84e3e2db39f9efaa584b149e6c856e74826f6266acc3d6847aed3eb856')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
