# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.10
pkgrel=5
pkgdesc="Lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.4.1' 'gmp' 'haskell-x11=1.5.0.1' 'sh' 'haskell-mtl=2.0.1.0' 'haskell-utf8-string=0.3.7')
makedepends=('setconf' 'gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
install='xmonad.install'
source=("http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz"
	'xmonad-gnome-session.session'
	'xmonad.svg')
md5sums=('f8381e1ec15137863558a454d4466467'
         '9d1bc84a7f32897d32f3fc71ef76228e'
         '72bfa5e62e4e44fe7fa59b6a7593d993')
_comment=('Tiling window manager')

build() {
  cd "$srcdir"
  gendesk

  # xmonad-app.desktop
  cp $pkgname.desktop $pkgname-app.desktop
  setconf $pkgname-app.desktop Icon xmonad.svg
  # Add NoDisplay=true, if needed

  # xmonad-session.desktop
  cp $pkgname.desktop $pkgname-session.desktop
  setconf $pkgname-session.desktop Icon xmonad.svg
  setconf $pkgname-session.desktop Comment "This session starts xmonad"

  # xmonad-gnome-session.desktop
  cp $pkgname.desktop $pkgname-gnome-session.desktop
  setconf $pkgname-gnome-session.desktop Icon xmonad.svg
  setconf $pkgname-gnome-session.desktop Type XSession
  setconf $pkgname-gnome-session.desktop Exec 'gnome-session --session=xmonad'
  setconf $pkgname-gnome-session.desktop Name "Xmonad GNOME"
  # Add TryExec gnome-session, if needed

  cd $srcdir/$pkgname-$pkgver
  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  #  runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad-gnome-session.session $pkgdir/usr/share/gnome-session/sessions/xmonad.session
  install -D -m644 $srcdir/xmonad-gnome-session.desktop $pkgdir/usr/share/xsessions/xmonad-gnome.desktop
  install -D -m644 $srcdir/xmonad-session.desktop $pkgdir/usr/share/xsessions/xmonad.desktop
  install -D -m644 $srcdir/xmonad-app.desktop $pkgdir/usr/share/applications/xmonad.desktop
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
}
