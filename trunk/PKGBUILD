# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=3.6.1
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('cinnamon-desktop' 'dbus-glib' 'libsm' 'libcanberra')
makedepends=('intltool' 'xtrans')
source=(${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz)
sha256sums=('1de855afe097c2fe4d63f0edd4f8876ace2d73620edb65b3c665e91e5a115d9e')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libexecdir=/usr/lib/cinnamon-session \
                --disable-schemas-compile \
                --disable-gconf \
                --enable-qt57-theme-support

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
