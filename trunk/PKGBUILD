# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=uget
pkgver=1.10.2
pkgrel=1
pkgdesc='GTK+ download manager featuring download classification and HTML import'
arch=('i686' 'x86_64')
url="http://urlget.sourceforge.net/"
license=('LGPL')
depends=('libnotify' 'gtk3' 'gstreamer0.10' 'curl' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
optdepends=('aria2: alternative backend')
install=$pkgname.install
source=(http://downloads.sourceforge.net/urlget/$pkgname-$pkgver.tar.gz)
md5sums=('c766e94fcf12e4b425c53c7c598fd702')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-appindicator
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
