# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=1.1.2
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="http://drumstick.sourceforge.net/"
license=('GPL2')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libpulse' 'qt5-svg')
makedepends=('cmake' 'doxygen' 'docbook-xsl')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('6e84fbf2ce717cdb3c669a477de65d2f32c277073d812546267c0a9556f7ead612e3140cbc412fcd560786ca024f201a1eb3ba9de4b037e3ba8d4db89e0e2e29')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DLIB_SUFFIX=
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO}
}
