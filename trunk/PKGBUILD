# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=3.0.1
pkgrel=2
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(dbus-glib cinnamon-desktop libsm libcanberra)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        qt-5.7.patch)
sha256sums=('d8935747bc8aa70518e70a7d338eaf9109b5307d8a44d49dc57e0f15d69e9c89'
            'b85c460d680f5b807be89b12158141e43e8c5133c5e4c347a958ae2e187d6d11')

prepare() {
  cd $pkgname-$pkgver

  # Add support for applying GTK style to QT apps when running QT 5.7+
  # https://github.com/linuxmint/cinnamon-session/pull/75
  patch -Np1 -i ../qt-5.7.patch
}

build() {
  cd $pkgname-$pkgver
  
  CFLAGS="${CFLAGS} -Wno-error=format-nonliteral" ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/cinnamon-session \
    --disable-schemas-compile \
    --disable-gconf \
    --enable-qt57-theme-support
    
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
