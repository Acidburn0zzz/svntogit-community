# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=6.14.1
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz")
sha512sums=('10e411971d016b342ff3a45bd6663bdeec84a2eae8de938fe2103a07ec37e8e524fa203d7ef9f57f2569d724e3cf60a32a4c4cabe6773dc8801d9a900396233e')

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
