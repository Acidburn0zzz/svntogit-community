# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wsgiproxy2
pkgver=0.5.0
pkgrel=1
pkgdesc="A WSGI Proxy with various http client backends"
arch=('any')
license=('MIT')
url="https://github.com/gawel/WSGIProxy2/"
depends=('python-webob' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-requests' 'python-webtest' 'python-waitress')
source=("https://github.com/gawel/WSGIProxy2/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e5747a56930476e865adf8b8d092336ae031dc94ce796ba146cc6c2131561e5d71f3990c61c49e19e137eec5fe739245acb1be07e9e0ded6a7c3b62d79454eb8')

build() {
  cd WSGIProxy2-$pkgver
  python setup.py build
}

check() {
  cd WSGIProxy2-$pkgver
  nosetests3
}

package() {
  cd WSGIProxy2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
