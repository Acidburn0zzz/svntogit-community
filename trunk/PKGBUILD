# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=12.0.1
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/XAMPPRocky/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/XAMPPRocky/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('c1f8e6a0a995d87bfabad6f2950d8d255b5271e2dfcbfccd58b54c8c5a94a594b8dad860bca075850867ea60332ebb5e4aa685837217182d7113abf2561ed06a')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --release --locked --features all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo test --release --locked --features all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

  install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
  install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
