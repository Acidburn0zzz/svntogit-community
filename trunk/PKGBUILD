# Maintainer: Stefan Husmann  <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve> 
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=1.0.0
pkgrel=4
pkgdesc="A python plotting library, making publication quality plots"
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/"
backup=(usr/lib/python2.7/site-packages/matplotlib/mpl-data/matplotlibrc)
depends=('python-dateutil' 'python-pytz' 'python-numpy')
optdepends=('tk: used by the TkAgg backend' 'pygtk: for use with the GTK or GTKAgg backend' 'wxpython: for use with the WXAgg backend' 'pygobject: for use with vala' 'qt:for use with the QT4 backend' 'qt3: for use with qt backend')
makedepends=('tk' 'wxpython' 'pygtk' 'qt' 'qt3')
provides=('matplotlib')
source=(license \
"http://downloads.sourceforge.net/matplotlib/matplotlib-$pkgver.tar.gz")
md5sums=('5e579927a4d338bba112471e52555d02'
         '7d964de548c9887917762a11e0ffecd3')
license=('custom')

build() {
    cd $srcdir/matplotlib-$pkgver
    sed -i "s/rc =.*/rc = dict({'backend':'GTKAgg', 'numerix':'numpy'})/g" \
      setup.py
    sed -i "s/^BUILD_GTKAGG .*/BUILD_GTKAGG = 1/g" setup.py
    sed -i "s/^BUILD_GTK .*/BUILD_GTK = 1/g" setup.py
    sed -i "s/^BUILD_WXAGG .*/BUILD_WXAGG = 1/g" setup.py
    sed -i "s/^BUILD_TKAGG .*/BUILD_TKAGG = 0/g" setup.py
    sed -i "s/^BUILD_QT4 .*/BUILD_QT4 = 1/g" setup.py
    sed -i "s/^BUILD_QT .*/BUILD_QT = 1/g" setup.py
    sed -i 's/^macosx = True/macosx = False/' setup.py
    sed -i 's/^pytz = True/pytz = False/' setup.py
    sed -i 's/^dateutil = True/Dateutil = False/' setup.py

    # python2 fix
    for file in $(find . -name '*.py' -print); do
        sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
    done

    python2 setup.py build
}
package() {
    cd $srcdir/matplotlib-$pkgver
    python2 setup.py install --root $pkgdir --prefix=/usr

    rm -rf $pkgdir/usr/lib/python2.7/site-packages/configobj.py*
    install -Dm644 $srcdir/license \
      $pkgdir/usr/share/licenses/$pkgname/license
}
