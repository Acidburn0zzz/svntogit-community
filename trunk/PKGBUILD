# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.9.0.3
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'vlc')
makedepends=('cmake' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9c306754d67349e225a30ebbf672a7f465b806d4472deb51be0257e18a7682c7c489931d3cf5a1cca6ad918d08f370db878e45a4db7a12d8e1c8273b6aa2cd2d')

prepare() {
  cd deepin-voice-note-$pkgver
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
