# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=python-pytables
pkgver=3.5.2
pkgrel=2
pkgdesc="A package for managing hierarchical datasets and designed to efficiently and easily cope with extremely large amounts of data"
arch=(x86_64)
url="https://www.pytables.org"
license=("BSD")
depends=(blosc lzo hdf5 python-numexpr)
makedepends=(python-setuptools cython)
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/PyTables/PyTables/archive/v${pkgver}.tar.gz")
sha256sums=('e4fc6f1194f02a8b10ff923e77364fb70710592f620d7de35f4d4e064dc70e91')

build() {
  cd PyTables-$pkgver
  python setup.py build --blosc=/usr --lzo=/usr --hdf5=/usr --bzip2=/usr
}

package() {
  cd PyTables-${pkgver}
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}
