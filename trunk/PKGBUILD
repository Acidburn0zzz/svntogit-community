# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=3.6.7
pkgrel=3
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('x86_64')
url="https://github.com/linuxmint/Cinnamon"
license=('GPL2')
depends=('accountsservice' 'caribou' 'cinnamon-control-center' 'cinnamon-menus' 'cinnamon-screensaver'
         'cinnamon-session' 'cinnamon-settings-daemon' 'cjs' 'clutter-gtk' 'gnome-backgrounds'
         'gnome-themes-standard' 'gstreamer' 'libgnomekbd' 'libkeybinder3' 'librsvg'
         'muffin' 'network-manager-applet' 'nemo' 'python2-cairo' 'python-dbus' 'python2-dbus'
         'polkit-gnome' 'python2-gobject' 'python2-pam' 'python2-pexpect' 'python2-pillow'
         'python2-pyinotify' 'xapps')
optdepends=('blueberry: Bluetooth support'
            'cinnamon-translations: i18n'
            'gnome-panel: fallback mode'
            'metacity: fallback mode'
            'system-config-printer: printer settings')
makedepends=('gnome-common' 'intltool' 'gtk-doc' 'gobject-introspection')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/commit/2dc04b8a26fa7a672c579ed62a70dca6c6e4b295.patch"
        "${url}/commit/5c9848a48da3387825f627117ad58521474e0b2e.patch"
        "0001-cs_user.py-Add-compatibility-for-multiple-pam-module.patch"
        "0001-cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch::${url}/pull/7382.patch"
        "set_wheel.patch"
        "default-theme.patch")
sha512sums=('446e0bbf1fcda7e193735115b4c0396e24428e102b0e74d4b1e150c279d97bbf45961fe57ac08e5828f957c390c39bf385bf29ae7549f5fead2bc59389e64add'
            '9cc53e23f7a11b97cb3e03802fdece1dda77a1f809e751dd513e61436e656c1083c63304334b3b7f5c1615752f3d630e5d40b7aa7360710e86778625f480f5e6'
            'c7bf90025271b82c80a7dd4fb67a5ce67d982aa5b15d95fe825f741460e1d8e5e12c00318b09a2302b4dd31266aa8071e136761784075b721f142fad3855409c'
            '6759926ba7e4c9d7f0cef54298107fe569fe3799d0ae78ac423c504d7f93e4577d3448caf5f3f8bacb3baaa03e8f6c6c77ad8ac06acf4fd36fd73e31a84051d5'
            'a0a9f4d25053fe96f9b1495394adb829252367099931d3f0e9bdfd2371093b4e86ff13fc945301b3a59691bbb7ee14da89e68c4ef3d8e7a1b5ec6bdedccb9137'
            '55929747b9cc1bc8893717c9fef257f8f2d560dc56849f1c74f88ed636b6f9f9997f6c4713768793b115e93482543a330b70eba672d696ea8456741ed2c71237'
            '3c460141b277df61c4546cc311fa5ecc7e7ea19a7d39a92d1d0214c37a91b4e163bc91823df7098bd2cf6fb430361cdb9839ab96abe53fe82f2a735e187de563')

prepare() {
    cd "${srcdir}"/Cinnamon-${pkgver}

    # GNOME Terminal desktop file was renamed in GNOME 3.20
    patch -p1 < ../2dc04b8a26fa7a672c579ed62a70dca6c6e4b295.patch
    # https://github.com/linuxmint/Cinnamon/issues/7497 Work with muffin 3.8
    patch -p1 < ../5c9848a48da3387825f627117ad58521474e0b2e.patch

    # Backport https://github.com/linuxmint/Cinnamon/pull/7327
    patch -p1 < ../0001-cs_user.py-Add-compatibility-for-multiple-pam-module.patch

    # Check for the cc-panel module path, not for the irrelevant binary
    patch -p1 < ../0001-cinanmon-settings-don-t-rely-on-the-presence-of-cinn.patch

    # Python2 fix
    sed -i 's:/usr/bin/python :/usr/bin/python2 :' files/usr/share/cinnamon/cinnamon-settings/bin/TreeListWidgets.py

    # Use wheel group instread of sudo (taken from Fedora)
    patch -Np1 -i ../set_wheel.patch

    # Set default theme to 'cinnamon'
    patch -Np1 -i ../default-theme.patch

    # Replace MintInstall with GNOME Software
    sed -i 's/mintinstall.desktop/org.gnome.Software.desktop/' data/org.cinnamon.gschema.xml.in

    # Add polkit agent to required components
    sed -i 's/RequiredComponents=\(.*\)$/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
        files/usr/share/cinnamon-session/sessions/cinnamon*.session

    # https://github.com/linuxmint/Cinnamon/issues/3575#issuecomment-374887122
    # Cinnamon has no upstream backgrounds, use GNOME backgrounds instead
    sed -i 's|/usr/share/cinnamon-background-properties|/usr/share/gnome-background-properties|' \
        files/usr/share/cinnamon/cinnamon-settings/modules/cs_backgrounds.py

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/Cinnamon-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/cinnamon \
                --localstatedir=/var \
                --disable-static \
                --disable-schemas-compile \
                --enable-compile-warnings=yes

    # https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/Cinnamon-${pkgver}

    make DESTDIR="${pkgdir}" install
}
