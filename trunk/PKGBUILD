# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.10
pkgrel=5
pkgdesc='Lightweight X11 tiled window manager written in Haskell'
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.6.1-1' 'gmp' 'haskell-x11=1.6.0.2' 'sh' 'haskell-mtl=2.1.2' 'haskell-utf8-string=0.3.7' 'haskell-extensible-exceptions')
makedepends=('setconf' 'gendesk')
optdepends=('xorg-xmessage: for displaying visual error messages')
install='xmonad.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
	xmonad.svg xmonad-0.10-x11-1.6.patch)

build() {
  cd "$srcdir"
  ghc-pkg list
  gendesk

  cd $srcdir/$pkgname-$pkgver
  # Patch XMonad so we support haskell 1.6
  patch -Np1 -i $srcdir/xmonad-0.10-x11-1.6.patch


  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  #runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
}
md5sums=('f8381e1ec15137863558a454d4466467'
         '72bfa5e62e4e44fe7fa59b6a7593d993'
         '22d29900d71f09a8f9fa6f0d2f3fed32')
