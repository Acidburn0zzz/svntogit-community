# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cantonese
pkgver=0.0.0.20200525
_commit=d3ecbe8e752c696e1dc0d64c610faaa8f855ce89
pkgrel=1
pkgdesc="Cantonese input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cantonese"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-cangjie' 'rime-emoji' 'rime-luna-pinyin' 'rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cantonese/archive/$_commit/rime-cantonese-$_commit.tar.gz")
sha512sums=('f692643873b09d2462a5424e954f1ab7e329df3fa7706371c109f54edc1ff735883b61afae5cc6aa1f0b581e8278d19681d83e095a40f6317b9ca5a3bff4d77a')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
