# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=browserify
pkgdesc='browser-side require() the node way'
pkgver=14.5.0
pkgrel=1
arch=('any')
url='http://browserify.org/'
license=('MIT' 'BSD')
depends=('nodejs' 'acorn')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('80a7ceb0d42ffeda16cfeeb49d23df633bb0484773bd5d96771ad53d46cf0ffaae8b8e2b024077b779ae36d9a61981eaac6e7a146c17f52504426ccd2c0792ee')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
