# Maintainer: Felix Yan <felixonmars@archlinux.org
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Pablo Arias <pabloariasal@gmail.com>
# Contributor: John Jenkins <twodopeshaggy@gmail.com>

pkgname=nnn
pkgver=2.7
pkgrel=2
pkgdesc="The fastest terminal file manager ever written."
arch=('x86_64')
depends=('bash')
optdepends=(
  'atool: for more archive formats'
  'libarchive: for more archive formats'
  'zip: for zip archive format'
  'unzip: for zip archive format'
  'trash-cli: to trash files'
  'sshfs: mount remotes'
  'rclone: mount remotes'
  'fuse2: unmount remotes'
  'xdg-utils: desktop opener'
)
url="https://github.com/jarun/${pkgname}"
license=('BSD')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jarun/nnn/archive/v${pkgver}.tar.gz")
sha256sums=('0592c7cbcf2cf66cacac49e9204636480820b1bc74e4187dd7ee06945a6d07c5')

prepare() {
  sed -i 's/install: all/install:/' "${pkgname}-${pkgver}/Makefile"
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" PREFIX=/usr install

  install -Dm644 misc/auto-completion/fish/nnn.fish "${pkgdir}/usr/share/fish/vendor_completions.d/nnn.fish"
  install -Dm644 misc/auto-completion/bash/nnn-completion.bash "${pkgdir}/usr/share/bash-completion/completions/nnn"
  install -Dm644 misc/auto-completion/zsh/_nnn "${pkgdir}/usr/share/zsh/site-functions/_nnn"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
