# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor : Drew DeVault <sir@cmpwn.com>

pkgname=samurai
pkgver=1.0
pkgrel=1
pkgdesc="ninja-compatible build tool written in C"
arch=('x86_64')
url="https://github.com/michaelforney/${pkgname}"
license=('ISC' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('55c73da66b5b8af8b6e26e74b55b3a9f06b763547fe6d0a6206dae68274a1438')
b2sums=('8a16590d4e93e1b399e545e80087ff38705ada24f144d9a6c8526e4e27ab062bc2b1785da108f06721a50bda069c2df9ecb54447a23b4df3b586ff28360f66db')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make install PREFIX=/usr DESTDIR="${pkgdir}"
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
