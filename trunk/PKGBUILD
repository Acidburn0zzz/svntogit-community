# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=primus_vk
pkgver=1.3
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/primus_vk"
license=('BSD')
depends=('nvidia-utils' 'bumblebee' 'vulkan-icd-loader' 'primus')
makedepends=('git' 'vulkan-validation-layers')
source=("${pkgname}-${pkgver}::git+${url}.git#tag=v${pkgver}")
sha512sums=('SKIP')

_backports=(
)

_reverts=(
)

prepare() {
  cd ${pkgname}-${pkgver}

  local _c
  
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"  
    git cherry-pick -n "${_c}"
  done

  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  cd ${pkgname}-${pkgver}

  export CXXFLAGS+=' -DNV_DRIVER_PATH=\"/usr/lib/libGLX_nvidia.so.0\"'
  make all primus_vk_diag
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}"/usr install
  install -Dm755 primus_vk_diag -t "${pkgdir}"/usr/bin/
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
