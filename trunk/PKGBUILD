# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.4.3
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'qt5-tools')
makedepends=('cmake')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('7eaac2611b7800696ff0de673f5bb5e5c922a4827aa6caac3f45bf79f16a3e868e1cf8aedb653de06282ccc2b4481cd35a2c15feea773ec5635847a6afb61cbe')

prepare() {
  cd dde-launcher-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=1
  make
}

package() {
  cd dde-launcher-$pkgver
  make DESTDIR="$pkgdir" install
}
