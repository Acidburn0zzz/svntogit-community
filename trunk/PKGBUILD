# $Id$
# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=1.8
pkgrel=1
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('i686' 'x86_64')
url="http://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoc' 'source-highlight' 'cmake' 'pkg-config' 'perl' 'python2'
             'lua' 'tcl' 'ruby' 'aspell' 'guile2.0' 'asciidoctor')
optdepends=('perl' 'python2' 'lua' 'tcl' 'ruby' 'aspell' 'guile2.0')
source=("${pkgname}-${pkgver}.tar.xz::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz.asc")
sha512sums=('34d5aa05e7a9df459c7ade7f654f56561838d59c2d553432697213a5d90614f55204364c4711e28aadd3dbae2cea733dfc6cafeffa9fca87a33eef0e2647a18f'
            'SKIP')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8')

prepare() {
  mkdir build
}

build() {
  cd build

  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DPYTHON_EXECUTABLE=/usr/bin/python2 \
        -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON

  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
