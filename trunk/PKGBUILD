# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sure
pkgname=("python-sure" "python2-sure")
pkgver=1.2.25
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="http://github.com/gabrielfalcao/sure"
license=('GPL')
makedepends=('python-six' 'python2-six' 'python-mock' 'python2-mock' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("https://pypi.io/packages/source/s/sure/sure-$pkgver.tar.gz")
sha512sums=('d1cdd7a3165ae0cb8ec30ca0cb636057d589638554e1d9aeecafed7149dd7ab1f220428028becb1507c54b888c831ae590e4781fb540092144abcf53a41be635')

prepare() {
  cp -a "sure-$pkgver"{,-py2}
}

package_python-sure() {
  depends=('python-six' 'python-mock')

  cd sure-$pkgver
  LC_CTYPE=en_US.UTF-8 python3 setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-sure() {
  depends=('python2-six' 'python2-mock')

  cd sure-$pkgver-py2
  LC_CTYPE=en_US.UTF-8 python2 setup.py install --root="${pkgdir}" --optimize=1
}

