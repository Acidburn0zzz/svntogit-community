# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=2.0.1
pkgrel=1
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
provides=('ukui-indicators')
replaces=('ukui-indicators')
depends=('gsettings-qt' 'kwindowsystem' 'libqtxdg' 'peony' 'qt5-webkit' 'ukui-biometric-auth'
         'ukui-menu' 'ukui-menus')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('8f5d6fde95a00ac8cebba2715a97d91123a65eb61633188a9edecf0476b6f5b9f626de935decdf77f9dfe5fc3fc8ec79793c3c7c5aaa187ca904206135378f53')

prepare() {
  mkdir -p build
  sed -i 's|../../panel/common/ukuitranslator.h|../panel/common/ukuitranslator.h|' $pkgname-$pkgver/cmake/ukui-build-tools/modules/*.cpp.in
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
