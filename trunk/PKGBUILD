# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=2.2.1
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=('python-twisted' 'python-cryptography' 'python-cssselect' 'python-lxml' 'libxml2'
         'python-parsel' 'python-pydispatcher' 'python-pyopenssl' 'python-queuelib'
         'python-service-identity' 'python-setuptools' 'python-w3lib' 'python-zope-interface'
         'python-protego' 'python-itemadapter')
checkdepends=('python-pytest-runner' 'python-pytest-twisted' 'python-botocore' 'bpython'
              'python-brotlipy' 'python-jmespath' 'ipython' 'mitmproxy' 'python-pillow'
              'python-sybil' 'python-testfixtures')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz")
sha512sums=('4bc7ed321f6b33f0cf9f2344d59bf0ed7724abc79104e0d06c0d7d198aa5a7be6b50a5e5d1a1dcf62c6ce4ab35190ee1edf3ee0c0959180d2233a8ff0567763b')

build() {
  cd scrapy-$pkgver
  python setup.py build
}

check() {
  cd scrapy-$pkgver
  python setup.py pytest --addopts tests
}

package() {
  cd scrapy-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
