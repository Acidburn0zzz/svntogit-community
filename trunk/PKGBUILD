# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>

pkgname=perl-module-install
pkgver=1.18
pkgrel=3
pkgdesc="Module::Install - Standalone, extensible Perl module installer"
arch=('any')
url='http://search.cpan.org/dist/Module-Install'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-file-remove' 'perl-json' 'perl-module-build' 'perl-module-scandeps' 'perl-par-dist'
         'perl-cpan-meta' 'perl-yaml-tiny')
source=("http://search.cpan.org/CPAN/authors/id/E/ET/ETHER/Module-Install-$pkgver.tar.gz")
sha512sums=('2126d35c9bab151548551629e6a470f9a7b90fd901f32cdc15e9aa83b931178f99e28e2de32ce7b8a677d187474efc428839d7e7254074f26fcfcfe18fa297a9')

build() {
  cd Module-Install-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-Install-$pkgver
  make test
}

package() {
  cd Module-Install-$pkgver
  make install DESTDIR="$pkgdir"
}
