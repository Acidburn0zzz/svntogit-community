# $Id: PKGBUILD 145358 2015-10-31 12:11:57Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-mutter
pkgver=3.20.25
pkgrel=1
pkgdesc="Base window manager for deepin, fork of gnome mutter"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-mutter"
license=('GPL3')
depends=('deepin-clutter' 'dconf' 'gobject-introspection-runtime' 'deepin-desktop-schemas'
         'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'upower'
         'libxkbcommon-x11' 'libgudev' 'deepin-cogl')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'intltool')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-mutter/archive/$pkgver.tar.gz")
sha512sums=('79bc86e9d814bc5d69477eb7abc4df928fea7896ea3b28bab80cf3ddaa7c245959a490ee1d5c9f58bbf195ad18f2d464dff2d84eb6ba92b39600547012d00824')

build() {
  cd deepin-mutter-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/deepin-mutter \
    --localstatedir=/var \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-native-backend \
    --disable-static \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd deepin-mutter-$pkgver
  make DESTDIR="$pkgdir" install
}
