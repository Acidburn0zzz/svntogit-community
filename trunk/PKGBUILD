# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Bjørn Lindeijer <bjorn lindeijer nl>

pkgname=dumb
pkgver=1.0
pkgrel=1
pkgdesc='IT, XM, S3M and MOD player library'
arch=('x86_64' 'i686')
license=('custom:dumb' 'GPL')
url='http://dumb.sourceforge.net/'
makedepends=('cmake')
depends=('glibc')
source=($pkgname-$pkgver.tar.gz::"https://github.com/kode54/dumb/archive/$pkgver.tar.gz")
sha256sums=('09ea62fd01f6e71cb28a8a8c1c1e356e7eb38f5e003b684e12eb1944c6d35fa5')

build() {
  cd $pkgname-$pkgver

  mkdir -p build
  cd build
  cmake ../dumb/cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
  install -Dm644 ../dumb/licence.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
