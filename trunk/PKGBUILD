# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=2.8.0
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-six' 'python-numpydoc' 'python-scipy' 'python-sqlitedict' 'python-pycodestyle'
         'python-pydocstyle' 'python-testflo' 'python-parameterized' 'python-pyparsing'
         'python-networkx' 'python-sphinx' 'python-redbaron' 'python-mock' 'python-requests-mock'
         'python-tornado' 'python-pydoe2')
checkdepends=('python-mpi4py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver.tar.gz")
sha512sums=('88b11f9884778d4fb60087fb2897e4a2613f9d6daa8c7a361a13e60898db45ca6c535abb7329ba8e7fe4fa21508e5b09c3ccd7d8e2aa062bf03ef44d11c8983d')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/from distutils.core/from setuptools/' \
      -e 's/==/>=/' \
      -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  PYTHONPATH="$PWD/build/lib" testflo -n 16 --timeout=120 openmdao || warning "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
