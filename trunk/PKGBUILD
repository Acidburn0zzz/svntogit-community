# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>
_pkgbasename=atk
pkgname=lib32-$_pkgbasename
pkgver=1.30.0
pkgrel=3
pkgdesc="A library providing a set of interfaces for accessibility (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2' $_pkgbasename)
makedepends=('gcc-multilib')
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/1.30/${_pkgbasename}-${pkgver}.tar.bz2)
url='http://www.gtk.org/'
sha256sums=('92b9b1213cafc68fe9c3806273b968c26423237d7b1f631dd83dc5270b8c268c')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
