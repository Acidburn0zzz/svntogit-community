# $Id$
# Maintainer : Felix Yan <felixonmars@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

_name=pyquery
pkgbase=python-pyquery
pkgname=('python-pyquery' 'python2-pyquery')
pkgver=1.2.11
pkgrel=2
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-lxml' 'python2-lxml' 'python-cssselect' 'python2-cssselect')
checkdepends=('python-requests' 'python2-requests' 'python-webob' 'python2-webob' 'python-nose' 'python2-nose'
              'python-coverage' 'python2-coverage' 'python-webtest' 'python2-webtest' 'python2-beautifulsoup4'
              'python2-restkit')
source=("http://pypi.python.org/packages/source/p/$_name/$_name-$pkgver.tar.gz"
        "https://bitbucket.org/olauzanne/pyquery/raw/b70710332032/LICENSE.txt")
sha512sums=('56ee20e0aefdd290069151d5085a876d6502a226ced72bd43e3e88572f31eecee7abecf4bdd38a1a17685021328963e9fe45eaaac121dc880bcb6ba74b8df12d'
            '4932f86cbe9ed4c2e658acf9c228fdb0b2430fd622d559555196c7db0f43ea435483c7ae9916c325b40130334f4706d30e5adea6bc1d097b3283a7fc64926685')

prepare() {
  cp -a "$_name-$pkgver"{,-python2}
}

build() {
  cd $_name-$pkgver
  python setup.py build

  cd ../$_name-$pkgver-python2
  python2 setup.py build
}

check() {
  cd $_name-$pkgver
  nosetests3

  cd ../$_name-$pkgver-python2
  nosetests2
}

package_python-pyquery() {
  depends=('python-lxml' 'python-cssselect')
  optdepends=('python-webob: query wsgi app')

  cd $_name-$pkgver
  python3 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python-pyquery/LICENSE"
}

package_python2-pyquery() {
  depends=('python2-lxml' 'python2-cssselect')
  optdepends=('python2-webob: to query wsgi app'
              'python2-restkit: to get url directly with a HostProxy app')

  cd $_name-$pkgver-python2
  python2 setup.py install -O1 --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python2-pyquery/LICENSE"
}
