# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>

pkgname=sdl
pkgver=1.2.15
pkgrel=14
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard"
arch=('x86_64')
url="https://www.libsdl.org"
license=('LGPL')
depends=('glibc' 'libxext' 'libxrender' 'libx11')
makedepends=('alsa-lib' 'mesa' 'libpulse' 'glu' 'git')
optdepends=('alsa-lib: ALSA audio driver'
            'libpulse: PulseAudio audio driver')
options=('staticlibs')
_commit=457d4e55ffe1b6ad4c4fa4559dbda8360bf8253d  # tags/release-1.2.15
source=("git+https://github.com/libsdl-org/SDL-1.2#commit=$_commit"
        0001-SDL-1.2.10-GrabNotViewable.patch
        0002-SDL-1.2.15-SDL_EnableUNICODE_drops_keyboard_events.patch
        0003-SDL-1.2.15-const_XData32.patch
        0004-SDL-1.2.15-ignore_insane_joystick_axis.patch
        0005-SDL-1.2.15-no-default-backing-store.patch
        0006-x11-Bypass-SetGammaRamp-when-changing-gamma.patch
        0007-sdl-1.2.14-fix-mouse-clicking.patch
        0008-sdl-1.2.14-disable-mmx.patch
        0009-libsdl-1.2.15-resizing.patch
        0010-X11_KeyToUnicode.patch
        0011-CVE-2019-7577-Fix-a-buffer-overread-in-MS_ADPCM_deco.patch
        0012-CVE-2019-7575-Fix-a-buffer-overwrite-in-MS_ADPCM_dec.patch
        0013-CVE-2019-7574-Fix-a-buffer-overread-in-IMA_ADPCM_dec.patch
        0014-CVE-2019-7572-Fix-a-buffer-overread-in-IMA_ADPCM_nib.patch
        0015-CVE-2019-7572-Fix-a-buffer-overwrite-in-IMA_ADPCM_de.patch
        0016-CVE-2019-7573-CVE-2019-7576-Fix-buffer-overreads-in-.patch
        0017-CVE-2019-7578-Fix-a-buffer-overread-in-InitIMA_ADPCM.patch
        0018-CVE-2019-7638-CVE-2019-7636-Refuse-loading-BMP-image.patch
        0019-CVE-2019-7637-Fix-in-integer-overflow-in-SDL_Calcula.patch
        0020-CVE-2019-7635-Reject-BMP-images-with-pixel-colors-ou.patch
        0021-SDL-1.2.15-CVE-2019-13616-validate_image_size_when_l.patch
        0022-CVE-2019-7577-Fix-a-buffer-overread-in-MS_ADPCM_nibb.patch
        0023-Reject-2-3-5-6-7-bpp-BMP-images.patch)
sha256sums=('SKIP'
            'f53a87ecd52913be7b1e2699d9d609725f6f857e98a68f55ed249e371673e7be'
            '1e76d0c064a50b67f8f4406b995fd2f583be9959f1242fc01449be953e53f18d'
            '42f53733e919b91e4f9b299732b5ef8a68cc23ee5c0cdd77c70f904461fb54a2'
            '35c07231ff40b11f07cb022bd86b5dc31f322d66ba51c518b068890ab099eb27'
            '5dbe8cbf81a021a01e2ec587330fd322ed4fdcdbf11f457bdda69fe56620d85a'
            '0a5d00c1f75158b1838706a0733a51c8c1014983528bafb4a477327f7595e135'
            '06761b7eb56d199477c25502d53504878580f15c30bd56726d46d8337ffad35a'
            'a99bb91981e091bb01bbedf5138008ef16155c9a6b24349a771555c9bb57fdcd'
            'd3be01d55f5d5dd7e9993ce1490831b0b2eea83833d0164a7242f59290514bb7'
            '22f723f3db3de9e02727f0976c3a402e1a7d3656b6bba1e1909bbfc7a24f35c2'
            '06fe75c9b15f3132019822f4670d93263fa2a0399b5f09afb4e11e2a7e0d30cb'
            'df0cac62bc99385cc595602368baa31c7a5616dbddc749ab8bebbc8e190ea52e'
            '8ed73a71eee6c73e4839fe8030159a34d90f41b2b5d568beb8befb23226787ba'
            '547fa0c3cdeae373c664d17ac1501392d6bdc2982edcebb4934cd7a73a2f8e53'
            'db325da88544fa6841dfec2d53fe629100786ba0fadac3b34538859d6ab82dbd'
            '9b020883d15ab65b7645c1c0f87af68cb6b9b85ee4450dbcd94f7b9049d976c7'
            '4c30311656c32c1a0655e22457a214d9900efe64a9241da835dc25488a355914'
            'c2c92fc40c55fcf628f943eb9509199f8c3968f6be13bf71ab1e12524c16cabf'
            'de2fce5f6803a08a2c55f3ce2ddf92394ad7366f5eff3cead4d7ec0035cee254'
            'd14dc355da691c41bf938c04e0111063a755719f41bbd047f4dbfaa8f2ff25bb'
            'd1f8f699d5e7ee933875380951fe0a756c1ad327fc376dd60511bd462e0781b0'
            'e4159c35b6814cfe2df23811179670ecd58f0276ebb23fa8251dcf3611cd9f2f'
            '3439ba01895df428ad0fd00e49a131db35e6841ca41f5d30c455f583ec18075e')
validpgpkeys=('1528635D8053A57F77D1E08630A59377A7763BE6') # Sam Lantinga <slouken@libsdl.org>

pkgver() {
  cd SDL-1.2
  git describe --tags | sed 's/^release-//;s/-/+/g'
}

prepare() {
  cd SDL-1.2

  git apply -3 ../0001-SDL-1.2.10-GrabNotViewable.patch
  git apply -3 ../0002-SDL-1.2.15-SDL_EnableUNICODE_drops_keyboard_events.patch
  git apply -3 ../0003-SDL-1.2.15-const_XData32.patch
  git apply -3 ../0004-SDL-1.2.15-ignore_insane_joystick_axis.patch
  git apply -3 ../0005-SDL-1.2.15-no-default-backing-store.patch

  # https://bugs.freedesktop.org/show_bug.cgi?id=27222
  git apply -3 ../0006-x11-Bypass-SetGammaRamp-when-changing-gamma.patch

  git apply -3 ../0007-sdl-1.2.14-fix-mouse-clicking.patch
  git apply -3 ../0008-sdl-1.2.14-disable-mmx.patch
  git apply -3 ../0009-libsdl-1.2.15-resizing.patch
  git apply -3 ../0010-X11_KeyToUnicode.patch

  # bunch of CVE fixes from Fedora - Thanks!
  git apply -3 ../0011-CVE-2019-7577-Fix-a-buffer-overread-in-MS_ADPCM_deco.patch
  git apply -3 ../0012-CVE-2019-7575-Fix-a-buffer-overwrite-in-MS_ADPCM_dec.patch
  git apply -3 ../0013-CVE-2019-7574-Fix-a-buffer-overread-in-IMA_ADPCM_dec.patch
  git apply -3 ../0014-CVE-2019-7572-Fix-a-buffer-overread-in-IMA_ADPCM_nib.patch
  git apply -3 ../0015-CVE-2019-7572-Fix-a-buffer-overwrite-in-IMA_ADPCM_de.patch
  git apply -3 ../0016-CVE-2019-7573-CVE-2019-7576-Fix-buffer-overreads-in-.patch
  git apply -3 ../0017-CVE-2019-7578-Fix-a-buffer-overread-in-InitIMA_ADPCM.patch
  git apply -3 ../0018-CVE-2019-7638-CVE-2019-7636-Refuse-loading-BMP-image.patch
  git apply -3 ../0019-CVE-2019-7637-Fix-in-integer-overflow-in-SDL_Calcula.patch
  git apply -3 ../0020-CVE-2019-7635-Reject-BMP-images-with-pixel-colors-ou.patch
  git apply -3 ../0021-SDL-1.2.15-CVE-2019-13616-validate_image_size_when_l.patch
  git apply -3 ../0022-CVE-2019-7577-Fix-a-buffer-overread-in-MS_ADPCM_nibb.patch
  git apply -3 ../0023-Reject-2-3-5-6-7-bpp-BMP-images.patch

  ./autogen.sh
}

build() {
  cd SDL-1.2
  ./configure --prefix=/usr --disable-nasm --enable-alsa \
              --with-x --disable-rpath --disable-static
  make
}

package() {
  cd SDL-1.2
  make DESTDIR="$pkgdir" install
}
