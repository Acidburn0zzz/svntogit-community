# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>
# Contributor: Martin Brodbeck <martin@brodbeck-online.de>

pkgname=qmapshack
pkgver=1.15.0
pkgrel=1
pkgdesc='Plan your next outdoor trip'
arch=('x86_64')
url='https://github.com/Maproom/qmapshack/wiki'
license=('GPL3')
depends=('desktop-file-utils' 'gdal' 'hicolor-icon-theme' 'proj' 'qt5-webengine'
         'quazip' 'routino>=3.1' 'alglib' 'qt5-tools')
makedepends=('cmake' 'patch')
source=($pkgname-$pkgver.tar.gz::https://github.com/Maproom/$pkgname/archive/V_$pkgver.tar.gz
        FindPROJ4.patch
        FindQuaZip5.patch
        qmapshack-qt5.15-compile.patch)
sha256sums=('a3a470657a66a21a59e812dd6f9351845ce3300c00c25622dcb4a3818ed884f2'
            '1ffb847b098fedf01e347971da33c58b3fbf1a0f27205d48f6eb52206e228046'
            '23f32e58daa7c69072ea59465f0541e86dcff3085e4e73a948713a02ae421889'
            'd4ecc3a27b35308511ddbe56d71a4d63289310e5f58f5152fba156259b08fdd9')

prepare() {
  cd "$pkgname-V_$pkgver"
  patch -Np1 -i "${srcdir}"/FindPROJ4.patch
  patch -Np1 -i "${srcdir}"/FindQuaZip5.patch
  patch -Np1 -i "${srcdir}"/qmapshack-qt5.15-compile.patch
}

build() {
  cd "$pkgname-V_$pkgver"

  cmake ./ -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$pkgname-V_$pkgver"

  make DESTDIR="${pkgdir}" install
}
