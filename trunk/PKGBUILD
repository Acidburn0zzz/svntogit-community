# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.24.2
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-yaml python-six python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools)
checkdepends=(python-pytest python-mock)
source=("https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver/cfn-python-lint-$pkgver.tar.gz")
sha256sums=('3a848146543a120b44951657f7eeef9168be1271a963645de7947e3bc50f90b8')

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  PYTHONPATH="$PWD/build/lib" pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
