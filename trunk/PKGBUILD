# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cmd2
pkgver=0.9.4
pkgrel=1
pkgdesc="A tool for building interactive command line apps"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
depends=('python-colorama' 'python-pyperclip' 'python-wcwidth')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'vi')
source=("$pkgname-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('fd4ff48aeed4184797d9741e635dd3da4cc95f6efdddb24933c7619942c46cf5408f02b3ad6765088dd2d293628bc4674011817daeed4f109494c01e3653d58c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd cmd2-$pkgver
  python setup.py build
}

check() {
  cd cmd2-$pkgver
  python setup.py pytest
}

package() {
  cd cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
