# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.2.0rcgit.29
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(x86_64)
url="http://www.remmina.org/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'avahi' 'vte3'
	 'libgcrypt' 'libxdmcp' 'libsecret' 'webkit2gtk')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib'
             'libvncserver' 'spice-protocol' 'spice-gtk3')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'spice-gtk3'
            'xorg-server-xephyr: XDMCP support'
            'libvncserver: VNC client/server support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/v${pkgver/rc/-rc}.tar.gz"
        "https://github.com/FreeRDP/Remmina/commit/5d00c7920a4b8c7d781160b7188747b422f130a3.patch")
sha256sums=('24fdcc8b69f8563c464b6ac77dd6bf93a1c963d90f8a114e1a3abad9af2b8682'
            '7df065f2243911c2f47e1bf91498c9494d19f1d3f154b3402b99d2572383126f')

prepare() {
  cd "$srcdir"/Remmina-${pkgver/rc/-rc}/
#  patch -p1 < "$srcdir"/5d00c7920a4b8c7d781160b7188747b422f130a3.patch
}

build() {
  cd "$srcdir"/Remmina-${pkgver/rc/-rc}/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd "$srcdir"/Remmina-${pkgver/rc/-rc}/
  make DESTDIR="$pkgdir" install
}
