# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20200201
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txthinking/brook/archive/v$pkgver.tar.gz")
sha512sums=('2a6589a9a67f5b7ac43fda2524f7fa4de05d7a0c353bd07ec39945a638c57b2287359d5bcdf4a97f56652d3a666e75904cdc272d3abb7b63c66ddb052b0e87a6')

prepare() {
  cd brook-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/txthinking
  ln -sf "$PWD" .gopath/src/github.com/txthinking/brook
  export GOPATH="$PWD/.gopath"

  go get github.com/mdp/qrterminal github.com/patrickmn/go-cache github.com/txthinking/ant \
         github.com/txthinking/socks5 golang.org/x/net/proxy github.com/gorilla/mux \
         github.com/urfave/cli github.com/txthinking/gotun2socks github.com/gorilla/websocket \
         github.com/urfave/negroni
}

build() {
  cd brook-$pkgver/cli/brook
  go build -ldflags "-extldflags ${LDFLAGS}" -o brook
}

package() {
  cd brook-$pkgver/cli/brook
  install -Dm755 brook "$pkgdir"/usr/bin/brook
}
