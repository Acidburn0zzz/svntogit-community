# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-api
pkgver=3.1.23
pkgrel=1
pkgdesc='Go-lang bindings for dde-daemon'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-api"
license=('GPL3')
depends=('glib2' 'gdk-pixbuf2' 'gtk3' 'libcanberra-pulse' 'libxi' 'libxfixes' 'rfkill'
         'poppler-glib' 'xcur2png' 'blur-effect')
makedepends=('git' 'deepin-gettext-tools' 'deepin-dbus-factory' 'deepin-gir-generator'
             'deepin-go-lib' 'bzr' 'go-pie')
conflicts=('dde-api')
replaces=('dde-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-api/archive/$pkgver.tar.gz")
sha512sums=('edf30f9b08010767e7afe779bb3a821234a8dcc58ecb865c3a9696ffc57a56d6499e8b3507f0850b4aeb9b1d9d72df92c6fa22e6a3b801943467d48dd42a9b09')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p build/src/pkg.deepin.io/dde/api
  cp -a dde-api-$pkgver/* build/src/pkg.deepin.io/dde/api/

  # https://github.com/niemeyer/gopkg/issues/50
  git config --global http.https://gopkg.in.followRedirects true

  cd "$srcdir"/dde-api-$pkgver
  make build-dep
  go get gopkg.in/alecthomas/kingpin.v2 github.com/nfnt/resize github.com/cryptix/wav
}

build(){
  cd dde-api-$pkgver
  make
}

package() {
  cd dde-api-$pkgver
  make DESTDIR="$pkgdir" SYSTEMD_LIB_DIR=/usr/lib install
}
