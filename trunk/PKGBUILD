# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=1.1
pkgrel=3
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=('GPL2')
depends=('python2' 'numactl')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-1.1.tar.gz"
        'use-the-ftrace-hwlatdetector.patch')
sha512sums=('2698bf131b21e9e632ffb4605d965f0e1158b5b3c332c9b8ca71f34a2eeb72cc5c5a18932aaf8ce9e5d03b8a81330d0e8306b99f321852b1234a80f7bf8fce2c'
            '7ecf0a90cc9b37d3faf6572f33877f2e361c3d14959c8cec21a562fb01e75141e97be236b41076174c3d5353b9820f59e2e02f6d8e40af90f97e70abf4790470')

prepare() {
  cd "${pkgname}-${pkgver}"
  sed -i s/'shell python'/'shell python2'/ Makefile
  sed -i 's/python/python2/' src/hwlatdetect/hwlatdetect.py
}

build() {
  cd "${pkgname}-${pkgver}"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -vDm 644 {MAINTAINERS,README.markdown} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
