# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.3.2
pkgrel=2
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11' 'qt5-declarative' 'qt5-quickcontrols2' 'zstd')
makedepends=('mesa' 'qt5-tools' 'python3')
optdepends=('python3: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('25dedfde6f48b0bd34a500481662c3da8c6f78b023b4d5c63559c4240db22fb664ec057d3d801714cb682a6bd2e870bf072ab688b1d00c017f0ea5af980d5ead')

build() {
  cd $pkgname-$pkgver
  qmake -r INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT="$pkgdir"
}
