# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.31.0
pkgrel=1
pkgdesc='Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs'
url='https://lib.filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'gcc-libs' 'nettle' 'gnutls' 'libnettle.so' 'libhogweed.so')
makedepends=(clang)
checkdepends=('cppunit')
provides=('libfilezilla.so')
source=(https://download.filezilla-project.org/libfilezilla/libfilezilla-${pkgver}.tar.bz2)
sha512sums=('d8be1658c0ece762dc5c87e58f87925579cb2e4b261fea67da407b5d78cfe601a4d1bcb792f00f0f6a41fc7a90a06476a6d2c7a1bf50a85014649116c895e963')
b2sums=('cbc3653bc8fb676793980221bcccab6ae546ff6bbcff133e44657012f29559da03e07387b4bbb543d479d8796fa7c106d662d8a1415942fa72eabee210f2517d')

build() {
  cd ${pkgname}-${pkgver}
  export CXX=clang++
  export CC=clang
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd ${pkgname}-${pkgver}
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
