# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-rfc2136
pkgver=1.5.0
pkgrel=1
pkgdesc="NS1 DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-dnspython' 'python-mock' 'python-setuptools'
         'python-zope-interface')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('64b34d0369f3847ad3c2b1d4a6c6114c8a6557c46a880a27b90808b0fc6afea9071eefcbf98cddfe6ff8795e1a81e682fe0284be1f07aca1656645b338021bcd'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
