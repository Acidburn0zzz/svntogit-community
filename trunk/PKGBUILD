# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pykerberos
pkgname=('python-pykerberos' 'python2-pykerberos')
pkgver=1.1.10
pkgrel=2
pkgdesc="High-level interface to Kerberos"
arch=('i686' 'x86_64')
license=('Apache')
url="https://github.com/02strich/pykerberos"
makedepends=('python-setuptools' 'python2-setuptools' 'krb5' 'git')
source=("git+https://github.com/02strich/pykerberos.git#tag=v$pkgver"
        issue15.patch)
sha512sums=('SKIP'
            'e0f413924a5f2acf6d48e3df860a301fa5d270edffd47e56c1d04ab972357a6d1934ceea3dc91b446ebb2eddcd39eb6ef312e39ec7b566027e180428191d8f9e')

prepare() {
  # https://github.com/02strich/pykerberos/issues/15
  (cd pykerberos; patch -p1 -i ../issue15.patch)

  cp -a pykerberos{,-py2}
}

build() {
  cd "$srcdir"/pykerberos
  python setup.py build

  cd "$srcdir"/pykerberos-py2
  python2 setup.py build
}

package_python-pykerberos() {
  depends=('python' 'krb5')

  cd pykerberos
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pykerberos() {
  depends=('python2' 'krb5')

  cd pykerberos-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
