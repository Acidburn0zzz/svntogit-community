# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-softlayer
pkgver=5.5.3
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz"
        click-7.patch)
sha512sums=('65844c940c7f32366b616cf7e29410bb7a488727d011f21e2e0b6e687f27555164a4c133635d2c65368333d586bfd49d0de9afb07f932ddf0386d27872854b21'
            '54395d5469fd63e5bbfd455f4e9945d4d3fe3dc5b4ff483734ba872485489fd7b883ba09602bd05ec8153d20a0d358e551877a7853670a362fdd694937f3f7ae')

prepare() {
  cd softlayer-python-$pkgver
  patch -p1 -i ../click-7.patch
}

build() {
  cd softlayer-python-$pkgver
  python setup.py build
}

check() {
  cd softlayer-python-$pkgver
  python setup.py pytest --addopts '-k "not test_edit_perms_on_bad"'
}

package() {
  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
