# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-openapi-spec-validator
pkgname=(python-openapi-spec-validator python2-openapi-spec-validator)
pkgver=0.2.4
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'python-yaml'
             'python2-yaml' 'python-jsonschema' 'python2-jsonschema')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('20426d71c29ffcd39fc24410296a62678b66e08a37c4eb6fb6cebbbdb69ae652aa880bb68cdfb8d7bbb7c2b99b73e60d98f7f88dba6c1849e0e846e314829be9')

prepare() {
  cp -a openapi-spec-validator-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  python setup.py build

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  PYTHONPATH="$PWD/build/lib" pytest2
}

package_python-openapi-spec-validator() {
  depends=('python-six' 'python-yaml' 'python-jsonschema')

  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openapi-spec-validator() {
  depends=('python2-six' 'python2-yaml' 'python2-jsonschema')

  cd openapi-spec-validator-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openapi-spec-validator{,2}
}
