# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-configuration
pkgver=4.3.1
pkgrel=3
pkgdesc="Zope Configuration Markup Language (ZCML)"
arch=('any')
url="https://github.com/zopefoundation/zope.configuration"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-schema' 'python-zope-i18nmessageid')
checkdepends=('python-manuel' 'python-zope-testing' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.configuration/archive/$pkgver.tar.gz")
sha512sums=('b67af884d573b017264a19c906d2199a6a466a7df1bc39202f56e02d65eee810d9cbeb2c7f27b73c30ab916bac75ec4eedf6082f4b6f4f0b7514acc6abcaeabd')

build() {
  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.configuration-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"
}

package() {
  cd "$srcdir"/zope.configuration-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
