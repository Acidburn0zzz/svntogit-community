# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=starlette
pkgname=python-$_pkgname
pkgver=0.13.7
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=('any')
url='https://github.com/encode/starlette'
license=('BSD')
depends=('python')
optdepends=('python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
            'python-yaml' 'python-requests' 'python-ujson')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner'
              'python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
              'python-yaml' 'python-requests' 'python-ujson'
              'python-unidecode')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('c4839d7a9320001a5cc7b0cdf6633923fbc4e8103780a6e2b76440e4c117bc07f9594c02bee22c497849fedd7e65258c848250997e2a4da14bf1a0e9774f10f8')

prepare() {
  cd $_pkgname-$pkgver

  # needs python-multipart
  rm tests/test_formparsers.py

  # needs python-databases
  rm tests/test_database.py

  # needs a different graphql version
  rm tests/test_graphql.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_request_form_urlencoded needs python-multipart
  python setup.py pytest --addopts -k-test_request_form_urlencoded
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

