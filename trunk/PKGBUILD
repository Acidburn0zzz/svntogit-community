# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=arch-repro-status
pkgver=1.1.0
pkgrel=1
pkgdesc="Check the reproducibility status of your Arch Linux packages"
arch=('x86_64')
url="https://gitlab.archlinux.org/archlinux/arch-repro-status"
license=('MIT')
depends=('pacman')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('cf1e02e97fcc013a342e7fe1f1f9ac70a975ac58d37d34557c70a8df76e7d2b26c306c7b2b60de662bca7298e126901ad3ba9657b168f5ca53a87d1fe2c560fe')

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}
