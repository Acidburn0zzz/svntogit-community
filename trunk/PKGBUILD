# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-utils
pkgname=(python-oslo-utils python2-oslo-utils)
pkgver=4.0.0
pkgrel=2
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-funcsigs'
             'python-iso8601' 'python2-iso8601' 'python-oslo-i18n' 'python2-oslo-i18n'
             'python2-monotonic' 'python-pytz' 'python2-pytz' 'python-netaddr' 'python2-netaddr'
             'python-netifaces' 'python2-netifaces' 'python-debtcollector' 'python2-debtcollector')
checkdepends=('python-oslotest' 'python-ddt' 'python-eventlet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz"
        oslo-utils-py38.patch)
sha512sums=('c2fd4c36c2ceaca482c8d0c2c12e9bd4e9ec18316bc05e2016d2d877cfe916737b8f47ee91c0b672b27e1813c6782c3b81cafac7d1da41a2d35ff0e7ccc11a56'
            'edc9861dfc02b82d0312774f2484bb5853a9e5b2e7de9ee19efbd7e1220be56e52d99abb15f71dba4d764832835371200e8d7cac2d40c0546d5cfc8daf68554a')

export PBR_VERSION=$pkgver

prepare() {
  patch -d oslo.utils-$pkgver -p1 -i ../oslo-utils-py38.patch
  cp -a oslo.utils-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py build
}

check() {
  cd oslo.utils-$pkgver
  stestr run
}

package_python-oslo-utils() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
           'python-netaddr' 'python-netifaces' 'python-debtcollector')

  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-utils() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-oslo-i18n' 'python2-monotonic'
           'python2-pytz' 'python2-netaddr' 'python2-netifaces' 'python2-debtcollector'
           'python2-funcsigs')

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
