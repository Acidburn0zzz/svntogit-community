# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.5.6
pkgrel=2
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "python2" "git")
_treesitter_tag=0.15.8
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz"
        "git+https://github.com/tree-sitter/node-tree-sitter#tag=v${_treesitter_tag}"
        "0001-node-tree-sitter-nodejs-12-update.patch"
        "bash-language-server-nodejs-12-update.patch")
sha512sums=('d102374fab5b849321ee09bddcc22807b0b81347b2495ccd280bbb9c835f8c6e21199f7827f0ebddaecbabc4b9dec5af1ad69cd491304a9f8013ad3fb6c2b1d5'
            'SKIP'
            'f7720946ad85380e863072b89053498ece7364af48a85c03f32fd3f4e430e406ff5584291c321291910d3714c0ab9b99bbfd0a8ba184028256184f3f3d497dff'
            'd46a755ad4877265aade7313cba0ef3cb49252038e9e7ff6d88a7975bc6333ac176a5456dff1446dfbcf4cc9717cd756ddcc9097a73435735fbfa40323e8b67c')

prepare() {
    cd "$srcdir/node-tree-sitter"
    git submodule update --init --recursive
    git apply ../0001-node-tree-sitter-nodejs-12-update.patch
    yarn pack --filename ../tree-sitter-${_treesitter_tag}.tgz

    cd "$srcdir/$pkgname-server-$pkgver"
    patch -Np1 -i ../bash-language-server-nodejs-12-update.patch
}

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    PYTHON=python2 yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
