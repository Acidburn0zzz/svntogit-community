# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-treq
pkgver=20.4.0
pkgrel=1
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
         'python-idna' 'python-attrs' 'python-twisted')
makedepends=('python-setuptools' 'httpbin')
checkdepends=('python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twisted/treq/archive/release-$pkgver.tar.gz")
sha512sums=('0d15ce79c1b53b6801e445993c4ea3a837998853642b9c829cc200b2223d35983629e666302556dd796e3032161f2d73a1d38408a5e22c7dd6a5f7e9e4b12cbb')

build() {
  cd treq-release-$pkgver
  python setup.py build
}

check() {
  cd treq-release-$pkgver
  PYTHONPATH="$PWD/build/lib" trial3 treq
}

package() {
  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
