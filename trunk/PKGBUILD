# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Jon Bergli Heier <snakebite@jvnv.net>

pkgname=('python2-pyxattr' 'python-pyxattr')
pkgbase=python-pyxattr
_pkgname=pyxattr
pkgver=0.5.1
pkgrel=1
pkgdesc='Python extension module that allows you to manipulate the extended attributes'
arch=('i686' 'x86_64')
url='http://pyxattr.k1024.org/'
license=('GPL')
makedepends=('python2' 'python' 'python2-distribute' 'python-distribute')
source=("https://github.com/downloads/iustin/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
md5sums=('bb9560510b17625015aa45f2fbc3b520')

package_python2-pyxattr() {
  depends=('python2')
  provides=('pyxattr')
  conflicts=('pyxattr')
  replaces=('pyxattr')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}"
}

package_python-pyxattr() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}"
}
