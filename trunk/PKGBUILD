# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libblastrampoline
pkgver=4.0.0
pkgrel=1
pkgdesc='Using PLT trampolines to provide a BLAS and LAPACK demuxing library'
arch=(x86_64)
url='https://github.com/JuliaLinearAlgebra/libblastrampoline'
license=(MIT)
depends=(glibc)
source=(https://github.com/JuliaLinearAlgebra/libblastrampoline/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8816dfba6f0c547bca5fba9d83e63062387def3089622a9514babf79e1737310')

build() {
  cd $pkgname-$pkgver/src
  make F2C_AUTODETECTION=0 CFLAGS="$CFLAGS -fPIC" LDFLAGS="-shared $LDFLAGS"
}

package() {
  cd $pkgname-$pkgver/src
  make F2C_AUTODETECTION=0 prefix="$pkgdir"/usr install
  install -Dm644 ../LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
