# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=piper
pkgver=0.2.904
pkgrel=1
pkgdesc='GTK application to configure gaming mice'
arch=('any')
url='https://github.com/libratbag/piper'
license=('GPL')
depends=('python' 'python-gobject' 'libratbag>=0.9.900' 'python-evdev' 'python-lxml' 'python-cairo')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('425d233a89eebf391650ae121134dd532bf75e0f6e15168c89ab86f3b5bac1adb6dfa985c0028a2285f85655226200b5cfe28f36cfd83814985f97ffa3774412')

prepare() {
  cd $pkgname-$pkgver

  # Remove install script
  # This is handled by pacman hooks
  sed -i "/meson.add_install_script('meson_install.sh')/d" meson.build
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
