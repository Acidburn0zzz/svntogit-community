# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.18
pkgrel=1
pkgdesc='Deepin Window Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'deepin-menu' 'granite' 'deepin-mutter'
         'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz")
sha512sums=('8745f6b33ca011185962965739ae6524d2518346ddef253ead5b5d785a350f46da1008ae08a6ac18e8f1511a3c6dc598411789af1c69b85669f2c6a44682a9ca')

prepare() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' $pkgname-$pkgver/src/Background/BackgroundSource.vala
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
