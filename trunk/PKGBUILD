# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2.19.0d
pkgver=2.19.0.d
_upstreamver=2.19.0
_patchver=2.19.0
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with vaultwarden'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-fermium' 'npm' 'python2')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        'git+https://github.com/bitwarden/jslib.git'
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$_pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        'SKIP'
        'eaa08d24a6493ea04fab5da695590f79c4b6bf3ced13d67ddc8cfc8554a28e1badc7852d6fc150a2c454d3776aaeac74d4685c06c9f44bfdddd5b6fd3aab9819')

prepare() {
  cd bitwarden-web
  git submodule init
  git config submodule.jslib.url ../jslib
  git submodule update
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch
  npm ci
}

build() {
  cd bitwarden-web
  npm run dist
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
