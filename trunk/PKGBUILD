# Maintainer:

pkgname=jxrlib
pkgver=0.2.0
pkgrel=1
pkgdesc="Open source implementation of jpegxr"
arch=(x86_64)
url="https://archive.codeplex.com/?p=jxrlib"
license=(GPL)
depends=(glibc)
makedepends=(swig java-environment python)
source=($pkgname-$pkgver.tar.gz::https://github.com/glencoesoftware/jxrlib/archive/v$pkgver.tar.gz)
md5sums=('cc8ead8dc4238fac11d08310c31fd5b2')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|install: all|install:|' -i Makefile # Don't build again on make install
}

build() {
  cd $pkgname-$pkgver
  make SHARED=1 PREFIX=/usr JAVA_INCLUDE=/usr/lib/jvm/default/include swig all
}

package() {
  cd $pkgname-$pkgver
  make SHARED=1 DIR_INSTALL="$pkgdir/usr" install
  mv "$pkgdir"/usr/include/libjxr/*/{*.h,*/*.h} "$pkgdir"/usr/include/libjxr/
  rm -r "$pkgdir"/usr/include/libjxr/{image,glue,test,common}
}
