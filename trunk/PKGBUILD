# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-configtool
pkgver=0.0.0.20200809
_commit=ecd16e5f5bfeaded9bb59b88f484871d14e016e5
pkgrel=1
pkgdesc="Configuration Tool for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-configtool"
license=('GPL')
groups=('fcitx5-im')
provides=('fcitx5-config-qt' 'kcm-fcitx5')
conflicts=('kcm-fcitx5')
replaces=('kcm-fcitx5')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews' 'qt5-x11extras')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'ninja' 'python')
source=("https://github.com/fcitx/fcitx5-configtool/archive/$_commit/fcitx5-configtool-$_commit.tar.gz")
sha512sums=('fb661789895be1baa438d659d4fa30023a2c0228c94892731f2febd2a8d3ba76ba3e4e0b95f94238d11077a12382ca2bcf2bc3d160f37cc894791c9d0dc45bd5')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
