# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.9.7
pkgrel=2
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'intel-tbb' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses')
makedepends=('boost' 'cmake' 'llvm' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
sha512sums=('ab232e45eaecb9d56938bfbd2a76999c3dc3033c31cdae3bd48f18642f98211b7255aa2a11bfda13a29100f4bfeade0643d34cb5007d23662894a48139b166e5')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  # Add system libraries needed to link against LLVM components (zlib, ncurses)
  # This is required in order to statically link against LLVM
  #patch -Np1 < $srcdir/llvm-static-system-libs.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake ../.. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLLVM_STATIC=0 \
    -DOSL_BUILD_CPP11=1 \
    -DENABLERTTI=ON \
    -DSTOP_ON_WARNING=OFF
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install

  install -Dm644 "$pkgdir"/usr/share/doc/OSL/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
