# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.18.19
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=('x86_64')
url="https://github.com/pymupdf/PyMuPDF"
license=('AGPL3')
depends=('glibc' 'gumbo-parser' 'jbig2dec' 'libmupdf' 'openjpeg2' 'python')
makedepends=('freetype2' 'libjpeg-turbo' 'python-distro' 'python-setuptools' 'swig' 'systemd')
checkdepends=('python-pillow' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('02406ad7ad11734e961bd35bc5f6ec73e7f87ae8ac1a9473faa2cf1be1284df9aca3dde98c35f9da2795a158f162e41ebf14b2b91936c41d98ff127a700d06b1')
b2sums=('a5c50e12b67fcd59a46878f7af3bacc819178f9bddf88fdbd3f370bfb2fd9cfde492b9ef9053d389590e28d8e6b467b0ba1cd950ef5c28d3b73abd8629508b9e')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"
  cd "$pkgname-$pkgver"
  mkdir -vp "${_test_dir}"
  # install to test dir for testing
  python setup.py install --optimize=1 --root="${_test_dir}"

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  pytest -v -k "not test_font1 and not test_pageids and not test_textbox3"
}

package() {
  depends+=('libfreetype.so' 'libjpeg.so')

  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
