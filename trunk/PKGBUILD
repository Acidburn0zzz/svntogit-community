# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cinderclient
pkgver=4.2.0
pkgrel=1
pkgdesc="OpenStack Block Storage API Client Library"
arch=('any')
url="https://docs.openstack.org/developer/python-cinderclient"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-prettytable' 'python-keystoneclient'
         'python-requests' 'python-oslo-utils')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-ddt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-cinderclient/archive/$pkgver.tar.gz")
sha512sums=('7e0a3ffc0294717a8dc12c2c09a4dc3368499ee2b5661e4b773c0cd27f8ab46b3628b1ea789a6474c471ba1e9ff1253eac2bcf110633a595630a257b2f8b55cd')

export PBR_VERSION=$pkgver

prepare() {
  sed -i '/simplejson/d' python-cinderclient-$pkgver/requirements.txt
}

build() {
  cd python-cinderclient-$pkgver
  python setup.py build
}

check() {
  cd python-cinderclient-$pkgver
  stestr run
}

package() {
  cd python-cinderclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
