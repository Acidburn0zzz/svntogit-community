# Maintainer: David Runge <dave@sleepmap.de>

pkgname=mephisto.lv2
pkgver=0.2.0
pkgrel=1
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('glibc' 'faust')
makedepends=('lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('0e1f36036bcab35b69b48c65b2dd53109929c1a65c12ba45b18568f595846d8775910999a49b57c43066a31718a1343c02e640d0fca5781283de086cf1a3712a')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  # lv2lint requires the lv2 plugin to be setup in LV2_PATH
  mkdir -vp "build/test/${pkgname}"
  cp -av "build/"*.{so,ttl} "build/test/${pkgname}"
  export LV2_PATH="${PWD}/build/test"
  ninja -C build test || echo "Known failure using lv2lint"
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
