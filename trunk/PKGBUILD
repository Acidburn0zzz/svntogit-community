# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=4.0.0
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-jinja' 'python-jinja_pluralize' 'python-pluggy'
'python-pygments' 'python-setuptools')
checkdepends=('flake8' 'python-pytest' 'python-mock' 'python-pytest-cov'
'python-pycodestyle' 'python-pyflakes' 'python-pylint' 'python-pydocstyle')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('aae6e3e35015968c5d9e83ad31a8872dde321690de3611c9d7d0ed8bd32699fa46866ce01bf31254757d5009b7fe9bcd892bcc8bcb024580c9e768ee0b443ea8'
            'SKIP')
b2sums=('adfecf10b06f347e0b06aac9975bd599de7a2dcb8f46323aaa7bdb5d1c2d3e2884d6fa1ef044e3923095f3b66c4e1064f5a59db937ecbbe396fb8592a3a65dbe'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # skip failing test relying on pylint output:
  # https://github.com/Bachmann1234/diff_cover/issues/158
  pytest -v -k "not test_added_file_pylint_console"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,NOTICE,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
