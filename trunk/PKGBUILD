# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

_name=gitdb2
pkgdesc="A pure-Python git object database"
pkgname=python-gitdb
pkgver=2.0.5
pkgrel=2
url="https://github.com/gitpython-developers/gitdb"
license=('BSD')
arch=('any')
depends=('git' 'python-smmap')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("${pkgname}-${pkgver}.tar.gz::https://pypi.org/packages/source/g/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.asc::https://pypi.org/packages/source/g/${_name}/${_name}-${pkgver}.tar.gz.asc")
sha512sums=('a1d52529afa10d01ce96fc33ba7e78e36e841fca8041a416a873075cb0b11b8bdc0bcfaf787918962f3c4eb442ae938c9cd280f85cb3ce71e1c916816018ad10'
            'SKIP')
validpgpkeys=('2CF6E0B51AAF73F09B1C21174D1DA68C88710E60') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# # tests fail due to test repo coupling with development environment
# # https://github.com/gitpython-developers/gitdb/issues/51
# check() {
#   cd "${pkgname}-${pkgver}"
#   local TEST_TMPDIR=$(mktemp -d)
#   ( cd "$TEST_TMPDIR" && git init . )
#   export GITDB_TEST_GIT_REPO_BASE="${TEST_TMPDIR}/.git"
#   nosetests -vd
# }

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
