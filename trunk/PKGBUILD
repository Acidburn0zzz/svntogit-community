# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=2.7.5
pkgrel=1
pkgdesc='Default terminal emulation application for Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-terminal"
license=('GPL3')
depends=('libsecret' 'libgee' 'libwnck3' 'expect' 'deepin-shortcut-viewer' 'deepin-menu'
         'deepin-manual')
makedepends=('vala' 'cmake' 'intltool' 'gobject-introspection' 'gperf')
groups=('deepin-extra')
#options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-terminal/archive/$pkgver.tar.gz")
sha512sums=('67d7599659e6cd3ac732c6603824f313b6fb7d1b6ee6e4341f3c067067c33bbea430afe6940b7ed3e4ea0074e3b14c39ec71b2c2650dde0b1e680215ad51793f')

prepare() {
  mkdir -p build

  sed -i 's|return __FILE__;|return "/usr/share/deepin-terminal/project_path.c";|' deepin-terminal-$pkgver/project_path.c
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../deepin-terminal-$pkgver
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
