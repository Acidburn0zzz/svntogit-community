# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

_pkgname=primus_vk
pkgname=lib32-primus_vk
pkgver=1.0
pkgrel=1
pkgdesc="Primus-Vk Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/$_pkgname"
license=('BSD')
depends=('lib32-nvidia-utils' 'bumblebee' 'lib32-vulkan-icd-loader' 'lib32-primus')
makedepends=('vulkan-validation-layers')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/felixdoerre/${_pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('5d4bb4be4e958f039144d90c33dc94dcdfcbaf0925f1f70676ae46ea3022d4a11ba07fa23974b434188fad0c54f4df1c590d20b76186634a852a99203e3a76c1')

build() {
  cd "${_pkgname}-${pkgver}"

  export CXXFLAGS+=' -m32 -DNV_DRIVER_PATH=\"/usr/lib32/libGLX_nvidia.so.0\"'
  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -D libnv_vulkan_wrapper.so "$pkgdir"/usr/lib32/libnv_vulkan_wrapper.so
  install -D libprimus_vk.so "$pkgdir"/usr/lib32/libprimus_vk.so
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
