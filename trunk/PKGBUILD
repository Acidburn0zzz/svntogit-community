# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=e-antic
pkgver=0.1.6
pkgrel=1
pkgdesc="Embedded algebraic number fields (on top of antic)"
arch=(x86_64)
url="https://github.com/videlec/e-antic"
license=(LGPL)
depends=(arb)
makedepends=()
source=(https://www.labri.fr/perso/vdelecro/e-antic/$pkgname-$pkgver.tar.gz)
sha256sums=('5f4df48a06074b44c8b4d9f3a4bb3cc07950ed3c4d57c78b22a6aa3eedfc93f5')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
