# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Johannes Maibaum <jmaibaum at gmail dot com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>
_name=setBfree
pkgname=setbfree
pkgver=0.8.7
pkgrel=3
pkgdesc="DSP tonewheel organ"
arch=('x86_64')
url="http://setbfree.org"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('ftgl' 'jack' 'liblo' 'pango')
makedepends=('gendesk' 'lv2' 'ttf-dejavu')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pantherb/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('f4da1391fd84439b7affc4862f1d3ccf6b3df6b17d1de3bed9c657af43e56a3b7dedcac27f668a3e4f6ba1bafc1b98f6023199c14187ea432ee86666f2fe7b5a')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  export FONTFILE=/usr/share/fonts/TTF/DejaVuSans-Bold.ttf
  # XDG desktop files
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name setBfree \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname x42-whirl \
          --pkgdesc "Leslie speaker emulation as JACK client" \
          --name x42-whirl \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # xdg desktop files
  install -vDm 644 ../{${pkgname},x42-whirl}.desktop \
    -t "${pkgdir}/usr/share/applications"
  # icons
  install -vDm 644 doc/{${_name},x42-whirl}.png \
    -t "${pkgdir}/usr/share/pixmaps/"
  # man
  install -vDm 644 doc/*.1 -t "${pkgdir}/usr/share/man/man1/"
  # docs
  install -vDm 644 {AUTHORS,ChangeLog,README.md} doc/*.png \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
