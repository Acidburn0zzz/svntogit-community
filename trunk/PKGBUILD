# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: nfnty <arch@nfnty.se>

pkgname=shotcut
pkgdesc='Cross-platform Qt based Video Editor'
pkgver=21.03.21
pkgrel=1
arch=('x86_64')
url='https://www.shotcut.org'
license=('GPL3')
depends=('qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-multimedia'
         'qt5-quickcontrols2' 'qt5-webkit' 'qt5-websockets' 'qt5-x11extras'
         'mlt' 'movit' 'ffmpeg' 'libx264' 'libvpx' 'lame' 'frei0r-plugins' 'ladspa')
optdepends=('swh-plugins: Several audio filters')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mltframework/shotcut/archive/v$pkgver.tar.gz")
sha512sums=('402d714df7285d49269891e5def0b8a1a46f54b6ec88b0201981bdf8d866c49e83bbbf513805795630e39335e0fddc2457e1a7db582affe4a4d057ac5ab4e2bb')

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  qmake .. \
    PREFIX='/usr' \
    QMAKE_CFLAGS_RELEASE="$CFLAGS" \
    QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS" \
    SHOTCUT_VERSION=ARCH-$pkgver \
    DEFINES+=SHOTCUT_NOUPGRADE

  make

  lrelease ../translations/*.ts
}

package() {
    cd $pkgname-$pkgver/build

    make INSTALL_ROOT="$pkgdir" install
}

