# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.11.94
pkgrel=1
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="http://www.fontconfig.org/release/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' $_pkgbasename)
makedepends=('python2')
install=lib32-fontconfig.install
source=(http://www.fontconfig.org/release/${_pkgbasename}-${pkgver}.tar.bz2
        Avoid_an_error_message_on_testing_when_no_fonts.conf_installed.diff)
sha256sums=('d763c024df434146f3352448bc1f4554f390c8a48340cef7aa9cc44716a159df'
            '911ec42fe2c0b57b2115d61aa1bb327b40189374cc115e648c58b57c8a7f3aba')

  # a nice page to test font matching:
  # http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html

prepare() {
  cd $_pkgbasename-$pkgver
  # fix a bug in the testsuite
  # https://cgit.freedesktop.org/fontconfig/commit/?id=04763135d47ae24a808fc15c4482e2bb6f847ab9
  patch -Np1 -i $srcdir/Avoid_an_error_message_on_testing_when_no_fonts.conf_installed.diff
}

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts \
    --libdir=/usr/lib32
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/{etc,usr/{include,share}}
  find "$pkgdir/usr/bin" -not -type d -not -name fc-cache -delete
  mv "$pkgdir"/usr/bin/fc-cache{,-32}

  # Install license
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
