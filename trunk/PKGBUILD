# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgname=peek
pkgver=1.4.0
pkgrel=1
pkgdesc='Simple screen recorder with an easy to use interface'
arch=('x86_64')
url='https://github.com/phw/peek'
license=('GPL3')
depends=('gtk3' 'libkeybinder3' 'ffmpeg')
makedepends=('meson' 'vala' 'appstream-glib' 'txt2man'
             'gst-plugins-good' 'gst-plugins-ugly')
checkdepends=('xorg-server-xvfb')
optdepends=('gst-plugins-good: Recording under Gnome Shell'
            'gst-plugins-ugly: MP4 output under Gnome Shell'
            'gifski: High quality GIF animations with thousands of colors')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('ef1723f72d1c575493b2e9192cee6038807d37d875a797781169e5f4640635deda947362dcf2e1a9149fcfe88de532b8ea05c665b14b1befaa9aa8c982d9aa31')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  xvfb-run meson test
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}

