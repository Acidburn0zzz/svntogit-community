# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Mathieu Westphal <mathieu.westphal@kitware.com>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: <xantares09@hotmail.com>

_pkg=paraview
_mpi=openmpi
pkgname=${_pkg}
#-${_mpi}
pkgver=5.9.1
pkgrel=15
pkgdesc="Parallel Visualization application using VTK (${_mpi} version)"
arch=(x86_64)
url="https://www.paraview.org"
license=(BSD custom)
depends=(boost-libs qt5-tools qt5-x11extras qt5-svg intel-tbb openmpi ffmpeg
         adios2 liblas ospray pdal python-numpy cgns protobuf
         double-conversion expat freetype2 gdal gl2ps glew hdf5 libjpeg jsoncpp
         libjsoncpp.so libharu libxml2 lz4 xz python-mpi4py netcdf libogg
         libpng pugixml rapidjson libtheora libtiff zlib)
optdepends=(python-matplotlib python-pandas)
makedepends=(cmake boost mesa gcc-fortran ninja qt5-tools qt5-xmlpatterns eigen utf8cpp)
# pegtl https://gitlab.kitware.com/vtk/vtk/-/issues/18151
source=(${url}/files/v${pkgver%.*}/ParaView-v${pkgver/R/-R}.tar.xz
        paraview-wrapper.sh
        paraview.sh
        pvpython.sh
        vtk-gcc11.patch::https://gitlab.kitware.com/vtk/vtk/-/merge_requests/7554.patch
        vtk-fix-shader-initialization.patch::https://gitlab.kitware.com/vtk/vtk/-/merge_requests/7978.patch
        vtk-tbb-2021.patch
        vtkm-tbb-2021-pre.patch::https://gitlab.kitware.com/vtk/vtk-m/-/merge_requests/2452.patch
        vtkm-tbb-2021.patch::https://gitlab.kitware.com/vtk/vtk-m/-/merge_requests/2509.patch
        vtk-ffmpeg5.patch::https://gitlab.kitware.com/vtk/vtk/-/merge_requests/8863.patch)
sha256sums=('0d486cb6fbf55e428845c9650486f87466efcb3155e40489182a7ea85dfd4c8d'
            '05bd6268b3a6dddfcdbc106a3414d0aa9663ae48ce4775fdd9f5ed02c1bbae02'
            'e81fa887b484d98fd0df3b13e9ba19f57e50131c173ea126265cec0b529dd3f5'
            '0f3557fd21de013628826c2c971a48564dca63cbf943c76a6f7021a9a84e6223'
            'c9959adcb59e2f2657f0144b0b68239d4174947fb2ab8051f2575241281e4d68'
            '10864f69e2d6577c56cc536438b5dd7a52b004f6bb253a17569899922d804fe8'
            '5e621ed053ae6e27fea32c09ff64a700684983703d2361c658e1701b0b413189'
            '39a84440097147338fcfcc937a2efed4ff887e83c2040d43abb97f5532bdb2fc'
            '37cff664c4eaacf44ecb995e62e9e54e54880bae0857d598c74660a2159ccb2e'
            '066ad5eb3f7cd101c4bbbe4d91ba97419f18537f9dadf6654776b7151c7c6a8d')

prepare() {
  # Specify python version in wrapper
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  sed -i "s|@PYTHON@|${python_version}|" paraview-wrapper.sh
  # Arch required changes
  cd ParaView-v${pkgver/R/-R}
  # We have a patched libharu
  sed -i "s|2.4.0|2.3.0|" VTK/ThirdParty/libharu/CMakeLists.txt
  # Fix licences install (upstreamed but not released)
  sed -i 's|CACHE PATH "License|CACHE STRING "License|g' CMakeLists.txt
  sed -i 's|CACHE PATH "License|CACHE STRING "License|g' VTK/CMakeLists.txt
  # Missing includes with GCC11
  patch -p1 -d VTK < ../vtk-gcc11.patch
  # FS#71081
  patch -p1 -d VTK < ../vtk-fix-shader-initialization.patch
  # Fix build with HDF5 1.12.1, https://gitlab.kitware.com/vtk/vtk/-/issues/18265
  sed -i 's/typedef int hid_t;/typedef int64_t hid_t;/' VTK/ThirdParty/xdmf3/vtkxdmf3/core/XdmfHDF5Controller.hpp
  # Fix build with TBB 2021
  # https://gitlab.kitware.com/vtk/vtk/-/merge_requests/7650
  # https://gitlab.kitware.com/vtk/vtk/-/merge_requests/7765
  # https://gitlab.kitware.com/vtk/vtk/-/merge_requests/8011
  patch -p1 -d VTK < ../vtk-tbb-2021.patch
  patch -p1 -d VTK/ThirdParty/vtkm/vtkvtkm/vtk-m < ../vtkm-tbb-2021-pre.patch
  patch -p1 -d VTK/ThirdParty/vtkm/vtkvtkm/vtk-m < ../vtkm-tbb-2021.patch
  # Fix build with FFmpeg 5
  patch -p1 -d VTK < ../vtk-ffmpeg5.patch
}

build() {
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  cmake -B build -S ParaView-v${pkgver/R/-R} -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/opt/paraview \
    -DCMAKE_INSTALL_LICENSEDIR=share/licenses/paraview \
    -DCMAKE_SKIP_INSTALL_RPATH=OFF \
    -DPARAVIEW_ENABLE_ADIOS2=ON \
    -DPARAVIEW_ENABLE_FFMPEG=ON \
    -DPARAVIEW_ENABLE_FIDES=ON \
    -DPARAVIEW_ENABLE_GDAL=ON \
    -DPARAVIEW_ENABLE_LAS=ON \
    -DPARAVIEW_ENABLE_MOTIONFX=ON \
    -DPARAVIEW_ENABLE_PDAL=ON \
    -DPARAVIEW_ENABLE_RAYTRACING=ON \
    -DPARAVIEW_ENABLE_VISITBRIDGE=ON \
    -DPARAVIEW_ENABLE_XDMF3=ON \
    -DPARAVIEW_USE_MPI=ON \
    -DPARAVIEW_USE_PYTHON=ON \
    -DPARAVIEW_VERSIONED_INSTALL=OFF \
    -DPARAVIEW_BUILD_WITH_EXTERNAL=ON \
    -DVTK_SMP_IMPLEMENTATION_TYPE=TBB \
    -DVTKm_ENABLE_MPI=ON \
    -DVTK_MODULE_USE_EXTERNAL_VTK_pegtl=OFF \
    -Wno-dev
  ninja -C build ${MAKEFLAGS}
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install

  # Install wrappers
  for file in paraview-wrapper.sh paraview.sh pvpython.sh
  do
      install -Dm755 ${file} "${pkgdir}"/usr/bin/${file%.sh}
  done

  # Install licenses, shortcuts, icons
  install -dm755 "${pkgdir}"/usr/share
  mv "${pkgdir}"/{opt/paraview,usr}/share/applications
  mv "${pkgdir}"/{opt/paraview,usr}/share/icons
  mv "${pkgdir}"/{opt/paraview,usr}/share/licenses
  mv "${pkgdir}"/{opt/paraview,usr}/share/metainfo
}
