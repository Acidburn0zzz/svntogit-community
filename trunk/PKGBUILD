# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything
pkgver=0.0.2
pkgrel=2
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('dkms' 'qt5-base')
groups=('deepin' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        deepin-anything-server.sysusers)
sha512sums=('90ebd80800e09f2f110d3b40472615fe82cbcdda48b8d71bfdf17691d63bfc74fc3eb4779920e94fd7292b086aafdf905a9405456d50210b6270ce1a92ec8090'
            '0ff6a6de1fbfb0c33eaac511b989da321a9e43ece92708af88aee34ad1a05e55572713b1290bc2705d70b91dc7bec4fb4abd3dc664a0abe01de27d88bd9e9c85')

prepare() {
  cd deepin-anything-$pkgver
  sed -i 's|^systemd_service.path.*|systemd_service.path = /usr/lib/systemd/system|' server/src/src.pro
}

build() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver
}

package() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install

  cp debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
  install -Dm644 ../deepin-anything-server.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-anything-server.conf"
}
