# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=parole
pkgver=1.0.2
pkgrel=1
pkgdesc="Modern media player based on the GStreamer framework"
arch=('x86_64')
url="https://docs.xfce.org/apps/parole/start"
license=('GPL')
groups=('xfce4-goodies')
depends=('gst-plugins-base' 'gst-plugins-good' 'libnotify' 'libxfce4ui')
makedepends=('intltool' 'python')
optdepends=('gst-libav: Extra media codecs'
	    'gst-plugins-bad: Extra media codecs'
	    'gst-plugins-ugly: Extra media codecs')
source=(http://archive.xfce.org/src/apps/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2)
sha256sums=('bff0fc846d0d7b8f435ac5514976f1cd1d82b62dbf1b7d470e253a5b439407da')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|$(datadir)/appdata|$(datadir)/metainfo|' data/appdata/Makefile.{am,in}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
