# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.8.6
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('pro-audio' 'vee-one' 'lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
'hicolor-icon-theme' 'shared-mime-info' 'fftw')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('de52219a3900b88e7d81b5c14d19483473a84c63e446f63db94eae4581834531d6f5ec0617fad82906a3bc0b85d1d45c0b3fca0771d236ea98bfedf236498392')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
