# Maintainer:
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgbase=python-mpi4py
pkgname=('python-mpi4py' 'python2-mpi4py')
pkgver=1.3.1
pkgrel=2
arch=('i686' 'x86_64')
url="http://mpi4py.scipy.org"
license=('BSD')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools' 'openmpi')
source=(http://mpi4py.googlecode.com/files/mpi4py-${pkgver}.tar.gz)
sha512sums=('15e81f2a1e6f6af7a54f0e8ec43ddf36cc565b7fe9e3dd35603c9d128199c5acd1584757ffca999c8826c0bbccf371175d40a7a24c85d3369efd77da5b3b603f')

build() {
    cd "${srcdir}"
    cp -a mpi4py-${pkgver}{,-python2}

    # Build python 3 module
    cd mpi4py-${pkgver}
    python3 setup.py build

    # Build python 2 module
    cd ../mpi4py-${pkgver}-python2
    python2 setup.py build
}

package_python-mpi4py() {
    pkgdesc="Python bindings of the Message Passing Interface (MPI)"
    depends=('python>=3.3' 'openmpi')

    cd "${srcdir}/mpi4py-${pkgver}"
    python3 setup.py install --root "${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

package_python2-mpi4py() {
    pkgdesc="Python2 bindings of the Message Passing Interface (MPI)"
    depends=('python2>=2.7' 'openmpi')

    cd "${srcdir}/mpi4py-${pkgver}-python2"
    python2 setup.py install --root "${pkgdir}" --optimize=1 --skip-build

    install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
}

# vim:set ts=2 sw=2 et:
