# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rofimoji
pkgver=5.0.0
pkgrel=1
pkgdesc='Character picker for rofi'
arch=('any')
url=https://github.com/fdw/rofimoji
license=('MIT')
depends=('python-configargparse')
makedepends=('python-setuptools')
optdepends=('emoji-font: for the emojis character file'
            'nerd-fonts: for the nerd_font character file'
            'rofi: for the X.Org selector'
            'xclip: for one of the X.Org clipboarders'
            'xsel: for one of the X.Org clipboarders'
            'xdotool: for the X.Org typer'
            'wofi: for the Wayland selector'
            'wl-copy: for the Wayland clipboarder'
            'wtype: for the Wayland typer')
install=$pkgname.install
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('2d80d0e8fa94635b1da9727b4fc1d2f3119d5af0bc61e6f5ed07bc959067e809dfd86e999bb4ccde24a9b2a57946519e6fffada2e7e3eb94c44b4c088becc488')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
