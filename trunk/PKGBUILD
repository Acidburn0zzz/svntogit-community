# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=fontobene-qt5
pkgver=0.1.0
pkgrel=1
pkgdesc='A header-only library to parse FontoBene stroke fonts with C++/Qt5'
arch=('x86_64')
url='https://github.com/fontobene/fontobene-qt5'
license=('MIT' 'Apache')
depends=('qt5-base')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('SKIP')

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  qmake -r ../$pkgname.pro \
      PREFIX=/usr \
      QMAKE_CFLAGS_RELEASE="$CFLAGS" \
      QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS"

  make
}

check() {
  cd $pkgname-$pkgver/build

  make check
}

package() {
  cd $pkgname-$pkgver/build

  make INSTALL_ROOT="$pkgdir" install

  install -Dm 644 ../LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 ../LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

