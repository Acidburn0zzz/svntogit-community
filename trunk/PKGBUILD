# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.6.post1
# The commit bumps version in setup.py, and it's not tagged
_commit=bc2fcd148d460ec9c0f0c12bc77e0326adad5546
pkgrel=2
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/$_commit.tar.gz"
        "pytest-asyncio-0.14.diff")
sha512sums=('476ec6bf8ef20255b685720fa501c0c672a0d6a32d0cd5bb1e6458a00236201dad8ff2a9d0d2a64365aebb4d3d2dc089a7cfc2bd84923f6ec9ecb7114322327c'
            '442176e351ba1c3937fc98e2a97b8ceab59f340d438d9d59aa2c156b299061b945f2a6a5f07b125138d347ff1cc1db4623ee7043469eaf25634628aa0cb02320')

prepare() {
  cd nvchecker-$_commit
  patch -Np1 -i ../pytest-asyncio-0.14.diff
}

build() {
  cd nvchecker-$_commit
  python setup.py build
}

check() {
  cd nvchecker-$_commit
  pytest
}

package() {
  cd nvchecker-$_commit
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
