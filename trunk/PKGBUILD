# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=openimagedenoise
_pkgname=oidn
pkgver=1.2.0
pkgrel=3
pkgdesc='Intel(R) Open Image Denoise library'
arch=('x86_64')
url='https://openimagedenoise.github.io'
license=('Apache')
depends=('intel-tbb')
makedepends=('git' 'cmake' 'ninja' 'ispc' 'python' 'git-lfs')
source=("git+https://github.com/OpenImageDenoise/oidn#tag=v$pkgver"
        'git+https://github.com/OpenImageDenoise/oidn-weights'
        "oidn-mkl-dnn::git+https://github.com/OpenImageDenoise/mkl-dnn"
        'fix-ispc.patch::https://github.com/OpenImageDenoise/oidn/commit/e321d7c90a2c706a521a3afd8913af36b121dc9e.patch'
        'fix-unwanted-simd.patch')
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'b03916098e771fb0467d32d60aa687c804da1d184956b392489e5943c8d71e439c05d05d809fc8e616a45a1cdcc9425cd29e73bd2222f4b6a2093c3eb09fadb2'
            'e180338d0b1fbfd52c3c7fe29cabcb482a6ec80f6459e7096b7773bd7f3ae77035168cec62d20103d25d14eeb809e769166a9d0f645c4dba8f1a59cdf1d2157b')

prepare() {
    cd $_pkgname

    git submodule init
    git config submodule.weights.url "$srcdir"/oidn-weights
    git config submodule.mkl-dnn.url "$srcdir"/oidn-mkl-dnn
    git submodule update

    patch -p1 -i ../fix-ispc.patch
    patch -p1 -i ../fix-unwanted-simd.patch
}

build() {
    cd $_pkgname

    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    ninja -C build
}

package() {
    cd $_pkgname
    DESTDIR="$pkgdir" ninja -C build install
    rm "$pkgdir/usr/bin/tests"
}

