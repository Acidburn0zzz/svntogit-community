# Contributor: Biru Ionut <ionut@archlinux.ro>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

pkgname=lib32-pango
pkgver=1.44.7+11+g73b46b04
pkgrel=1
epoch=1
pkgdesc="A library for layout and rendering of text (32-bit)"
url="https://www.pango.org/"
arch=(x86_64)
license=(LGPL)
depends=(lib32-libthai lib32-cairo lib32-libxft lib32-harfbuzz lib32-fribidi pango)
makedepends=(git meson)
checkdepends=(ttf-dejavu cantarell-fonts)
provides=(libpango{,cairo,ft2,xft}-1.0.so)
_commit=73b46b04c724335ad73534cc69955baa2388b861  # pango-1-44
source=("git+https://gitlab.gnome.org/GNOME/pango.git#commit=$_commit"
        0001-Use-shape-flags-for-tab-width.patch
        test.diff)
sha256sums=('SKIP'
            'd87bf1bb8dcd7edba0d8f49ddc7fd6bd9965f21a38f186fbfb297828ad53f4d1'
            '401b51f3aee44510fc94878b5549ff4a4a6d79584b5d6df2dd7e0978b9d574ce')

pkgver() {
  cd pango
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd pango

  # https://bugs.archlinux.org/task/63914
  git apply -3 ../0001-Use-shape-flags-for-tab-width.patch

  # Harmless test case failure; could be from Cantarell update
  git apply -3 ../test.diff
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pango build --libdir=/usr/lib32 -D introspection=false
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -r "$pkgdir"/usr/{bin,include}
}
