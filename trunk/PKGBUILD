# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.1.0a4
pkgrel=1
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
makedepends=(python2-setuptools python-setuptools libmpc git)
source=("https://github.com/aleaxit/gmpy/releases/download/gmpy2-${pkgver/.0/}/gmpy2-$pkgver.tar.gz")
sha256sums=('7822b3336ee022b64edd66695375bd06dfc8586a52450128c0a3cd0d9cb4b857')

prepare() {
  cp -r gmpy2-$pkgver{,-py2}  
}

build() {
  cd gmpy2-$pkgver
  python setup.py build

  cd ../gmpy2-$pkgver-py2
  python2 setup.py build
}

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy2-$pkgver-py2
  python2 setup.py install --root=$pkgdir --optimize=1 --skip-build

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

