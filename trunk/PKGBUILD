# Maintainer: David Runge <dave@sleepmap.de>
_name=spreed
pkgname=nextcloud-app-spreed
pkgver=6.0.2
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud>=16.0.0' 'nextcloud<17.0.0')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('4fc03bb1f88d93343ce8c38f754d9f9fcf2ce1c88840ec36b54725216314370c7adaee1d57347f2bf03198b84543c4f6e4e21010f9aba990bd171d59ebf031db')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
