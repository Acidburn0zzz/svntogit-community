# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Adam Hani Schakaki <adam@schakaki.net>
# Contributor: Ner0

pkgname=muffin
pkgver=4.4.1
pkgrel=1
pkgdesc="Cinnamon window manager based on Mutter"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('cinnamon-desktop' 'gobject-introspection-runtime' 'libcanberra' 'libinput'
         'libsm' 'libxkbcommon-x11' 'startup-notification' 'zenity')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc' 'gnome-doc-utils')
options=('!emptydirs')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('86b59ca3dbb0ff1257a753971b8520c2b0939f9eb0eddd9260d67cf1c2b39d3d')
b2sums=('29b9bc722f3fd99b5a0e125987c2a27c52c0d73ff381ef36f2be6e44624ee844bfa6da058b9f223ccca9d99a819294c3ccc630b6c8fc1b2d0f5184e7ebf6343e')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # thou shalt not -Werror
    sed -i 's/--warn-error//g' src/Makefile.am

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/muffin \
                --localstatedir=/var \
                --disable-gtk-doc \
                --disable-static \
                --disable-schemas-compile \
                --disable-wayland-egl-server \
                --enable-compile-warnings=minimum

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install

    # Remove unused stuff
    make -C src DESTDIR="${pkgdir}" uninstall-binPROGRAMS uninstall-desktopfilesDATA
    make -C src/tools DESTDIR="${pkgdir}" uninstall
    make -C src/compositor/plugins DESTDIR="${pkgdir}" uninstall
    make -C doc/man DESTDIR="${pkgdir}" uninstall
}
