# Maintainer : Felix Yan <felixonmars@archlinux.org>

pkgname=rust-bindgen
pkgver=0.54.0
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs')
makedepends=('rust')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz")
sha512sums=('bb77376dee773c74a6aec514b98a2f843f432af96f448abf122fd13b9b59d94b8da0b64e3752a35492854f8e0cbc1500521a6d2890f5191397bc6b6efdb1f084')

build() {
  cd $pkgname-$pkgver
  cargo build --release
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
