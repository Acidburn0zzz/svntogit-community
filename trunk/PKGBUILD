# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-identify
pkgver=1.4.11
pkgrel=1
pkgdesc="File identification library for Python"
url="https://github.com/chriskuehl/identify"
license=('MIT')
arch=('any')
depends=('python')
optdepends=('python-editdistance: for license support')
makedepends=('python-setuptools' 'python-editdistance')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/chriskuehl/identify/archive/v$pkgver.tar.gz")
sha512sums=('e28c4c9f5a9b2499def3b5ad7b29f17066a4fd29396af90d7d7d5806bfe35f078c13c3844206d0c90ec18215eef0e72f70f435b2fb48b7bf81d660d69fe5dd9e')

build() {
  cd identify-$pkgver
  python setup.py build
}

check() {
  cd identify-$pkgver
  python setup.py pytest
}

package() {
  cd identify-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
