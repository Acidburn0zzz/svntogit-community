# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: robertfoster

pkgname=libpam-google-authenticator
pkgver=1.06
pkgrel=1
pkgdesc='PAM module for google authenticator app'
arch=('x86_64')
url="https://github.com/google/google-authenticator-libpam"
license=('Apache')
depends=('pam')
optdepends=('qrencode: scannable QR codes for google auth phone app')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/google-authenticator-libpam/archive/$pkgver.tar.gz")
sha512sums=('a278360be1a3c5a03ddb38d40be45dbba16b6f565ee919377a918565bd8c567ce261890dc10d277edea4f469cf1da1d9cf2c9f3ccfeef8c6887d713c785a5440')

build() {
  cd google-authenticator-libpam-$pkgver
  ./bootstrap.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd google-authenticator-libpam-$pkgver
  make DESTDIR="$pkgdir" install
  libtool --finish "$pkgdir"/usr/lib/security
}
