# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=thefuck
pkgver=3.26
pkgrel=1
pkgdesc="Magnificent app which corrects your previous console command"
arch=('any')
url="https://github.com/nvbn/thefuck"
license=('MIT')
depends=('python-setuptools' 'python-psutil' 'python-six' 'python-colorama' 'python-decorator'
         'python-pyte')
makedepends=('python-pip')
checkdepends=('python-pytest-mock' 'python-mock' 'python-pexpect')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nvbn/thefuck/archive/$pkgver.tar.gz")
sha512sums=('2dbcc7ba0adb24b3f7d80d0c575a4ed515508e0ab5bc2fc4eb9ba82ad36325af1784eb9c241794ce0e038ed8e99042eb21cc3b29eeaa273517aebc7205ab787f')

build() {
  cd thefuck-$pkgver
  python setup.py build
}

check() {
  cd thefuck-$pkgver

  # Hack distribution test by installing it and set PYTHONPATH afterwards
  python setup.py install --root="$PWD/tmp_install" --optimize=1

  # Hack $HOME as it's / in the chroot and not writable
  cp tests/test_conf.py "$srcdir/test_conf.py.bak"
  sed -i "s|data = {}|data = {'HOME': '$srcdir'}|" tests/test_conf.py
  LC_CTYPE=en_US.utf8 PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test tests
  mv "$srcdir/test_conf.py.bak" tests/test_conf.py
}

package() {
  cd thefuck-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
