# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>
 
pkgname=megaglest
pkgver=3.4.0
pkgrel=1
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL2')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'mesa' 'lua')
makedepends=('ftjam' 'p7zip' 'wxgtk' 'cmake')
source=("http://downloads.sourceforge.net/project/${pkgname}/current_release/${pkgname}-source-${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/project/${pkgname}/current_release/megaglest-data-${pkgver}.7z"
        "megaglest.png"
        "megaglest.desktop"
        "megaglest.sh")
md5sums=('904846c60369f7898c7f83d506624d5a'
         'e46270881f1949e0876f4c39ecbf0a05'
         '5622ce716033d9afb62a3291b47844ed'
         '97dc7ea865efbea5daaf300207dad518'
         'e3d6abbe79263d2fa5edb0bcbcf8755a')

build() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  7z x -y ${srcdir}/megaglest-data-${pkgver}.7z

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake .. \
   -DWANT_SVN_STAMP=OFF \
 	 -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version
  make
}

package() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  # Megaglest doesn't have a nice installer. Let's package it ourselves. 
  # Damnit, upstream.
  install -d -m 0755 "${pkgdir}"/usr/{bin,share/${pkgname}}
  install -m 0755 "${srcdir}"/$pkgname.sh "${pkgdir}"/usr/bin/$pkgname
  install -m 0755 mk/linux/glest.bin "${pkgdir}"/usr/share/${pkgname}/$pkgname
  install -m 0755 mk/linux/glest_configurator "${pkgdir}"/usr/bin/glest_configurator
  install -m 0755 mk/linux/glest_editor "${pkgdir}"/usr/bin/glest_editor
  install -m 0644 glest.ini glestkeys.ini servers.ini "${pkgdir}"/usr/share/$pkgname

  install -D -m 0644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -D -m 0644 "${srcdir}"/${pkgname}.png "${pkgdir}"/usr/share/pixmaps/${pkgname}.png
}

# vim: sw=2:ts=2 et:
