# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-control-center
pkgver=4.2.5.11
pkgrel=1
pkgdesc='New control center for linux deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-control-center"
license=('GPL3')
depends=('desktop-file-utils' 'dtkwidget' 'deepin-account-faces' 'deepin-api' 'geoip'
         'geoip-database' 'geoip-database-extra' 'deepin-daemon' 'startdde'
         'deepin-qt-dbus-factory' 'deepin-qt5integration')
makedepends=('deepin-dock' 'qt5-tools')
conflicts=('dde-control-center' 'dde-dock-applets')
replaces=('dde-control-center' 'dde-dock-applets')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-control-center/archive/$pkgver.tar.gz"
        fix-disable-sys-update.patch)
sha512sums=('25a0a59916dc8747babaf4ed828c99b90867529c648c24496324d31fc9ad71e6a84dab8a561810dc56f51b1ff4baa9e8187c41ecfed04ebbf31fcf2b0d4ff1f8'
            '119b4d6ad6cac8aa5f71a30f12180987f8c613987c055f185f24b3f8c0184828d2e46bf5edb74fd1c8a3c2a6d677aabfcf113e999aa9d78732823cca0de1db5b')

prepare() {
  cd dde-control-center-$pkgver
  patch -p1 -i ../fix-disable-sys-update.patch
}

build() {
  cd dde-control-center-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr DISABLE_SYS_UPDATE=YES
  make
}

package() {
  cd dde-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
