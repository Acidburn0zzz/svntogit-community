# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: J.W. Birdsong  <jwbirdsongATmailDOTcom>

pkgname=teamspeak3
pkgver=3.1.5
pkgrel=1
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com"
license=('custom')
optdepends=('libpulse')
arch=('i686' 'x86_64')
source_i686=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_x86-${pkgver}.run")
source_x86_64=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_amd64-${pkgver}.run")
source=('teamspeak3.desktop'
        'teamspeak3.png'
        'LICENSE')
sha512sums=('4cac0b7446dc3e728b232fdd6c6d75be1d2a5530635eab5918a4e7dd6a4a3fc21d6a75055da919b56e5cf849f4c5ba26ece56759a45bc4850eba3eb73853759d'
            'af18ed36a3c8de90b60a7205ebf91a35d4b517f22f97d541c82debdb12697882469d9771fdf3fb8b081aeb74a01237f3afd4732daccf4b374dc6d39e7df5d444'
            'd6d03f1bda86c90390ce0032e4d30e889977f80ff76ba1e5c443109e93cbedbee92c6fc2759f58d23659c15dba4f2ac718861571239ba117dcfe11f136f556dc')
sha512sums_i686=('a5b2ce1e20fe6b491d724de9180965285723faaae39e5c42a41825f4c7f99985b6a95e64709dd73572d19d0a712e2262a3df5e5e44bd2213d19fb6a5bdbaa5ac')
sha512sums_x86_64=('14229c2f33819a8ccf5133b24eb4159c8f6fb6fad93e99d8e0e2b68d0478c35fcfe6fb7e7932e5f1141323f6ee0555eb72a884c7e84ca5ccd5110f24045a9dc4')

[[ "$CARCH" == "i686" ]] && _TSARCH='x86'
[[ "$CARCH" == "x86_64" ]] && _TSARCH='amd64'

prepare() {
  mkdir archive && cd archive
  sh ../TeamSpeak3-Client-linux_${_TSARCH}-${pkgver}.run --tar -xf 2>/dev/null

  # Fix permissions
  find -type d | xargs chmod 755
  find -type f | xargs chmod 644
  find -name *.so | xargs chmod 755
  chmod +x ts3client*
  chmod +x package_inst
}

package() {
  install -d "${pkgdir}"/{usr/bin/,opt/teamspeak3}

  cp -r archive/* ${pkgdir}/opt/teamspeak3/

  install -D -m644 "${srcdir}"/teamspeak3.desktop "${pkgdir}"/usr/share/applications/teamspeak3.desktop
  install -D -m644 "${srcdir}"/teamspeak3.png "${pkgdir}"/usr/share/pixmaps/teamspeak3.png
  install -D -m644 "${srcdir}"/LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
  ln -s /opt/teamspeak3/ts3client_runscript.sh "${pkgdir}"/usr/bin/teamspeak3
}
# vim:set ts=2 sw=2 et:
