# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=arch-release-promotion
pkgver=0.1.1
pkgrel=1
pkgdesc="Promote official releases with signatures and additional artifacts"
arch=('any')
url="https://gitlab.archlinux.org/archlinux/arch-release-promotion"
license=('GPL3')
depends=('python-dotenv' 'python-email-validator' 'python-gitlab'
'python-orjson' 'python-prometheus_client' 'python-pydantic' 'python-pyxdg'
'python-toml' 'python-torrentool')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
backup=("etc/${pkgname}/projects.toml")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('56cf61e4b9dfcae82653402f129b226598cbca9aebf5d13a27326031767a264e')
b2sums=('3796df10b60625f3a26e677a7065e9c38dae217eef32cbf35ef9f6661667906936c12804294a511e348f350d1f7675a90e52fcdcd5f26cccc05c42e7d7c9b1bc')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {examples/example.toml,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/projects.toml -t "${pkgdir}/etc/${pkgname}"
}
