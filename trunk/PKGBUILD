# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lv2lint
pkgver=0.4.0
pkgrel=1
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=('Artistic2.0')
depends=('glibc' 'libcurl.so' 'libelf' 'liblilv-0.so')
makedepends=('lv2' 'meson')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('a3df72ae7bca0471ae3fd08b721f96e482bd6b9fc55688ffe332889e9c4205a404aa91b1898181ed4fd56116588eef93a0b2196d6b56c8fbf30b092716e2a7f9')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr \
        -Donline-tests=true \
        -Delf-tests=true \
        build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  ninja -C build test
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="$pkgdir/" ninja -C build install
  install -vDm 644 {ChangeLog,README.md,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
