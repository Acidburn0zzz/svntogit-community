# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.4.12
pkgrel=1
pkgdesc='E-mail client for GNOME'
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'libwebkit' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview2' 'gtkspell' 'gnome-icon-theme' 'desktop-file-utils')
makedepends=('perlxml' 'gnome-doc-utils' 'intltool')
install=$pkgname.install
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('3f72fc69bb7fecb59e64c4cb88dc5083ed58dc76c9345b22d6c15af2c6b1e3c4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-ssl \
        --with-gpgme=gpgme-config \
        --with-gss \
        --with-ldap \
        --with-gtksourceview \
        --with-gtkspell \
        --with-rubrica \
        --with-sqlite \
        --without-nm \
        --without-gnome \
        --with-html-widget=webkit
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
