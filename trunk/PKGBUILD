# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.7.2
pkgrel=2
pkgdesc="Batch image converter and resizer"
arch=('x86_64')
url="http://converseen.fasterland.net/"
license=('GPL3')
depends=('ghostscript' 'hicolor-icon-theme' 'libheif' 'libmagick' 'libraw' 'librsvg' 'libwebp' 'libwmf' 'openexr' 'qt5-base')
makedepends=('cmake' 'imagemagick' 'qt5-tools')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('27b322450a80b6eb7d7f9d936f33ffe18cb06c6ab684c027dd474af91a03fb76')

prepare() {
  mkdir build
  cd $pkgname-$pkgver

  # Don't use legacy path for AppStream metainfo file
  # https://github.com/Faster3ck/Converseen/pull/61
  sed -i 's|/usr/share/appdata/|/usr/share/metainfo/|' CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname-$pkgver
  for size in 16x16 22x22 24x24 32x32 48x48 64x64 128x128 256x256; do
    install -dm755 "$pkgdir"/usr/share/icons/hicolor/$size/apps/
    convert res/converseen.png +set date:create +set date:modify -resize $size -alpha on "$pkgdir"/usr/share/icons/hicolor/$size/apps/converseen.png
  done
  rm -r "$pkgdir"/usr/share/pixmaps/
}
