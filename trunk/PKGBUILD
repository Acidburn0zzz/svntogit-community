# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libwapcaplet
pkgver=0.4.1
pkgrel=2
pkgdesc='String internment library with rapid string comparison'
arch=(x86_64)
url='https://www.netsurf-browser.org/projects/libwapcaplet/'
license=(MIT)
makedepends=('netsurf-buildsystem>=1.7')
source=("https://download.netsurf-browser.org/libs/releases/$pkgname-$pkgver-src.tar.gz")
sha256sums=('c8ff6ea5b64feda806f5b79ba970442f5edb237046a044bc0f7e4d54a9a4978c')

prepare() {
  sed -i 's:_BSD_SOURCE:_DEFAULT_SOURCE:;s:rns:rns -w:' \
    $pkgname-$pkgver/Makefile
}

build() {
  make -C $pkgname-$pkgver PREFIX=/usr COMPONENT_TYPE="lib-shared"
}

package() {
  make -C $pkgname-$pkgver \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    COMPONENT_TYPE="lib-shared" \
    install
  install -Dm644 $pkgname-$pkgver/COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
