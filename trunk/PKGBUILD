# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.2.8.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="https://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
_tarname=jruby-dist-$pkgver-bin.tar.gz
noextract=($_tarname)
source=(https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/$_tarname)
md5sums=('7d652b586a18f1c665289fd7e6edf4f5')
sha1sums=('5b0b73b3d696afaeac92e6f8879dedcc63ac39d8')
sha256sums=('b7c58688093f54acd89d732a8bf40e3ae0ac4c92488d6f5b424c33e4fb09c7bb')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$_tarname"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -print -delete
  rm -rv opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}

  chmod -cR g-w opt/jruby

  mkdir -p usr/bin
  ln -srt usr/bin opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}

  mkdir -p usr/share/licenses/$pkgname
  ln -srt usr/share/licenses/$pkgname opt/jruby/COPYING
}
