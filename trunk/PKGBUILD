# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-elasticsearch
pkgname=('python-elasticsearch' 'python2-elasticsearch')
pkgver=7.0.3
pkgrel=1
arch=('any')
pkgdesc='Python client for Elasticsearch'
url='https://github.com/elastic/elasticsearch-py'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-urllib3' 'python2-urllib3')
checkdepends=('python-nose' 'python2-nose' 'python-requests' 'python2-requests' 'python-coverage'
              'python2-coverage' 'python-mock' 'python2-mock' 'python-pyaml' 'python2-pyaml'
              'python-nosexcover' 'python2-nosexcover')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/elastic/elasticsearch-py/archive/$pkgver.tar.gz")
sha512sums=('32bd4923cc9a9187315b1029ccd3ffd6498726f48bbb0a225b7a7f1016064c630a3d002866b7033d7410c632b25c10503baf5f2cde29606fddf9089e8bf2151d')

prepare() {
  sed -i 's/urllib3<.*,>/urllib3>/' elasticsearch-py-$pkgver/setup.py
  cp -a elasticsearch-py-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/elasticsearch-py-$pkgver
  python setup.py build

  cd "$srcdir"/elasticsearch-py-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/elasticsearch-py-$pkgver
  # https://github.com/elastic/elasticsearch-py/issues/816
  # python setup.py test

  cd "$srcdir"/elasticsearch-py-$pkgver-py2
  # python2 setup.py test
}

package_python-elasticsearch() {
  depends=('python-urllib3')

  cd elasticsearch-py-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}

package_python2-elasticsearch() {
  depends=('python2-urllib3')

  cd elasticsearch-py-$pkgver-py2
  python2 setup.py install --root "$pkgdir" --optimize=1
}
