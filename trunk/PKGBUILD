# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-crypto
pkgver=0.0.20190213
_commit=a4c6cb3142f211c99e4bf4cd769535b29a9b616f
pkgrel=1
pkgdesc='Go supplementary cryptography libraries'
arch=('any')
url="https://github.com/golang/crypto"
license=('BSD')
depends=('go-pie' 'golang-golang-x-net' 'golang-golang-x-sys')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/crypto/archive/$_commit.tar.gz")
sha512sums=('2f5414a2f03285802b64e55ce7eb38c305ac8d82899f87fbd5e38fc07792f3398dbc59e78dcbf1dd0b44f448fff79df70b5a049ccb3b356f3ade56450ff103b9')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$srcdir"/build/src/golang.org/x/crypto
  cd "$srcdir"/build/src/golang.org/x/crypto
  go test -v $(go list ./... | grep -v vendor)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/crypto

  install -Dm644 crypto-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
