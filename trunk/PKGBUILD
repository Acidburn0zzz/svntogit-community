# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=tree-sitter
pkgver=0.19.3
pkgrel=1
pkgdesc='An incremental parsing system for programming tools'
arch=('x86_64')
url='https://github.com/tree-sitter/tree-sitter'
license=('MIT')
provides=('libtree-sitter.so')
makedepends=('git' 'cargo' 'npm' 'emscripten')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('49997fc69249a7fa935cf8a66d44bf4ba8dc8f19e56d3e0ea3ff08ed4bf2334b274b1d119989bdc7d693b284148cf7537e5f2acf5708661232bcb03f8a52c552')

build() {
  cd $pkgname-$pkgver

  make

  ./script/build-wasm

  cd cli

  cargo build --release --locked --all-features
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 755 target/release/$pkgname -t "$pkgdir"/usr/bin

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

