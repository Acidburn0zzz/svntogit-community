# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=x11vnc
epoch=1
pkgver=0.9.16
pkgrel=3
_commit="4ca006fed80410bd9b061a1519bd5d9366bb0bc8"
pkgdesc='VNC server for real X displays'
url='https://github.com/LibVNC/x11vnc'
arch=('x86_64')
license=('GPL2')
optdepends=('tk: GUI support'
            'net-tools: -auth guess'
            'xf86-video-dummy: Xdummy script')
depends=('libvncserver' 'openssl' 'libjpeg' 'libxtst' 'libxinerama'
         'libxdamage' 'libxrandr' 'avahi' 'xorg-xdpyinfo')
makedpends=('git')
source=("git+https://github.com/LibVNC/x11vnc.git#commit=${_commit}?signed"
        "$pkgname-xfc-null-ptr.patch::https://github.com/LibVNC/x11vnc/commit/95a10ab64c2d.patch"
        "$pkgname-gcc10.patch::https://github.com/LibVNC/x11vnc/commit/a48b0b1cd887.patch"
        'service')
validpgpkeys=('25E71D2709955ECD4D041E03421BB3B45C6067F8') # Christian Beier
sha256sums=('SKIP'
            'eaa4678185dee4e2df6db8c74b5f5159ca982050825093362a7be79b4552ebf8'
            '9358217c23e586cb34d98564e5031dd58bf43e621d23435629d2105c901d0aa8'
            'cfb19d44e09e960e2fdb958c9258bccf23c2677715314985f7e819f1dcedb6e4')

prepare() {
	cd "${srcdir}/${pkgname}"
	patch -p1 -i ../$pkgname-xfc-null-ptr.patch # FS#65851
	patch -p1 -i ../$pkgname-gcc10.patch
	autoreconf --force --install
}

build() {
	cd "${srcdir}/${pkgname}"
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "${srcdir}/${pkgname}"
	make DESTDIR="${pkgdir}" install
	install misc/{rx11vnc,Xdummy} "${pkgdir}"/usr/bin
	install -Dm644 ../service "${pkgdir}/usr/lib/systemd/system/x11vnc.service"
}
