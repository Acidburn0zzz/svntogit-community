# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=m2r
pkgname=(m2r python2-m2r)
pkgver=0.1.5
_commit=539a0794236ad25d03bab717c2cc5555d3b098de
pkgrel=1
epoch=1
pkgdesc="Markdown to reStructuredText converter"
arch=('any')
url="https://github.com/miyakogi/m2r"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-mistune' 'python2-mistune'
             'python-docutils' 'python2-docutils')
checkdepends=('python2-mock' 'python-pygments' 'python2-pygments' 'python-pytest-runner'
              'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/miyakogi/m2r/archive/$_commit.tar.gz")
sha512sums=('7e7ffeca35733b92daba8f01e80171da5a54508a4ed6a31ce9c846f764e439c882cd7eb3b2c1a0f9d1248ffee70f3de2059fcb54ec4817729d07f3b18d523dcb')

prepare() {
  cp -a m2r-$_commit{,-py2}
}

build() {
  cd "$srcdir"/m2r-$_commit
  python setup.py build

  cd "$srcdir"/m2r-$_commit-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/m2r-$_commit
  python setup.py pytest

  cd "$srcdir"/m2r-$_commit-py2
  python2 setup.py pytest
}

package_m2r() {
  depends=('python-mistune' 'python-docutils' 'python-setuptools')

  cd "$srcdir"/m2r-$_commit
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-m2r() {
  depends=('python2-mistune' 'python2-docutils' 'python2-setuptools')

  cd "$srcdir"/m2r-$_commit-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/m2r{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
