# $Id$
# Maintainer:  Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: TripleE <eric1548@yahoo.com>

pkgname=sword
pkgver=1.6.2
pkgrel=4
pkgdesc="SWORD libraries for Bible programs"
arch=('i686' 'x86_64')
url="http://www.crosswire.org/sword/index.jsp"
license=('GPL')
depends=('curl' 'icu' 'clucene' 'swig')
makedepends=('cmake')
backup=('etc/sword.conf')
source=("http://www.crosswire.org/ftpmirror/pub/${pkgname}/source/v1.6/${pkgname}-${pkgver}.tar.gz")
md5sums=('a7dc4456e20e915fec46d774b690e305')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make 
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
