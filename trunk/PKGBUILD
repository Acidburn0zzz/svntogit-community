# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu (.desktop and install files)

pkgname=mupdf
pkgver=0.8
pkgrel=2
pkgdesc="lightweight PDF viewer and toolkit written in portable C"
arch=('i686' 'x86_64')
url="http://mupdf.com"
license=('GPL3')
depends=('freetype2' 'libjpeg' 'jbig2dec' 'openjpeg' 'libxext')
install=mupdf.install
source=("http://mupdf.com/download/$pkgname-$pkgver-source.tar.gz")
md5sums=('ce105056957706dd62990966a2717cc1')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make build=release prefix="$pkgdir/usr"
}

package() {
	
	# Install
	cd "$srcdir/$pkgname-$pkgver"
	make build=release prefix="${pkgdir}/usr" install

	# Rename executables
	msg "Renaming executables"
	cd "${pkgdir}/usr/bin"
	for i in pdf*; do 
		mv "$i" "$i-mupdf";
	done

	cd "$srcdir/$pkgname-$pkgver"
	# Fix mupdf.desktop
	sed -i "s/mupdf.xpm/mupdf/" debian/mupdf.desktop
  	sed -i "s/application\/x-pdf/application\/x-pdf/" debian/mupdf.desktop

	# Install other stuff
	cd "$srcdir/$pkgname-$pkgver/debian"
	install -Dm644 mupdf.desktop "$pkgdir/usr/share/applications/mupdf.desktop"
  	install -Dm644 mupdf.xpm "$pkgdir/usr/share/pixmaps/mupdf.xpm"
  	install -Dm644 mupdf.1 "$pkgdir/usr/share/man/man1/mupdf.1"
}
