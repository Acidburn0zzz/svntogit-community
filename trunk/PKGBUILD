# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Johnathan Jenkins <twodopeshaggy@gmail.com>
# Contributor: prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive <almostalive2003@gmail.com>

pkgname=libretro-desmume
pkgver=6239
pkgrel=1
pkgdesc='Nintendo DS core'
arch=(x86_64)
url='https://github.com/libretro/desmume'
license=(GPL2)
groups=(libretro)
depends=(
  libgl
  libretro-core-info
)
makedepends=(
  git
  mesa
)
_commit='48975f33baeb982795f08bf5bb19d8ecae9859b9'
source=(libretro-desmume::git+https://github.com/libretro/desmume.git#commit=${_commit})
sha256sums=(SKIP)

pkgver() {
  cd libretro-desmume

  git rev-list --count HEAD
}

build() {
  cd libretro-desmume/desmume/src/frontend/libretro

  make -f Makefile.libretro
}

package() {
  cd libretro-desmume/desmume/src/frontend/libretro

  install -Dm 644 desmume_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
