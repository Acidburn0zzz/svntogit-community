# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.18.0
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("https://github.com/watchexec/watchexec/archive/refs/tags/cli-v${pkgver}.tar.gz")
options=('!lto')
sha256sums=('dc022fab03c47381b52028fe4cdd57984cde55b9af4561cb0133e881eac7e6d0')

build() {
  cd "watchexec-cli-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "watchexec-cli-v$pkgver"
  cargo test --locked
}

package() {
  cd "watchexec-cli-v$pkgver"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
