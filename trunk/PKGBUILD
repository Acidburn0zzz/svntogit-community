# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tenacity
pkgver=5.1.5
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
depends=('python-six')
optdepends=('python-tornado: for Tornado coroutines support')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('cbc4a5a82b235d57c8703cf688b039850ff9a69443c8260bb0d7667e98b959bae83ee9b68f1a03b268dd8c12182a27f2bd7df966182e0d6851e205156c962278')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tenacity-$pkgver
  python setup.py build
}

check() {
  cd tenacity-$pkgver
  python setup.py pytest
}

package() {
  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
