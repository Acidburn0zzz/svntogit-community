# Maintainer: Christian Hesse <arch@eworm.de>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: Marc St-Laurent <mstlaurent@canada.com>

pkgname=xfmpc
pkgver=0.2.90
pkgrel=1
pkgdesc="A graphical GTK+ MPD client focusing on low footprint"
arch=('x86_64')
url="https://goodies.xfce.org/projects/applications/xfmpc/"
license=('GPL')
depends=('libxfce4ui' 'libmpd')
makedepends=('intltool')
source=("https://archive.xfce.org/src/apps/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('7044fbb734ca1cd827f206951cd0ce675b44cecd1552d5302bd849fc540beb49')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}

