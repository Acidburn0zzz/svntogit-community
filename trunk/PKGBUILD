# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.2.1
pkgrel=1
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'libraw' 'openexr' 'qt' 'qtwebkit')
makedepends=('cmake')
optdepends=('hugin: align image stack functionality')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://downloads.sourceforge.net/qtpfsgui/luminance-hdr-v${pkgver}.tar.gz)
sha256sums=('a8d781ca7a07737dfb1fe00702f948ecd044be6b8405a23d9e18fe46b27036f1')

build() {
  cd "${srcdir}/luminance-hdr-v${pkgver}"

  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/luminance-hdr-v${pkgver}"

  make DESTDIR="${pkgdir}" install
}
