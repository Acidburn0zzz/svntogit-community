# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=toolbox
pkgver=0.0.16
pkgrel=1
pkgdesc='Unprivileged development environment'
arch=(any)
url='https://github.com/containers/toolbox'
license=(APACHE)
depends=(podman bash)
makedepends=(shellcheck go-md2man bash-completion ninja git meson)
_commit=2a2867789d508f29191e718e25f8704cbd69246b  # tags/0.0.16^0
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd toolbox
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
