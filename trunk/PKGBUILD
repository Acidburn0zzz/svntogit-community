# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.0.1
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gtk2' 'gsettings-qt' 'kwindowsystem' 'libnotify' 'poppler-qt5' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('2134f1fd230be5616690c6c81292f9926462350509812c9ef7eb3152bf84abb108189054eef07adbaa7a1daede2629371a527faeebbf5721d085de249a770acc')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
