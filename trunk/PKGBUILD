# Maintainer: David Runge <dave@sleepmap.de>

pkgname=cage
pkgver=0.1.1
pkgrel=1
pkgdesc="A kiosk compositor for Wayland"
arch=('x86_64')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('wlroots')
makedepends=('meson' 'wayland-protocols' 'xorg-server-xwayland')
optdepends=('xorg-server-xwayland: X11 support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Hjdskes/${pkgname}/archive/v${pkgver}.tar.gz"
        "https://github.com/Hjdskes/cage/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
noextract=()
sha512sums=('f071f04f1ff7d2b89ae72238ef1a28fd3fbc389803e4a4fe0c3d938c7a2a1cc962b63929d081d975f98131ff6dd9637762e8036bb320277e2580f0cbeb0c6201'
            'SKIP')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             -Dxwayland=true \
             build
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
