# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>

pkgname=xdg-desktop-portal-wlr
pkgver=0.2.0
pkgrel=1
pkgdesc='xdg-desktop-portal backend for wlroots'
url="https://github.com/emersion/xdg-desktop-portal-wlr"
arch=('x86_64')
license=('MIT')
provides=('xdg-desktop-portal-impl')
depends=('xdg-desktop-portal' 'pipewire')
makedepends=('meson' 'wayland-protocols' 'wayland')
source=(
  "${pkgname}-${pkgver}::https://github.com/emersion/xdg-desktop-portal-wlr/archive/v${pkgver}.tar.gz"
)
sha512sums=('9b01919b380510389710c005901810ac6fa00299f2b35a37fa949aab361438ab08c30fc95b88e1805dcea7e97ebe2d19111693f82eee545864077347d2659ca9')

build () {
	cd "${pkgname}-${pkgver}"
	arch-meson -Dsd-bus-provider=libsystemd build
	ninja -C build
}

package () {
	cd "${pkgname}-${pkgver}"
	DESTDIR="${pkgdir}" ninja -C build install
	install -Dm644 -t "$pkgdir/usr/share/licenses/${pkgname}" LICENSE
}

