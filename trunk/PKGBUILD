# Maintainer: David Runge <dvzrv@archlinux.org>

_name=mailmanclient
pkgname=python-mailmanclient
pkgver=3.3.0
pkgrel=1
pkgdesc="Official Python bindings for the GNU Mailman 3 REST API"
arch=('any')
url="https://gitlab.com/mailman/mailmanclient"
license=('LGPL3')
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-vcr' 'python-pytest-services'
'mailman3' 'python-falcon')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('b6498f0f268f139dfa5e5d1b5f2228bb0040a3a8cda08077da8212f7c50cd75e7564dbde2095b697c6fa60912636b7fb7615dc454127d2db3b594af5db21b921'
            'SKIP')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # pytest.ini wants to use pytest-pep8 which is super ancient
  rm -v pytest.ini
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
