# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-devel-patchperl
pkgver=0.66
pkgrel=1
pkgdesc="Patch perl source a la Devel::PPPort's buildperl.pl"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0' 'perl-file-pushd>=1.00')
url=https://metacpan.org/release/Devel-PatchPerl
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Devel-PatchPerl-${pkgver}.tar.gz")
md5sums=(7da4bd1f688e9ea3c5f369c264b16fd4)
sha512sums=(d71a11819495530cc7b3ebdc42ac618172e0b805ecbf63b116b5fbcd702e20207b4326ede92e94bcb982c65f40e53b8f027f2fca6a6338eb86168972c06e0772)
_distdir="${srcdir}/Devel-PatchPerl-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
