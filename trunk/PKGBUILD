# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: coderoar <coderoar@gmail.com>
# Contributor: leemars <leemars@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=ibus-table
pkgver=1.9.25
pkgrel=1
pkgdesc='IM Engine framework for table-based input methods, such as ZhengMa, WuBi, ErBi, CangJie and so on'
arch=('any')
url="https://github.com/kaio/ibus-table"
license=('LGPL')
makedepends=('docbook2x' 'docbook-sgml')
depends=('python-pyenchant' 'ibus' 'aspell-en')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaio/ibus-table/archive/$pkgver.tar.gz"
        sgml_fix.patch)
sha512sums=('8a7af5242d6f5b55aa901eac41cb7d30a357ef464fdb6e8bd96ddb5babdf8bd014c33165bf466768d86f2de3e4c79e56328b50e7bcfa777a4112ef7a5b5d8756'
            '930cd45e9687295159c44d5d4133e99a2259fb8f2ed51675b55b18ca59acb410144689a2d51b052b7bbcbe061f8135ebd511898658d1c71e7004303df969c880')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 -i ../sgml_fix.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/ibus

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}
