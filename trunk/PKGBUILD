# Maintainer: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.10.1
pkgrel=1
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-psutil' 'python-pydantic' 'python-typeguard')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('01927d80f6ba961ab0f745be3c62d67683603203c7b94429c2420869d8d90c2b4aa8e02cf2e54a04feb26e4144f2423882086f6007afc4648decc39f0814ae13')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
