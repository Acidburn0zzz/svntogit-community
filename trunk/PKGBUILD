# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.1.1
pkgrel=1
pkgdesc="CSS parser and selection engine"
arch=('i686' 'x86_64')
url="http://www.netsurf-browser.org/projects/libcss/"
license=('MIT')
depends=('libparserutils' 'libwapcaplet')
source=("http://www.netsurf-browser.org/projects/releases/libcss-$pkgver-src.tar.gz")
sha256sums=('383c424c1f7134be67e22a7a633e31d47ba6b87aba835a4e73eca893aa01c0a0')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  export CFLAGS="-Wno-error=unused-but-set-variable"
  make PREFIX=/usr COMPONENT_TYPE="lib-shared"
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
