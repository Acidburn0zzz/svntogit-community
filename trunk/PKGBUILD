# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-image
pkgver=0.0.20190312
_commit=3fc05d484e9f77dd51816890e05f2602e4ca4d65
pkgrel=1
pkgdesc='Go supplementary image libraries'
arch=('any')
url="https://github.com/golang/image"
license=('BSD')
depends=('go-pie' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/image/archive/$_commit.tar.gz")
sha512sums=('8c71bb3682fabdd99a974500d27df2de8aa747cc04f2b5d5e293725c89046ff8f95be09800d92d014acbba57e560845e937561273e28f94ebc1a5074dadc3293')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$srcdir"/build/src/golang.org/x/image
  cd "$srcdir"/build/src/golang.org/x/image
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/image

  install -Dm644 image-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
