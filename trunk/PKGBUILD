# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.8.11
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('0c073c7b88e548543718812c731196e59bda6bc8bbb5c5a554b447de96d5e0d528f2745e186963e34a09209e9e7cfa222f239fdace805e331a5b2a70b866ba04')

prepare() {
  cp -a construct-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/construct-$pkgver
  python setup.py build

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct-$pkgver
  python setup.py pytest

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py pytest
}

package_python-construct() {
  depends=('python')

  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')

  cd construct-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
