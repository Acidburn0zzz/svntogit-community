# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tomas A. Schertel <tschertel@gmail.com>

pkgname=cherrytree
pkgver=0.38.7
pkgrel=2
pkgdesc='Hierarchical note taking application featuring rich text and syntax highlighting'
arch=('any')
url='https://www.giuspen.com/cherrytree/'
license=('GPL3')
depends=('pygtksourceview2' 'python2-dbus')
optdepends=('python2-pyenchant: for spell checking support'
            'p7zip: for password protection support')
source=(https://www.giuspen.com/software/$pkgname-$pkgver.tar.xz
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('22a195b86644ed74e49cf11c4b9433cfd9dfa1fa99d698527b45c291d2df47d3'
            'b380caff7f589c69f56ac72ff82866fd18a7eea0d53cca8bb2327612f4a21886')

prepare() {
  cd $pkgname-$pkgver

  # Fix AppStream metadata validation
  # https://github.com/giuspen/cherrytree/pull/440
  patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch

  # Don't use legacy path for AppStream metainfo file
  # https://github.com/giuspen/cherrytree/pull/439
  sed -i 's|share/appdata|share/metainfo|' setup.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
