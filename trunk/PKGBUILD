# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-persistent
pkgver=4.5.1
pkgrel=1
pkgdesc="Translucent persistent objects"
arch=('x86_64')
license=('ZPL')
url="http://www.zope.org/Products/ZODB"
depends=('python-zope-interface')
makedepends=('python-setuptools' 'python-cffi')
checkdepends=('python-zope-testrunner' 'python-manuel')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/persistent/archive/$pkgver.tar.gz")
sha512sums=('411efe082c263e3997616edc8a3d35d84b2955f8590e3a9dc6fcb37673b3729f85c53d4e41bfd0cdd9c5fa7f43964ad49fa020b04c5c08bead32077da59fd356')

build() {
  cd persistent-$pkgver
  python setup.py build
}

check() {
  cd persistent-$pkgver
  python -m venv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py develop
    python -m zope.testrunner --test-path=.
  )
}

package() {
  cd persistent-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
