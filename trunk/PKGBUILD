# Maintainer: David Runge <dave@sleepmap.de>

pkgname=autorandr
pkgver=1.7
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python-setuptools' 'xorg-xrandr')
optdepends=('xorg-xdpyinfo: For detecting the primary XRandR output')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('065e1b6ae22a6ce11c3d7603c38f2bd97f6c0f1a8fd96d7b180e99a5188cf17b7fc44a2970fbe9f50561d4c896b9a12cb9f7e49482c6ef8748ea9e99247ec090')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
