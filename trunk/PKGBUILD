# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=kmplayer
pkgver=0.12.0.a
_commit=e2681f00
pkgrel=1
pkgdesc="Video player plugin for Konqueror and basic MPlayer/Xine/ffmpeg/ffserver/VDR frontend for KDE"
arch=(i686 x86_64)
license=(GPL LGPL FDL)
url="http://kmplayer.kde.org"
depends=(kmediaplayer kdelibs4support hicolor-icon-theme)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools python)
optdepends=('mplayer: mplayer backend')
source=($pkgname-$pkgver.tar.gz::"http://quickgit.kde.org/?p=kmplayer.git&a=snapshot&h=$_commit&fmt=tgz")
md5sums=('d1e42186462d7887affe2cec8901082e')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname \
	-DCMAKE_BUILD_TYPE=Release \
	-DCMAKE_INSTALL_PREFIX=/usr \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}

