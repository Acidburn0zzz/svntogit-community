# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=jnoisemeter
pkgver=0.1.0
pkgrel=2
pkgdesc="Measure audio test signals and in particular noise signals"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2')
groups=('pro-audio')
depends=('clthreads' 'clxclient' 'jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
sha512sums=('744f69007aa7d5a5410e1c73fb5894cf9639c2958d6434f96eb5ec3ee191487ed919a030ba724e9a86d60fd15ce0c75c8ef1787e5eceec90d4f359c067a66781')

prepare() {
  cd "$pkgname-$pkgver"
  # remove optimization
  sed -e '/march=native/d' \
      -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver/source"
  make LIBDIR=lib PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver/source"
  make PREFIX=/usr DESTDIR="$pkgdir/" install

  # doc
  install -vDm 644 ../{AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

