# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: DonVla <donvla@users.sourceforge.net>

pkgname=caps
pkgver=0.9.25
pkgrel=1
pkgdesc="The LADSPA C* Audio Plugin Suite"
arch=('x86_64')
url="http://quitte.de/dsp/caps.html"
license=('GPL3')
groups=('ladspa-plugins' 'pro-audio')
depends=('gcc-libs')
makedepends=('ladspa' 'python')
source=("http://quitte.de/dsp/${pkgname}_${pkgver}.tar.bz2")
sha512sums=('8b34fe7aa86495ce1443edd818defa13e34c373d704970158c85132753a1ef5d961ac36354b5ec5eae898ccf4f066b6848c1c4245486302d4984b6909764cbd8')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure.py
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX="$pkgdir/usr" install
  install -vDm 644 {CHANGES,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
