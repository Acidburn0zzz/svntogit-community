# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=0.9.6
pkgrel=1
pkgdesc="cURL for humans"
url="https://github.com/jkbr/httpie"
depends=('python-requests' 'python-pygments')
makedepends=('python-setuptools')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD')
arch=('any')
source=("https://github.com/jkbrzt/httpie/archive/$pkgver.tar.gz")
md5sums=('f7dd071fa00fbbf003bfa8ca83233e69')

build() {
  cd httpie-$pkgver
  python3 setup.py build
}

package() {
  cd httpie-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python3 setup.py install --root="$pkgdir" --optimize=1
}
