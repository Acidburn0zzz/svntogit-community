# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sagemath-doc
pkgname=(sagemath-doc sagemath-src)
pkgver=7.5
pkgrel=2
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
makedepends=(sagemath sage-notebook python2-pyzmq python2-pillow python2-docutils
             python2-jupyter_client python2-pkgconfig cython2 thebe) # python2-sphinx
makedepends+=(python2-imagesize python2-sphinx-alabaster-theme) # remove when we can use system sphinx again
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        "https://pypi.io/packages/source/S/Sphinx/Sphinx-1.4.9.tar.gz"
	package.patch disable-fes.patch docbuild_main.patch is-package-installed.patch ecm-7.patch)
md5sums=('bff1fb8793ebc2934d14cf1efdcfbea3'
         '6c7472d3630835113ee9cc251cc36c31'
         'ccfd5b1bc4796f414f1531be52504dd7'
         '06a67e611d10aa2381efe1f08ea4ffd9'
         'f3bf3fa4e11e5af2bd0573cf2620e1f8'
         '409b0a2c520eb33281b5f262afcb6c76'
         '0c9a57d35de80c2cd418ebec912efbbb')

prepare() {
  cd sage-$pkgver

# Arch-specific patches
# assume all optional packages are installed
  patch -p0 -i ../package.patch
# fix regressions with ECM 7
  patch -p1 -i ../ecm-7.patch

# Upstream patches
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
#  patch -p0 -i ../fes02.patch
# disable fes module, fails to build
  patch -p0 -i ../disable-fes.patch
# replace is_package_installed usage http://trac.sagemath.org/ticket/20377
  patch -p1 -i ../is-package-installed.patch

# fix relative imports
  patch -p0 -i ../docbuild_main.patch
# fix thebe.js link
  rm src/doc/common/themes/sage/static/thebe.js
  ln -s /usr/share/thebe/thebe.js src/doc/common/themes/sage/static/thebe.js

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py

  mkdir -p local-python
  ln -sr src/sage_setup local-python
  cp -r "$srcdir"/Sphinx-1.4.9/* local-python
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages:$srcdir/sage-$pkgver/local-python"
  python2 sage_setup/docbuild --no-pdf-links -k all html
}

package_sagemath-doc() {
  pkgdesc="HTML documentation and inline help for SageMath"
  depends=(sage-notebook python2-sphinx gap-doc thebe)
  conflicts=(sage-mathematics-doc)
  replaces=(sage-mathematics-doc)

  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage
}

package_sagemath-src() {
  pkgdesc="Source files for SageMath"
  conflicts=(sage-mathematics-src)
  replaces=(sage-mathematics-src)
  
  mkdir -p "$pkgdir"/usr/share/sage/source
  cp -r sage-$pkgver/src/{sage,sage_setup} "$pkgdir"/usr/share/sage/source
}
