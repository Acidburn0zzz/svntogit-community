# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Mellite
_app_name=de.sciss.mellite.Mellite
pkgname=mellite
pkgver=3.10.0
pkgrel=1
pkgdesc="An environment for creating experimental computer-based music and sound art"
arch=('any')
url="https://www.sciss.de/mellite/"
license=('AGPL3')
groups=('pro-audio')
depends=('java-environment>=16' 'supercollider')
makedepends=('gendesk' 'sbt')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Sciss/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('f11d06830f29382390101fda053988cb49e60dcd89d514d34dd9c791e654e98b580769290421a436ab655e36f665dc0ba9c2cf5f8271283c9b6de0a74ec1cb80'
            '1a6a572b4c365cdad87930bef805afa229b329cb2ceefb6b9ce42b233ab86f768ae3e3d5fd7cac1129786878c38fd891015aaf6637d9bd7b5ce3b2ef5fe9d30b')
b2sums=('17ee4e606736837bffc307540101dd9ad862c0d9f8538540ee03f877886807d29b322fdde6ecfb2570d09a81aee308ebc3f4b342faf4fb79d882811a12202844'
        '4f1fd83d7cf5561e76f746860e5436e7dfab9bd50ad103f499b82de99a67034122f0e8e99e6a58574d6611585d63b2976e5d31979c7233294464683c86cce3ea')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  gendesk -n \
          --pkgname "${_app_name}" \
          --name "Mellite" \
          --pkgdesc "${meter_types[$type]}" \
          --exec "${pkgname}" \
          --icon "${_app_name}" \
          --genericname "Audio Programming" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  sbt +mellite-app/test +mellite-app/assembly
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 "app/${_name}.jar" -t "${pkgdir}/usr/share/scala/${pkgname}"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  install -vDm 644 icons/application.png "${pkgdir}/usr/share/icons/hicolor/256x256/de.sciss.mellite.Mellite.png"
  install -vDm 644 icons/application.svg "${pkgdir}/usr/share/icons/hicolor/scalable/de.sciss.mellite.Mellite.svg"
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications"
}
