# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=yapf
pkgver=0.27.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
depends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('d161c232d485ce7a6173fc6d796ed9bd880736d365d61f27fd608b590dddfc3fa8fd60fa88d8ebee8e63db616761ede6f1dfa74579c8ec9df5d3c81b50dc2ade')

build() {
  cd yapf-$pkgver
  python setup.py build
}

check() {
  cd yapf-$pkgver
  nosetests3 || warning "https://github.com/google/yapf/issues/586"
}

package() {
  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
