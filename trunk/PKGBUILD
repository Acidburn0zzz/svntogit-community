# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Aaron Bull Schaefer <aaron@elasticdog.com>
# Contributor: Ron Huang <ronhuang+aur at gmail dot com>

pkgname=fcgiwrap
pkgver=1.0.3
_gitrev='b9f03e6'
pkgrel=4
pkgdesc='A simple server for running CGI applications over FastCGI.'
arch=('i686' 'x86_64')
url='http://nginx.localdomain.pl/wiki/FcgiWrap'
license=('MIT')
depends=('fcgi' 'systemd')
backup=('etc/conf.d/fcgiwrap')
source=("ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${_gitrev}.tar.gz"
        'fcgiwrap.conf.d'
        'fcgiwrap.rc.d'
        'LICENSE')
md5sums=('96d5f5a82b96f6eb7b4117fbab5e1e58'
         '9fbb5b0e861ffea74e655143a4c1a8f9'
         '71ea12f43aebca39f73ff2e0b684f361'
         '5aee62c27b4308f25ab32f05da387366')

build() {
  cd "${srcdir}/${pkgname}"

  autoreconf --install
  ./configure --prefix=/usr --mandir=/share/man
  make
}

package() {
  cd "${srcdir}/${pkgname}"

  make DESTDIR="${pkgdir}" install
  install -Dm0644 ../fcgiwrap.conf.d "${pkgdir}/etc/conf.d/${pkgname}"
  install -Dm0755 ../fcgiwrap.rc.d "${pkgdir}/etc/rc.d/${pkgname}"
  install -Dm0644 ../LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
