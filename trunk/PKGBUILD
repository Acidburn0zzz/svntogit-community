# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-power-manager
pkgver=2.0.0
pkgrel=1
pkgdesc="Power management tool for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-power-manager"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libgnome-keyring' 'libnotify' 'upower')
makedepends=('mate-common' 'yelp-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-power-manager/archive/v$pkgver.tar.gz")
sha512sums=('b5b6ce28cdb6542298532403bd2966ce4152ac317ef4471e815fc7c770728b1b482ccccd24c67e9f81f5e8de3ae9e12da6ff729adf2757399a5c960bd8f2126c')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
