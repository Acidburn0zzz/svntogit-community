# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=('loki' 'loki-canary' 'promtail' 'logcli')
pkgver=2.2.1
pkgrel=3
pkgdesc='like Prometheus, but for logs'
url='https://github.com/grafana/loki'
arch=('x86_64')
license=('Apache')
depends=('glibc')
makedepends=('go' 'systemd')
source=($pkgname-$pkgver.tar.gz::https://github.com/grafana/loki/archive/v$pkgver.tar.gz
        https://github.com/grafana/loki/commit/d6ecfd24313ecc9ad03e26875cb11394de7e988a.patch # Patch for Go 1.16; issue #3485
        promtail.sysusers promtail.service promtail.tmpfiles
        loki.sysusers loki.service loki.tmpfiles)
sha512sums=('8777f66dda3893c52ad41244ca4c96950e2fe15c05b08e3f07f848b3672cbdda701958cf98781e9f6c848f4711df6f5d2fcbb5c31f7de2535faa367b3eceb1db'
            '2b17d1146c07945d715da0bee8eb36490461e93c54931a6c1f0420a8b03e60b7fa6fd698f60f41951436bebc8fbc1ad4ffa58141081047b7da4b3479d8216dfc'
            '2b6c44b18ea3c9f955a7450222180d0b20b5fc551d0b7e5d0d8949e40adc847c4166829146260f87a75732cc5473eab0347dd56fc2125517698bac0652738c74'
            '7ff5c7324a4c883f5156b55cd025d2df8d8be134c18b1ecc007f89cafe20b4bed6c612b81588c9c660dd2fd3b6091fd952fcd3ad69a44e1ffa02e1eaeb05b290'
            '598042c40673a7914c5a1eeccfb78f832379a61f4360212c5d86f667343cf2fc78e98d9025f9717ea64f3e16e0a28f08cd7709706d811656722019f6167dd788'
            'f00b3cb64b71d3ca5a422a2bdff1f81a3e2707c4f73bcb27e24338eabff039b96125c847aaae43c9b22ccc47f89585118c1d0e0b29c4eb7b6f9260c68f8a8324'
            '03904b67617e4064a1eb39b6edfaec5d55bc85f142e867d115d3d0e12494a052fddf13bb6f5c541aaca29a18582df2332bb0cc6b8f29928cf6a0d5d2710ead74'
            'de40d1e6752edbf8c21317ce1ee10f98dfc869e569c07092c613f7144e261e9438683a145a2e0e37e2a9fc758c5c2f02e1d0ac1c60347e98b147e4a550ec6040')

prepare() {
  cd loki-$pkgver

  patch -p1 < "${srcdir}/d6ecfd24313ecc9ad03e26875cb11394de7e988a.patch"
}

build() {
  cd loki-$pkgver

  for cmd in loki loki-canary promtail logcli; do
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags " \
         -X github.com/grafana/loki/pkg/build.Version=$pkgver
         -X github.com/grafana/loki/pkg/build.BuildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH})
         -linkmode external
         -extldflags \"${LDFLAGS}\"" \
      ./cmd/"$cmd"
  done
}

check() {
  cd loki-$pkgver

  go test -v ./...
}

package_loki() {
  backup=('etc/loki/loki.yaml')

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki

  install -Dm644 cmd/loki/loki-local-config.yaml $pkgdir/etc/loki/loki.yaml

  install -Dm644 ${srcdir}/loki.sysusers "$pkgdir/usr/lib/sysusers.d/loki.conf"
  install -Dm644 ${srcdir}/loki.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/loki.conf"
  install -Dm644 ${srcdir}/loki.service "$pkgdir/usr/lib/systemd/system/loki.service"
}

package_loki-canary() {
  pkgdesc="A standalone app that audits the log capturing performance of Loki"

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin loki-canary
}

package_promtail() {
  pkgdesc="An agent which ships the contents of local logs to a private Loki instance or Grafana Cloud"
  backup=('etc/loki/promtail.yaml')

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin promtail

  install -Dm644 cmd/promtail/promtail-local-config.yaml $pkgdir/etc/loki/promtail.yaml

  install -Dm644 ${srcdir}/promtail.sysusers "$pkgdir/usr/lib/sysusers.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/promtail.conf"
  install -Dm644 ${srcdir}/promtail.service "$pkgdir/usr/lib/systemd/system/promtail.service"
}

package_logcli() {
  pkgdesc="A command-line for loki"

  cd loki-$pkgver

  install -Dm755 -t "$pkgdir"/usr/bin logcli
}
