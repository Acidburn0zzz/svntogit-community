# $Id$
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=1.6.7
pkgrel=5
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('caribou' 'gjs' 'gnome-bluetooth' 'gnome-control-center' 'gnome-panel' 'gnome-session'
         'gnome-settings-daemon' 'gnome-themes-standard' 'gstreamer0.10' 'libgnomekbd' 'muffin'
         'notification-daemon' 'python2-gconf' 'python2-imaging' 'python2-lxml' 'python2-dbus' 'gksu')
makedepends=('gnome-common' 'intltool')
optdepends=('nautilus: the official Gnome file manager')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/$pkgver"
        "cinnamon-screensaver.desktop"
        "cinnamon2d-screensaver.desktop"
        "fix-cursor-change.patch"
        "gnome-autogen.sh")

build() {
  cd ${srcdir}/linuxmint-Cinnamon*

  patch -Np1 -i ${srcdir}/fix-cursor-change.patch

  cp ${srcdir}/gnome-autogen.sh .
  sed -i 's/\ --warn-all\ --warn-error//' src/Makefile.am
  sed -i 's/gnome-autogen.sh/.\/gnome-autogen.sh/g' autogen.sh
  chmod +x gnome-autogen.sh

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
 
  make
}

package() {
  cd ${srcdir}/linuxmint-Cinnamon*
  make DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i 's/bin\/python/bin\/python2/g' \
    "${pkgdir}/usr/bin/$pkgname-extension-tool" \
    "${pkgdir}/usr/bin/$pkgname-launcher" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/share/$pkgname/applets/panel-launchers@$pkgname.org/$pkgname-add-panel-launcher.py"
    
  sed -i 's/env python/env python2/g' \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py"

  # Screensaver gnome 3.6 fix
  mkdir -p $pkgdir/etc/xdg/autostart
  cp $srcdir/cinnamon*-screensaver.desktop $pkgdir/etc/xdg/autostart
}

md5sums=('ea73c6367d43d82c61f977477bf0c08c'
         'd55c079cdc8bec606f510f87b2a6f776'
         '5c6af851ab1cf7e9219f11d86353196a'
         '8d7a93a1eb0f1d44e4869abd342ed22a'
         'a925691c9b57a6a884dcf07da057fd1f')
