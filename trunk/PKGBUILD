# $Id: PKGBUILD 149613 2012-02-08 20:20:01Z pierre $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=krusader
pkgver=2.4.0beta1
_pkgver=2.4.0-beta1
pkgrel=6
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
arch=('i686' 'x86_64')
url="http://krusader.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'kdebase-lib' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'kdeutils-ark')
optdepends=("kdebase-keditbookmarks: to edit bookmarks"
            "kdeutils-ark: extract archive with right-click")
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$_pkgver.tar.bz2" \
        krusader-2.4.0-beta1-fix-for-g++47.patch)
md5sums=('d2340718d05d69e7f2a7f0846471ee12'
         '1b2ea9a70321ff82a35cf078304fdef2')

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  # fix compilation with gcc 4.7 (patch from Fedora)
  patch -Np1 < "$srcdir/krusader-2.4.0-beta1-fix-for-g++47.patch"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
}
