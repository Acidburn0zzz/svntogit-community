# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.3.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python-zipp')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ad5a0d5b72e8e49a158254d7146411a86bacda3d9019867bad92e8209dd924ae0928dfe46b574967975622d0809d4e1693dab4b6bf8d78867b09cac8c424072c')
b2sums=('876d9bdc1360eecc973f53d6d3397db5708dda1b0f30e5a10d372535f0831cc0fcdcc0b361412df8a1f4453f27315e6547096e14ec7bdbcd1d1af41778d5dd39')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
