# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Oliver Sherouse <oliver DOT sherouse AT gmail DOT com>

pkgname=python-seaborn
pkgver=0.10.1
pkgrel=1
pkgdesc="Statistical data visualization"
arch=('any')
url="https://seaborn.pydata.org/"
license=('BSD')
depends=('python-pandas' 'python-matplotlib' 'python-scipy')
makedepends=('python-setuptools')
optdepends=('python-statsmodels: for some advanced statistical plots'
            'python-beautifulsoup4: for getting dataset names')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mwaskom/seaborn/archive/v${pkgver}.tar.gz")
sha256sums=('25bb4cd6d7d8cd2869354f6d61ce4b64cb60b10a84830ad0565cb35d87a537f0')

build() {
  cd "$srcdir"/seaborn-${pkgver}
  python setup.py build
}

package() {
  cd "$srcdir"/seaborn-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
