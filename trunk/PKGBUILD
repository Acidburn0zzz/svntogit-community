# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
pkgver=10.5.2+24+g7a5dcfda
pkgrel=1
pkgdesc='Modern desktop environment from the Solus Project'
arch=('x86_64')
url='https://github.com/solus-project/budgie-desktop'
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-extra' 'graphene' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('budgie-desktop-view: Desktop icons'
            'gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
_commit=7a5dcfdaa25e887432e7edb3f8aee060992ee1c1  # master
source=("git+https://github.com/solus-project/budgie-desktop.git#commit=$_commit"
        'git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git'
        'git+https://github.com/getsolus/budgie-translations.git'
        '0001-Add-launcher-for-gnome-screensaver.patch'
        '0001-Override-settings-only-for-Budgie.patch')
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '9d33293b7f98eaecff87f52e772502938cf9bb945ab7a375623e429cc92e6ec9'
            '0708ac986f378d54fb6f171abb7511e38c3f68abc3a65b1e5bd1122137c92931')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Add launcher for gnome-screensaver
  patch -Np1 -i ../0001-Add-launcher-for-gnome-screensaver.patch

  # Override settings only for Budgie
  patch -Np1 -i ../0001-Override-settings-only-for-Budgie.patch

  git submodule init
  git submodule set-url subprojects/gvc "$srcdir/libgnome-volume-control"
  git submodule set-url subprojects/translations "$srcdir/budgie-translations"
  git submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
