# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-brotli
epoch=1
pkgver=0.1.2
pkgrel=6

_modname="ngx_${pkgname#nginx-mod-}"

pkgdesc="Brotli compression filter module for nginx"
arch=('x86_64')
depends=("nginx" 'brotli')
makedepends=("nginx-src")
url="https://github.com/eustas/ngx_brotli"
license=('CUSTOM')

source=(
	https://github.com/eustas/$_modname/archive/v$pkgver/$_modname-$pkgver.tar.gz
)
validpgpkeys=(B0F4253373F8F6F510D42178520A9993A1C052F8) # Maxim Dounin <mdounin@mdounin.ru>
sha256sums=('309af9e96c10e80f1884acea96379980979581adc287ce338f084607bd48c185')

prepare() {
  cp -r /usr/src/nginx .

	cd "$srcdir"/$_modname-$pkgver
	sed 's@/usr/local@/usr@' -i config
}

build() {
	cd "$srcdir"/nginx
	./configure --with-compat --add-dynamic-module=../$_modname-$pkgver
	make modules
}

package() {
	install -Dm644 "$srcdir"/$_modname-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx/objs
	for mod in ngx_*.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
