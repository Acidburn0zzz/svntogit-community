# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=insert-ordered-containers
pkgname=haskell-insert-ordered-containers
pkgver=0.2.1.0
pkgrel=22
pkgdesc="Associative containers retating insertion order for traversals."
url="https://github.com/phadej/insert-ordered-containers"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc' 'haskell-aeson' 'haskell-base-compat' 'haskell-hashable' 'haskell-lens'
         'haskell-semigroupoids' 'haskell-semigroups' 'haskell-text' 'haskell-unordered-containers')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('baff8b23700a7d0209810208183d9856c0a5219018c5f09e82f8820ac87630073543da19e1198fc42f3feec143895707e5e33fdf1edbf8f4e6c441271cc3243b')

prepare() {
    sed -i 's/, semigroupoids         >=4.3      && <5.2/, semigroupoids         >=4.3      \&\& <5.3/' ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
