# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=0.7.5
pkgrel=1
arch=('i686' 'x86_64')
url="http://ettercap.sourceforge.net/"
license=('GPL')
makedepends=('ghostscript' 'cmake' 'libpcap' 'libltdl' 'libnet' 'gtk2')
options=('!libtool' '!makeflags' '!emptydirs')
source=("http://sourceforge.net/projects/ettercap/files/$pkgbase/$pkgver-Assimilation/$pkgbase-$pkgver.tar.gz")
md5sums=('ab2354bf9c91195f06ad0b5d058565a4')

build() {
  cd "${srcdir}"
#sed -i 's/su-to-root -c/gksu/' ${pkgbase}-${pkgver}/desktop/ettercap.desktop

  cp -r ${pkgbase}-${pkgver} ${pkgbase}-${pkgver}-gtk

  cd ${pkgbase}-${pkgver}
  mkdir build
  cd build
  cmake -D ENABLE_GTK=OFF -D CMAKE_INSTALL_PREFIX=/usr -D INSTALL_PREFX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make

  cd "$srcdir"/${pkgbase}-${pkgver}-gtk
  mkdir build
  cd build
  cmake  -D INSTALL_PREFX=/usr -D CMAKE_BUILD_TYPE=RELEASE ../
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  depends=('openssl' 'libpcap' 'pcre' 'libltdl' 'libnet')
  backup=('etc/etter.conf')

  cd "${srcdir}"/${pkgbase}-${pkgver}/build
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK"
  depends=('ettercap' 'gtk2')

  cd "${srcdir}"/${pkgbase}-${pkgver}-gtk/build
  install -Dm755 src/ettercap "${pkgdir}"/usr/bin/ettercap-gtk
}
md5sums=('037b28cd94efa086a795060fc0f76080')
