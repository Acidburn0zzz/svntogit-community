# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fluxctl
_pkgname=flux
pkgver=1.23.1
pkgrel=1
pkgdesc="Controller for the GitOps Kubernetes operator flux"
arch=('x86_64')
url="https://github.com/fluxcd/flux"
license=('Apache')
makedepends=('go' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/fluxcd/flux/archive/${pkgver}.tar.gz")
sha512sums=('43c7161d9e96ab9ac311f966fcd6a6ee9b9c391fc04b028584ca36aeddc93bc16b274b4d2c069e98a5e69f45a4a477d87233b49d0cd243a56c55f6c944c72e79')
b2sums=('80fd5f85a174fd0ba3817f184876ddac82c59fb752f51b317849bb048f23882e9c08ce6b657aa0daad0abf1c9db69bce9834845d3f9caccfbd4213999fcc6219')

build() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build -ldflags "-w -X main.version="${pkgver}" -linkmode=external" .
  chmod +x ./fluxctl
}

check() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  go test ./...
}

package() {
  cd "${_pkgname}-${pkgver}/cmd/fluxctl"
  install -Dsm755 ./fluxctl "${pkgdir}/usr/bin/fluxctl"
}
