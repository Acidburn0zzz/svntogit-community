# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=(python-paramiko python2-paramiko)
pkgver=2.2.1
pkgrel=1
pkgdesc="Python module that implements the SSH2 protocol"
url="https://github.com/paramiko/paramiko/"
license=('LGPL')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bcrypt' 'python2-bcrypt'
             'python-cryptography' 'python2-cryptography' 'python-pyasn1' 'python2-pyasn1'
             'python-pynacl' 'python2-pynacl')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/paramiko/paramiko/archive/$pkgver.tar.gz")
sha512sums=('e51b49596f1ef38aa514bb551a7a35d1492d9aa46bce061086561cbe6e50c73dfd424ec995a9dd12885550c5d381575845acd7a19f83a55121fc018efa90aac9')

prepare() {
  cp -a paramiko-$pkgver{,-py2}

  find paramiko-$pkgver-py2 -name '*.py' -print0 | xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
        -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}

check() {
  cd "$srcdir"/paramiko-$pkgver
  LANG=en_US.UTF-8 python test.py

  cd "$srcdir"/paramiko-$pkgver-py2
  LANG=en_US.UTF-8 python2 test.py
}

package_python-paramiko() {
  depends=('python-bcrypt' 'python-cryptography' 'python-pyasn1' 'python-pynacl')

  cd paramiko-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}

package_python2-paramiko() {
  depends=('python2-bcrypt' 'python2-cryptography' 'python2-pyasn1' 'python2-pynacl')

  cd paramiko-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -dm755 "$pkgdir"/usr/share/doc/$pkgname/demos
  install -m644 demos/* "$pkgdir"/usr/share/doc/$pkgname/demos
  chmod 755 "$pkgdir"/usr/share/doc/$pkgname/demos/*.py
}
