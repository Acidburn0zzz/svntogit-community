# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSlizr
pkgname=bslizr
pkgver=1.2.14
pkgrel=1
pkgdesc="LV2 audio effect plugin for sequenced slicing of stereo audio input signals"
arch=('x86_64')
url="https://github.com/sjaehn/bslizr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('080a340730638df98c8e9b4686940cc0c051ea90979290b74e98279b0b3be267e7c457be32d79ac1098e31d78332917118216bccc9ed0e10941f5cd16b3a4a41')
b2sums=('84f8aaaeab4475106601bebd343dc731ec8367042e55727e35ded1e57208b3ecbc35015ba706f937017117161c7fc72b08ff178eda664fcc97440700b5c0853a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "https://www.jahnichen.de/plugins/lv2/BSlizr"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
