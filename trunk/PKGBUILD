# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.2.2.8
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz"
        dtkwidget-1882.patch::https://github.com/linuxdeepin/dtkwidget/commit/a2ffddc1cc18d6fd5db567d9dcb5e3185f0e351d.patch)
sha512sums=('235b381d4dad3dc882ace3fb085743b12b9876884c8431caadf67a9125d90d166b7e4bd8be88c2fd90ae97c0ce34c6d6ac0596065dd5b09eefadfeda0f9a730f'
            'cf6853b0e8f0d19509b79f2ddfb60de04b16e380d44bd72fdc3184535ab82701129aa5de76f21d8f2f11c55136651192dc9c3f59baa6ba123f058c39a1d97ea3')

prepare() {
  cd dtkwidget-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/1882
  patch -p1 -i ../dtkwidget-1882.patch
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
