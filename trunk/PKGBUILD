# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Paul Mattal <paul@mattal.com>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: GARETTE Emmanuel <gnunux at laposte dot net>

# --->>> remember to rebuild/bump pigeonhole in one step <<<---

pkgname=dovecot
pkgver=2.2.27
pkgrel=1
pkgdesc="An IMAP and POP3 server written with security primarily in mind"
arch=('i686' 'x86_64')
url="http://dovecot.org/"
license=("LGPL")
depends=('krb5' 'openssl' 'sqlite' 'libmariadbclient'
         'postgresql-libs' 'bzip2' 'lz4' 'expat' 'curl' 'pam')
makedepends=('libcap' 'libldap' 'clucene')
optdepends=('libldap: ldap plugin'
            'xz: imap zlib  plugin'
            'clucene: alternative FTS indexer')
provides=('imap-server' 'pop3-server')
install=$pkgname.install
source=(http://dovecot.org/releases/2.2/${pkgname}-${pkgver}.tar.gz{,.sig}
        dovecot.tmpfilesd)
sha256sums=('897f92a87cda4b27b243f8149ce0ba7b7e71a2be8fb7994eb0a025e54cde18e9'
            'SKIP'
            'bb7620be7fc0217d77fcac940441c2abbd9efc6ef660153288d09ba412e27e06')
validpgpkeys=('E643F0BDFDCD04D9FFCB6279C948525140558AC9') # Timo Sirainen <tss@iki.fi>

prepare() {
  cd $pkgname-$pkgver
  # fix path in helper script
  sed -i 's:OPENSSLCONFIG=${OPENSSLCONFIG-dovecot-openssl.cnf}:OPENSSLCONFIG=${OPENSSLCONFIG- /etc/ssl/dovecot-openssl.cnf}:' doc/mkcert.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --libexecdir=/usr/lib \
    --with-moduledir=/usr/lib/dovecot/modules \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --disable-static \
    --with-nss \
    --with-pam \
    --with-mysql \
    --with-pgsql \
    --with-sqlite \
    --with-ssl=openssl \
    --with-ssldir=/etc/ssl \
    --with-gssapi \
    --with-ldap=plugin \
    --with-zlib \
    --with-bzlib \
    --with-lzma \
    --with-lz4 \
    --with-libcap \
    --with-solr \
    --with-lucene \
    --with-docs

  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  # system user/group dovenull - 74
  # system user/group dovecot  - 76

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/dovecot.tmpfilesd" \
    "$pkgdir/usr/lib/tmpfiles.d/dovecot.conf"
  install -d -m755 "$pkgdir/etc/dovecot/conf.d"
  rm -f $pkgdir/etc/dovecot/README

  # install mkcert helper script
  install -m 755  doc/mkcert.sh "$pkgdir/usr/lib/dovecot/mkcert.sh"
}
