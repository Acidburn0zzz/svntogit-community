# $Id$
# Maintainer: 
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kaffeine
pkgver=2.0.1
pkgrel=3
pkgdesc='KDE media player'
license=(GPL)
arch=(i686 x86_64)
url="http://kaffeine.kde.org"
depends=(kio hicolor-icon-theme libxss vlc)
makedepends=(extra-cmake-modules kidletime)
source=("http://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.bz2")
md5sums=('8e976eeda060addb09df2431c2301bf2')

prepare() {
  mkdir -p build

# Fix installing translations
  sed -e 's|ecm_optional_add_subdirectory|ki18n_install|' -i $pkgname/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
