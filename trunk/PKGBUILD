# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-recipes
pkgver=1.4.4
pkgrel=1
pkgdesc="Recipe management application for GNOME"
arch=(x86_64 i686)
url="https://wiki.gnome.org/Apps/Recipes"
license=(GPL3)
depends=(gnome-autoar gspell libcanberra libsoup)
makedepends=(git itstool meson)
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"
        add-missing-languages.patch)
sha256sums=('b8e3bcee39b85437a68039ddb35f6f54cfb3ed7ab2a6320bb74dad4846f186e6'
            '36045e0e0d8d554248966544f2651e178ab41df7ef54c4f310905cf336fe4ef4')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np1 -i ../add-missing-languages.patch
}

build() {
  cd build
  meson --prefix=/usr --buildtype=release ../$pkgname-$pkgver
  ninja
}

check() {
  cd build
  # FAIL
  # ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
