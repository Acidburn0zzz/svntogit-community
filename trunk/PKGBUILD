# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Jeffrey Tolar <tolar.jeffrey at gmail dot com>

pkgname=verilator
pkgver=4.210
pkgrel=2
pkgdesc='The fastest free Verilog HDL simulator'
url='https://www.veripool.org/projects/verilator/wiki/Intro'
arch=('x86_64')
license=('LGPL')
depends=('perl')
optdepends=('systemc')
makedepends=('python' 'systemc')
source=("https://www.veripool.org/ftp/verilator-$pkgver.tgz")
sha512sums=('ca16cf2c2f3ce6da5b7dadb47358efea1c0179fdfd8ea021cf1a9ffea85f3d01432582791be60a5425256776c2d9fec9b6e382a1a719b8c63630f07d4ea7afb0')

prepare() {
  cd verilator-$pkgver
  sed -i 's/#_MY_CXX_CHECK_SET(CFG_CXXFLAGS_STD_NEWEST,-std=gnu++17)/_MY_CXX_CHECK_SET(CFG_CXXFLAGS_STD_NEWEST,-std=gnu++17)/' configure.ac
  autoconf
}

build() {
  cd verilator-$pkgver
  ./configure --prefix=/usr
  make
}

check() {
  cd verilator-$pkgver
  make test
}

package() {
  cd verilator-$pkgver
  make install DESTDIR="$pkgdir"
}
