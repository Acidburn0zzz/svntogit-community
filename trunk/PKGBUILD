# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgbase=python-furl
pkgname=('python-furl' 'python2-furl')
pkgver=1.2
pkgrel=2
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
makedepends=('python-orderedmultidict' 'python-setuptools' 'python-six' 'python2-orderedmultidict' 'python2-setuptools' 'python2-six')
checkdepends=('flake8' 'python-pycodestyle' 'python-pyflakes' 'python2-flake8' 'python2-pycodestyle' 'python2-pyflakes')
license=('custom:Unlicense')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('dc14048dda61e7f33466f9547b3185bc482a27ad3ff4701a97364ca5e845e8ea7cca7747588d50478d37acf3b8ef641427957f3a63775bed24ace2de55a1fb65')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py test
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py test
}

package_python-furl() {
  depends=('python-six' 'python-orderedmultidict')
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm644 {API,README}.md
}

package_python2-furl() {
  depends=('python2-six' 'python2-orderedmultidict')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -t "${pkgdir}/usr/share/doc/${pkgname}" -vDm 644 {API,README}.md
}
