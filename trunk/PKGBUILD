# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=singular
_majver=4-2-1
_patchver=2
pkgver=${_majver//-/.}.p${_patchver}
#pkgver=${_majver//-/.}
pkgrel=2
pkgdesc='Computer Algebra System for polynomial computations'
arch=(x86_64)
url='https://www.singular.uni-kl.de/'
license=(GPL)
depends=(flint cddlib)
makedepends=(doxygen)
source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}p${_patchver}.tar.gz
#source=(ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/singular-${_majver//-/.}.tar.gz)
         doc-$pkgver.tbz2::ftp://jim.mathematik.uni-kl.de/pub/Math/Singular/SOURCES/$_majver/doc.tbz2
         https://github.com/Singular/Singular/commit/18bb792e.patch)
sha256sums=('b413d2bce7a887d61fef4b47c78bcaea0e8f3b5c0b1071a3252d63d0adfedce1'
            '0e3860de6d39204cf623fa6486175f560e862002f433a93b9a1bda4acbac1d8e'
            '5430a7062ad361bf79e8e139a930bb8f05b358c8c9e9a371cc8ee3d9ec6b8063')
options=(!zipman)

prepare() {
  patch -d singular-${_majver//-/.} -p1 < 18bb792e.patch # Fix API conflict with Givaro
}

build() {
  cd singular-${_majver//-/.}
  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --docdir=/usr/share/doc \
    --enable-bigintm-module \
    --enable-bigintm-module \
    --enable-Order-module \
    --enable-python-module \
    --enable-gfanlib-module \
    --enable-polymake-module
# https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd singular-${_majver//-/.}
  make check
}
  
package() {
  cd singular-${_majver//-/.}
  make DESTDIR="$pkgdir" install

  install -Dm644 ../singular.idx -t "$pkgdir"/usr/share/singular
}
