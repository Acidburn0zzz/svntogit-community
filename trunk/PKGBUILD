# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=dephell_argparse
pkgname=python-dephell-argparse
pkgver=0.1.2
pkgrel=1
pkgdesc='Argparse with groups, commands, colors, and fuzzy matching'
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('6c33dd01e3fccfe52085d21bfffe61204748e0ec926652adf9f59c3ac6c4d3123ef40b129fae980f3ef75c48311fa2db347c9fdb003c9b5ae295ca9a4e02179e')
b2sums=('860320e63469c4e9317f68403388090d0e58cea608966a03064422c003c01b11cdc86b096b96f7fa9f33629a135962142072e98bb4a870d12fb57fccce4f0f51')

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
