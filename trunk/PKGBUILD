# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.2.0
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'ca-certificates')
makedepends=('cmake' 'boost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json')
sha512sums=('db1f6e84b9ca1f61803d6b6f86b221a573a9f0bdf2a485a2ae539da9f3481414e66c6e85805d6b860a4688326ba0b13b1f0fcb6cd7e6af5f20641c1e13346f1e')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}
package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
