# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=6.14.0
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz")
sha512sums=('8b395595bef81f29f4642afcd68b52c2ee77cb1a0fbf7368d3c561f936cd1f6d4adf287a29f8538ef16d685209beae2fc94c972824f2113d35e76ad4cbb734b3')

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
