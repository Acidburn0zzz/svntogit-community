# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>
 
pkgname=megaglest
pkgver=3.3.7
pkgrel=1
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('GPL2')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'mesa' 'lua')
makedepends=('ftjam' 'p7zip' 'wxgtk')
source=("http://downloads.sourceforge.net/project/${pkgname}/current_release/${pkgname}-source-${pkgver}.tar.bz2"
        "http://downloads.sourceforge.net/project/${pkgname}/current_release/megaglest-data-${pkgver}.7z"
        "megaglest.png"
        "megaglest.desktop"
        "megaglest.sh")
md5sums=('2a0610289b2103be9187a570a4395ef3'
         '19c288096a68348499f2cfbc41fa1aba'
         '5622ce716033d9afb62a3291b47844ed'
         '97dc7ea865efbea5daaf300207dad518'
         'e3d6abbe79263d2fa5edb0bcbcf8755a')

build() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  7z x ${srcdir}/megaglest-data-${pkgver}.7z

  # Initial cleanup.
  find . -name "*~" -exec rm -f {} \;
  
  # Fix megaglest.ini file for 800x600 resolution. May cause problems for smaller
  # screens. User can change settings within game after installation.
  sed -i \
  -e '/^ScreenHeight/s|=.*|=600|' \
  -e '/^ScreenWidth/s|=.*|=800|' glest.ini

  # Build megaglest.
  ./configure --prefix=/usr

  # wxgtk is only needed to build the application for optional menu editor.
  # We only care about the game! :)
  WX_AVAILABLE=no \
  jam -q -j10
}

package() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  # Megaglest doesn't have a nice installer. Let's package it ourselves. 
  # Damnit, upstream.
  install -d -m 0755 "${pkgdir}"/usr/{bin,share/${pkgname}}
  install -m 0755 "${srcdir}"/$pkgname.sh "${pkgdir}"/usr/bin/$pkgname
  install -m 0755 glest.bin "${pkgdir}"/usr/share/${pkgname}/$pkgname
  install -m 0644 glest.ini glestkeys.ini servers.ini "${pkgdir}"/usr/share/$pkgname

  install -D -m 0644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -D -m 0644 "${srcdir}"/${pkgname}.png "${pkgdir}"/usr/share/pixmaps/${pkgname}.png
}

# vim: sw=2:ts=2 et:
