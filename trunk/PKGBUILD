# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=ifaddr
pkgname=python-ifaddr
pkgver=0.1.6
pkgrel=1
pkgdesc="Enumerates all IP addresses on all network adapters of the system"
arch=('any')
url="https://github.com/pydron/ifaddr"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=(https://github.com/pydron/ifaddr/archive/${pkgver}.tar.gz)
sha512sums=('f8bfe0141f0c4e83404c8feead15a144e3f81fdfd9aeb5c9ca490789dfc7f74fa8cf36c895d048b595c340e17f7d82d96e3cdc25174ff67f3bb06a306094d59a')

build() {
  cd "${_name}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${_name}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

}

check() {
  cd "${_name}-${pkgver}"
  nosetests ${_name}/test_ifaddr.py
}

# vim:set ts=2 sw=2 et:
