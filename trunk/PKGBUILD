# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=furl
pkgname=python-furl
pkgver=2.1.1
pkgrel=1
pkgdesc="A small Python library that makes parsing and manipulating URLs easy"
url="https://github.com/gruns/furl"
depends=('python-orderedmultidict' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
license=('Unlicense')
arch=('any')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-2.1.1-python39.patch::https://github.com/gruns/furl/pull/140/commits/cb702b5de318799a7936f15da76f97e506fcae6d.patch"
        "${pkgname}-2.1.1-urllib.patch::https://github.com/gruns/furl/pull/139/commits/322a20bd04e589a3d633a1c461d3d7d937f52b4e.patch")
sha512sums=('9501e057bc331ba31e35698aa1f62bd1b47865206bc69cb08d31d8fe708cf0b6d50f01165164811888917a86de811a31ddf2a81fdcf8d945c4507f5bbba9edb7'
            '72bab5bac8a274fe54fbe7ed5e8e381a8d5ec306ebe83efa7aee89f676809fe2906f7a6ca13cf15cc2565bb10dbd60b072a07f0dfa2359794986049ad3e3efd1'
            '23858361b7a492bb3f8d73221d96280e175bec67e88744880c3bcc848a380437078664f5cfddaf75de868b1456a7e383d9e9bf1a06b1e948e987a341cefa01fd')
b2sums=('f58c1f43f6cc8bb2bc5026edc4d0ad6f56e83a330893c7300e57e7f432cd8b8a76b49ac8fa4f38d500b5cfe38e16998767db26ad699a5061e98bc9b06bf00084'
        '5b1fc1046ffece6e60cbf1b2f424f74075aa013725190bed4db165c190969f55f0a550489e4b00f56dccf30520b75492c16b2f8e034928ccf2c71f0d36fe5dba'
        'ac1243d02f20e7afcc38bccd600683219ff3afddcb2a0a6eb45e8cc2625f8c55da3af716276a64719e0e13b6d4af04ef14c12c86ab50d76db42c994a86ff7a8a')

prepare() {
  cd "${_name}-${pkgver}"
  patch -Np1 -i "../${pkgname}-2.1.1-python39.patch"
  patch -Np1 -i "../${pkgname}-2.1.1-urllib.patch"
}

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
