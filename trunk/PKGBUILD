# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.5.4
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
depends=('ncurses')
makedepends=('rust')
url=https://github.com/lotabout/skim
license=('MIT')
source=("$url/archive/v$pkgver/skim-v$pkgver.tar.gz")
sha512sums=('f21af6c86ca6b1c4aab1b715c1c47ced1d06c45216a03493cfc3f01d7ed6ecb6e287a335d0756f4b82b97d4d22493b01598569a0529bc57983a5a4716427af83')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 shell/skim.1
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}
