# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libcontentaction
pkgver=0.3.8
pkgrel=1
pkgdesc="Library for associating content with actions"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libcontentaction"
license=('LGPL')
depends=('qt5-systems' 'mlite')
makedepends=('python' 'qt5-tools')
source=("https://git.merproject.org/mer-core/libcontentaction/-/archive/$pkgver/libcontentaction-$pkgver.tar.bz2"
        py3.patch)
sha512sums=('4f0cec2eb42776ba1eeb41ac3597b771ea575f8b33b298f64e6d2c2057c501014a87d82c20219406ec2e8bbc1fe83313a5d68165159e9edf2295cfcb2e5ae862'
            'dc808f1e5ab507ca222556b2f199df59120e8cf7810bcec6a74e7b4e5292a4a5ceaff958f6893aa3ce4dc752b3adf0e92b759a8e9ee4045a0701a71f012c8d79')

prepare() {
  cd libcontentaction-$pkgver
  patch -p1 -i ../py3.patch
}

build() {
  cd libcontentaction-$pkgver

  qmake-qt5
  make
}

package() {
  cd libcontentaction-$pkgver

  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
