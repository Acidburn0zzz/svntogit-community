# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.3.0
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=('x86_64')
url="https://github.com/ijl/orjson"
license=('Apache' 'MIT')
depends=('python')
makedepends=('maturin' 'python-pip' 'rustup')
checkdepends=('python-arrow' 'python-pendulum' 'python-psutil' 'python-pytest'
'python-pytz' 'python-xxhash')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('cb19f190ce88b71fe4e62122fe865b5534d4fd21a667aec9f8f812ee2065763a65216a361acdf071f58fde25240e959354f0a91ba7973adaede6dfb5ac327d84')
b2sums=('511459771dea55d3190431c8d0a72f59af904f3bbcf5c228c6ca34ebd15235ef3eafffe5deb15ecca2484451ee0a51aeab38f245d1214fd65c772973d0edcfdc')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  rustup override set nightly-2020-06-09
}

build() {
  cd "$pkgname-$pkgver"
  maturin build --release \
                --strip \
                --manylinux
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgname-$pkgver"
  mkdir -vp testdir
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --root="testdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PIP_CONFIG_FILE='/dev/null'
  pip install --isolated \
              --root="$pkgdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
