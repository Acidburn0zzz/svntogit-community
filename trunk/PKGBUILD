# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.11.4
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=("python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('8850d4c955b27149fa0004f80d63c78be878912f4295927cb25779f62527b9ffbbb55bfa4d426f9ddd27ec7c77619094e0d59e49e564c8c773cdd362afc62c74')

build() {
  cd python-phonenumbers-$pkgver
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}
