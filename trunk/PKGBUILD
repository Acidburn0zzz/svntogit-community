# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=cm256cc
pkgver=1.0.2
pkgrel=1
pkgdesc='Fast GF(256) Cauchy MDS Block Erasure Codec in C++'
arch=('x86_64')
url='https://github.com/f4exb/cm256cc'
license=('GPL3')
makedepends=('cmake' 'boost')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('bc57d0f62b97926be674c46845ae8f79eb243f49a892a9da38256586922597e7373d7dc8d8777aaa90471ca95813b419277133c092483c2f7ccf6432b92a54b1')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

check() {
  $pkgname-$pkgver/build/cm256_test
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}
