# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-menu
pkgver=2.0.3
pkgrel=1
pkgdesc="Start menu development library and advanced graphical user interface for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-menu"
groups=('ukui')
depends=('qt5-svg')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-menu/archive/v$pkgver.tar.gz"
        fix-installation.patch)
sha512sums=('0804c5836a673e8d269a18a893d078ae674ea54bd038c6063f50c54b3c87fa4eb40de7ece3708978c97c8bdd6078fb431751b1d398d7fecdb21c05bb0deb14a9'
            'eb22d4cb171ee9ca46f55a6802515e3ed0c8ba4160a3dc975b6ec43ccb4e7e00e7321d071013491accb8f7c3842c7aa4f0370af4c9e902d9f1a7405836309a90')

prepare() {
  cd ukui-menu-$pkgver
  patch -p1 -i ../fix-installation.patch
}

build() {
  cd ukui-menu-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-menu-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
