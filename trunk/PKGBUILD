# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.16
pkgname=nomacs
pkgver=3.16.224
pkgrel=1
epoch=1
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('exiv2' 'gcc-libs' 'glibc' 'libraw' 'libtiff' 'opencv' 'qt5-base'
'qt5-svg' 'quazip')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('7ca8e713eedd71bc3dd3804a9a09ece15badb74949b155ea4da72a0ea129be173c8d0951604a6139d4490525cd420da8635773c11f0d133f666a48dcd9064257'
            'cd72f6ddbe3e07d7ed97f08405bac7574f4f3285c81e835f5630e011c8a9b105f23d892fed8084cb35ce521aca5524db071d0edb8bb04850dd2209d9ed019747')
b2sums=('4a2dc6ef70b0ae5dc9c9397bd2c88ede0db2bb8660e1907d08b36c4a9cf5a9bd151180aa4b2d6b5f529c1786004a6621eff88fa4bc851d60835e8ba6d6269602'
        '2bda4f36d56709653f6696af3404e416fd2d9fe7fa11de9636643c728028018ac769df3e2e519799322c5c42006cdc114d0e6406f9f60294234b07d9fd8d8409')

prepare() {
  cd "${pkgname}-${pkgver}"
  # copy plugin sources into place
  cp -av "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}/"* "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -DUSE_SYSTEM_QUAZIP=ON \
        -DENABLE_AVIF=ON \
        -DENABLE_TRANSLATIONS=ON \
        -W no-dev \
        -B build \
        -S ./ImageLounge
  make VERBOSE=1 -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
}
