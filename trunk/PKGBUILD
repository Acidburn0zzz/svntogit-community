# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=1.3.3
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('i686' 'x86_64')
url="http://skrooge.org/"
license=('GPL')
depends=('kdebase-runtime' 'libofx' 'qca-ossl')
makedepends=('cmake' 'automoc4' 'docbook-xsl')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://skrooge.org/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('251ffca5ad8e3696901fb29df2714fcb30541fe9e3107b9de2c2cb9f6b47cee9')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  cmake . -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
