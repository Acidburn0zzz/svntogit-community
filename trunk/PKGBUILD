# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Magnus Jonsson <jmagnusj@gmail.com>

pkgname=qgo
pkgver=dda95c9
pkgrel=1
pkgdesc='Go client and full featured SGF editor'
arch=('x86_64' 'i686')
url="http://qgo.sourceforge.net/"
license=('GPL')
depends=('qt5-base' 'qt5-multimedia' 'libsm')
makedepends=('git')
source=("$pkgname::git://github.com/pzorin/$pkgname.git")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  # fragments in the git url doesn't seem to work?
  git checkout -q qt5

  git describe --always | sed 's|-|.|g'
}

build() {
  cd "$pkgname"

  export QTDIR=/usr/lib/qt
  qmake && make
}

package() {
  export QTDIR=/usr/lib/qt
  make -C "$pkgname" INSTALL_ROOT="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
