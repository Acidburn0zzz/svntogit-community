# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.9
pkgrel=1
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign})
sha512sums=('6d87ba8cc487a9da0b3dc525f27d4643591d2c14f1dae2a5c5d07d29df30bb77e6184810dbfd6f54fc70ca9f21b6e0f11d75e7ee324f6cb43bdd1a8fb5aeaa42'
            'SKIP')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim: set ts=2 sw=2 ft=sh noet:
