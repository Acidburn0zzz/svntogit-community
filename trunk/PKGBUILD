# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-terminal
pkgver=1.24.0
pkgrel=2
pkgdesc="The MATE Terminal Emulator"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libsm' 'gettext' 'mate-desktop' 'vte3')
makedepends=('itstool' 'python')
groups=('mate-extra')
conflicts=('mate-terminal-gtk3')
replaces=('mate-terminal-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
		"https://github.com/mate-desktop/mate-terminal/commit/c21a7b374f420b224c24307325061b3bb55aa3bf.patch")
sha256sums=('274d98e3646980b93366daa23ca72a91afc85a41bf1879dc7342f9b9aa1d8259'
            '380810b77996c055ed49d7120f5b446a5f8fd470ec4c8b3d858f74166e366cab')

prepare() {
	cd "${pkgname}-${pkgver}"
	patch -p1 -i "$srcdir/c21a7b374f420b224c24307325061b3bb55aa3bf.patch"
}

build() {
	cd "${pkgname}-${pkgver}"
	./configure \
		--prefix=/usr
	make
}

package() {
	cd "${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
