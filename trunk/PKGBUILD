# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=avr-binutils
pkgver=2.22
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files for the avr architecture"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
depends=('glibc>=2.12-5' 'zlib')
provides=("binutils-avr=$pkgver")
replaces=('binutils-avr')
options=('!libtool' '!distcc' '!ccache')
source=(http://ftp.gnu.org/gnu/${pkgname/avr-}/${pkgname/avr-}-${pkgver}.tar.bz2{,.sig})
md5sums=('ee0f10756c84979622b992a4a61ea3f5'
         '6aea2485019497583e5c3f5fc4a39cd5')

build() {
    cd "${srcdir}"/${pkgname/avr-}-${pkgver}

    mkdir build
    cd build

    [[ $CARCH == "x86_64" ]] && CONFIGFLAG="--enable-64-bit-bfd --disable-multilib"

    CC="gcc -L$(pwd)/bfd/.libs/"
    ../configure --build=$(../config.guess) \
        --disable-nls \
        --enable-install-libbfd \
        --includedir=/usr/$(../config.guess)/include \
        --infodir=/usr/share/info \
        --libdir=/usr/lib \
        --mandir=/usr/share/man \
        --prefix=/usr \
        --target=avr \
        $CONFIGFLAG

    # This checks the host environment and makes sure all the necessary tools are available to compile Binutils.
    make configure-host

    make tooldir=/usr
}

package() {
    cd "${srcdir}"/${pkgname/avr-}-${pkgver}

    cd build
    make DESTDIR="${pkgdir}" tooldir=/usr install

    rm -f ${pkgdir}/usr/lib/libiberty.a

    for bin in addr2line ar as c++filt gprof ld nm objcopy objdump ranlib readelf size strings strip ; do
        rm -f ${pkgdir}/usr/bin/${bin}
    done

    for info in as bfd binutils configure gprof ld standards; do
        mv ${pkgdir}/usr/share/info/${info}.info ${pkgdir}/usr/share/info/avr-${info}.info
    done
}

