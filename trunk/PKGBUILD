# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Michael Kanis <mkanis gmx de>
# Contributor: Paulo Freire <paulofreire gmail com>
# Contributor: Brice Maron <brice bmaron net>

pkgname=merkaartor
pkgver=0.18.3
pkgrel=6
pkgdesc='OpenStreetMap editor'
arch=('x86_64')
url='http://merkaartor.be/'
license=('GPL2')
depends=('exiv2' 'gdal' 'gpsd' 'hicolor-icon-theme' 'qt5-svg')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstreetmap/$pkgname/archive/$pkgver.tar.gz"
        merkaartor.appdata.xml
        nowebkit.patch)
sha256sums=('d6b3e4d982b2c7b82331bc5bc8d993d733c443250f02919c3c4dbd6924e6e947'
            '4082cae211a5480501abb95c302608c45363cc3152a70746a3f41f57c4252df6'
            'f976a3bb6bcd0f0ef8723dcc8597f5ae9789c93143e7ef47f24f43c67913129b')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../nowebkit.patch
}

build() {
  cd $pkgname-$pkgver
  lrelease src/src.pro
  qmake Merkaartor.pro PREFIX=/usr GPSDLIB=1 GEOIMAGE=1 USEWEBENGINE=0
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../merkaartor.appdata.xml "$pkgdir/usr/share/metainfo/merkaartor.appdata.xml"
}
