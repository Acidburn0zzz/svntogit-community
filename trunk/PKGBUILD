# $Id$
# Maintainer: FFY00 <filipe.lains@gmail.com>

pkgname=limesuite
pkgver=18.06.0
pkgrel=1
pkgdesc="Driver and GUI for LMS7002M-based SDR platforms (LimeSDR, Novena RF7, etc.)"
arch=('x86_64')
url="https://github.com/myriadrf/LimeSuite"
license=('Apache')
depends=('sqlite3' 'wxgtk3' 'freeglut' 'libusb>=1.0' 'soapysdr' 'wget')
makedepends=('cmake')
provides=('soapylms7')
conflicts=('soapylms7')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
	'limesuitegui.desktop')
sha512sums=('1f4f20a9779f9516216c9ffa6ee96a047c873e87a256b3e97a02bc9415eb7a63ee4780f5a7ed3f27acf8b4f764994b9492ea0ce95b9fad3853583fe0b7bbe628'
	    '71b5bc64d505b97b3c1282233ab4282cbb44d541b63bd6809b8b3bef4f6e4b4bb67b0615ec28816bcb2acb1e37d0f280e3b9d1285c680deab26a43bfb424c885')

build() {
  mkdir -p "$srcdir"/LimeSuite-$pkgver/build
  cd "$srcdir"/LimeSuite-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DLIME_SUITE_EXTVER=$pkgver

  make
}

package() {
  cd "$srcdir"/LimeSuite-$pkgver/build

  make DESTDIR="$pkgdir" install

  # Install udev rules, changing mode to 666 and ommitting plugdev group
  mkdir -p "$pkgdir"/etc/udev/rules.d
  sed 's|MODE="660", GROUP="plugdev"|MODE="666"|g' "$srcdir"/LimeSuite-$pkgver/udev-rules/64-limesuite.rules > "$pkgdir"/etc/udev/rules.d/64-limesuite.rules

  install -Dm 664 "$srcdir"/limesuitegui.desktop "$pkgdir"/usr/share/applications/limesuitegui.desktop
}
