# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pynitrokey
pkgname=python-pynitrokey
pkgver=0.4.4
pkgrel=1
pkgdesc="A command line interface for the Nitrokey FIDO2 and Nitrokey Start"
arch=('any')
url="https://github.com/Nitrokey/pynitrokey/releases"
license=('Apache')
depends=('python-cbor' 'python-cffi' 'python-click' 'python-cryptography'
'python-dateutil' 'python-ecdsa' 'python-fido2' 'python-intelhex'
'python-nkdfu' 'python-pygments' 'python-pyserial' 'python-pyusb'
'python-requests' 'python-urllib3')
makedepends=('python-build' 'python-flit' 'python-install')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ac2c07382287361cd35d3f7b1315ce2356cd1da186a3b40ab2165648a9054e94ece23a56b8b606339c620aac7174ab5f20ff86599215fc4ca8038e7d2e0e4abd')
b2sums=('322c4d6975acfa6a3ed062fd62dbe6de2189e990bd7321b3aaf6229286773e8744e0a49355028a6d7062504849f7fca393645c5688b67eabf29f19364b5d39de')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

package() {
  cd "$pkgname-$pkgver"
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
