# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sshguard
pkgver=2.0.0
pkgrel=2
pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables"
arch=('i686' 'x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
backup=('etc/sshguard.conf')
install=sshguard.install
source=("http://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.gz"
	"sshguard.service"
	"sshguard-journalctl")
sha256sums=('e87c6c4a6dddf06f440ea76464eb6197869c0293f0a60ffa51f8a6a0d7b0cb06'
            '299b41c68e3c97535c1eb0f579903303383084a788edc46e6f447c13eacc8352'
            '58afcafe81d7e6af0ccd37471c6317bb76933bf618202ab76eed45eb931a14b7')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  echo 'BACKEND="/usr/lib/sshguard/sshg-fw-iptables"' >>examples/sshguard.conf.sample
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/sshguard \
    --with-firewall=iptables
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 "$srcdir/sshguard.service" "$pkgdir/usr/lib/systemd/system/sshguard.service"
  install -Dm0755 "$srcdir/sshguard-journalctl" "$pkgdir/usr/lib/systemd/scripts/sshguard-journalctl"
  install -Dm0644 "examples/sshguard.conf.sample" "$pkgdir/etc/sshguard.conf"
}
