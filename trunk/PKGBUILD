# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.5
pkgrel=2
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz"
        $pkgname-update-tests.patch::https://github.com/yan12125/nvchecker/commit/7055ad555e033da7211852e3a2c45ef161deef6b.patch)
sha512sums=('78902e43bbd9208b6668eab9a59036fe699918a87dc84ffbf174941597c6f23bf6952952d839a3a2941fad3cbcdc5a8c6336367d6d232491b67caa4631f85a58'
            '1c6a692ef7d86c3ad0ef2adcb671ec1028c976774d6f5a72f4a6f8a70f24fbefda3ca0d299951950b066563b9fa9f0b37421a573e9c867041eac4fc43fceeda5')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-update-tests.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
