# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Simons <simons@cryp.to>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=rfc
pkgver=20160616
pkgrel=1
pkgdesc="RFCs from rfc-editor.org"
arch=('any')
url="https://www.rfc-editor.org/"
license=("custom:as-is")
options=('docs')
source=(rfc-index-$pkgver.txt::https://www.rfc-editor.org/rfc/rfc-index.txt
        license
        https://www.rfc-editor.org/rfc/tar/RFCs0001-0500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs0501-1000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs1001-1500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs1501-2000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs2001-2500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs2501-3000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs3001-3500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs3501-4000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs4001-4500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs4501-5000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs5001-5500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs5501-6000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs6001-6500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs6501-7000.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs7001-7500.tar.gz
        https://www.rfc-editor.org/rfc/tar/RFCs7501-latest.tar.gz)
sha256sums=('0eb5701ca6ba5d23e16ca2e6f78d5576bdab5c3ad47a70a65516c8b417b5c446'
            'ac9cd91c2e15b0840b6ecd4f7d30d6d12c49e18b4f9eb383a0ce05a64901624e'
            '0d42b5fd777c0a5bc31970eddae7ddb73f1a35ac970c7b9ebb7e8dd8346d531c'
            'c99697b3901b76e12db65764e2f0211aa57d93602da7ac8d6eecdcfa82365380'
            '1faf46334b1edbf1f377f75f86e0fac6744cfaff0b947c75d89ad73e6106631b'
            'd65c28cb5cdd2c109c1cbc5292d5e6b023bc076ee97e08a0ef834b21ecb0e223'
            'de6e213fc91e8e3edb49cf38fa92bf3e0b1f66ef3fa350ccdc3621c92c55565f'
            '62a71b6a38409ab5225be292faa9ae818002357b981cd82916b6ff4fc739b05a'
            '01b1b2c43a04050c6ff2c9532027ba66baff04a05d98135b3962aadda4d28ecc'
            'e6ad2c0f89fb2a75b587f330514e60cada2ef6683b5462e0fa2f3dfd61699ee9'
            'feb106bd2caeac091e2cf7b1f0d858ced6b00ff26d8723dfe3a1136ccf7ec84c'
            '397ab6bfccd690fecfe95114ac59da1a170ffb3a220c15c187a8240397d5f7a6'
            '38454cdd015ad6772450554685671df6bcfa8cc356da418dc12ccd018fc64fa0'
            '6b6fa33976d55c72d19a73c329995863436460bfda03eacafb4794ec744e0799'
            '7956b0e95c6838091801ac7b021c6d6f163a4ec31ecc38a601bb45d6abb70d6e'
            '7a33f1ede745fea6571fa5046fe7b29ea16c6c64bd394b922e636f5b589c6fb4'
            'daef9ee7ef18c3d4849d9c015e073b03781cbd60bc556a3957b398c87d48530b'
            '6dc955d0e260d42cc7f57d82ed68882e3667c36d7dfe2904f7bd21db0156c634')

package() {
    cd "$srcdir"

    mkdir -p "$pkgdir/usr/share/doc/rfc"
    cp rfc-index-$pkgver.txt "$pkgdir/usr/share/doc/rfc/rfc-index.txt"
    rm -f rfc-index-$pkgver.txt

    tar xf rfc1305.tar
    rm -f rfc1305.tar
    for n in ntpv3*; do
      mv "$n" "rfc1305-$n"
    done

    for fmt in txt ps pdf; do
      mkdir -p "$pkgdir/usr/share/doc/rfc/${fmt}"
      mv *.${fmt} "$pkgdir/usr/share/doc/rfc/${fmt}/"
    done

    cd "$pkgdir/usr/share/doc/rfc"
    find . -type f -exec chmod 444 {} \;
    find . -type d -exec chmod 555 {} \;
    chown -R root:root .
    install -D -m0644 "$srcdir"/license "$pkgdir"/usr/share/licenses/$pkgname/license
}
