# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=diet-ng
pkgver=1.6.0
pkgrel=2
pkgdesc='Generic compile-time template system based on an XML-like structure for D'
arch=('x86_64')
url='https://github.com/rejectedsoftware/diet-ng'
license=('MIT')
depends=('liblphobos')
makedepends=('meson' 'ldc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e0aeef9db109e2e09891c573839a48c3de68045f536db871e699cc37966649ded751b606310c6d18523881c29eea92da2b02afb7172fb56848507d6e2c3b76fd')

prepare() {
  cd $pkgname-$pkgver

  # Meson forgot to add this flag when creating a shared library.
  sed -i "/soversion:/a ,link_args: '-shared'," meson.build
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  export DC=ldc

  arch-meson ..

  # meson broke -soname for D in the latest update
  sed -i "s/-soname,/=-soname=/g" build.ninja

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  ninja test
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  install -Dm 644 ../LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

