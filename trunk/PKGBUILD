# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=crystal
pkgver=0.10.1
pkgrel=1
_binary_rel=1
pkgdesc='The Crystal Programming Language'
arch=(i686 x86_64)
url='http://crystal-lang.org'
license=(Apache)
depends=(gc libatomic_ops pcre libpcl libevent libffi)
# depend on previous llvm version until https://github.com/manastech/crystal/issues/1614 is resolved
makedepends=(libxml2 llvm35)
checkdepends=(libyaml libxml2 gmp git)
optdepends=('shards: crystal language package manager'
            'libyaml: For YAML support'
            'gmp: For BigInt support'
            'libxml2: For XML support')
source=(crystal-$pkgver.tar.gz::https://github.com/manastech/crystal/archive/$pkgver.tar.gz)
source_i686+=(https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-i686.tar.gz)
source_x86_64+=(https://github.com/manastech/crystal/releases/download/$pkgver/$pkgname-$pkgver-$_binary_rel-linux-x86_64.tar.gz)
sha256sums=('f567866ea4cf7d0ca1356806f4871c964eca939ddcd93796ecc0e3f3889cd7f3')
sha256sums_i686=('0c8cead9c3cc5d90815972b87f66a2abe431c8da8b20446e374218221647227a')
sha256sums_x86_64=('6bb5aed56e579bc84bfe57c3bc920efc79ec329ad021c968370a7fe9956a3026')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|Please install shards: https://github.com/ysbaddaden/shards|Please install shards: pacman -S shards|' src/compiler/crystal/command.cr
}

build() {
  cd $pkgname-$pkgver

  make release=1 \
       PATH="$srcdir/$pkgname-$pkgver-$_binary_rel/bin:$PATH" \
       CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
       CRYSTAL_CONFIG_VERSION="$pkgver" \
       CRYSTAL_CONFIG_PATH="libs:/usr/lib/crystal"
  make doc
}

check() {
  cd $pkgname-$pkgver

  make spec CRYSTAL_PATH="$srcdir/$pkgname-$pkgver/src" \
            CRYSTAL_CONFIG_VERSION="$pkgver"
}

package() {
  cd $pkgname-$pkgver

  # /usr/bin/crystal                compiled executable
  # /usr/lib/crystal/               compiler src & core libs
  # /usr/share/doc/crystal/api      api docs
  # /usr/share/doc/crystal/samples/ samples

  install -Dm755 ".build/crystal" "$pkgdir/usr/bin/crystal"

  install -dm755 "$pkgdir/usr/lib"
  cp -r src "$pkgdir/usr/lib/crystal"

  install -dm755 "$pkgdir/usr/share/doc/crystal"
  cp -r doc "$pkgdir/usr/share/doc/crystal/api"
  cp -r samples "$pkgdir/usr/share/doc/crystal/"

  install -Dm644 etc/completion.zsh "$pkgdir/usr/share/zsh/site-functions/_crystal"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
