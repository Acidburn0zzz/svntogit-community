# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.6.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=(any)
url="https://github.com/python/importlib_resources"
license=(Apache)
depends=(python-zipp)
makedepends=(python-build python-installer python-setuptools-scm python-toml python-wheel)
checkdepends=(python-pytest python-tests)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('d3c88dd5f1877b205b4c53ab9c8d4180ae87be0ff9dd11483024de40c84fe7da359f70337aeacc5df3b6deca88b903bb01850da8a54fd057e4174806ae767deb')
b2sums=('9a83554cd65b7851c4a8c977509f3fc255de3c5927661764e702eeae142b7d0cc75f809fe2a438b6f3acdb2facbdc3dadbf3f9dfed5615643dc15e04545dcf8c')

build() {
  cd $_name-$pkgver
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -v
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 README.rst -t "$pkgdir/usr/share/doc/$pkgname/"
}
