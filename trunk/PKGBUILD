# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Johannes Wienke <languitar@semipol.de>
pkgname=xsecurelock
pkgver=1.3.3
pkgrel=1
pkgdesc="X11 screen lock utility with security in mind"
arch=('x86_64')
url="https://github.com/google/xsecurelock"
license=('APACHE')
depends=('pam' 'libxcomposite' 'libxmu' 'libxft' 'libxrandr' 'libxss')
optdepends=('mplayer: for the saver_mplayer module'\
            'mpv: for the saver_mpv module'\
            'imagemagick: for the auth_pamtester module'\
            'pamtester: for the auth_pamtester module'\
            'xorg-xset: for the saver_blank module'\
            'xscreensaver: for the saver_xscreensaver module')
source=("https://github.com/google/xsecurelock/archive/v${pkgver}.tar.gz")
sha512sums=('a8c1e70068ed01889aecf75336550e27b071616b35aba090d4a230a85dada27a535f8d61a0e8a0fcb67ba70edd2e54acb13f6af7dcd1ca258837f3acee6d5dec')

prepare() {
    cd "${pkgname}-${pkgver}"
    # TODO remove with the next release (https://github.com/google/xsecurelock/issues/59)
    echo 'const char *const git_version = "1.3";' > version.c
}

build() {
    cd "${pkgname}-${pkgver}"
    sh autogen.sh
    ./configure --prefix=/usr \
                --libexecdir=/usr/lib \
                --with-pam-service-name=system-auth
    make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
