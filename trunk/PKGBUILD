# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.38.2
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Boxes"
license=(LGPL)
groups=(gnome)
depends=(cdrtools edk2-ovmf freerdp gtksourceview4 gtk-vnc libarchive libgudev libhandy libosinfo
         libsecret libvirt-glib mtools qemu spice-gtk tracker3 webkit2gtk)
makedepends=(git gobject-introspection yelp-tools meson spice-protocol vala)
_commit=0bc5f1b131d443ddc73278df5355752da0c17e4d
source=("git+https://gitlab.gnome.org/GNOME/gnome-boxes.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/gtk-frdp.git"
        "git+https://gitlab.gnome.org/felipeborges/libovf-glib.git"
        "libhandy1.patch")
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            '208eaee9724da213c20c2235f4e55b64f60051535197d70873bcf2da8d3549f8')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd $pkgname

  # Port to libhandy-1
  git revert --no-commit d43264bae43cd8d0e45d62df7e863b85441fa584
  patch -Np1 -i ../libhandy1.patch

  git submodule init
  git submodule set-url subprojects/libovf-glib "$srcdir/libovf-glib"
  git submodule set-url subprojects/libhandy "$srcdir/libhandy"
  git submodule update
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
