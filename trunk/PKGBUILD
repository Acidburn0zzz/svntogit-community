# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

pkgname=intel-gpu-tools
pkgver=1.15
pkgrel=1
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(i686 x86_64)
license=(MIT)
url='http://cgit.freedesktop.org/xorg/app/intel-gpu-tools/'
depends=(libdrm libpciaccess cairo python)
makedepends=(swig gtk-doc xorg-util-macros dri2proto)
source=(https://xorg.freedesktop.org/releases/individual/app/$pkgname-$pkgver.tar.bz2
        https://www.x.org/releases/individual/app/$pkgname-$pkgver.tar.bz2.sig)
md5sums=('dea5d3a60f93054760d773aaa3a36b1f'
         'SKIP')
validpgpkeys=(664F2250EF4D2A5E3C79BDBDB98B588010CF213E)

build() {
  cd $pkgname-$pkgver
  export LANG=en_US.UTF-8
  ./configure --prefix=/usr --libexecdir=/usr/lib --disable-tests --without-libunwind
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/intel-gpu-tools/COPYING"
}
