# Maintainer: Robin Broda <coderobe @ archlinux.org>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>
# Contributor:  Achilleas Pipinellis <axilleas archlinux gr>

pkgname=supermin
pkgver=5.1.20
pkgrel=3
pkgdesc="Tool for creating supermin appliances"
arch=('x86_64')
url="http://people.redhat.com/~rjones/supermin/"
license=('GPL')
makedepends=('ocaml' 'ocaml-findlib' 'cpio')
depends=('e2fsprogs')
conflicts=('febootstrap<=3.21')
source=("http://libguestfs.org/download/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.sig}
        "pacman-iter.patch")
sha512sums=('ccc9ada98593f91399b323f0beb027185874f82547a1df4274c1206a9b6d4465e98b7aef5618274d41fb902747f6843fd2cc6192a4ea8526811261ac5a16848e'
            'SKIP'
            '1921a5373c2a251048224871fcc9ccf2f3420ba444e6f0673e4efda2e7036cd31235c981c41f185ce15f6d5e0de2dd8c84290c410e12714b87992c150b799ad3')
validpgpkeys=('F7774FB1AD074A7E8C8767EA91738F73E1B768A0') # Richard W.M. Jones <rjones@redhat.com>

prepare() {
  cd "${pkgname}-${pkgver}"

  patch src/ph_pacman.ml "${srcdir}/pacman-iter.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}/" install
}
