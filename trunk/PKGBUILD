# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Marcin Karpezo <sirmacik at gmail dot com>

pkgbase=scala
pkgname=(scala scala-docs scala-sources)
pkgver=2.11.0M7
pkgrel=1
_distdate=20131118-113951
_distsha=c243435f11
pkgdesc="A Java-interoperable language with object-oriented and functional features"
arch=('any')
url="http://www.scala-lang.org"
license=('BSD')
depends=('java-runtime')
makedepends=('apache-ant' 'git')
source=("scala-$pkgver.tar.gz::https://github.com/scala/scala/archive/v${pkgver/M/-M}.tar.gz"
	"http://www.scala-lang.org/files/archive/scala-docs-${pkgver/M/-M}.txz")
md5sums=('675bfb36388f34be519858c47bdecc47'
         '701fd61cf93386a484cdcbce2fa907e9')

build() {
  cd ${srcdir}/${pkgname}-${pkgver/M/-M}
  chmod 0755 tools/*
  echo -e "#!/bin/bash\necho ${_distdate}" >tools/get-scala-commit-date
  echo -e "#!/bin/bash\necho ${_distsha}" >tools/get-scala-commit-sha

  export ANT_OPTS="-Xms2500M -Xmx2500M -Xss8M -XX:MaxPermSize=1024M -XX:+UseParallelGC"

  ant docs
  ant dist.src
  ant dist-opt -Dbuild.release=true
}

package_scala() {
  depends=('java-runtime')
  optdepends=('scala-docs' 'scala-sources' 'graphviz: generate diagrams')

  cd ${srcdir}/${pkgname}-${pkgver/M/-M}/dists/latest/

  install -d ${pkgdir}/usr/{bin,share} ${pkgdir}/usr/share/man/man1 ${pkgdir}/usr/share/scala/{bin,lib}
  cp -r {lib,misc} ${pkgdir}/usr/share/scala/
  cp -r man ${pkgdir}/usr/share/
  install -m 755 bin/{fsc,scala,scalac,scalap,scaladoc} ${pkgdir}/usr/share/scala/bin
  install -D -m0644 ${srcdir}/${pkgname}-${pkgver/M/-M}/doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  rm -f $pkgdir/usr/share/scala/lib/scalacheck.jar

  ln -s ../share/scala/bin/fsc ${pkgdir}/usr/bin/fsc
  ln -s ../share/scala/bin/scala ${pkgdir}/usr/bin/scala
  ln -s ../share/scala/bin/scalac ${pkgdir}/usr/bin/scalac
  ln -s ../share/scala/bin/scalap ${pkgdir}/usr/bin/scalap
  ln -s ../share/scala/bin/scaladoc ${pkgdir}/usr/bin/scaladoc
}

package_scala-docs() {
  replaces=('scala-doc' 'scala-devel-docs')
  pkgdesc="Scala documentation"
  depends=()

  cd ${srcdir}
  mkdir -p $pkgdir/usr/share/doc/
  cp -r scala-docs-${pkgver/M/-M} $pkgdir/usr/share/doc/scala
  install -D -m0644 ${srcdir}/${pkgname}-${pkgver/M/-M}/doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_scala-sources() {
  replaces=('scala-src')
  pkgdesc="Scala sources"
  depends=()

  cd ${srcdir}/scala-${pkgver/M/-M}/dists
  cd latest
  mkdir -p $pkgdir/usr/share/scala/
  cp -r src $pkgdir/usr/share/scala/src
  install -D -m0644 ${srcdir}/${pkgname}-${pkgver/M/-M}/doc/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
