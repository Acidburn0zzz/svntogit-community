# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-jclient
pkgver=0.4.0
pkgrel=3
pkgdesc="Create a Jack client with a few lines of C++"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/downloads/index.html"
license=('GPL')
depends=('jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
sha512sums=('2c23d9b9accc2458af4de2ff4ae2652534280b6911a15eb915eb71959d8df359cb284a6a837feb540a26c1d88e8839bee02025759a1180170bc66fe30fee20d9')
validpgpkeys=()

prepare() {
  cd "$pkgname-$pkgver"
  # remove optimization and call to ldconfig
  sed -e '/march=native/d' \
      -e '/ldconfig/d' \
      -i libs/Makefile
}

build() {
  cd "$pkgname-$pkgver/libs"
  make
}

package() {
  cd "$pkgname-$pkgver/libs"
  make DESTDIR="$pkgdir/" LIBDIR=lib PREFIX=/usr install
}
