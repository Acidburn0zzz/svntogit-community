# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioresponses
pkgver=0.5.1
pkgrel=1
pkgdesc="A helper to mock/fake web requests in python aiohttp package."
url="https://github.com/pnuckowski/aioresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp')
makedepends=('python-pbr')
checkdepends=('python-pytest-runner' 'python-ddt' 'python-asynctest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pnuckowski/aioresponses/archive/$pkgver.tar.gz")
sha512sums=('4e52c7944f59fdf79ac9394ffdf6df8c452cf9b672892b93f829d04f113c1da34d1e6aa5c392773dc791a61e7ad34d9db953230a554f4465bd409556ed3a35b7')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd aioresponses-$pkgver
  python setup.py build
}

check() {
  cd aioresponses-$pkgver
  python setup.py pytest
}

package() {
  cd aioresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
