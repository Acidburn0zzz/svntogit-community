# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-xdist
pkgname=('python-pytest-xdist' 'python2-pytest-xdist')
pkgver=1.18.0
pkgrel=1
pkgdesc='py.test xdist plugin for distributed testing and loop-on-failing modes'
arch=('any')
license=('MIT')
url='https://bitbucket.org/pytest-dev/pytest-xdist'
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools_scm' 'python2-setuptools_scm'
             'python-execnet' 'python2-execnet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-xdist/archive/v$pkgver.tar.gz")
sha512sums=('d7074df3617de40571f517ea7be900135c8b11dced9061d490ead8dbfca540e7afd3f7ac2c77c920571a9f6b7bf9c122990a708c817469a92146aedc86891f0a')

prepare() {
  cp -a pytest-xdist-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-xdist-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-xdist-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it
  # https://github.com/pytest-dev/pytest-xdist/issues/163

  cd "$srcdir"/pytest-xdist-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" py.test || warning "Tests failed"

  cd "$srcdir"/pytest-xdist-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" py.test2 || warning "Tests failed"
}

package_python-pytest-xdist() {
  depends=('python-pytest' 'python-execnet')

  cd pytest-xdist-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-xdist() {
  depends=('python2-pytest' 'python2-execnet')

  cd pytest-xdist-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
# vim:set ts=2 sw=2 et:
