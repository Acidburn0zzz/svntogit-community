# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Rich Li <rich at dranek com>
# Contributor: Thomas Haider <t.haider@deprecate.de>
# Contributor: Filip S. Adamsen <fsa [at] fsadev [dot] com>
# Contributor: Samed Beyribey <ras0ir@eventualis.org>

pkgname=python-authres
pkgver=1.1.1
pkgrel=1
pkgdesc='Python Authentication-Results headers generation and parsing'
arch=('any')
url=https://launchpad.net/authentication-results-python
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/a/authres/authres-$pkgver.tar.gz")
sha512sums=('61e73e85e524279d5db37375fbe7b1fcf65a24280ef61124a94e4236ce477c225637690e9d424f5ee3fef8b9cd3d26c6d512dfa3c1c8491fa4f4bf266f88ac80')
 
build() {
  cd authres-$pkgver
  python setup.py build 
}

check() {
  cd authres-$pkgver
  python -m authres -v
}

package() {
  cd authres-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

