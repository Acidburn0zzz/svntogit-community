# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=4.4.21
pkgrel=1
pkgdesc="A Real-time Transport Protocol (RTP) library"
arch=(x86_64)
url="https://github.com/BelledonneCommunications/ortp"
license=(GPL3)
depends=(bctoolbox)
makedepends=(cmake doxygen)
source=($pkgname-$pkgver.tar.gz::"https://github.com/BelledonneCommunications/ortp/archive/$pkgver.tar.gz")
sha256sums=('7f4510738a8483bc560880075bdb43028f597624b01e2e90d240e36366be733a')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
