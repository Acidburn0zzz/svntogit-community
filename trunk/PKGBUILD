# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=38
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl' 'sdl' 'tcl')
makedepends=('boost' 'glu')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d731e78b75c8ceda8de47399371ad61c02ef757f0c9ec915b0a924687e7a35878b937a02b658e0d5c9e025b2211ce7e950827e76823ad056e2bcd3f6b6abe155')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  CXXFLAGS="${CXXFLAGS} -O3 -D__UNIX_ALSA__ -D__UNIX_JACK__" \
  CFLAGS="${CFLAGS} -O3" \
  ./configure --prefix='/usr'
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
