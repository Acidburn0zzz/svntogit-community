# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgbase=python-pony
_pkgname=${pkgbase#python-}
pkgname=(python-$_pkgname python2-$_pkgname)
pkgver=0.7.10
pkgrel=1
pkgdesc='Pony Object Relational Mapper'
arch=('any')
url='https://github.com/ponyorm/pony'
makedepends=('python-setuptools' 'python2-setuptools')
license=('Apache')
source=("$url/archive/v$pkgver.tar.gz")
sha512sums=('ed8b89260c9326faa87c94b2d1b94bda4a118934e554ee1ce8a39155a828f2d399a6ab2c70d96c1c6a9c9691111903bb1c00a512ff12e273503dacd6a6f60243')

prepare() {
  cp -a $_pkgname-$pkgver{,-py2}
}

build() {
  echo "Building python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py build

  echo "Building python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py build
}

check() {
  echo "Running tests for python-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver
  python setup.py test

  echo "Running tests for python2-$_pkgname $pkgver"
  cd "$srcdir"/$_pkgname-$pkgver-py2
  python2 setup.py test
}

package_python-pony() {
  depends=('python')
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pony() {
  depends=('python2')
  cd $_pkgname-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

