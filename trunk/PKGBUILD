# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-blosc
pkgname=('python2-blosc' 'python-blosc')
pkgver=1.8.1
pkgrel=1
pkgdesc="A Python wrapper for the extremely fast Blosc compression library"
arch=('x86_64')
url="http://python-blosc.blosc.org/"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'blosc')
source=("https://github.com/Blosc/python-blosc/archive/v${pkgver}.tar.gz")
sha256sums=('302ec8da715a2058f1e1bd3349cb5dd201c50270e99c04afed5ab0ae998507be')

prepare() {
  cd "$srcdir"
  cp -a ${pkgbase}-${pkgver} ${pkgbase}-py2-${pkgver}
  cd ${pkgbase}-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  export BLOSC_DIR=/usr

  # "Building Python2"
  cd "$srcdir"/${pkgbase}-py2-${pkgver}
  python2 setup.py build_clib
  python2 setup.py build_ext --inplace
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/${pkgbase}-${pkgver}
  python setup.py build_clib
  python setup.py build_ext --inplace
  python setup.py build
}

package_python2-blosc() {
  depends=('python2' 'blosc')
  optdepends=('python2-numpy: for tests and benchmarks')

  cd "$srcdir"/${pkgbase}-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSES/PYTHON-BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-blosc() {
  depends=('python' 'blosc')
  optdepends=('python-numpy: for tests and benchmarks')

  cd "$srcdir"/${pkgbase}-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSES/PYTHON-BLOSC.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

