# $Id$
# Maintainer : Felix Yan <felixonmars@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Arnau Sanchez <tokland {at} gmail {dot} com>

_name=pyquery
pkgbase=python-pyquery
pkgname=('python-pyquery' 'python2-pyquery')
pkgver=1.2.5
pkgrel=1
pkgdesc="A jquery-like library for python."
arch=('any')
url="https://github.com/gawel/pyquery/"
license=('custom:BSD')
makedepends=('python-distribute' 'python2-distribute' 'python-lxml' 'python2-lxml' 'python-cssselect' 'python2-cssselect')
source=("http://pypi.python.org/packages/source/p/$_name/$_name-$pkgver.zip"
        "https://bitbucket.org/olauzanne/pyquery/raw/b70710332032/LICENSE.txt")
 
build() {
  cd "$srcdir"
  cp -r "$_name-$pkgver" "$_name-$pkgver-python2"

  cd "$srcdir/$_name-$pkgver"
  python3 setup.py build

  cd "$srcdir/$_name-$pkgver-python2"
  python2 setup.py build
}

package_python-pyquery() {
  depends=('python-lxml' 'python-cssselect')
  cd "$srcdir/$_name-$pkgver"
  python3 setup.py install --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python-pyquery/LICENSE"
}

package_python2-pyquery() {
  depends=('python2-lxml' 'python2-cssselect')
  cd "$srcdir/$_name-$pkgver-python2"
  python2 setup.py install --root="$pkgdir/"

  install -Dm644 ../LICENSE.txt "$pkgdir/usr/share/licenses/python2-pyquery/LICENSE"
}

sha512sums=('c438c9ce6821336eb94ca9bbf43f6a4df53281ed58ce4c07479cf8c34d79051c195b50351a972c7444a49813aad8125e7b4622f8ef45bb390dde5ad64e541293'
            '4932f86cbe9ed4c2e658acf9c228fdb0b2430fd622d559555196c7db0f43ea435483c7ae9916c325b40130334f4706d30e5adea6bc1d097b3283a7fc64926685')
