# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=uvloop
pkgname=python-$_pkgname
pkgver=0.15.1
pkgrel=1
pkgdesc='Ultra fast asyncio event loop'
arch=('x86_64')
url='https://github.com/MagicStack/uvloop'
license=('Apache' 'MIT')
depends=('python' 'libuv')
makedepends=('python-setuptools' 'cython')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('cbe424a40ab2c045af1c4c6eac08f0e9896ea39aa884c8ec793ed23c2eb889a3d2fc889a3cb85112854f8f34ce9b742287fad8c6ec9e3819b494f246790f372d')

build() {
  cd $_pkgname-$pkgver

  python setup.py build build_ext --use-system-libuv
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
