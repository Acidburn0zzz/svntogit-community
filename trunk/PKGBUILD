# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.21
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
depends=('glibc')
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('3b0dd3e6d8ae4b172d0be934ada6a8dcadc3024d0f27450ed107d55cf7f08b6b0b5eeb739a210fc9a76db42f7cc75ad5842ca7415afa3d7b17d9a34f23920a59')
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath"
	go build -o terraform-binary
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
