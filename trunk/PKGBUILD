# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Daichi Shinozaki <dsdseg@gmail.com>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=python-paho-mqtt
pkgver=1.4.0
pkgrel=2
url="https://www.eclipse.org/paho/clients/python"
pkgdesc="Python client library for MQTT v3.1 and v3.1.1"
depends=('python')
checkdepends=('python-pytest' 'python-pytest-runner')
makedepends=('python-setuptools')
license=('custom')
arch=('any')
source=(https://github.com/eclipse/paho.mqtt.python/archive/v${pkgver}.tar.gz)
sha256sums=('fce9a3d8477a4d0c9bfb632c66ad64d9fd0440b5dc7815d5727b49acf710d799')

prepare() {
  cd paho.mqtt.python-${pkgver}
  sed -i '/addopts/d' setup.cfg
}

build() {
  cd paho.mqtt.python-${pkgver}
  python setup.py build
}

check() {
  cd paho.mqtt.python-${pkgver}
  PYTHONPATH=$PWD/src pytest tests
}

package_python-paho-mqtt() {
  cd paho.mqtt.python-${pkgver}
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
