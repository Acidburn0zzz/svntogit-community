# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Musikolo <musikoloAThotmailDOTcom>
# Contributor: krani1
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
pkgname=groovy
pkgver=1.8.5
pkgrel=3
pkgdesc="Programming language based on Java, inspired by Python, Ruby and Smalltalk"
url="http://groovy.codehaus.org"
license=('APACHE' 'BSD' 'custom')
depends=('bash' 'java-environment')
optdepends=('groovy-docs: html and pdf documentation for Groovy')
arch=('any')
source=("http://dist.codehaus.org/groovy/distributions/$pkgname-binary-$pkgver.zip")
sha256sums=('5d6f6d5905f179cd7f3bccd29af4918eea67eeebe12aaa7144e31c7f5d0b5f06')

build() {
  cd "$srcdir/$pkgname-$pkgver/embeddable"

  ln -s "$pkgname-all-$pkgver.jar" "$pkgname-all.jar"
  for f in ../bin/*; do
    sed 's:bin/sh:bin/sh\nGROOVY_HOME=/usr/share/groovy:' -i $f
  done
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$pkgdir/usr/share/groovy" "$pkgdir/usr/bin"
  cp -r lib "$pkgdir/usr/share/groovy"
  cp -r conf "$pkgdir/usr/share/groovy"
  cp -r embeddable "$pkgdir/usr/share/groovy"
  cp bin/* "$pkgdir/usr/bin"
  find "$pkgdir" -name '*.bat' -exec rm {} \;
  # APACHE
  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 CLI-LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/CLI-LICENSE"
  # BSD
  install -Dm644 ANTLR-LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/ANTLR-LICENSE"
  # custom
  install -Dm644 ASM-LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/ASM-LICENSE"
  install -Dm644 JSR223-LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/JSR223-LICENSE"
}

# vim:set ts=2 sw=2 et:
