# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  hydro <hydro@freenet.de>

pkgname=libmediainfo
pkgver=18.12
pkgrel=1

pkgdesc="shared library for mediainfo"
url="http://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('curl' 'glib2' 'libmms' 'libzen')

source=(https://mediaarea.net/download/source/libmediainfo/$pkgver/libmediainfo_$pkgver.tar.xz)

sha256sums=('976f3d42c092359769f9ed95168a2c9e661edd3afe63b8de9b4e8a2123836e4f')

build() {
  cd MediaInfoLib/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --with-libcurl \
              --with-libmms

  make
}

package() {
  cd MediaInfoLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -D -m 644 "$srcdir"/MediaInfoLib/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
