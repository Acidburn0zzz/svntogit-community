# Maintainer: David Runge <dvzrv@archlinux.org>
_name=pytest-rerunfailures
pkgname=python-pytest-rerunfailures
pkgver=8.0
pkgrel=1
pkgdesc="A plugin for py.test that re-runs failed tests to eliminate intermittent failures"
arch=('any')
url="https://github.com/pytest-dev/pytest-rerunfailures/"
license=('MPL2')
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('538fe5ca579ec50fc1689c4b7df3a55e0bdb08f87c2dfeb219ee141ef67c6c74a324c64f183f3ff85a5289ecfb54beef0fde2105eb0ae888ed861a15194a9dc5')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build/lib:$PYTHONPATH"
  py.test test_pytest_rerunfailures.py
}

package() {
  cd "${_name}-${pkgver}"
  local py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # fix missing world-readable bit
  chmod 644 "${pkgdir}/usr/lib/python${py_ver}/site-packages/__pycache__/"*.pyc
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES.rst,README.rst}
}
