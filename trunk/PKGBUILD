# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgname=gnome-packagekit
pkgver=3.24.0
pkgrel=1.1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('desktop-file-utils' 'gtk3' 'libnotify' 'packagekit')
makedepends=('intltool' 'itstool' 'docbook-xsl' 'libcanberra')
source=("https://download.gnome.org/sources/${pkgname}/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        '0001-alpm-hooks.patch')
sha256sums=('8d038681f6879c93f3f58faaa37c41c7ac8cf53eba6b8cb871f5f683ac0bd13f'
            'f30c6ba74f47d10f56fc9c602cc4e4a03a19526ef02eea28752739f267c8483c')

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -Np1 < "${srcdir}"/0001-alpm-hooks.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-schemas-compile \
		--disable-gtk-doc
	sed -i 's|docbook2man|docbook2man --sgml|' man/Makefile
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
}

