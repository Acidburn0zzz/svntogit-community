# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ogre
pkgver=1.12.3
pkgrel=1
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('freeimage' 'freetype2' 'libxaw' 'libxrandr' 'openexr' 'pugixml'
         'nvidia-cg-toolkit' 'zziplib' 'sdl2' 'glu' 'tinyxml')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'python' 'swig' 'systemd' 'git')
install=ogre.install
replaces=('ogre-docs')
source=("https://github.com/OGRECave/ogre/archive/v${pkgver}.tar.gz"
        "git+https://github.com/ocornut/imgui.git#commit=688cf868ea83db6c2958dd6bf7a20d471d00940b")
optdepends=('cppunit: unit testing'
            'python: python bindings')
sha512sums=('1381b342e763f006952e8b2cf70e129f0e4ac399495450a72b2d9a97f39fd4346897b83e9acaa47611c1d5c8565cc6b5b5f1b516078a8904afa1c9e73188b586'
            'SKIP')

prepare() {
  mkdir ogre-${pkgver}/build

  cp -r "$srcdir"/imgui ogre-${pkgver}/Components/Overlay/src/
}

build() {
  cd ogre-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_BUILD_DEPENDENCIES=FALSE \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DOGRE_BUILD_PLUGIN_FREEIMAGE=TRUE \
    -DOGRE_BUILD_PLUGIN_EXRCODEC=TRUE

  make
  make OgreDoc
}

package() {

  cd ogre-${pkgver}/build

  make DESTDIR=${pkgdir} install

  mv ${pkgdir}/usr/bin/SampleBrowser ${pkgdir}/usr/bin/OgreSampleBrowser
  install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
