# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Alain Kalker <a.c.kalker@gmail.com>
# Maintainer: Juergen Hoetzel <juergen@archlinux.org>

pkgname=cmucl
pkgver=21c
pkgrel=1
pkgdesc="CMU Common Lisp"
depends_i686=('glibc' 'openmotif')
depends_x86_64=('lib32-glibc' 'openmotif')
provides=('common-lisp')
makedepends=('sed' 'bc' 'clang')
license=('custom')
#license PublicDomain
source=("https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-src-${pkgver}.tar.bz2"
        "https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-src-${pkgver}.tar.bz2.asc"
        "https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-${pkgver}-x86-linux.tar.bz2"
        "https://common-lisp.net/project/cmucl/downloads/release/${pkgver}/cmucl-${pkgver}-x86-linux.tar.bz2.asc"
        82bf2c727ba1d3c461fa360aaae176608eb9349b.patch)
sha256sums=('3435963b594d44b5ca0e12674222b40fb00b3c44ea0226fead5d66041369a74d'
            'SKIP'
            'f94a79ca4c2918cc4e7bdc58c934c98e56accc3c08f7cccde70ade02ab438105'
            'SKIP'
            '6cb5b7921bee5c5cba19939de635870f56f1bce662d457aeccde29878a4d0ebc')
validpgpkeys=('0EF50ED55514BFF6B72B9DAC06CE3819086C750B') # "Raymond Toy <toy.raymond@gmail.com>
url="https://www.cons.org/cmucl/"
arch=('x86_64')

prepare() {
  cd "${srcdir}"
  patch -p1 -i 82bf2c727ba1d3c461fa360aaae176608eb9349b.patch

  if [[ $CARCH == "i686" ]]; then
    sed -i 's|i386\*|i686\*|' bin/build-all.sh
  fi
}

build() {
  cd "${srcdir}"
  export CMUCLLIB="${srcdir}"/lib/cmucl/lib
  # Build using binary-dist lisp
  ./bin/build-all.sh -o "${srcdir}"/bin/lisp -C x86_linux_clang
}

package() {
  cd "${srcdir}"
  # install distribution
  ./bin/make-dist.sh -I "${pkgdir}"/usr -M share/man/man1 -V 21c linux-4
  # move docs
  mv "${pkgdir}"/usr/doc "${pkgdir}"/usr/share/
  # backwards compatibility
  ln -sf lisp "${pkgdir}"/usr/bin/cmucl
  # license
  install -D -m644 "${srcdir}"/src/general-info/COPYRIGHTS \
                   "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

