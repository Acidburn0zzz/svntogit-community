# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.2.6
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests' 'python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('fe3c317bfff18afc4109890aa6967d446755ccaa7ff308f28ab06f6f4ba2ccceefdd8b3429c5f8867f4d5a55dfd947a8392dfdd81f00948f950b9c5cbc2992a8')

build() {
  cd python-sdk-$pkgver
  python setup.py build
}

check() {
  # Extracted from .travis.yml
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd python-sdk-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
