# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=4.1.0
pkgrel=1
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz")
sha512sums=('7ad5517cd52f6f31448db9b608d8a7b274741727487ede21b7f80e4060e3ddbb34c731166f67ae7136c86a8d3e7fd23128e035a148f18f771066bfa71319624f')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
