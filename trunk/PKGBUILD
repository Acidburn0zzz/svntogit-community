# Contributor: JJDaNiMoTh <jjdanimoth@gmail.com>
# Contributor: Bjørn Lindeijer <bjorn lindeijer nl>

pkgname=dumb
pkgver=0.9.3
pkgrel=3
pkgdesc="An IT, XM, S3M and MOD player library"
arch=('i686' 'x86_64')
license=('custom:dumb' 'GPL')
url="http://dumb.sourceforge.net/"
depends=('glibc' 'allegro')
source=(http://downloads.sourceforge.net/sourceforge/dumb/$pkgname-$pkgver.tar.gz)
md5sums=('f48da5b990aa8aa822d3b6a951baf5c2')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ "${CARCH}" = "x86_64" ] && export CFLAGS="${CFLAGS} -fPIC"
  # Create the directories which the Makefile somehow fails to create
  install -d lib/unix/{debug,release}
  # Make with Allegro support
  make PREFIX=$pkgdir/usr << EOF
Y

EOF
}
package() {
  cd $srcdir/$pkgname-$pkgver
  # Create directories
  install -d $pkgdir/usr/{lib,bin,include}
  install -D -m644 licence.txt \
    $pkgdir/usr/share/licenses/$pkgname/licence.txt

  make install
}
