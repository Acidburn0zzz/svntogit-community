# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Philipp A. <flying-sheep@web.de>

pkgname=cargo-generate
pkgver=0.10.2
pkgrel=1
pkgdesc="Use pre-existing git repositories as templates"
arch=('x86_64')
url="https://github.com/cargo-generate/cargo-generate"
license=('MIT' 'Apache')
depends=('curl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('a1ad082a6006d0a8b4d8ed65d5214d3d37635ad894eecbea72b825657fe40155')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --lib
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE-MIT -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
