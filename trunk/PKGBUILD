# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-environ
pkgname=python-django-environ
pkgver=0.8.0
pkgrel=1
pkgdesc="Utilize 12factor inspired environment variables to configure your Django application"
arch=('any')
url="https://github.com/joke2k/django-environ"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9c378a846fefc8730a7020569db669e411d84025280dc45bccfb16671dcec04e92fd3f80d5b4a1a8d9c6839ec90562bb7514966940ee90d7926c6a1fa703c68a')
b2sums=('6fce5b2d0441275f1c87e9a3d2b875618c85d85a2fb31301a70ddc344878da24afcd132a9abf8ba460b33012ed1975629e2b6ce6056bdfb73b6c88b5549f35c6')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -vv
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
