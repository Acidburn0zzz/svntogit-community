# $Id$
# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>

pkgname=lutris
pkgver=0.4.18.1
pkgrel=4
pkgdesc='Open Gaming Platform'
arch=('any')
url='https://lutris.net'
license=('GPL3')
depends=('python-gobject' 'python-yaml' 'python-evdev' 'gtk3' 'glib2' 'psmisc' 'cabextract' 'unrar' 'unzip' 'p7zip' 'curl')
makedepends=('python-setuptools')
checkdepends=('xorg-server-xvfb' 'xterm' 'xorg-xrandr' 'python-nose-cover3' 'wine')
optdepends=('wine: Run windows games'
            'wine-staging: Run windows games - Staging patches')
makedepends=('python-setuptools')
source=("$url/releases/${pkgname}_$pkgver.tar.xz")
sha512sums=('d5f760860d7b88101e8833e7c8697ce6584a041b82049a7068e6f1e52c564a74ba97e4d2899706e6f3684404acfa8b858ddd41f67301acf42be11e6768607ada')

build() {
  cd $pkgname

  python setup.py build
}

check() {
  cd $pkgname

  xvfb-run nosetests --cover-erase --with-xunit --xunit-file=nosetests.xml --with-coverage --cover-package=lutris --cover-xml-file=coverage.xml
}

package() {
  cd $pkgname

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

