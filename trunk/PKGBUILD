# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.5.4
pkgrel=3
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2')
makedepends=('python2-docutils')
source=("http://cloud.github.com/downloads/spaetz/offlineimap/offlineimap-v${pkgver}.tar.gz"
        '00-fix-readonly.patch'
        '01-properly-detect-readonly-folders.patch')
sha1sums=('a9ad5f32f8bc0ec042f8059ea9d34282bb8b682a'
          'a9faa6d857b0d46a485f7551a3e56f44546370f8'
          'd0b9f816c05b0b3cac352fce29ca6920ef598bb2')

prepare() {
	cd "${srcdir}"/spaetz-offlineimap-*
	patch -p1 -i ../00-fix-readonly.patch
	patch -p1 -i ../01-properly-detect-readonly-folders.patch
	find . -type f -exec \
		sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
	sed -i 's/\^/=/g' docs/MANUAL.rst
}

build() {
	cd "${srcdir}"/spaetz-offlineimap-*
	python2 setup.py build
	rst2man2 docs/MANUAL.rst offlineimap.1
}

package() {
	cd "${srcdir}"/spaetz-offlineimap-*
	python2 setup.py install --root="${pkgdir}" --optimize=1
	install -Dm644 offlineimap.1 "${pkgdir}"/usr/share/man/man1/offlineimap.1
	install -Dm644 offlineimap.conf "${pkgdir}"/usr/share/offlineimap/offlineimap.conf
	install -Dm644 offlineimap.conf.minimal "${pkgdir}"/usr/share/offlineimap/offlineimap.conf.minimal
}
