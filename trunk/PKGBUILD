# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=monero-gui
pkgver=0.17.0.1
_commit=afc2e846fd280248610ab56eb5e39966f0305d5d
pkgrel=1
pkgdesc="QT GUI wallet for Monero: the secure, private, untraceable peer-to-peer currency"
license=('BSD')
arch=('x86_64')
url="https://getmonero.org/"
depends=('monero' 'boost-libs' 'libunwind' 'openssl' 'readline' 'zeromq' 'pcsclite' 'hidapi' 'unbound' 'hicolor-icon-theme'
    'qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-svg' 'qt5-xmlpatterns')
makedepends=('git' 'cmake' 'boost' 'qt5-tools')
source=(
    "git+https://github.com/monero-project/monero-gui?signed#commit=${_commit}"
    "git+https://github.com/monero-project/monero"
    "git+https://github.com/monero-project/unbound.git"
    "git+https://github.com/monero-project/miniupnp.git"
    "git+https://github.com/Tencent/rapidjson.git"
    "git+https://github.com/trezor/trezor-common.git"
    "git+https://github.com/tevador/randomx.git"
    "git+https://github.com/monero-project/supercop.git"
    "https://github.com/monero-project/monero-gui/commit/6ed536982953d870010d8fa065dccbffeb6cae50.patch"
    "monero-gui.desktop"
)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'dcc0004ffdb7f73899dcb70e233608c24346599286af5bbfb36f603e3c51208f1a474e92a9aa4e4fd70c3eee9a5d259e97c813aa55d179e499e139b630b418d3'
            '8aa10a47c3217e5bf12bb40a02828bb75cb38e4b5b124a8b05e487e7d3212c132911d5025193d4a7d984aebef94177dee42acf958c11a18b99fd13e872a771e8')
validpgpkeys=('8777AB8F778EE89487A2F8E7F4ACA0183641E010') # luigi1111 <luigi1111w@gmail.com>

prepare() {
  cd "${pkgname}"
  patch -Np1 < ../6ed536982953d870010d8fa065dccbffeb6cae50.patch
  git submodule init
  git config submodule.monero.url "$srcdir/monero"
  git submodule update
  cd monero
  git config submodule.external/unbound.url "$srcdir/unbound"
  git config submodule.external/miniupnp.url "$srcdir/miniupnp"
  git config submodule.external/rapidjson.url "$srcdir/rapidjson"
  git config submodule.external/trezor-common.url "$srcdir/trezor-common"
  git config submodule.external/randomx.url "$srcdir/randomx"
  git config submodule.external/supercop.url "$srcdir/supercop"
  git submodule update
}

build() {
  cd "${pkgname}"
  ARCH=default make release
}

package() {
  cd "${pkgname}"
  install -Dm755 build/release/bin/monero-wallet-gui -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  install -Dm644 ../monero-gui.desktop -t "${pkgdir}/usr/share/applications"
  for x in 16 24 32 48 64 96 128 256; do
    install -Dm644 "images/appicons/${x}x${x}.png" "${pkgdir}/usr/share/icons/hicolor/${x}x${x}/apps/monero-gui.png"
  done
}

# vim: ts=2 sw=2 et:
