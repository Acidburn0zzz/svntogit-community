# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: see .contrib

pkgname=snd
pkgver=18.6
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('fftw' 'gsl' 'gtk3' 'jack' 'libmpc' 'ruby')
makedepends=('flac' 'gmp' 'glu' 'ladspa' 'libpulse' 'libxpm' 'mpg123'
'openmotif' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'pulseaudio: For pulseaudio support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('c3f5aa175bbd92c9c8f442d0147a708485417ffd1e79c04f868b76528b3c0f9511b31997e19697b1bfa30e9712f495fd7159ae2455f01d677e98f31591e686b8')

build() {
  cd "${pkgname}-${pkgver}"
#  LDFLAGS+=" -Wl,-z,noexecstack"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gmp \
              --with-gtk \
              --with-jack \
              --with-ladspa \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -vDm 644 HISTORY.Snd  "${pkgdir}/usr/share/doc/${pkgname}/HISTORY"
  install -vDm 644 NEWS "${pkgdir}/usr/share/doc/${pkgname}/NEWS"
  install -vDm 644 README.Snd  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim:set ts=2 sw=2 et:
