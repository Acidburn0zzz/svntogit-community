# Contributor: Jared Casper <jaredcasper@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pcb
pkgver=20110918
pkgrel=1
pkgdesc="An interactive printed circuit board editor for the X11 window system."
url="http://pcb.gpleda.org/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('tcl' 'gtkglext' 'gd' 'perlxml' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('tk: for the graphical QFP footprint builder')
install=$pkgname.install
changelog=Changelog
source=(http://downloads.sourceforge.net/pcb/$pkgname-$pkgver.tar.gz)
md5sums=('54bbc997eeb22b85cf21fed54cb8e181')

build() {
  cd $srcdir/$pkgname-$pkgver/
  if [ ! -x /usr/bin/wish ]; then
     config="env WISH=/usr/bin/true ./configure --enable-dbus"
  else
     config="./configure --enable-dbus"
  fi
  $config --prefix=/usr \
          --disable-update-mime-database \
          --disable-update-desktop-database
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver/
  make -j1 prefix=$pkgdir/usr install
  rm $pkgdir/usr/share/info/dir
}

