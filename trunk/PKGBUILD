# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.7.0
pkgrel=2
pkgdesc="Access the Mediathek of many German TV stations"
arch=('any')
url="https://github.com/mediathekview/MediathekView"
license=('GPL3')
depends=('bash' 'hicolor-icon-theme' 'java-runtime>=15')
provides=('mediathek')
conflicts=('mediathek')
replaces=('mediathek')
makedepends=('maven')
optdepends=('libnotify: to use desktop notifications'
            'mplayer: for recording streams'
            'vlc: for stream playback')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "de.${pkgname}.${_name}.desktop"
        "${pkgname}.sh")
sha512sums=('f456422deaa6342812bfc6dfd0f7d4082e2182852766f09ede0e55a2f6bbd68d10a3f57f3412e0a198e11c2e8309af37e0006264338d2189c93b9bf16dad4d59'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            'c70e7ffc83ef6f05552edcf90f69461b543af9b601983ceeb4deaf3d372fe9d3ab0750928dd73db4da29727583c7814202d2a6bbf6d0b51642bacf14c972666c')
b2sums=('aa15936f4274fb60e48846838d33c4d49378208a8aecc51c137cefa25546566ad70628efb59275b7ffc5c47557f01d70ceb043d7eee1d965855fd816aafcc309'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        '12b16d33c4c0e1ab6f6b7963ad2d9cc508116cc512521bdfa7b361c2443154558ccfacf42be3032c261392c6d60424021cb69fb9753821bd063c21911b2cae81')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  ./mvnw clean install -Plinux,install4j-linux
}

package() {
  local _size
  cd "$pkgname-$pkgver"
  # jar
  install -vDm 644 "target/${_name}.jar" \
    -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
