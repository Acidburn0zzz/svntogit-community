# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-movie
epoch=1
pkgver=5.0.0
pkgrel=1
pkgdesc='Movie player based on QtAV'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-movie-reborn"
license=('GPL3')
depends=('deepin-qt5integration' 'mpv' 'ffmpegthumbnailer')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-movie-reborn/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/deepin-movie-reborn/pull/48.patch)
sha512sums=('ff06c3bfd498f186141c54bf63bcd9bb44d9ac2c70b6cb41ce4b399cfd65047f0784f8238b7ba61b3b984f3250d11e0d0934617ee17e898b69403dc98e54e1cd'
            'ca94d3674cf2203daf9a2df1c3afa83ff2d26098c4058c58807c59748cb88c3bdd721d0e904f1f79acf403b3ea7780a6c3f00c276e5a85fe5a86792b12a2321e')

prepare() {
  cd deepin-movie-reborn-$pkgver
  # Suggested by upstream, waiting for a real fix
  patch -p1 -i ../48.patch
}

build() {
  cd deepin-movie-reborn-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-movie-reborn-$pkgver
  make DESTDIR="$pkgdir" install
}
