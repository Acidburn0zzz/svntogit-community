# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=zn_poly
pkgver=0.9.1
pkgrel=1
pkgdesc="Polynomial arithmetic over Z/nZ"
arch=(x86_64)
url="http://web.maths.unsw.edu.au/~davidharvey/code/zn_poly/"
license=(GPL)
depends=(gmp)
makedepends=(python2)
source=("http://mirrors.mit.edu/sage/spkg/upstream/zn_poly/$pkgname-$pkgver.tar.gz")
sha256sums=('d472965ddee240c701ae5822897a2a13eb8a4184864edabcb6c5b0ac34b0f74e')

build() {
  python2 makemakefile.py --prefix=/usr --cflags="$CFLAGS -fPIC" --cxxflags="$CXXFLAGS" --ldflags="$LDFLAGS" > makefile
  make
  make libzn_poly.so
}

package() {
  python2 makemakefile.py --prefix="$pkgdir"/usr > makefile
  make install
# Install shared library
  cp libzn_poly.so "$pkgdir"/usr/lib
}
