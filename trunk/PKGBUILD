# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openxr
pkgver=1.0.12
pkgrel=1
pkgdesc='OpenXR'
arch=('x86_64')
url='https://www.khronos.org/openxr/'
provides=("openxr-loader")
depends=("jsoncpp" "libx11")
makedepends=('cmake' 'git' 'vulkan-headers' 'libx11' 'libxxf86vm' 'libxrandr' 'python' 'vulkan-icd-loader' 'ninja')
license=('APACHE')
source=("https://github.com/KhronosGroup/OpenXR-SDK-Source/releases/download/release-${pkgver}/OpenXR-SDK-Source-release-${pkgver}.tar.gz"{,.asc})
validpgpkeys=(F029455EAB70F520FF4A03BD7F534CD56F00321E)
sha512sums=('e7c358dbd730ef24798993225e882cf35fc474426536aee9f54cbe610422f70c467b3c83876b993d1a45f0d8c3b6869f93efba8fb356baf64b5ade73dee885c9'
            'SKIP')

build() {
  cd OpenXR-SDK-Source-release-${pkgver}

  cmake \
    -GNinja \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DBUILD_TESTS=ON \
    -DPRESENTATION_BACKEND=xlib \
    -DDYNAMIC_LOADER=ON
  ninja -C build
}

package() {
  cd OpenXR-SDK-Source-release-${pkgver}

  DESTDIR="$pkgdir" ninja -C build install
}
