# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ismael Carnales <icarnales at gmail dot com>
# Contributor: Sergey Mastykov <smastykov[at]gmail[dot]com>

pkgbase=python-pycodestyle
pkgname=('python-pycodestyle' 'python2-pycodestyle')
pkgver=2.7.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://pycodestyle.pycqa.org"
license=('custom:Expat')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://github.com/PyCQA/pycodestyle/archive/$pkgver/$pkgbase-$pkgver.tar.gz")
sha512sums=('9066ab243f98f5fe6f4d764289b5be2bd2a2b063e6273bd977025509faa2eabd197480b7a2522d4de89b0c6cec388950dad3c69c6d1aa3184883e00909815a2d')

prepare() {
  cp -a pycodestyle-$pkgver{,-py2}

  sed -i '1s/env python$/python2/' pycodestyle-$pkgver-py2/pycodestyle.py
}

build() {
  cd "$srcdir"/pycodestyle-$pkgver
  python setup.py build

  cd "$srcdir"/pycodestyle-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pycodestyle-$pkgver
  python -m unittest discover

  cd "$srcdir"/pycodestyle-$pkgver-py2
  python2 -m unittest discover
}

package_python-pycodestyle() {
  depends=('python-setuptools')
  replaces=('pep8-python3' 'pep8')

  cd pycodestyle-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir/"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pycodestyle() {
  depends=('python2-setuptools')
  replaces=('pep8-python2' 'python2-pep8')

  cd pycodestyle-$pkgver-py2
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir/"
  mv "$pkgdir"/usr/bin/pycodestyle{,2}

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
