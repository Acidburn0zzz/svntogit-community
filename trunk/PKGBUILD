# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.0.6
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-tool-kit' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center' 'startdde' 'gtk2' 'xorg-xrandr' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
provides=('lightdm-deepin-greeter')
conflicts=('dde-workspace' 'dde-session-ui')
replaces=('dde-workspace' 'dde-session-ui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz"
        issue54075.patch)
sha512sums=('b16bdf0085872a781e26b6cbd0604809f0a435a3a0152e325f0fac725ecf0b77981aa7b7f541d0f3dd1713a617c7786fad77c408d74c73606c21c5bebf54e66e'
            '000368298dbf31e806f2bc814ae74e4a5f561a7b2d6cf6083b82ab817b10c95a1ee6e3f6eb7eb58e9f92e19de8aac21ef4ac7cfe24afdc9b5767c18dc39deed7')

prepare() {
  cd dde-session-ui-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/286
  patch -p1 -i ../issue54075.patch
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
