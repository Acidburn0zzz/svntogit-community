# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-stretchr-objx
pkgver=0.1.1.20190212
_commit=c61a9dfcced1815e7d40e214d00d1a8669a9f58c
pkgrel=1
pkgdesc='Go package for dealing with maps, slices, JSON and other data.'
arch=('any')
url="https://github.com/stretchr/objx"
license=('MIT')
depends=('go-pie')
source=("$pkgname-$_commit.tar.gz::https://github.com/stretchr/objx/archive/$_commit.tar.gz")
sha512sums=('cab0b4e28c82a9dc0a59abb0c33124b84b6069c73e9bf456abfd27f8584054a93ec7374fab48b7dabe94c6792f1a0fd6d1b70d7c15e8962fedc40cb878dff445')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/stretchr
  cp -a "$srcdir"/objx-$_commit "$srcdir"/build/src/github.com/stretchr/objx
  cd "$srcdir"/build/src/github.com/stretchr/objx
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/stretchr
  cp -a "$srcdir"/objx-$_commit "$pkgdir"/usr/share/gocode/src/github.com/stretchr/objx

  install -Dm644 objx-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
