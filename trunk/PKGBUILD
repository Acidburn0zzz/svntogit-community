# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-polkit
pkgver=1.22.0
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="https://mate-desktop.org"
arch=('x86_64')
license=('LGPL')
groups=('mate')
depends=('gtk3' 'polkit')
makedepends=('accountsservice' 'gobject-introspection' 'intltool')
conflicts=('mate-polkit-gtk3')
replaces=('mate-polkit-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('c0cb8a2bb01e3afcbd56557563491ecf7fa8dad6e419a7a1e01b52d7c3b1280b')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--libexecdir=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--enable-introspection

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
