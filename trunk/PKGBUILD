# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.4.26
pkgrel=1
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-wallpapers' 'kwin')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch
        dde-kwin.5.4.26.patch)
sha512sums=('0ce42973d5c4fb861e758f27b7afc3ebe3c743b7157b527c08771bc5d624e124914fbc92ad9e1a9845cc8d302f83352d6991cb287bf2447dbab6a2e87013809c'
            '07be0f26270c7b87b311d402c002fcc3d6c73eff6a80abdc33fe1b7b1b4a8acc502a0c1c3957d92cf39b5a3c2e1f1c689c90fe0f765f7bd8308f87ee9cbf138b'
            '8ab15772a8707c30c14e1005434016ef711e7c47ad98ad2813fb5213b50abf5b0b7be81a6e9b72ad6f62cb63e48f96810a87c815b7fef6d8b01206a670e9127a')

prepare() {
  cd dde-kwin-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' plugins/kwineffects/multitasking/background.cpp
  patch -p1 -i ../dde-kwin.5.4.26.patch
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
}

build() {
  cd dde-kwin-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DUSE_KWIN_NO_SCALE=OFF -DUSE_PLUGINS=OFF -DUSE_WINDOW_TOOL=OFF
  ninja
}

package() {
  cd dde-kwin-$pkgver
  DESTDIR="$pkgdir" ninja install
}
