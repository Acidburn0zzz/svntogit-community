# $Id$
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Bodor Dávid Gábor <david.gabor.bodor@gmail.com>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-scipy
_app=scipy
pkgname=('python-scipy' 'python2-scipy')
pkgver=0.9.0
_theapp=$_app-$pkgver
pkgrel=2
pkgdesc="SciPy is open-source software for mathematics, science, and engineering."
arch=('i686' 'x86_64')
url="http://www.scipy.org/"
license=('BSD')
depends=('python-numpy')
makedepends=('gcc-fortran' 'python2-numpy')
source=("http://downloads.sourceforge.net/$_app/$_theapp.tar.gz")
md5sums=('ebfef6e8e82d15c875a4ee6a46d4e1cd')

build() {
  unset LDFLAGS
  unset FFLAGS

  # Compatibility symlinks for current stable numpy
  #   - no need to patch numpy just for this
  # see http://projects.scipy.org/numpy/ticket/1749
  ln -sf /usr/lib/liblapack.so liblapack.cpython-32mu.so
  ln -sf /usr/lib/libblas.so libblas.cpython-32mu.so
  export LAPACK="$srcdir"
  export BLAS="$srcdir"

  # Changing the arithmetic parameter (Thanks to Fabrizio Castellano)
  sed -i "s/\#define\ UNK\ 1/\#define\ IBMPC\ 1/" \
    $_theapp/$_app/special/cephes/mconf.h

  # 2 builds
  cp -r $_theapp $_theapp-py2

  # build for python3
  cd $_theapp
  python setup.py config_fc --fcompiler=gnu95 build

  # build for python2
  cd ../$_theapp-py2

  for file in $(find . -name '*.py' -print); do
       sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
       sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  python2 setup.py config_fc --fcompiler=gnu95 build
}

package_python-scipy() {
  provides=('python3-scipy' 'scipy')

  cd "$srcdir/$_theapp"

  python setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-scipy() {
  depends=('python2-numpy')
  conflicts=('python-scipy<0.9.0')

  cd "$srcdir/$_theapp-py2"

  python2 setup.py config_fc --fcompiler=gnu95 install \
    --prefix=/usr --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
