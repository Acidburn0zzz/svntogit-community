# Maintainer: David Runge <dave@sleepmap.de>
pkgname=lsp-plugins
pkgver=1.1.4
pkgrel=1
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=('x86_64')
url="https://lsp-plug.in"
license=('LGPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'glu' 'jack' 'libglvnd' 'libsndfile')
makedepends=('ladspa' 'lv2' 'php')
source=("https://github.com/sadko4u/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz"
        'relro_pie.patch')
sha512sums=('214af5c9cb3adf5013359768356594ba9e8d2ee214e9b10e2f51d7b1b51e6c7a7aaebff826149dcd7a9fa945cbac623422c53e46165b2091f5c35ca0d0807617'
            '8801dc4f00ec5feb3766b532db413462e4d0c64329c8ce1941d875705c99d772d60796fa88aa92c94e59be8324b8a1ff88664d98d2e5541b26f56fa8b1654ae0')

prepare() {
#  local _relro="-Wl,-z,relro,-z,now"
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../relro_pie.patch
  # removing /usr/local/lib from RPATH
  sed -e '/LD_PATH/ s|:/usr/local/lib||' \
      -i Makefile
}

build() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  make
  make OBJDIR=${_test_path} clean
  make OBJDIR=${_test_path} test
}

check() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  ${_test_path}/lsp-plugins-test utest --nofork --debug --verbose
}

package() {
  cd "$pkgname-$pkgver"
  make BIN_PATH=/usr/bin \
       LIB_PATH=/usr/lib \
       DOC_PATH=/usr/share/doc \
       DESTDIR="$pkgdir/" \
       install
}
