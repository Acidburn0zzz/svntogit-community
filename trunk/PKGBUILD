# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=5.0.11
pkgrel=1
pkgdesc='A Real-time Transport Protocol (RTP) library'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/ortp'
license=(GPL3)
depends=(bctoolbox)
makedepends=(cmake doxygen)
source=(https://github.com/BelledonneCommunications/ortp/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('dff1afe20a36c7285f135ba915c852ed49aaa469831de4cb113709886c1c39c8')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
