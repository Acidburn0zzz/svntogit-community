# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.7.0
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=('i686' 'x86_64')
url="https://amsynth.github.io/"
license=('GPL')
groups=('dssi-plugins' 'lv2-plugins')
depends=('gtkmm' 'jack' 'liblo')
makedepends=('dssi' 'ladspa' 'intltool')
source=("https://github.com/amsynth/amsynth/releases/download/release-$pkgver/amsynth-$pkgver.tar.bz2")
md5sums=('4856f582caed59fca79dfe3ca4597938')

build() {
  cd $pkgname-$pkgver

  # enable required C++11 support
  export CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
