# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Simone Sclavi 'Ito' <darkhado@gmail.com>

_pkgbasename=libjpeg-turbo
pkgname=lib32-$_pkgbasename
pkgver=2.0.4
pkgrel=1
pkgdesc="libjpeg derivative with accelerated baseline JPEG compression and decompression (32-bit)"
arch=('x86_64')
url="https://libjpeg-turbo.org/"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('nasm' 'cmake')
provides=('lib32-libjpeg=8.2.2' 'lib32-turbojpeg')
conflicts=('lib32-libjpeg' 'lib32-turbojpeg')
replaces=('lib32-libjpeg' 'lib32-turbojpeg')
validpgpkeys=('7D6293CC6378786E1B5C496885C7044E033FDE16')
source=(https://sourceforge.net/projects/$_pkgbasename/files/$pkgver/$_pkgbasename-$pkgver.tar.gz{,.sig})
sha512sums=('708c2e7418d9ed5abca313e2ff5a08f8176d79cad2127573cda6036583c201973db4cfb0eafc0fc8f57ecc7b000d2b4af95980de54de5a0aed45969e993a5bf9'
            'SKIP')

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake -DWITH_JPEG8=1 \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib32 \
        .

  make
}

check() {
  cd "$_pkgbasename-$pkgver"
  make test
}

package() {
  cd "$_pkgbasename-$pkgver"

  make DESTDIR="$pkgdir/" docdir=/usr/share/doc/libjpeg-turbo \
    exampledir=/usr/share/doc/libjpeg-turbo install

  rm -rf "${pkgdir}"/usr/{include,share,bin,sbin}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 ChangeLog.md -t"${pkgdir}/usr/share/doc/${pkgname}"
}
