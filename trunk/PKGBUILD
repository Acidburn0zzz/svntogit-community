# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.6
pkgrel=2
pkgdesc="Linux /proc abstraction classes for python"
arch=('any')
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git/"
license=('GPL2')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://git.kernel.org/pub/scm/libs/python/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ca5c9dc2b9b0869557713e10ab3b431a88e23153c1c13f08976deac4333c08b7c1d3813a9e660985113f8a4831725a83ec0239fcc7069c3f5faa2464b8212518')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  python bitmasklist_test.py
}

package() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}
