# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.0.3
pkgrel=1
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('desktop-file-utils' 'ffmpegsource' 'fftw' 'hicolor-icon-theme' 'hunspell' 'lua51' 'wxgtk2.9' 'hicolor-icon-theme')
makedepends=('mesa')
install=aegisub.install
source=("http://ftp.aegisub.org/pub/releases/${pkgname}-${pkgver}.tar.xz")
sha256sums=('9623e1967ae774d8a34fae531dcf76f15ae060f864cf941d34ba3d64e1986efc')

build() {
  cd "${srcdir}"/${pkgname}/${pkgname}

  ACLOCAL=aclocal AUTOMAKE=automake ./autogen.sh --prefix=/usr --without-{portaudio,openal,oss} --with-wxdir=/usr/include/wx-2.9 --with-wx-config=/usr/bin/wx-config-2.9
  make
}

package() {
  cd "${srcdir}"/${pkgname}/${pkgname}

  make DESTDIR="$pkgdir" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
