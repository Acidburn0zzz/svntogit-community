# Maintainer: David Runge <dave@sleepmap.de>
pkgname=wiiuse
pkgver=0.15.4
pkgrel=1
pkgdesc="A library written in C that connects with several Nintendo Wii remotes"
arch=('x86_64')
url="htps://github.com/wiiuse/wiiuse"
license=('GPL3')
depends=('bluez-libs')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('a57147ec86bbbd245500d438a790a68e4692d23509f1068494040a98fece5b5cf4cb71c09b3c07ef1fdbab608bf0cc25fecb291478e68f3cc127d263fb4c4b6e')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver/build"
  cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd "$pkgname-$pkgver/build"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 ../{{CHANGELOG,README}.mkd,RELEASE.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -v "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
