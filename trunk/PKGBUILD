# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=ansible
pkgver=2.2.1.0rc5
_pkgver1=2.2.1.0
_pkgver2=2.2.1.0-0.5.rc5
pkgrel=2
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='http://www.ansible.com'
license=('GPL3')
depends=('python2' 'python2-yaml' 'python2-paramiko' 'python2-jinja' 'python2-crypto')
optdepends=('python2-passlib: crypt values for vars_prompt'
            'python2-netaddr: for the ipaddr filter'
            'python2-systemd: log to journal')
makedepends=('asciidoc' 'fakeroot')
backup=('etc/ansible/ansible.cfg')
source=("https://releases.ansible.com/ansible/ansible-${_pkgver2}.tar.gz")
sha512sums=('5e2078181e54e6713ea0674fa7c6334023e65354334c192f19945631e4215cf8cc871e93f3e1c2af411c053c4185f3e6ba81f24c0ee7b59d75c9144fa7500f5b')

build() {
  cd ${pkgname}-${_pkgver1}
  python2 setup.py build
}

package() {
  cd ${pkgname}-${_pkgver1}
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/ansible/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}
