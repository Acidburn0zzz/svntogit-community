# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgname=gmic
pkgver=1.4.7.1
pkgrel=1
pkgdesc="GMIC image procession framework"
depends=('fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
arch=('i686' 'x86_64')
license=('custom:CeCILL')
url="http://gmic.sourceforge.net"
replaces=('greycstoration')
options=('docs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz"
	"build-fix.patch")
md5sums=('82e1db2c8767b64e626239e56b2339a4'
         'b54ace496079ea66163aa521ead1f8c7')

build() {
  cd "$srcdir/gmic-$pkgver"

  patch -p1 <$srcdir/build-fix.patch

  # Enable lapack optimizations
  CFLAGS+=" -Dcimg_use_lapack"
  LDFLAGS+=" -llapack"

  cd src
  make linux
}

package() {
  cd "$srcdir/gmic-$pkgver"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cd src
  make install USR=/usr DESTDIR=$pkgdir
}
