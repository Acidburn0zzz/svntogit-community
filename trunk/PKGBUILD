# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: zlowly <zlowly@gmail.com>

pkgname=smartdns
pkgver=30
pkgrel=1
pkgdesc="A local DNS server to obtain the fastest website IP for the best Internet experience"
arch=('x86_64')
license=('GPL')
url="https://github.com/pymumu/smartdns"
backup=("etc/smartdns/smartdns.conf")
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pymumu/smartdns/archive/Release$pkgver.tar.gz"
        systemd.patch)
sha512sums=('f9b2f8947822da1dfb84a2d4c7a8175690d7425ceedaf7217ddb25b79d16677100385a505326c91f85928020aaa66f4c862678132ca415c319325f54913174c0'
            '8868441c983fe4426f3876a1fa6f1e89bfde2ede78b7fae564cd840a269523b6952c1fbd7f44a562d0c875e35d6d563b335e1f0503e7b49d0aed0fa9655abdab')

prepare() {
  patch -p1 -i systemd.patch smartdns-Release$pkgver/systemd/smartdns.service
}

build() {
  cd smartdns-Release$pkgver/src
  make
}

package() {
  cd smartdns-Release$pkgver
  install -Dm755 src/smartdns "$pkgdir"/usr/bin/smartdns
  install -Dm644 etc/smartdns/smartdns.conf "$pkgdir"/etc/smartdns/smartdns.conf
  install -Dm644 etc/default/smartdns "$pkgdir"/etc/default/smartdns
  install -Dm644 systemd/smartdns.service "$pkgdir"/usr/lib/systemd/system/smartdns.service
}
