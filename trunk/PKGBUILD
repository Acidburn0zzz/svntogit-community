# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64@gmail.com>

pkgname=mtpaint
pkgver=3.50.01
pkgrel=1
pkgdesc='Paint program for creating icons and pixel art'
arch=(x86_64)
url='https://github.com/wjaguar/mtPaint'
license=(GPL3)
makedepends=(git)
depends=(giflib gtk3 openjpeg2)
optdepends=('gifsicle: export GIF files from mtPaint')
source=("git+$url#commit=a4675ff5cd9fcd57d291444cb9f332b48f11243f") # version: 3.50.01
sha256sums=('SKIP')

build() {
  cd mtPaint
  export CFLAGS="-w $(pkg-config openjp2 --cflags) $CFLAGS"
  ./configure gtk3 --prefix=/usr --mandir=/usr/share/man \
    man intl GIF jpeg tiff cflags lcms2 jp2v2
  make
}

package() {
  make -C mtPaint DESTDIR="$pkgdir" install
}
