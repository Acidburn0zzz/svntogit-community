# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.11.2
pkgrel=1
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm-libs' 'clang' 'bcc' 'libbpf')
makedepends=('cmake' 'llvm' 'git' 'linux-headers')
options=(!strip)
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz"
  bpf1.patch::https://github.com/iovisor/bpftrace/commit/5e78eb8a733a83398d2c8cbbf7ed6b81e8123d41.patch
  bpf2.patch::https://github.com/iovisor/bpftrace/commit/a2440d7860b8c156bffae999ea0c444bcae751f6.patch
)
sha512sums=('25bcc9863d24646108252f0010b96afb823eb795c8c6d94112b7804740d7688139ead7d862a205a1b44b86ef135b412e533fc119939ce550e5509b1bd37952fa'
            '2511e19782a44f1242753dc3e2b45f2b7cb98796c763cc486cdadf9af2168f5a47466d59423d02f32cb8872d682a8fccdc433f0c4c61afa252234a9752fb3a2b'
            '649be95f454407c0c91cf755ec69402a2615908c01066b5eac5d4c08df98cd1203da9de17ab956e3f9fd2daf1fcb5a2411ab47ce540a9da184d347f501c36707')

prepare() {
  cd bpftrace-$pkgver

  # https://github.com/iovisor/bpftrace/issues/1610
  patch -p1 < ../bpf1.patch
  patch -p1 < ../bpf2.patch
}

build() {
  cd bpftrace-$pkgver

  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd bpftrace-$pkgver/build

  make DESTDIR="$pkgdir" install
}
