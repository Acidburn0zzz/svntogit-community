# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel 'angvp' Velasquez <angvp@archlinux.org>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@gmail.com>
# Contributor: Patrick Melo <patrick@patrickmelo.eti.br>

pkgname=geany-plugins
pkgver=1.34
pkgrel=1
pkgdesc='Plugins for Geany'
arch=('x86_64')
url='https://plugins.geany.org/'
license=('GPL')
depends=('ctpl' "geany>=$pkgver" 'gpgme' 'gtkspell3' 'libgit2' 'lua51' 'python')
makedepends=('cppcheck' 'intltool')
source=("https://plugins.geany.org/$pkgname/$pkgname-$pkgver.tar.bz2"{,.sig})
sha256sums=('244417c681636e82a63cf6f9901b5b7f3889168f7d9b5ba9b45601767a589c3b'
            'SKIP')
validpgpkeys=('6D0E68FCE198824C27C90EB0B507ACD04BA283C9') # Frank Lanitz <frank@geany.org>

prepare() {
  cd $pkgname-$pkgver

  autoreconf -fi
}

build() {
  cd "$pkgname-$pkgver"

  export PYTHON=/usr/bin/python
  ./configure --prefix=/usr --libexecdir=/usr/lib
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# getver: plugins.geany.org/downloads.html
# vim: ts=2 sw=2 et:
