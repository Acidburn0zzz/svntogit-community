# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-files
pkgver=4.0
pkgrel=2
pkgdesc='The Pantheon File Browser'
arch=(x86_64)
url='https://github.com/elementary/files'
license=(GPL3)
groups=(pantheon)
depends=(
  atk
  cairo
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgee
  libgranite.so
  libnotify
  pango
  sqlite
  zeitgeist
)
makedepends=(
  cmake
  git
  gnome-common
  ninja
  vala
)
optdepends=(
  'contractor: Various context menu entries'
  'gvfs: Cleaner devices section'
  'tumbler: Thumbnails generation'
)
source=(pantheon-files::git+https://github.com/elementary/files.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../pantheon-files \
    -G Ninja \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DCMAKE_SKIP_RPATH='TRUE' \
    -DGSETTINGS_COMPILE='FALSE'
  cmake --build .
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install
}

# vim: ts=2 sw=2 et:
