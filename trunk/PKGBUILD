# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ocrfeeder
pkgver=0.7.8
pkgrel=1
pkgdesc="GTK+ document layout analysis and optical character recognition application"
arch=('any')
url="http://live.gnome.org/OCRFeeder"
license=('GPL')
depends=('python2-pyenchant' 'pygoocanvas' 'python2-gtkspell' 'python-imaging' 'python-reportlab' 'ghostscript' 'unpaper' 'sane' 'desktop-file-utils')
makedepends=('intltool' 'gnome-doc-utils')
optdepends=('tesseract: character recognition'
            'ocrad: character recognition'
            'gocr: character recognition'
            'cuneiform: character recognition')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('7ef98de31ca278e2ea17406d351304d434b82aa711aa25092a2e7cfe7b2bead7')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/ocrfeeder*.in src/ocrfeeder/odf/*.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-scrollkeeper \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../ocrfeeder/icons/ocrfeeder.svg "$pkgdir/usr/share/pixmaps/ocrfeeder.svg"
}
