# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=teeworlds
pkgver=0.7.0
pkgrel=1
pkgdesc='Fast-paced multiplayer 2D shooter game'
arch=('x86_64')
url='https://www.teeworlds.com'
license=('custom')
depends=('alsa-lib' 'glu' 'sdl2' 'freetype2')
makedepends=('python' 'bam' 'mesa' 'imagemagick' 'gendesk')
source=("https://github.com/teeworlds/teeworlds/archive/${pkgver}.tar.gz"
        "https://github.com/matricks/bam/archive/v0.5.1.tar.gz")
sha512sums=('5ec82e097aa1f45ca90818b7d91273a2e9066c5b4d940141c05d828ce26ea4d0eea61180c008dd55052d54e4555001c136a2eb33fc5a8ca5f62ffe3340fdf047'
            'e6f1b3daad6073c58b0e3cbf836bb0a6b66f0c36532d6e6eca9949239ab8b584cc88f892cce6f74974e370a8a688f33a95dde86dd65cc1790e49e5f8aeab0fef')

prepare() {
  convert +set date:create +set date:modify "$pkgname-$pkgver/other/icons/Teeworlds.ico" "$srcdir/$pkgname.png"
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name 'Teeworlds' --categories 'Game;ArcadeGame'
}

build() {
  cd bam-0.5.1
  ./make_unix.sh
  cd ..

  cd "$pkgname-$pkgver"
  ../bam-0.5.1/bam conf=release
}

package() {
  cd "$pkgname-$pkgver"

  # Install data files
  mkdir -p ${pkgdir}/usr/share/${pkgname}/data
  cp -r build/x86_64/release/data/* ${pkgdir}/usr/share/${pkgname}/data

  install -Dm755 build/x86_64/release/${pkgname} ${pkgdir}/usr/bin/${pkgname}
  install -Dm755 build/x86_64/release/${pkgname}_srv ${pkgdir}/usr/bin/${pkgname}_srv

  install -Dm644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop
  install -Dm644 ${srcdir}/${pkgname}-0.png ${pkgdir}/usr/share/pixmaps/${pkgname}.png

  install -Dm644 license.txt ${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}

# vim:set ts=2 sw=2 et:
