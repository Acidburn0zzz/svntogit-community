# Maintainer: Anatol Pomozov
# Contributor: László Várady <laszlo.varady93@gmail.com>

pkgname=rocksdb
pkgver=6.5.3
pkgrel=1
pkgdesc='Embedded key-value store for fast storage'
arch=(x86_64)
url="https://rocksdb.org/"
license=(GPL2 Apache)
depends=(bzip2 gcc-libs gflags jemalloc lz4 snappy zlib zstd)
makedepends=(cmake)
#checkdepends=('python2')
source=(rocksdb-$pkgver.tar.gz::https://github.com/facebook/rocksdb/archive/v$pkgver.tar.gz)
sha256sums=('6dc023a11d61d00c8391bd44f26ba7db06c44be228c10b552edc84e02d7fbde2')

build() {
  cd rocksdb-$pkgver
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_BZ2=ON -DWITH_LZ4=ON -DWITH_SNAPPY=ON -DWITH_ZLIB=ON -DWITH_ZSTD=ON \
        -DUSE_RTTI=ON -DWITH_JEMALLOC=ON -DWITH_TESTS=OFF -DFAIL_ON_WARNINGS=OFF -DPORTABLE=ON \
	-S . -B build
  cmake --build build
}

check() {
  cd rocksdb-$pkgver
  # cmake --build build --target test
}

package() {
  cd rocksdb-$pkgver
  cmake --build build --target install -- DESTDIR="$pkgdir"
}
