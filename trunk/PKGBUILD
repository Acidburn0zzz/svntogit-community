# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: getzze <getzze at gmail dot com>
pkgname=python-tensorflow-serving-api
_name=tensorflow_serving_api
_commit=7bbcc6d
pkgver=1.12.0.git1.${_commit}
pkgrel=1
pkgdesc="Flexible, high-performance serving system for machine learning models, designed for production environments"
arch=(any)
url="https://www.tensorflow.org/serving/"
license=('Apache')
depends=('python' 'python-grpcio' 'python-tensorflow' 'python-protobuf')
makedepends=('bazel' 'python-wheel' 'python-pip')
source=("https://github.com/tensorflow/serving/archive/${_commit}.tar.gz")
sha256sums=('e64c1367d9d54cc3ff657b0887b09be5c6ba0e28f6ddbaa76ccc5d9934a8a420')

build() {
  cd serving-*
  bazel build -c opt tensorflow_serving/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_serving/tools/pip_package/build_pip_package /tmp/pip
}

package() {
  cd serving-*
  pip install --ignore-installed --upgrade --root "${pkgdir}"/ /tmp/pip/tensorflow_serving*.whl --no-dependencies
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
