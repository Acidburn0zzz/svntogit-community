# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=pageedit
pkgver=0.7.1
pkgrel=1
pkgdesc="Simple Single Page XHTML Visual Editor, split from Sigil's BookView"
arch=('x86_64')
url="https://github.com/Sigil-Ebook/PageEdit"
license=('GPL3')
depends=('qt5-webengine')
makedepends=('cmake' 'qt5-tools')
source=("${pkname}-${pkgver}")
source=("PageEdit-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "${url}/releases/download/${pkgver}/PageEdit-${pkgver}.tar.gz.sig")
sha256sums=('0d6641e8e3a7791026c067a3bea44e7e0171938de08841f9b32ec81b8c981ca1'
            'SKIP')
b2sums=('e9b67f392fddcaad72921a62b35b6430f6e4bb31d3c02e1a8248919c9deca96e71ea0d3c4879018ad37ae8c51402d8a14a3a5a2f40a38895d2afcf33543899ec'
        'SKIP')
validpgpkeys=('B5A56206AB0FBC1A24EFAB8AA166D29A8FCDAC63') # Doug Massay <douglaslmassay@gmail.com>

build() {
    mkdir -p "${srcdir}"/PageEdit-${pkgver}/build
    cd "${srcdir}"/PageEdit-${pkgver}/build

    cmake -G "Unix Makefiles" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
        -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
        -DCMAKE_BUILD_TYPE=Release \
        ..
    make
}

package() {
    cd "${srcdir}"/PageEdit-${pkgver}/build

    make install DESTDIR="${pkgdir}"
}
