# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.6.0
pkgrel=1
pkgdesc="Access the Mediathek of many German TV stations"
arch=('any')
url="https://github.com/mediathekview/MediathekView"
license=('GPL3')
depends=('bash' 'hicolor-icon-theme' 'java-runtime=14')
provides=('mediathek')
conflicts=('mediathek')
replaces=('mediathek')
makedepends=('maven')
optdepends=('mplayer: for recording streams'
            'vlc: for stream playback')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "de.${pkgname}.${_name}.desktop"
        "${pkgname}.sh")
sha512sums=('35e8b4c8797acd43ad5594ac121b779c237c5e1d7f007289a8fdfaae921060c2e5ca4568f1be1720b61e6a40a91d5a39a035e6fdfc8a8b67655c9e133b7db6bd'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '14e06bfdfa76517d60e15cef0abf2a483dadbbc6433aa0d3a1ddd998ed3ca958ba17619ee386ee4d41ea98d2d57bf439c2432091aca57b348a697ef978b6c38f')
b2sums=('7521bdb0dfbcc93b78e6fb1fa5dd897e53dc7041ee14af24f048a0c70ca2c5474eb37c2b3c9825f1d577f990bce628380fdcd8d7ae62525b4e6efdba28b000f1'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        '70c56ce2e3d9c606638c68745e22b7361150589314286b01a44c518c6eb73c90ab21ef3a1252c5aabf4c66cfea2b100f411db29ff6101f4dba43c2f60f2d45c6')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  ./mvnw clean install -Plinux,install4j-linux
}

package() {
  local _size
  cd "$pkgname-$pkgver"
  # jar
  install -vDm 644 "target/${_name}.jar" \
    -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
