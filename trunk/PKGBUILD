# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Javier 'Phrodo_00' Aravena <phrodo.00@gmail.com>

pkgname=gbrainy
pkgver=1.61
pkgrel=1
pkgdesc="A brain teaser game and trainer to have fun and to keep your brain trained"
arch=('any')
url="http://live.gnome.org/gbrainy"
license=('GPL2')
depends=('gnome-sharp' 'mono-addins' 'hicolor-icon-theme' 'librsvg')
makedepends=('intltool' 'gettext' 'gnome-doc-utils')
install=gbrainy.install
source=(http://gent.softcatala.org/jmas/$pkgname/$pkgname-$pkgver.tar.gz
        fix-cs0188-compiler-error-in-mono-2.9.patch)
md5sums=('59390c98d434841aef9490d18ed6e390'
         'c829210c56d5712cc5b16262528f5562')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/fix-cs0188-compiler-error-in-mono-2.9.patch"

  export MONO_SHARED_DIR="$srcdir"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
