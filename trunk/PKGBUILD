# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.9.5
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=(x86_64)
url="https://qsynth.sourceforge.io/"
license=(GPL)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt6-base)
makedepends=(cmake fluidsynth qt6-tools)
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('157e49e8ebfcf550754379c4f2bae221354cffbfe47dcaea747749c00520339f59de5fe7f17e105674ecc6099a98af5f2326d2d258fe359c0c7522366f4267d4')
b2sums=('3973c2ea0599b8bf9f12c3ce1bb7eff00b5d20b4f051ebcb578f556dbfce714cbe0bf77038b553289f27cfedc521bd7e179af652c721375e6bedc4d22ce061d5')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DCONFIG_QT6=ON \
        -Wno-dev \
        -B build \
        -S "${pkgname}-${pkgver}"
  make VERBOSE=1 -C build
}

package() {
  depends+=(libfluidsynth.so)

  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "${pkgname}-${pkgver}/"{ChangeLog,README,TRANSLATORS} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
