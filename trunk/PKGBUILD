# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=ncompress
pkgver=4.2.4.5
pkgrel=3
pkgdesc="a fast, simple LZW file compressor"
arch=('x86_64')
url="https://github.com/vapier/${pkgname}"
license=('custom:PublicDomain')
provides=('compress')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz"
        "${url}/commit/0a56673929dde4b761d33724e1dd6b8f1e94c3d5.patch")
sha256sums=('f575bbcdd5f844ce22d753b9acd23d2a6b73ffc15f204bebbaf8bd6f6574903b'
            '41c999bcc579efe5c72ec132b2d1703402a9bd788c22edc2b757bed69c4c364f')
b2sums=('a09661659a72cf4be40e143d8cff53779de7423c0337d8c92863509390d8adc355d660da98db9de431bb4c990f8d10955e19a97188b100450ff9ea2caf19ae8d'
        '8cbcc730879e80014c115333cd3bf4c9d8cf6d50c45899b0b38c942a45a5db6dba076025ee289b89dd36a808fb9a1df19600ba78e4361008ed1b576800293f37')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # let the Makefile be better about installing in a distro-conforming way
    # e.g. use the right mandir, don't clobber gzip programs
    patch -p1 -i ../0a56673929dde4b761d33724e1dd6b8f1e94c3d5.patch
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" PREFIX=/usr install_core
    # an uncompress implementation which handles gzipped files is provided by gzip
    mv "${pkgdir}"/usr/bin/uncompress{,.real}

    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LZW.INFO UNLICENSE
}
