# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-glib-networking
pkgver=2.60.0.1
pkgrel=1
pkgdesc='Network-related giomodules for glib'
arch=('x86_64')
url='https://git.gnome.org/browse/glib-networking/'
license=('GPL2')
depends=('glib-networking' 'lib32-glib2' 'lib32-gnutls' 'lib32-libproxy')
makedepends=('ca-certificates' 'gcc-multilib' 'git' 'intltool' 'meson')
source=("git+https://gitlab.gnome.org/GNOME/glib-networking.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  arch-meson glib-networking build \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32/glib-networking'
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{lib,share}
}

# vim: ts=2 sw=2 et:
