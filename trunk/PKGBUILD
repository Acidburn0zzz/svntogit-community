# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=megaglest
pkgver=3.13.0
pkgrel=1
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world"
arch=('i686' 'x86_64')
url="https://megaglest.org"
license=('GPL3')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl2' 'libvorbis' 'openal' 'libgl' 'lua51' 'icu' 'ftgl' 'glew'
'libircclient' 'miniupnpc' 'wxgtk' 'glu' 'libx11')
makedepends=('ftjam' 'cmake' 'mesa' 'git')
source=("git+https://github.com/MegaGlest/megaglest-source.git#tag=${pkgver}" megaglest-gcc6.patch megaglest-miniupnpc2.patch)
md5sums=('SKIP'
         'da91416d9e8e7a2ec0dde379474ecf6a'
         '98259043b47888d0f5e2308539c07057')

prepare() {
  cd megaglest-source
  # Fix build with GCC 6 (Fedora)
  # patch -p1 -i ../megaglest-gcc6.patch
  # Fix build with miniupnpc 2 (Fedora)
  # patch -p1 -i ../megaglest-miniupnpc2.patch
}

build() {
  cd megaglest-source

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-lX11" \
    -DWANT_GIT_STAMP=0
  make VERBOSE=1
}

package() {
  cd megaglest-source/build

  make DESTDIR="$pkgdir" install
}

# vim: sw=2:ts=2 et:
