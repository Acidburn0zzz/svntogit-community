# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libcontentaction
pkgver=0.3.17
pkgrel=1
pkgdesc="Library for associating content with actions"
arch=('x86_64')
url="https://git.merproject.org/mer-core/libcontentaction"
license=('LGPL')
depends=('qt5-systems' 'mlite')
makedepends=('python' 'qt5-tools')
source=("https://git.merproject.org/mer-core/libcontentaction/-/archive/$pkgver/libcontentaction-$pkgver.tar.bz2")
sha512sums=('c0a2a82ab8f490a0b3d2a48064984492b8ae494919db6956b0400cf6e69ece5347db4047523a029ee0c5b532b141929e8b1a65943e5c177ef6537681821931c1')

build() {
  cd libcontentaction-$pkgver

  qmake-qt5
  make
}

package() {
  cd libcontentaction-$pkgver

  make INSTALL_ROOT="$pkgdir" install

  # Remove tests
  rm -r "$pkgdir/opt"
}
