# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=marked
pkgver=0.3.18
pkgrel=1
pkgdesc="A markdown parser built for speed"
arch=('any')
url='https://github.com/chjj/marked'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('e3d8b641885f50ba9a5f3359a710bcd3c3e2b2e093193d9f806d33af374223d377ac87c059f5b49e082f6d7afacefa729d974e1b9fbdc4dc5dccfd24c199c447')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -d "$pkgdir"/usr/share/licenses/$pkgname
  ln -s ../../../lib/node_modules/marked/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
