# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)
pkgname=drumgizmo
pkgver=0.9.18
pkgrel=1
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=('x86_64')
url="https://www.drumgizmo.org"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'libjack.so' 'libsmf.so' 'libsndfile'
'libxext' 'libx11' 'libzita-resampler.so')
makedepends=('lv2')
source=("https://www.${pkgname}.org/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('885b576d8b390a8987c780174101b751c36f4b7b3a9bb77425690114d4d5cb6ab0081dfd7c4c2534ac0ce470ce782164e5dc910bfde5bcf43fc18352a961c56d'
            'SKIP')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-lv2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -vDm 644 {AUTHORS,ChangeLog,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
