# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=postgresql-binary
pkgname=haskell-postgresql-binary
pkgver=0.12.2
pkgrel=14
pkgdesc="Encoders and decoders for the PostgreSQL's binary format"
url="https://github.com/nikita-volkov/postgresql-binary"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-base-prelude' 'haskell-binary-parser'
         'haskell-bytestring-strict-builder' 'haskell-loch-th' 'haskell-network-ip'
         'haskell-placeholders' 'haskell-scientific' 'haskell-unordered-containers'
         'haskell-uuid' 'haskell-vector')
makedepends=('ghc' 'haskell-postgresql-libpq' 'haskell-tasty' 'haskell-tasty-quickcheck'
             'haskell-tasty-hunit' 'haskell-quickcheck' 'haskell-quickcheck-instances'
             'haskell-json-ast' 'haskell-conversion' 'haskell-conversion-bytestring'
             'haskell-conversion-text' 'haskell-rerebase' 'pifpaf' 'postgresql')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('f4876c89d8b19ea7e39d6e6dfe41c972de3aa072b7f6550bb8b650f6aedccd24526f8385cca770c11f8212d02c00ec07eedfa64f92c1250c90102d7743637c8b')

prepare() {
    cd $_hkgname-$pkgver
    # Do not use default postgres port
    sed -i 's/5432/9824/' tasty/Main/DB.hs
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver

    eval $(pifpaf run postgresql --host 127.0.0.1)
    createuser -s postgres
    runhaskell Setup test
    pifpaf_stop
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
