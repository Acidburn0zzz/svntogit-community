# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>
pkgname="python-pecan"
_pkgname="${pkgname#python-}"
pkgdesc='A WSGI object-dispatching web framework, designed to be lean and fast with few dependencies.'
pkgver=1.4.0
pkgrel=4
arch=('any')
url='https://github.com/pecan/pecan'
license=('BSD')
depends=('python' 'python-webob' 'python-mako' 'python-webtest' 'python-six'
         'python-logutils')
makedepends=('python' 'python-setuptools' 'python-sphinx' 'python-webob'
             'python-mako' 'python-webtest' 'python-six' 'python-logutils')
checkdepends=('python-virtualenv' 'python-jinja' 'gunicorn' 'python-mock'
              'python-sqlalchemy' 'python-genshi' 'python-nine' 'uwsgi'
              'python-pytest' 'python-kajiki')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}.tar.gz"
        ${pkgname}-sqlalchemy-1.4.patch::https://github.com/pecan/pecan/commit/a520bd544c0b.patch
        python310.patch)
sha512sums=('3f7e072b294a732af6ff65621b1e10254260b9d42abd2bc38c3bff8e141b2ae4592567075813faf40227e7f0a3f2893c27270926fcfad18d1cdd9dc8a85bd14e'
            'aa71f0ae65d245afe6038f9765d557764cf8f80eb699e1b52fe71fb502d097f08ef638f11137b9a716f144de4077058a10fd0f2e57f82f8af79608cb63a0b99e'
            '6e66a845d701aa63fadd67a8dd644956e7c444690a13fa0eafe6482e959c52318037c053c8fd8df503ffa293129d18858dc63fae6dc316ae3284c6e1753b5864')

prepare(){
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -Np1 -i ../${pkgname}-sqlalchemy-1.4.patch
  patch -Np1 -i ../python310.patch # https://github.com/pecan/pecan/pull/131
  # fix manpage build
  sed -i '/^dist = /d' docs/source/conf.py
  sed -i "s/^version = release = .*\$/version = release = '${pkgver}'/" docs/source/conf.py
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # python setup.py test
  py.test -vvv -s -ra --showlocals --noconftest pecan/tests/
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python ./setup.py install --root="$pkgdir/" --optimize=1
  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd docs
  PYTHONPATH="${PWD}/../" make man
  install -D -m644 "./build/man/pecan.1" "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}
