# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=6.3.1
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='http://cordova.apache.org'
license=('Apache')
depends=('npm' 'acorn' 'semver' 'browserify')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
md5sums=('8f66b359f708a9ca07010fe6d020d0cc')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver npm browserify; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/cordova-lib/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done
}
