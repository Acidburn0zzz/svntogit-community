# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: karolina.lindqvist@kramnet.se

pkgname=algol68g
pkgver=2.7
pkgrel=1
pkgdesc='Algol 68 Genie, an Algol 68 compiler-interpreter'
arch=('x86_64' 'i686')
url='http://www.xs4all.nl/~jmvdveer/algol.html'
license=('GPL')
depends=('gsl' 'plotutils')
optdepends=('postgresql-libs: for postgresql support')
source=("http://jmvdveer.home.xs4all.nl/algol68g-$pkgver.tar.gz"
      	'plotutils.patch'
      	'http://www.xs4all.nl/~jmvdveer/a68gdoc.pdf')
sha256sums=('ebcf029ae65ceafca87090cc470dd37f86ac0bdb1529cd4dd7a92454d224d325'
            '50afcc7812d117e5f0a2f5240c9cd58f8bfcc04b3da0634b9aa0ab06183ed0aa'
            '8cbfb480cb5dfb20f79c2391d7111fc8b727c3e9e02008ab4c0cde9acb62e7d3')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 -i ../plotutils.patch
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" docdir="/usr/share/doc/$pkgname" install
  install "$srcdir/a68gdoc.pdf" "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 et:
