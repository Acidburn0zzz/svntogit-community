# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=peony
pkgver=3.0.0
pkgrel=1
pkgdesc="file manager for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/peony"
depends=('gtk2' 'gsettings-qt' 'kwindowsystem' 'libnotify' 'poppler-qt5' 'qt5-x11extras' 'udisks2')
makedepends=('qt5-tools')
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/peony/archive/v$pkgver.tar.gz")
sha512sums=('10d45e74b152b12dad029eddec5503da577294baa6c508b763342d25dce1add587b06c6c28caa681f83d59b0a54f56ac568b289294dc7cc94965bc66e1a93f19')

build() {
  cd peony-$pkgver
  qmake-qt5
  make
}

package() {
  cd peony-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 data/peony{,-computer,-home,-trash}.desktop -t "$pkgdir"/usr/share/applications/
  install -Dm644 data/peony-desktop.desktop -t "$pkgdir"/etc/xdg/autostart/
}
