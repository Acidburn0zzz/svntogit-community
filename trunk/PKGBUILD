# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

pkgname=lv2file
pkgver=0.93
pkgrel=1
pkgdesc="A simple program which you can use to apply effects to your audio files"
arch=(x86_64)
url="https://github.com/jeremysalwen/lv2file"
license=(GPL3)
groups=(lv2-plugins pro-audio)
depends=(argtable glibc)
makedepends=(libsndfile lilv lv2)
# upstream didn't add the manpage to the release tarball:
# https://github.com/jeremysalwen/lv2file/issues/4
source=("$pkgname-$pkgver.tar.gz::https://github.com/jeremysalwen/${pkgname}/archive/upstream/${pkgver}.tar.gz"
        "https://github.com/jeremysalwen/${pkgname}/blob/9c6fa09f829afa4e4dd47841ec57959c958ddce3/debian/${pkgname}.1")
sha512sums=('4f4b610b44c34920671eaae0735a3f71a2bdc8ac874e61e408d49518e19ca59c30f989dc1cd7542e733d799db1fa5ad1242fd290978b07304b88848095ce0128'
            'd477981f1c2f7dc5937e8a061f3a000c08a4df656506e0677296cd6d7ffdb3055c629979e01fdded580054534a726a4b2bacd98b6643d25ac7659713b14e0a24')
b2sums=('1fc1f2440f7c90cc967c1decc820b4222253968da691b89db582d46b5deb942296e405c2403ca13c5c8e0ec507cbf2da175d75cb79b1921dbe1b7c54a4d975c3'
        '92497a36ad44e6d1f6d40f671269b397993bbfa124142cbebdb13928ead02741d3008746016b29e54d7aa854b6ed0395ef419a6499cb8aecbbb099db2d0b9207')

build() {
  make all -C "$pkgname-upstream-$pkgver"
}

package() {
  depends+=(liblilv-0.so libsndfile.so)

  make DESTDIR="$pkgdir/" install -C "$pkgname-upstream-$pkgver"
  install -vDm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644  "$pkgname-upstream-$pkgver/README" -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
