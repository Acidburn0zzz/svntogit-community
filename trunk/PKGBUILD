# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=('boinc' 'boinc-nox')
pkgbase=boinc
pkgver=7.0.25
pkgrel=1
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
makedepends=('curl' 'libxslt' 'perl-xml-sax' 'subversion' 'wxgtk' 'libnotify')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(boinc.rc boinc.bash boinc.desktop)
sha256sums=('92fe2468a1b2a85755c52dcb8045bb5443a4ff6a70d56ad586f44dab65730a6e'
            '63c7d8560a603f09ff46caf3797228c0b1552c69ee14d0a987f6e02e7142b025'
            '9cfe8ab04206131552af5030d4e1f8ddbfb0c1f8daeabe81384741373a7e9610')

_svnroot="http://boinc.berkeley.edu/svn/tags"
_svnmod="boinc_core_release_${pkgver//./_}"

build() {
  cd ${srcdir}

  msg "Connecting to boinc.berkeley.edu SVN server..."
  svn co ${_svnroot}/${_svnmod} || (error "SVN checkout failed" && return 1)
  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  rm -fr ${_svnmod}-build
  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./_autosetup

  ./configure --prefix=/usr --disable-server --enable-unicode --with-ssl \
              --with-wxdir=/usr/lib --with-wx-config=$(which wx-config) \
              --disable-static --enable-client --enable-manager
  make
}

package_boinc() {
pkgdesc='Berkeley Open Infrastructure for Network Computing for desktop with X'
  depends=('curl' 'wxgtk' 'libnotify')
  conflicts=('boinc-nox')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/usr/share/bash-completion/completions/$pkgname

#install .desktop File
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop

#install icons
  install -D -m644 ${srcdir}/${_svnmod}-build/clientgui/res/boincmgr.48x48.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}

package_boinc-nox() {
pkgdesc='Berkeley Open Infrastructure for Network Computing for server without X'
  depends=('curl')
  provides=('boinc')
  conflicts=('boinc')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

#install rc-script
  install -D -m755 ${srcdir}/boinc.rc ${pkgdir}/etc/rc.d/boinc

#install bash-completion
  install -D -m644 ${srcdir}/boinc.bash ${pkgdir}/usr/share/bash-completion/completions/boinc

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d

#removing graphical tools
  rm -rf ${pkgdir}/usr/share/boinc
  rm ${pkgdir}/usr/bin/boincmgr
  find ${pkgdir}/usr/share/locale -type f -name "BOINC-Manager.mo" -exec rm {} \;
}
