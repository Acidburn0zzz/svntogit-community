# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Konrad <konrad AT knauber DOT name>

pkgname=pinta
pkgver=1.3
pkgrel=2
pkgdesc="A drawing/editing program modeled after Paint.NET. It's goal is to provide a simplified alternative to GIMP for casual users"
arch=('any')
url="http://pinta-project.com/"
license=('MIT')
depends=('gtk-sharp-2' 'mono-addins' 'desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool')
install=$pkgname.install
source=(https://github.com/downloads/PintaProject/Pinta/$pkgname-$pkgver.tar.gz
        gtk-sharp-2.12.11.patch)
md5sums=('2080f32e2204184ba442e617252fd43d'
         '5213e6691683e48c682dbd70e3b834f2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # https://bugs.launchpad.net/pinta/+bug/956195
  patch -Np1 -i "$srcdir/gtk-sharp-2.12.11.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 license-*.txt readme.md "$pkgdir"/usr/share/licenses/$pkgname/
}
