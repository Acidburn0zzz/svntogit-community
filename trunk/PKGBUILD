# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.3.0.4
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'gsettings-qt')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('c164cebc022a1827d6071b6d94909e31c6fd41c0082047efcd7fcc8a5f3bbd6925143d27682921c1a2136fefb7614d81027cbcac9473dc58331235a7c0c77718')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of commercial company's homepage
  sed -i '/www.uniontech.com/i \    "https://www.archlinux.org/favicon.ico",' connectivitychecker.cpp
}

build(){
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
