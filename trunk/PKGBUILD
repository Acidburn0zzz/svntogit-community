# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Filipe Laíns <lains@archlinux.org>
# Contributor: Marq Schneider <queueRAM@gmail.com>
# Contributor: Nick Østergaard
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgname=kicad
pkgver=6.0.0
pkgrel=5
pkgdesc='Electronic schematic and printed circuit board (PCB) design tools'
arch=('x86_64')
url='http://kicad.org/'
license=('GPL')
depends=('wxgtk3' 'python' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade' 'python-wxpython')
makedepends=('cmake' 'mesa' 'boost' 'swig')
optdepends=(
  'kicad-library: for footprints, symbols and templates'
  'kicad-library-3d: for 3D models of components'
)
source=(
  "https://gitlab.com/kicad/code/kicad/-/archive/$pkgver/kicad-$pkgver.tar.bz2"
  'fix-archive-permissions.patch'
)
sha512sums=('1267b7cd35a63a3f26c2e9b0695722cbb9b279d1326f52f0b8497bd3011d79a5732e6da27f6413fb72064fe2aca72da95252011c6cb0b70c209f296f40a77b90'
            'e655d37f743e131e60ea8143b78c195ea5233b1336a0cd73c3388bf27848240622843e31c895467fcb8af5d74e351676b353c524c835b9dde49e1e6bc85e0dce')
b2sums=('21a43f952c748ec90bde29621b927e1cb69b4f9faf6842924888c74e15cad768826bba5eea973c4b5b0e43bb1e45eaf1e1bfa691691248ec238d8b0f3f24ca5d'
        'fd1312d6adc0a9d00734a95a76fd0ed82824cfae0e626bfa2e0a24a710dd7a9f30d8d9d8b80a278d94aab8dd6d06bfe5edb41f03bb3bb8d4c3b5e9f066d49a84')

prepare() {
  cd "$pkgname-$pkgver"

  patch -p1 -i ../fix-archive-permissions.patch
}

build() {
  cmake \
    -S "$pkgname-$pkgver" \
    -B build \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKICAD_USE_OCC=ON \
    -DKICAD_SCRIPTING_WXPYTHON=ON \
    -DwxWidgets_CONFIG_EXECUTABLE=/usr/bin/wx-config-gtk3 \
    -DKICAD_BUILD_I18N=ON \
    -DKICAD_I18N_UNIX_STRICT_PATH=ON \
    -DKICAD_BUILD_QA_TESTS=OFF \
    -Wno-dev

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

# vim:set ts=2 sw=2 et:
