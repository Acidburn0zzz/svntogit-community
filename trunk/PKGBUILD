# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=phpldapadmin
pkgver=1.2.5
pkgrel=1
pkgdesc="A PHP and hence web-based tool to administrate LDAP over the WWW"
arch=('any')
url="https://github.com/leenooks/phpLDAPadmin"
license=('GPL')
depends=('libldap' 'php')
makedepends=('optipng')
backup=("etc/webapps/phpldapadmin/config.php")
source=($pkgname-$pkgver.tar.gz::https://github.com/leenooks/phpLDAPadmin/archive/$pkgver.tar.gz
        sort-in-templates.patch)
sha256sums=('74581f5af1abdbf672b6a1655e9fb3c34a16c7510019348812571fcc10fdde1b'
            'fc2d4bdf4d43a90aa541fe20eeed494caa339eacf3f08b06539efcdbacc3578f')

prepare() {
  cd "$srcdir"/phpLDAPadmin-${pkgver}/
  find -type f -name '*.png' -exec optipng -quiet -force -fix {} \;
  patch -p1 -i "$srcdir"/sort-in-templates.patch
}

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpldapadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phpldapadmin
  cd ${_instdir}

  cp -ra "$srcdir"/phpLDAPadmin-${pkgver}/* .

  ln -s /etc/webapps/phpldapadmin/config.php ${_instdir}/config/config.php
  cp ${_instdir}/config/config.php.example "$pkgdir"/etc/webapps/phpldapadmin/config.php
}
