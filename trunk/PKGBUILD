# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=2.0.2
pkgrel=1
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
provides=('ukui-indicators')
replaces=('ukui-indicators')
depends=('gsettings-qt' 'kwindowsystem' 'libqtxdg' 'peony' 'qt5-webkit' 'ukui-biometric-auth'
         'ukui-menu' 'ukui-menus')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('bb8a765c21e4153dbdd4a3086bc39362ce832145a41567395f4ba8b8f9722e93566521adf08c940b1ea7c6b14e2bfbba0be142615f7420bc9747177d30d320f1')

prepare() {
  mkdir -p build
  sed -i 's|../../panel/common/ukuitranslator.h|../panel/common/ukuitranslator.h|' $pkgname-$pkgver/cmake/ukui-build-tools/modules/*.cpp.in
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
