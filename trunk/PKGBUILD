# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-filebytes
pkgname=('python-filebytes' 'python2-filebytes')
_pyname=${pkgbase/python-/}
pkgver=0.9.9
pkgrel=1
pkgdesc='Library to read and edit executable binary files'
url='https://github.com/sashs/filebytes'
arch=('any')
license=('GPL2')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/sashs/filebytes/archive/v${pkgver}.tar.gz)
sha512sums=('947400996cb1e58590cae047befd86e7a5dbcb76d95713acc9a3710f2b7e230bc274f1971770f0397ff4a78e607041b793848d6383c3e93c688d1e76cd65446e')

prepare() {
  cp -ra ${_pyname}-${pkgver}{,-py2}
  sed -r 's|env python$|env python2|' -i ${_pyname}-${pkgver}-py2/samples/*
}

package_python-filebytes() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

package_python2-filebytes() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 samples/* -t "${pkgdir}/usr/share/doc/${pkgname}/samples"
}

# vim: ts=2 sw=2 et:
