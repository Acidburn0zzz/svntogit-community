# Maintainer: David Runge <dvzrv@archlinux.org>

_name=authheaders
pkgname=python-authheaders
pkgver=0.12.1
pkgrel=1
epoch=
pkgdesc="Library for the generation of email authentication headers"
arch=('any')
url="https://github.com/ValiMail/authentication-headers"
license=('BSD' 'MPL2' 'ZPL')
groups=()
# python-publicsuffix should actually not be in use:
# https://github.com/ValiMail/authentication-headers/issues/2
depends=('python-authres' 'python-dkim' 'python-dnspython'
'python-publicsuffix')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
optdepends=()
provides=()
conflicts=()
replaces=()
backup=()
options=()
install=
changelog=
# latest version doesn't offer verification:
# https://github.com/ValiMail/authentication-headers/issues/1
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
noextract=()
sha512sums=('68ac36227d0ef602ed89fcd5852c0984b0875bd4a3e1465d87791178a0e42072bca79335054c82bbbe6e06f6484598cabfa83676bd3338114f82bf9dbd23ccc3')
# validpgpkeys=('E7729BFFBE85400FEEEE23B178D7DEFB9AD59AF1')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
