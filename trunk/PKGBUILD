# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bctoolbox
pkgver=5.0.26
pkgrel=1
pkgdesc='A library written in C that allows you to create and run audio and video streams'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/bctoolbox'
license=(GPL)
depends=(mbedtls bcunit)
makedepends=(cmake)
source=(https://github.com/BelledonneCommunications/bctoolbox/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('c4c84ba1aad1422b83b65f9728f323d4661989509e95bd5546560c8585855fcd')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
