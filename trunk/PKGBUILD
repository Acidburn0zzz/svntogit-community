# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=dask
pkgname=python-${_pkg}
pkgver=2.21.0
pkgrel=2
pkgdesc="Parallel computing with task scheduling"
arch=(any)
url="https://dask.org"
license=(BSD)
makedepends=(python-setuptools)
depends=(python python-yaml mpdecimal)
optdepends=(
#    'python-bokeh: visualizing dask diagnostics'
    'python-cloudpickle: bag/delay support'
#    'python-cityhash: faster hashing of arrays'
    'python-distributed: distributed computing'
#    'python-fastparquet: storing and reading data from parquet files'
    'python-fsspec: local, cluster and remote data IO, bag/dataframes support'
#    'python-gcsfs: file-system interface to Google Cloud Storage'
#    'python-murmurhash: faster hashing of arrays'
    'python-numpy: arrays support'
    'python-pandas: dataframes support'
    'python-partd: bag/dataframes support'
    'python-psutil: more accurate CPU count'
#    'python-pyarrow: Apache Arrow, for parquet support'
#    'python-s3fs: Amazon S3 support'
    'python-sqlalchemy: writing and reading from SQL databases'
    'python-toolz: array/bag/dataframe/delay support'
    'python-xxhash: faster hashing of arrays'
)
checkdepends=(
    ipython
    python-pytest
#    python-bokeh
    python-cloudpickle
#    python-cityhash
    python-distributed
#    python-fastparquet
    python-fsspec
#    python-gcsfs
    python-graphviz
    python-h5py
    python-jsonschema
    python-matplotlib
#    python-murmurhash
    python-numexpr
    python-numpy
    python-pandas
    python-partd
    python-psutil
#    python-pyarrow
    python-pytables
    python-requests
#    python-s3fs
    python-scipy
    python-sqlalchemy
    python-toolz
    python-xarray
    python-xxhash
)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
#source=(https://github.com/dask/dask/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('c188195a7c73e0d27af5308e8ac7d62b6d1e95b670280db2c102d737bf711b55')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  pytest dask/tests dask/array/tests dask/bag/tests dask/bytes/tests dask/dataframe/tests
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

