# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Phillip Schichtel <phillip.public@schich.tel>

pkgname=adapta-gtk-theme
pkgver=3.90.1.21
pkgrel=1
pkgdesc='An adaptive Gtk+ theme based on Material Design Guidelines'
arch=('any')
url='https://github.com/tista500/Adapta'
license=('GPL2')
depends=('gtk-engine-murrine' 'gtk3')
makedepends=('git' 'gnome-shell' 'inkscape' 'libxml2' 'parallel' 'sassc')
optdepends=('budgie-desktop: The Budgie desktop'
            'cinnamon: The Cinnamon desktop'
            'gnome-shell: The GNOME Shell'
            'gnome-flashback: The GNOME flashback shell'
            'noto-fonts: Recommended font'
            'ttf-roboto: Recommended font'
            'xfdesktop: The Xfce desktop')
source=("adapta-gtk-theme::git+https://github.com/tista500/Adapta.git#tag=${pkgver}")
sha256sums=('SKIP')

build() {
  cd adapta-gtk-theme

  ./autogen.sh \
    --prefix='/usr' \
    --enable-parallel \
    --enable-chrome \
    --enable-plank \
    --enable-telegram \
    --disable-unity
  make
}

package() {
  cd adapta-gtk-theme

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/plank/themes
  ln -s /usr/share/themes/Adapta/plank "${pkgdir}"/usr/share/plank/themes/Adapta
}

# vim: ts=2 sw=2 et:
