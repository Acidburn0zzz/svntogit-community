# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=1.5.0
pkgrel=3
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('dbus-glib' 'libdaemon' 'vhba-module'
         'dbus' "libmirage=$pkgver" 'libao')
optdepends=('alsa-lib: to enable the audio driver')
backup=('etc/conf.d/cdemud'
        'etc/dbus-1/system.d/cdemud-dbus.conf')
install=cdemud.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        'cdemud.conf'
        'cdemud.rc'
        '60-vhba.rules'
        'cdemu-daemon-system.patch')
md5sums=('5ba780caa26d855942512b5b3c22405a'
         'd65c30651389900c46e62a9e5559b9d5'
         'ef47f06a3d4e07885bfbb57d4d6d0be4'
         '549bd2d9696bd1884c8eed7193c00e21'
         'a3ae9c51c460d31a521dd00dd3f0a852')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np0 -i $srcdir/cdemu-daemon-system.patch

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/cdemud.conf" "$pkgdir/etc/conf.d/cdemud"
  install -Dm755 "$srcdir/cdemud.rc" "$pkgdir/etc/rc.d/cdemud"

  # install udev rule for non-root mounting of iso's
  install -Dm644 "$srcdir/60-vhba.rules" \
    "$pkgdir/lib/udev/rules.d/60-vhba.rules"

  # should we really be doing this?
  #install -d "$pkgdir/usr/lib/modules-load.d"
  #echo "vhba" > "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
