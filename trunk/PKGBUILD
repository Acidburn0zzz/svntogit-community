# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=k9s
pkgver=0.13.7
pkgrel=1
pkgdesc='Kubernetes TUI for managing clusters and pods'
arch=(x86_64)
url='https://github.com/derailed/k9s'
license=(APACHE)
makedepends=(git go-pie)
source=("git+$url#commit=7df87a80ab15d767648af9ca1a7601fb03b58237") # 0.13.7
sha256sums=('SKIP')

build() {
  cd $pkgname
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}"
}

package() {
  cd $pkgname
  install -Dm755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
