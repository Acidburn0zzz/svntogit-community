# $Id$
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=epdfview
pkgver=0.1.7
pkgrel=7
pkgdesc="A free lightweight PDF document viewer."
url="http://www.emma-soft.com/projects/epdfview/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib>=0.16.2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig')
install='epdfview.install'
source=("http://www.emma-soft.com/projects/${pkgname}/chrome/site/releases/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop.patch"
        "0001-r329.patch"
        "0002-r354.patch"
        "0003-r357.patch")
md5sums=('1919bb19c16ef0a97d48b0a8303d3c7b'
         'fbf22bbabdbb7544db615ac5775d57e2'
         '14397ae0baa69fb9791ccaa35c243470'
         '45a3b7d56f28f94a230c476ce2de54f0'
         '932da23c324326c5baea3b5beae12ad5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/${pkgname}.desktop.patch"
  patch -Np2 -i "${srcdir}/0001-r329.patch"     # fixes mousewheel scrolling
  patch -Np2 -i "${srcdir}/0002-r354.patch"     # patches poppler api changes, can
  patch -Np2 -i "${srcdir}/0003-r357.patch"     # be removed with a new epdfview release

  # apply changes from r354 and r357 to configure.ac
  touch ChangeLog                               # missing ChangeLog results in
  autoreconf -fi                                # error during autoreconf

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for size in 24 32 48; do
      install -Dm644 data/icon_${pkgname}-${size}.png \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
  done
}
