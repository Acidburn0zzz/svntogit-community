# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>

pkgname=cm256cc
pkgver=1.0.1
pkgrel=1
pkgdesc='Fast GF(256) Cauchy MDS Block Erasure Codec in C++'
arch=('x86_64')
url='https://github.com/f4exb/cm256cc'
license=('GPL3')
makedepends=('cmake' 'boost')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('2271ddb238fbe09457908ce59367bcc4643b538587d2657611c7eb5679669ed26e185a8e30d75ba9a926bf7a64e0bd1379973f73935d01fda43213f040e49eae')

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_PREFIX=/usr

  make
}

check() {
  $pkgname-$pkgver/build/cm256_test
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}
