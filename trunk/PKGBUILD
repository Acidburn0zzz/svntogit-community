# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Mohammad AlSaleh <CE.Mohammad.AlSaleh at gmail dot com>

pkgname=lua-language-server
pkgver=2.3.1
pkgrel=1
license=('MIT')
pkgdesc='Lua Language Server coded by Lua'
url=https://github.com/sumneko/lua-language-server
arch=('x86_64')
depends=('gcc-libs')
makedepends=('git' 'ninja')
source=("git+$url.git#tag=$pkgver"
        'git+https://github.com/actboy168/bee.lua.git'
        'git+https://github.com/sqmedeiros/lpeglabel.git'
        'git+https://github.com/actboy168/luamake.git'
        'git+https://github.com/electron/rcedit.git'
        'wrapper')
b2sums=('SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'SKIP'
        'fbc4df76ae79a4a170fdc969d257adde36cecf19ade6d03f6624c08fea132a8b74c4e7c357acfb8f10024c251aaa83b348e1cd78943cb43469d947d83043c967')

prepare() {
  cd $pkgname
  git submodule init
  for i in ../{bee.lua,lpeglabel,luamake,rcedit}; do
    git config submodule.3rd/${i##../}.url $i
  done
  git submodule update
  cd 3rd/luamake
  git submodule init
  git config submodule.3rd/bee.lua.url ../../../bee.lua
  git submodule update
}

build() {
  cd $pkgname
  ninja -C 3rd/luamake -f compile/ninja/linux.ninja
  ./3rd/luamake/luamake rebuild
}

package() {
  cd $pkgname
  install -D ../wrapper "$pkgdir"/usr/bin/$pkgname
  install -Dt "$pkgdir"/usr/lib/$pkgname bin/Linux/$pkgname
  install -m644 -t "$pkgdir"/usr/lib/$pkgname bin/Linux/*.so
  install -Dm644 -t "$pkgdir"/usr/share/$pkgname {debugger,main,platform}.lua
  cp -r locale meta script "$pkgdir"/usr/share/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
