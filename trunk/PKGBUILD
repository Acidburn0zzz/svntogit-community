# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=snd
pkgver=21.3
pkgrel=1
pkgdesc="An advanced sound editor"
arch=('x86_64')
url="https://ccrma.stanford.edu/software/snd/"
license=('custom:free')
groups=('pro-audio')
depends=('glibc' 'glu' 'gmp' 'gsl' 'libglvnd' 'libmpc' 'libx11' 'libxext'
'libxpm' 'libxt' 'mpfr' 'openmotif' 'ruby')
# TODO: maybe replace mpg123 with mpg321
makedepends=('alsa-lib' 'fftw' 'flac' 'gendesk' 'jack' 'ladspa' 'libpulse'
'libsamplerate' 'mpg123' 'speex' 'timidity++' 'vorbis-tools' 'wavpack')
optdepends=('wavpack: For wav support'
            'flac: For flac support'
            'speex: For speex support'
            'mpg123: For MPEG support'
            'pulseaudio: For pulseaudio support'
            'timidity++: For MIDI to WAVE support'
            'vorbis-tools: For OGG support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e4c75b72645472a1924f0d9c9ea4df7acde13ef9778f8fc0ea03069f026acbc18b1f5e40c8e8002fc4295c32c0c5762f053ca56c29e6039501e66c9ddd0aea05')
b2sums=('92d809ab95380141556875c0fcdd330a6a19319754029a88225b1ff0815b130b394b3e71682052f385b0706fa3241783001352e9895d1441d89dc1ce4c3f200c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # create XDG desktop file, as the default is broken
  gendesk -f -n \
          --pkgname "${pkgname}" \
          --name "${pkgname}" \
          --exec "${pkgname}" \
          --genericname "Sound Editor" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-gl \
              --with-gmp \
              --with-jack \
              --with-ladspa \
              --with-motif \
              --with-pulseaudio \
              --with-ruby
  make
}

package() {
  depends+=('libasound.so' 'libfftw3.so' 'libjack.so' 'libsamplerate.so')
  cd "${pkgname}-${pkgver}"
  make prefix="${pkgdir}/usr" install
  install -vDm 644 *.desktop -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {{HISTORY,README}.Snd,NEWS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 *.html -t "${pkgdir}/usr/share/doc/${pkgname}/html"
  install -vDm 644 pix/*.png -t "${pkgdir}/usr/share/doc/${pkgname}/html/pix"
}

# vim:set ts=2 sw=2 et:
