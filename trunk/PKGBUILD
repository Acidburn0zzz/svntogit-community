# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=limesuite
pkgver=18.10.0
pkgrel=2
pkgdesc='Driver and GUI for LMS7002M-based SDR platforms (LimeSDR, Novena RF7, etc.)'
arch=('x86_64')
url='https://github.com/myriadrf/LimeSuite'
license=('Apache')
depends=('sqlite3' 'wxgtk2' 'freeglut' 'libusb>=1.0' 'soapysdr' 'wget' 'gnuplot' 'glew' 'fltk')
makedepends=('cmake' 'doxygen' 'octave')
optdepends=('octave: Octave plugin')
provides=('soapylms7')
conflicts=('soapylms7')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('696113d1726dd2f86994f0c0771ceaf5d1c9fa53ee8a1dec50069d885f6b294d0cee2bc2a25d85a0d2b06415f4840479e8643a5be085eecca08834206ea15f5b')

prepare() {
  cd LimeSuite-$pkgver

  # Change mode to 666 and ommit plugdev group
  sed -i 's|MODE="660", GROUP="plugdev"|MODE="666"|g' udev-rules/64-limesuite.rules
}

build() {
  mkdir -p LimeSuite-$pkgver/build
  cd LimeSuite-$pkgver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_UTILITIES=True \
    -DENABLE_LIME_UTIL=True \
    -DCMAKE_SKIP_RPATH=True \
    -DENABLE_DESKTOP=False \
    -DENABLE_SOAPY_LMS7=True \
    -DENABLE_EVB7COM=True \
    -DENABLE_PCIE_XILLYBUS=True \
    -DENABLE_REMOTE=True \
    -DENABLE_QUICKTEST=True \
    -DENABLE_MCU_TESTBENCH=True \
    -DENABLE_FTDI=True \
    -DENABLE_FX3=True \
    -DENABLE_STREAM_UNITE=True \
    -DENABLE_EXAMPLES=True \
    -DENABLE_HEADERS=True \
    -DENABLE_GUI=True \
    -DENABLE_OCTAVE=True \
    -DENABLE_API_DOXYGEN=True \
    -DLIME_SUITE_EXTVER=$pkgver

 #    -DENABLE_NOVENARF7=True \

  make
}

package() {
  cd LimeSuite-$pkgver/build

  make DESTDIR="$pkgdir" install

  # Install udev rules, changing mode to 666 and ommitting plugdev group
  install -Dm 644 ../udev-rules/64-limesuite.rules "$pkgdir"/etc/udev/rules.d/64-limesuite.rules

  # Install icons and .desktop file
  install -Dm 644 ../Desktop/lime-suite-128.png "$pkgdir"/usr/share/pixmaps/lime-suite.png
  install -Dm 755 ../Desktop/lime-suite.desktop "$pkgdir"/usr/share/applications/lime-suite.desktop

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r --no-preserve=ownership ../docs/LMS_API_Documentation/* "$pkgdir"/usr/share/doc/$pkgname
}

