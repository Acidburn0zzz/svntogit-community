# $Id$
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>
# Contributor: domanov <domanov@gmail.com>

pkgname=ditaa
pkgver=0.11.0
pkgrel=1
pkgdesc='Utility for converting ASCII art diagrams into bitmaps'
arch=('any')
url='https://github.com/stathissideris/ditaa'
license=('GPL')
depends=('gsfonts' 'java-environment>=8')
makedepends=('apache-ant' 'git')
source=("git+https://github.com/stathissideris/ditaa#tag=v$pkgver"
        $pkgname.sh)
sha256sums=('SKIP'
            '77f95a7c3235a1263b560844f988445083895550385bff1a4c28313b0767e38f')

prepare() {
  local oldver="0_9"
  local shortver=${pkgver%.0}
  local underver=${shortver/./_}
  sed -i "s|$oldver|$underver|" $pkgname/build/release.xml
  sed -i 's|1.6|1.8|g' $pkgname/build/release.xml
  sed -i "s|VERSION|$underver|" $pkgname.sh
  mkdir -p $pkgname/{bin,lib}
  sed -i 's|</javac>|<compilerarg value="-Xlint:unchecked"/></javac>|g' $pkgname/build/release.xml
}

build() {
  cd "$pkgname"

  # TODO: Build with leiningen, after moving from AUR to [community]

  ant -buildfile build/release.xml release-jar
}

package() {
  local shortver=${pkgver%.0}
  local underver=${shortver/./_}
  install -Dm755 "$pkgname/releases/$pkgname$shortver.jar" \
    "$pkgdir/usr/share/java/$pkgname/ditaa-$shortver.jar"
  install -Dm755 $pkgname.sh "$pkgdir/usr/bin/$pkgname"
}

# vim: ts=2 sw=2 et:
