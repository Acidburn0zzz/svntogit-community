# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-material-color
pkgver=0.0.0.20200405
_commit=9210f113db3a5995fdef087cb500b23f8cd9a4e9
pkgrel=1
pkgdesc="Material color theme for fcitx5"
arch=('any')
url="https://github.com/hosxy/Fcitx5-Material-Color"
license=('GPL')
source=("https://github.com/hosxy/Fcitx5-Material-Color/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('459def3269b01ff0f9850af42d37c828183e37336ade2847594d4cb4e4b382fa18e0dfd2325e2c86a40bb1988783b7541384bff1257716135eaf92a5b47c55ca')

package() {
  cd Fcitx5-Material-Color-$_commit
  install -Dm644 arrow.png radio.png -t "$pkgdir"/usr/share/$pkgname/
  for _variant in blue brown deepPurple indigo pink red teal; do
    _variant_name=Material-Color-${_variant^}
    install -Dm644 highlight-$_variant.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/highlight.png
    install -Dm644 panel-$_variant.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/panel.png
    ln -s ../../../$pkgname/arrow.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    ln -s ../../../$pkgname/radio.png "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    install -Dm644 theme.conf -t "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/
    sed -i "s/^Name=.*/Name=$_variant_name/" "$pkgdir"/usr/share/fcitx5/themes/$_variant_name/theme.conf
  done
}
