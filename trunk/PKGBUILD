# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Arne Hoch <arne@derhoch.de>

pkgbase='dbeaver'
pkgname=('dbeaver' 'dbeaver-office-extension' 'dbeaver-apache-poi')
pkgver=4.1.2
pkgrel=1
pkgdesc="Free universal SQL Client for developers and database administrators (community edition)"
arch=('i686' 'x86_64')
url="http://dbeaver.jkiss.org/"
license=("Apache")
depends=('java-runtime>=8' 'gtk2' 'gtk-update-icon-cache')
makedepends=('maven' 'java-environment>=8')
source=("${pkgbase}-${pkgver}.tar.gz"::"https://github.com/serge-rider/dbeaver/archive/${pkgver}.tar.gz"
        "${pkgbase}.desktop"
        "${pkgbase}.profile.gz"
        "${pkgbase}.hook"
        "dbeaver.install"
        "dbeaver-office-extension.info"
        "dbeaver-apache-poi.info")
sha256sums=('05e7f9262571899af1b556e081b1b3d7658d36b0c971346e44400d8ed797b086'
            'd1365a1e1a9a7b3949fc1b9e9928e93b7d8b3d04b5d0eee7c37eaaa362466c47'
            '29b9b2f32bcdf33b14680b4582e95c42cb5109c2168c741278630b68d1f265ee'
            'b035ea82c5c349cf992a411f208d8c874c2844d2f8490e4ad21cafb5be3fbfba'
            'f8d65dd933049b587a5815ea75a30ef944300b812df383ca1c2dcd68280bc7ab'
            '71572bc2403e251367c6096cc623e989f913ae143025bc79675f68aa9c2df695'
            'fc7127a98a8de5347c9ce31f78d1d55c911d86477825a5632d9d53d89d4f8490')

build() {
  cd "${pkgbase}-${pkgver}/"
  mvn --batch-mode package
}

package_dbeaver() {
  install="${pkgname}.install"

  cd "${pkgbase}-${pkgver}/product/standalone"
  # Install icons into /usr/share/icons/hicolor
  for _size in 16 32 48 64 128 256 512
  do
    install -m 644 -D "icons-sources/icon_${_size}x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/dbeaver.png"
  done

  # Set system architecture
  if [ "${CARCH}" = 'x86_64' ]; then
    _arch="x86_64"
  else
    _arch='x86'
  fi

  # Move into the target directory
  cd "target/products/org.jkiss.dbeaver.core.product/linux/gtk/${_arch}"

  # Initially install everything into /usr/lib/dbeaver
  install -m 755 -d "${pkgdir}/usr/lib"
  cp -r "dbeaver" "${pkgdir}/usr/lib/${pkgname}"

  # Move shared data to /usr/share/dbeaver
  cd "${pkgdir}/usr/lib/${pkgname}"
  install -m 755 -d "${pkgdir}/usr/share/${pkgname}"
  for _file in configuration features p2 .eclipseproduct artifacts.xml dbeaver.ini readme.txt
  do
    mv "${_file}" "${pkgdir}/usr/share/${pkgname}"
    ln -s "/usr/share/${pkgname}/${_file}" .
  done

  # Install additional licenses
  install -m 755 -d "${pkgdir}/usr/share/licenses"
  mv licenses "${pkgdir}/usr/share/licenses/${pkgname}"

  # Install icons
  install -m 755 -d "${pkgdir}/usr/share/pixmaps"
  mv dbeaver.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  mv icon.xpm "${pkgdir}/usr/share/pixmaps/${pkgname}.xpm"

  # Install executable files into /usr/bin
  install -m 755 -d "${pkgdir}/usr/bin"
  ln -s "/usr/lib/${pkgname}/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # Install application launcher into /usr/share/applications
  install -m 755 -d "${pkgdir}/usr/share/applications"
  install -m 755 -t "${pkgdir}/usr/share/applications" "${srcdir}/${pkgname}.desktop"

  # Clean up and install new profile
  rm -rf "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.core"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine/profileRegistry/DefaultProfile.profile"
  find . -name "*.profile.gz" -delete
  install -m 644 "${srcdir}/${pkgname}.profile.gz" "1502633007017.profile.gz"
  cd "${pkgdir}/usr/share/${pkgname}/p2/org.eclipse.equinox.p2.engine"
  rm ".settings/org.eclipse.equinox.p2.artifact.repository.prefs"
  rm ".settings/org.eclipse.equinox.p2.metadata.repository.prefs"
  rmdir ".settings"

  # Install system hook
  install -m 755 -d "${pkgdir}/usr/share/libalpm/hooks"
  install -m 644 "${srcdir}/${pkgbase}.hook" "${pkgdir}/usr/share/libalpm/hooks"

  # Create configuration file (handled by the hook)
  cd "${pkgdir}/usr/share/dbeaver/configuration/org.eclipse.equinox.simpleconfigurator"
  install -m 755 -d "${pkgdir}/etc/${pkgbase}/bundles.d"
  mv "bundles.info" "${pkgdir}/etc/${pkgbase}/bundles.d/00-${pkgname}.info"
  ln -s "/etc/${pkgbase}/bundles.info" .
}

package_dbeaver-apache-poi() {
  depends=("dbeaver=${pkgver}")
  pkgdesc="DBeaver library for Microsoft Office documents"
  arch=('any')

  # Extract plugin
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_3.16.0"
  cd "${pkgdir}/usr/lib/dbeaver/plugins/org.jkiss.bundle.apache.poi_3.16.0"
  jar -xf "${srcdir}/${pkgbase}-${pkgver}/product/updateSite/target/repository/plugins/org.jkiss.bundle.apache.poi_3.16.0.jar"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/${pkgbase}/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/${pkgbase}/bundles.d/20-${pkgname}.info"
}

package_dbeaver-office-extension() {
  depends=("dbeaver-apache-poi=${pkgver}")
  pkgdesc="DBeaver plugin to export data to Microsoft Office format"
  arch=('any')

  # Install plugin
  cd "${pkgbase}-${pkgver}/product/updateSite/target/repository"
  install -m 755 -d "${pkgdir}/usr/lib/dbeaver/plugins"
  install -m 644 -t "${pkgdir}/usr/lib/dbeaver/plugins" "plugins/org.jkiss.dbeaver.data.office_1.1.0.jar"

  # Extract feature
  install -m 755 -d "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_1.1.0"
  cd "${pkgdir}/usr/share/dbeaver/features/org.jkiss.dbeaver.ext.office.feature_1.1.0"
  jar -xf "${srcdir}/${pkgbase}-${pkgver}/product/updateSite/target/repository/features/org.jkiss.dbeaver.ext.office.feature_1.1.0.jar"

  # Add configuration file (handled by the hook)
  install -m 755 -d "${pkgdir}/etc/${pkgbase}/bundles.d"
  install -m 644 "${srcdir}/${pkgname}.info" "${pkgdir}/etc/${pkgbase}/bundles.d/20-${pkgname}.info"
}
