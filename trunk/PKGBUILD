# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-bleach
pkgname=(python-bleach python2-bleach)
pkgver=1.5.0
pkgrel=3

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="http://pypi.python.org/pypi/bleach"
arch=('any')
license=('Apache')

makedepends=('python-setuptools'
             'python-html5lib'
             'python2-setuptools'
             'python2-html5lib')

source=("python-bleach-$pkgver.tar.gz::https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz")

md5sums=('b663300efdf421b3b727b19d7be9c7e7')

prepare() {
	cp -a bleach-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py build
}

package_python-bleach() {
	depends=('python-html5lib')

	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-bleach() {
	depends=('python2-html5lib')

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
}
