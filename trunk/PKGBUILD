# Maintainer: Dan Printzell <wild@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: dsboger <https://github.com/dsboger>
# Contributor: Carl George < arch at cgtx dot us >

pkgname=tilix
pkgver=1.9.4
pkgrel=7
pkgdesc="A tiling terminal emulator for Linux using GTK+ 3"
arch=('x86_64')
url="https://github.com/gnunn1/tilix"
license=('MPL')
depends=('libx11' 'gtkd' 'vte3' 'dconf' 'gsettings-desktop-schemas')
makedepends=('ldc' 'po4a' 'meson' 'appstream')
optdepends=('python-nautilus: for "Open Tilix Here" support in nautilus'
            'libsecret: for the password manager')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('2a9482770391d11d5edc8351d426c700c2cc6c194a30391ef2ae25bb7095b59b')

prepare() {
  mkdir -p build

	cd "$srcdir"/$pkgname-$pkgver
	# Fixes "ERROR: Function does not take positional arguments."
	sed -i "/i18n.merge_file/{n;d}" data/meson.build
}

build() {
  cd build

  # Build with LDC
  export DC=ldc
	export LDFLAGS="$(echo -ne $LDFLAGS | sed -e 's/-flto/--flto=full/')"

  arch-meson ../$pkgname-$pkgver

  ninja
}

package() {
  cd build

  DESTDIR="$pkgdir" ninja install
}
