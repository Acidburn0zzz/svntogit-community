# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=lib32-clang
pkgver=9.0.1
pkgrel=1
pkgdesc="C language family frontend for LLVM (32-bit)"
arch=('x86_64')
url="https://clang.llvm.org/"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('lib32-llvm-libs' 'gcc-multilib')
makedepends=('lib32-llvm' 'cmake' 'ninja' 'python')

_source_base=https://releases.llvm.org/$pkgver
if [[ ${pkgver##*.} != 0 ]]; then
  _source_base=https://github.com/llvm/llvm-project/releases/download/llvmorg-$pkgver
fi

source=($_source_base/clang-$pkgver.src.tar.xz{,.sig}
        $_source_base/llvm-$pkgver.src.tar.xz{,.sig}
        enable-SSP-and-PIE-by-default.patch)
sha256sums=('5778512b2e065c204010f88777d44b95250671103e434f9dc7363ab2e3804253'
            'SKIP'
            '00a1ee1f389f81e9979f3a640a01c431b3021de0d42278f6508391a2f0b81c9a'
            'SKIP'
            'fde58a4902bec3715bc3d0c9250fa4c5f241af90bfd1e46f7128fa1435c03026')
validpgpkeys+=('B6C8F98282B944E3B0D5C2530FC3042E345AD05D') # Hans Wennborg <hans@chromium.org>
validpgpkeys+=('474E22316ABF4785A88C6E8EA2C794A986419D8A') # Tom Stellard <tstellar@redhat.com>

prepare() {
  cd "$srcdir/clang-$pkgver.src"
  mkdir build
  patch -Np1 -i ../enable-SSP-and-PIE-by-default.patch

  # Avoid shipping libclang-cpp before removing BUILD_SHARED_LIBS=ON
  sed -i '/add_clang_subdirectory(clang-shlib)/d' tools/CMakeLists.txt
}

build() {
  cd "$srcdir/clang-$pkgver.src/build"

  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  
  # Upstream strongly recommends against BUILD_SHARED_LIBS=ON for distro
  # builds but CLANG_LINK_CLANG_DYLIB=ON is not yet supported by clang's
  # extra tools. Keep building all libraries as shared and revisit this.

  cmake .. -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DPYTHON_EXECUTABLE=/usr/bin/python \
    -DLLVM_LIBDIR_SUFFIX=32 \
    -DCMAKE_C_FLAGS:STRING=-m32 \
    -DCMAKE_CXX_FLAGS:STRING=-m32 \
    -DLLVM_CONFIG="/usr/bin/llvm-config32" \
    -DBUILD_SHARED_LIBS=ON \
    -DLLVM_LINK_LLVM_DYLIB=ON \
    -DCLANG_LINK_CLANG_DYLIB=OFF \
    -DLLVM_ENABLE_RTTI=ON \
    -DLLVM_BUILD_TESTS=OFF \
    -DLLVM_INCLUDE_DOCS=OFF \
    -DLLVM_MAIN_SRC_DIR="$srcdir/llvm-$pkgver.src"    
  ninja
}

package() {
  cd "$srcdir/clang-$pkgver.src/build"

  DESTDIR="$pkgdir" ninja install
  rm -rf "$pkgdir"/usr/{bin,include,libexec,share}

  install -Dm644 ../LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
