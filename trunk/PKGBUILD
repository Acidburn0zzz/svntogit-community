# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@googlemail.com>
pkgname=kmscon
pkgver=6
pkgrel=3
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS)'
arch=(i686 x86_64)
url='https://github.com/dvdhrm/kmscon'
license=('MIT')
depends=(systemd libdrm mesa libegl libgbm libgles pango libxkbcommon libpciaccess xkeyboard-config)
makedepends=('libxslt' 'docbook-xsl' 'linux-api-headers')
options=(!libtool)
source=(https://github.com/downloads/dvdhrm/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('12f6966cef8e846f31dbcad916a9f347')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure
    --prefix=/usr \
    --enable-kmscon \
    --disable-wlterm \
    --enable-multi-seat \
    --enable-hotplug \
    --enable-pciaccess \
    --with-video=fbdev,dumb,drm \
    --with-fonts=pango,freetype2,8x16 \
    --with-sessions=dummy,terminal
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/usr/lib/systemd/system"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/"
  cp docs/kmscon{,@}.service "$pkgdir/usr/lib/systemd/system/"
}
