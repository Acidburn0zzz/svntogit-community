# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Lubosz Sarnecki <lubosz@gmail.com>
# Contributor: Kerrick Staley <mail@kerrickstaley.com>
# Contributor: Thijs Vermeir <thijsvermeir@gmail.com>

pkgname=gst-editing-services
pkgver=1.10.4
pkgrel=1
pkgdesc='GStreamer editing services'
arch=('i686' 'x86_64')
license=('LGPL')
url='http://gstreamer.freedesktop.org/modules/gst-editing-services.html'
depends=('gst-plugins-base')
makedepends=('intltool' 'pkgconfig' 'gtk-doc' 'gobject-introspection'
	     'bash-completion' 'python2-gobject')
checkdepends=('gst-plugins-good' 'gst-plugins-bad')
source=("https://gstreamer.freedesktop.org/src/gstreamer-editing-services/gstreamer-editing-services-$pkgver.tar.xz"{,.asc})
sha256sums=('f2ad6d02dc9d12e899059796e8de03a662f41e4d732797fb4b5ecbc973582144'
            'SKIP')
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5')

build() {
  cd "$srcdir"/gstreamer-editing-services-$pkgver
  ./configure --prefix=/usr --enable-gtk-doc \
    --with-package-name="GStreamer (Arch Linux)" \
    --with-package-origin="http://www.archlinux.org/"
  make
}

check() {
  cd "$srcdir"/gstreamer-editing-services-$pkgver
  # Broken on i686
  make check || :
}

package() {
  cd "$srcdir"/gstreamer-editing-services-$pkgver
  make DESTDIR="$pkgdir" install
}
