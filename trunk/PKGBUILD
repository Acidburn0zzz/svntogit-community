# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-themes
pkgver=0.0.0+git20200225
_commit=9e9b986593a28eeff716d0d03a04567f1f24add6
pkgrel=1
pkgdesc="Official themes for the UKUI desktop"
arch=('any')
license=('GPL')
url="https://github.com/ukui/ukui-themes"
groups=('ukui')
depends=('dconf')
makedepends=('sassc')
source=("$pkgname-$_commit.tar.gz::https://github.com/ukui/ukui-themes/archive/$_commit.tar.gz")
sha512sums=('6fec88baf4bae1c2a23527cde2963bc3e614b6fa3d493c51199b803978cb43b102f8b17056ac5ba6756e700bc6343211393842860caeef5e67b9a171ce838927')

build() {
  cd $pkgname-$_commit
  make SASS=sassc
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install

  install -Dm644 debian/25_ukui-themes.gschema.override -t "$pkgdir"/usr/share/glib-2.0/schemas
  install -dm755 "$pkgdir"/usr/share/icons
  cp -a ukui-icon-theme "$pkgdir"/usr/share/icons/
  cp -a ukui-theme "$pkgdir"/usr/share/themes/
}
