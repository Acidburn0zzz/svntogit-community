# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=darkstat
pkgver=3.0.716
pkgrel=1
pkgdesc="Network statistics gatherer (packet sniffer)"
url="http://dmr.ath.cx/net/darkstat/"
license=("GPL")
arch=(i686 x86_64)
depends=('libpcap' 'zlib' 'gcc-libs')
backup=('etc/conf.d/darkstat.conf')
source=(http://dmr.ath.cx/net/darkstat/darkstat-$pkgver.tar.bz2
	darkstat.service
	darkstat.conf)
install=darkstat.install
md5sums=('f334d3b8acd45cd06b5929a249abebe2'
         '91dd1cf9d01c2b37896f01b4570b5521'
         'aa9a9effd1e8f08860afcd6439fc94c5')

build() {
  cd $srcdir/darkstat-$pkgver
  ./configure --prefix=/usr --sbindir=/usr/bin
  make
}

package() {
  cd $srcdir/darkstat-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 "$srcdir"/darkstat.conf "$pkgdir"/etc/conf.d/darkstat.conf
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
