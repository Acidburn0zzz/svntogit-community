# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=1.3.0
pkgrel=2
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{pycryptodome,isodate,pycountry,requests,pysocks,websocket-client} 'rtmpdump')
checkdepends=("python-"{freezegun,mock,pytest,requests-mock})
makedepends=("python-"{recommonmark,setuptools,sphinx})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        streamlink-fix-mpv-player-parameter-format.patch::https://github.com/streamlink/streamlink/commit/acd7f0727f.patch)
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('00649658b74c76022a04564919431e4d6156d974caf56f25cd52a07fa5732315'
            'SKIP'
            '80cf960b74d8be712814cdf482722e3b7c9dbc95e65991ac58faf3bcf4e16166')
sha512sums=('5a114e0b1e617b2a41f8fe00f9bd6846843dd7643b4283d2fe30ea869cc9d6f495458b0ddb97955a87b7ad64e5628343262af5068b8a7a19d0d84a32020b3365'
            'SKIP'
            'fab7105f049ae034dd76df5ef69dfbae52d85f72b4a200c9c9e72ac521019deca85a33910cd7e82195da620552221e8ec487517596fc09c36b7c05cc402c72b9')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i ../streamlink-fix-mpv-player-parameter-format.patch
}

build() {
  cd "$pkgname-$pkgver"
  export STREAMLINK_USE_PYCOUNTRY="true"
  python setup.py build
  python setup.py build_sphinx -b man
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 build/sphinx/man/streamlink.1 \
    "$pkgdir/usr/share/man/man1/streamlink.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
