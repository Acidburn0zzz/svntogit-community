# Maintainer: Jonas Witschel <diabonas@archlinux.org>
# Contributor: Bruno Pagani <archange@archlinux.org>
pkgname=tpm2-abrmd
pkgver=2.3.2
pkgrel=2
pkgdesc='Trusted Platform Module 2.0 Access Broker and Resource Management Daemon'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-abrmd'
license=('BSD')
depends=('glib2' 'tpm2-tss' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-sys.so' 'libtss2-tctildr.so')
checkdepends=('cmocka' 'ibm-sw-tpm2' 'iproute2')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('76ebce3523efd49820e8f03e7848d637dccd3733176fda8943e39eee8424d0308de3319abb47bd551779f354b0d62628739faa8ff2805159ae9b4d3bc14eba90'
            'SKIP')
validpgpkeys=('42007E876F248E04A3F2FE25AE4548D043DEC7C3'  # Philip Tricca <philip.b.tricca@intel.com>
              'D760B790CCF0A41CBE7B047C316CC1FB24ABDC72') # Tadeusz Struk <tadeusz.struk@intel.com>

build() {
	cd "$pkgname-$pkgver"
	./configure \
	    --prefix=/usr \
	    --sbindir=/usr/bin \
	    --with-dbuspolicydir=/usr/share/dbus-1/system.d \
	    $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir/usr/lib/systemd/system-preset"
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
