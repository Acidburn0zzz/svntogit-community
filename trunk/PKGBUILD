# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hunter
pkgver=3.1.2
pkgrel=1
arch=('x86_64')
pkgdesc='Hunter is a flexible code tracing toolkit'
license=('BSD')
url='https://github.com/ionelmc/python-hunter'
depends=('python-manhole' 'python-colorama')
optdepends=('gdb: for remote support')
makedepends=('python-pip' 'python-wheel' 'python-manhole' 'cython')
checkdepends=('python-pytest-benchmark' 'python-virtualenv' 'python-pytest-runner' 'gdb'
              'python-process-tests' 'python-aspectlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ionelmc/python-hunter/archive/v$pkgver.tar.gz")
sha512sums=('d39f6bcf986b8f20cb6f1c636a963a0149474aa7eda901e3344db30e7bf259dd78823659316ef194eba554d6b351756277243663d3c0a362c59ca4ac62a8af08')

prepare() {
  rm python-hunter-$pkgver/src/hunter/*.c
}

build() {
  cd "$srcdir"/python-hunter-$pkgver
  python setup.py build
}

check() {
  # Hack to process .pth files properly
  # ptrace needed to pass the remote tests

  cd "$srcdir"/python-hunter-$pkgver
  virtualenv "$srcdir/pyvenv" --system-site-packages
  (
    . "$srcdir/pyvenv/bin/activate"
    python setup.py install
    python setup.py pytest
  ) || warning "Tests failed"
}

package() {
  cd python-hunter-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
