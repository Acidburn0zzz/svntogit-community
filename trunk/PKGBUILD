# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-power
pkgver=2.4.0
pkgrel=1
pkgdesc='Switchboard Power Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-power
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-power.git#tag=557432f4dc4abf8c8cf9908c6b10637aa9c9ea47)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-power

  git describe --tags
}

prepare() {
  sed "s/dbus_dep.get_pkgconfig_variable('sysconfdir')/dbus_dep.get_pkgconfig_variable('datadir')/" -i switchboard-plug-power/cli/meson.build
}

build() {
  arch-meson switchboard-plug-power build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
