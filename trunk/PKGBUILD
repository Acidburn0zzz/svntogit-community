# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>
# Contributor: loqs <bugs-archlinux@entropy-collector.net>

pkgname=catfish
pkgver=1.4.13
pkgrel=4
pkgdesc='Versatile file searching tool'
arch=(any)
url='https://docs.xfce.org/apps/catfish/start'
license=(GPL)
install=$pkgname.install
depends=(gtk3 python-cairo python-gobject python-pexpect
         python-ptyprocess python-xdg xdg-utils python-dbus)
makedepends=(git intltool python-distutils-extra)
conflicts=('zeitgeist') # FS#65344
optdepends=('mlocate: filename search')
#            'zeitgeist: integration with zeitgeist')
source=("git+https://gitlab.xfce.org/apps/catfish.git#commit=bd10e884b21ab3df5020a8d1c06f0011910c5ba8") # catfish-1.4.13
sha512sums=('SKIP')

prepare() {
  cd $pkgname

  # Support python 3.9
  git format-patch -1 --stdout 04dd64cff5d9f561830ff546ebd455e948f6b581 \
    catfish_lib/Builder.py | patch -Np1
}

build() {
  cd "$pkgname"

  python setup.py build
}

package () {
  cd "$pkgname"

  python setup.py install --root="$pkgdir" --optimize=1
  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# getver: git.xfce.org/apps/catfish/plain/catfish.1
# vim: ts=2 sw=2 et:
