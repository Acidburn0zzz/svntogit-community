# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.5.8.1
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL')
groups=('pro-audio')
depends=('cairo' 'fftw' 'fltk' 'jack' 'mxml')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('10e54f12a3f33a339a368db2116ccec882c283ab298dc1cef07a4e8092a02855910db8cc6f046a716d89567dd8e731e715f3e57d1003726662f49c8e18b75adb')

build() {
  cd "${pkgname}-${pkgver}/src"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' .
  make
}

package() {
  cd "${pkgname}-${pkgver}/src"
  make DESTDIR="${pkgdir}" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 ../{Changelog,Dependencies,README.txt,Yoshimi_Helpers}
}

# vim:set ts=2 sw=2 et:

