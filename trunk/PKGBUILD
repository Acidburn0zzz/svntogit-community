# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxtst
pkgname=lib32-$_pkgbasename
pkgver=1.1.0
pkgrel=3
pkgdesc="X11 Testing -- Resource extension library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libxext>=1.1' 'lib32-libxi>=1.3' 'recordproto>=1.14' 'inputproto>=2.0' $_pkgbasename)
makedepends=('gcc-multilib' 'pkgconfig' 'xmlto')
options=('!libtool')
source=(${url}/releases/individual/lib/libXtst-${pkgver}.tar.bz2)
md5sums=('dd6f3e20b87310187121539f9605d977')

build() {
  cd "${srcdir}/libXtst-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXtst-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
