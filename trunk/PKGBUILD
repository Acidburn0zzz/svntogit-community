# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pytest-django
pkgname=('python-pytest-django' 'python2-pytest-django')
pkgver=3.4.8
pkgrel=1
pkgdesc="A Django plugin for py.test"
arch=('any')
license=('BSD')
url="https://github.com/pytest-dev/pytest-django"
makedepends=('python-pytest' 'python2-pytest' 'python-setuptools-scm' 'python2-setuptools-scm'
             'python2-pathlib')
checkdepends=('python-django' 'python2-django' 'python-pytest-xdist' 'python2-pytest-xdist')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-django/archive/$pkgver.tar.gz"
        django-1.11.patch::https://github.com/pytest-dev/pytest-django/pull/739.patch
        django-2.1.patch::https://github.com/pytest-dev/pytest-django/pull/741.patch)
sha512sums=('0757f1b5aa61b0de20a18701192b18a12e0e4623f9894a9991f0609126087b955e29b1e83ee4ab00a09da5bd45461df35d2d5de9bf8202d963c208cdd7594218'
            '5a0301e19fb6bc81a966aa0fd28735b40421073fd4cd93da7fa37dff42814d5c438e527beb59baec4bfc0de0b764e68ade129082425f7e906bfa88867986427d'
            '5e6311eb5e696d49df62258a352293defcf51df5c8c1facc68925c699486910a63d4f894a651fd242da912fc52c2afab53417ae7613391fe0e767330e58170da')

prepare() {
  patch -d pytest-django-$pkgver -p1 -i ../django-1.11.patch
  patch -d pytest-django-$pkgver -p1 -i ../django-2.1.patch
  cp -a pytest-django-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-django-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-django-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test tests

  cd "$srcdir"/pytest-django-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH:$PWD" \
  DJANGO_SETTINGS_MODULE=pytest_django_test.settings_sqlite \
    py.test2 tests
}

package_python-pytest-django() {
  depends=('python-pytest')

  cd pytest-django-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest-django() {
  depends=('python2-pytest' 'python2-pathlib')

  cd pytest-django-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
