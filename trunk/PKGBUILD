# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=gitlab-gitaly
pkgver=0.13.0
pkgrel=2
pkgdesc="Speed up Git access using caching"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitaly"
license=('MIT')
depends=('glibc')
makedepends=('go')
backup=("etc/gitlab-gitaly/config.toml")
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/gitlab-org/gitaly/repository/archive.tar.gz?ref=v${pkgver}"
        "gitlab-gitaly.service")
sha512sums=('ed75be44cac04df10c810a07e0dce5b85a4bd5d20c0d855941c40903bcee6e08dd549ce9d58bdd67d60c5a0920785d4afa48fe9307ad8b4b87d22929aed4ddcb'
            'db27d797dfacd28763edc990503a56faaad6fccd191a4295b26676d9860714252e4af9a35652a65990d4028b1f4755be30654478d9edfcc5c179c8a6c04d794a')

_homedir="/var/lib/${pkgname}"

package() {
  cd "gitaly-v${pkgver}-"*

  make PREFIX=/usr DESTDIR=${pkgdir} install
  mkdir -p "${pkgdir}/etc/${pkgname}"
  sed \
    -e "s|^socket_path =.*|socket_path = ${_homedir}/sockets/gitlab-gitaly.socket|" \
    -e "s|^path =.*|path = ${_homedir}/repositories|" \
    config.toml.example > "${pkgdir}/etc/${pkgname}/config.toml"

  install -Dm644 config.toml.example "${pkgdir}/usr/share/${pkgname}/config.toml.example"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-gitaly.service" "${pkgdir}/usr/lib/systemd/system/gitlab-gitaly.service"
}

# vim:set ts=2 sw=2 et:
