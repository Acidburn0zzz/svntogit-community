# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-geoip2
pkgver=4.0.2
_testdatacommit=f6ed981c23b0eb33d7c07568e2177236252afda6
pkgrel=1
pkgdesc="MaxMind GeoIP2 API"
url="https://github.com/maxmind/GeoIP2-python"
license=('Apache')
arch=('any')
depends=('python-aiohttp' 'python-maxminddb' 'python-requests')
makedepends=('python-setuptools')
checkdepends=('python-mocket')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxmind/GeoIP2-python/archive/v$pkgver.tar.gz"
        https://github.com/maxmind/MaxMind-DB/archive/$_testdatacommit.tar.gz)
sha512sums=('28b38ac41ebc39b77460bc845f8a4115b61bc6c249753753f2fede89365934f3675ec93464c93e5d4062e70b3fd55fec26373d4dc563ab1054c422b45f82b28f'
            'f7b9d370c330980d9419c7bea486b258aef3fa8ab49f83b860ef73036fc577c402e2f631090c5d1d23f2a8e34f927030a8fc6dc15edcd8002136673685aecb12')

prepare() {
  cd GeoIP2-python-$pkgver
  rmdir tests/data
  ln -s "$srcdir"/MaxMind-DB-$_testdatacommit tests/data
}

build() {
  cd GeoIP2-python-$pkgver
  python setup.py build
}

check() {
  cd GeoIP2-python-$pkgver
  python setup.py test
}

package() {
  cd GeoIP2-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
