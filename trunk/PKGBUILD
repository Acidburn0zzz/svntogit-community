# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=liquidsfz
pkgver=0.2.0
pkgrel=2
pkgdesc="SFZ Sampler"
arch=('x86_64')
url="https://github.com/swesterfeld/liquidsfz"
license=('LGPL2.1')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('jack' 'libsndfile' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
provides=('soundfont-synthesizer')
source=("$pkgname-$pkgver.tar.gz::https://github.com/swesterfeld/${pkgname}/archive/${pkgver}.tar.gz"
        "$pkgname-0.2.0-fix_relro.patch::https://github.com/swesterfeld/liquidsfz/pull/6.patch"
        "${pkgname}-0.2.0-fix_test_headers.patch::https://github.com/swesterfeld/liquidsfz/pull/13/commits/cd49ccd6b6f6096de60a7e64ad04811c935a3153.patch"
        )
sha512sums=('b5c9804c0de7467f00a6cdbbd54b5c8b34785a997184d2edd40979cbe0ad368a995c49ded727b1dc7bdea7dffa0ec48a4e09634d0e21ece7aa59a33590de6d2e'
            '2e2c9e3b62ee1d5115d3b8d9711d6938d18540546c193f1d33860f5d3a39d6483af70ff758a05abf1dd2e6eed99bed20d6cb2ce39cacfa103c84f837fa82f13d'
            '6a60d647002963e0e8166a3565a74ad3f6b9f3bf39d36c4153d1307284dcca5ab98b4c4d083d4e718bb69fc3e49a557cf08b57dfaa71b70bdc056b4e6746456d')
b2sums=('0980aa110d4615ecf8528bc871fc9b37c388db6ddd47b46b8a5c8b9d8e9518f92fb0c3d033a691ae9070295bf2b66126299d8257ab51f220ae7952d72d6ae871'
        'dfd88830ee9d2441e3fb42681a00e188bd3ce78a048e03d0c69d0f3082aeb17753e2a5dd6bc7505909df9d5f61a96ff56073abfeae9183e62eca224f68d77be8'
        '9e42633d532ed12948b1bda8955cc2c8ebe69bb30a8dd6823ea4f744449a2022aaeba8b62344854fdeac3545f3c76283de4f405535824b5e2f2acc3dc20ab0b5')

prepare() {
  cd "$pkgname-$pkgver"
  # fix lv2 Makefile to achieve full RELRO:
  # https://github.com/swesterfeld/liquidsfz/issues/5
  patch -Np1 -i "../$pkgname-0.2.0-fix_relro.patch"
  # add missing headers: https://github.com/swesterfeld/liquidsfz/pull/13
  patch -Np1 -i "../$pkgname-0.2.0-fix_test_headers.patch"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  # currently only static library is possible:
  # https://github.com/swesterfeld/liquidsfz/issues/7
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --disable-static-cxx
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
  lv2lint -Mpack -I "${PWD}/lv2" "http://spectmorph.org/plugins/liquidsfz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {NEWS,TODO,{OPCODES,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
