# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=yubikey-touch-detector
pkgver=1.7.0
pkgrel=1
pkgdesc='A tool that can detect when your YubiKey is waiting for a touch'
arch=('x86_64')
url='https://github.com/maximbaz/yubikey-touch-detector'
license=('MIT')
depends=('glibc')
makedepends=('go' 'git')
optdepends=('gnupg: for GPG'
            'openssh: for SSH'
            'libnotify: for desktop notifications')
source=("${url}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz"
        "${url}/releases/download/${pkgver}/${pkgname}-${pkgver}-src.tar.gz.asc")
sha256sums=('34de65f40ad9fa3b5850f47af90dea8e356d009b706ac29722e744418ba07db5'
            'SKIP')
validpgpkeys=('EB4F9E5A60D32232BB52150C12C87A28FEAC6B20')

build() {
    cd "${pkgname}-${pkgver}"
    make build
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}

# vim:set ts=4 sw=4 et:
