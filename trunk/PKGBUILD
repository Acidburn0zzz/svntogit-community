# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=log4cxx
pkgver=0.12.0
pkgrel=1
pkgdesc="A C++ port of Log4j"
url="http://logging.apache.org/log4cxx"
license=("Apache")
depends=('apr-util' 'libxml2')
arch=('x86_64')
makedepends=('autoconf' 'automake' 'libtool' 'patch' 'zip' 'gzip' 'sed')
conflicts=('log4cxx-svn')
source=("http://archive.apache.org/dist/logging/$pkgname/$pkgver/apache-$pkgname-$pkgver.tar.gz")
sha512sums=('00fe571f9c511bf93b17cac8269ce00f7e817e8d723acf62bddff1bbe0e7facd0ab2fd75c01a93870f7e5c66718b2b73fc22c5f193bfd204e34d052b1123e60d')

build() {
  cd "$srcdir/apache-$pkgname-$pkgver"
  ./autogen.sh
  ./configure --prefix=/usr --disable-static
  make
}

package() {
  cd "$srcdir/apache-$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
