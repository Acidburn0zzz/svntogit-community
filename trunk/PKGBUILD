# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=7.2.8
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("https://pypi.python.org/packages/source/p/phonenumbers/phonenumbers-$pkgver.tar.gz")
sha512sums=('5c1b507cd69a1dc97e0fc0988dc35e0b40b24b8afedef478d203a39fd4ed43914bba02d4015cc3082c707f7a526e443f2476659a056d15dc34b7e361ddcf3f1f')

prepare() {
  cp -a phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/phonenumbers-$pkgver
  python2 setup.py build
}

check() {
  cd "$srcdir"/phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/phonenumbers-$pkgver
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd phonenumbers-$pkgver
  python2 setup.py install -O1 --root "$pkgdir"
}
