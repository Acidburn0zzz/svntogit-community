# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-jung-kurt-gofpdf
pkgver=2.1.4
pkgrel=1
pkgdesc='A PDF document generator with high level support for text, drawing and images'
arch=('any')
url="https://github.com/jung-kurt/gofpdf"
license=('MIT')
depends=('go-pie' 'golang-github-boombuler-barcode' 'golang-github-ruudk-golang-pdf417'
         'golang-golang-x-image')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jung-kurt/gofpdf/archive/v$pkgver.tar.gz")
sha512sums=('614267b0aa612e2a36895fc6b5e2c36cf278d5f5e28ec29878824295984972236c8e682db236da729a85f458186a9645a9c4395980f754f7a05047f4cf84f3c6')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  cd "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt/gofpdf

  install -Dm644 gofpdf-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
