# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=dokuwiki
pkgver=20130510
_pkgver=${pkgver:0:4}-${pkgver:4:2}-${pkgver:6:2}
pkgrel=3
pkgdesc="DokuWiki engine"
arch=('any')
url="http://www.splitbrain.org/projects/dokuwiki"
license=('GPL')
depends=('php')
backup=('etc/webapps/dokuwiki/.htaccess')
install=dokuwiki.install
options=('!strip')
source=(http://www.splitbrain.org/_media/projects/dokuwiki/dokuwiki-${_pkgver}.tgz)
md5sums=('9cb5bb79c0445df849845b586e872677')

package() {
  cd $srcdir
  install -dm0755 $pkgdir/usr/share/webapps/
  install -dm0755 $pkgdir/var/lib/dokuwiki
  install -dm0755 $pkgdir/etc/webapps/
  cp -a $pkgname-${_pkgver} $pkgdir/usr/share/webapps/dokuwiki

  mv $pkgdir/usr/share/webapps/dokuwiki/data $pkgdir/var/lib/dokuwiki/data
  ln -s /var/lib/dokuwiki/data $pkgdir/usr/share/webapps/dokuwiki/data

  mv $pkgdir/usr/share/webapps/dokuwiki/lib/plugins $pkgdir/var/lib/dokuwiki/plugins
  ln -s /var/lib/dokuwiki/plugins $pkgdir/usr/share/webapps/dokuwiki/lib/plugins

  touch $pkgdir/var/lib/dokuwiki/changes.log
  chmod 0644 $pkgdir/var/lib/dokuwiki/changes.log
  find $pkgdir/var/lib/dokuwiki -type f -exec chmod g=rw {} \; && find -type d -exec chmod g=rwx {} \;

  mv $pkgdir/usr/share/webapps/dokuwiki/conf $pkgdir/etc/webapps/dokuwiki
  ln -s /etc/webapps/dokuwiki $pkgdir/usr/share/webapps/dokuwiki/conf
  chmod g=rwx $pkgdir/etc/webapps/dokuwiki

  cp $pkgdir/usr/share/webapps/dokuwiki/.htaccess.dist $pkgdir/etc/webapps/dokuwiki/.htaccess
  ln -s /etc/webapps/dokuwiki/.htaccess $pkgdir/usr/share/webapps/dokuwiki/.htaccess
}
