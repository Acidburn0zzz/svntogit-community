# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: eolianoe <eolianoe [at] gmail [DoT] com>
# Contributor: Romain Schmitz <slopjong .at. slopjong .dot. de>

pkgbase=python-sphinx_rtd_theme
pkgname=('python-sphinx_rtd_theme' 'python2-sphinx_rtd_theme')
pkgver=0.1.9
pkgrel=3

pkgdesc="Python Sphinx Read The Docs Theme"
url='https://github.com/snide/sphinx_rtd_theme'
arch=('any')
license=('MIT')

makedepends=('python-setuptools' 'python2-setuptools')

source=("https://pypi.python.org/packages/source/s/sphinx_rtd_theme/sphinx_rtd_theme-$pkgver.tar.gz"
        "0001-Add-missing-SOURCELINK_SUFFIX.patch")

sha256sums=('273846f8aacac32bf9542365a593b495b68d8035c2e382c9ccedcac387c9a0a1'
            '514e392a88d8123e65db21f151dee0b9b861b8828fe76209f8eec74277989c5e')

prepare() {
  cd sphinx_rtd_theme-$pkgver
  patch -p1 <"$srcdir"/0001-Add-missing-SOURCELINK_SUFFIX.patch
}

build() {
  cp -r sphinx_rtd_theme-$pkgver sphinx_rtd_theme-$pkgver-py2

  cd sphinx_rtd_theme-$pkgver
  python setup.py build

  cd "$srcdir"/sphinx_rtd_theme-$pkgver-py2
  python2 setup.py build
}

package_python-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sphinx_rtd_theme() {
  cd sphinx_rtd_theme-${pkgver}-py2 
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

