# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-jukebox
_pkgname=Jukebox
pkgver=34
pkgrel=1
pkgdesc="Media player for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Jukebox"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3' 'gst-plugins-base')
optdepends=('gst-plugins-good: Extra media codecs'
            'gst-plugins-bad: Extra media codecs'            
            'gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('3679077a7f6a0af1e04e62b7bb2654752843d055f2718db6b5be4480f5e1ffeb')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
