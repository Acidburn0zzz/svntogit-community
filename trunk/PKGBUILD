# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.9.1
pkgrel=2
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpegthumbnailer' 'freeimage' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
         fix-build.patch)
sha512sums=('d66b8e8e6acae44060b9a3b0f621ebc69f3450c04a1ba5f96722846c04a77c273f909c67aed3fc7ee8890e60d57c12d1d45c8f04318ba100ee7ccd3fdfc2cfc6'
            'bc78d3708df52a3b17d606d84c3e1072f2ac6f73e4be73552816cea6acc7fe909ec17fc073b0228168b14bea127baf61ec114d60345f6c41c8a9e13a8d1d6ace')
options=(!lto)
	
prepare() {
  cd deepin-album-$pkgver

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp

  patch -p1 -i ../fix-build.patch
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
