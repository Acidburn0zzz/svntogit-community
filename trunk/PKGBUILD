# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Ashwin Vishnu <ashwinvis+arch at pr0t0nm4il dot com>

pkgname=python-beniget
pkgver=0.3.0
pkgrel=2
pkgdesc='A static analyzer for Python code'
arch=(any)
url='https://github.com/serge-sans-paille/beniget'
license=(BSD)
depends=(python-gast)
makedepends=(python-setuptools)
source=(https://files.pythonhosted.org/packages/source/b/beniget/beniget-$pkgver.tar.gz)
sha256sums=('062c893be9cdf87c3144fb15041cce4d81c67107c1591952cd45fdce789a0ff1')

build() {
  cd beniget-$pkgver
  python setup.py build
}

package() {
  cd beniget-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
