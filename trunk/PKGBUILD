# Maintainer: David Runge <dave@sleepmap.de>

pkgname=php-redis
_name=redis
_upstream=phpredis
pkgver=5.0.0
pkgrel=1
pkgdesc="An API for communicating with the Redis key-value store"
arch=('x86_64')
url="https://github.com/phpredis/phpredis/"
license=('PHP')
depends=('php' 'liblzf')
checkdepends=('redis')
optdepends=('redis: use a local redis instance')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_upstream}/${_upstream}/archive/${pkgver}.tar.gz")
sha512sums=('3826cdb48c2747d36ea3ce44eb046ffb3e56e525181276dfa2b88d5484064aa444ae8254954a93260e8d21a095d690eea908d9394721dc5d169e05e92256b575')

prepare() {
  mv -v "${_upstream}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  echo ";extension=${_name}" > "${_name}.ini"
  phpize
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-redis-lzf \
              --with-liblzf=/usr/lib/
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k test
}

package() {
  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.ini" -t "${pkgdir}/etc/php/conf.d/"
  install -vDm 644 {{README,arrays,cluster}.markdown,CREDITS} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
