# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Tomas A. Schertel <tschertel@gmail.com>

pkgname=cherrytree
pkgver=0.29.2
pkgrel=1
pkgdesc='Hierarchical note taking application featuring rich text and syntax highlighting'
arch=('any')
url='http://www.giuspen.com/cherrytree/'
license=('GPL')
depends=('pygtksourceview2' 'hicolor-icon-theme' 'desktop-file-utils')
install=$pkgname.install
source=(http://www.giuspen.com/software/$pkgname-$pkgver.tar.xz)
sha256sums=('9272e789a2283d191b7371884d2ea98ff22575b6514cc317f0425e3112934d3b')

build() {
	cd $srcdir/$pkgname-$pkgver

	# python2 fix
	for file in $(find . -name '*.py') cherrytree; do
 		echo "Applying python2 fix for ${file}"
 		sed -i 's_/usr/bin/env python_/usr/bin/env python2_' ${file}
 	done
}
	
package() {
	cd $srcdir/$pkgname-$pkgver
  	python2 setup.py install --root=$pkgdir/ --optimize=1
}
