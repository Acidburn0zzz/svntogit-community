# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alois Nespor <alium at artixlinux.org>

pkgname=pdfarranger
pkgver=1.4.2
pkgrel=1
pkgdesc='Helps merge or split PDF documents and rotate, crop and rearrange pages'
arch=(any)
url='https://github.com/jeromerobert/pdfarranger'
license=('GPL3')
source=($pkgname-$pkgver.tar.gz::https://github.com/jeromerobert/pdfarranger/archive/$pkgver.tar.gz)
depends=(gtk3 python-gobject python-cairo python-pikepdf ghostscript python-setuptools poppler-glib)
makedepends=(python-distutils-extra)
sha256sums=('7fcf7bb3cc4a093a17013bac3c3857e9dcfd46aa3814d17287b58bac9ae90772')

build () {
  cd $pkgname-$pkgver
  python setup.py build
}

package () {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
