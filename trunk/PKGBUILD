# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: getzze <getzze at gmail dot com>
pkgname=python-tensorflow-serving-api
_name=tensorflow_serving_api
pkgver=1.11.1
pkgrel=1
pkgdesc="Flexible, high-performance serving system for machine learning models, designed for production environments"
arch=(any)
url="https://www.tensorflow.org/serving/"
license=('Apache')
depends=('python' 'python-grpcio' 'python-tensorflow' 'python-protobuf')
makedepends=('bazel' 'python-wheel' 'python-pip')
source=("https://github.com/tensorflow/serving/archive/${pkgver}.tar.gz")
sha256sums=('ac13c29e09987dccca0c8b66b0f6c152bd9b248f640091cf8765902cb3894485')

build() {
  cd serving-${pkgver}
  bazel build -c opt tensorflow_serving/tools/pip_package:build_pip_package
  bazel-bin/tensorflow_serving/tools/pip_package/build_pip_package /tmp/pip
}

package() {
  cd serving-${pkgver}
  pip install --ignore-installed --upgrade --root $pkgdir/ /tmp/pip/tensorflow_serving*.whl --no-dependencies
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
