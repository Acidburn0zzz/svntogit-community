# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsbeautifier
pkgname=('python-jsbeautifier' 'python2-jsbeautifier')
pkgver=1.9.0
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
makedepends=('python-setuptools' 'python2-setuptools' 'python-editorconfig' 'python2-editorconfig')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('cd9d21c0d2fcfeb357023713c5ff653d5d1dbdae593eded230b1604cd3df5ba8893c71ab65c3351f18a4c4d985e39daf3a97abfb2b3a5f6306dfa52140e0de15')

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
  python2 setup.py build
}

check() {
  cd js-beautify-$pkgver/python
  python setup.py test || warning "Broken tests"
  python2 setup.py test || warning "Broken tests"
}

package_python-jsbeautifier() {
  depends=('python-six' 'python-editorconfig')

  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsbeautifier() {
  depends=('python2-six' 'python2-editorconfig')

  cd js-beautify-$pkgver/python
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/js-beautify{,2}
}

# vim:set ts=2 sw=2 et:
