# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Federico Cuello <fedux@fedux.com.ar>

pkgname=redmine
pkgver=4.0.3
pkgrel=2
pkgdesc="A flexible project management web application written using Ruby on Rails framework."
arch=('any')
url="https://www.redmine.org"
license=('GPL2')
depends=('ruby' 'libmagick6' 'ruby-bundler' 'libxml2' 'libxslt')
makedepends=('mariadb-libs' 'postgresql-libs')
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'mysql: MySQL database support'
            'postgresql: PostgreSQL database support'
            'sqlite: SQLite database support')
source=("$url/releases/$pkgname-$pkgver.tar.gz"
	"Gemfile.local"
	"redmine.service")
sha512sums=('010b6c190b762bb12ee936c043eda9ee532384c1ac408883919d59cd782ef1797fccb94c20619e3d454e68cea624ad9c81c91b748c50a27505b03ae63b3b46ac'
            '750ae2aa12ab6b94ab818f329b95e7aa04a148512472548a3d2f91df4af59709dd841d553402d4332e6fc49a8bb643b17e5bacb1c26a99702fde32cae053ec29'
            'aa2186ed970667baf8b80c4b65fce76291074bed25cf8e78c6a265afb6e8a00c67eb195ceb8fc021cdc0b70731a008ccb64cdd28f492e130d469a71f364d40a7')

build() {
    cd "$srcdir"/redmine-$pkgver

    export PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig"
    cp "${srcdir}"/Gemfile.local Gemfile.local
    bundle lock
    bundle install --no-cache --deployment --without development test
}

package() {
    install -dm 755 "$pkgdir"/usr/share/webapps/
    cp -a --no-preserve=ownership "$srcdir"/redmine-$pkgver "$pkgdir"/usr/share/webapps/redmine

    install -Dm0644 "${srcdir}"/redmine.service "${pkgdir}"/usr/lib/systemd/system/redmine.service

    cd "$pkgdir"/usr/share/webapps/redmine
    rm -rf files log
    install -dm0755 -o http -g http "$pkgdir"/var/lib/redmine/files
    install -dm0755 -o http -g http "$pkgdir"/var/log/redmine
    mv tmp "$pkgdir"/var/lib/redmine/tmp
    chown -R http:http "$pkgdir"/var/lib/redmine/tmp
    ln -s /var/log/redmine log
    ln -s /var/lib/redmine/files files
    ln -s /var/lib/redmine/tmp tmp
}
