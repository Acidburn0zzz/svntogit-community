# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: Mario Danic <mario.danic@gmail.com>

pkgbase=python-paramiko
pkgname=python2-paramiko
pkgver=1.7.7.1
pkgrel=3
pkgdesc="Python module that implements the SSH2 protocol"
url="http://www.lag.net/paramiko/"
license=('LGPL')
arch=('any')
depends=('python2-crypto')
conflicts=('python-paramiko<=1.7.7.1-2')
replaces=('python-paramiko<=1.7.7.1-2')
source=(http://www.lag.net/paramiko/download/paramiko-$pkgver.tar.gz)
md5sums=('ce8e2c254378312a264206f65c354d72')

package_python2-paramiko() {
  cd "$srcdir/paramiko-$pkgver"

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
  install -dm755 "$pkgdir/usr/share/doc/$pkgname/demos"
  install -m644 demos/* "$pkgdir/usr/share/doc/$pkgname/demos"
  chmod 755 "$pkgdir/usr/share/doc/$pkgname/demos/"*.py

  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}
