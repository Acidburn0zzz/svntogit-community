# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=ortp
pkgver=5.0.23
pkgrel=1
pkgdesc='A Real-time Transport Protocol (RTP) library'
arch=(x86_64)
url='https://github.com/BelledonneCommunications/ortp'
license=(GPL3)
depends=(bctoolbox)
makedepends=(cmake doxygen)
source=(https://github.com/BelledonneCommunications/ortp/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('bc7b5926c442ffc8efc2be5727a74264b7caeb9ded0caf1e2faa1197bbc4e2a9')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
