# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=4.0.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'dbus-glib' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapps')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'intltool')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "cinnamon-screensaver.pam")
sha512sums=('c46488d4ad8c09ee543f4e6a201b1983f5a8728948391646b6c950a2c1886e81fed2fda57d3352b2a2962674e6c2bdbd9f41aa60fd30548760fbf66952df5913'
            'bbda5b733a1b84345e960e463692cc34475176b49c0c55ed49a5eaef46a496e6acda8467d94ebaa8a7a1e23199b94d7685f9b8af048c429df8fccd31cf172e45')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/cinnamon-screensaver \
                --localstatedir=/var
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
    install -Dm644 ../cinnamon-screensaver.pam "${pkgdir}"/etc/pam.d/cinnamon-screensaver
}
