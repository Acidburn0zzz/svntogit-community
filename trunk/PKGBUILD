# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>

pkgname=fop
pkgver=1.0
pkgrel=5
pkgdesc='XSL-FO implementation in Java'
arch=('any')
url='http://xmlgraphics.apache.org/fop/'
license=('APACHE')
depends=('java-xmlgraphics-commons' 'java-commons-logging' 'java-commons-io' 'java-batik'
         'java-avalon-framework')
makedepends=('apache-ant' 'java-environment')
source=("http://ftp-stud.hs-esslingen.de/pub/Mirrors/ftp.apache.org/dist/xmlgraphics/$pkgname/source/$pkgname-$pkgver-src.tar.gz"
        'fop.conf')
md5sums=('95dcc4c2dd08b4bc88ce9ce1ee88c439'
         '3dc3325bf77afc787024f33e7d6ab99e')
backup=('etc/fop.conf')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ant package 
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm0644 "build/fop.jar" "$pkgdir/usr/share/java/fop/fop.jar"
  install -Dm0644 "../fop.conf" "$pkgdir/etc/fop.conf"
  install -Dm0755 "$pkgname" "$pkgdir/usr/bin/$pkgname"
}
