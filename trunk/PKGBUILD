# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.br>

pkgbase=python-pyro
pkgname=("$pkgbase" "${pkgbase/-/2-}")
pkgver=4.57
pkgrel=1
pkgdesc='Distributed object middleware for Python (RPC)'
url='http://pypi.python.org/pypi/Pyro4/'
arch=('any')
license=('MIT')
depends=('python' 'python2')
source=("https://pypi.python.org/packages/bb/12/130d40e80788abec6cf47cf6b583032a5a9137ef87d9606dfeb378c0032a/Pyro4-4.57.tar.gz#md5=5480615d5c9b53e6c47900e53cff7fd0")
md5sums=('5480615d5c9b53e6c47900e53cff7fd0')

package_python-pyro() {
  depends=('python')

  cd "Pyro4-$pkgver"

  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pyro() {
  depends=('python2')

  cd "Pyro4-$pkgver"

  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
