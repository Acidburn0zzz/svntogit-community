# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: xyzzy <628208@gmail.com>

pkgname=flameshot
pkgver=0.8.1
pkgrel=2
pkgdesc="Powerful yet simple to use screenshot software"
arch=('x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-svg' 'hicolor-icon-theme')
makedepends=('make' 'qt5-tools' 'cmake')
source=("${pkgname}-v${pkgver}.tar.gz::https://github.com/lupoDharkael/flameshot/archive/v${pkgver}.tar.gz"
"pr969.patch::https://github.com/flameshot-org/flameshot/commit/0d5386edd4223dca682c0a50e53f6931af080ee9.patch"
"pr648.patch::https://github.com/flameshot-org/flameshot/commit/e1214e23e5a2641d585e47aa064cfcbb8a2b6111.patch")
sha256sums=('92394c469536b264f36e592b51468d9bbe4ea619888bb0090e8a4831c2ce2d7c'
            'c69c5055452e688cd3f2ca8b2d7ba9e3f8985727884f5ee6cba8e1d686e815f2'
            '0f5aeb9cab8540ef0fec2ce71de18aabb7fd8ffeb2b080fab1ff88b88569a22c')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i ${srcdir}/pr969.patch # https://github.com/flameshot-org/flameshot/commit/0d5386edd4223dca682c0a50e53f6931af080ee9
  patch -Np1 -i ${srcdir}/pr648.patch # https://github.com/flameshot-org/flameshot/commit/e1214e23e5a2641d585e47aa064cfcbb8a2b6111
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  
  # zsh _flameshot completion is provided by zsh-completions so exclude from packaging
  rm -rf ${pkgdir}/usr/share/zsh/
}
