# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.6.0
pkgrel=2
pkgdesc="A fast AppStream metadata generator"
arch=(i686 x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb)
makedepends=(dmd meson ninja mustache-d curl)
source=("https://github.com/ximion/appstream-generator/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz" metainfo-no-summary-warning.patch)
sha256sums=('68f538f2163004b8e2171c694233af401c2cd47a9ea1472265753a48b20600e3'
            'ca060c10da22acaf572c78917b7a9587fa989757bfed26f468280ce1f078dc81')

prepare() {
  mkdir -p build

# fix build
  sed -e 's/core.sys.linux.stdio/core.sys.posix.stdio/' -i $pkgname-$pkgver/src/asgen/utils.d

# Don't treat 'metainfo-no-summary' hints as errors, it excludes too many packages
  cd $pkgname-$pkgver
  patch -p1 -i ../metainfo-no-summary-warning.patch
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload_js=false --prefix=/usr
  ninja -j1
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
