# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Tim Meusel <tim@bastelfreak.de>

_gemname='concurrent-ruby'
pkgname='ruby-concurrent'
pkgver=1.1.7
pkgrel=1
pkgdesc='Modern concurrency tools including agents, futures, promises, thread pools, actors, supervisors, and more.'
arch=('any')
url='https://github.com/ruby-concurrency/concurrent-ruby'
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc')
optdepends=('java-runtime>=8: jruby support')
options=('!emptydirs')
source=("https://rubygems.org/gems/${_gemname}-${pkgver}.gem")
noextract=("${_gemname}-${pkgver}.gem")
sha512sums=('037add9697ac36a2b7e24e39e6c11c1c7b593bd55bf0f3c3c56bdc320c05b05bcafefd463bb9211859e50851cc3858309d0cd5dacc4e521c1ca433dcf8956ba8')

package() {
  local _gemdir="$(ruby -e 'puts Gem.default_dir')"

  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
