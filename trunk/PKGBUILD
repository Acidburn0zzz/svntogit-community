# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-parsel
pkgname=('python-parsel' 'python2-parsel')
pkgver=1.1.0
pkgrel=1
pkgdesc="Parsel is a library to extract data from HTML and XML using XPath and CSS selectors"
arch=('any')
license=('BSD')
url="https://github.com/scrapy/parsel"
makedepends=('python-cssselect' 'python2-cssselect' 'python-lxml' 'python2-lxml'
             'python-pytest-runner' 'python2-pytest-runner' 'python-six' 'python2-six'
             'python-w3lib' 'python2-w3lib' 'git')
source=("git+https://github.com/scrapy/parsel.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a parsel{,-py2}
}

build() {
  cd "$srcdir"/parsel
  python setup.py build

  cd "$srcdir"/parsel-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsel
  python setup.py pytest

  cd "$srcdir"/parsel-py2
  python2 setup.py pytest
}

package_python-parsel() {
  depends=('python-cssselect' 'python-lxml' 'python-six' 'python-w3lib')

  cd parsel
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-parsel() {
  depends=('python2-cssselect' 'python2-lxml' 'python2-six' 'python2-w3lib')

  cd parsel-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
