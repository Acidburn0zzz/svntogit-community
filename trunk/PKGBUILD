# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=2.6.5
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=(x86_64)
url=https://github.com/elementary/photos
license=(GPL3)
depends=(
  cairo
  gdk-pixbuf2
  geocode-glib
  glib2
  gst-plugins-base-libs
  gstreamer
  gtk3
  json-glib
  libexif
  libgee
  libgexiv2
  libgphoto2
  libgranite.so
  libgudev-1.0.so
  libraw
  libsoup
  libxml2
  pango
  rest
  sqlite
)
makedepends=(
  git
  intltool
  meson
  vala
  webkit2gtk
)
optdepends=('webkit2gtk: Publishing plugins')
groups=(pantheon)
source=(pantheon-photos::git+https://github.com/elementary/photos.git#tag=4c01882555f6dac04515e737e6fb15f54a58074c)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-photos

  git describe --tags
}

build() {
  arch-meson pantheon-photos build \
    -D libunity=false
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
