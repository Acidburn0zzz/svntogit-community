# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-meilisearch
pkgver=0.14.1
pkgrel=1
pkgdesc='Python client for MeiliSearch API'
arch=(any)
license=('MIT')
url='https://pypi.org/project/meilisearch/'
depends=(python)
makedepends=(python-requests)
checkdepends=('python-pytest' 'python-pytest-ordering')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/meilisearch-python/archive/v${pkgver}.tar.gz)
sha512sums=('7dafbd937182e6129d89fd21c897155732b89843e0d017d464faf9f2c9e6dff4603f2ed2a0a343b998f44c8fba0f9879aa4bbd42b57dd51994da8d540b06eb22')

build() {
  cd "meilisearch-python-${pkgver}"
  python setup.py build
}

check() {
  # requires running meilisearch https://github.com/meilisearch/meilisearch-python/blob/master/.github/workflows/test.yml#L20
  cd "meilisearch-python-${pkgver}"
  #pytest .
}

package() {
  cd "meilisearch-python-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
