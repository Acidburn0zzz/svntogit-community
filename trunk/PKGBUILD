# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.7.0
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('51bbd83a5a68bf9dcf565758a72660b8231b7aacdbba87c0882df9e54886493a8058b2ceca7ff908bce0fe9aabbb885bb2098a14b20c3e306536701326c4d3a5')
b2sums=('44b6c5bea51b4ac6beca519eebe1e92cc6f8846d5ab26424fb01feb96a4b4e761486cb28400eee34d98ce495943a080cac90a3e0b30f6158f85ed5fb40ce810e')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # fixtures are not imported correctly without poetry
  # https://github.com/tmux-python/tmuxp/issues/658
  PYTHONPATH="build:tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_awf" \
  PYTHONPATH+=':tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_bs' \
  PYTHONPATH+=':tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_bwb' \
  PYTHONPATH+=':tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_fail' \
  PYTHONPATH+=':tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_owc' \
  PYTHONPATH+=":tests/fixtures/pluginsystem/plugins/tmuxp_test_plugin_r:$PYTHONPATH"
  export PYTHONPATH
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
