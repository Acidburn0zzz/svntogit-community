# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
# Contributor: Hao Long <imlonghao@archlinuxcn.org>

pkgname=trivy
pkgver=0.25.1
pkgrel=1
pkgdesc='A Simple and Comprehensive Vulnerability Scanner for Containers, Suitable for CI'
arch=('x86_64')
url='https://github.com/aquasecurity/trivy'
license=('Apache')
depends=('device-mapper')
optdepends=('rpm: RHEL/CentOS based image support')
makedepends=('go' 'btrfs-progs')
provides=('trivy')
conflicts=('trivy')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
b2sums=('bd06862845a2b37b7eaa5733b48973038ef418407d322144b318afea5e2c3062a3e6ddc1c1647e99c120dfead30875ce2e85d1b6bf83b44f96b524f413cbc69f')

prepare() {
  cd ${pkgname}-${pkgver}

  find docs -type f -name "*.md" \
    -exec sed "s|\(../\)*imgs/\(.*\.[a-z]\{3\}\)|${url}/raw/main/docs/imgs/\2|g" -i {} \;

  rm -Rf docs/{build,imgs}
}

build() {
  cd ${pkgname}-${pkgver}/cmd/trivy

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build .
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 "cmd/trivy/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  mkdir -p "${pkgdir}/usr/share/doc"
  cp -r docs "${pkgdir}/usr/share/doc/${pkgname}"
}
