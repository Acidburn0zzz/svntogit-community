# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# PKGBUILD base; Simper < simonpersson1 AT gmail DOT com >

pkgname=kup
pkgver=0.7.3
pkgrel=1
pkgdesc="A KDE bup backup software for helping people to keep up-to-date backups"
arch=(x86_64)
url="https://www.linux-apps.com/p/1127689/"
license=(GPL2)
depends=(kidletime kinit hicolor-icon-theme)
makedepends=(extra-cmake-modules plasma-framework)
optdepends=('bup: support for incremental backups'
            'rsync: support for synced backups')
source=("https://github.com/spersson/Kup/archive/$pkgname-$pkgver.tar.gz")
sha256sums=('47b97314dd11579a6ecc723de687c9ebe7cafb300d2ce39f2510d36c17de2f3b')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../Kup-$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
