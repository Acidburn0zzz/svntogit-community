# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-generator
pkgver=5.1.0
pkgrel=1
pkgdesc="Generation of API client libraries, server stubs, documentation and configuration"
arch=('any')
url="https://github.com/openapitools/openapi-generator/"
license=('Apache')
depends=('bash' 'java-runtime=8')
makedepends=('maven' 'java-environment=8' 'strip-nondeterminism')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}.sh")
sha512sums=('87b8cc6aedb2865ca765495d7b352d5419784bcbadc97db05fe60afbab625c53be562c3e7ed567cdffccafab1ccbd0e2b048bcb1d10fa58c692ad19b6c55038a'
            '0b81955e2da12182b70366c4d8ff48d970a0edee6242a13dc657879b2e1c15205a2bfa01cbef51dd86d90e1176569a211b704d30d19b529e15ead825a89a4e9c')
b2sums=('3d1928e397715da639b335cb39ed2c3c31d46025241da60b173285582fff95a248943b82acd3d2a5076faed59446d38dc7f078372a9cf6653a4734cae31d6b71'
        'd2f38df8fd23a32e95e9e09624283ca4927dce8cf6dbdbfc11547396ae2bd9c6c1ff65082447f1e30caba3654fd6e50558272d524242ca6727df7024729a1123')

build() {
  cd "$pkgname-$pkgver"
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" \
    modules/openapi-generator/target/openapi-generator-${pkgver}.jar \
    modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    modules/openapi-generator-core/target/openapi-generator-core-${pkgver}.jar \
    modules/openapi-generator-maven-plugin/target/openapi-generator-maven-plugin-${pkgver}.jar \
    modules/openapi-generator-online/target/openapi-generator-online.jar

}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644  modules/openapi-generator-cli/target/openapi-generator-cli.jar \
    -t "${pkgdir}/usr/share/java/${pkgname}/"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -svf "${pkgname}" "${pkgdir}/usr/bin/${pkgname}-cli"
  install -vDm 644 scripts/openapi-generator-cli-completion.bash \
    "${pkgdir}/usr/share/bash/bash-completion/completions/openapi-generator"
}
