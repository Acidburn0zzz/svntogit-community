# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aioitertools
_pkgname=aioitertools
# https://github.com/omnilib/aioitertools/blob/main/CHANGELOG.md
pkgver=0.9.0
pkgrel=1
pkgdesc='Implementation of itertools, builtins, and more for AsyncIO and mixed-type iterables'
arch=(any)
url='https://github.com/omnilib/aioitertools'
license=(MIT)
depends=(python)
makedepends=(python-flit-core python-build python-install)
source=("https://files.pythonhosted.org/packages/source/a/$_pkgname/$_pkgname-$pkgver.tar.gz")
sha256sums=('0fd203d53192193973cae71fa14f0d3689328388ad41ca4a4e03e433f98871ac')

build() {
  cd $_pkgname-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_pkgname-$pkgver
  python -m unittest -v aioitertools.tests
}

package() {
  cd $_pkgname-$pkgver
  python -m install --optimize 0 1 --destdir="$pkgdir" dist/*.whl
  # Replace python-install with python-installer when the latter comes with a new version
  # https://github.com/pradyunsg/installer/issues/104
  # python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
