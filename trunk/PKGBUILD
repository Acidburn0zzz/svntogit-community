# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.2
pkgrel=1
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org/
license=('GPL3')
depends=('python-aiohttp' 'python-defusedxml' 'python-dulwich'
         'python-icalendar' 'python-jinja' 'python-multidict')
makedepends=('python-setuptools')
optdepends=('python-prometheus_client: for metrics with the standalone server'
            'uwsgi-plugin-python: for the WSGI interface')
source=("https://files.pythonhosted.org/packages/source/x/xandikos/xandikos-$pkgver.tar.gz"
        'https://github.com/jelmer/xandikos/commit/a2a96c3e876bf79f8a91f312b1ffb8749d582688.patch')
sha256sums=('9b9fdb2a80a8b973c7f9877c6920b9f2ff7d8165ddfce36d9d26498678628847'
            'e9c6b4eff2467e00e5028a2e2b3c7dbf2f0dac300075fed133b7694b9ef75889')

prepare() {
  cd xandikos-$pkgver
  patch -p1 -i ../a2a96c3e876bf79f8a91f312b1ffb8749d582688.patch
}

build() {
  cd xandikos-$pkgver
  python setup.py build
}

check() {
  cd xandikos-$pkgver
  python -m unittest xandikos.tests.test_suite
}

package() {
  cd xandikos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos/examples examples/*
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 xandikos.1
}

# vim:set ts=2 sw=2 et:
