# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

_pkgbase='websocket-client'
__pkgbase="${_pkgbase//-/_}"
pkgname=("python-${_pkgbase}" "python2-${_pkgbase}")
pkgver=0.54.0
pkgrel=2
pkgdesc="WebSocket client library for Python"
arch=(any)
url="https://github.com/${_pkgbase}/${_pkgbase}"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=("https://pypi.io/packages/source/w/${_pkgbase}/${__pkgbase}-${pkgver}.tar.gz"
        'retry-connect-on-EINTR.patch')
sha512sums=('ba4dfb6b879ec88ca446f44a77b91bbace55faa1a276f493174f3dc5b96b4d400d43c886798666da98e331610f71b4629f894d97f4614acf8d64affe8370bfec'
            '51c1c84de6f6c7600aa4bbacb923908acd69b4ae2dc21e0861c9f3e14e339580b0b094991821c2b34277fb427d4cbbd2407360316b267a3e67354ca89725c50c')

prepare() {
  # Not sure why the conditions are not working :/
  sed -i "s/'backports.ssl_match_hostname'//" "${srcdir}/${__pkgbase}-${pkgver}/setup.py"

  cd "${srcdir}/${__pkgbase}-${pkgver}"
  patch -p1 -N -l -i "${srcdir}/retry-connect-on-EINTR.patch"

  cp -a "${srcdir}/${__pkgbase}-${pkgver}"{,-py2}
}

build() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py build
}

check() {
  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py test

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py test
}

package_python-websocket-client() {
  depends=('python-six')

  cd "${srcdir}/${__pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-websocket-client() {
  depends=('python2-six')

  cd "${srcdir}/${__pkgbase}-${pkgver}-py2"
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  mv "$pkgdir"/usr/bin/wsdump{,2}.py
}

# vim:set ts=2 sw=2 ft=sh et:
