# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: TDY <tdy@gmx.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=pd
pkgver=0.49.0
_ver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="The Pure Data real-time music and multimedia environment"
arch=('x86_64')
url="http://msp.ucsd.edu/software.html"
license=('custom:BSD')
groups=('pro-audio')
depends=('fftw' 'portaudio' 'tk')
makedepends=('gendesk' 'portmidi')
optdepends=('portmidi: for alternative portmidi support')
provides=('puredata')
source=("http://msp.ucsd.edu/Software/${pkgname}-${_ver}.src.tar.gz"
        "${pkgname}.png")
sha512sums=('5a8e606d2e1b62bc65903c7a30c6339f0f9acc676290d233bcb8ed87a817c7c3ff63207779f5e0d9caa231ad988770e5c2225921c2a616df661b3960b2c72480'
            '61300b58f10018b0bd28424ac00d1b8158f8ace9855742e19e48c98f915cabcade6c6041460aa795005c0fb6abc93e7116f9999c6d001999916c9fe6c85c162d')

prepare() {
  mv -v "${pkgname}-${_ver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  # XDG desktop files
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --exec ${pkgname} \
          --name "Pure Data" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-alsa \
              --enable-fftw \
              --enable-jack \
              --enable-portaudio \
              --enable-portmidi \
              --without-local-portaudio \
              --without-local-portmidi
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  # license
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # desktop file
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  # icon from puredata.info
  # https://puredata.info/Members/claudiusmaximus/icons/index_html/
  install -vDm 644 "${srcdir}/${pkgname}.png" \
    -t "${pkgdir}/usr/share/pixmaps/"
  # readme
  install -vDm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # fix broken symlink
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  rm -v "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ln -sv "/usr/bin/${pkgname}" "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
  ls -lah "${pkgdir}/usr/lib/${pkgname}/bin/${pkgname}"
}
