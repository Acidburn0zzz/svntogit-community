# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=pencil2d
pkgver=0.6.4
pkgrel=3
pkgdesc="Easy, intuitive tool to make 2D hand-drawn animations"
arch=(x86_64)
url="https://www.pencil2d.org/"
license=(GPL2)
depends=(ffmpeg hicolor-icon-theme qt5-multimedia qt5-svg qt5-xmlpatterns)
source=("$pkgname-$pkgver.tar.gz::https://github.com/pencil2d/pencil/archive/v${pkgver}.tar.gz"
        "pencil2d.appdata.xml"
        "qt5-5.15.patch")
sha256sums=('9be6d699cb5a960af89a10ee77dce33ddfe050eb3f0327a055e13f395d92afe6'
            '8bf2cf68f0ca45a153e36d36defeb1f4602ec821bebb48dca8b8e6b4691f2eb8'
            'c148c3ac06cd9851c3ee032b3693dcf014097f96c21bbc5431b64ace40aff246')

prepare() {
  cd pencil-$pkgver
  patch -Np1 -i ../qt5-5.15.patch
}

build() {
  cd pencil-$pkgver
  qmake PREFIX=/usr
  make
}

package() {
  cd pencil-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 ../pencil2d.appdata.xml "$pkgdir/usr/share/metainfo/pencil2d.appdata.xml"
}
