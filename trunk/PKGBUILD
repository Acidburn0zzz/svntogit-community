# $Id$
# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>

pkgname=tarsnap
pkgver=1.0.33
pkgrel=1
pkgdesc='An online encrypted snapshotted backup service'
arch=('i686' 'x86_64')
url='http://www.tarsnap.com/'
license=('custom')
depends=('bzip2' 'openssl' 'xz>=5.0.0')
makedepends=('e2fsprogs')
source=("https://www.tarsnap.com/download/$pkgname-autoconf-$pkgver.tgz")
md5sums=('a6267972d834198716b9fe09680e47e2')
sha256sums=('0c0d825a8c9695fc8d44c5d8c3cd17299c248377c9c7b91fdb49d73e54ae0b7d')

build() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  ./configure --prefix=/usr --sysconfdir="/etc/$pkgname"
  make
}

package() {
  cd "$srcdir/$pkgname-autoconf-$pkgver"

  make DESTDIR="$pkgdir" install
  install -D -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
