# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=0.0.5
pkgrel=1
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'kwin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        new-kwin.patch)
sha512sums=('b7762cad4b6b06e9fba16d80528dec05dafa075222b15ac1ad285a169256ca69dbc3690bbfee59c8cda4f91ba8f28c9880c3727494cefbeb53f137b0123d0328'
            '771ac8b0b80f15e46fa857ab5ee7c5f28ac79fc82e95999077ffbf8dfa63669a9e6c746f355438211145390ea3af108b31b7683e5193b82ccd9cc00d24abb3f2')

prepare() {
  cd dde-kwin-$pkgver
  patch -p1 -i ../new-kwin.patch
}

build() {
  cd dde-kwin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-kwin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
