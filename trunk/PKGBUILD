# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-media-sdk
pkgname=('intel-media-sdk' 'libmfx')
pkgver=19.4.0
pkgrel=3
pkgdesc='API to access hardware-accelerated video on Intel Gen graphics hardware platforms'
arch=('x86_64')
url='https://software.intel.com/en-us/media-sdk/'
license=('MIT')
makedepends=('libdrm' 'libva' 'wayland'
             'cmake' 'libpciaccess' 'libx11' 'libxcb' 'python'
             'opencl-headers' 'ocl-icd' 'intel-compute-runtime')
source=("https://github.com/Intel-Media-SDK/MediaSDK/archive/intel-mediasdk-${pkgver}.tar.gz"
        'libmfx.conf'
        'intel-media-sdk.conf'
        'intel-media-sdk.sh')
sha256sums=('289a40ffe73dc291461d97edf452bdc622f76e52666483fd1eb4f131d2921cd1'
            '82b64d0c5adde9ab73b8d2ac6cfb80e5ceacefef9ef6215fa550f4cd957f9fec'
            '12a37e6e12d93fac5829082773b9f010a3c6c763ddeee177618b8e1a0547fbbc'
            '315ea6f304cf2b7b6a8aaabb0b8f71fcd480677c7fb9c8cbfa51c7830bb159bc')

build() {
    cmake -B build -S "MediaSDK-intel-mediasdk-${pkgver}" \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DBUILD_ALL:BOOL='ON' \
        -DBUILD_TOOLS:BOOL='ON' \
        -DENABLE_ITT:BOOL='OFF' \
        -DENABLE_OPENCL:BOOL='ON' \
        -DENABLE_WAYLAND:BOOL='ON' \
        -DENABLE_X11_DRI3:BOOL='ON' \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package_intel-media-sdk() {
    depends=('gcc-libs' 'libdrm' 'libva' 'wayland' "libmfx=${pkgver}" 'intel-media-driver')
    optdepends=('ocl-icd: for rotate_opencl plugin'
                'intel-compute-runtime: for rotate_opencl plugin')
    install="${pkgname}.install"
    
    make -C build DESTDIR="$pkgdir" install
    
    # metrics_monitor
    install -D -m755 build/__bin/None/libcttmetrics.so -t "${pkgdir}/opt/intel/mediasdk/share/mfx/samples"
    install -D -m755 build/__bin/None/metrics_monitor  -t "${pkgdir}/opt/intel/mediasdk/share/mfx/samples"
    ln -s ../share/mfx/samples/libcttmetrics.so "${pkgdir}/opt/intel/mediasdk/lib/libcttmetrics.so"
    
    # ld.so and profile configuration files
    install -D -m644 intel-media-sdk.conf -t "${pkgdir}/etc/ld.so.conf.d"
    install -D -m755 intel-media-sdk.sh   -t "${pkgdir}/etc/profile.d"
    
    # remove core component libmfx :/
    [ -d 'libmfx' ] && rm -rf libmfx
    mkdir -p libmfx/lib/pkgconfig
    mv "${pkgdir}/opt/intel/mediasdk/include" libmfx
    mv "${pkgdir}/opt/intel/mediasdk/lib/libmfx.so"* libmfx/lib
    mv "${pkgdir}/opt/intel/mediasdk/lib/pkgconfig/"{,lib}mfx.pc libmfx/lib/pkgconfig
    
    # license
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_libmfx() {
    pkgdesc='Intel Media SDK dispatcher library'
    depends=('gcc-libs')
    
    # install core component libmfx into a separated package
    mkdir -p "${pkgdir}/opt/intel/mediasdk"
    mv libmfx/{include,lib} "${pkgdir}/opt/intel/mediasdk"
    
    # ld.so configuration file
    install -D -m644 libmfx.conf -t "${pkgdir}/etc/ld.so.conf.d"
    
    # license
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
