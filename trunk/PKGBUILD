# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=ogre
pkgver=1.7.1
pkgrel=3
pkgdesc="A scene-oriented, flexible 3D engine written in C++"
arch=('i686' 'x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('boost-libs' 'freeimage' 'freetype2' 'libxaw' 'libxrandr' 
         'nvidia-cg-toolkit' 'mesa' 'zziplib' 'ois')
makedepends=('boost' 'cmake' 'doxygen' 'dot2tex')
optdepends=('cppunit: unit testing'
            'intel-tbb: better threading support'
            'poco: portability')
install=ogre.install
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}_src_v${pkgver//./-}.tar.bz2")
md5sums=('85083083dc65d49572654ea8c8b5c552')

build() {
  cd ${srcdir}/${pkgname}_src_v${pkgver//./-}

  # get a clean build dir
  [[ -d build ]] && rm -rf build
	mkdir build
	cd build

  # generate CMake Makefile
  cmake .. \
 	 -DCMAKE_INSTALL_PREFIX=/usr \
	 -DOGRE_INSTALL_PLUGINS_HEADERS=TRUE \
	 -DOGRE_INSTALL_SAMPLES=TRUE \
	 -DOGRE_INSTALL_DOCS=TRUE \
   -DOGRE_INSTALL_MEDIA=TRUE \
	 -DOGRE_INSTALL_SAMPLES_SOURCE=TRUE \
   -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version

  # compile
  make || return 1

  # generate docs
  if [[ $(which dot) && $(which doxygen) ]]; then
    make doc 2>/dev/null || return 1
  fi
}

package() {
  cd ${srcdir}/${pkgname}_src_v${pkgver//./-}/build

  # install the bugger
  make DESTDIR=${pkgdir} install || return 1

  # fix up samples
  install -dm775 -o root -g users ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/share/OGRE/*.cfg ${pkgdir}/opt/OGRE/samples/
  mv ${pkgdir}/usr/bin/SampleBrowser ${pkgdir}/opt/OGRE/samples/

  # make sample launcher
  echo "#!/bin/bash" > ${pkgdir}/usr/bin/OgreSampleBrowser
  echo "cd /opt/OGRE/samples && ./SampleBrowser" >> ${pkgdir}/usr/bin/OgreSampleBrowser
  chmod +x ${pkgdir}/usr/bin/OgreSampleBrowser
  
  # install license
  install -Dm644 ../Docs/License.html ${pkgdir}/usr/share/licenses/${pkgname}/license.html
}
# vim:set ts=2 sw=2 et:
