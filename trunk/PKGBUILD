# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=wishbone-utils
pkgver=0.6.6
pkgrel=1
pkgdesc='Utilities for working with a Wishbone bridge'
arch=(x86_64)
url="https://github.com/litex-hub/wishbone-utils"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::https://github.com/litex-hub/wishbone-utils/archive/v$pkgver.tar.gz)
sha512sums=('ffb730e9b110497314e9427001e5d6951d23a98428b8261c777d6742c8d06353c4d9422e1b18e60ed50b2d5e21ab5b9a4ea0085818fe2bbc7759830638afec52')

build() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 wishbone-tool/target/release/wishbone-tool -t "${pkgdir}/usr/bin/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
