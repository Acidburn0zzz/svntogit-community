# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=openshadinglanguage
pkgver=1.2.1
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(i686 x86_64)
url="http://code.google.com/p/openshadinglanguage/"
license=('custom')
depends=('boost-libs' 'openimageio' 'openexr' 'llvm' 'intel-tbb' 'freetype2' 'libpng' 'libtiff')
makedepends=('boost' 'cmake' 'clang')
source=(https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz)
md5sums=('5635d0c3005d839b9590b49c20e8b821')

build() {
  cd OpenShadingLanguage-Release-$pkgver/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd OpenShadingLanguage-Release-$pkgver/src/build

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
