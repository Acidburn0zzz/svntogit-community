# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.6
pkgrel=5
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'aspell' 'guile2.0' 'lua' 'perl' 'python' 'ruby' 'tcl')
optdepends=('aspell: spellchecker support'
            'guile2.0: support for guile scripts'
            'lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for python scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "weechat-python38.patch")
sha512sums=('cf56692e12b2e034675d4c15a8822836b2fcf93d584948e338d263f5a7207edf4599cc72bfb940427146f5f75e7239207cf54f5ed07dd5b6119f68c445725a2d'
            'SKIP'
            'a773c345ce691fb48838b3ac5faf427f2596299a0ec3807eee536ac59e3a29502f7379b6b7a43d2864ca4ae55aa57869547fa1c7d70fc3a1c524403f148d7c90')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # https://github.com/weechat/weechat/pull/1421
  patch -p1 -i ../weechat-python38.patch
}

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
