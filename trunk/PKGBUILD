# Maintainer: David Runge <dvzrv@archlinux.org>

_name=i18n
pkgname=ruby-i18n
pkgver=1.9.1
pkgrel=1
pkgdesc="New wave internationalisation support for Ruby"
arch=(any)
url="https://github.com/ruby-i18n/i18n"
license=(MIT)
depends=(ruby ruby-concurrent)
makedepends=(ruby-rdoc)
checkdepends=(ruby-bundler ruby-activesupport ruby-json ruby-minitest ruby-mocha ruby-rake ruby-test_declarative)
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ruby-i18n/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('661f3f62a0933188b8b09f91d816605dbd50f6afc45c54e38ae70e76f03fb428dde33070e1dde48459d0b385521535ebfff1b18323abc2a1a104c5a6097970a4')
b2sums=('00fadafb96dca76f7ed180fe5af5e8b44db96262fd98868b2fad5f57a461c3abd61e17d3c8e143603108cf05a58f8a74ed8e2c8cb062d63ec4edc8081b5580c4')

prepare() {
  cd ${_name}-${pkgver}

  # we don't do version pinning
  sed -r 's|~>|>=|g' -i "${_name}.gemspec"

  # we can not rely on Gemfiles as they usually contain all sorts of development dependencies as well
  rm -v Gemfile
}

build() {
  cd ${_name}-${pkgver}
  gem build "${_name}.gemspec"
}

check(){
  cd ${_name}-${pkgver}
  rake test
}

package() {
  local _gemdir="$(gem env gemdir)"

  cd ${_name}-${pkgver}
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem

  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
