# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sanpi <sanpi+aur@homecomputing.fr>

pkgname=routino
pkgver=3.1.1
pkgrel=4
pkgdesc='Router for OpenStreetMap Data'
arch=('i686' 'x86_64')
url='http://www.routino.org/'
license=('AGPL3')
depends=('zlib' 'bzip2')
source=(http://www.routino.org/download/$pkgname-$pkgver.tgz)
sha256sums=('23b9fce43f0a85c9efe220c6c72026dd7b37f6a4a5153bb9dc4c4b0747c7f282')

prepare() {
  cd "$pkgname-$pkgver"

#paths fix
  sed -i 's|prefix=/usr/local|prefix=/usr|' Makefile.conf
  sed -i 's|/doc/routino|/share/doc/routino|' Makefile.conf

#RPATH fix
  sed -i 's|LDFLAGS_LDSO=-Wl,-R.||' Makefile.conf
}

build() {
  cd "$pkgname-$pkgver"

  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
