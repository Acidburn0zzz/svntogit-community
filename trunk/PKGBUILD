# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-crypto
pkgver=0.0.20191228
_commit=53104e6ec876ad4e22ad27cce588b01392043c1b
pkgrel=1
pkgdesc='Go supplementary cryptography libraries'
arch=('any')
url="https://github.com/golang/crypto"
license=('BSD')
depends=('go-pie' 'golang-golang-x-net' 'golang-golang-x-sys')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/crypto/archive/$_commit.tar.gz")
sha512sums=('9c84f46a2b0b8141eb42796418e0460a3c9acd256679420298985b389ae720b3dfd1ecb0c640970c9135e68e36c0cc2976280ba574e37afc85b69a9f60b4425a')

prepare() {
  # Removed pinned version
  rm crypto-$_commit/go.{mod,sum}
}

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$srcdir"/build/src/golang.org/x/crypto
  cd "$srcdir"/build/src/golang.org/x/crypto
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/crypto-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/crypto

  install -Dm644 crypto-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
