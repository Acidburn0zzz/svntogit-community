# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Erik Johnson <palehose at gmail dot com>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgbase=python-hglib
pkgname=('python2-hglib' 'python-hglib')
pkgver=2.6.1
pkgrel=3
arch=('any')
url="https://pypi.python.org/pypi/python-hglib"
license=('MIT')
makedepends=('python2' 'mercurial' 'python')
checkdepends=('python-nose' 'python2-nose')
source=(https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz{,.asc}
        hglib-fix-obsolete-markers.patch::https://www.mercurial-scm.org/repo/python-hglib/raw-rev/12e6aaef0f6e)
validpgpkeys=('2BCCE14F5C6725AA2EA8AEB7B9C9DC824AA5BDD5') # Augie Fackler <raf@durin42.com> v2.5
# v2.4 validpgpkeys=('FAD61395F642FC2B33C84B9A205781AC682A2D72') # Matthew Mackall <mpm@selenic.com>
sha256sums=('7c1fa0cb4d332dd6ec8409b04787ceba4623e97fb378656f7cab0b996c6ca3b2'
            'SKIP'
            'ea0d32a061746ebe621e070ddb4653bbb4f755503ed63489c100870e82b7f553')

prepare() {
  cd "${pkgbase}-${pkgver}"
  # pass tests with recent mercurial versions
  patch -p1 -i ../hglib-fix-obsolete-markers.patch

  cd ../
  cp -rf "${pkgbase}-${pkgver}" "${pkgbase}-${pkgver}-2"
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python test.py

  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 test.py
}

package_python2-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python2' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 setup.py install --root=${pkgdir} --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root=${pkgdir} --optimize=1
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
