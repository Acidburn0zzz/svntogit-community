# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perlbrew
pkgver=0.32
pkgrel=1
pkgdesc="Manage perl installations in your \$HOME"
arch=(any)
license=(custom:MIT)
options=(!emptydirs)
depends=('perl>=5.8.0' 'perl-devel-patchperl>=0.46' 'perl-file-path-tiny>=0.1'
         'perl-local-lib>=1.0')
makedepends=('perl-extutils-makemaker>=6.42' 'perl-file-temp')
checkdepends=('perl-test-exception' 'perl-test-output'
              'perl-test-simple>=0.98' 'perl-test-spec' 'perl-io-all'
              'perl-path-class')
provides=("perl-app-perlbrew=${pkgver}")
url=https://metacpan.org/release/App-perlbrew
source=("http://search.cpan.org/CPAN/authors/id/G/GU/GUGOD/App-perlbrew-${pkgver}.tar.gz")
md5sums=(da56334934e57f8a38624917b4009fa6)
sha512sums=(fcced480f9397fbec97b2828309cb5cb42b251b69f03f9c39a3d7fe7cf6017cb5f98cd3dc1631d5ece997efd95bc7233c8aabbb97f31c46aaff70c84c65018a2)
_distdir="${srcdir}/App-perlbrew-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
    mkdir -p "$pkgdir/usr/share/licenses/perlbrew"
    cp LICENSE "$pkgdir/usr/share/licenses/perlbrew/LICENSE"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
