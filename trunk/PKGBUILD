# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>
# Contributor: Carl George < arch at cgtx dot us >

_name=gitdb
pkgdesc="A pure-Python git object database"
pkgname=python-gitdb
pkgver=4.0.6
pkgrel=1
epoch=1
url="https://github.com/gitpython-developers/gitdb"
license=('BSD')
arch=('any')
depends=('git' 'python-smmap')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("https://pypi.org/packages/source/g/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('b9cf964390cf18894c02e8854ef5099ef239bf4a43ae8659bf3def22a0fc35814c7416fd8b8312d9d0a170e5e0ea84380edbcac47cc435dc9ec1d98cf7f6463f'
            'SKIP')
validpgpkeys=('27C50E7F590947D7273A741E85194C08421980C9') # Sebastian Thiel (In Rust I trust!) <byronimo@gmail.com>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# some tests need to be disabled, because of coupling with the project
# repository: https://github.com/gitpython-developers/gitdb/issues/51
check() {
  cd "${pkgname}-${pkgver}"
  local TEST_TMPDIR=$(mktemp -d)
  (
    cd "$TEST_TMPDIR"
    git init
    git config user.name "Test User"
    git config user.email "test@user.org"
    for commit in {1..50}; do
      touch "file${commit}"
      git add "file${commit}"
      git commit -m "file${commit}"
    done
  )
  export GITDB_TEST_GIT_REPO_BASE="${TEST_TMPDIR}/.git"
  nosetests -vd -e 'test_pack_writing'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
