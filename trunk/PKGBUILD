# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: GordonGR <ntheo1979@gmail.com>

pkgname=lib32-gtk3
pkgver=3.18.6
pkgrel=1
pkgdesc='GObject-based multi-platform GUI toolkit (v3)'
arch=('x86_64')
license=('LGPL')
url='http://www.gtk.org/'
depends=('gtk3' 'lib32-at-spi2-atk' 'lib32-colord' 'lib32-gdk-pixbuf2'
         'lib32-json-glib' 'lib32-libcups' 'lib32-libepoxy' 'lib32-librsvg'
         'lib32-libxcomposite' 'lib32-libxcursor' 'lib32-libxinerama'
         'lib32-libxkbcommon' 'lib32-libxrandr' 'lib32-pango' 'lib32-rest')
makedepends=('gcc-multilib' 'gobject-introspection')
install='gtk3.install'
source=("https://download.gnome.org/sources/gtk+/${pkgver:0:4}/gtk+-$pkgver.tar.xz"
        '0001-x11-Only-do-cursor-name-fallback-for-standard-names.patch'
        '0002-wayland-Only-do-cursor-name-fallback-for-standard-na.patch')
sha256sums=('78cabf0fd5a662f8723f62d5ac633072c76c557c1d700454c9c3deaa37e441ef'
            '0402c3b7801810beb0f24ad20e4fb2eb7519cf3984f39e093a6e2752a0f7d9de'
            '94722b06284e76f628fc8933617ac19c14f6775a5250fe3c382defab63d7b10e')

prepare() {
  cd gtk+-${pkgver}

  patch -Np1 -i ../0001-x11-Only-do-cursor-name-fallback-for-standard-names.patch
  patch -Np1 -i ../0002-wayland-Only-do-cursor-name-fallback-for-standard-na.patch
}

build() {
  cd gtk+-${pkgver}

  export CC='gcc -m32'
  export CXX='/bin/false'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --localstatedir='/var' \
    --sysconfdir='/etc' \
    --enable-{broadway,wayland,x11}-backend \
    --disable-libcanberra \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd gtk+-${pkgver}

  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}"/usr/bin/gtk-query-immodules-3.0{,-32}
  rm "${pkgdir}"/usr/bin/{broadwayd,gtk-{builder-tool,encode-symbolic-svg,launch,update-icon-cache},gtk3-{demo,demo-application,icon-browser,widget-factory}}
  rm -rf "${pkgdir}"/{etc,usr/{include,share}}
}

# vim: ts=2 sw=2 et:
