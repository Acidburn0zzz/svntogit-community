# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jake
pkgver=10.4.4
pkgrel=1
pkgdesc="JavaScript build tool, similar to Make or Rake"
arch=('any')
url="https://jakejs.com"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('5be0bdfed80fc2d51caf3b8cc94df3cd080cefea160897e652bc34f41556acd47c660815edec79774e104bc426e9129e5d5fb584ce3c38bbb635f8161bb3fe27')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
