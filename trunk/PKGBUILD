# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=2.6.0
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest-runner)
source=("https://pypi.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('a582558e38342f54f7c1eda1f9038728dd5904729aaec4e5719e56bb447bc6fcb93bf5ea3af134ce245eda059b9be81d15b268165cbc10799ba112fc157f939b'
            'SKIP')
b2sums=('089d7a57a8469968bed147a01907f35d1430349130e0f7152bbd38dd65053cf3d6424942eee8cc9006baaec5f13bffcbef81bd8723cad7ee03b1358a5c2d3d27'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
