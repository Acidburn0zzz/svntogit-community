# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=3.10
pkgrel=1
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking gwenhywfar boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gmock libdbi-drivers mariadb-libs postgresql-libs)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-${pkgver}.tar.bz2
        06033c19cdff0abf4849ecc96e3ce0b0690fcd13.diff
        e1b014035753beb111388c8468460b0c61d816bc.diff)
sha256sums=('b60772c276a63639964d1e24c795fbb9a7c0c6c45959168fe5cdfe08eb796f16'
            '9cf2660e056854be4d793d7b90c309c9fcc3c2ec0153d24c4235da813e45205c'
            'bfb67a1e07908d6fbfa7798464d56276025a6ef8fd09a9076c029a9393abed7c')

prepare() {
  cd $pkgname-$pkgver
  # Fix build
  patch -Np1 -i ../06033c19cdff0abf4849ecc96e3ce0b0690fcd13.diff
  patch -Np1 -i ../e1b014035753beb111388c8468460b0c61d816bc.diff
}
build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DHAVE_GWEN_GTK3=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 1 test failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
