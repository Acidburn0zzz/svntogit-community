# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=4
pkgrel=1
pkgdesc="Random number generator related utilities"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gkernel/"
license=('GPL')
backup=(etc/conf.d/rngd)
source=(http://downloads.sourceforge.net/sourceforge/gkernel/$pkgname-$pkgver.tar.gz
        rngd
        rngd.conf
        rngd.service)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm0644 "$srcdir/rngd.service" "$pkgdir/usr/lib/systemd/system/rngd.service"
	install -Dm0755 "$srcdir/rngd" "$pkgdir/etc/rc.d/rngd"
	install -Dm0644 "$srcdir/rngd.conf" "$pkgdir/etc/conf.d/rngd"
}

sha256sums=('b71bdfd4222c05e8316001556be90e1606f2a1bac3efde60153bd84e873cc195'
            '45084eff83306c74e707a72b44b5b20d8b94777377af414e1c43f738c7a720ed'
            'a4a19dbe5403b9dcb2bc6b3699dcaba7a47ff3e3ccf58aa3bdbfe14eb1d16003'
            'abf163089fc466e45955fd0da98a20a966b3b0b4c714c27b470d03fedfc04ca3')
