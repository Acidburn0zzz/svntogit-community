# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=2.2.2
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('21c1098e62056fd5d68c0bf94a151be4224055eabf7973fee85d3632c0b509b454c5f990bd80db3d62fb5ac64b0592a3d3db62445fe5950f3bf9be75aa488ec1')

prepare() {
  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python setup.py pytest --addopts '--ignore build'
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # use the snapshot version, because generating a new one requires Internet access and root permission
  ln -s .tld_set_snapshot "$pkgdir"/usr/lib/python3.8/site-packages/tldextract/.tld_set
}

# vim:set sw=2 et:
