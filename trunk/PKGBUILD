# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=communicator
pkgver=2.0.0
pkgrel=1
pkgdesc='Contacts and dialer application'
url='https://mauikit.org/'
arch=(x86_64)
license=(GPL3)
depends=(mauikit-filebrowsing mauikit-texteditor hicolor-icon-theme kpeople kcontacts)
makedepends=(extra-cmake-modules qt5-location)
groups=(maui)
source=(https://download.kde.org/stable/maui/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz
        https://invent.kde.org/maui/communicator/-/commit/e84f6a70.patch)
sha256sums=('46f8acea2a68745ab0393ff03059f90933d14b532e71934fdf589ca9d1386f4b'
            '9dc7a2d60d6714ee8fdbc05851412d43f3586f6594d40b3c23389f7f8a88e3ab')

prepare() {
  patch -d $pkgname-$pkgver -p1 < e84f6a70.patch # Fix install
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
