# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-randomly
pkgver=3.0.0
pkgrel=1
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-randomly'
depends=('python-pytest')
checkdepends=('python-faker' 'python-numpy' 'python-factory-boy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('feef2c4f525065f141e748eff483308ec1c2119d835b114915f5684c5a554829214cd90b6d6e72fa49e65296261618f9a62a5de3912ee3028e11e6a020ff71ec')

prepare() {
  cd pytest-randomly-$pkgver
  sed -i 's/pytest.py: error: argument --randomly-seed/pytest: error: argument --randomly-seed/' test_pytest_randomly.py
}

build() {
  cd pytest-randomly-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-randomly-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages:$PYTHONPATH" pytest
}

package() {
  cd pytest-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
