# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=rsibreak
pkgver=0.12.1
pkgrel=1
pkgdesc="Takes care of your health and regularly breaks your work to avoid repetitive strain injury (RSI)"
url="https://userbase.kde.org/RSIBreak"
arch=(i686 x86_64)
license=(GPL)
depends=(knotifyconfig kidletime hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools)
source=("http://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
install=$pkgname.install
md5sums=('717f7f129c1a742e3049ed0c0f0d15e5')

prepare() {
  mkdir -p build
}

build() {
  cd build 
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIB_INSTALL_DIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DBUILD_TESTING=OFF 
  make
}

package() {
  cd build
  make DESTDIR=$pkgdir install
}

