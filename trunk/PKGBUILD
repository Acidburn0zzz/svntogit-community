# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=2.11.0
pkgrel=4
pkgdesc="A tool for examining the internals of a Qt application and to some extent also manipulate it"
arch=(x86_64)
url="https://www.kdab.com/gammaray/"
license=(GPL)
depends=(syntax-highlighting qt5-tools qt5-svg qt5-3d)
makedepends=(cmake kcoreaddons qt5-script qt5-wayland qt5-webengine qt5-scxml qt5-location qt5-connectivity doxygen glslang)
optdepends=('qt5-wayland: Wayland compositor inspector plugin'
            'qt5-webengine: web inspector plugin'
            'qt5-scxml: state machine viewer plugin'
            'qt5-connectivity: bluetooth plugin'
            'qt5-script: script engine debugger plugin'
            'kcoreaddons: KJob tracker plugin')
source=(https://github.com/KDAB/GammaRay/releases/download/v$pkgver/gammaray-$pkgver.tar.gz{,.asc}
        gammaray-qt-5.14.patch::"https://github.com/KDAB/GammaRay/commit/3bde5464.patch"
        gammaray-qt-5.14b.patch::"https://github.com/KDAB/GammaRay/commit/7cd6950e.patch")
sha256sums=('ab0488d2178c532816d491ab361ac3d362590f0e63912f7198f34c1b582209ca'
            'SKIP'
            'fcec0545414bc77dd3fc6a16be3e4a798d230a43a89fefeb138d32374600d2c2'
            '05c01038cfd6319b3b1b67a5189c0cd346255d5690d84dff18224ff544b1c7d8')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
  mkdir -p build

# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt/plugins/gammaray|' -i $pkgname-$pkgver/CMakeLists.txt

  patch -p1 -d $pkgname-$pkgver -i ../gammaray-qt-5.14.patch # Fix build with Qt 5.14
  patch -p1 -d $pkgname-$pkgver -i ../gammaray-qt-5.14b.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/gammaray
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
