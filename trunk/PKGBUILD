# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: phrakture <aaronmgriffin--gmail--com>
# Contributor: Fazlul Shahriar
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=plan9port
pkgver=20130320
pkgrel=1
pkgdesc='Port of many programs from Plan 9 to Unix-like operating systems'
arch=('x86_64' 'i686')
url='http://swtch.com/plan9port/'
license=('custom')
depends=('xorg-server' 'libxt' 'libxext' 'sh' 'fuse')
optdepends=('python2: for the codereview script')
provides=('plan9')
install='plan9.install'
options=('!zipman')
source=("http://swtch.com/$pkgname/$pkgname-$pkgver.tgz"
        'plan9.sh')
sha256sums=('49a5e6c97c76f0846c45791bda10d6b3ba5c4453855e401980e8e04f92e830fa'
            '0247c4446497359d305aaec069b07180fbf79ce4fa5191464cd9ebb8c7f0228d')

package() {
  cd "$srcdir/plan9"

  # Package
  ./INSTALL -b
  install -Dm755 ../plan9.sh "$pkgdir/etc/profile.d/plan9.sh"
  mkdir -p "$pkgdir/opt" "$pkgdir/usr/share/doc/$pkgname"
  cp -r "$srcdir/plan9" "$pkgdir/opt/"
  cd "$pkgdir/opt/plan9"
  ./INSTALL -c -r "$pkgdir/opt/plan9"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Clean up
  rm -rf .hg/
  rm -f .hgignore .hgtags
  find . -name '.cvsignore' -print0 |xargs -0 rm -f
  rm -f config install.log install.sum install.txt configure Makefile INSTALL \
    LICENSE

  # Fix hardcoded paths
  for f in `grep -H -r "$pkgdir/opt/plan9" | cut -d: -f1`; do
    echo -n "\t$f"
    [ -e "$f" ] && sed -i "s:$pkgdir/opt/plan9:/opt/plan9:" "$f" || true
  done

  # Fix python scripts
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'

  # Package text files
  for i in CHANGES CONTRIBUTORS README TODO; do
    install -m644 $i "$pkgdir/usr/share/doc/$pkgname"
    rm -f $i
  done

  # Decompress the plan9 man pages
  for i in `find /opt/plan9/man -type f`; do
    if [ ${i##*.} = "gz" ]; then
      gunzip "$i"
    fi
  done
}

# vim:set ts=2 sw=2 et:
