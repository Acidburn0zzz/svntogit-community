# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
_pkgver=0.3.4-alpha.preview
pkgver=${_pkgver//-/.}
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arduino/arduino-cli/archive/$_pkgver.tar.gz")
sha512sums=('f8519b8b9419d083f8e28a3011ab634e40fe8ee2ee550dc0026a1f86fe76a68d51b1279f85cee365246d72052059bf8526ca1b00a48a61663d07c96c1b81e0a5')

prepare(){
  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf "$pkgname-$_pkgver" "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	.
}

package() {
  install -Dm 755 $pkgname-$_pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
}

