# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rapidjson
pkgver=0.8.0
pkgrel=1
pkgdesc="Python wrapper around rapidjson"
arch=('x86_64')
url="https://pypi.python.org/pypi/python-rapidjson"
license=('MIT')
depends=('python')
makedepends=('python-setuptools' 'rapidjson')
checkdepends=('python-pytest-runner' 'python-pytest-benchmark' 'python-pytz')
source=("https://pypi.io/packages/source/p/python-rapidjson/python-rapidjson-$pkgver.tar.gz"
        python-3.8.patch)
sha512sums=('7d843cfe46a269c88a8f4c05ad6b3dc86d60fa3ceb24ac39639d95bbc8ed2bdba18f0467dd222c39416e48054b2ad70ae2b8219eb441a5571b872f28ba4872d7'
            '39850c03e7b87d3c081e4af9ff7b0bb7d06791c818acb97828341564407737bcaa935f3e42c042d5d3cfc65e827965540416bd1735de1a652a0c4b5ee8f39930')

prepare() {
  #rm -r python-rapidjson-$pkgver/rapidjson
  #sed -i 's/if not os.path.isdir/if os.path.isdir/' python-rapidjson-$pkgver/setup.py
  # Shared rapidjson fails tests.

  patch -Np1 -d python-rapidjson-$pkgver <python-3.8.patch
  cp -a python-rapidjson-$pkgver{,-py2}
}

build() {
  cd python-rapidjson-$pkgver
  python setup.py build # --rj-include-dir=/usr/include
}

check() {
  cd python-rapidjson-$pkgver
  python setup.py pytest --addopts '--ignore benchmarks/'
}

package() {
  cd python-rapidjson-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
