# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Llewelyn Trahaearn <WoefulDerelict [at] GMail [dot] com>
# Contributor: falkTX <falktx [at] gmail [dot] com>

_name=Carla
pkgname=carla
pkgver=2.0.0
pkgrel=1
pkgdesc="Audio Plugin Host"
arch=('x86_64')
url="https://kxstudio.linuxaudio.org/Applications:Carla"
license=('GPL2')
groups=('pro-audio')
depends=('ffmpeg' 'file' 'fluidsynth' 'gtk2' 'gtk3' 'python-pyqt5'
'python-pyliblo' 'python-rdflib' 'qt5-svg')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/falkTX/${pkgname}/archive/v${pkgver}.tar.gz"
        carla-qt5.13.patch)
sha512sums=('7cb1a8a6376e2fbb58a75fc2f60693984b5a8d03e47fe695f59f8a602ccd776ec3a578d93858faa704623fd3bf0521cb2f5e87600f6c55ff2df67f5451c35d5a'
            '7b3fb89e3d8470d27a213fd130cccb57e02bd26f1ddd48f5525f39023fae538902c07eaf682086643a2e45593ca6964918c2f12472a287c70b14276175dcc0d5')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"

  cd $pkgname-$pkgver
  patch -p1 -i ../carla-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd "${pkgname}-${pkgver}"
  make features
  make DEFAULT_QT=5
}

package() {
  cd "${pkgname}-${pkgver}"
  make DEFAULT_QT=5 \
       DESTDIR="${pkgdir}/" \
       PREFIX=/usr \
       install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
