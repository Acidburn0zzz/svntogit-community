# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

_gemname=stomp
pkgname="ruby-${_gemname}"
pkgver=1.4.8
pkgrel=4
pkgdesc='Ruby client for the Stomp messaging protocol'
url='https://github.com/stompgem/stomp'
arch=('any')
license=('Apache')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rspec' 'ruby-bundler' 'ruby-rdoc' 'ruby-test-unit')
options=('!emptydirs')
source=("${_gemname}-v${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha512sums=('29fd8623bbda2038be6baad8a5e1edc9a866f01fa4f205d840ad346ed35364712af19a91a516636465d0446a9b08d8251053d3b5a3649521141f79b9b800cfc6')

prepare() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  :
}

build() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  mkdir -p pkg
  gem build -o "pkg/${_gemname}-${pkgver}.gem"
}

check() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  # testsuite is broken and needs older versions of rspec / rspec-mocks
  #rake test
}

package() {
  cd "${srcdir}/${_gemname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"

  gem install \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    "pkg/${_gemname}-${pkgver}.gem"

  rm -rf "${pkgdir}/${_gemdir}/cache"

  install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
