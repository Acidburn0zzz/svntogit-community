# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Gem
pkgname=pd-gem
pkgver=0.94
pkgrel=9
pkgdesc="Graphics Environment for Multimedia"
arch=(x86_64)
url="https://github.com/umlaeute/Gem"
license=(GPL2)
groups=(pd-externals)
# TODO: potentially add gmerlin, libmpeg3 and glewmx (if that's still a thing)
depends=(assimp ftgl glew libdv libiec61883 pd sdl2 ttf-dejavu zlib)
makedepends=(imagemagick libvncserver vlc)
optdepends=('vlc: video capture support'
            'libvncserver: video capture support'
            'imagemagick: image processing support')
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/umlaeute/${_name}/archive/v${pkgver}.tar.gz"
  "$pkgname-0.94-error.patch::https://salsa.debian.org/multimedia-team/pd/gem/-/raw/master/debian/patches/pd_error.patch"
)
sha512sums=('356e4f2a27131f927cd5d20ba9010088cc22930f313f61f54dd107db041b6cb2042ab5d530bae069e3ccdccd0a8fed1152631c6f187f4f4602f37d763356d316'
            '7a5cb47fdf33cdfb0a9879b1bfc56861c7c3f802b2b8c50226bedda101e46bb7c998054e9c80a84747712ed0222062208f1568076fe8d839902d3a792c8c6c08')
b2sums=('1ca45c5a71e094aef04778538e6d0c83f4b13f233e6038507d5510ca904476f19ed1e70c011796e66e3688f8a8b40b13f823678b6b1baba877ac0b67eb9c6698'
        'd3f07dc907f381480e1e0309883b658fe1a6647bd52b0146b3849c76cf0274c3878d1b5ba134956c31b9b15543ac7508a1985ae056571f649c18c7ed37ee3e4c')

prepare() {
  cd "${_name}-${pkgver}"
  # fix issues with error() calls: https://github.com/umlaeute/Gem/issues/287
  patch -Np1 -i ../"$pkgname-0.94-error.patch"
  autoreconf -fiv
}

build() {
  cd "${_name}-${pkgver}"
  ./configure --prefix=/usr \
              --with-default-font=/usr/share/fonts/TTF/DejaVuSans.ttf
  # prevent excessive overlinking due to libtool
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  make DESTDIR="$pkgdir/" install -C "${_name}-${pkgver}"
}
