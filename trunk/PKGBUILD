# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cmake-fedora
pkgver=2.7.1
pkgrel=2
pkgdesc='CMake helper modules for fedora developers'
arch=('any')
license=('custom:BSD')
url='https://pagure.io/cmake-fedora'
depends=('cmake')
source=("https://releases.pagure.org/cmake-fedora/cmake-fedora-$pkgver-Source.tar.gz")
sha512sums=('1f264a3527c4938916de91d2b8ce10c58fa222c992820d6ea5c5e850a190476900a4cdb18f7efc0680222d220efb5e56c8102a9f034385e15b5f88f9a25601cf')

build() {
  cd cmake-fedora-$pkgver-Source
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd cmake-fedora-$pkgver-Source
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/usr/share/cmake{,-3.7}

  install -d -m755 "$pkgdir"/usr/share/licenses/cmake-fedora
  ln -s ../../doc/cmake-fedora/COPYING "$pkgdir"/usr/share/licenses/cmake-fedora/COPYING
}
