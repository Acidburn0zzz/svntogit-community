# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=1.4.0
pkgrel=1
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/so-fancy/diff-so-fancy"
license=('MIT')
depends=('perl')
makedepends=('git')
checkdepends=('bash-bats-assert' 'bash-bats-support')
source=("https://github.com/so-fancy/diff-so-fancy/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-bats-testfix.patch::https://github.com/so-fancy/diff-so-fancy/commit/edcfd043a01b2c798375c76d17a7b8733f30e3d4.patch")
sha384sums=('fa08823eaa093b45957ab610541a19aeffc3c5fdcb3ca5491a38e62049f25cf5238d3c5e2b601df6b3f3f35b423266d8'
            '1bee3081672f17eafc548ebf433b3d3c0bd8792e6ffa17264663f09f177931a97f7e21e31c04d9f0ce742f4b7b61c2bf')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../$pkgname-bats-testfix.patch

  rm -r third_party # lib

  sed -e 's|test_helper/bats-support|/usr/lib/bats-support|' \
      -e 's|test_helper/bats-assert|/usr/lib/bats-assert|' \
      -e "s|load'$|load.bash'|" \
      -i test/*.bats
}

check() {
  cd $pkgname-$pkgver
  git init
  bats test/*.bats
}

package() {
  cd $pkgname-$pkgver

  sed -i 's|^use lib .*$|use lib "/usr/share/diff-so-fancy";|' diff-so-fancy

  install -Dm755 diff-so-fancy "$pkgdir"/usr/bin/diff-so-fancy
  install -Dm755 lib/DiffHighlight.pm "$pkgdir"/usr/share/diff-so-fancy/DiffHighlight.pm
  install -Dm644 README.md "$pkgdir"/usr/share/doc/diff-so-fancy/README.md
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
