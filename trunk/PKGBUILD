# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Cédric Bellegarde cedric bellegarde at adishatz dot org

pkgname=eolie
pkgver=0.9.63
pkgrel=2
pkgdesc="Simple web browser for GNOME"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Eolie"
license=('GPL3')
depends=('gtkspell3' 'python-cairo' 'python-dateutil' 'python-gobject' 'webkit2gtk')
makedepends=('git' 'gobject-introspection' 'meson')
optdepends=('python-beautifulsoup4: Import html bookmarks'
            'python-crypto: Firefox Sync support' 
            'python-fxa: Firefox Sync support'
            'python-pyopenssl: Show SSL certificates'
            'python-requests-hawk: Firefox Sync support')
_commit=656b1ec6e1ae4f3db22de853ca03d1dcadd463af  # tags/0.9.63
source=("git+https://gitlab.gnome.org/World/eolie.git#commit=$_commit"
        "git+https://gitlab.gnome.org/gnumdk/eolie-po.git")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
	cd $pkgname
	git describe --tags | sed 's/-/+/g'
}

prepare() {
	cd $pkgname
	git config submodule.subprojects/po.url "$srcdir/eolie-po"
	git submodule update --init

  sed -e "s|'python3'|'python3-embed'|" -i python-webextension/meson.build # Fix linking with python 3.8
}

build() {
	arch-meson $pkgname build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" meson install -C build
}
