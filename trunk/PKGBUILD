# $Id: PKGBUILD 101980 2013-12-03 09:48:38Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=libvirt-python
pkgname=(libvirt-python libvirt-python3)
pkgver=2.2.0
pkgrel=2
pkgdesc="libvirt python binding"
arch=('i686' 'x86_64')
url="http://libvirt.org/"
license=('LGPL')
makedepends=('python' 'python2' 'libvirt')
options=('emptydirs')
install="libvirt-python.install"
validpgpkeys=('C74415BA7C9C7F78F02E1DC34606B8A5DE95BC1F')
source=("http://libvirt.org/sources/python/libvirt-python-$pkgver.tar.gz"{,.asc}
        python3.6.patch)
sha256sums=('ba6b2eb3f018275e9720498d7b771959c2d51ebd54f6e956de5055f912afea76'
            'SKIP'
            'dee0682e6b1378d7a6ec0fe84a951ce4fa9766b656ef17c54d60455ceba97bf0')

prepare() {
  cd $pkgbase-$pkgver
  patch -p1 -i ../python3.6.patch
}

package_libvirt-python() {
  depends=('python2' 'libvirt')

  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py clean
  python2 setup.py install --root="$pkgdir"
}

package_libvirt-python3() {
  depends=('python' 'libvirt')

  cd "$srcdir/$pkgbase-$pkgver"
  python setup.py clean
  python setup.py install --root="$pkgdir"
}
