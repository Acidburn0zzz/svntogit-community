# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=intel-gmmlib
pkgver=19.2.2
pkgrel=1
pkgdesc="Intel Graphics Memory Management Library"
arch=('x86_64')
url="https://github.com/intel/gmmlib/"
license=('MIT')
depends=('gcc-libs')
makedepends=('cmake')
provides=('gmmlib')
conflicts=('gmmlib')
replaces=('gmmlib')
options=('!emptydirs')
source=("https://github.com/${pkgname/-//}/archive/${pkgname}-${pkgver}.tar.gz")
sha256sums=('5efa836ff9af7fe4cd48a70fafb9f352a015dae4fe557c6cdb7d06fe5d010e95')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname#*-}-${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE='None' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DRUN_TEST_SUITE=ON \
        -Wno-dev
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 ../${pkgname#*-}-${pkgname}-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
