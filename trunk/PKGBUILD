# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=tracer
pkgver=0.7.7
pkgrel=1
pkgdesc="OTracer finds outdated running applications in your system"
arch=('any')
url="http://tracer-package.com/"
license=('GPL')
depends=(python python-distro python-psutil python-six dbus-python pyalpm)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=("$pkgname-$pkgver.tar.gz::https://github.com/FrostyX/tracer/archive/refs/tags/$pkgname-$pkgver-1.tar.gz"
        python3.9-compat.patch)
sha512sums=('33e6521a4211e0cb9b04604f5797e5f12f235e2b41d975511e383a231363a112cbcdcbcf73c833b1313befaf17df75b4df29de7414eb9dafa02d058c17cefc88'
            'b6f7cf4bc25d5ecec0b8816c66af92d1248a6b8cd885519fda94c785560b2ab6db46acb6b79a6e238fd1993e2832feeb6e6087d40b7dda6c00c7d1d64f2ba159')

prepare() {
    cd "$pkgname-$pkgname-$pkgver-1"
    patch -Np1 -i ${srcdir}/python3.9-compat.patch
    cp LICENSE LICENCE.md
}

check() {
    cd "$pkgname-$pkgname-$pkgver-1"
    PYTHONPATH=. pytest tests
}

build() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py build
}

package() {
    cd "$pkgname-$pkgname-$pkgver-1"

    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
    install -Dm644 LICENCE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
