# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingpign@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.4.0
pkgrel=2
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=('x86_64')
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=('GPL2')
depends=('curl' 'gtk3' 'libnotify' 'libproxy' 'geoip')
makedepends=('intltool')
source=("${url}/releases/download/${pkgver}/transmission-remote-gtk-${pkgver}.tar.xz"{,.asc}
        transmission-fix-409.patch::"${url}/commit/cc8b8db3b7493324d6a436a31c7db78bb0f99ad3.patch")
sha256sums=('7e23608530bc4108ce4bc7f4209152c4e19c3dd7a2fbd1ed503249ecff655fc5'
            'SKIP'
            'ccc9b93a2bb3910f972db12b0223160535f035031bf79f0c641ed509e21c0bee')
validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis <tingping@tingping.se>

prepare() {
	cd ${pkgname}-${pkgver}
    patch -p1 -i ../transmission-fix-409.patch
}

build() {
	cd ${pkgname}-${pkgver}
	./configure --prefix=/usr --disable-desktop-database-update
	make
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="${pkgdir}" install
}
