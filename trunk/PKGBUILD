# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-themes
pkgver=0.0.0+git20190820
_commit=e9a87343d75a90af53d4badbe78d531a1715ec5f
pkgrel=1
pkgdesc="Official themes for the UKUI desktop"
arch=('any')
license=('GPL')
url="https://github.com/ukui/ukui-themes"
groups=('ukui')
depends=('dconf')
makedepends=('sassc' 'git')
source=("$pkgname-$_commit.tar.gz::https://github.com/ukui/ukui-themes/archive/$_commit.tar.gz"
        ukui-themes-remove-broken-icons.patch::https://github.com/ukui/ukui-themes/pull/4.patch)
sha512sums=('89755dff48b4b00493062f9de33522a8d7e0684ba334a2edc3010f7935f1aa1587f2bdaa5e9e7157963c5a91aa18cc9ae88dbf37a19a4fed0935bb3d4fe79677'
            '291810cdb53436c48d6d2dcc21f1954daf5cd80d1a76e890fdafe973f65e4c06cc2e9470ed1cc72433fe2fbfb1a37507589cdb9b2b491a4acd3877f203391546')

prepare(){
  cd $pkgname-$_commit
  git apply ../ukui-themes-remove-broken-icons.patch
  sed -i 's/sass/sassc/' */Makefile
}

build() {
  cd $pkgname-$_commit
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install

  install -Dm644 debian/25_ukui-themes.gschema.override -t "$pkgdir"/usr/share/glib-2.0/schemas
  install -dm755 "$pkgdir"/usr/share/icons
  cp -a ukui-icon-theme "$pkgdir"/usr/share/icons/
  cp -a ukui-theme "$pkgdir"/usr/share/themes/
}
