# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.8.4
pkgrel=3
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg optipng glibd)
makedepends=(ldc meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        "FixCompilationDueToBrokenGeneratedCode.patch::https://github.com/ximion/appstream-generator/commit/d5dad3b174eea7e9fbe0ed6e7b55a366b8eb203d.patch"
        "FixCompilation.patch")
sha256sums=('49bebcc959f02d72c31f26dad84e3acb3d1a12f64f18fe0a58db23e4d66f97b5'
            '0a272985dbffeabd1f8de765b3e33a01c82277d7f4da1efaf629497fcb9aea3b'
            '5d98fafea28931368ce1a42489fa11fe58f7d7e441634d01273f3b822a9cddca')

prepare() {
  # Fix compilation due to broken generated GdkPixbuf code
  # https://github.com/ximion/appstream-generator/pull/90
  patch -d $pkgname-$pkgver -p1 -i "$srcdir"/FixCompilationDueToBrokenGeneratedCode.patch

  # Some more fixes needed to get it to compile
  patch -d $pkgname-$pkgver -p1 -i "$srcdir"/FixCompilation.patch
}

build() {
  meson build $pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build
}
