# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.1.0.2
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5platform-plugins')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        deepin-qt5integration-qt5.15.patch)
sha512sums=('bd4212ec5c2dab711ddc2dc58b15e8c3b91a3e516ade512b9b35059de6fe987161aae864642dc85fcb30cf07e72f1aafb903138df8f7c22db3a1c4ee0aa28ba7'
            'e77cccce5de00881305d1c117d571ee33e18ca2d7a4b1fabb86d38786045079c7b92ae3ced4093688af23af415c978c9035498188fa63481253393eaef16bd05')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../deepin-qt5integration-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
