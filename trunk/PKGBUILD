# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
_pkgver=0.3.3-alpha.preview
pkgver=${_pkgver//-/.}
pkgrel=3
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arduino/arduino-cli/archive/$_pkgver.tar.gz")
sha512sums=('217ec44db600e9667c15b201064448fe8eda86e9708969c7df5931bfe52529e62ec9133dce7ef3a9fb304965d3a0e3177dd54997d515e853114a00ed1226fc21')

prepare(){
  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf "$pkgname-$_pkgver" "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	.
}

package() {
  install -Dm 755 $pkgname-$_pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
}

