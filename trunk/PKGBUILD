# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.7
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang)
makedepends=(cmake clang llvm zlib python2)
options=('!emptydirs')
source=("${url}/archive/igc-${pkgver}.tar.gz"
        'igc-fix-build-with-system-opencl-clang.patch')
noextract=("igc-${pkgver}.tar.gz")
sha256sums=('b95afb8f983f38a8aedfa9d77ecb619706f58baae31dd7da0c4e31962b9205c1'
            'e4221ca82774d8afda7b581d5b80cffa6c47d8326aaee068546d8e63f5bef63d')

prepare() {
    mkdir -p build ${pkgname}-${pkgver}
    bsdtar xf igc-${pkgver}.tar.gz -C ${pkgname}-${pkgver} --strip-components='1'
    
    # fix build with system opencl-clang
    # https://github.com/intel/intel-graphics-compiler/issues/95
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/igc-fix-build-with-system-opencl-clang.patch"
}

build() {
    cd build
    cmake ../"${pkgname}-${pkgver}" \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='8.0.0' \
        -Wno-dev
    make
    
    # License
    sed -n '2,20p' IGC/AdaptorOCL/igc.opencl.h > LICENSE # create file
    sed -i '1,22s/^.\{,3\}//' LICENSE # erase C comments
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
