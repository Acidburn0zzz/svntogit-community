# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=44.0.1
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'jack' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "din-44.0.1-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('6c81111f0b5adb8bf2680d8dea6922a1f3805309e5d1e88f7610f38861b90a7e98fda89a93611419840fc85496a49568e3e0ee52e7a7c196fdc3af4ff8e5af91'
            '012783612de0919988a83d1166de21eb09261e5a766d097c58f82c81df2c04686f1ffa7045f51f3719bbb08f5a4ffc5e917a073e9630b290ff122ba59bd4d9c1')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../din-44.0.1-devendor_rtaudio_rtmidi.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs jack rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('libjack.so' 'librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
