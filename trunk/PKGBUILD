# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidinet
pkgver=0.5.5
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="http://qmidinet.sourceforge.net/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('951dff98fe92b305f05427bc3e7b2b5dbb5ea8d10ff8a6936feb7a39ad5327135c0b1bc11329badf9784f1e3a970d7be92f574afa9750b1c9b10ff1ed2753fd0')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
