# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Thomas Weißschuh <thomas@t-8ch.de>

pkgname=smlnj
pkgver=110.88
pkgrel=1
pkgdesc='Standard ML compiler from New Jersey'
url='https://www.smlnj.org/'
license=(BSD)
arch=(x86_64)
provides=(sml)
install=smlnj.install
depends=(lib32-glibc)
makedepends=(gcc-multilib)
_url="http://smlnj.cs.uchicago.edu/dist/working/$pkgver/"
source=(
  "$pkgname-$pkgver-boot.x86-unix.tgz::${_url}boot.x86-unix.tgz"
  "$pkgname-$pkgver-config.tgz::${_url}config.tgz"
  "$pkgname-$pkgver-cm.tgz::${_url}cm.tgz"
  "$pkgname-$pkgver-compiler.tgz::${_url}compiler.tgz"
  "$pkgname-$pkgver-runtime.tgz::${_url}runtime.tgz"
  "$pkgname-$pkgver-system.tgz::${_url}system.tgz"
  "$pkgname-$pkgver-MLRISC.tgz::${_url}MLRISC.tgz"
  "$pkgname-$pkgver-smlnj-lib.tgz::${_url}smlnj-lib.tgz"
  "$pkgname-$pkgver-ckit.tgz::${_url}ckit.tgz"
  "$pkgname-$pkgver-nlffi.tgz::${_url}nlffi.tgz"
  "$pkgname-$pkgver-cml.tgz::${_url}cml.tgz"
  "$pkgname-$pkgver-ml-lpt.tgz::${_url}ml-lpt.tgz"
  "$pkgname-$pkgver-ml-lex.tgz::${_url}ml-lex.tgz"
  "$pkgname-$pkgver-ml-yacc.tgz::${_url}ml-yacc.tgz"
  "$pkgname-$pkgver-ml-burg.tgz::${_url}ml-burg.tgz"
  "$pkgname-$pkgver-trace-debug-profile.tgz::${_url}trace-debug-profile.tgz"
  urlgetter.sh
  profile.d-smlnj.sh
  smlnj.sh
)
sha256sums=('b26f17bcdf3a53d2542f0392ee3f95903a80c1eedae6a03b41545f3d4a01cf33'
            '738c87cfd594e8dddaeb4f0ea6f044fb167656762944c3a3856cc5c6f1bc5da2'
            'b609e73245edbe385d1eedd8d60d359b82a79b4e5fc0a1f51f69418d958d5cc8'
            '3f217e23b42f7f092f2ad166f04e3c45e0c73f9c25df78dc603f8cf876b5d472'
            'f21f1b8dee319888cec663c45557715209c453df091dc2b86137ed9836f43c4e'
            'cff3de2281e481eae82ea1c503cae01ef651e881c807e561682ba2a544dc7e6e'
            'eb095034dc44831e15b5b4ed75aca418e157e0efab533748abf172c04c9c2dec'
            '8e3b2c2b76524435cd1c38f0be07d66fcb4c5633cd975460932431cda3cab60f'
            '302a63615ad2725b1a913012c36aa47d4e719595a82df761a28dc39e26960a11'
            '35105ed5bdb479485eaec6d6108b1cf9348f5313ec2dd033604db1f0e451449b'
            '058932583542ea24d1e4140c4e8838377be3e93a708e69813960bd4714d02075'
            '928adab85adb2004be9b38f2c44e36a56672200c20acf2debb41068e646fea74'
            'eca7b34ed60711be67f7ca15f8b5c508b23ef1ae6beed27c0706a7e9d6994b21'
            '07bbb40abf396cd0bdd59a1cff71c4e813154aaabbc6cd7f89f0754f174ed0e7'
            'ccd6f0bdc200ae043774685a1ab3211d5fd7e7bf6cb198a1980b7c50ea9e1b0f'
            'c9fe90029f6aae35d2ee312642ac54eb070b6a6da4c603141a61bbd5435759ab'
            '4da78effe7d3644c28c731c8e4003a9cecec9f8f61d2fa4553981a729f2d200b'
            'dd20a81a5d2899f60183215ab6a412d522d2c6801d454c142225716899e089f3'
            '3012a08e113b0cf6feff06689c1ae2ad48524e200d30ac46676f3c9e53665218')

prepare() {
  mkdir -p install
}

build() {
  unset SMLNJ_HOME MAKEFLAGS
  export srcdir
  export pkgver
  export INSTALLDIR="$srcdir/install"
  URLGETTER="$srcdir/urlgetter.sh"
  config/install.sh
}

package() {
  install -d "$pkgdir/usr/"{lib/smlnj,share}
  install -Dm755 "$srcdir/profile.d-smlnj.sh" "$pkgdir/etc/profile.d/smlnj.sh"
  install -Dm644 compiler/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  cp -R "$srcdir/install/"{bin,lib} "$pkgdir/usr/lib/smlnj"
  cp -R "$srcdir/doc/man" "$pkgdir/usr/share/"
  find "$pkgdir/usr/share/man" -name '._*' -delete
  install -Dm755 smlnj.sh "$pkgdir/usr/bin/smlnj"
}

# getver: smlnj.org
# vim: ts=2 sw=2 et:
