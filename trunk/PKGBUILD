# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-quart
pkgver=0.11.3
pkgrel=1
pkgdesc='A Python ASGI web microframework with the same API as Flask'
url=https://gitlab.com/pgjones/quart
arch=(any)
license=(MIT)
depends=(
  hypercorn
  python-aiofiles
  python-blinker
  python-click
  python-itsdangerous
  python-jinja
  python-multidict
  python-sortedcontainers
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-asynctest
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-cov
  python-pytest-sugar
  python-tox
)
source=(git+https://gitlab.com/pgjones/quart.git#tag=b8ec24b9336d06e7cf506b39ce241c25af82e146)
sha256sums=(SKIP)

pkgver() {
  cd quart

  git describe --tags
}

build() {
  cd quart

  python setup.py build
}

check() {
  cd quart

  tox -e py38
}

package() {
  cd quart

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-quart/
}

# vim: ts=2 sw=2 et:
