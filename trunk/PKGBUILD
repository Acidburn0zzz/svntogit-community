# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: mathieu.clabaut <mathieu.clabaut@gmail.com>
# Contributor: Buce <dmbuce@gmail.com>
# Contributor: Danie Roux <accounts@danieroux.com>

pkgname=etckeeper
pkgver=1.18.10
pkgrel=2
pkgdesc="Collection of tools to let /etc be stored in a git, hg or bzr repository."
arch=(any)
url="https://etckeeper.branchable.com/"
license=(GPL2)
depends=(git diffutils inetutils sed)
makedepends=(python2 mercurial bzr)
optdepends=('mercurial: use mercurial for version control'
        	'bzr: use bazaar for version control')
backup=('etc/etckeeper/etckeeper.conf')
_tag=395272b2600139777fdcaa2ff9a73c094d54cdcd # git rev-parse ${pkgver}
source=("git+https://git.kitenet.net/git/etckeeper.git#tag=${_tag}?signed")
sha256sums=(SKIP)
validpgpkeys=(E85A5F63B31D24C1EBF0D81CC910D9222512E3C7) # Joey Hess <joeyh@joeyh.name>

build() {
	cd ${pkgname}

	# Fix python name
	sed -i '/^PYTHON=/c PYTHON=python2' Makefile
	sed -i '1s/python/python2/' zypper-etckeeper.py

	# We want pacman :D
	sed -i -e "/^LOWLEVEL_PACKAGE_MANAGER/c LOWLEVEL_PACKAGE_MANAGER=pacman" \
		-e "/^HIGHLEVEL_PACKAGE_MANAGER/c HIGHLEVEL_PACKAGE_MANAGER=pacman" etckeeper.conf
}

package() {
	cd ${pkgname}
	make DESTDIR="${pkgdir}" systemddir=/usr/lib/systemd/system install
}
