# Maintainer: David Runge <dvzrv@archlinux.org>

_name=doit
pkgname=python-doit
pkgver=0.33.0
pkgrel=1
pkgdesc="A task management and automation tool"
arch=('any')
url="https://pydoit.org/"
license=('MIT')
depends=('python-cloudpickle' 'python-pyinotify' 'python-setuptools')
checkdepends=('python-doit-py' 'python-pyflakes' 'python-pytest')
optdepends=('bash-completion: autocomplete for bash'
            'zsh-completions: autocomplete for ZSH')
# tests missing in pypi sdist:
# https://github.com/pydoit/doit/issues/371
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/pydoit/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('4a1ad9717c4bc55fddb2509dc818e163fcd8b7a88702c599bd7ffce752b6a7fc56c135bfd1f8e82621890878a61cc25c2c001f678d3b24be34a2ab1bc5fa6282')
b2sums=('d49dc0d6210d15923f4b856b4fc74fbdcbb37567f4c69c0e1c388e9a5a9f502f69af75090fe768563a391ced0a2bbe4b5fd77cf54b6bc3557d1d8a3f8f90efda')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {AUTHORS,CHANGES,CONTRIBUTING.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 "bash_completion_${_name}" \
    "${pkgdir}/usr/share/bash-completion/completions/${_name}"
  install -vDm 644 "zsh_completion_${_name}" \
    "${pkgdir}/usr/share/zsh/site-functions/_${_name}"
}
