# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=vico
pkgver=1.2
pkgrel=1
pkgdesc="Minimalistic Midi sequencer intended to be used in parallel with other software"
arch=('x86_64')
url="https://www.laborejo.org/vico.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('glib2' 'jack' 'nuitka' 'libsndfile' 'libutil-linux')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('74446d3f9513cb7a17dfc423af74b1e7395a33f44efe5b82fe24e0724fde4b02acbbe787ffe3de4e6b3c4050b6c8c0dae910a1f9cee1fbff29c453ba91e875fd'
            'SKIP')
b2sums=('39611e551ac2c4021cedef98fea4007e6e331abacebd9ba4fc5be89494e954368ec6ba07b63cb8e7331bb9f7c88c073956bd893bcdad4a145012865209f745cd'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so' 'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
