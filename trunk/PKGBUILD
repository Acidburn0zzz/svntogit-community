# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Omar Sandoval <osandov at cs dot washington dot edu>
# Contributor: Alexander Slesarev <slesarew at gmail dot com>

pkgbase=python-gmpy2
pkgname=(python2-gmpy2 python-gmpy2)
pkgver=2.1.0a1
pkgrel=3
pkgdesc="Provides C-coded Python modules for fast multiple-precision arithmetic."
arch=(x86_64)
url="https://github.com/aleaxit/gmpy"
license=(LGPL2.1)
makedepends=(python2 python libmpc)
source=("https://pypi.io/packages/source/g/gmpy2/gmpy2-$pkgver.tar.gz"
        gmpy2-no-mpc.patch::"https://github.com/aleaxit/gmpy/commit/916b4eae.patch"
        gmpy2-data-structures.patch::"https://github.com/aleaxit/gmpy/commit/82cfcac4.patch")
sha256sums=('d28fbb0b990233b2339be020418d14d1a132eddfd7a763e56ecef8c87790a881'
            '86a65da514a614f0e5c5b4b4f62ff86d4ee664197d7f2e23aa0cf65aac21d693'
            '489761d16d91a59ad2c8e23f968cf81b59dd83f8705e09c130139b45797a4dfe')

prepare() {
  cd gmpy2-$pkgver
    patch -p1 -i ../gmpy2-no-mpc.patch # neeed by sagemath 8.2
    patch -p1 -i ../gmpy2-data-structures.patch
  cd ..

  cp -r gmpy2-$pkgver{,-py2}  
}

build() {
  cd gmpy2-$pkgver
  python setup.py build

  cd ../gmpy2-$pkgver-py2
  python2 setup.py build
}

package_python2-gmpy2() {
  depends=(python2 libmpc)

  cd gmpy2-$pkgver-py2
  python2 setup.py install --root=$pkgdir --optimize=1 --skip-build

  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

package_python-gmpy2() {
  depends=(python libmpc)
  
  cd gmpy2-$pkgver 
  python setup.py install --root=$pkgdir --optimize=1 --skip-build
 
  install -d -m 755 $pkgdir/usr/share/doc/$pkgname
  install -m 644 -t $pkgdir/usr/share/doc/$pkgname docs/*
}

