# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-reader
pkgver=5.6.14
pkgrel=1
pkgdesc='A simple PDF reader, supporting bookmarks, highlights and annotations'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-reader"
license=('GPL3')
depends=('deepin-qt5integration' 'djvulibre' 'karchive' 'libspectre' 'poppler-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-reader/archive/$pkgver.tar.gz")
sha512sums=('eeed11fdac59edfbc61c26fe006c2049d8456fdbdfbc97879023a952a9a06936deed1fae12c51137bb6124d0e28f96d75913e4e0506d4046090219de49d2fd7f')

prepare() {
  cd deepin-reader-$pkgver/src
  sed -i '/include <QMetaType>/a #include <QRectF>' pdfControl/docview/commonstruct.h
  sed -i '/include <QPainter>/a #include <QPainterPath>' CustomControl/RoundColorWidget.cpp lpreviewControl/bookmark/bookmarkdelegate.cpp \
                                                         utils/utils.cpp lpreviewControl/thumbnail/ThumbnailDelegate.cpp lpreviewControl/note/notesdelegate.cpp \
                                                         lpreviewControl/search/searchresdelegate.cpp
  sed -i '/include <QRect>/a #include <QPainterPath>' document/model.h
  sed -i '/include <QMap>/a #include <QSet>' uiframe/DocSheet.h
  sed -i '/include <QBitmap>/a #include <QMouseEvent>' djvuControl/SheetBrowserDJVU.cpp
  sed -i '/include <QDesktopWidget>/a #include <QMouseEvent>' widgets/SlideWidget.cpp
}

build() {
  cd deepin-reader-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-reader-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
