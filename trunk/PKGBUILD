# $Id$
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=filezilla
pkgver=3.5.1
pkgrel=2
pkgdesc="Fast and reliable FTP, FTPS and SFTP client"
arch=('i686' 'x86_64')
url="http://filezilla-project.org/"
license=('GPL')
depends=('dbus-core' 'xdg-utils' 'wxgtk' 'libidn' 'hicolor-icon-theme' 'sqlite3')
install=filezilla.install
source=("http://downloads.sourceforge.net/project/filezilla/FileZilla_Client/${pkgver}/FileZilla_${pkgver}_src.tar.bz2"
gnutls-3.patch)
md5sums=('8afb7aee4ba72b6363dcfe469bc7fbef'
'0646cf7d6f90766961724102cf2cebd6')

build() {
  cd "${pkgname}-${pkgver}"
  patch -p1 -i "$srcdir/gnutls-3.patch"
   ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --disable-static \
    --with-tinyxml=builtin

  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
