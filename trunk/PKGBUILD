# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-calculator
pkgver=5.5.28
pkgrel=1
pkgdesc='An easy to use calculator for ordinary users'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-calculator"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-calculator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7c93f86cb8e4fa7c3f9a0a1f7f867a038b6b19eeb97d2d5ee4736f1e63dfdac73f853a915463cb32726141a660121df8003f24cf8b4d1b6b22b2a58346e4b12d')

prepare() {
  cd deepin-calculator-$pkgver
  sed -i '1i#include <QPainterPath>' src/modules/simplelistdelegate.cpp src/widgets/equalbutton.cpp
}

build() {
  cd deepin-calculator-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd deepin-calculator-$pkgver
  DESTDIR="$pkgdir" ninja install
}
