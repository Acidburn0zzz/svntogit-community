# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.180
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('f82b56a7a1480a10d4953e9d5df8c464a127aa98327c0c84ce3676a69a11ae605eac32bac0cc2166fedfc0ef2b4b879bd8c3f4d7d98979215f933d5104e746de')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

package() {
  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s ../lib/node_modules/$pkgname/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/usr/lib/node_modules/$pkgname
}

# vim:set ts=2 sw=2 et:
