# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qmidictl
pkgver=0.9.0
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=('x86_64')
url="https://qmidictl.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('cmake' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d593cbec08cbf7c773ee43e2b4493cf6c3b52f59a3e9ae8a09931b8df7433630e76477a3574262a2438212c222ab9f7d02d069126ccfa68bb14537907f6d949b')
b2sums=('47114419ec3940849fcd03c788b8565964e5d47716dd642036b6146db949f6fde156b110a92434a62a55923b08c6a99739e1cd452d362ed6a2c1b45d0ff62e39')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
