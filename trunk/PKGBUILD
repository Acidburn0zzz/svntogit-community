# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kylin-nm
pkgver=1.2.5
pkgrel=1
pkgdesc="Kylin network monitor used in ubuntu-kylin operation system"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/kylin-nm"
groups=('ukui')
depends=('gsettings-qt' 'kwindowsystem' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/kylin-nm/archive/v$pkgver.tar.gz")
sha512sums=('e06a6beffe6bd752f5b8bf871ab29f9cdc64c0ae7eee1866898e74a83641ed1a4066b20e612d717720d472950f93a5fb5c60e8776ff70f2619d1ed5678e283b1')

prepare() {
  cd kylin-nm-$pkgver
  sed -i '/dpkg-buildflags/d' kylin-nm.pro
}

build() {
  cd kylin-nm-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd kylin-nm-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
