# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=4.6.0
pkgrel=1
pkgdesc="The Cinnamon Session Handler"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('cinnamon-desktop' 'dbus-glib' 'libsm' 'libcanberra' 'xapps')
optdepends=('cinnamon-translations: i18n')
makedepends=('meson' 'samurai' 'xtrans')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('abfe4fc2305f10d53349f58c17a2798c6af7b87b0074ce25630a8744bfd09c43')
b2sums=('613a7c87fcdb7666be871b2ddfa703304d7d3328627da11767413cd46b5ededa25fa48e85d0672488434ac8e017012f5d5b518d1741608ec637f4e174ddef047')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          -Dgconf=false \
          -Ddocbook=false \
          ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
