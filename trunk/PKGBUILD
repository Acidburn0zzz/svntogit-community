# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=39.0.1
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl' 'sdl' 'tcl')
makedepends=('boost' 'glu')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz")
sha512sums=('25ab9f9129090ed4d66664554b647644a51d577ad692978e074e6712ad05fa2e2f1be9c80b7199fdcf48234abf5bacc3734cffb1fe0bd99c81c5acc601af3632')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  CXXFLAGS="${CXXFLAGS} -O3 -D__UNIX_ALSA__ -D__UNIX_JACK__" \
  CFLAGS="${CFLAGS} -O3" \
  ./configure --prefix='/usr'
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
