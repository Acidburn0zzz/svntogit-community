# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=liblxqt
pkgver=1.1.0
pkgrel=2
pkgdesc="Common base library for LXQt components"
arch=("x86_64")
url="https://github.com/lxqt/$pkgname"
license=("LGPL2.1")
depends=("kwindowsystem" "libxss" "polkit-qt5" "libQt5Xdg.so")
makedepends=("lxqt-build-tools")
source=(
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
  "liblxqt-308.patch::https://github.com/lxqt/liblxqt/commit/ead8ceefa2a142b59e346b9df39a0070de709c73.patch"
)
sha256sums=('364db8bad17c0aad12c04cda15a7c74c80dec96b1ef4b1175aec9282c39b2cba'
            'SKIP'
            '5325a7c2903c4fd68043473483b8b43c889af0cc96b21253bd4bfae667213f38')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i ../liblxqt-308.patch
}

build() {
  cmake -B build -S "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  make -C build
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
