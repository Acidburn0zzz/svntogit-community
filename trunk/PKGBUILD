# Maintainer: Ray Rashif <schivmeister@gmail.com>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=1.4
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=(i686 x86_64)
url="http://www.breakfastquay.com/rubberband/"
license=('GPL')
depends=('libsamplerate' 'fftw' 'vamp-plugin-sdk')
makedepends=('ladspa')
source=(http://www.breakfastquay.com/$pkgname/files/$pkgname-$pkgver.tar.bz2
	gcc44.patch
	makeinstall.patch)

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.4 compatibility
  patch -Np1 -i ../gcc44.patch || return 1
  # fix installation paths
  patch -Np1 -i ../makeinstall.patch || return 1

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir/" install
}
md5sums=('8b73459d6e0f74299b7abbf740491982'
         'd26e45c82709fd1d21ba3c77a4fc13db'
         '5f5ece55abe7357631ad0ec3e8c8848b')
