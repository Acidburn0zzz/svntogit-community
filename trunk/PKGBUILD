# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: sputnick <gilles DOT quenot AT gmail DOT com>

pkgname=xwax
pkgver=0.8
pkgrel=1
pkgdesc="Open-source vinyl emulation software for Linux."
arch=('i686' 'x86_64')
url='http://www.xwax.co.uk/'
license=('GPL')
depends=('alsa-lib' 'sdl_ttf' 'ttf-dejavu')
optdepends=('vorbis-tools: for OGG import'
            'faad2: for AAC import'
            'cdparanoia: for CD import'
            'mpg123: for MP3 import'
            'flac: for FLAC import'
            'ffmpeg: for video fallback import')
source=("http://www.xwax.co.uk/releases/${pkgname}-${pkgver}.tar.gz")
md5sums=('e015363e96412d0e6702596cfa6a3485')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # Patch "rig.c" to use Arch Linux's default rtprio value
  sed -i 's/^#define REALTIME_PRIORITY 80/#define REALTIME_PRIORITY 65/' rig.c

  # Patch installation path for "xwax-scan" and "xwax-import"
  sed -i "s#^EXECDIR = \$(PREFIX)/libexec#EXECDIR = \$(PREFIX)/share/${pkgname}#" Makefile

  ./configure --enable-alsa --prefix /usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make PREFIX="${pkgdir}/usr" install
}
