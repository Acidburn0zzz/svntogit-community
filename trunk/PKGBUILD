# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-nginx
pkgver=0.14.0
pkgrel=1
pkgdesc="Nginx plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=("certbot=$pkgver" "python2-acme=$pkgver" 'python2-pyopenssl' 'python2-pyparsing'
         'python2-setuptools' 'python2-mock' 'python2-zope-interface')
checkdepends=('python2-nose')
replaces=("letsencrypt-nginx")
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('583cbf40e1f859bcc6fa805ae08b923961eaf0c7025178f56a9d853dced32c8a6178119c68a4f48443227e8c78afbeaa49e2dfcd1c956d158617c309f51f5c7d'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python2 setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  nosetests2
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python2 setup.py install --root="$pkgdir"
}
