# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.36.1
pkgrel=2
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=('lib32-pcre' 'lib32-zlib' 'lib32-dbus-core' lib32-libffi $_pkgbasename)
makedepends=('gcc-multilib' python2)
options=('!libtool' '!docs')
source=(http://ftp.gnome.org/pub/GNOME/sources/glib/${pkgver%.*}/glib-${pkgver}.tar.xz
  wall.patch)
sha256sums=('7de37586794e92c024feebe5d306bf5f245fef4803c3666af1ae8dac6ee10b24'
            '35ea3634f2c04b953206a0ec2dd224e61fa7468c22f9415fea3f5c734a001020')
prepare() {
  cd "${srcdir}/glib-${pkgver}"

  # use Wall during the complete build otherwise skype segfaults on startup
  # FS#34630 https://bugs.archlinux.org/task/34630
  patch -p1 <"$srcdir/wall.patch"
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/glib-${pkgver}"
  PYTHON=/usr/bin/python2 ./configure --prefix=/usr --sysconfdir=/etc --libdir=/usr/lib32 \
      --enable-static --enable-shared --with-pcre=system --disable-fam
  make
}

package() {
  cd "${srcdir}/glib-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include}}

  cd "${pkgdir}"/usr/bin
  mv gio-querymodules gio-querymodules-32
  rm -f gdbus glib* gobject-query gsettings gtester*
  rm -rf "$pkgdir"/usr/lib32/gdbus-2.0
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules-32 -delete
}
