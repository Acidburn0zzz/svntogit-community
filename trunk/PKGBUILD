# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Rainy <rainylau(at)gmail(dot)com>
# Contributor: Lee.MaRS <leemars@gmail.com>
# Contributor: Hiroshi Yui <hiroshiyui@gmail.com>

pkgname=ibus-chewing
pkgver=1.3.9.2
pkgrel=3
pkgdesc='Chinese Chewing Engine for IBus Framework'
arch=('i686' 'x86_64')
license=('GPL')
url='http://ibus.googlecode.com'
depends=('ibus' 'libchewing' 'libxtst' 'gconf')
makedepends=('swig' 'cmake' 'gob2')
install="${pkgname}.install"
source=("http://ibus.googlecode.com/files/${pkgname}-${pkgver}-Source.tar.gz")
md5sums=('8d177d67647944f5d1f9cca0654eaccb')

build() {
  cd ${pkgname}-${pkgver}-Source

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONF_INSTALL_DIR=/usr/share \
    .

  make
}

package() {
  cd ${pkgname}-${pkgver}-Source

  make DESTDIR=${pkgdir} install

  # check up on http://code.google.com/p/ibus/issues/detail?id=1172 to fix nonstandard path
  install -d ${pkgdir}/usr/lib/ibus
  mv ${pkgdir}/usr/libexec/ibus-engine-chewing ${pkgdir}/usr/lib/ibus/ibus-engine-chewing
  sed -i 's#/usr/libexec/ibus-engine-chewing#/usr/lib/ibus/ibus-engine-chewing#' ${pkgdir}/usr/share/ibus/component/chewing.xml
}
