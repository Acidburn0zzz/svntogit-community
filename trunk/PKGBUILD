# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.8
pkgrel=2
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'liblo' 'libmad' 'lilv'
'qt5-x11extras' 'rubberband' 'suil')
makedepends=('qt5-tools' 'ladspa' 'dssi')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('93e1e6e4b587731e24f39b8d51a3a2afd91cedd892713bd4b6b221a56f0a94605eddb7d14c549b509b6241a81afdcd576353fc6c1aea4377cf3d53779cb706da')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
