# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Richard Murri <admin@richardmurri.com>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=ecryptfs-utils
pkgver=100
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc='Enterprise-class stacked cryptographic filesystem for Linux'
url="https://launchpad.net/ecryptfs"
license=('GPL')
makedepends=('swig' 'intltool' 'gettext' 'python2')
depends=('keyutils' 'nss' 'openssl')
optdepends=('python2: for python module')
source=("http://launchpad.net/ecryptfs/trunk/${pkgver}/+download/${pkgname}_${pkgver}.orig.tar.gz")
options=(!libtool)
md5sums=('efaa5eabbd368268874536036397f73b')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --with-pamdir=/usr/lib/security PYTHON=python2
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  chmod +s "$pkgdir"/sbin/mount.ecryptfs_private
}
