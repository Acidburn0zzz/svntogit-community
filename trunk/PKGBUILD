# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.27.2
pkgrel=1
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('sqlite')
makedepends=('python' 'python2')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('e764cdfb4519da33fd09f00602379554ea6df1f74a75895dc6c7fb04194511b0e9f0fcb8648f07204b1fb3fe439d942dfdba11af7af2364569ddc8f7bd789be0')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
  python2 setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH-"2*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
  PYTHONPATH="${pythonpaths[1]}" python2 setup.py test
}

package_python-apsw() {
depends+=('python')
pkgdesc="Python wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apsw() {
depends+=('python2')
pkgdesc="Python2 wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
