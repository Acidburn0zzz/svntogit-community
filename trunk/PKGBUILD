# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-cantonese
pkgver=0.0.0.20200714
_commit=9456c0872348086ebb2b3e6343afc1b345665a54
pkgrel=1
pkgdesc="Cantonese input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-cantonese"
license=('CCPL:cc-by')
# dependency for reverse lookup
depends=('rime-cangjie' 'rime-emoji' 'rime-luna-pinyin' 'rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-cantonese/archive/$_commit/rime-cantonese-$_commit.tar.gz")
sha512sums=('4ea94e097d7bac7b204791086d3a8d3cfee9a813b45fe5c53c667029205e3f55512cf1cfbbf29caf8971d2b9a8f0b464b61321f7a409093cfe5d54d972ad0dac')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
  install -Dm644 opencc/* -t "$pkgdir"/usr/share/rime-data/opencc/
}
