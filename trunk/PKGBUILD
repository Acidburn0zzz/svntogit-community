# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=ansible
pkgver=4.7.0
pkgrel=1
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-core')
provides=('python-ansible_collections')
optdepends=('python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'python-proxmoxer: proxmos modules'
            'python-openstacksdk: OpenStack module'
            'python-pynetbox: NetBox module'
            'python-ldap: LDAP modules'
            'python-keyring: Keyring module'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('e3f8ea265ba9c94648da8226f261a04cbf9d41c380b91d9f4a6dc3084b4c243948418c1fa747d80e67131ecd929787e1c127a0c1509fe09ee338d035b71e1c76')

build() {
  cd ansible-${pkgver}
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}
