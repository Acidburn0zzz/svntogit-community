# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=sdrangel
pkgver=4.4.5
pkgrel=1
pkgdesc='Qt5/OpenGL SDR and signal analyzer frontend'
arch=('x86_64')
url='https://github.com/f4exb/sdrangel'
license=('GPL3')
depends=('pkg-config' 'log4cpp' 'opencv' 'fftw'  'ffmpeg'
         'cm256cc' 'dsdcc' 'pulseaudio' 'lz4' 'nanomsg'
         'qt5-base' 'qt5-tools' 'qt5-multimedia') # QT5
makedepends=('cmake' 'ffmpeg' 'airspy' 'bladerf' 'hackrf' 'limesuite' 'rtl-sdr' 'boost')
optdepends=('ffmpeg: DATV demodulator'
            'rtl-sdr: RTLSDR support'
            'hackrf: HackRF support'
            'limesuite: LimeSDR support'
            'bladerf: BladeRF support'
            'airspy: AirSPY support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz" sdrangel-opencv4.patch)
sha512sums=('b9d092466012887c5715e71bbeb4648084ac5701babb0b0dec9c3b68db589bbd2884b31ab02c79425b603b0aea1cdd4cfcf17097829cfcee689cf09209874e23'
            'bc9519a0f08cc6372b3290ed475e3a70b71d4a29f2cedefd349f9ba220a9d446c05c02ca648f068ee298612b85faeda96d45d186120555382b2df99596576e7e')

prepare() {
  sed -i 's|sdrangel_icon.xpm|sdrangel.xpm|' $pkgname-$pkgver/desktop/sdrangel.desktop
  cd $pkgname-$pkgver
  patch -p1 -i ../sdrangel-opencv4.patch # Fix build with openCV 4
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDSDCC_INCLUDE_DIR=/usr/include/dsdcc \
    -DCM256CC_INCLUDE_DIR=/usr/include/cm256cc

  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install

  # Install .desktop file
  install -Dm 644 ../desktop/sdrangel_icon.xpm "$pkgdir"/usr/share/pixmaps/sdrangel.xpm
  install -Dm 644 ../desktop/sdrangel.desktop "$pkgdir"/usr/share/applications/adrangel.desktop
}

