# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=4.7.2
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='http://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('e8734a5a02eeac498842df628160c6d00170e66be53f66d453016d3ec62bb3b6a89917b8ff5f0e42615957c01b5e4aeba8ae4d9317019b6e1051cf6edd52b662')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done
}
