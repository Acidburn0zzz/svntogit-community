# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-hyperframe
pkgname=('python-hyperframe' 'python2-hyperframe')
pkgver=5.0.0
pkgrel=1
pkgdesc="HTTP/2 framing layer for Python"
arch=('any')
license=('MIT')
url="http://hyper.rtfd.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-hyper/hyperframe/archive/v$pkgver.tar.gz")
sha512sums=('a728229ceb73766ffb23dc9c38df11e3615cb41916d6937689c7c1178972529d3833ef38f3eaa8984890e22be5c7eeb4b1bee1e6c014a757763dba9a73d7765e')

prepare() {
  cp -a hyperframe-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py build

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/hyperframe-$pkgver
  python setup.py pytest

  cd "$srcdir"/hyperframe-$pkgver-py2
  python2 setup.py pytest
}

package_python-hyperframe() {
  depends=('python')

  cd hyperframe-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-hyperframe() {
  depends=('python2')

  cd hyperframe-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
