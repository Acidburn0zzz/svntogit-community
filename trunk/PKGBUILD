# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.13
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'gcc-libs' 'hicolor-icon-theme' 'libmad' 'libogg' 'qt5-base'
'qt5-x11extras' 'zlib')
makedepends=('alsa-lib' 'aubio' 'dssi' 'jack' 'ladspa' 'liblo' 'libsamplerate'
'libsndfile' 'libvorbis' 'lilv' 'lv2' 'qt5-tools' 'rubberband' 'suil')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('9344a992d315e024aaf4d1e7277665e26085667077835cad0f75682bf2a474877808dbe244680e7fe3a2dd81184c8962d22a58d84ac24402afada2dabcd2b8d4')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  depends+=('libasound.so' 'libaubio.so' 'libjack.so' 'liblilv-0.so' 'liblo.so'
  'librubberband.so' 'libsamplerate.so' 'libsndfile.so' 'libsuil-0.so'
  'libvorbisenc.so' 'libvorbisfile.so' 'libvorbis.so' )
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
