# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=0.14
pkgrel=1
pkgdesc="an application that provides lots of terminals in a single window"
arch=('i686' 'x86_64')
url="http://www.tenshu.net/terminator/"
license=('GPL2')
depends=('vte' 'pygtk' 'xdg-utils')
makedepends=('desktop-file-utils' 'gettext' 'intltool')
optdepends=('gnome-python: gnome-terminal profile support')
install=terminator.install
source=(https://launchpad.net/terminator/trunk/${pkgver}/+download/${pkgname}_${pkgver}.tar.gz)
md5sums=('27872eb7100c2388d05e8c7aaecc97a1')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	python setup.py build || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	python setup.py install --skip-build --root=${pkgdir}

	rm -f ${pkgdir}/usr/share/applications/${pkgname}.desktop
	rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
	install -Dm 644 ${srcdir}/${pkgname}-${pkgver}/data/${pkgname}.desktop.in ${pkgdir}/usr/share/applications/${pkgname}.desktop
	sed -i 's|_||' ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
