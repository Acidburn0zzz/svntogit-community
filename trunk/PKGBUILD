# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libmusicxml
pkgver=3.17
pkgrel=1
pkgdesc="Portable C++ library around the MusicXML format"
arch=('x86_64')
url="https://github.com/grame-cncm/libmusicxml"
license=('MPL2')
depends=('gcc-libs' 'glibc')
makedepends=('cmake')
provides=('libmusicxml2.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grame-cncm/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('fe8dfa2efd95d4f27b33a976a0e8a8200b9da03b2c945da18850f083de50e5a3ba45a1e0827e8dfab1160dd5958553eca6d66088856e5396181dcd7756b8ef47')

build() {
  cd "$pkgname-$pkgver"
  # CMakeLists.txt is in build and generates Makefile in root
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        build
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
