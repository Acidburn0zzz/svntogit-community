# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
epoch=6
pkgver=10.1.15
pkgrel=1
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools"
arch=('i686' 'x86_64')
url="https://github.com/vmware/open-vm-tools"
license=('LGPL')
depends=('libdnet' 'icu' 'procps-ng' 'uriparser' 'libsigc++' 'libxss'
	 'iproute2' 'fuse2' 'lsb-release' 'libmspack')
makedepends=('chrpath' 'doxygen' 'gtkmm' 'libxtst' 'python')
optdepends=('gtkmm: DnD/CP plugin'
	    'libxtst: DnD/CP, resolution set plugins'
	    'netctl: suspend-resume network state'
	    'networkmanager: suspend-resume network state')
backup=('etc/xdg/autostart/vmware-user.desktop')
options=('docs')
source=($pkgname-$pkgver.tar.gz::https://github.com/vmware/open-vm-tools/archive/stable-${pkgver/_/-}.tar.gz
	vmtoolsd.service
	vmware-vmblock-fuse.service)
sha256sums=('2257122f5fee37bf1da1a764708a098b2e8d2847b5ba23dbffe97fd9c4b4214f'
            '5a9403f78356873258521644068f2c9639b994d5954e5ad953934136822b2689'
            '99e3cc1da20a751049144cc02dec77174a55109b5b5960e1facd78709da7724f')

build() {
  cd "$srcdir/$pkgname-"*"/open-vm-tools"
  autoreconf -vi
  sh ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --with-udev-rules-dir=/usr/lib/udev/rules.d \
    --without-xmlsecurity \
    --without-kernel-modules
  make
}

package() {
  cd "$srcdir/$pkgname-"*"/open-vm-tools"

  make install DESTDIR="$pkgdir"

  # install vmware-xdg-detect-de
  install -D -m 755 scripts/common/vmware-xdg-detect-de "$pkgdir"/usr/bin/vmware-xdg-detect-de
  chmod 07755 "$pkgdir"/usr/bin/vmware-user-suid-wrapper

  # We don't want a symlink in /sbin
  rm "$pkgdir"/sbin/mount.vmhgfs
  rmdir "$pkgdir"/sbin

  # install systemd files
  install -Dm644 "$srcdir"/vmtoolsd.service "$pkgdir"/usr/lib/systemd/system/vmtoolsd.service
  install -Dm644 "$srcdir"/vmware-vmblock-fuse.service "$pkgdir"/usr/lib/systemd/system/vmware-vmblock-fuse.service
}
