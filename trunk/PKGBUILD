# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.1.1
pkgrel=2
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'deepin-tool-kit' 'qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('git' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/dde-dock.git#tag=$pkgver"
        fix-null-pixmap.patch)
sha256sums=('SKIP'
            'a6340bddad48f6cf27387c130f0546b042873654f3ea05615d6896b46cda54de')

prepare() {
  cd dde-dock
  patch -p1 -i ../fix-null-pixmap.patch
}

build() {
  cd dde-dock
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock
  make INSTALL_ROOT="${pkgdir}" install
}
