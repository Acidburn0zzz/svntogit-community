# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=php-memcached
pkgver=1.0.2
pkgrel=6
pkgdesc="PHP extension for interfacing with memcached via libmemcached library"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/memcached"
license=('PHP')
depends=('php' 'libmemcached')
backup=('etc/php/conf.d/memcached.ini')
source=(http://pecl.php.net/get/memcached-$pkgver.tgz)
sha256sums=('16de77317e43b0a5bfd802c0bd2ef575adbad2093e8632d58738974e6cb14129')

build() {
  cd "$srcdir/memcached-$pkgver"

  phpize
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/memcached-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
  echo ';extension=memcached.so' >memcached.ini
  install -Dm644 memcached.ini "$pkgdir/etc/php/conf.d/memcached.ini"
}

# vim:set ts=2 sw=2 et:
