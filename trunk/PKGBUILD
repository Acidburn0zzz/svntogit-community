# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Antonio Rojas <nqn1976 @ gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>

pkgname=kbibtex
pkgver=0.7.git20180320
pkgrel=3
_commit=547a73bc1e6966a9e42fdabc7f66c4355ee189d9
pkgdesc="A BibTeX editor for KDE"
arch=(x86_64)
url='https://userbase.kde.org/KBibTeX'
license=(GPL)
depends=(poppler-qt5 qt5-webengine qoauth kparts)
optdepends=('okular: Document preview')
makedepends=(extra-cmake-modules git kdoctools qca-qt5)
source=(git://git.kde.org/$pkgname#commit=$_commit kbibtex-icu61.patch)
#source=("https://download.kde.org/stable/KBibTeX/$pkgname-$pkgver.tar.xz"{,.asc})
sha256sums=('SKIP'
            '75b6e114efb1cea49254fd448dfc7dab6ac8ea549d9f577ca43e73d853c020f5')
validpgpkeys=('1808CB466328F4380685A3B1A264FD738D861F41') # Thomas Fischer <fischer@unix-ag.uni-kl.de>

prepare() {
  mkdir -p build

  cd $pkgname
  patch -p1 -i ../kbibtex-icu61.patch # Fix build with ICU 61
}

build() {
  cd build

  cmake ../$pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
