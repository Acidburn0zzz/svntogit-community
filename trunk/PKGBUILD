# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2020-11-16
pkgver=20201116
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
b2sums=('0d9792c554c344599707b00ac2cc776ed28508ad4d46a7d8a48f79336bc4956468208399d5b451e2b97fcb3e252f1f290909f03695c59a6ca1b22ca53db19626')

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
