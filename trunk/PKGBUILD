# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=python-boto3
pkgver=1.16.13
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
makedepends=('python-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('ed7f58fe7529e6ec34f1a3d8d173fce532fe4ee7185657b1dc41186eb42a506d55f2803f69ea0b6ae216a6590f33ad15fff0c31975a6002566b54d7b187d5edc')

build() {
  cd boto3-$pkgver
  python setup.py build
}

check() {
  cd boto3-$pkgver
  # scripts/ci/run-tests
}

package() {
  cd boto3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
