# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSlizr
pkgname=bslizr
pkgver=1.2.12
pkgrel=1
pkgdesc="LV2 audio effect plugin for sequenced slicing of stereo audio input signals"
arch=('x86_64')
url="https://github.com/sjaehn/bslizr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('00ca939a200b1c94a09b6a4d7a2d66f06de9539fd02a811267b38ef9ee7c72ecc559342820548d91abfa630a9237ad159e31487cd15dc64ee94cd4f2f1a86ce7')
b2sums=('332b4ce4483fa243a5046b4d5fc25d69c659ccc813376ada9dcb6722c1ee5804575af80cb5ea0c85c39b97db4d566cde85893d0694af45493ec1b9db8049dda1')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${_name}.lv2/" "https://www.jahnichen.de/plugins/lv2/BSlizr"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
