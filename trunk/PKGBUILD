# Maintainer: David Runge <dave@sleepmap.de>
pkgname=dragonfly-reverb
_name=DragonflyReverb
_reverbs=('DragonflyHallReverb' 'DragonflyRoomReverb')
pkgver=2.0.0
pkgrel=2
pkgdesc="A concert hall reverb and a room reverb"
arch=('x86_64')
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=('GPL3')
groups=('pro-audio' 'lv2-plugins' 'vst-plugins')
depends=('freeverb3' 'jack' 'libglvnd')
makedepends=('gendesk' 'liblo' 'lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/michaelwillis/${pkgname}/releases/download/${pkgver}/${_name}-Source-v${pkgver}.tar.gz"
        "${pkgname}-2.0.0-devendor_freeverb3.patch::https://github.com/michaelwillis/dragonfly-reverb/pull/55/commits/a504c09103e27b6e5a084cddbb90d4bc4887a331.patch")
sha512sums=('2d6e7c4cdf852e956888437776eb22eea43eb8b6c7c6be3faef1b51b57c80d62c3239e206240acce0ebb5f6212382b1aea5d90b149453bbed4356bc3da2054de'
            '8838a149900319cb2c34371f044b071db7f985dc0d8a39ac587bb22ae7e04d37b7579d9f92fb51ca808b22d0405bcceee8320ecd536c1e14dfdf45e84b024da9')

prepare() {
  mv -v "DragonflyReverb-Source-v${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"

  # devendor freeverb3: https://github.com/michaelwillis/dragonfly-reverb/pull/55
  patch -Np1 -i "../${pkgname}-2.0.0-devendor_freeverb3.patch"

  gendesk -n \
          --pkgname ${pkgname}-hall \
          --pkgdesc "A free stereo-to-stereo concert hall reverb effect" \
          --name DragonflyHallReverb \
          --exec DragonflyHallReverb \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-room \
          --pkgdesc  "A free stereo-to-stereo room reverb effect" \
          --name DragonflyRoomReverb \
          --exec DragonflyRoomReverb \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  export SYSTEM_LIBSAMPLERATE="true"
  export SYSTEM_FREEVERB3="true"
  make
}

package() {
  cd "$pkgname-$pkgver"
  for _reverb in "${_reverbs[@]}"; do
    install -vDm 755 "bin/${_reverb}" -t "${pkgdir}/usr/bin"
    install -vDm 755 "bin/${_reverb}-vst.so" -t "${pkgdir}/usr/lib/vst"
    install -vDm 755 "bin/${_reverb}.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${_reverb}.lv2/"
    install -vDm 644 "bin/${_reverb}.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${_reverb}.lv2/"
  done
  install -vDm 644 *".desktop" -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
