# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>

pkgname=openbox
pkgver=3.5.0
pkgrel=7
pkgdesc='Highly configurable and lightweight X11 window manager'
arch=('i686' 'x86_64')
url="http://openbox.org"
license=('GPL')
depends=('startup-notification' 'libxml2' 'libxinerama' 'libxrandr'
         'libxcursor' 'pango' 'imlib2')
optdepends=('pyxdg: for the xdg-autostart script')
groups=('lxde')
install=${pkgname}.install
options=('!libtool')
backup=('etc/xdg/openbox/menu.xml' 'etc/xdg/openbox/rc.xml')
source=(http://www.icculus.org/openbox/releases/${pkgname}-${pkgver}.tar.gz
        which-2.20.patch
        movesize.patch
        title-matching.patch)
md5sums=('00441b53cf14c03566c8e82643544ff9'
         '7ca3b5244bb092d46f5bcf1e2bdf4a18'
         'e09d77842aac9a0cd7eebe112df88383'
         '6f99711eee0b1c413dca9fa691917b71')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    patch -Np1 -i "${srcdir}"/which-2.20.patch      # FS#11455
    patch -Np1 -i "${srcdir}"/movesize.patch        # FS#29812
    patch -Np0 -i "${srcdir}"/title-matching.patch  # FS#5277 on Openbox bugtracker

    sed -i 's|/usr/bin/env python|/usr/bin/env python2|' \
            data/autostart/openbox-xdg-autostart
  
    ./configure --prefix=/usr \
                --with-x \
                --enable-startup-notification \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/openbox
  
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install

    sed -i "s:startkde:/usr/bin/\0:" \
           "${pkgdir}"/usr/share/xsessions/openbox-kde.desktop
    sed -i "s:=gnome-session:=/usr/bin/gnome-session:" \
           "${pkgdir}"/usr/share/xsessions/openbox-gnome.desktop
}
