# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari
pkgver=2.11.3
pkgrel=2
pkgdesc='Computer algebra system designed for fast computations in number theory'
url='https://pari.math.u-bordeaux.fr/'
license=('GPL')
arch=('x86_64')
depends=('gmp' 'readline' 'libx11')
makedepends=('perl' 'texlive-core')
optdepends=('perl: gphelp, tex2mail')
validpgpkeys=('42028EA404A2E9D80AC453148F0E7C2B4522E387')
source=("https://pari.math.u-bordeaux.fr/pub/pari/unix/${pkgname}-${pkgver}.tar.gz"{,.asc}
        'c7a1d35f.patch')
sha256sums=('c7100a467eaf908942bb403cbd38036a26d7222e6ee6d39b50ab667d052ca6c9'
            'SKIP'
            'a1cbb79f04d686f6c5e2e61e3240bd28aeab786690502981d5da2da5a2fbcb05')

conflicts=('pari-sage')
replaces=("pari-sage<=1:${pkgver}")

prepare() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../c7a1d35f.patch
}

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	./Configure \
		--prefix=/usr \
		--with-readline \
		--mt=pthread \
		--with-gmp \

	make all
}

check() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make bench
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -sf gp.1.gz "${pkgdir}"/usr/share/man/man1/pari.1
}
