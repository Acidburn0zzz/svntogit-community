# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=geonkick
pkgver=1.9.2
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'desktop-file-utils' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libsndfile' 'libx11' 'shared-mime-info')
makedepends=('cmake' 'libjack.so' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint' 'sord')
optdepends=('libjack.so: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('445d8103365b583cd57c62f21faed36c964b687e2b887747977f301fe1d1cd5f5f3f3080b9f727d06bfa9a3f37972082436d8d84aae2e9534213adf32bd6f04f')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DGKICK_REDKITE_SDK_PATH=/usr \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  LV2_PATH="${PWD}/plugin/lv2" lv2lint "http://geontime.com/geonkick" || echo "Known to fail: https://gitlab.com/geontime/geonkick/issues/144"
  sord_validate "${PWD}/plugin/lv2/${pkgname}.lv2/"*.ttl || echo "Known to fail: https://gitlab.com/geontime/geonkick/issues/144"
}

package() {
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 examples/*.gkick \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  for directory in examples/*; do
    if [ -d "$directory" ]; then
      install -vDm 644 "$directory/"*.gkick \
        -t "${pkgdir}/usr/share/doc/${pkgname}/examples/$(basename "$directory")"
    fi
  done
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
