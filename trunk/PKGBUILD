# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-yaspin
pkgver=2.1.0
pkgrel=1
pkgdesc="Yet Another Terminal Spinner"
url="https://github.com/pavdmyt/yaspin"
license=('MIT')
arch=('any')
depends=('python-termcolor')
makedepends=('python-dephell')
checkdepends=('python-pytest-runner')
source=("https://github.com/pavdmyt/yaspin/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        setup.py)
sha512sums=('ea2379fe052ac7523a6383b51babc867adccbf98938f2ca6755a33fc932fee54c5bc84e91ab09ad1d88b436f55d7bc4f69ae63a13453b7aac3877bcd1efe232a'
            '901fcb24388efa19ef29825c742075af1869d57423f2286e834cf28da3ad035a18bce28ea090e29bd0dde41607b514e73e098766528cfc9e23e9e2dd47965cea')

prepare() {
  cd yaspin-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  #dephell deps convert --from pyproject.toml --to setup.py

  # Bootstrapping for Python 3.10
  cp ../setup.py .
}

build() {
  cd yaspin-$pkgver
  python setup.py build
}

check() {
  cd yaspin-$pkgver
  python setup.py pytest
}

package() {
  cd yaspin-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
