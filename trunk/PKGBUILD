# $Id$
#Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxxf86vm
pkgname=lib32-$_pkgbasename
pkgver=1.1.0
pkgrel=3
pkgdesc="X11 XFree86 video mode extension library (32-bit)"
arch=('x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('lib32-libxext>=1.1' $_pkgbasename)
makedepends=('pkgconfig' 'xf86vidmodeproto>=2.3' gcc-multilib)
options=('!libtool')
source=(${url}/releases/individual/lib/libXxf86vm-${pkgver}.tar.bz2)
md5sums=('b431ad7084e1055fef99a9115237edd8')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXxf86vm-${pkgver}"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXxf86vm-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
