# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=12.5.8
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('nodejs-nopt' 'npm' 'semver')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('f120d9eaa61723468a7fadf349b072a81f5098af3282b67e51571783d906432bc7edd1887056ae145d24b02d0d753847914ff9421a302554875ef6bdcee8e426')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Experimental dedup
  rm -r "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/{,.bin/}nopt
  rm -r "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/{,.bin/}semver

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
