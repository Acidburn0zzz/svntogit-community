# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-mockmodule
pkgver=0.174.0
pkgrel=1
pkgdesc="Override subroutines in a module for unit testing"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl-super')
makedepends=('perl-module-build')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage' 'perl-test-warnings')
url='https://search.cpan.org/dist/Test-MockModule'
source=("https://search.cpan.org/CPAN/authors/id/G/GF/GFRANKS/Test-MockModule-v$pkgver.tar.gz")
sha512sums=('dacff732b966d545d34350935518f359efe12ebe8774f1cd1eb3ddbe6075cd5e11df938e5224d26df8b6b4463c163ec3f5b8129e1020c43cd28aaaadc3499d33')

build() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build test
}

package() {
  cd "$srcdir/Test-MockModule-v$pkgver"
  perl Build install destdir="$pkgdir"
}

# vim:set ts=2 sw=2 et:
