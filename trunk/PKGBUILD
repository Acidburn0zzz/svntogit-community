# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vincent Bernardoff <vb@luminar.eu.org>

pkgname=clingo
pkgver=5.2.0
pkgrel=1
pkgdesc='Grounding tools for (disjunctive) logic programs'
arch=('x86_64' 'i686')
url='https://potassco.org/'
license=('GPL3')
depends=('python' 'lua')
conflicts=('clasp')
makedepends=('bison' 're2c' 'cmake' 'ninja' 'python' 'lua')
source=("https://github.com/potassco/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('57d8979b0972a091e1921309ca9ba9c18dd0cf83afcfb1586a8c0bff54ed8b9b')

build() {
  mkdir -p build
  cd build
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCLINGO_REQUIRE_PYTHON=ON \
    -DCLINGO_BUILD_PY_SHARED=ON \
    -DPYCLINGO_USER_INSTALL=OFF \
    -DCLINGO_REQUIRE_LUA=ON \
    -DCLINGO_BUILD_LUA_SHARED=ON \
    -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
