# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000058
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('30355d603a142c10b63db3d04c906dc6ecf91803757b5f01c820d1487d33a3dfd31bc3f56289670a3425f53d02de52bc71a1ca036680800e3b4387aab0c5651d')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
