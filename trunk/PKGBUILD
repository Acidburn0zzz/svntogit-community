# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=fcitx-libpinyin
pkgver=0.5.2
_model=model.text.20161206.tar.gz
pkgrel=1
pkgdesc="Fcitx Wrapper for libpinyin, Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-libpinyin"
license=('GPL')
depends=('fcitx' 'libpinyin' 'fcitx-qt5' 'qt5-webengine')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fcitx/fcitx-libpinyin/archive/$pkgver.tar.gz"
        "$pkgname-$_model::http://download.fcitx-im.org/data/$_model")
sha512sums=('c74628613445afda7cbadc9f0f475d8a6fbf34add25f5a86f9ede9e3948604b50def841293201914608d2993835bd356dfa0acf95e4d7a68977b38c9c113e7ae'
            '26450e347b7a211e5abd94acbb9befc8cee7d8fc20e5161ef11b8b9b1ad47cde4f1add5c41484d9af04e4dde2aa748090e297e799ee7f6d2f9fcec3873eee429')

prepare() {
  mkdir build
  ln -s "$srcdir"/$pkgname-$_model fcitx-libpinyin-$pkgver/data/$_model
}

build() {
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
