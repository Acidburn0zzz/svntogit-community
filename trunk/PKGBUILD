# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9902
pkgrel=1
pkgdesc='Ameliorated interface to the CPAN'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-log-message' 'perl-archive-extract' 'perl-term-ui' 'perl-log-message-simple'
         'perl-module-pluggable' 'perl-object-accessor' 'perl-package-constants')
url=https://metacpan.org/release/CPANPLUS
source=("https://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
sha512sums=('a3fdddf69a34d1cc9805d8ddcfa5849963d1214ef6e52f41622eed855eec8d39e495fd7401e24643b8549ca7c40e7f03af2f3c10d9c8034c129cc9bb04a124de')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
