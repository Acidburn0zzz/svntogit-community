# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-bopomofo
pkgver=0.0.0.20210102
_commit=615ec0d903f4dba50bf7a6f735adf18c0476ac54
pkgrel=1
pkgdesc="Bopomofo input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-bopomofo"
license=('LGPL')
# Rime will regenerate yaml&prism bin, which makes terra-pinyin required on runtime (for translator)
# rime-cangjie is dependency for reverse lookup
depends=('rime-cangjie' 'rime-terra-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-bopomofo/archive/$_commit/rime-bopomofo-$_commit.tar.gz")
sha512sums=('964b01e7d29b743f2fea78a95b56e159102420877c4491b26326f9901c1de5f8d4f6eebe9917098d214348a63069297998ca0bc2a835ae12fe0ed8e1346e45b0')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay rime-terra-pinyin | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/terra_pinyin.*
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
