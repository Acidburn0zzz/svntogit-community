# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Nagy <danielnagy at gmx de>

pkgname=uglify-js
pkgver=3.3.27
pkgrel=1
pkgdesc="JavaScript parser, mangler/compressor and beautifier toolkit"
arch=('any')
url='http://lisperator.net/uglifyjs'
license=('BSD')
depends=('nodejs')
makedepends=('npm')
install=${pkgname}.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('3bde30c4c49bded77f4e5a1f9084d8bd8225bc855d95dbcd5c35517a4ccad7709066e2f7eee6b89eb7575f4468ecc6a97d4573825447b34ffe8a63d151d3a086')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/uglify-js/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix npm derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}

# vim:set ts=2 sw=2 et:
