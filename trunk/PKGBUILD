# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.8.5
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'xorg-server' 'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory'
         'qt5-tools')
makedepends=('deepin-gettext-tools')
provides=('lightdm-deepin-greeter' 'deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('ec88b8d19b240e9787073715aeb32c8af608fe152ce0033543727795354f5030e9723e2242b2b235889b4108bafc6a02730f0da2e474d180ed0eab49a37e2020')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp boxframe/*.cpp
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
