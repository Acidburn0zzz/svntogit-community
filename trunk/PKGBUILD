# $Id: PKGBUILD 120612 2014-10-13 14:26:12Z arodseth $
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

pkgname=mlton
pkgver=20130715
pkgrel=4
pkgdesc='Whole-program, optimizing Standard ML compiler'
url='http://mlton.org/'
license=('BSD' 'MIT' 'LGPL')
depends=('bash' 'gmp')
makedepends=('patch' 'setconf')
arch=('x86_64' 'i686')
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.src.tgz")
sha256sums=('215857ad11d44f8d94c27f75e74017aa44b2c9703304bcec9e38c20433143d6c')

if [ $CARCH = x86_64 ]; then
  source+=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-1.amd64-linux.tgz")
  sha256sums+=('f6f912009b0946b73af88ff5f3e713b6be63b871696e2697ccf1564b56e7d03b')
else
  source+=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-1.x86-linux.tgz")
  sha256sums+=('92d89627cf760c56d0e9727f328b006341bf5be283fc7d616a6276c26d95b2cf')
fi

prepare() {
  setconf usr/bin/mlton lib "$srcdir/usr/lib/mlton/"
}

build() {
  PATH="$srcdir/usr/bin:$PATH" make -C "$pkgname-$pkgver" all-no-docs
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install-no-docs
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  # Copy over the handful of licenses and the README as explanation
  cp "$pkgname-$pkgver/doc/license/"* "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim:set ts=2 sw=2 et:
