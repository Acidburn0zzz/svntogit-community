# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.4.4
pkgrel=1
pkgdesc='JSON library built on GLib'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/JsonGlib'
license=('GPL')
depends=('json-glib' 'lib32-glib2')
makedepends=('git' 'gobject-introspection' 'meson')
source=("git+https://gitlab.gnome.org/GNOME/json-glib.git#tag=${pkgver}")
sha256sums=('SKIP')

build(){
  export CC='gcc -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  meson json-glib build \
    --buildtype='release' \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --libexecdir='/usr/lib32'
  ninja -C build
}

package(){
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,lib32/installed-tests,share}
}

# vim: ts=2 sw=2 et:
