# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Daniel M. Capella <polyzen@archlinux.info>
# Contributor: Sirat18 <aur@sirat18.de>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

_name=icalendar
pkgbase=python-icalendar
pkgname=('python-icalendar' 'python2-icalendar')
pkgver=4.0.3
pkgrel=1
pkgdesc='A parser/generator of iCalendar files (RFC 2445)'
arch=('any')
url="https://github.com/collective/icalendar"
license=('custom:BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-dateutil' 'python-pytz' 'python2-dateutil' 'python2-pytz')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('2f0ac5a7f36c7d37047ba20bec3f2ca7df51a9e3ec3f29751c71cdd264c01f7e0abd6efc298dac5a7c87911cb4916e56bf77716d6ca05a2d090e26486e6a56be')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgbase}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  python -m unittest discover src
  python2 -m unittest discover src
}

package_python-icalendar() {
  depends=('python-dateutil' 'python-pytz')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm 644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-icalendar() {
  depends=('python2-dateutil' 'python2-pytz')
  cd "${pkgbase}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  mv -v "${pkgdir}/usr/bin/${_name}"{,2}
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGES,CONTRIBUTING,README}.rst
  install -vDm 644 LICENSE.rst \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

