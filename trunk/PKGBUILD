# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=4.4.0
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'dbus-glib' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapps')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'intltool')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('594ce21d13dad9ca577880e5d20c4d22a9e1aaad8d84520673b5990553b772050997d5b8ad1ed0ee094e4f0c23939bc5d17bc3c1d5bd6090fdbf2c1cc2b4e476')
b2sums=('801b6fbb4dec73e47a9380cdaabb6f85e6f64b111733c89e577375f633ef4d69227cc1b02eb1c0a5e291cffc748a7df322a69ab14887669ce90d1188d6032dc2')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --libexecdir=/usr/lib/cinnamon-screensaver \
                --localstatedir=/var
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
}
