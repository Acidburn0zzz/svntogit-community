# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=tokei
pkgver=9.1.1
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('05c8e4f0feeaf41b94aec5ded423ca20ef04901545834adf23dc76a9d4c3f9df241280ae2e1f38b19d36fdc5078c6214eab598834f307fcbde56184d77ee2d58')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cargo build --release
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
