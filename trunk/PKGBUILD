# Maintainer: Ronald van Haren <ronald@archlinux.org>
# Maintainer: Carsten Haitzler <raster@archlinux.org>
# Contributor: Doug Newgard <scimmia22 at outlook dot com>
# Contributor: Paul Ezvan <paul@ezvan.fr>

pkgname=terminology
pkgver=1.12.0
pkgrel=1
pkgdesc="EFL based terminal emulator"
arch=('x86_64')
url="https://www.enlightenment.org/about-terminology"
license=('BSD')
depends=('efl')
makedepends=('meson')
source=("https://download.enlightenment.org/rel/apps/${pkgname}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('74d2d3b253a77bcb215fc36eedf0ccb59643452e2ef15c510430ffaa6034dcf0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export CFLAGS="${CFLAGS} -fvisibility=hidden"
  meson builddir --prefix=/usr
  ninja -C builddir
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C builddir install

  # install license files
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/COPYING" \
        "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
