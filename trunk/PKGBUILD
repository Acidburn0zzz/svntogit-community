# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-terminal
pkgver=2.3.1
pkgrel=2
pkgdesc='Default terminal emulation application for Deepin'
arch=('i686' 'x86_64')
url="https://github.com/manateelazycat/deepin-terminal"
license=('GPL3')
depends=('vte3' 'libsecret' 'libgee' 'libwnck3' 'expect' 'deepin-shortcut-viewer' 'deepin-menu'
         'deepin-manual')
makedepends=('vala' 'cmake')
groups=('deepin-extra')
#options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/manateelazycat/deepin-terminal/archive/$pkgver.tar.gz")
sha512sums=('b437418b2a3f865f0ef6c6f8de640591ab8f69f874d7c5a18e5ee4f307b22263fee5824b04d2e17b44fcc948bba30b77f18957057289f1cd430f27ca7fe06f5a')

prepare() {
  mkdir -p build

  sed -i 's|return __FILE__;|return "/usr/share/deepin-terminal/project_path.c";|' deepin-terminal-$pkgver/project_path.c
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../deepin-terminal-$pkgver
  make
}

package() {
  make -C build DESTDIR="$pkgdir" install
  ln -s deepin-terminal "$pkgdir"/usr/bin/x-terminal-emulator
}
