# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>

pkgname=ldoc
pkgver=1.3.2
pkgrel=1
pkgdesc='A LuaDoc-compatible documentation generation system'
arch=('any')
url='http://stevedonovan.github.com/ldoc/'
license=('custom')
depends=('lua' 'lua-penlight')
source=("$pkgname-$pkgver.tar.gz::https://github.com/stevedonovan/ldoc/archive/$pkgver.tar.gz")
md5sums=('486080cd4dd2ac834eb846e1f1406ceb')

package() {
  cd LDoc-$pkgver
  # install library
  install -dm755 "$pkgdir/usr/share/lua/5.2"
  cp -a ldoc "$pkgdir/usr/share/lua/5.2"
  # install binaries
  install -Dm755 ldoc.lua "$pkgdir/usr/bin/ldoc.lua"
  ln -s ldoc.lua "$pkgdir/usr/bin/ldoc"
  # copy docs
  install -Dm644 readme.md "$pkgdir/usr/share/docs/$pkgname/readme.md"
  install -Dm644 docs/doc.md "$pkgdir/usr/share/docs/$pkgname/doc.md"
  # copy license
  install -Dm644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
