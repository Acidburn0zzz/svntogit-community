# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Markus Richter <mqus at disroot dot org>

pkgname=vaultwarden-web
_pkgver=2.25.0
pkgver=2.25.1
_upstreamver=${pkgver%[[:lower:]]}
_patchver=$_pkgver
pkgrel=1
pkgdesc='Bitwarden web vault with the patches to make it work with Vaultwarden'
arch=('any')
url=https://github.com/dani-garcia/bw_web_builds
license=('GPL3')
depends=('vaultwarden')
makedepends=('git' 'nodejs-lts-gallium' 'npm')
provides=('bitwarden_rs-vault' 'bitwarden_rs-web')
replaces=('bitwarden_rs-vault' 'bitwarden_rs-web')
install=$pkgname.install
source=("bitwarden-web::git+https://github.com/bitwarden/web.git#tag=v$_upstreamver"
        "$pkgname-v$_patchver.patch::https://raw.githubusercontent.com/dani-garcia/bw_web_builds/v$pkgver/patches/v$_patchver.patch")
b2sums=('SKIP'
        '5023c74e342d9e416f8de2a781a846a1027fba28bf763330c196b8ed836bfc971bf156715beaf88f6a73c24c660f25f61d27457cc50a7bc6e1c4630001f3c989')

prepare() {
  cd bitwarden-web
  git submodule update --init
  patch --forward --strip=1 --input=../$pkgname-v$_patchver.patch

  npm ci
}

build() {
  cd bitwarden-web
  npm run dist:oss:selfhost
}

package() {
  cd bitwarden-web
  install -d "$pkgdir"/usr/share/webapps/$pkgname
  cp -R build/* "$pkgdir"/usr/share/webapps/$pkgname
}

# vim:set ts=2 sw=2 et:
