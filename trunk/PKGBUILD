# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-checkdocs
pkgver=2.5.0
pkgrel=1
pkgdesc='Check the README when running tests'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-checkdocs'
depends=('python-docutils' 'python-jaraco-functools' 'python-pep517')
makedepends=('python-pip' 'python-setuptools-scm' 'python-toml')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-checkdocs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('2546190ca6ef1546ffa56200d4c8a241ac39adc40f2370a692650ac1431f6fa909b6a7d337a9f5e53359c793a4a17a949f1bd5a353888f160302c6f4257a9658')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-checkdocs-$pkgver
  python setup.py build
}

check() {
  cd pytest-checkdocs-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-checkdocs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
