# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb ][ gmail ? com>
# Contributor: Jonathan Schaeffer <joschaeffer ][ gmail ? com>
# Contributor: chfoxli <lg ][ romandie ? com>

pkgname=kphotoalbum
pkgver=4.7
pkgrel=1
pkgdesc="KDE Photo Album, picture collection manager"
url="http://www.kphotoalbum.org/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('kdebase-runtime' 'libkipi4' 'libkdcraw4' 'libkface4' 'libkgeomap4')
makedepends=('cmake' 'automoc4' 'kdemultimedia-mplayerthumbs')
optdepends=('kipi-plugins: add extra functionality'
	'kdemultimedia-mplayerthumbs: video thumbnails')
install=${pkgname}.install
source=("http://download.kde.org/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.xz")
sha256sums=('8b33cbe995b652740ee965b907abb96689588c1eac36340d9ace646d3018735d')

build() {
# workaround build failure
#  mv $pkgname-$pkgver $pkgname

  mkdir build
  cd build

  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install
}
