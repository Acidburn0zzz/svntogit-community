# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=20210331102817
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2fly/domain-list-community"
license=('MIT')
makedepends=('go' 'git')
source=("https://github.com/v2fly/domain-list-community/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f3d23a15dc84187830ff35de21d5c6d7e3972d743a706082a2e212af5a2d54a2d6bde7087b01530054307b1b74ea962c82dc35081ea06d4d33215984cdc5d902')

build() {
  cd domain-list-community-$pkgver
  go run main.go
}

package() {
  cd domain-list-community-$pkgver
  install -Dm644 dlc.dat "$pkgdir"/usr/share/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
