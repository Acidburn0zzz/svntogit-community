# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>

pkgname=mailnag-goa-plugin
pkgver=1.2.0+3+ga8ccbe7
pkgrel=1
pkgdesc="Mailnag GNOME Online Accounts plugin"
arch=('any')
url="https://github.com/pulb/mailnag-goa-plugin"
license=('GPL')
depends=('gnome-online-accounts' 'mailnag')
makedepends=(git)
_commit=a8ccbe77adadc0ea01899f5cfc465d3ac22debb2  # master
source=("git+https://github.com/pulb/mailnag-goa-plugin.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1
}
