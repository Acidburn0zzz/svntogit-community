# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.6
pkgrel=1
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='https://www.fresse.org/dateutils/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha1sums=('a5ef4361edeebaef683d6f7ac03dc42ed3f921ce'
          'SKIP')
sha512sums=('014e8411774250d2862943bbbdd69cc7ec2ca934349272ae7c44b136a57ef30c23f841018cc38f86d49e2348b2199848076db224e3653d6d7337a3f17709c8cb'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-old-links=no
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
