# Maintainer: David Runge <dave@sleepmap.de>
_name=spreed
pkgname=nextcloud-app-spreed
pkgver=5.0.2
pkgrel=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud<16.0.0')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('ef849cb56f7973a92a61d19f36d1aaa795ec2d4c6c44e11d52ee675a21b8751a46003636dfdb2aacc87c0bf25e9e1dbb6fa61ffee3aa61a7e8f0ba627996ac0b')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -dm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/${_name}/"
  cp -av ${_name}/* "${pkgdir}/usr/share/webapps/nextcloud/apps/${_name}/"
}
