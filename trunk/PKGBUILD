# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-artwork
pkgver=0.116
pkgrel=1
pkgdesc="Sugar icons and themes"
arch=('x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
makedepends=('gtk2' 'gtk3' 'icon-naming-utils' 'python-empy' 'xorg-xcursorgen')
optdepends=('gtk2: GTK+ 2 theme'
            'gtk3: GTK+ 3 theme')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('050eaef2ff889e830cd72726cb3dcc3868de9b82a1b4bddc1ecd4d12b3a65e8b')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr EMPY=`echo /usr/lib/python3.*/site-packages/em.py`
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
