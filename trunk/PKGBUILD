# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xreader
pkgver=2.8.2
pkgrel=1
pkgdesc="Document viewer for files like PDF and Postscript. X-Apps Project."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
groups=('x-apps')
depends=('poppler-glib' 'webkit2gtk' 'xapps')
optdepends=('djvulibre: support for djvu files'
            'libgxps: support for xps files'
            'libspectre: support for dvi and ps files'
            'mathjax2: support for math in epub files'
            'texlive-bin: support for dvi files')
makedepends=('meson' 'samurai' 'intltool' 'itstool' 'gobject-introspection' 'djvulibre'
             'libgxps' 'libspectre' 'texlive-bin')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('a58165734d549b4f963ce36f6f41d076bbeea07a5bcfc45458691ad22018c524')
b2sums=('77ba8ebe0bdcef76705ce7338822ddd1e07a6926e3b45c0decd2fd26325920bace6fba6e241314b73ec2616bec3d656cfdc20b6938cbdbbff61008bc9b7f157e')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/${pkgname} \
          --buildtype=plain \
          -Dmathjax-directory=/usr/share/mathjax2 \
          -Dcomics=true \
          -Ddjvu=true \
          -Ddvi=true \
          -Dt1lib=true \
          -Dpixbuf=true \
          -Dintrospection=true \
          -Dhelp_files=true \
          -Dtests=false \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
