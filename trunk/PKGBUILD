# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screen-recorder
pkgver=5.8.0.11
pkgrel=2
pkgdesc='Deepin Screen Recorder'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screen-recorder"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-turbo' 'byzanz' 'ffmpeg')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screen-recorder/archive/$pkgver.tar.gz")
sha512sums=('8fb57603905d46efa52fb347a9b9e0101b55ff747ea8beed7cd248be9f488927f3a809bb112e98fddd16bb574386dba5706f71f181a19615cd2f1bf4e769d28e')

prepare() {
  cd deepin-screen-recorder-$pkgver
  sed -i '/include <X11.extensions.XTest.h>/a #undef min' src/event_monitor.cpp
}

build(){
  cd deepin-screen-recorder-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-screen-recorder-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
