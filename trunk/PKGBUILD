# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=3.1.13
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'taglib' 'libcue' 'qt5-svg' 'gst-plugins-bad')
makedepends=('python' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz")
sha512sums=('ecbfe8ff6bab1898e9d5316888d5b24170c85b94c64ef06549a036e176282d659e8f42e314fe2f8b810061fb3c8891f26b70389d6e4e96cc04d40c9c7f5a2e8a')

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
