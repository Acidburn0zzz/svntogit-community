# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: poplarch <poplarch@gmail.com>

pkgname=fcitx-libpinyin
pkgver=0.5.1
_model=model.text.20161206.tar.gz
pkgrel=5
pkgdesc="Fcitx Wrapper for libpinyin, Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/fcitx/fcitx-libpinyin"
license=('GPL')
depends=('fcitx' 'libpinyin' 'fcitx-qt5' 'qt5-webengine')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/fcitx/fcitx-libpinyin/archive/$pkgver.tar.gz"
        "$pkgname-$_model::http://download.fcitx-im.org/data/$_model")
sha512sums=('5847d98c639584aa53be630c7d96f529fa03f5c88f2e2dc657256c7192d09e93abc36be5c77da2a3b6f57674a4a9e0f666df9da9d5b18e02a42d914c5cc4aacf'
            '26450e347b7a211e5abd94acbb9befc8cee7d8fc20e5161ef11b8b9b1ad47cde4f1add5c41484d9af04e4dde2aa748090e297e799ee7f6d2f9fcec3873eee429')

prepare() {
  mkdir build
  ln -s "$srcdir"/$pkgname-$_model fcitx-libpinyin-$pkgver/data/$_model

  sed -i 's/pinyin_guess_candidates(m_inst, pinyinOffset());/pinyin_guess_candidates(m_inst, pinyinOffset(), SORT_BY_PHRASE_LENGTH_AND_FREQUENCY);/' fcitx-libpinyin-$pkgver/src/eim.cpp
}

build() {
  cd build

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
