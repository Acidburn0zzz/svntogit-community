# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Unknown47 <unknown47r@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=pcmanfm
pkgver=1.1.1
pkgrel=1
pkgdesc="An extremely fast and lightweight file manager"
arch=('i686' 'x86_64')
url="http://pcmanfm.sourceforge.net/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'desktop-file-utils' 'libfm' 'lxmenu-data')
makedepends=('intltool' 'pkgconfig')
install=$pkgname.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-$pkgver.tar.gz)
md5sums=('853ef5e3eba6bf36e15985bfe3170456')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --sysconfdir=/etc --prefix=/usr 
  make LDFLAGS+=' -lm'
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install 

  sed -e 's|x-directory/normal|inode/directory|' -i $pkgdir/usr/share/applications/pcmanfm.desktop
}
