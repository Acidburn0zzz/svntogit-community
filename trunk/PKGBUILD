# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=5.0.14.1
pkgrel=3
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
groups=('deepin')
depends=('deepin-qt5integration' 'kwin')
makedepends=('cmake' 'expac' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        $pkgname-tabbox-chameleon-rename.patch::https://github.com/linuxdeepin/dde-kwin/pull/106.patch
        $pkgname-build-fix.patch::https://github.com/linuxdeepin/dde-kwin/pull/109.patch
        $pkgname-unload-blur.patch::https://github.com/linuxdeepin/dde-kwin/pull/112.patch)
sha512sums=('9b3b55401646b189650076d9aae4266634f531de8100fbb0cdc7d487d9d51e8bb2c9567f21839a49f8bcbdd442d5010a20718ca9010582389e112c71bf209115'
            '7ac1a577868b73314274d336287334acc5bf4ac06dde046c93e4c2d3f68a112389c8b0644d1cbc3fde5ba1c01f368676f4f34b4a1b7b6c97a5b73d66507d134d'
            '3e10241050959b51bdd6bf752253af61a2b5899c3df5d84716bd1688d9292ff65d2b7d3aea8f61e896081a9bee013cb3f4c2f42f7915038f7f476120a44cf029'
            '800bb8770b4a6d835e27f33226f81dce92ddf10c295e02015419788e667e7d46b014d69bde73d03af8f0480c938526b48498368d2f16619689352f0e8e183912')

prepare() {
  cd dde-kwin-$pkgver
  patch -p1 -i ../$pkgname-tabbox-chameleon-rename.patch
  patch -p1 -i ../$pkgname-build-fix.patch
  patch -p1 -i ../$pkgname-unload-blur.patch
}

build() {
  cd dde-kwin-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DKWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  make
}

package() {
  cd dde-kwin-$pkgver
  make DESTDIR="$pkgdir" install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
