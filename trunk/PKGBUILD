# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=1.2.5
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz")
sha512sums=('6e4f3b72cea6c95826c54deeaf6eee262a7cbdf43be86c85c0f6798370060ad2db51a7f654bcde3e0dbd03a5dee0ba8e0e99ad788491bd5e8513a518dc91f9ea')

prepare() {
  cd ukui-greeter-$pkgver
  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
