# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-vcrpy
pkgver=4.0.2
pkgrel=1
pkgdesc='Automatically mock your HTTP interactions to simplify and speed up testing'
arch=('any')
license=('MIT')
url='https://github.com/kevin1024/vcrpy'
depends=('python-yaml' 'python-wrapt' 'python-six' 'python-yarl')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-httpbin' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kevin1024/vcrpy/archive/v$pkgver.tar.gz")
sha512sums=('17eebc9574ff14f97ad200b99c50b0440a93fbf38afe8ad95f443370d84c43177ac55ab655ffea7e7985b0cd76124eff3c394898e71a0ba0c434014f07e526f9')

build() {
  cd vcrpy-$pkgver
  python setup.py build
}

check() {
  cd vcrpy-$pkgver
  python setup.py pytest
}

package() {
  cd vcrpy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
