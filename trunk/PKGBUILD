# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-pippy
_pkgname=Pippy
pkgver=70
pkgrel=2
pkgdesc="Sugar activity to learn and develop in Python programming language"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Pippy"
license=('GPL')
groups=('sugar-fructose')
depends=('python2-pybox2d' 'python2-pygame' 'sugar' 'vte3')
source=($pkgname-$pkgver.tar.gz::https://github.com/sugarlabs/$_pkgname/archive/v$pkgver.tar.gz
        collabwrapper.patch
        various-fixes.patch)
sha256sums=('7c7ca995661a4e1c0964e5788ad5dfce539beca7acaa46b278b7f71429ccf575'
            'ef94c4f263c45515bc3c2b1a506ad6385203f5e691f7a6221ca1716406410490'
            '48c514de0f440a0f33278a0d7f5e477a7472a3c8e495856cffe5f39b3bc62652')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../collabwrapper.patch
  patch -Np1 -i ../various-fixes.patch
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python %s; sleep 1/python2 %s; sleep 1/' pippy_app.py
  rm -r library/box2d_*/ library/Box2D.py library/LICENSE
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
