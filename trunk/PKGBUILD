# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Chris Down <chris@chrisdown.name>

pkgname=clipnotify
pkgver=1.0.0
pkgrel=2
pkgdesc='Polling-free clipboard notifier'
url='https://github.com/cdown/clipnotify'
arch=('x86_64')
license=('custom:Public Domain')
depends=('libx11' 'libxfixes')

source=("${pkgname}-${pkgver}.tar.gz::https://github.com/cdown/clipnotify/archive/${pkgver}.tar.gz"
        'clipnotify-0001-add-buildflags.patch')
sha512sums=('b0659920d9191011399b646cba92546c8b5849dba35f863d6338d8e7159c63b040faadc519fa770249378a1361c0f2da7dfd637f20c0c7d9cccb8b1d8ee8892b'
            '901c639bd88f495712984538112c9147bc3d8862776ed544f0e99843c8deafbe38ef6296bf1bf37323d6927f82efa14a06d8cf0ecf7d0cf1f9c38b56c11bda3c')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -p1 -N -l -i "${srcdir}/clipnotify-0001-add-buildflags.patch"
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    install -Dm755 clipnotify "${pkgdir}/usr/bin/clipnotify"
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
