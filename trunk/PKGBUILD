# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-invoke
pkgver=1.6.0
pkgrel=1
pkgdesc="Pythonic task execution"
url="https://github.com/pyinvoke/invoke"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-relaxed')
source=("https://pypi.io/packages/source/i/invoke/invoke-$pkgver.tar.gz")
sha512sums=('640719d576b6c655d2a1b8e2f0556b49621c846fbce26120abc818c20dc6f9445c7663b9aafe41e7e4f7f6160bd17d1678bb6912cbe13dcbaded278cc78eb824')

prepare() {
  find invoke-$pkgver -type f -name '*.pyc' -delete
}

build() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/invoke-$pkgver
  python setup.py pytest || echo "Tests failed"
}

package() {
  cd invoke-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
