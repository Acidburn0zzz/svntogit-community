# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Streetwalrus <streetwalrus@codewalr.us>

pkgname=shotgun
pkgver=2.2.0
pkgrel=1
pkgdesc='Minimal X screenshot utility'
arch=('x86_64')
url=https://github.com/neXromancers/shotgun
license=('MPL2')
depends=('gcc-libs' 'libxrandr')
makedepends=('git' 'rust')
source=("$url/archive/v$pkgver/shotgun-v$pkgver.tar.gz")
sha512sums=('d5e88c0f8be7c02f4244122e842fcef086d335618943058b1b121f2dff6cf40a9c1e78729e1c1b25ace3da49ef08d43fb3f9241a2f2d4d7f28b4fd84c8bbc9b3')

build() {
  cd shotgun-$pkgver
  cargo build --release --locked
}

package() {
  cd shotgun-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/shotgun
}

# vim:set ts=2 sw=2 et:
