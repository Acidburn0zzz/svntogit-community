# Maintainer: David Runge <dvzrv@archlinux.org>

_name=Tidal
pkgname=haskell-tidal
pkgver=1.7.3
pkgrel=2
pkgdesc="A domain specific language for live coding pattern."
arch=('x86_64')
url="https://tidalcycles.org/"
license=('GPL3')
groups=('pro-audio')
depends=('ghc' 'ghc-libs' 'glibc' 'haskell-bifunctors' 'haskell-clock'
'haskell-colour' 'haskell-hosc' 'haskell-mwc-random' 'haskell-network'
'haskell-primitive' 'haskell-random' 'haskell-vector' 'supercollider')
checkdepends=('haskell-microspec')
optdepends=('sc3-plugins: Examples using special UGens')
conflicts=('tidalcycles')
replaces=('tidalcycles')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tidalcycles/${_name}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('4c57a2cf7d3d9f16e0acf09278272ffdd588a68d71312cca049842e516bd8541462a552e29bd61fdad6431ae76a7a80cc03f6e30cadf14485c30176484d71c20')
b2sums=('4f0971bfcc23965ca6a8df7c89d9518874a2b67ce297a254ea3439896801b7d68a8b3c3240b73be6c7bdd4c5c99ff9ec05b7b53448dee1b0da623674a1dd5318')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  runhaskell Setup configure -O \
                             --enable-shared \
                             --enable-executable-dynamic \
                             --disable-library-vanilla \
                             --prefix=/usr \
                             --docdir="/usr/share/doc/${pkgname}" \
                             --enable-tests \
                             --dynlibdir=/usr/lib \
                             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd "$pkgname-$pkgver"
  runhaskell Setup test --show-details=direct
}

package() {
  cd "$pkgname-$pkgver"
  runhaskell Setup copy --destdir="${pkgdir}"
  install -vDm 744 register.sh "$pkgdir/usr/share/haskell/register/$pkgname.sh"
  install -vDm 744 unregister.sh "$pkgdir/usr/share/haskell/unregister/$pkgname.sh"
}
