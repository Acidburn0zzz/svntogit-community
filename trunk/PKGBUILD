# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.6.7
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('ffc74c026697a662763dd7ab04d9bd472be6c7a734e8a4bd79cf5fafc3983e8ea0459bf795e42ed09ec24537aa0bfcbe77642901b607baa1ac4b393399eaa6dc')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core github.com/sun8911879/shadowsocksR
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
