# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lsp-plugins
pkgver=1.1.17
pkgrel=2
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=('x86_64')
url="https://lsp-plug.in"
license=('LGPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('jack' 'ladspa' 'libsndfile' 'lv2' 'php')
optdepends=('jack: for standalone applications')
source=("https://github.com/sadko4u/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-1.1.17-xdg_desktop.patch::https://github.com/sadko4u/lsp-plugins/commit/bd357bf0718a58dab3ec655d344282e938067080.patch")
sha512sums=('7d6bc8f7da11a5f4756df80d9ec2d7fa3c7e2066b77e66fa6bf78035ec5852d2d63b227e8da3724ceb27cdb32da042c671db369c047b011348cd4ef5dd6387c5'
            '6bf2b3be944b739be316a382194a4fda6824b7334a89740393076b16bf964946312a945f4c64f613b50b319666459978768c0e1d0f908bae342d837cef128977')

prepare() {
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # removing all Categories from desktop files:
  # https://github.com/sadko4u/lsp-plugins/issues/97
  patch -Np1 -i "../${pkgname}-1.1.17-xdg_desktop.patch"
}

build() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  make
  make OBJDIR="${_test_path}" clean
  make OBJDIR="${_test_path}" test
}

check() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  "${_test_path}/lsp-plugins-test" utest \
    --nofork \
    --debug \
    --verbose || echo "Known flaky test: https://github.com/sadko4u/lsp-plugins/issues/19"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' \
       DESTDIR="$pkgdir/" \
       install
}
