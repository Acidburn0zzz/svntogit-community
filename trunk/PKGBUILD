# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgbase=python-regex
pkgname=('python-regex' 'python2-regex')
pkgver=2020.5.14
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python')
makedepends=('python-setuptools' 'python2-setuptools')
# https://bitbucket.org/mrabarnett/mrab-regex/issues/366/tests-use-internal-test-module-instead-of
checkdepends=('python-tests')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('ce450ffbfec93821ab1fea94779a8440e10cf63819be6e176eb1973a6017aff5')
b2sums=('bf846ce6565053e2da25fb250b2f28c5219ca27acc8ad976c180db52ce85737e4b30919ef1f6b515690d7f1333313f251bc8e76bbfaf1b9e154b3752e6f2255b')

build() {
  cd "regex-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "regex-${pkgver}"

  pushd build/lib.linux-${CARCH}-3*/
  python -c 'from regex.test_regex import test_main; test_main()'
  popd

  pushd build/lib.linux-${CARCH}-2*/
  python2 -c 'from regex.test_regex import test_main; test_main()'
  popd
}

package_python2-regex() {
  depends=('python2')
  pkgdesc="Alternative python regular expression module. (python2 version)"

  cd "regex-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_2/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python-regex() {
  depends=('python')
  pkgdesc="Alternative python regular expression module. (python3 version)"

  cd "regex-${pkgver}"
  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build

  sed -n '1,/^$/p' regex_3/regex.py | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
