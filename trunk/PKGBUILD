# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.12.34
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('81ff69a4c26d1c76107a0a66ee8e29a0cce76aaab0f549d251865a11090b9833896847fb3f8b408f1e143a0991f582dae7be2137431aa9489ed9cfd703c32997')

build() {
  cd python-phonenumbers-$pkgver/python
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver/python
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver/python
  python setup.py install -O1 --root "$pkgdir"
}
