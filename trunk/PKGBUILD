# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.0.1
pkgrel=2
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('dcraw' 'exiv2' 'fftw' 'gsl' 'hicolor-icon-theme' 'libtiff' 'openexr' 'qt')
replaces=('qtpfsgui')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/qtpfsgui/luminance-v${pkgver}.tar.gz)
md5sums=('3d94bb4c40c7cbff2ebe8f3c15b46596')

build() {
  cd ${srcdir}/luminance-v${pkgver}

  if [ ${CARCH} = i686 ]; then
    sed -i 's/ -msse2//' project.pro
  fi

  lrelease project.pro
  qmake PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/luminance-v${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
