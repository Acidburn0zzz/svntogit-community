# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=catfish
pkgver=0.3.2
pkgrel=4
pkgdesc="A versatile file searching tool"
arch=('any')
url="http://software.twotoasts.de/index.php?/pages/catfish_summary.html"
license=('GPL')
depends=('pygtk' 'pyxdg' 'xdg-utils' 'hicolor-icon-theme')
install=$pkgname.install
source=(http://www.twotoasts.de/media/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('b28db355094bfbe7de097678e91ce271')

build() {
  cd ${srcdir}/$pkgname-$pkgver

# python2 build fix
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr

#Correct symlinks
  sed -i 's|ln -sf $(DESTDIR)/$(PREFIX)/share|ln -sf ..|' Makefile
  make
}

package () {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# python2 run fix
  sed -i 's|python|python2|' ${pkgdir}/usr/bin/$pkgname

#icon file
  install -d ${pkgdir}/usr/share/pixmaps
  ln -s /usr/share/icons/hicolor/scalable/apps/$pkgname.svg ${pkgdir}/usr/share/pixmaps/$pkgname.svg
}
