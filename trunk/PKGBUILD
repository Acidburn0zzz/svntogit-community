# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Gergely Imreh <imrehgATgmailDOTcom>
# Contributor: champus
# Contributor: madeye
# Contributor: Ranguvar
# Contributor: ninja_pt

pkgname=hwinfo
pkgver=19.1
_subver=1.2
pkgrel=1
pkgdesc='Hardware detection tool from openSUSE'
arch=('x86_64' 'i686')
url='http://www.opensuse.org/'
license=('GPL2')
depends=('perl' 'libx86emu')
makedepends=('sysfsutils' 'rpmextract' 'flex')
source=("http://download.opensuse.org/source/factory/repo/oss/suse/src/$pkgname-$pkgver-$_subver.src.rpm"
        'custom_ioctl.patch')
sha256sums=('77b67512f7a7a027200233b6c89be2b777f96b2a6d742b695cb062f9cf4ec258'
            '6e83b50aa34f31db5fe6de938402e7c4dda6ffa9ffe1346c076e4779018fcf1d')

build() {
  cd "$srcdir"

  msg2 "Extracting source rpm..."
  rpmextract.sh "$pkgname-$pkgver-$_subver.src.rpm"
  tar jxf $pkgname-$pkgver.tar.bz2
  cd "$pkgname-$pkgver"
  msg2 "Patching..."
  patch -p0 < ../custom_ioctl.patch
  msg2 "Compiling..."
  make -j1 CFLAGS+="-fPIC -I$srcdir/$pkgname-$pkgver/src/hd" LIBDIR=/usr/lib
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Packaging files..."
  make LIBDIR=/usr/lib DESTDIR="$pkgdir" install
  msg2 "Cleaning up..."
  rmdir "$pkgdir/sbin"
}

# vim:set ts=2 sw=2 et:
