# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>

pkgbase=guvcview
pkgname=(guvcview guvcview-qt)
pkgver=2.0.6
pkgrel=1
pkgdesc="Simple interface for capturing and viewing video from v4l2 devices"
arch=('x86_64')
url="http://guvcview.sourceforge.net/"
license=('GPL3')
depends=('ffmpeg' 'gsl' 'gtk3' 'qt5-base' 'portaudio')
makedepends=('intltool')
source=("https://downloads.sourceforge.net/$pkgbase/$pkgbase-src-$pkgver.tar.gz")
sha256sums=('95381cef5ee139e15f90b79d1425cc22bbaae43f87452cdce6674636aff37e85')

prepare() {
  cd $pkgbase-src-$pkgver
  cp -a ../$pkgbase{,-qt}-src-$pkgver
}

build() {
  # GTK+ version
  cd $pkgbase-src-$pkgver
  ./configure --prefix=/usr --disable-debian-menu
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # Qt version
  cd ../$pkgbase-qt-src-$pkgver
  ./configure --prefix=/usr --disable-debian-menu --disable-gtk3 --enable-qt5
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package_guvcview() {
  pkgdesc="Simple GTK+ interface for capturing and viewing video from v4l2 devices"
  depends=('ffmpeg' 'gsl' 'gtk3' 'portaudio')

  cd $pkgbase-src-$pkgver
  make DESTDIR="$pkgdir" install
}

package_guvcview-qt() {
  pkgdesc="Simple Qt interface for capturing and viewing video from v4l2 devices"
  depends=('ffmpeg' 'gsl' 'qt5-base' 'portaudio')
  conflicts=('guvcview')

  cd $pkgbase-qt-src-$pkgver
  make DESTDIR="$pkgdir" install
}
