# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-acme-alien-dontpanic
pkgver=1.96
pkgrel=1
pkgdesc="Test Module for Alien::Base"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Acme-Alien-DontPanic"
depends=('dontpanic' 'perl-alien-build')
makedepends=('perl-alien-base-modulebuild' 'perl-capture-tiny' 'perl-io-socket-ssl'
             'perl-net-ssleay' 'perl-test2-suite')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Acme-Alien-DontPanic-$pkgver.tar.gz")
sha512sums=('6eeb8a77af3c0baf2dd7b3e8ac482c9f5983ea9f42df494df4e6c826c6e71d83b6b79c4553f34701b7572ed09c0f8c2e30418bd3120aafc11c43cc952aad533e')

build() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build test
}

package() {
  cd Acme-Alien-DontPanic-$pkgver
  perl Build install destdir="$pkgdir"
}
