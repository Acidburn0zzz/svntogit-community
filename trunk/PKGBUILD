# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-geoip
pkgver=20191119
pkgrel=1
pkgdesc="GeoIP List for V2Ray"
arch=('any')
url="https://github.com/v2ray/geoip"
license=('MIT')
makedepends=('go-pie' 'git')
source=("GeoLite2-Country-CSV-$pkgver.zip::https://geolite.maxmind.com/download/geoip/database/GeoLite2-Country-CSV.zip")
sha512sums=('ede6b3a69db469d00e6f05a13baed7307e02f9d59198b93a497ab37250f6888f8cfdd1af2ddf169c5d01c134bafb2ec94c7320b26fa34d19f7934ac81427fdf3')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  go get -insecure github.com/v2ray/geoip

  mv "$srcdir"/GeoLite2-Country-CSV_$pkgver .gopath/geoip
}

build() {
  cd .gopath
  bin/geoip --country=./geoip/GeoLite2-Country-Locations-en.csv --ipv4=./geoip/GeoLite2-Country-Blocks-IPv4.csv --ipv6=./geoip/GeoLite2-Country-Blocks-IPv6.csv
}

package() {
  cd .gopath
  install -Dm755 geoip.dat "$pkgdir"/usr/lib/v2ray/geoip.dat
  install -Dm644 "$GOPATH"/src/github.com/v2ray/geoip/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
