# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Hengfu <rhfcaesar@gmail.com>

pkgname=xl2tpd
pkgver=1.3.1
pkgrel=1
pkgdesc="an open source implementation of the L2TP maintained by Xelerance Corporation"
arch=('i686' 'x86_64')
url="http://www.xelerance.com/services/software/xl2tpd/"
license=('GPL')
depends=('ppp')
options=(emptydirs zipman docs)
backup=(etc/ppp/options.l2tpd)
source=($pkgname-$pkgver.tar.gz::https://github.com/xelerance/xl2tpd/archive/v$pkgver.tar.gz
	xl2tpd.rc
	xl2tpd.conf.client-example
	xl2tpd.conf.server-example
	options.l2tpd
	xl2tpd.service
	xl2tpd.tmpfiles
	ipparam.patch)
md5sums=('38c2461d3562f98b08454ca403706886'
         '1439d13084fc09c2a19c97b24a834345'
         '5f0ae07026e61a3dd15a76fd4144a6d5'
         '16ed0886115d4dcc21072cfb847250bb'
         'ed57eb67335cdf246251285dad502cc7'
         '5ba9ddaf66c9827fe4a2caef67cf61c2'
         '2e95e4fc03e1521ab20fd7e249efab5a'
         '4d258a58cc109d5c4d96af8f81d481cf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 <$srcdir/ipparam.patch

  make KERNELSRC=/usr/src/linux-`uname -r` PREFIX=/usr
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make KERNELSRC=/usr/src/linux-`uname -r` PREFIX=/usr DESTDIR=$pkgdir install

  install -D -m0755 $srcdir/xl2tpd.rc $pkgdir/etc/rc.d/xl2tpd
  install -D -m0644 $srcdir/xl2tpd.conf.client-example $pkgdir/etc/xl2tpd/xl2tpd.conf.client-example
  install -D -m0644 $srcdir/xl2tpd.conf.server-example $pkgdir/etc/xl2tpd/xl2tpd.conf.server-example
  install -D -m0644 $srcdir/options.l2tpd $pkgdir/etc/ppp/options.l2tpd

  install -Dm0644 $srcdir/xl2tpd.service $pkgdir/usr/lib/systemd/system/xl2tpd.service
  install -Dm0644 $srcdir/xl2tpd.tmpfiles $pkgdir/usr/lib/tmpfiles.d/xl2tpd.conf
}
