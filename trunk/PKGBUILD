# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-configargparse
pkgname=('python-configargparse' 'python2-configargparse')
pkgver=0.14.0
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-yaml' 'python2-yaml' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bw2/ConfigArgParse/archive/$pkgver.tar.gz")
sha512sums=('e48ca61027abf0e0acb8414b6d89b276b186b4f714368b8c33975537c2264bb2d81f18cf3013b99d9ce752014361afa973f704b3f6957ca4b98319bb0ca5ca8c')

prepare() {
  cp -a ConfigArgParse-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ConfigArgParse-$pkgver
  python setup.py build

  cd "$srcdir"/ConfigArgParse-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ConfigArgParse-$pkgver
  python setup.py test || warning 'Ignoring TestWrappingMetavar failure'

  cd "$srcdir"/ConfigArgParse-$pkgver-py2
  python2 setup.py test
}

package_python-configargparse() {
  depends=('python')
  optdepends=('python-yaml: for YAML support')

  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-configargparse() {
  depends=('python2')
  optdepends=('python2-yaml: for YAML support')

  cd ConfigArgParse-$pkgver-py2

  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
