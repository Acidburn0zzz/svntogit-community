# Maintainer: Justin Kromlinger <hashworks@archlinux.org>

pkgname=opensearch-asynchronous-search-plugin
pkgver=1.2.0.0
pkgrel=1
pkgdesc="OpenSearch Asynchronous Search Plugin"
arch=('x86_64')
url="https://opensearch.org/docs/latest/search-plugins/async"
license=('Apache')
depends=('opensearch')
makedepends=('java-environment=11' 'unzip')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/opensearch-project/asynchronous-search/archive/${pkgver}.tar.gz"
)
sha256sums=('cafc8dbb74eb712d59b3c6b3052b03d5dcb5c9e31bfcde1a4ef9c75c34081fb6')

build() {
  cd "asynchronous-search-${pkgver}"
  export PATH=/usr/lib/jvm/default/bin:$PATH
  export GRADLE_OPTS="-Dbuild.snapshot=false"
  # integTest (Reaper) requires JDK 14
  ./gradlew assemble \
    --exclude-task ":integTest" \
    --exclude-task ":jacocoTestReport"
}

package() {
  install -dm755 "${pkgdir}/usr/share/opensearch/plugins/opensearch-asynchronous-search"
  cd "${pkgdir}/usr/share/opensearch/plugins/opensearch-asynchronous-search"
  unzip "${srcdir}/asynchronous-search-${pkgver}/build/distributions/opensearch-asynchronous-search-${pkgver}.zip"

  install -Dm644 "${srcdir}/asynchronous-search-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
