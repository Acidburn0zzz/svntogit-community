# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Simo Leone <neotuli@gmail.com>

pkgname=darcs
pkgver=2.8.4
pkgrel=3
pkgdesc="Decentralized replacement for CVS with roots in quantum mechanics"
arch=(i686 x86_64)
url="http://darcs.net/"
license=('GPL')
depends=('curl' 'gmp' 'ncurses>=5.6-7')
makedepends=('ghc' 'haskell-tar' 'haskell-text<0.12'
    'haskell-hashed-storage<0.6' 'haskell-haskeline' 'haskell-random'
	'haskell-vector'
    'haskell-html<1.1' 'haskell-parsec<3.2' 'haskell-regex-compat')
source=("http://darcs.net/releases/$pkgname-$pkgver.tar.gz")
md5sums=('7456b7957add1b04c49de1b72c137a77')

build(){
    cd "$srcdir/$pkgname-$pkgver"
    runhaskell Setup.lhs configure --ghc -O --prefix=/usr \
         --disable-library-for-ghci --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup.lhs build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    runhaskell Setup.lhs copy --destdir="$pkgdir"
    rm -r "$pkgdir/usr/lib/"
    chmod 755 "$pkgdir/usr/share/man/man1/$pkgname.1"
}
