# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>

pkgname=python-fuse
pkgver=1.0.1
pkgrel=1
arch=('x86_64')
pkgdesc="Python bindings for FUSE"
url="https://github.com/libfuse/python-fuse"
license=('LGPL')
makedepends=('python-setuptools' 'fuse2')
depends=('fuse2' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/libfuse/python-fuse/archive/v$pkgver.tar.gz")
sha512sums=('d87f16b6a7114421d04d9f73895e2fb87ea5620f06c10fd704f6230ce66d2508ae152d0b0929e666e89048e7cad1e1917d23acb83d26ecf7ae7f375c50b28e93')

build() {
  cd python-fuse-$pkgver
  python setup.py build
}

package() {
  cd python-fuse-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}
