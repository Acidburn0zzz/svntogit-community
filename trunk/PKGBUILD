# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.005
pkgrel=1
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('d219971a636a2404c903f5fc25db9973deae21a6e9e48bdee22428bc1b9ca2525cde827dc0f2039bc56a26d994566614eabb7765c9387f9fac5e856a9407a53a')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
