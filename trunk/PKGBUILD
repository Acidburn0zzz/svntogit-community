# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jeff Mickey <j@codemac.net>

pkgname=wtf
pkgver=20180621
pkgrel=1
pkgdesc="Acronym dictionary, with additions from NetBSD and wikipedia"
arch=('any')
url="http://www.mu.org/~mux/wtf/"
license=('BSD')
makedepends=('git')
#source=("http://www.mu.org/~mux/${pkgname}/${pkgname}-${pkgver}.tar.gz"
source=("git+https://github.com/void-linux/netbsd-wtf#commit=b1e5be48"
        "https://raw.github.com/keenerd/wtf/0623381d92b2c5d3e94b00c9eb06fb5d4d85d014/acronyms.computing"
        'LICENSE')
md5sums=('SKIP'
         '3d99862438ec7977ff8d7f4e3e5dd300'
         'abac213cf24e9d5cab4e8115441eb717')

build() {
  cd "$srcdir/netbsd-wtf"
  make PREFIX='/usr'
}

package() {
  cd "$srcdir/netbsd-wtf"
  make install PREFIX='/usr' DESTDIR="$pkgdir"

  install -m644 ../acronyms.computing "$pkgdir/usr/share/wtf/"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
