# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=2.0.0
pkgrel=2
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
provides=('ukui-indicators')
replaces=('ukui-indicators')
depends=('gsettings-qt' 'kwindowsystem' 'qt5-webkit' 'libqtxdg' 'ukui-biometric-auth' 'ukui-menu' 'ukui-menus')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('ccc19dafca4d9c0ac4e59a45f689d6f75dca1b5a7a7d3baae2b76defa78c0fe82eee306ff165fa731afa4cda0d71f6b91feb4e284995f4810820707efe6efc22')

prepare() {
  mkdir -p build
  sed -i 's|../../panel/common/ukuitranslator.h|../panel/common/ukuitranslator.h|' $pkgname-$pkgver/cmake/ukui-build-tools/modules/*.cpp.in
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
