# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: farid <farid at archlinuc-br.org>
# Contributor: Archie <Mymaud@gmail.com>

pkgbase=gmic
pkgname=(gmic gimp-plugin-gmic)
pkgver=1.4.8.3
pkgrel=1
pkgdesc="GMIC image procession framework"
arch=('i686' 'x86_64')
license=('custom:CeCILL')
url="http://gmic.sourceforge.net"
makedepends=('gimp' 'fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
options=('docs' '!emptydirs')
source=("http://downloads.sourceforge.net/sourceforge/gmic/gmic_$pkgver.tar.gz")
md5sums=('c8921256f64af774860a8f9021dc6457')

build() {
  cd "$srcdir/gmic-$pkgver"
  CFLAGS+=" -Dcimg_use_lapack"
  LDFLAGS="-llapack"
  sed -i "s#-lhighgui#-lopencv_core -lopencv_imgproc -lopencv_highgui -lopencv_ml -lopencv_video -lopencv_features2d -lopencv_calib3d -lopencv_objdetect -lopencv_contrib -lopencv_legacy -lopencv_flann#" configure
  sed -i "s#-lcv#-lopencv_core -lopencv_imgproc -lopencv_highgui -lopencv_ml -lopencv_video -lopencv_features2d -lopencv_calib3d -lopencv_objdetect -lopencv_contrib -lopencv_legacy -lopencv_flann#" configure
  ./configure --prefix=/usr
  sed -i 's|OPENCV_CFLAGS = .*|OPENCV_CFLAGS = -Dcimg_use_opencv -I$(USR)/include/opencv `pkg-config --cflags opencv`|' src/Makefile
  sed -i 's|OPENCV_LDFLAGS = .*|OPENCV_LDFLAGS = `pkg-config --libs opencv`|' src/Makefile
  sed -i 's|cp -f gmic_gimp.*||' src/Makefile
  make -C src all
}

package_gmic() {
  depends=('fftw' 'lapack' 'opencv' 'graphicsmagick' 'openexr')
  replaces=('greycstoration')

  cd "$srcdir/gmic-$pkgver"
  make -C src install DESTDIR="$pkgdir" USR=/usr
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gmic/LICENSE"
}

package_gimp-plugin-gmic() {
  pkgdesc="Gimp plugin for the GMIC image procession framework"
  depends=('gimp' 'fftw' 'lapack')
  replaces=('gimp-plugin-greycstoration' 'gimp-plugin-gmic4gimp')

  cd "$srcdir/gmic-$pkgver"
  install -Dm755 src/gmic_gimp "$pkgdir/usr/lib/gimp/2.0/plug-ins/gmic_gimp"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/gimp-plugin-gmic/LICENSE"
}
