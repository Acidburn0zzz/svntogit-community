# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxi
pkgname=lib32-$_pkgbasename
pkgver=1.3.2
pkgrel=3
pkgdesc="X11 Input extension library (32-bit)"
arch=('x86_64')
url="http://xorg.freedesktop.org"
depends=('lib32-libxext>=1.1.2' 'inputproto>=2.0' $_pkgbasename)
makedepends=('pkgconfig' gcc-multilib)
options=(!libtool)
license=('custom')
source=(${url}/releases/individual/lib/libXi-${pkgver}.tar.bz2)
sha1sums=('50fdac374d45feb4fe39938d3530bd4f314f3a85')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/libXi-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXi-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
