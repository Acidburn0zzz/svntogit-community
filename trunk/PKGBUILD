# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>
# Co-Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161

_pkgname=python-progressbar
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=3.42.0
pkgrel=1
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=("any")
depends=('python' 'python-utils')
makedepends=('python' 'python-setuptools' 'python-utils')
source=("${_pkgname}-${pkgver}.tar.xz::https://github.com/WoLpH/python-progressbar/releases/download/v${pkgver}/python-progressbar-v${pkgver}.tar.xz"
        "${_pkgname}-${pkgver}.tar.xz.asc::https://github.com/WoLpH/python-progressbar/releases/download/v${pkgver}/python-progressbar-v${pkgver}.tar.xz.asc")
sha512sums=('fefcf9c211413b108de4aac18470a2335a55cf01aca29df01d9a92746320efc656b63d837af9d90fcf4c3078c4af5b451048be1017b0a2af9843f5311bd355f0'
            'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
    cd "${srcdir}/${_pkgname}-v${pkgver}"
    python setup.py build
}

package() {
    cd "${srcdir}/${_pkgname}-v${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
