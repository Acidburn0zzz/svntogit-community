# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=fscrypt
pkgver=0.2.5
pkgrel=1
pkgdesc='A tool for managing Linux filesystem encryption'
arch=('x86_64')
url='https://github.com/google/fscrypt'
license=('Apache')
makedepends=('go')
depends=('pam')
source=("fscrypt-$pkgver.zip::https://github.com/google/fscrypt/archive/v$pkgver.zip"
        "pam_config")
sha256sums=('7ec0dfe6d28079829dfd52bde20821d96957b3399fa47b3b2f4b7ae39b977dc7'
            'ae6ceaefc6d936c95a9b7a3f925111ffb946e6fd0152373247f1d40132f05aef')

prepare() {
  cd "fscrypt-$pkgver"
  # TODO: replace it with $SOURCE_DATE_EPOCH?
  sed "s/\$(shell date)//g" -i Makefile
}

build() {
  cd "fscrypt-$pkgver"
  make
}

package() {
  cd "fscrypt-$pkgver"
  install -Dm755 -t "$pkgdir/usr/bin/" bin/fscrypt
  install -Dm755 -t "$pkgdir/usr/lib/security/" bin/pam_fscrypt.so
  install -Dm644 -t "$pkgdir/usr/share/licenses/fscrypt/" LICENSE
}
