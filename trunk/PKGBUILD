# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=1.3.7
pkgrel=2
pkgdesc='Deepin Image Viewer'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-qt5integration' 'libexif' 'freeimage' 'libraw' 'qt5-svg' 'deepin-shortcut-viewer')
makedepends=('qt5-tools')
optdepends=('deepin-manual: view the help manual')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz"
        fix-crash.patch)
sha512sums=('ad297d2bccdff89d9cc9e2f80c11936bd3910c8dcca72ca88118d2562cfe006944f09a212659efc6eedccc42c14dd5eac19488a05c572b4cab0bd646b8ab9970'
            '16171440fdb3a430d21406431aab2140d5a1bdde864f815149eca4c03bb4f45a93b9a4936e962826f627793c544b8538723e8087342cebc786daa055db025626')

prepare() {
  cd deepin-image-viewer-$pkgver
  patch -p1 -i ../fix-crash.patch
}

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
