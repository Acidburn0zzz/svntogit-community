# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: jiangxq <jiangxueqian at gmail dot com>
# Contributor: zh99998 <zh99998@gmail.com>
# Contributor: 4679kun <admin at 4679 dot us>

pkgname=simple-obfs
pkgver=0.0.4
pkgrel=1
pkgdesc='A simple obfusacting tool designed as plugin server of shadowsocks'
arch=('i686' 'x86_64')
url='https://github.com/shadowsocks/simple-obfs'
license=('GPL')
depends=('c-ares' 'libev' 'libcork')
makedepends=('asciidoc' 'xmlto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/shadowsocks/simple-obfs/archive/v$pkgver.tar.gz")
sha512sums=('ab0568ce0d793675f3c8266efe30c809f8a5b2c21d795a31a29cc03ecc8d5196dc118eb43ba8d68d6bd4de2398664c4cd73abc7aa422ea06a8468a4abba2c4d2')

prepare() {
  cd simple-obfs-$pkgver
  patch -p1 -i debian/patches/0001-Use-libcork-dev-in-system.patch
}

build() {
  cd simple-obfs-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --enable-system-shared-lib
  make
}

package() {
  cd simple-obfs-$pkgver
  make DESTDIR="$pkgdir/" install
}
