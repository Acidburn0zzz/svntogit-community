# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Contributor: Ian D. Scott <ian@perebruin.com>
# Contributor: Tony Crisci <tony@dubstepdish.com>
pkgname=playerctl
pkgver=0.5.0
pkgrel=2
pkgdesc="mpris media player controller and lib for spotify, vlc, audacious, bmp, xmms2, and others."
arch=('i686' 'x86_64')
url="https://github.com/acrisci/playerctl"
license=('LGPL3')
depends=('glib2' 'gobject-introspection')
optdepends=('python-gobject: python bindings')
makedepends=('autoconf' 'gtk-doc')
source=("playerctl-${pkgver}.tar.gz::https://github.com/acrisci/playerctl/archive/v${pkgver}.tar.gz")
sha512sums=('307cad5eeea7efa0c97c885660f49468f442fe907ef2495b56ac512e0551c7248750897b6d00934cbfcfcb35f486a101a238b276ef71d2ef866132ca3cc0932c')

build() {
    cd "${srcdir}/playerctl-${pkgver}"
    NOCONFIGURE=true ./autogen.sh
    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}/playerctl-${pkgver}"
	  make DESTDIR="${pkgdir}/" install
}
