# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Aoibhinn Nic Aoidh <oibindnicaoidh@gmail.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: J. W. Birdsong <jwbirdsong@gmail.com>

pkgname=luakit
pkgver=2.1
pkgrel=2
pkgdesc='Browser framework based on Webkit and Lua'
arch=(x86_64)
url=https://luakit.github.io/
license=(GPL3)
depends=(
  lua51-filesystem
  luajit
  webkit2gtk
)
makedepends=(
  git
  help2man
)
backup=(
  etc/xdg/luakit/rc.lua
  etc/xdg/luakit/theme.lua
)
source=(git+https://github.com/luakit/luakit.git#tag=${pkgver})
sha256sums=(SKIP)

build() {
  make DEVELOPMENT_PATHS=0 PREFIX=/usr USE_LUAJIT=1 -C luakit
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C luakit install
}

# vim: ts=2 sw=2 et:
