# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce
pkgver=1019
pkgrel=1
pkgdesc='NEC PC Engine/Supergrafx/CD core'
arch=(x86_64)
url=https://github.com/libretro/beetle-pce-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
)
makedepends=(git)
source=(libretro-beetle-pce::git+https://github.com/libretro/beetle-pce-libretro.git#commit=a54a259bb6f26ffca438a2e2ddda27a67d424981)
sha256sums=(SKIP)

pkgver() {
  cd libretro-beetle-pce

  git rev-list --count HEAD
}

build() {
  make -C libretro-beetle-pce
}

package() {
  install -Dm 644 libretro-beetle-pce/mednafen_pce_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
