# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything
pkgver=0.0.1
pkgrel=2
pkgdesc="Deepin Anything file search tool"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('dkms')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz"
        linux-4.17.patch)
sha512sums=('6a210f2b3797647b0bf280ecd97b2d71ff6c882d9cd8d8daadf141b2125c5ade3af4f27086be3c6e68e3f823c0acb4753afbbadcee2cf82b0d18559409b7d64d'
            'fe951583cf18401eef4b8f85c1113dee9de50633da059ad9e0f8f887c9cd8c4a80998bc16d66438efe7ccf8ca524426bf980aa00e4c4b840d1ced10c72da32c9')

prepare() {
  cd deepin-anything-$pkgver
  patch -p1 -i ../linux-4.17.patch
}

build() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver
}

package() {
  cd deepin-anything-$pkgver
  make VERSION=$pkgver DESTDIR="$pkgdir" install

  cp debian/deepin-anything-dkms.dkms "$pkgdir"/usr/src/deepin-anything-$pkgver/dkms.conf
}
