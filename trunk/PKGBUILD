# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-control-center
pkgver=2.0.2
pkgrel=1
pkgdesc="Utilities to configure the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-control-center"
groups=('ukui')
depends=('kconfig' 'kconfigwidgets' 'ki18n' 'kscreen' 'kwidgetsaddons' 'peony' 'ukui-interface'
         'ukui-panel' 'ukui-media' 'ukui-menus' 'ukui-power-manager' 'ukui-screensaver'
         'ukui-settings-daemon' 'upower')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-control-center/archive/v$pkgver.tar.gz"
        fix-build.patch)
sha512sums=('223f952a7cfdafaf1105b078af542c1774198500c645b7bc7611069ee16e75b08e3be377a88fdb3e0fff440348f63d3269c85fc0c0ad19fc99de9e0c5b56b3e5'
            '333ee4084e2c658392ef8d3a09132e59c5419030c10024ef9ea70784e1ace25668464d12089b9986e3bbeea1106ec21380a5881c0d4df40553a512bfb634b3c8')

prepare() {
  cd ukui-control-center-$pkgver
  # https://github.com/ukui/ukui-control-center/issues/36
  patch -p1 -i ../fix-build.patch
}

build() {
  cd ukui-control-center-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.service -t "$pkgdir"/usr/share/dbus-1/system-services/
  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.conf -t "$pkgdir"/usr/share/dbus-1/system.d/
}
