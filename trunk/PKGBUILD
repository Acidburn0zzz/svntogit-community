# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname='matrix-appservice-irc'
pkgver=0.11.2
pkgrel=1
pkgdesc="Node.js IRC bridge for Matrix"
arch=('any')
url="https://github.com/matrix-org/matrix-appservice-irc"
license=('Apache')
depends=('nodejs')
makedepends=('npm' 'git')
optdepends=('python: for maintenance scripts')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('9d1f56254c5903cb2bd55c9564b8eaed4415f00377527b00184cb148fbb136c9')

package() {
  cd ${pkgbase}-${pkgver}
  npm install --cache "${srcdir}"/npm-cache -g --user root --prefix "${pkgdir}"/usr

  # Apparently it now symlinks to the source folderâ€¦
  rm -f "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  mkdir -p "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  cp -r * "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc/
  chmod -R go-w "${pkgdir}"/usr/lib/node_modules/
  chmod go-w "${pkgdir}"/usr/bin
}
