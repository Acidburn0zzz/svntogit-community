# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Lucas Hoffmann
# Contributor: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Kazuo Teramoto <kaz.rag at gmail.com>

pkgname=afew
pkgver=1.3.0
pkgrel=4
pkgdesc='Initial tagging script for notmuch mail'
arch=(any)
url=https://github.com/afewmail/afew
license=('custom:BSD')
depends=('notmuch' 'python-chardet' 'python-dkim' 'python-setuptools')
makedepends=('python-sphinx')
source=("https://files.pythonhosted.org/packages/source/a/afew/afew-$pkgver.tar.gz")
sha512sums=('2e15dc2aa6032b14c3fed57cb5912e3c3471592586f5fa40991b2a007d8a9579c0e1f69e455e1405923b93de64b9b8c126b5ccf8da72144528c57108beea7370')

build() {
  cd afew-$pkgver
  python setup.py build
  make -C docs man
}

check() {
  cd afew-$pkgver
  python -m unittest discover afew/tests
}

package() {
  cd afew-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 docs/build/man/afew.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/afew LICENSE
}

# vim:set ts=2 sw=2 et:
