# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-podcasts
pkgver=0.4.6
pkgrel=2
pkgdesc="Podcast application for GNOME"
arch=(x86_64)
url="https://gitlab.gnome.org/World/podcasts"
license=(GPL3)
depends=(gst-plugins-bad gst-plugins-base gst-plugins-good libhandy)
makedepends=(git meson rust)
optdepends=('gst-plugins-ugly: Extra media codecs'
            'gst-libav: Extra media codecs')
_commit=885b796f855b659c2d3dda1b7f7e22e7e726ff43  # tags/0.4.6
source=("git+https://gitlab.gnome.org/World/podcasts.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd podcasts
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd podcasts
  arch-meson . build
  ninja -C build
}

check() {
  cd podcasts
  meson test -C build
}

package() {
  cd podcasts
  DESTDIR="$pkgdir" meson install -C build
}
