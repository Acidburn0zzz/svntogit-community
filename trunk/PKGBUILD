# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jachym Barvinek <jachymb@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=swi-prolog
pkgver=8.0.1
pkgrel=1
pkgdesc='Prolog environment'
arch=('x86_64')
url='https://swi-prolog.org/'
license=(GPL LGPL)
depends=(gmp libarchive openssl readline)
makedepends=(cmake libjpeg libxft libxinerama libxpm ninja unixodbc)
optdepends=('unixodbc:    for using the odbc4pl library'
            'libjpeg:     for using the pl2xpce library'
            'libxpm:      for using the pl2xpce library'
            'libxinerama: for using the pl2xpce library'
            'libxft:      for using the pl2xpce library')
# invalid HTTPS cert
source=("http://www.swi-prolog.org/download/stable/src/swipl-${pkgver/_/-}.tar.gz")
sha256sums=('e42c703ce373f6435ab917989a7827a041d131cf79300311bb4c8ef188f1092f')

build() {
  mkdir -p build
  cd build
  cmake ../swipl-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_C_FLAGS="$CFLAGS -fPIC -w" \
    -DVALA_CFLAGS="$CFLAGS -fPIC -w" \
    -G Ninja
  ../swipl-$pkgver/scripts/pgo-compile.sh
  ninja
}

check() {
  cd build

  # the zlib test fails
  ctest -j 8 | true
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# getver: swi-prolog.org/download/stable
# vim: ts=2 sw=2 et:
