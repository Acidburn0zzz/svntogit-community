# Maintainer: Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Frederik "Freso" S. Olesen <archlinux@freso.dk>
# Contributor: Bastien Traverse <firstname at lastname dot email>
# Contributor: Samantha Baldwin <fuhsaz+git@cryptic.li>
# Contributor: Caleb Reach <jtxx000@gmail.com>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: Karol "Kenji Takahashi" Woźniak <kenji.sx>
# Contributor: Mantas Mikulėnas <grawity@gmail.com>

pkgname=whipper
pkgver=0.8.0
pkgrel=1
pkgdesc="Unix CD ripper aiming for accuracy over speed -- forked from morituri"
arch=(x86_64)
url="https://github.com/whipper-team/whipper"
license=(GPL3)
depends=(
    libcdio-paranoia              # for the actual ripping
    cdrdao                        # for session, TOC, pregap, and ISRC extraction
    libsndfile                    # for reading wav files (pulls in flac, also required for reading flac files)
    python2-gobject
    python2-mutagen               # for metadata handling
    python2-musicbrainzngs        # for metadata lookup
    python2-pycdio                # for storing drive identification in config file
    python2-requests
    python2-setuptools            # for plugin support
    sox                           # for track peak detection'
    python2-ruamel.yaml           # for log output
    )
makedepends=(python2-setuptools-scm git)
optdepends=(flac)
checkdepends=(python2-twisted)
conflicts=(morituri accuraterip-checksum)
provides=(accuraterip-checksum)
replaces=(accuraterip-checksum)
_tag=bf381b51f46735d1142c8b9c427cbd5b38ec31fd # git rev-parse v${pkgver}
source=(git+${url}.git#tag=${_tag})
sha512sums=(SKIP)

build() {
    cd ${pkgname}
    python2 setup.py build
}

check() {
    cd ${pkgname}
    # https://github.com/whipper-team/whipper/issues/420
    sed -i 's|\\\.dev\[\\w\\\.\\+\]+|(\\.dev[\\w\\.+]+)?|' whipper/test/test_result_logger.py
    # The accuraterip C extension needs to be in the python path
    PYTHONPATH="build/lib.linux-${CARCH}-2.7/" python2 -m unittest discover
    # https://github.com/whipper-team/whipper/issues/422
    git checkout whipper/test/test_result_logger.py
}

package() {
    cd ${pkgname}
    python2 setup.py --version
    python2 setup.py install --root="${pkgdir}"/ --optimize=1 --skip-build
}
