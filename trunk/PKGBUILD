# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.2pl2
pkgrel=1
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
depends=('gtk2')
makedepends=('ocaml' 'lablgtk2' 'camlp5-transitional' 'netpbm')
source=("http://coq.inria.fr/distrib/V${pkgver}/files/${pkgname}-${pkgver}.tar.gz")
md5sums=('0fa1bcce3ab83d95571d2094f5091e6b')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -docdir '/usr/share/doc' \
    -opt \
    -coqide

  make world
}

package() {
  cd ${pkgname}-${pkgver}

  make COQINSTALLPREFIX=${pkgdir} install
}
