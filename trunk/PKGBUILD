# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=libmirage
pkgver=3.2.5
pkgrel=2
pkgdesc="CD-ROM image (B6T/C2D/CCD/CDI/CIF/CUE/ISO/MDS/MDX/NRG/TOC) access library"
arch=(x86_64)
url="https://cdemu.sourceforge.io/"
license=(GPL)
depends=(glib2 libsndfile libsamplerate zlib bzip2 xz shared-mime-info)
makedepends=(gtk-doc gobject-introspection cmake intltool)
source=(https://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.xz
        fix-fwupd.diff)
sha256sums=('d50d97e15f180f99fddcdf388b0d9fec073fb8fe080ba6c69cd3674214141139'
            '3859903af5700f2536bfe4758f5176e3fc962a290d955a23526c342fa46a10d3')

prepare() {
  cd $pkgname-$pkgver
  
  # remove overly broad mime magic that breaks fwupd
  # https://github.com/cdemu/cdemu/issues/20
  patch -Np1 -i ../fix-fwupd.diff
}

build() {
  cmake -S $pkgname-$pkgver -B build \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DPOST_INSTALL_HOOKS:BOOL=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
