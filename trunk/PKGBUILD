# Maintainer: Dan Printzell <arch@vild.io>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: dsboger <https://github.com/dsboger>
# Contributor: Carl George < arch at cgtx dot us >

pkgname=tilix
pkgver=1.9.0
pkgrel=2
pkgdesc="A tiling terminal emulator for Linux using GTK+ 3"
arch=('x86_64')
url="https://github.com/gnunn1/tilix"
license=('MPL')
depends=('libx11' 'gtkd' 'vte3' 'dconf' 'gsettings-desktop-schemas')
makedepends=('ldc' 'po4a')
optdepends=('python-nautilus: for "Open Tilix Here" support in nautilus'
            'libsecret: for the password manager')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('192b86776b0e8e89fe3963a4c6b54884dc1909f762758390a407c4de744ab3f0')

prepare() {
    cd "$pkgname-$pkgver"

    ./autogen.sh
}

build() {
    cd "$pkgname-$pkgver"

    ./configure \
    --prefix=/usr \
    PO4A_TRANS=/usr/bin/vendor_perl/po4a-translate \
    DC='ldmd' \
    DCFLAGS='-O -inline -release -version=StdLoggerDisableTrace -L='"\"$LDFLAGS\""
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}

