# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.9.0
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
url=https://github.com/lotabout/skim
license=('MIT')
depends=('ncurses')
makedepends=('rust')
optdepends=('bash: for resp. completions and key bindings, and for sk-tmux script'
            'fish: for resp. key bindings'
            'tmux: for sk-tmux script'
            'vim-plugin-runtime: for N/Vim plugin'
            'zsh: for resp. completions and key bindings')
source=("skim-$pkgver.tar.gz::https://static.crates.io/crates/skim/skim-$pkgver.crate")
sha512sums=('7b29ac9bbe4006981dd6d678d0a04237e2c1f221bdba2dc37a6d9957b3b549a274766e08459741654314d62dc13fff85d437f8758ee584867b3e20ec3ae0b9b8')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}

# vim:set ts=2 sw=2 et:
