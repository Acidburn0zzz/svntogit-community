# Maintainer : Christian Rebischke <Chris.Rebischke@archlinux.org>
# Maintainer : Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: lucke <lucke at o2 dot pl>

pkgname=weechat
pkgver=2.8
pkgrel=4
pkgdesc="Fast, light and extensible IRC client (curses UI)"
arch=('x86_64')
url="https://www.weechat.org/"
license=('GPL')
depends=('gnutls' 'curl' 'libgcrypt')
makedepends=('asciidoctor' 'cmake' 'aspell' 'guile' 'lua' 'perl' 'python'
             'ruby' 'tcl')
optdepends=('aspell: spellchecker support'
            'guile: support for guile scripts'
            'lua: support for lua scripts'
            'perl: support for perl scripts'
            'python: support for python scripts'
            'ruby: support for ruby scripts'
            'tcl: support for tcl scripts')
source=("https://www.weechat.org/files/src/${pkgname}-${pkgver}.tar.xz"{,.asc}
        "weechat-cmake-datarootdir.patch::https://github.com/weechat/weechat/commit/683464626d.patch"
        "weechat-fix-bugged-sharedir.patch::https://github.com/weechat/weechat/commit/973a4b2e77.patch")
sha512sums=('3bebb42118fca6b601f1de8f4d52378619a10e5c490f5b46f86d27a9680f00a56d48a1ade729e78d965cae589e6c80647c22414393000c7d3cfcc94a1e41c4df'
            'SKIP'
            '973990a496370c35a4aaf607e660dd28b6bfcd561d7f354f551ad59a1ec84ccb77ad6cc09a2389fa65d5b4059dc1a544b08dc26c02f3d3d4f584c62759109c21'
            '75ad367745a08805f11835f5b451ead8220b7940cf2f792ad77bda017ef25d987d098b111ffd423c38b5f891b13fb45ff688fdda646e77308600e466d4e858da')
b2sums=('756ce1719ecaa351a2780641758bb7587b45b8e6b16e5a82833009ddb27f71783a461cf89662e04118c75e2d372103eda5a11c98feb62d624b330b223bf22ede'
        'SKIP'
        '158300f1bd5b36c3689808a2198dda5ba5471943e32047622045bd14722b2628d9092386c691a8ebc17ab2775db288a120676ca701388071fc2712d67667f95d'
        'e39fd2dbeecce7821f4e28d502ed72238ff0f607b7ec8b39a1a6ac23fd0e64962b7db3ece145d50908ed967370475a2916a3943decb4c7ddc3acf2692ae36cc2')
validpgpkeys=('A9AB5AB778FA5C3522FD0378F82F4B16DEC408F8') # WeeChat (signing key) <webmaster@weechat.org>

# cmake does not correctly handle CPPFLAGS, so kludge it in anyway:
# https://gitlab.kitware.com/cmake/cmake/-/issues/12928
CFLAGS+=" ${CPPFLAGS}"

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # https://github.com/weechat/weechat/pull/1461
  # make /usr/share/weechat work, e.g. community/weechat-matrix
  patch -p1 -i ../weechat-cmake-datarootdir.patch
  patch -p1 -i ../weechat-fix-bugged-sharedir.patch
}

build() {
  mkdir -p build
  cd build
  cmake -Wno-dev "${srcdir}/${pkgname}-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DENABLE_MAN=ON \
        -DENABLE_DOC=ON \
        -DENABLE_JAVASCRIPT=OFF \
        -DENABLE_PHP=OFF
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
