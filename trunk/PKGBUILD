# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgbase=python-pam
pkgname=(python-pam python2-pam)
pkgver=1.8.3
pkgrel=1
pkgdesc=" Pure Python interface to the Pluggable Authentication Modules system on Linux"
arch=('any')
url="https://github.com/FirefighterBlu3/${pkgbase}"
license=('MIT')
depends=('pam')
makedepends=('python-setuptools' 'python2-setuptools')
# only available as a wheel?
#source=("https://files.pythonhosted.org/packages/source/${pkgbase:0:1}/${pkgbase}/${pkgbase}-${pkgver}.tar.gz")
_commit=7f71db2c4e63e317cb607cd5353b4ec176d7a137
source=("${url}/archive/${_commit}.tar.gz")
sha256sums=('a914e4c39e461c694630ac457bea1426d097f9a9f0cb5514591ef5d9bb864ef0')
validpgpkeys=('766460A814AE0D0066C3EE922AC27DE01E1DFAD6') # David Ford <david@blue-labs.org>

build() {
	cd "${srcdir}/${pkgbase}-${_commit}"
	python setup.py build
	python2 setup.py build
}

package_python-pam() {
    depends+=('python')
	cd "${srcdir}/${pkgbase}-${_commit}"

	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-pam() {
    depends+=('python2')
	cd "${srcdir}/${pkgbase}-${_commit}"

	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
