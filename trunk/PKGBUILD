# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.10.0
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=('x86_64')
url="https://amsynth.github.io/"
license=('GPL2')
groups=('dssi-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'fontconfig' 'gcc-libs' 'gdk-pixbuf2' 'glibc')
makedepends=('atk' 'dssi' 'freetype2' 'glib2' 'gtk2' 'harfbuzz' 'jack' 'ladspa'
'liblo' 'lv2' 'intltool' 'pandoc')
checkdepends=('lv2lint')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'non-session-manager: for session management with the standalone application'
            'oscpack: for non-session-manager support of standalone application')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/amsynth/amsynth/archive/release-${pkgver}.tar.gz")
sha512sums=('951a74d10e4669b0b4bff5d02f12a1dcb62dbb2b04044174554df8ff30ff2f6b5965af922943b13bbafb969cc2ae78eade394e98aca7df8fd8c27feacbe64605')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  intltoolize -f
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
    --with-alsa \
    --with-jack \
    --with-dssi \
    --with-lv2 \
    --with-vst \
    --with-pandoc
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  cp .libs/${pkgname}_lv2*.so data/${pkgname}.lv2/
  lv2lint -Mpack \
          -I "data/${pkgname}.lv2" \
          "http://code.google.com/p/amsynth/amsynth" || echo "Known to fail: https://github.com/amsynth/amsynth/issues/159"
}

package() {
  depends+=('libatk-1.0.so' 'libfreetype.so' 'libgdk-x11-2.0.so'
  'libgio-2.0.so' 'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-x11-2.0.so'
  'libharfbuzz.so' 'liblo.so' 'libpangocairo-1.0.so' 'libpangoft2-1.0.so'
  'libpango-1.0.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
