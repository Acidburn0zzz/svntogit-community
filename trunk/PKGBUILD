# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=sleuthkit
pkgver=3.2.0
pkgrel=1
pkgdesc='File system and media management forensic analysis tools.'
arch=('i686' 'x86_64')
url='http://www.sleuthkit.org/sleuthkit'
license=('GPL2' 'CPL' 'custom:"IBM Public Licence"')
depends=('perl')
options=('!libtool')
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
md5sums=('05517963942aa92be77c05ca1c47f0de')

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --without-afflib --without-ewf --prefix=/usr
  make LDFLAGS+="-lpthread -ldl"
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # licenses
  install -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm0644 licenses/* "${pkgdir}/usr/share/licenses/${pkgname}"
}
