# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributer: Arthur Zamarin <arthurzam@gmail.com>
# Contributer: Martin Wimpress <code@flexion.org>

pkgname=blueman
pkgver=2.0.2
pkgrel=1
pkgdesc="GTK+ Bluetooth Manager"
arch=('i686' 'x86_64')
url="https://github.com/blueman-project/blueman"
license=('GPL')
depends=('bluez' 'bluez-libs' 'gtk3' 'libnotify' 'python-cairo' 'python-dbus' 'python-gobject')
makedepends=('cython' 'intltool')
optdepends=('dnsmasq: Network Access Point (NAP) support'
            'networkmanager: Dial Up Networking (DUN) and Personal Area Networking (PAN) support'
            'pulseaudio-bluetooth: audio devices support')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::https://github.com/blueman-project/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('0e393da0362102110be16d43993637c739fbfd30df4dc3dd07c2198037d58171')

prepare() {
  cd $pkgname-$pkgver

  # Fix path for bluetoothd
  sed -i 's|/usr/sbin/bluetoothd|/usr/lib/bluetooth/bluetoothd|' apps/blueman-report

  # Fix non-standard icon names
  sed -i 's/gtk-find/edit-find/' data/ui/device-list-widget.ui
  sed -i 's/mouse/input-mouse/' blueman/services/Input.py

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/$pkgname \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
