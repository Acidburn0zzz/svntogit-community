# Maintainer:  Bart≈Çomiej Piotrowski <barthalion@gmail.com>
# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.1.7
pkgrel=1
pkgdesc="Parallel implementation of the gzip file compressor"
arch=('i686' 'x86_64')
url="http://www.zlib.net/pigz/"
license=('custom')
depends=('zlib')
source=(http://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('a09e1097fdc05ac0fff763bdb4d2d5e4')

build() {
  cd "${srcdir}/$pkgname"
  sed -i s/"CFLAGS=".*/"CFLAGS=${CFLAGS}"/g Makefile 
  make
}

package() {
  cd "${srcdir}/$pkgname"

  install -Dm755 "${srcdir}"/${pkgname}/${pkgname} \
    "${pkgdir}"/usr/bin/${pkgname} 

  pushd "${pkgdir}"/usr/bin
    ln -s pigz unpigz 
  popd

  install -Dm644 "${srcdir}"/${pkgname}/${pkgname}.1 \
    "${pkgdir}"/usr/share/man/man1/${pkgname}.1
  install -Dm644 "${srcdir}"/${pkgname}/${pkgname}.pdf \
    "${pkgdir}"/usr/share/doc/${pkgname}/${pkgname}.pdf
  install -Dm644 "${srcdir}"/${pkgname}/README \
    "${pkgdir}"/usr/share/licenses/${pkgname}/README
}
