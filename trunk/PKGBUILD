# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=openshadinglanguage
pkgver=1.11.15.0
pkgrel=1
pkgdesc="Advanced shading language for production GI renderers"
arch=(x86_64)
url="https://github.com/imageworks/OpenShadingLanguage"
license=('custom')
depends=('boost-libs' 'openimageio' 'imath' 'freetype2' 'libpng'
         'libtiff' 'zlib' 'ncurses' 'clang')
makedepends=('boost' 'cmake' 'python' 'llvm' 'ninja')
optdepends=('python: the Python module')
source=($pkgname-$pkgver.tar.gz::https://github.com/imageworks/OpenShadingLanguage/archive/Release-${pkgver}.tar.gz
        llvm13.patch)
sha512sums=('d1df69349cb43690d0578b32caf92fe33ff570713fd87899e2ab312962c6474bd9c19df5b3b018a7498390e5933fcfbfdd3ecd450a4d91d64dd680ee74f492c5'
            '5131ec598a4134993876a3935cf76b27acd4a05399228b547a3f71cfeeb5462e8044c91c04b8d254fddb12b22be9dc3a9e035baa79db9c5eb086cc4c56b438eb')

prepare() {
  cd OpenShadingLanguage-Release-$pkgver

  # https://github.com/AcademySoftwareFoundation/OpenShadingLanguage/pull/1412
  patch -Np1 -i ../llvm13.patch
}

build() {
  cd OpenShadingLanguage-Release-$pkgver

  cmake \
    -B build \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_CXX_STANDARD=14 \
    -DLLVM_STATIC=0 \
    -DSTOP_ON_WARNING=OFF
  ninja -C build
}

package() {
  cd OpenShadingLanguage-Release-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install

  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}

# vim:set ts=2 sw=2 et:
