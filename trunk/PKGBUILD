# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Konrad Tegtmeier <konrad.tegtmeier+aur@gmail.com>

pkgname='hcloud'
pkgver='1.2.0'
pkgrel='5'
pkgdesc="CLI for Hetzner Cloud"
arch=('x86_64')
url='https://github.com/hetznercloud/cli'
license=('MIT')
optdepends=(
    'bash-completion: tab auto-completion'
    'zsh: tab auto-completion')
makedepends=('go' 'git')
provides=('hcloud')
_hcloud_commit='56fa3db24bafede2616a6d2c3b1e5cdca1cb851d'
source=("git+https://github.com/hetznercloud/hcloud#commit=${_hcloud_commit}")
sha512sums=('SKIP')

prepare(){
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hetznercloud
  mv "${pkgname}" src/github.com/hetznercloud
}

build(){
  cd src/github.com/hetznercloud/"${pkgname}"
  go build -o hcloud-binary
  ./hcloud-binary completion bash > "$pkgname-completion.bash"
}

check(){
  cd src/github.com/hetznercloud/"${pkgname}"
  go test -v -x
}

package(){
  cd src/github.com/hetznercloud/"${pkgname}"
  install -Dsm755 hcloud-binary "${pkgdir}/usr/bin/hcloud"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${pkgname}-completion.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
}
