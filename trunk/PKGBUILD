# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=css-parser
pkgname=python-css-parser
pkgver=1.0.5
pkgrel=1
pkgdesc="A CSS Cascading Style Sheets library for Python"
arch=('any')
url="https://github.com/ebook-utils/${_pkgname}"
license=('LGPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('3cdb6db5366fb05a7e277fbe9139cd4279afd8209f8813046c24fe4bde2c668765c06c9067ba4e9d894eabb2c33b71e7cfa6c807fe33e5c63f0394abdf489f0e'
            'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C') # Kovid Goyal (New longer key) <kovid@kovidgoyal.net>

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
