# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-user-share
pkgver=1.22.0
pkgrel=1
pkgdesc="User level public file sharing via WebDAV for MATE"
url="http://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('gtk3' 'mod_dnssd')
makedepends=('caja' 'dbus-glib' 'intltool' 'itstool' 'libcanberra' 'libnotify')
optdepends=('caja: File sharing extension')
groups=('mate-extra')
conflicts=('mate-user-share-gtk3')
replaces=('mate-user-share-gtk3')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('f07044aebccf4bb0fbbdeb4fd489437d6b3219317804a7879a433eb39dc97091')

build() {
    	cd "${pkgname}-${pkgver}"
    	PYTHON=/usr/bin/python2 ./configure \
        	--prefix=/usr \
        	--libexec=/usr/lib/${pkgname} \
        	--sysconfdir=/etc \
        	--disable-bluetooth

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make  DESTDIR="${pkgdir}" install
}
