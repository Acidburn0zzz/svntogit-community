# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cookiecutter
pkgname=python-cookiecutter
pkgver=1.7.0
pkgrel=2
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/cookiecutter/cookiecutter"
license=('BSD')
depends=('python-binaryornot' 'python-click' 'python-future' 'python-jinja'
'python-jinja-time' 'python-poyo' 'python-requests' 'python-whichcraft')
makedepends=('python-setuptools')
checkdepends=('git' 'python-freezegun' 'python-pytest' 'python-pytest-cov'
'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-1.7.0-cli_tests.patch")
sha512sums=('c57315251085ed55494d09564a43dd6348971f8b78e5755642cd64473a73b03c00a7c513f5dd05d098db9fd7a74aef90f115975386084f3df8abc08fd318922c'
            '6bac3de41862373593345f7f9face57385473784f6fff367585314bade2262c0b55564f817092f32f38b0b166a5fc2f65cfcbcca4a431719858e8e7b59637df2')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # fixing issues with click output:
  # https://github.com/cookiecutter/cookiecutter/issues/1327
  patch -Np1 -i "../${pkgname}-1.7.0-cli_tests.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
