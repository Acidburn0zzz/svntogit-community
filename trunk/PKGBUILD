# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.6
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz"
        $pkgname-update-tests-4.patch::https://github.com/lilydjwg/nvchecker/commit/4fc62d35888c3d695956793d5724a52ba3a99574.patch)
sha512sums=('adca20abcae1fcab2b87d166f18bc14d7e374de3041fcabd35eaaa8bc3d4b086ad16f9c94c5d242259011a0b1435a9c74e0d7959dffabf8da4eda6cf0694f5ee'
            'ddf02d38b38560ee8edd8b814197e015d77425d546b4f436c42bea202f1e7514766e47bfaaec9733cb6df6d416c9a68c67baf48994045f07533e0cbc707f0b7d')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-update-tests-4.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
