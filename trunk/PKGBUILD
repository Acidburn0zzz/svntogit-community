# Maintainer: David Runge <dave@sleepmap.de>
_name=Nikola
pkgname=nikola
pkgver=8.0.1
pkgrel=1
pkgdesc="A static site and blog generator"
arch=('any')
url="https://getnikola.com/"
license=('MIT')
depends=('python-babel' 'python-blinker' 'python-doit' 'python-dateutil'
'python-docutils' 'python-logbook' 'python-lxml' 'python-mako'
'python-markdown' 'python-natsort' 'python-piexif' 'python-pillow'
'python-pygments' 'python-pyrss2gen' 'python-requests' 'python-setuptools'
'python-unidecode' 'python-yapsy')
makedepends=('python-jinja' 'python-pyphen' 'python-husl' 'jupyter-notebook'
'python-ipykernel' 'python-pyaml' 'python-toml' 'python-micawber'
'python-ghp-import' 'python-phpserialize' 'python-pygal' 'python-typogrify'
'python-watchdog')
checkdepends=('python-coverage' 'python-freezegun' 'python-pytest-cov')
optdepends=('jupyter-notebook: .ipynb support'
            'python-aiohttp: for `nikola auto`'
            'python-ghp-import: for upload to Github pages'
            'python-husl: optional color support'
            'python-ipykernel: .ipynb support'
            'python-jinja: jinja2 based themes'
            'python-micawber: for embedding media in posts'
            'python-phpserialize: for Wordpress import'
            'python-pygal: for SVG graph plotting'
            'python-pyphen: hyphenation support'
            'python-pyaml: for something'
            'python-toml: .toml support'
            'python-typogrify: for typographical enhancements'
            'python-watchdog: for `nikola auto`')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('3063e762889daed50db23e8a376e072f075fae1271cc823b54782e97b20ac9a71f32360160465aa3c68b02cc4cb7b7c500cde82676ab9be002b46820a0bfb292')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # removing upper limit for python-markdown (seemingly compatible to 3.0.1)
  # https://github.com/getnikola/nikola/issues/3173
  sed -e 's/,<3.0.0//' -i requirements.txt
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 {AUTHORS,CHANGES}.txt {CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # shell completions
  export PYTHONPATH="${pkgdir}/usr/lib/python3.7/site-packages:${PYTHONPATH}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell bash \
    --hardcode-tasks \
    > "${pkgname}_bash"
  install -vDm 644 ${pkgname}_bash \
    "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" tabcompletion \
    --shell zsh \
    --hardcode-tasks \
    > "${pkgname}_zsh"
  install -vDm 644 ${pkgname}_zsh \
    "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
}
