# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Robert Knauer <robert@privatdemail.net>
# Contributor: xav <xav at ethertricks dot net>

pkgname=umurmur
pkgver=0.2.16_a
pkgrel=1
pkgdesc='Minimalistic Mumble server'
arch=('i686' 'x86_64')
url='https://github.com/umurmur/umurmur'
license=('custom')
depends=('mbedtls' 'libconfig' 'protobuf-c')
install=$pkgname.install
backup=('etc/umurmur/umurmur.conf')
source=($pkgname-$pkgver.tar.gz::$url/archive/${pkgver/_/}.tar.gz
	mbedtls2.patch
        umurmur.service)
md5sums=('061aa71eb059eb00d2b123ec9200b405'
         'e8206f12304ec742221c69c151d5a817'
         'd9d556e4ffa77e193fb40ce508804720')

prepare() {
  cd $pkgname-${pkgver/_/}
  patch -p1 <$srcdir/mbedtls2.patch
  ./autogen.sh
}

build() {
  cd $pkgname-${pkgver/_/}
  LIBS="-lmbedtls -lmbedcrypto -lmbedx509" ./configure --prefix=/usr --mandir=/usr/share/man --with-ssl=polarssl
  echo "#define USE_POLARSSL 1" >>src/config.h
  make
}

package() {
  cd $pkgname-${pkgver/_/}
  make DESTDIR="$pkgdir" install

  install -Dm644 umurmur.conf.example "$pkgdir"/etc/umurmur/umurmur.conf
  install -Dm644 ../umurmur.service "$pkgdir"/usr/lib/systemd/system/umurmur.service
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
