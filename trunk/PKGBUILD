# Maintainer: Pierre Neidhardt <ambrevar@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=uncrustify
pkgver=0.70.0
pkgrel=1
pkgdesc="A source code beautifier"
arch=(x86_64)
url="http://uncrustify.sourceforge.net/"
license=(GPL)
depends=(gcc-libs)
makedepends=(cmake python)
source=("https://github.com/uncrustify/uncrustify/archive/uncrustify-$pkgver.tar.gz")
sha256sums=('d8ff60852e6db90c855cab18dd8003fde6f8694d8a85556a86f01114729c5185')

build() {
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" PREFIX=/usr install

  cd ../$pkgname-$pkgname-$pkgver
  install -d -m755 "$pkgdir"/usr/share/uncrustify
  install -m644 etc/*.cfg "$pkgdir"/usr/share/uncrustify
}
