# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=8.1.2
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='https://cordova.apache.org'
license=('Apache')
depends=('npm' 'acorn' 'semver' 'browserify' 'uglify-js' 'eslint')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('21fb2533730fe36080fde6d5255962b7a1614363fa15eadcc31f4d3508e456cd306a1130a9a98be1b72a87580a893b62efefd9b03b4145256646ffb2ecb7136e')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Delete npm installed pyc files
  find "$pkgdir" -name "*.pyc" -delete

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver browserify uglify-js eslint; do
    rm -r $dep;
  done
}
