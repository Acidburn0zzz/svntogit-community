# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ciano
pkgver=0.2.2
pkgrel=1
pkgdesc="Multimedia file converter focused on simplicity"
arch=(x86_64)
url="https://robertsanseries.github.io/ciano/"
license=(GPL3)
depends=(ffmpeg granite imagemagick)
makedepends=(git meson vala)
_commit=92f02c23f8809ec48e889cec42d3efd9617f7628  # tags/0.2.2
source=("git+https://github.com/robertsanseries/ciano#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ciano
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ciano
  # Use standard icon names
  sed -i 's/application-add-symbolic/list-add-symbolic/' src/Widgets/DialogConvertFile.vala
}

build() {
  arch-meson ciano build
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
