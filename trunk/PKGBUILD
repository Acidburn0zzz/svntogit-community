# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Kirill "reflexing" Churin <reflexing@reflexing.ru>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=keepass
pkgver=2.24
pkgrel=1
pkgdesc='KeePass Password Safe, the free, open source, light-weight and easy-to-use password manager'
arch=('any')
url='http://keepass.info/'
license=('GPL')
depends=('mono' 'desktop-file-utils' 'xdg-utils' 'shared-mime-info' 'hicolor-icon-theme')
makedepends=('icoutils')
optdepends=('xdotool: if you want to use auto-type'
            'xsel: clipboard operations in order to work around Mono clipboard bugs')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/keepass/KeePass-$pkgver.zip"
        'keepass'
        'keepass.1'
        'keepass.desktop'
        'keepass.xml'
        'KeePass.config.xml')

sha256sums=('180c2e1187400d23dc5fda0342891f39179cb699c9b9b312f41a3b06e6557076'
            '7547f2af38771b66c8c2f3d7900b051b37214af49de3dfa27a5d383bf847d582'
            'a5fff678466443c0c8256c4771128c86103da47b6a2c49351d9941191b65dd6f'
            '1d5420e8babce5f4bbb3c68bdffe3bc0d3c3be25ad689138cd02fa14edd89140'
            '3d017c17a8788166c644e2460ba3596fd503f300342561921201fe5f69e5d194'
            'c4a3626bbcdfe4b17759e75582ad5f89beaa28efc857431f373e104fbe7b8440')

package() {
  cd "$srcdir"

  install -dm755 "$pkgdir"/usr/bin
  install -dm755 "$pkgdir"/usr/share/keepass/XSL

  install -Dm755 keepass "$pkgdir"/usr/bin/keepass
  install -Dm755 KeePass.exe "$pkgdir"/usr/share/keepass/KeePass.exe
  install -Dm755 KeePass.config.xml "$pkgdir"/usr/share/keepass/KeePass.config.xml
  install -Dm755 KeePass.exe.config "$pkgdir"/usr/share/keepass/KeePass.exe.config
  install -Dm755 KeePass.chm "$pkgdir"/usr/share/keepass/KeePass.chm

  install -m644 XSL/* "$pkgdir"/usr/share/keepass/XSL

  install -Dm644 keepass.1 "$pkgdir"/usr/share/man/man1/keepass.1

  # Proper installation of .desktop file
  desktop-file-install -m 644 --dir "$pkgdir"/usr/share/applications/ keepass.desktop

  # Extract and install icons
  wrestool -o . --type=14 -x KeePass.exe
  icotool -x KeePass.exe_*.ico
  for size in 16 32 48 256; do
    install -Dm644 \
    KeePass_*_${size}x${size}x32.png \
    "$pkgdir"/usr/share/icons/hicolor/${size}x${size}/apps/keepass.png
  done

  # Needed for postinst with xdg-utils
  install -Dm644 keepass.xml "$pkgdir"/usr/share/mime/packages/keepass.xml
}
