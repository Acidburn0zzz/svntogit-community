# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aws-sam-translator
pkgver=1.10.0
pkgrel=1
pkgdesc='AWS Serverless Application Model (AWS SAM) prescribes rules for expressing Serverless applications on AWS'
arch=(any)
url='https://github.com/awslabs/serverless-application-model'
license=(Apache)
depends=(python python-boto3 python-jsonschema python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-yaml python-mock python-parameterized)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/awslabs/serverless-application-model/archive/v$pkgver.tar.gz")
sha256sums=('16d93f60123a4ad4b23dfb6267111bf5899a1f7ec95b815dabcbfb618f2b4e40')

prepare() {
  cd serverless-application-model-$pkgver

  # remove coverage.py options - running coverage as a non-developer does not make much sense
  sed -i 's#addopts = .*##' pytest.ini
}

build() {
  cd serverless-application-model-$pkgver
  python setup.py build
}

check() {
  cd serverless-application-model-$pkgver

  pytest -v tests
}

package() {
  cd serverless-application-model-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
