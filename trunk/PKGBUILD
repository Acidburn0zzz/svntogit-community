# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.1.2
pkgrel=1
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://gitlab.com/warsaw/flufl.i18n"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
# upstream removed tests from the sdist tarball: https://gitlab.com/warsaw/flufl.i18n/-/issues/8
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("https://gitlab.com/warsaw/${_name}/-/archive/${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('560ca3d28708a1a2203f95c8ae6cc8ad69849884493508c63d4ac5824d4e7eacafbb8dddca2a815a26d9af5c1af62d126031e8a5db4378950b4ea39636d81407')
b2sums=('0ea7d45322c1dcf7e743abe8371f33981001e36e5b21fbccd9b8bdd0964a2ceedacc80eafda1971b5a55bf0989ad1a3ef512cd2e9467954fa06ec2768219e0e6')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
