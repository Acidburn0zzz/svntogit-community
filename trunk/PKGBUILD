# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=python-bleach
pkgver=3.1.1
pkgrel=1

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="https://pypi.org/project/bleach/"
arch=('any')
license=('Apache')
depends=('python-webencodings')
makedepends=('python'
             'python-setuptools')
checkdepends=('python-pytest')

source=(https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz
        pytest5.patch)

sha256sums=('aa8b870d0f46965bac2c073a93444636b0e1ca74e9777e34f03dd494b8a59d48'
            'a1d3dea566e7c56540b83bcd006c730f6c71fe531570fa2c5cccf0a917070bc6')

prepare() {
	# https://github.com/mozilla/bleach/pull/483
	patch -Np1 -d bleach-$pkgver <pytest5.patch
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build
}

# Upstream broke the test suite.
#check() {
#	cd "$srcdir"/bleach-$pkgver
#	python -m pytest
#}

package() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}
