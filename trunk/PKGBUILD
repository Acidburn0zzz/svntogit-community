# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>
# Maintainer: Polyzen <polyzen@archlinux.org>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=yq
pkgver=2.11.1
pkgrel=2
pkgdesc="Command-line YAML/XML processor - jq wrapper for YAML/XML documents"
arch=('any')
url="https://github.com/kislyuk/yq"
license=('Apache')
depends=('jq' 'python-setuptools' 'python-yaml' 'python-xmltodict'
         'python-argcomplete')
checkdepends=('python-toml')
optdepends=('python-toml')
source=("https://files.pythonhosted.org/packages/source/y/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('74f64e3784a34d8a18efd8addc83cf5ca3478a0a69517d70fd9158a3809f99e0')
b2sums=('4b51652958121c16462304d21ebe545cca835b6c189a6d7550c7ea0ffc8b7088a02eb61ee412295d85d502a339ec3ccbc67b8dc12cd45a07246cbafa7d7feaf7')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python test/test.py
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
