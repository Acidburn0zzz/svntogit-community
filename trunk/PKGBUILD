# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.13.0
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('mypy' 'python-pytest-runner' 'python-pytest-cov' 'flake8'
              'python-pytest-localserver')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('49d08eba5b49884cb9a09bac7f544a480ac9a324746b9aec642854a68c7139fe9d2f6f3e5f06e957764fd514944699db58040e83df82b9f9571adcac854f7f2b')

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
