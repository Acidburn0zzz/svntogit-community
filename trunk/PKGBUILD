# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-edit
pkgver=0.6.0
pkgrel=1
pkgdesc='Managing cargo dependencies from the command line'
url='https://github.com/killercup/cargo-edit/releases'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('cargo')
source=(https://github.com/killercup/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('485c2ed37a53c1145e79d1bf2db039f1cdd07c12327a6641848574da9e9f3a69fbe1f20ff7f7a255edac5cac35075411569afb37ec26d3b692a1aace3c1e57fd')
b2sums=('218fa8f723ae10ea780b9eca38a3a3a76504596827aaa334dc799ce3d96c2947e649b2f3871de7ec4d6ccb51ef110b8076001fec00f64d4a521ff041ea6559b2')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  # tests depend on target/debug/
  cargo test --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 \
    target/release/cargo-add \
    target/release/cargo-rm \
    target/release/cargo-upgrade \
    -t "${pkgdir}/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
