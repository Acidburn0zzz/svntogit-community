# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pius
pkgver=2.2.7
pkgrel=2
pkgdesc="PGP Individual User Signer"
arch=('any')
url="https://www.phildev.net/pius/"
license=('GPL')
depends=('python2-six')
checkdepends=('python2-backports.unittest_mock' 'python2-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaymzh/pius/archive/v$pkgver.tar.gz"
        shebang.patch::https://github.com/jaymzh/pius/commit/f5f0d7ec21e784c6c876dfdaf82afbb10cd973cb.patch)
sha512sums=('079fe93592383d0974a8d583772087d3ac0544dce92d4cbb0b4fd5169bd8e26bac71729f9ef989bbdea2911ba11a60eef48e274864f6601e53aba6b887f79978'
            '0e2fc3609f9ed3e56b6d9a630626724c7eb76db241426e90ab69bd84db8a67eac9408363c3a8b57e6bcb054536ef0f08b380f2aa693f4cdc8c7e761f09083c7c')

prepare() {
  cd pius-$pkgver
  patch -p1 -i ../shebang.patch
}

build() {
  cd pius-$pkgver
  python2 setup.py build
}

check() {
  cd pius-$pkgver
  python2 -m pytest
}

package() {
  cd pius-$pkgver
  python2 setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
