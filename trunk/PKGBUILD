# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=53
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=(x86_64)
url="https://dinisnoise.org/"
license=(GPL2)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme libglvnd sdl tcl)
makedepends=(boost glu rtaudio rtmidi)
options=(debug)
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-52-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('b359eb84085be80c13f5cf2a5af0c15903778fec677f91c9aa9029849df39d4bda95e537d7f85f290a8bb38932ef0da35c240921154788b5e89e347d7ac90a7e'
            '56aaa4f2e39297d3b64d254ddbf53ae7c7d0ea7df462a7f9ab3071ce3a21c997874b8a39065422b012a10e8ecd90b5373c887e924a8843134a6b095f588ffd26')
b2sums=('f1631c4b923a5f737181120c2a20dd4079e31b9fe02552a9895cab0f0913b2e4d8185404611795e9379178755168c1975c7520b0ae549708f4916a67631b922c'
        'f38ddc9c92a025fe1e361e8c1e48fd2fced13e19894c117f5e79f00eb5444fd4861562895dbef9884bd9996e2248923b511e3879655bb6cb87a1242b7fbb6042')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-52-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -fiv
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=(librtaudio.so librtmidi.so)

  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
