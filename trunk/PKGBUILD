# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.7
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
# Indices for patches should never be reused! Last index = 7
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-update-tests-5.patch::https://github.com/lilydjwg/nvchecker/commit/44687da8a6f4e3bdeb18baf1132518b17bddd484.patch"
        "$pkgname-update-tests-6.patch::https://github.com/lilydjwg/nvchecker/commit/3a6746ed2447bf2d9c3e7d79a5cfe59571582068.patch"
        "$pkgname-update-tests-7.patch::https://github.com/lilydjwg/nvchecker/commit/b5067de68c562ab2bccdba0c73edc0c0992e49ee.patch")
sha512sums=('6243960747de33bd004bb3377109699f702bb64d65320e0c219f540546f33ace2d87be4eb2b18d988874eb98812176488fa5c98aec6f93cd218b8549b38482d0'
            '24d14870de13bbb2beec128854cfb04a65e3fee7720f62856c4c20afadf83975b0d3e26888a96f3663218556ddd0e173831b4c9c89305cacf8f94e531025d0e0'
            '56e1f12b73f484a93ce7e9c2a6b2bc6cbd0c3b4fd25c9fc5a4190d6feaf38457fa36b5eb1e1ad107f278ccb7df7811f71285ddf98cd486f58fa8adbd7ff40eda'
            '46e08b877e807fb010c0cc5aa8c1cf3642ec64d3078a31e8567fa1dbb17f4caf352d952c6acf0ed67593b63446b22e3a174a297478bb0ea33b5aea46596b2b64')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-update-tests-5.patch
  patch -Np1 -i ../$pkgname-update-tests-6.patch
  patch -Np1 -i ../$pkgname-update-tests-7.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  # use PYTHONHASHSEED=0 work around https://bugs.python.org/issue34722
  PYTHONHASHSEED=0 python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
