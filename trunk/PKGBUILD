# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.5.1
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python')
optdepends=('python-six: for logilab.common.testlib')
makedepends=('python-setuptools')
checkdepends=('python-six' 'python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha512sums=('2cea358f58d17b96931d6812d794d6df2deb833fd62dd2f20c14b084c15c9496636e532c2aa4bca48742859be421dec0f5f94e4a5c383ec93e088efc7b074204')

prepare() {
  cp -a logilab-common-$pkgver{,-py2}

  sed -i 's/from cgi import escape/from html import escape/' logilab-common-$pkgver/logilab/common/ureports/html_writer.py
}

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
