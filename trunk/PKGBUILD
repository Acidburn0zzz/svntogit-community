# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=blinksocks
pkgver=3.3.0
pkgrel=1
pkgdesc='A framework for building composable proxy protocol stack.'
arch=('any')
url='https://github.com/blinksocks/blinksocks'
license=('Apache')
depends=('nodejs' 'semver')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        blinksocks@.service)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('81c63ce23073c177dcafb626adc401c19d9305f686d5cdafc609cfb8a4924004869b0ce0d963c622a2d62b77197d926b76b0f9ed826ee3f2071a541e891f95ce'
            '709153bb95492cf0d6fa070ad8eef16220a97896731f688ec79e03cfddf112b4475fff589490740a8dae33b7bc4c1a331a4baf0d190eaf219f193fbb8128abeb')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  rm -r "$pkgdir"/usr/lib/node_modules/blinksocks/node_modules/{,.bin/}semver

  install -Dm644 "$srcdir/blinksocks@.service" "$pkgdir/usr/lib/systemd/system/blinksocks@.service"
}
