# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=min
pkgver=1.16.1
_commit=e517e21c9d830dda0045d9961ea5b68d638b71f8
pkgrel=1
pkgdesc='A smarter, faster web browser'
arch=('x86_64')
url='https://minbrowser.github.io/min'
license=('Apache')
depends=('electron9' 'libsecret')
makedepends=('git' 'npm')
options=(!emptydirs)
source=("git+https://github.com/minbrowser/min.git#commit=${_commit}"
        'min.desktop'
        'min.js'
        'icon.patch')
sha256sums=('SKIP'
            'a779a8daff1d684779ebc6d13ea4c2f09e838df018de41fa19fa3f25aa12e32f'
            '75b2b763b69d61681fe083418fdc5ff49f8df727bfa90a1447aeda8c799c3188'
            'fb5a96cc98766a3ec2c1458f1a647122a71172a516e014adcb455144f8f51e8b')

prepare() {
    cd ${pkgname}

    patch -Np1 -i "${srcdir}"/icon.patch
}

build() {
    cd ${pkgname}

    npm install
    npm run build
    rm -r node_modules

    npm install --production --no-optional
}

package() {
    cd ${pkgname}

    appdir=/usr/lib/${pkgname}

    install -dm755 "${pkgdir}"${appdir}
    cp -r * "${pkgdir}"${appdir}

    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/256x256/apps
    mv icons/icon256.png \
        "${pkgdir}"/usr/share/icons/hicolor/256x256/apps/${pkgname}.png

    install -Dm755 "${srcdir}"/${pkgname}.js "${pkgdir}"/usr/bin/${pkgname}
    install -Dm644 "${srcdir}"/${pkgname}.desktop \
            "${pkgdir}"/usr/share/applications/${pkgname}.desktop

    # Clean up
    rm "${pkgdir}"${appdir}/dist/build.js
    rm -r "${pkgdir}"${appdir}/icons
    rm -r "${pkgdir}"${appdir}/localization
    rm -r "${pkgdir}"${appdir}/main
    rm -r "${pkgdir}"${appdir}/scripts
    find "${pkgdir}"${appdir} \
        -name "package.json" \
            -exec sed -e "s|${srcdir}/${pkgname}|${appdir}|" \
                -i {} \; \
        -or -name ".*" -prune -exec rm -r '{}' \; \
        -or -name "*.Makefile" -exec rm '{}' \; \
        -or -name "*.h" -exec rm '{}' \; \
        -or -name "*.c" -exec rm '{}' \; \
        -or -name "*.cc" -exec rm '{}' \; \
        -or -name "*.gypi" -exec rm '{}' \; \
        -or -name "*.mk" -exec rm '{}' \; \
        -or -name "Gruntfile.js" -exec rm '{}' \; \
        -or -name "Makefile" -exec rm '{}' \; \
        -or -name "bin" -prune -exec rm -r '{}' \; \
        -or -name "bin.js" -exec rm '{}' \; \
        -or -name "bower.json" -exec rm '{}' \; \
        -or -name "cli.js" -exec rm '{}' \; \
        -or -name "cmd.js" -exec rm '{}' \; \
        -or -name "coffee" -prune -exec rm -r '{}' \; \
        -or -name "example" -prune -exec rm -r '{}' \; \
        -or -name "examples" -prune -exec rm -r '{}' \; \
        -or -name "gulpfile.js" -exec rm '{}' \; \
        -or -name "man" -prune -exec rm -r '{}' \; \
        -or -name "obj.target" -prune -exec rm -r '{}' \; \
        -or -name "scripts" -prune -exec rm -r '{}' \; \
        -or -name "test" -prune -exec rm -r '{}' \; \
        -or -name "tests" -prune -exec rm -r '{}' \;
}
