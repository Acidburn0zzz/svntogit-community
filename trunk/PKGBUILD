# $Id: PKGBUILD 95035 2013-08-04 09:44:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy3
pkgver=5.5.0
_pkgver=5.5.0
pkgrel=1
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('expat' 'bzip2' 'gdbm' 'openssl' 'libffi' 'zlib')
makedepends=('python2' 'sqlite' 'tk')
optdepends=('sqlite: sqlite module'
            'tk: tk module')
options=(!buildflags)
license=('custom:MIT')
source=("https://bitbucket.org/pypy/pypy/downloads/pypy3.3-v${pkgver}-alpha-src.tar.bz2")
md5sums=('536008fd7b17af8878915393fc1ecfc3')

build() {
  cd pypy3-v${_pkgver}-src/pypy/goal

  python2 ../../rpython/bin/rpython -Ojit --no-shared targetpypystandalone
  # python2 ../../rpython/bin/rpython -Ojit --shared targetpypystandalone
}

package() {
  cd pypy3-v${_pkgver}-src

  install -Dm755 pypy/goal/pypy-c "${pkgdir}"/opt/pypy3/bin/pypy-c
  # install -Dm755 pypy/goal/libpypy-c.so "${pkgdir}"/usr/lib/libpypy3-c.so
  cp -r include lib_pypy site-packages "${pkgdir}"/opt/pypy3/

  mkdir "${pkgdir}"/opt/pypy3/lib-python/
  cp -r lib-python/3 "${pkgdir}"/opt/pypy3/lib-python/

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy-c "${pkgdir}"/usr/bin/pypy3

  install -Dm644 README.rst "${pkgdir}"/opt/pypy3/README.rst
  install -Dm644 LICENSE "${pkgdir}"/opt/pypy3/LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE

  # Compile binary modules
  export LD_LIBRARY_PATH="${pkgdir}"/usr/lib:${LD_LIBRARY_PATH}
  for module in _curses syslog dbm sqlite3 _tkinter; do
    "${pkgdir}/opt/pypy3/bin/pypy-c" -c "import ${module}"
  done

  # Generate bytecode (currently fails with errors)
  "${pkgdir}"/opt/pypy3/bin/pypy-c -m compileall "${pkgdir}"/opt/pypy3 || true
  "${pkgdir}"/opt/pypy3/bin/pypy-c -O -m compileall "${pkgdir}"/opt/pypy3 || true

}
# vim: ts=2 sw=2 et:
