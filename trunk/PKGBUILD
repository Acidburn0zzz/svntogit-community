# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=cozy-stack
pkgver=1.2.0
pkgrel=1
epoch=1
pkgdesc="Digital home: brings all your web services in the same private space â€“ Stack component"
arch=('x86_64')
url="https://cozy.io"
license=('AGPL3')
backup=('etc/cozy/cozy.yml')
depends=('couchdb' 'imagemagick' 'git')
makedepends=('go-pie')
#optdepends=('cozy-coclyco: X.509 certificates management for instances'
optdepends=('nodejs: konnectors without isolation'
            'nsjail: isolated konnectors'
            'smtp-forwarder: to allow sending mail to users')
source=("https://apt.cozy.io/debian/pool/testing/c/${pkgname}/${pkgname}_${pkgver}.orig.tar.xz"
        "cozy.yml"
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('dadcff186233f6b1fa2f79102531ee5666a6ba65951d3069667126288c0fcac1'
            'ecfcdf3e2dfec1bdeae15a843c41c9a77b713fe66a4094e1b95dce9514af31e8'
            'ad9b40170e2b07d5aa5ea6d444ad16c96bb39adb5ff579db5cc39cb4e2ec3f91'
            'a6bea52350e85163c3141509a52903223fa0f6e7390b1b1f9336c326a8fff984'
            'fd333c2fd0de859890204554f52a5c64b953664f6cb262b20bb839aa70ed9ecb')

build() {
    export GOPATH="${srcdir}"/cozy-stack
    cd cozy-stack/src/github.com/cozy/cozy-stack
    go build -v -o "${srcdir}"/bin/cozy-stack \
             -gcflags "all=-trimpath=${GOPATH}" \
             -asmflags "all=-trimpath=${GOPATH}" \
             -ldflags "-X github.com/cozy/cozy-stack/pkg/config.Version=${pkgver} \
                       -X github.com/cozy/cozy-stack/pkg/config.BuildTime=$(date -u +"%Y-%m-%dT%H:%M:%SZ") \
                       -X github.com/cozy/cozy-stack/pkg/config.BuildMode=production \
                       -linkmode external -extldflags ${LDFLAGS} -s -w"
}

package() {
    install -Dm755 bin/cozy-stack -t "${pkgdir}"/usr/bin/
    install -Dm644 cozy.yml -t "${pkgdir}"/etc/cozy/
    install -Dm644 cozy-stack/src/github.com/cozy/cozy-stack/cozy.example.yaml -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 cozy-stack/src/github.com/cozy/cozy-stack/scripts/konnector-node-run.sh -t "${pkgdir}"/usr/share/cozy/
    install -Dm755 cozy-stack/src/github.com/cozy/cozy-stack/scripts/konnector-nsjail-run.sh -t "${pkgdir}"/usr/share/cozy/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
