# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mrshpot <mrshpot at gmail dot com>
# Contributor: Michael Fellinger <m.fellinger@gmail.com>

pkgname=smalltalk
pkgver=3.2.4
pkgrel=6
pkgdesc='Implementation of Smalltalk-80'
url='http://smalltalk.gnu.org/'
license=('GPL' 'LGPL')
arch=('x86_64' 'i686')
options=('!libtool' '!emptydirs')
depends=('gmp' 'libffi' 'libsigsegv' 'readline' 'libltdl')
makedepends=('gdbm' 'gtk2' 'sqlite' 'tk' 'zip')
install=smalltalk.install
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('901cc7090a1fa44bc60900a970d207de4aa3bdfdce72bea55114e243bc1eb1b3')
optdepends=('tk: for gst-blox'
            'sqlite: database in a file'
            'sed: for examples'
            'gtk2: GUI toolkit')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --libexecdir=/usr/lib/smalltalk \
    --with-imagedir=/var/lib/smalltalk \
    --enable-gtk=yes \
    --with-system-libffi \
    --with-system-libsigsegv \
    --with-readline \
    --with-tcl \
    --with-tk \
    --with-x \
    --without-emacs

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

#  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # fix manpage symlink
  rm -f "$pkgdir/usr/share/man/man1/gst-reload.1"
  ln -s gst-load.1 "$pkgdir/usr/share/man/man1/gst-reload.1"
}

# vim:set ts=2 sw=2 et:
