# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=valabind
pkgver=1.7.1
pkgrel=1
pkgdesc='Tool to parse vala or vapi files to transform them into swig interface files, C++, NodeJS-ffi or GIR'
url='https://radare.org/r/'
arch=('x86_64')
license=('GPL3')
depends=('sh' 'glibc' 'glib2' 'vala' 'swig' 'libvala-0.44.so')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz
        valabind-fix-rpath.patch::https://github.com/radare/valabind/commit/774707925962fe5865002587ef031048acbe9d89.patch)
sha256sums=('b463b18419de656e218855a2f30a71051f03a9c4540254b4ceaea475fb79102e'
            'b4a46f94d90da957e57a6f2483eb4d7ce0c1769590c910a493599497aaa2cd8e')
sha512sums=('b82c733777e0caf449cd27f0ed3597a108462072c5663945ec03f4c1da90d389313c0f3ca0f6db2d52c7f80d09b1751992f733454358cd428c2571b7e53850a4'
            '829bf15a10a4d8f420993f5e378d00dcf836e06068e340aea3306fe4a684a064aeff34cc79ac0e61c2de4ac012183c5c6961cac3f373213f80ca44851e5eb546')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../valabind-fix-rpath.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
