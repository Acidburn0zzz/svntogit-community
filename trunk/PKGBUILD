# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.3.0
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('356e3c983273a771c06863a63fb53bdca9e3df3a50382f47da0d5ef7cee1b83d9279eb0d1e6c28409b2550f6e16eb793901147983582d792cdd39bb4d4c6e9f1')
b2sums=('d51911a69a9845d136c0b5ce50a3ed94c50d80d6d8c89a2c823f1a85cafef373dab68716c5b38907bc920ca5f4d67ef969d598539e59e32bc2c36a0427d37765')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
