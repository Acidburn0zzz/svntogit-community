# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-allauth
pkgname=python-django-allauth
pkgver=0.48.0
pkgrel=1
pkgdesc="Authentication, registration, account management and 3rd party account authentication"
arch=(any)
url="https://github.com/pennersr/django-allauth"
license=(MIT)
depends=(python-django python-openid python-requests python-requests-oauthlib
python-pyjwt)
makedepends=(python-setuptools)
checkdepends=(python-mock python-pytest)
# tests are not available in pypi sdist:
# https://github.com/pennersr/django-allauth/issues/2439
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=(
  "${_name}-${pkgver}.tar.gz::https://github.com/pennersr/${_name}/archive/${pkgver}.tar.gz"
)
sha512sums=('998cc8cd383eb62805258333b91d34073ded6b998336c07bd8fce773b06118833e084887f57da7423d19ee2c52a4cdea7971992dec68a42ee9146ec15ecd32c5')
b2sums=('0cc66865f227ff750ebcb323dd5d07a31f9f7bc649c6dd088286670f763760ee30b2e273c868cdc691d6e99742b4eea982bcab960931e102e8823fda6423930e')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python manage.py test allauth
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
