# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.17.0
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="https://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/v$pkgver.tar.gz")
sha512sums=('79e57a3d1d01b465bad24d3017cf9c0fc52190e958b262ff024c8cb984e2158d03e3a6348664adf3e5fe38dab83566fcedb9fd2df5aeceb82a86a824c8c9bb8b')

prepare() {
  cp -a yapf-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-$pkgver
  python setup.py build

  cd "$srcdir"/yapf-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-$pkgver
  nosetests3

  cd "$srcdir"/yapf-$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
