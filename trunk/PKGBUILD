# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Hinrich Harms <arch@hinrich.de>
# Contributor: sylvain alborini <sylvain.alborini@gmail.com>

pkgname=gpscorrelate
pkgver=1.6.2
_gitrev=365f6e1b3fbf7015f80f0a19334f701d0482e095
pkgrel=4
pkgdesc="Correlate (geotagging) digital camera photos with GPS data in GPX format"
arch=('x86_64')
url="https://github.com/freefoote/gpscorrelate"
license=('GPL2')
depends=('exiv2' 'gtk3')
makedepends=('libxslt')
source=($pkgname-$_gitrev.tar.gz::https://github.com/freefoote/$pkgname/archive/$_gitrev.tar.gz
        gpscorrelate.appdata.xml
        gtk3-port.patch
        gpscorrelate-exiv2-0.27.1.patch)
sha256sums=('449023f21f5dd0a2149592754d7f94635697653b6dcfcd4432986e85c1155a4f'
            'c5915b4fdee2fd4120ca12679c471ddaa3b39e53589e7ee0b3197a0ac3784209'
            '19a707fae49d83e30cd71b3eead9345ca316fde8a1c3c960b90a05228a2765f8'
            '87f337404a268266bfa3e01673ad95358ced5462b729ee75eea2b4301d448464')

prepare() {
  cd $pkgname-$_gitrev
  patch -Np1 -i ../gtk3-port.patch
  patch -p1 -i ../gpscorrelate-exiv2-0.27.1.patch # Fix build with exiv2 0.27.1
}

build() {
  cd $pkgname-$_gitrev
  make prefix=/usr
}

package() {
  cd $pkgname-$_gitrev
  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 gpscorrelate-gui.svg "$pkgdir/usr/share/icons/hicolor/scalable/apps/gpscorrelate-gui.svg"
  install -Dm644 gpscorrelate.desktop "$pkgdir/usr/share/applications/gpscorrelate.desktop"
  install -Dm644 ../gpscorrelate.appdata.xml "$pkgdir/usr/share/metainfo/gpscorrelate.appdata.xml"
}
