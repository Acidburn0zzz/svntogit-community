# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-cpanplus
pkgver=0.9906
pkgrel=1
pkgdesc='Ameliorated interface to the CPAN'
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl' 'perl-log-message' 'perl-archive-extract' 'perl-term-ui' 'perl-log-message-simple'
         'perl-module-pluggable' 'perl-object-accessor' 'perl-package-constants')
url=https://metacpan.org/release/CPANPLUS
source=("https://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPANPLUS-$pkgver.tar.gz")
sha512sums=('b963973597b39e9b43c51482229f2d0451e0636543e032b15618f4435da5f5aa4a1f6d192907e11c8ef3e4b6f0ecba589075bcba2a6ae9bdff2d2e949f9dc25f')
_ddir="CPANPLUS-$pkgver"

build() {
  cd "$srcdir/$_ddir"
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/$_ddir"
  make test
}

package() {
  cd "$srcdir/$_ddir"
  make install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
