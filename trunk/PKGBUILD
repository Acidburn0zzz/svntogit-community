# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.26
pkgrel=1
pkgdesc='The Oild Runtime Compiler'
arch=('x86_64')
license=('custom')
url='https://code.entropywave.com/projects/orc/'
depends=('lib32-glibc' 'orc')
makedepends=('valgrind')
source=("https://gstreamer.freedesktop.org/data/src/orc/orc-${pkgver}.tar.xz"{,.asc})
validpgpkeys=('7F4BC7CC3CA06F97336BBFEB0668CC1486C2D7B5') # Sebastian Dr√∂ge
sha256sums=('7d52fa80ef84988359c3434e1eea302d077a08987abdde6905678ebcad4fa649'
            'SKIP')

build() {
  cd orc-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --enable-backend='mmx,sse,altivec'
  make
}

package() {
  cd orc-${pkgver}

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s orc "${pkgdir}"/usr/share/licenses/lib32-orc
}

# vim: ts=2 sw=2 et:
