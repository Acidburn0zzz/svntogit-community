# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Chris Salzberg <chris@dejimata.com>
# Contributor: Leonidas Spyropoulos <artafinde@gmail.com>

pkgname=neomutt
pkgver=20171006
pkgrel=1
pkgdesc='a version of mutt with added features'
url='http://www.neomutt.org/'
license=('GPL')
arch=('i686' 'x86_64')
depends=('mime-types' 'notmuch-runtime' 'lua')
optdepends=('urlview: for url menu'
            'python: keybase.py')
makedepends=('git' 'gnupg' 'libxslt')
source=("https://github.com/neomutt/neomutt/archive/$pkgname-$pkgver.tar.gz"
        "https://github.com/neomutt/neomutt/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.gz.sig")
sha256sums=('86e9d945919438a377828b15f03c8690f639062e44151e219e2f8443ae27147c'
            'SKIP')
validpgpkeys=('86C2397270DD7A561263CA4E5FAF0A6EE7371805') # Richard Russon (flatcap) <rich@flatcap.org>

prepare() {
    cd "$pkgname-$pkgname-$pkgver"
    autoreconf -i
}

build() {
    cd "$pkgname-$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-debug \
        --enable-gpgme \
        --enable-lua \
        --enable-notmuch \
        --with-gss=/usr \
        --with-ssl=/usr \
        --with-sasl \
        --with-curses=/usr \
        --with-idn \
        --with-gdbm
    make
}

package() {
    cd "$pkgname-$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
