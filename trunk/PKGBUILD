# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ilya Elenskiy <elenskiy.ilya@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=python-rx
pkgver=1.6.1
pkgrel=3
pkgdesc='Reactive Extensions for Python'
arch=(any)
url=http://reactivex.io
license=(APACHE)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-coverage
  python-nose
  python-pytest
  python-pytest-asyncio
)
source=(git+https://github.com/ReactiveX/RxPY.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  cd RxPY

  # some tests fail with Python 3.7
  rm tests/test_observable/test_dowhile.py
  rm tests/test_observable/test_while.py
}

build() {
  cd RxPY

  python setup.py build
}

check() {
  cd RxPY

  python setup.py test
}

package() {
  cd RxPY

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
