# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=kdesvn
pkgver=1.5.5
pkgrel=2
pkgdesc="A pure C++ SVN-client for KDE"
arch=('i686' 'x86_64')
url="http://kdesvn.alwins-world.de/"
license=('GPL')
depends=('kdebase-runtime' 'subversion')
makedepends=('automoc4' 'cmake' 'patch')
conflicts=('qsvn')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://kdesvn.alwins-world.de/downloads/${pkgname}-${pkgver}.tar.bz2
        $pkgname-1.5.5-qt48.patch
        $pkgname-1.5.5-doctype-version.patch)
sha256sums=('67fee0374c9e3ac0514d2ef2b0fdd0b072dca173f1eeae2742cd37022e194b0b'
            '94dae568c51a05d44e1fc7c56e2b0fb0af0eee2a1cc49620a20e10f05b3ba00f'
            '6bb9af737ef835c894a49706e0146f895105377a1ed1de51b8cbae0a2f737577')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  patch -Np1 -i ${srcdir}/kdesvn-1.5.5-qt48.patch
  patch -Np1 -i ${srcdir}/kdesvn-1.5.5-doctype-version.patch

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_FLAGS="-DQT_THREAD_SUPPORT" \
    -DLIB_SUFFIX=

  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

#removing conflicting files - kdesdk
  rm -f ${pkgdir}/usr/share/kde4/services/svn*

#removing $srcdir reference
  rm -rf ${pkgdir}/usr/share/doc
}
