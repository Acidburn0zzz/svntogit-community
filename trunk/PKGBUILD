# Maintainer: George Rawlinson <george@rawlinson.net.nz>

pkgname=dovecot-fts-xapian
_pkgname="${pkgname#dovecot-}"
pkgver=1.4.9b
pkgrel=1
pkgdesc="Dovecot FTS plugin based on Xapian"
arch=('x86_64')
url="https://github.com/grosjo/fts-xapian"
license=('LGPL2.1')
depends=('dovecot' 'icu' 'xapian-core')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('1f49139d9fb3d334a1adae00b5a3fc364df9a36418c64674db00bac807b681bc3aafd3033f3e38f4696d537fe2f5f772a5e4fd1313b2a0d5c77fea9dfa78bdad')

build() {
  cd "$_pkgname-$pkgver"
  autoreconf -vi
  ./configure \
    --prefix=/usr \
    --with-dovecot=/usr/lib/dovecot

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
