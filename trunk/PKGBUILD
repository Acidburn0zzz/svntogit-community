# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=qsynth
pkgver=0.9.2
pkgrel=1
pkgdesc="Qt GUI for FluidSynth"
arch=('x86_64')
url="https://qsynth.sourceforge.io/"
license=('GPL')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('cmake' 'fluidsynth' 'qt6-tools')
optdepends=('qt6-wayland: for native wayland support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ab9a2268e3926439e4784c8dc2511d7c39aa2bfb97ffb7bb34f6761f1e859d1ea53f9f6da4d5c31395c6c03dc250b74c8c250b5d71321f2a151b75ec636be3d6')
b2sums=('9560efe95992fc6e992871c8057898e1acac46718534c7e79060dde4d1519f9fd4798385812f384c97a764676e3e1005ebe24708545db3dfb9f8e08973f07441')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libfluidsynth.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
