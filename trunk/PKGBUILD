# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=8.9.2
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('6467b10ba7e971b55ac4632ae249b9b473ea8a37dd5e8e5b26281f8edd9cfaa966ec59aceba814daf09bbffcdc64b0dbbcfa69aa592554ef2a9848ab78ee9653')

prepare() {
  cp -a python-phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd python-phonenumbers-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
}
