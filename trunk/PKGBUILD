# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=obconf-qt
pkgver=0.14.0
pkgrel=1
pkgdesc="Openbox configuration tool. Qt port of ObConf"
arch=("x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("hicolor-icon-theme" "openbox" "qt5-x11extras")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('44cbea3ba0372bb06c8d3be2f8646432df01bc49081b3ffa6f6dc2a0dedb6402ba5089299167f41fbc4d719876ce872113af4bbb79439c9e2dc454225e2bb530'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
