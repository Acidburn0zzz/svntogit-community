# Maintainer: David Runge <dave@sleepmap.de>
_name=piexif
pkgbase=python-piexif
pkgname=('python-piexif' 'python2-piexif')
pkgver=1.1.1
pkgrel=1
pkgdesc="To simplify exif manipulations with python. Writing, reading, and more"
arch=('any')
url="https://github.com/hMatoba/Piexif"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python-pillow' 'python-sphinx' 'python2-pytest' 'python2-pillow' 'python2-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('1ce2f817c74acb60b178b6d953eed7549093d6081a3264bb8ae8285913cd3562ccad34e986e837b4be0d98feb18ec2e336b85444f1cf68a4116e04305c93e7c2')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgbase-$pkgver"
}

build() {
  cd "$pkgbase-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$pkgbase-$pkgver"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test
  py.test2
}

package_python-piexif() {
  cd "$pkgbase-$pkgver"
  depends=('python')
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-piexif() {
  cd "$pkgbase-$pkgver"
  depends=('python2')
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
