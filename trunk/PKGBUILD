# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=go-ipfs
pkgver=0.4.19
pkgrel=1

pkgdesc='A peer-to-peer hypermedia distribution protocol'
url='https://ipfs.io/'
arch=(x86_64)
license=(MIT)

depends=(glibc)
optdepends=('fuse2: for mounting/advanced use')
makedepends=(go-pie git)

source=(go-ipfs-$pkgver.tar.gz::https://dist.ipfs.io/go-ipfs/v$pkgver/go-ipfs-source.tar.gz
        ipfs.service
        ipfs@.service)
noextract=(go-ipfs-$pkgver.tar.gz)

sha512sums=('89d32fc560683b5bd4f67ae22859d57865ec364e28f065e81bd2618412dccbcc9c5a395f5f297b3ce4c9f6112560ea209e4b2a794c9b0d759b2980569b7ccf82'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6'
            'aed168a35228c4f873e0040d1412f3568bfe1ad1e94ffb8b1a2507058daaf75d7346381d299cf9f0b91951b21553170249ff5f45d5fc060e711cf628ab5e5aad')

prepare() {
  export GOPATH="$PWD"/.gopath
  mkdir -p "$GOPATH"/src/github.com/ipfs/go-ipfs

  tar -C "$GOPATH"/src/github.com/ipfs/go-ipfs -xf go-ipfs-$pkgver.tar.gz
}

build() {
  export GOPATH="$PWD"/.gopath
  cd "$GOPATH"/src/github.com/ipfs/go-ipfs

  make build
}

package() {
  export GOPATH="$PWD"/.gopath
  cd "$GOPATH"/src/github.com/ipfs/go-ipfs

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644 "$srcdir"/ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 "$srcdir"/ipfs@.service "$pkgdir/usr/lib/systemd/system/ipfs@.service"
  install -Dm 644 misc/completion/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
