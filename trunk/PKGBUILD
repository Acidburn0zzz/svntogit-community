# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Albert.Zhou <albert.zhou@wiz.cn>

pkgname=wiznote
pkgver=2.7.3
_commit=04a09ffdf676d3e8c029c89761580af327ac73c0
pkgrel=1
pkgdesc="Opensource cross-platform cloud based note-taking client"
arch=('x86_64')
url="http://www.wiznote.com"
license=('GPL3' 'custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'qt5-websockets' 'qt5-webengine'
         'qt5-svg') # 'crypto++')
# shared clucene/quazip triggers segfault
makedepends=('cmake' 'qt5-tools' 'boost')
source=("$pkgname-$_commit.tar.gz::https://github.com/WizTeam/WizQTClient/archive/$_commit.tar.gz")
sha512sums=('6e4c871a83cdd5016d351b3c23f9ccaac4752c6f053a56eaa7a7b12b2c9a76577e83d809c7745f21e0b797174e5d13cd60abcb34ca66d32250c7dd31408c8546')

prepare() {
  #sed -e 's/add_subdirectory(cryptopp)//' \
  #    -i WizQTClient-$_commit/lib/CMakeLists.txt
  #sed -e 's/cryptlib/cryptopp/' \
  #    -i WizQTClient-$_commit/src/CMakeLists.txt

  rm -rf build; mkdir build
}

build() {
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../WizQTClient-$_commit
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
