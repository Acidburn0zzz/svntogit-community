# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.10.0
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'openssl' 'mariadb-libs')
optdepends=('ca-certificates: server certificate verification'
            'mariadb: advanced user management')
makedepends=('cmake' 'boost' 'openssl' 'mariadb-libs')
checkdepends=('openssl' 'python' 'curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json'
        'etc/trojan/config.json')
sha512sums=('9d9a74218369f8094b292b8892e5f877ccec7d8912657491bb37da8e9ae4817f07a83fe401e33bbbdd2ac9c50c1440f3a021f93d85999b43df4849b0a04049cf')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
    cd $pkgname-$pkgver
    ctest
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
