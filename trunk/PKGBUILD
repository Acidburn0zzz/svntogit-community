# $Id$
# Maintainer: Lukas Fleischer <archlinux@cryptocrack.de>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: Tom Burdick <thomas.burdick@wrightwoodtech.com>

pkgname=erlang
pkgver=R15B
pkgrel=1
pkgdesc='A small concurrent functional programming language developed by Ericsson.'
arch=(i686 x86_64)
url='http://www.erlang.org/index.html'
license=('custom')
depends=('ncurses' 'glibc')
makedepends=('java-environment' 'perl' 'openssl' 'wxgtk' 'unixodbc>=2.3.1' 'mesa')
optdepends=('wxgtk: for wx support'
            'mesa: for wx support'
            'unixodbc: database'
            'java-environment: for Java support')
options=('!makeflags')
source=("http://www.erlang.org/download/otp_src_${pkgver/./-}.tar.gz"
        "http://www.erlang.org/download/otp_doc_man_${pkgver/./-}.tar.gz")
md5sums=('dd6c2a4807551b4a8a536067bde31d73'
         '9738da523737712a9db87db0dee05338')

build() {
  cd "$srcdir/otp_src_${pkgver/./-}"

  export ERTSVERSION=$(sed -n 's/^VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < erts/vsn.mk)
  export ERLINTERFACEVERSION=$(sed -n 's/^EI_VSN[ ]*=[ ]*\([0-9.]\)/\1/p' < \
    lib/erl_interface/vsn.mk)

  sed -i '/SSL_DYNAMIC_ONLY=/s:no:yes:' erts/configure

  CFLAGS="${CFLAGS} -fno-strict-aliasing" ./configure --prefix=/usr --enable-smp-support \
    --enable-threads -enable-native-libs
  make
}

package() {
  cd "$srcdir/otp_src_${pkgver/./-}"

  make INSTALL_PREFIX="$pkgdir" install

  # fix prefix
  cd "$pkgdir/usr/lib/erlang"
  sed -i "s#$pkgdir##" bin/erl bin/start "erts-${ERTSVERSION}/bin/erl" \
    "erts-${ERTSVERSION}/bin/start" releases/RELEASES

  # fix symlinks
  cd bin/
  ln -sf "../erts-${ERTSVERSION}/bin/epmd"

  cd "$pkgdir/usr/bin"
  for file in *; do
    ln -sfv "../lib/erlang/bin/$file"
  done

  ln -s "../lib/erlang/lib/erl_interface-${ERLINTERFACEVERSION}/bin/erl_call" \
    "$pkgdir/usr/bin/erl_call"

  # install documentation
  install -d "$pkgdir/usr/share/doc/erlang"
  install -Dm0644 "$srcdir"/{COPYRIGHT,PR.template,README} "$pkgdir/usr/share/doc/erlang"

  # install man pages
  cp -r "$srcdir/man" "$pkgdir/usr/lib/erlang"

  # install license
  install -Dm644 "$srcdir/otp_src_${pkgver/./-}/EPLICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/EPLICENCE"
}
