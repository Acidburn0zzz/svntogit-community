# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.3.0
pkgrel=2
pkgdesc='LISP dialect for the JVM'
url='http://www.clojure.org/'
arch=('any')
license=('CPL')
depends=('java-environment')
optdepends=('rlwrap: for a friendlier shell with readline support')
install=clojure.install
# major asshattery happening on behalf of maven.org, WHY WOULD YOU BLOCK WGET?!?!?!
DLAGENTS=('http::/usr/bin/wget -c -t 3 --waitretry=3 -O %o %u -U YUBLOCKWGET')
source=("http://repo1.maven.org/maven2/org/clojure/clojure/${pkgver}/clojure-${pkgver}.zip"
        'clj'
        'clojure.sh')
sha256sums=('3a4472d75bbc4ee02da3f285349663c49d91849f74c19f1838767f9e75f57182'
            'd903b9e804518574575edf0976921623399310a0adba2b75d2714e7397a2fd8d'
            'b3fa6257dad10fb39851e87563269b80b2473623126595d9c279bbd90e97f052')

package() {
  cd "$srcdir"

  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm755 clojure.sh "$pkgdir/etc/profile.d/clojure.sh"
  install -Dm644 "clojure-$pkgver/clojure-$pkgver.jar" \
    "$pkgdir/usr/share/clojure/clojure.jar"
}

# vim:set ts=2 sw=2 et:
