# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.10
pkgrel=2
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha256sums=("289d5287fda0bb870c1fb1ed504523bf24885ccf763b9547d91f8225310dc4f5")
changelog="CHANGELOG.md"

build() {
	cd "$pkgname-$pkgver"
	export CGO_LDFLAGS="$LDFLAGS"
	export GOFLAGS="-trimpath"
	go build -o terraform-binary
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
