# Maintainer: Alad Wenter <alad@mailbox.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=cabextract
pkgver=1.9
pkgrel=1
pkgdesc="A program to extract Microsoft cabinet (.CAB) files"
arch=('x86_64')
license=('GPL')
url='https://www.cabextract.org.uk/'
depends=('glibc')
source=("https://cabextract.org.uk/$pkgname-$pkgver.tar.gz")
sha256sums=('1bbc793d83c73288acd7e28ce33ec04955a76c73bf6471424ff835d725fcc4c1')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
