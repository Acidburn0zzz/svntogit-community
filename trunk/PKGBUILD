# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=padthv1
pkgver=0.9.15
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'fftw' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'non-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('64d789cd9c7b4a9c56d72ebf78f9af4e2116824c465abdba8a33f7a87c5089cc4f89a8c6701fdcca3b9e52700219659236f355d75d5a37cb1854cd5cd959c5fd')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  depends+=('libfftw3.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
