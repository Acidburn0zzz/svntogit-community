# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-cliff
pkgver=2.18.0
pkgrel=2
pkgdesc="Command Line Interface Formulation Framework"
arch=('any')
url="https://docs.openstack.org/cliff/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-cmd2' 'python-prettytable' 'python-pyparsing'
         'python-stevedore' 'python-yaml')
makedepends=('git')
checkdepends=('python-testrepository' 'python-mock' 'python-sphinx' 'python-oslosphinx'
              'python-testscenarios')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/cliff/archive/$pkgver.tar.gz")
sha512sums=('693d8e9678567c88719a9f2e309951dcebf61277efcdbb250b11e808960ab47d5161db50d2d5d760f3ecc30629db78d986cf77959ffb2084aa2c56f81da7d6a2')

export PBR_VERSION=$pkgver

build() {
  cd cliff-$pkgver
  python setup.py build
}

check() {
  cd cliff-$pkgver
  python setup.py testr
}

package() {
  cd cliff-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
