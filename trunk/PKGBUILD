# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-teamcity-messages
pkgver=1.30
pkgrel=1
pkgdesc="Send test results to TeamCity continuous integration server from unittest, nose, py.test, twisted trial, behave"
url="https://github.com/JetBrains/teamcity-messages"
license=('Apache')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-virtualenv')
source=("https://github.com/JetBrains/teamcity-messages/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f84a5e91464da22ccf2121628412112fe475b08014ac16935ed891fb94d261ac6702dd49037489e88b686aa32dc1bd918ae1a6932860f48824ec8473561c63c7')

prepare() {
  cd teamcity-messages-$pkgver
  sed -i "s/virtualenv_version = 'virtualenv==20.7.2'/virtualenv_version = 'virtualenv'/" setup.py
}

build() {
  cd teamcity-messages-$pkgver
  python setup.py build
}

check() {
  cd teamcity-messages-$pkgver
  python setup.py test || echo "Tests failed"
}

package() {
  cd teamcity-messages-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
