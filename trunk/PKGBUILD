# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=19.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'desktop-file-utils' 'fftw' 'gcc-libs' 'glibc' 'libjack.so'
'liblrdf.so' 'liblo.so' 'libsamplerate' 'libsndfile' 'lirc'
'hicolor-icon-theme' 'qt5-base' 'zlib')
makedepends=('cmake' 'dssi' 'imake' 'ladspa' 'qt5-tools')
optdepends=('cups: Printing support'
            'evince: Print preview support'
            'lilypond: Notation display support'
            'mupdf: Print preview support'
            'okular: Print preview support'
            'timidity++: Softsynth support'
            'flac: FLAC support'
            'wavpack: Lossless audio codec support')
provides=('dssi-host' 'ladspa-host')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('22c45346b1a4d69f009e59091bdf5f5f68633241a48df7bd6ee73462cc281ee7182be3d48c3afc0843aa551145c203a8b881501891eb84b6d3f3def5f7650fc6')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  make test -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
# vim:set ts=2 sw=2 et:
