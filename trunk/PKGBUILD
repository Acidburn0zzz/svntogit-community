# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200831
_pkgver=2020-08-31
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('58376c7fbbae650ae23c9d39bb4957561c07a5ac4cbced37fc2c0df1e30b7049bec9e4d5d52b78d8418c9214593ba673caf1348651ec3fef02383980b3c7e167')

pkgver() {
  echo ${_pkgver//-}
}

prepare() {
  cd $pkgname-$_pkgver
  sed -i '/ensure_rustfmt/d' xtask/src/codegen.rs
  sed -i '/ensure_rustfmt()?;/d' xtask/src/lib.rs
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$_pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
