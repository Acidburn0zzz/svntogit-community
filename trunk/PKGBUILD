# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=5.2.2.13
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('LGPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'dtkgui' 'librsvg' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$pkgver.tar.gz")
sha512sums=('7d41319914154e2939097d81267322d95a30701bd55527eb469e033c18dea9197f7b9a93e0e3d7fb8548ebc4e6a20226cef1079f678350fdba344020f3a192f2')

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
