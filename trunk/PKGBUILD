# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=4.0.2
_i18nv=4.0.1
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('i686' 'x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
depends=('wxgtk' 'hicolor-icon-theme' 'desktop-file-utils' 'libsm' 'boost-libs' 'glew')
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'gettext')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
# python optdep for plugins?
install=kicad.install
source=("https://launchpad.net/kicad/4.0/$pkgver/+download/kicad-$pkgver.tar.xz"
        "kicad-i18n.$_i18nv.tar.gz::https://github.com/KiCad/kicad-i18n/archive/$_i18nv.tar.gz")
md5sums=('6c40f3a511f47c1145ccc75b9c3729f7'
         '42db67dac237675af91e705de4913d04')

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DKICAD_STABLE_VERSION=ON     \
              -DKICAD_REPO_NAME=stable      \
              -DKICAD_BUILD_VERSION=$pkgver \
              -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DKICAD_SKIP_BOOST=ON         \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$_i18nv"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$_i18nv/build/Release"
  make DESTDIR="$pkgdir" install
}
