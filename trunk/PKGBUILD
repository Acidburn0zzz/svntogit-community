# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=8.7.1
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('4d0e4f453cbd8404cfd046f0fc86b6a079bb4146e267cc569ac3fc7689bcf7a713591a0dd899542d41db07aecbe1287ddb1a376b055ca5b5af190597d68ca785')

prepare() {
  cp -a python-phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd python-phonenumbers-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
}
