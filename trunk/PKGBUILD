# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMuPDF
pkgname=python-pymupdf
pkgver=1.18.16
pkgrel=1
pkgdesc="Python bindings for MuPDF's rendering library"
arch=('x86_64')
url="https://github.com/pymupdf/PyMuPDF"
license=('AGPL3')
depends=('glibc' 'gumbo-parser' 'jbig2dec' 'libmupdf' 'openjpeg2' 'python')
makedepends=('freetype2' 'libjpeg-turbo' 'python-distro' 'python-setuptools' 'swig' 'systemd')
checkdepends=('python-pillow' 'python-pytest')
# pypi sdist tarball does not have tests
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('4f247ea33638b095fea1d34eeeb8366ddd9d1180c276736d4841365156d871db9a379839d5e65f56de48816ed3c25c72e018ffa93d3f3f148dca0f756eb536fa')
b2sums=('68100466fd28d0f3dc995480cf58e7d85218a04e27665e3975ca9d8c6c3a187d943cd3e2e2af4d0753a9bddeba5951a49f2b5fb9980f1fb98114ec3ef6ae7e3a')

prepare() {
  mv -v "$_name-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="test_dir"
  cd "$pkgname-$pkgver"
  mkdir -vp "${_test_dir}"
  # install to test dir for testing
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${_test_dir}"

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  pytest -v -k "not test_font1 and not test_pageids and not test_textbox3"
}

package() {
  depends+=('libfreetype.so' 'libjpeg.so')
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  rm -rv "${pkgdir}/usr/README.md"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
