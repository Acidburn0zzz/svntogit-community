# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=libxext
pkgname=lib32-$_pkgbasename
pkgver=1.1.2
pkgrel=3
pkgdesc="X11 miscellaneous extensions library (32-bit)"
arch=(x86_64)
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('lib32-libx11>=1.3.3' 'xextproto>=7.1.1' $_pkgbasename)
makedepends=('pkgconfig' 'gcc-multilib')
options=('!libtool')
source=(${url}/releases/individual/lib/libXext-${pkgver}.tar.bz2)
sha1sums=('b48a4e55d21e5ce9dd3356281f32e00b17b4c9f7')

build() {
  cd "${srcdir}/libXext-${pkgver}"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --sysconfdir=/etc --disable-static \
    --libdir=/usr/lib32
  make
}

package() {
  cd "${srcdir}/libXext-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname" 
}
