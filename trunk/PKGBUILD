# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=v2ray-domain-list-community
pkgver=202003050337
pkgrel=1
pkgdesc="A list of domains to be used as geosites for routing purpose in Project V"
arch=('any')
url="https://github.com/v2ray/domain-list-community"
license=('MIT')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/domain-list-community/archive/$pkgver.tar.gz")
sha512sums=('636fd1ecc20efdfb8840358e678cabd2278beee5908e26529fd1635b3f4b1ccd7f35a7e1d02373a5f8efa7967c2128028a7284703fc1dc135b68c7ed95466636')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir/.gopath"

  mkdir -p .gopath/src/github.com/v2ray
  ln -s "$PWD/domain-list-community-$pkgver" .gopath/src/github.com/v2ray/domain-list-community

  go get github.com/golang/protobuf/proto
  go get -insecure v2ray.com/core/app/router
}

build() {
  cd .gopath
  go run ./src/github.com/v2ray/domain-list-community/main.go
}

package() {
  cd .gopath
  install -Dm755 dlc.dat "$pkgdir"/usr/lib/v2ray/geosite.dat
  install -Dm644 "$srcdir"/domain-list-community-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
