# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: galiyosha@gmail.com
# Contributor: Jon Kristian Nilsen <jokr.nilsen@gmail.com>

pkgname=libffado
pkgver=2.3.0
pkgrel=5
pkgdesc="Driver for FireWire audio devices"
arch=('x86_64')
url="http://www.ffado.org/"
license=('GPL')
depends=('dbus-c++' 'jack' 'libavc1394' 'libconfig' 'libiec61883' 'libxml++')
makedepends=('python2-pyqt4' 'scons')
optdepends=('python2: ffado-diag'
            'python2-pyqt4: ffado-mixer')
provides=('ffado')
source=("http://www.ffado.org/files/${pkgname}-${pkgver}.tgz"
        'python2-qt.patch'
        'libxml++3.patch'
        'gcc-warnings.patch'
        'cpuinfo-parsing.patch'
        'gcc6.patch'
        'libffado-gcc7.patch'
        'libffado-sigev_thread.patch'
)
sha512sums=('24a8853281a17b04623e41dc590f7a9b8f4aebe4dfaece2e3b8fe28457a6ff011586e817712b4913d1fca660b701dddc70576c12d8c56470cf991eb17b4297c4'
            '82f2d9329358f03cc241529b8199cf97da0edacb773bbf8df07d909abd34b6b2441ba16585c9f3549f17e22fea8da43e4d04c7032695d259d5d1ff22804988f7'
            '6dd786961f68d9136f78369aff6b8232db79f026f084f75c90410f8945b2fadf73747a6d8887f9f5d41beca3520d740f25c4854a3663461c1cdaff66c5ad785c'
            '439925b4f8cd14a89792aafdd83b37aabdc0c57d380d328c736b5f48e15a32070c31ab38c3462a77b172971e47488278747804e0b20a158cabc78adc2ff8d244'
            '0af084f5915180ad413004ec0cdc41763c6a34c74f8e5eb1e471f6ea18dc588faee903222d9865ad0a111a4f782ce89e9fc571ec2d9f18940527976b5cd7ea21'
            '308dd62acc839a89c4b9c979d374a8aa92d51b5d424992db6498726969e2bb0c176786133b2335659e3a1b030b74e322601eb1efe0ae0c976e9888ccb884569d'
            'a43d28568f6df448068bb0547a3726df8f3a0378fa843b7d0f6953c62c3c2f32bd0a742d29df8e88202163b0c507e88e5dcfdfab18ab5ebe0bf0441fa3ea34db'
            'b125060d0c4a3e8efa8beaf45ad33afbfecd5e09d3fe6d880fa1661bfc86a7caa5281febdd6dde6ba873f808655e52dc9391ae442705c9b8e82d89e71a8b6521')

prepare() {
  cd "${pkgname}-${pkgver}"

  msg2 "Applying Python 2 compatibility patch (downstream)"
  patch -Np1 -i "$srcdir/python2-qt.patch"
  patch -Np3 -i "$srcdir/libxml++3.patch"
  patch -Np3 -i "$srcdir/gcc-warnings.patch"
  patch -Np1 -i "$srcdir/cpuinfo-parsing.patch"
  patch -Np1 -i "$srcdir/gcc6.patch"
  patch -Np1 -i "$srcdir/libffado-gcc7.patch" # Fedora
  patch -Np1 -i "$srcdir/libffado-sigev_thread.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  scons PREFIX=/usr \
        DEBUG=no \
        MANDIR=/usr/share/man \
        UDEVDIR=/usr/lib/udev/rules.d \
        ENABLE_OPTIMIZATIONS=True
}

package() {
  cd "${pkgname}-${pkgver}"

  scons DESTDIR="${pkgdir}" WILL_DEAL_WITH_XDG_MYSELF="True" install

  install -Dm644 support/xdg/ffado.org-ffadomixer.desktop \
    "${pkgdir}/usr/share/applications/ffadomixer.desktop"

  install -Dm644 support/xdg/hi64-apps-ffado.png \
    "${pkgdir}/usr/share/pixmaps/ffado.png"
}

# vim:set ts=2 sw=2 et:
