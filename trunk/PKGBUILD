# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=sshguard
pkgver=2.3.0
pkgrel=1
pkgdesc="Brute force detector for SSH, Exim, VSFTPD and more. Blocks by ip with iptables"
arch=('x86_64')
url="http://www.sshguard.net/"
license=('GPL')
depends=('iptables')
backup=('etc/sshguard.conf')
install=sshguard.install
source=("https://downloads.sourceforge.net/sourceforge/sshguard/sshguard-$pkgver.tar.gz")
sha256sums=('d4b53f87a3c26652e5a2899b16506b815fa56a36aaf5bde9c2704bfb1a0ca068')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|tables.target|tables.service|g' examples/sshguard.service
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/sshguard \
    --with-firewall=iptables
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -Dm0644 "examples/sshguard.service" "$pkgdir/usr/lib/systemd/system/sshguard.service"
  install -dm0755 "$pkgdir/usr/share/doc/" "$pkgdir/etc"
  cat <<EOF >"$pkgdir"/etc/sshguard.conf
LOGREADER="LANG=C /usr/bin/journalctl -afb -p info -n1 -t sshd -o cat"
BLACKLIST_FILE=120:/var/db/sshguard/blacklist.db
BACKEND="/usr/lib/sshguard/sshg-fw-iptables"
EOF
  cp -a examples "$pkgdir/usr/share/doc/sshguard"
}
