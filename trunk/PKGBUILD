# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: David Runge <dave@sleepmap.de>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=2.2.0
pkgrel=3
pkgdesc="Free, open source software for digital DJ'ing."
arch=('x86_64')
url='https://www.mixxx.org'
license=('GPL2')
groups=('pro-audio')
depends=('chromaprint' 'faad2' 'gperftools' 'glu' 'libid3tag' 'libmad'
'libmp4v2' 'libshout' 'lilv' 'opusfile' 'portaudio' 'portmidi' 'protobuf'
'qt5-script' 'qt5-svg' 'qt5-x11extras' 'rubberband' 'taglib' 'upower'
'wavpack')
makedepends=('qt5-tools' 'qtkeychain' 'scons')
optdepends=('qtkeychain: Credentials storage support for Live Broadcasting profiles')
checkdepends=('xorg-server-xvfb')
source=("https://github.com/${pkgname}dj/${pkgname}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-audiotaperpottest.patch::https://github.com/mixxxdj/mixxx/pull/1979.patch"
)
sha512sums=('8174b504d236cde36c636985e0e224915f0be69b13691bf0d88c21087c20c1100ff55561b6e395d97fe5b21a0229fe9ecdac2493cb44fa99a42549c368fa2134'
            '91760005610bbb20eae5265ef723c59f9b5cd858d29781b16d8774cd743cbcafa1a3e55012be06dc1f318fab30a2d282717bde59582b516a8b4af1e3264f761a')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "../${pkgname}-audiotaperpottest.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  export SCONSFLAGS="-j $(nproc)"
  scons prefix=/usr \
        build=release \
        faad=1 \
        modplug=1 \
        perftools=1 \
        perftools_profiler=1 \
        qt_sqlite_plugin=0 \
        test=1 \
        target=linux \
        virtualize=0 \
        wv=1
}

check() {
  cd "${pkgname}-${pkgver}"
  xvfb-run lin64_build/${pkgname}-test
}

package() {
  cd "${pkgname}-${pkgver}"
  scons prefix=/usr \
        install_root="${pkgdir}/usr" \
        install
  # removing the test executable
  rm -v "${pkgdir}/usr/bin/${pkgname}-test"
}
