# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Doug Newgard <scimmia at archlinux dot org>
# Contributor: XavierCLL <xavier.corredor.llano (a) gmail.com>
# Contributor: SaultDon <sault.don gmail>
# Contributor: Lantald < lantald at gmx.com >
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Eric Forgeot < http://esclinux.tk >

pkgname=qgis
pkgver=3.20.3
pkgrel=3
pkgdesc='Geographic Information System (GIS) that supports vector, raster & database formats'
url='https://qgis.org/'
license=('GPL')
arch=('x86_64')
depends=('exiv2' 'gdal' 'gsl' 'hicolor-icon-theme' 'libzip' 'ocl-icd' 'pdal' 'protobuf'
         'python-gdal' 'python-jinja' 'python-owslib' 'python-psycopg2' 'python-pygments'
         'python-qscintilla-qt5' 'python-sip4' 'python-yaml' 'qca' 'qt5-3d' 'qt5-imageformats'
         'qt5-serialport' 'qt5-webkit' 'qtkeychain-qt5' 'qwt' 'spatialindex')
makedepends=('cmake' 'fcgi' 'ninja' 'opencl-clhpp' 'python-setuptools' 'python-six' 'qt5-tools'
             'txt2tags' 'sip4')
optdepends=('fcgi: Map server'
            'gpsbabel: GPS Tools plugin')
source=("https://qgis.org/downloads/$pkgname-$pkgver.tar.bz2"
         qgstyle-infinite-loop.patch::https://github.com/qgis/QGIS/commit/19823b76.patch
         qgis-qwt-6.2-1.patch::https://github.com/qgis/QGIS/commit/6f9cbde7.patch
         qgis-qwt-6.2-2.patch::https://github.com/qgis/QGIS/commit/581cb406.patch)
sha256sums=('93ac52138f4f7832078e6e79ed5249dcd8c3dd34c8801c4039f87b690657294e'
            'c5bc388da987c7c21451b3f0f74220c8bd82cfba04657ff599d1896e6ae4b304'
            '3d976937aa83ef76fa2fbdd1bf0e3e6c8467b50649fadbcdd338f99963cd6a91'
            '91f4458c1c2d97a1e3f809e46b38c63b22f6aa6b919257f409f169f2fdb84b7f')

prepare() {
  patch -d $pkgname-$pkgver -p1 < qgstyle-infinite-loop.patch # Fix infinite loop
  patch -d $pkgname-$pkgver -p1 < qgis-qwt-6.2-1.patch # Fix build with qwt 6.2
  patch -d $pkgname-$pkgver -p1 < qgis-qwt-6.2-2.patch # Fix build with qwt 6.2
}

build() {
  cmake -S $pkgname-$pkgver -B build -G Ninja \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DWITH_3D=TRUE \
    -DWITH_SERVER=TRUE \
    -DWITH_CUSTOM_WIDGETS=TRUE \
    -DBINDINGS_GLOBAL_INSTALL=TRUE \
    -DQGIS_MANUAL_SUBDIR=share/man \
    -DWITH_QWTPOLAR=TRUE \
    -DQWTPOLAR_LIBRARY=/usr/lib/libqwt.so \
    -DQWTPOLAR_INCLUDE_DIR=/usr/include/qwt \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -DQWT_POLAR_VERSION=0x060200" \
    -DWITH_INTERNAL_QWTPOLAR=FALSE \
    -DWITH_PDAL=TRUE
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -Dm644 $pkgname-$pkgver/rpm/sources/qgis-mime.xml "$pkgdir/usr/share/mime/packages/qgis.xml"
}
