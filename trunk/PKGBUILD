# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Georg Grabler (STiAT) <ggrabler@gmail.com>

pkgname=proxytunnel
pkgver=1.9.0
pkgrel=4
pkgdesc="a program that connects stdin and stdout to a server somewhere on the network, through a standard HTTPS proxy"
arch=('i686' 'x86_64')
url="http://proxytunnel.sourceforge.net"
license=('GPL')
depends=('openssl')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tgz
        fix-Makefile-ssl-FTBFS.patch
        migrate-from-SSLv3.patch)
md5sums=('d74472b89c3f3b3b0abf6bd809ae34c2'
         '2c6c2a4c37cd2f28ee09e92d00274334'
         '502197e4f4b7530e54933733f02c4112')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|PREFIX =/usr/local|PREFIX =/usr|g' Makefile
  patch -Np1 -i ../fix-Makefile-ssl-FTBFS.patch
  patch -Np1 -i ../migrate-from-SSLv3.patch
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
