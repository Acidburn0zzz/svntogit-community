# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: wizzomafizzo <wizzomafizzo@gmail.com>
# Contributor: Benjamin Andresen <benny@klapmuetz.org>

pkgname=phppgadmin
pkgver=5.6.0
pkgrel=1
pkgdesc="A web-based administration tool for PostgreSQL"
arch=('any')
url="https://github.com/phppgadmin/phppgadmin/"
license=('GPL')
depends=('php' 'php-pgsql')
backup=('etc/webapps/phppgadmin/config.inc.php')
source=("https://github.com/phppgadmin/phppgadmin/releases/download/REL_${pkgver//./-}/phpPgAdmin-$pkgver.tar.bz2")
sha256sums=('33e0fc7bb038872d2f1dd269f77c99258220a5f4f659f8d0f7b4290737a0dd35')

package() {
  _instdir="$pkgdir"/usr/share/webapps/phppgadmin
  mkdir -p ${_instdir} "$pkgdir"/etc/webapps/phppgadmin
  cd ${_instdir}
  cp -ra "$srcdir"/phpPgAdmin-$pkgver/* .
  cp ./conf/config.inc.php-dist "$pkgdir"/etc/webapps/phppgadmin/config.inc.php
  rm -f ${_instdir}/conf/config.inc.php
  ln -s /etc/webapps/phppgadmin/config.inc.php ${_instdir}/conf/config.inc.php
}
