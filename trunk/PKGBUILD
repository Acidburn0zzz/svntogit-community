# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Tobias Bohrmann (PlainTextField) <honeypot.carbage@aechelon.de>
# Contributor: Bernd Prünster (JesusMcCloud) <aur@berndpruenster.org>

pkgname=gnome-latex
pkgver=3.30.0
pkgrel=1
pkgdesc="LaTeX editor for the GNOME desktop"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/GNOME-LaTeX"
license=(GPL3)
depends=(gsettings-desktop-schemas gspell libgee tepl)
makedepends=(autoconf-archive appstream-glib git gobject-introspection gtk-doc intltool vala yelp-tools)
optdepends=('texlive-core: build LaTeX documents')
replaces=(latexila)
_commit=8468c6ab6a89f54d38bd54d2a1d21d8482accf2c  # tags/3.30.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-latex.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --enable-gtk-doc --disable-Werror
  make
}

package() {
	cd $pkgname
	make DESTDIR="$pkgdir" install
}
