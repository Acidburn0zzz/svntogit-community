# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=arch-repro-status
pkgver=1.2.4
pkgrel=1
pkgdesc="Check the reproducibility status of your Arch Linux packages"
arch=('x86_64')
url="https://gitlab.archlinux.org/archlinux/arch-repro-status"
license=('MIT')
depends=('pacman')
makedepends=('rust')
groups=('archlinux-tools')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('f42630870dbadb1c068cc7641facdbcd7443b9413b404f9172b13adaadc38d894fe21eb02bfc4a9bbc7cb2145fe8bd662a303695776b600484982c2bc5cf0ab4')

prepare() {
  cd "$pkgname-v$pkgver"
  mkdir completions/
  cargo fetch --locked
}

build() {
  cd "$pkgname-v$pkgver"
  cargo build --frozen --release
  OUT_DIR=completions/ cargo run --bin completions
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "completions/$pkgname.bash" "$pkgdir/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "$pkgdir/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "$pkgdir/usr/share/zsh/site-functions"
}
