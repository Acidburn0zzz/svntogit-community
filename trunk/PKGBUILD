# $Id: PKGBUILD 95035 2013-08-04 09:44:24Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

#_hgrev=63547

pkgname=pypy3
pkgver=2.1
#[[ -n $_hgrev ]] && pkgver=2.0beta2.$_hgrev
pkgrel=1
pkgdesc="A Python3 implementation written in Python, JIT enabled"
url="http://pypy.org"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python')
options=(!buildflags)
makedepends=('python' 'mercurial')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')
#source=("hg+https://bitbucket.org/pypy/pypy#revision=$_hgrev")
source=("https://bitbucket.org/pypy/pypy/downloads/$pkgname-$pkgver-src.tar.bz2")
md5sums=('7c43cd95aa37a7f0ca2465ed930646c0')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/goal

  python ../../rpython/bin/rpython -Ojit targetpypystandalone
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}-src/pypy/tool/release

  python package.py --without-tk ../../../ pypy pypy-c "${srcdir}"/${pkgname}.tar.bz2

  mkdir -p "${pkgdir}"/opt
  tar x -C "${pkgdir}"/opt -f "${srcdir}"/${pkgname}.tar.bz2
  mv pypy pypy3

  mkdir -p "${pkgdir}"/usr/bin
  ln -s /opt/pypy3/bin/pypy-c "${pkgdir}"/usr/bin/pypy3

  install -Dm644 "${pkgdir}"/opt/pypy3/LICENSE "${pkgdir}"/usr/share/licenses/pypy3/LICENSE
}
# vim: ts=2 sw=2 et:
