# $Id: PKGBUILD 176641 2016-05-25 06:11:43Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=python-dae
pkgver=1.0.1
pkgrel=3
pkgdesc='Deepin desktop application engine'
arch=('any')
url="https://github.com/linuxdeepin/dae"
license=('GPL3')
depends=('python-xlib' 'python-pyqt5')
makedepends=('git')
source=("git+https://github.com/linuxdeepin/dae.git#tag=$pkgver"
        python3.5.patch)
sha256sums=('SKIP'
            '901f388e2ec29f4f54ef8e71e6db8d879577d129efca27d8e5cd0d8b1b8c9954')

prepare() {
  cd dae
  # FS#52126 & https://cr.deepin.io/#/c/18253/
  patch -p1 -i ../python3.5.patch
}

build() {
  cd dae
  python setup.py build
}

package() {
  cd dae
  python setup.py install --root="$pkgdir" --optimize=1
}
