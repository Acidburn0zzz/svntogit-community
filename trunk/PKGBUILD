# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Megumi_fox <i@megumifox.com>

pkgname=qliveplayer
pkgver=3.21.0
pkgrel=1
pkgdesc='Cute and useful Live Stream Player with danmaku support.'
arch=('x86_64')
url="https://github.com/IsoaSFlus/QLivePlayer"
license=('GPL2')
depends=('ffmpeg' 'mpv' 'python-aiohttp' 'qt5-base' 'qt5-quickcontrols2' 'qt5-graphicaleffects'
         'qt5-quickcontrols' 'python')
makedepends=('extra-cmake-modules' 'ninja')
optdepends=('python-protobuf: for YouTube LiveChat support'
            'streamlink: for foreign streaming service support')
source=("https://github.com/IsoaSFlus/QLivePlayer/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('435747c5ec9f07d271cba99ca02c3932fdef86bc516f7491dd001502bc2c97db')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib ../QLivePlayer-$pkgver
    ninja
}

package() {
    cd build
    DESTDIR="$pkgdir" ninja install
}
