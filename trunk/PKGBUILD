# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgbase=python-texttable
pkgname=(python-texttable python2-texttable)
pkgver=1.6.0
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python-cjkwrap' 'python2-cjkwrap' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/foutaise/texttable/archive/v$pkgver.tar.gz")
sha512sums=('df685004f24b8c1e7aa1bd6a44c658599805f1a0c6999a4a3e95ecf3a68499f515bea9cb5ccd54f5ee2563af15e9fcde2a7e1827934e4a1fc83c9ae2f5e88a52')

build() {
  cd texttable-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd texttable-$pkgver
  pytest tests.py
  pytest2 tests.py
}

package_python-texttable() {
  depends=('python')
  optdepends=('python-cjkwrap: for better CJK support')

  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-texttable() {
  depends=('python2')
  optdepends=('python2-cjkwrap: for better CJK support')

  cd texttable-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
