# $Id$
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=openimageio
pkgver=1.0.9
pkgrel=4
pkgdesc='Library for reading and writing images, including classes, utilities, and applications'
arch=(i686 x86_64)
url="http://www.openimageio.org/"
license=('custom')
depends=('openexr' 'boost-libs' 'jasper' 'glew')
makedepends=('cmake' 'qt' 'python2' 'boost' 'mesa')
optdepends=('qt: iv image viewer'
            'python2: bindings support')
source=(https://github.com/OpenImageIO/oiio/tarball/Release-$pkgver)
md5sums=('4b98a057515cc95fc954743c987f19ba')

build() {
  cd "$srcdir"/$_pkgname*/src

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages ..
  make
}

package() {
  cd "$srcdir"/$_pkgname*/src/build

  make DESTDIR="$pkgdir" install

  # license
  cd ../..
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
