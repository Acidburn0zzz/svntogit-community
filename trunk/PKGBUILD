# Maintainer: Bart≈Çomiej Piotrowski

pkgbase=podman
pkgname=(podman podman-docker)
pkgver=1.0.0
pkgrel=2
pkgdesc='Tool and library for running OCI-based containers in pods'
arch=(x86_64)
url='https://github.com/containers/libpod'
license=(Apache)
depends=(cni-plugins conmon device-mapper iptables libseccomp ostree runc skopeo
         btrfs-progs slirp4netns)
makedepends=(go-pie go-md2man git)
source=(libpod-$pkgver.tar.gz::$url/archive/v${pkgver}.tar.gz
        0001-Revert-SELinux-support.patch)
sha256sums=('f7a462563dd587208eff3c3c0689bc4d01071a8f7933bec2a13126be123f63a8'
            '441380487cb62895d878084f98210affc30f65c2237c8ee9a1eae1adef7fa566')

prepare() {
  patch -p1 -i "$srcdir/0001-Revert-SELinux-support.patch" \
    -d libpod-$pkgver/vendor/github.com/containers/image

  mkdir -p src/github.com/containers src/github.com/varlink
  cp -r libpod-$pkgver src/github.com/containers/libpod
}

build() {
  export GOPATH="$srcdir"
  export BUILDTAGS='seccomp ostree varlink'

  # buildsystem passes LDFLAGS to GO's -ldflags, which isn't really compatible
  unset LDFLAGS

  cd src/github.com/containers/libpod
  make install.tools
  make
}

package_podman() {
  optdepends=('podman-docker: for Docker-compatible CLI')

  cd src/github.com/containers/libpod
  make install install.completions install.config DESTDIR="$pkgdir" PREFIX="$pkgdir/usr"
}

package_podman-docker() {
  pkgdesc='Emulate Docker CLI using podman'
  depends=(podman)
  conflicts=(docker)

  cd src/github.com/containers/libpod
  make install.docker DESTDIR="$pkgdir" PREFIX="$pkgdir/usr"
}
