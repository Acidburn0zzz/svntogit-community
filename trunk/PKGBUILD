# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-harparser
pkgname=('python-harparser' 'python2-harparser')
pkgver=0.3
_commit=f2f8b525c6045eab95ced0767322244a337ec536
pkgrel=1
pkgdesc="Python HAR Parser Utility"
arch=('any')
license=('MIT')
url="https://github.com/JustusW/harparser"
makedepends=('python' 'python2')
source=("$pkgbase-$_commit.tar.gz::https://github.com/JustusW/harparser/archive/$_commit.tar.gz")
sha512sums=('604f998e760bd9bf7f2caef5833fbc933da9c2b9deb94b72b09d00589616e22b99f4d7fad4600bd285998c281e08529ef9cd3e76d837a568351bc380f7236b25')

prepare() {
  mv harparser-{$_commit,$pkgver}

  sed -i 's/iteritems/items/' harparser-$pkgver/harparser.py

  cp -a harparser-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/harparser-$pkgver
  python setup.py build

  cd "$srcdir"/harparser-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/harparser-$pkgver
  python -m unittest test.log

  cd "$srcdir"/harparser-$pkgver-py2
  python2 -m unittest test.log
}

package_python-harparser() {
  depends=('python')

  cd harparser-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-harparser() {
  depends=('python2')

  cd harparser-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
