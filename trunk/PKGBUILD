# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest(at)gmail.com>

pkgname='python-tempora'
_pkgbase="${pkgname//python-/}"
pkgver=2.0.0
pkgrel=1
pkgdesc="Objects and routines pertaining to date and time (tempora)."
arch=('any')
url="https://github.com/jaraco/tempora"
license=('MIT')
depends=('python' 'python-six' 'python-pytz' 'python-jaraco')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-six'
             'python-pytz' 'python-jaraco')
checkdepends=('python-tox')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz"
        'LICENSE')
sha512sums=('9f488ecca0673a0071503475378bc8f5ddcfabb32fcc548392434df45767fbeb8b914117622ec6f9dc6a8de90e5bfe6e5571a9a18ddecf7c25e94fc1f7fff102'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944')

build() {
    # setuptools wont find version from git tag

    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py build
}

check() {
    # setuptools wont find version from git tag
    export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"

    cd "${srcdir}/${_pkgbase}-${pkgver}"
    tox
}

package() {
    cd "${srcdir}/${_pkgbase}-${pkgver}"
    python ./setup.py install --root="${pkgdir}" --optimize=1

    # the author has promised to include a LICENSE file in future releases:
    # https://github.com/jaraco/skeleton/issues/1
    # for now the LICENSE file has been taken from:
    # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
    install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
