# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.3.1
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="http://pwmt.org/projects/girara"
arch=('x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3' 'glib2' 'json-c' 'pango')
makedepends=('meson' 'ninja')

source=(https://pwmt.org/projects/girara/download/girara-$pkgver.tar.xz)

sha512sums=('5c4b2972bfb0296cd1b984c9426b67b50e29e2928fb0e9a3aa3f9bc21aebc28dcc93570e888060e0bb02561022b379cdfcfa3317b52e78cac04e7fc1ba476107')

build() {
  cd girara-$pkgver
  arch-meson build

  cd build
  ninja
}

package() {
  cd girara-$pkgver/build
  DESTDIR="$pkgdir/" ninja install

  install -D -m664 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
