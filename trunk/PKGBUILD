# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.4.4
pkgrel=1
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-flaky')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz"
        'fix-tests.patch')
sha512sums=('5fb95901641953a093a8d6c10d5b72ad9982dc8cc6161e769b30a861e4057100b861dbe9ae26c288b4fa1126b261f34c82afeb0052670429c1b7f97d1b2cf6ff'
            '398dd6d080bdd4d50905a32012fb33fb3d057d4e655a5a908c3ddf0f9f944a55ab5b20fd0545cb2b3e2d9ff2325c596cad8116012e6a281ca01414ead66be92e')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../fix-tests.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  # Debian API appears broken
  pytest -k 'not test_debianpkg'
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
