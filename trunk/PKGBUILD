# Maintainer: Connor Behan <connor.behan@gmail.com>
# Contributor: mar77i <mysatyre at gmail dot com>

pkgname=ming
pkgver=r3160.50098023
pkgrel=1
pkgdesc="SWF output library"
arch=('x86_64')
url="http://www.libming.net/"
license=('GPL' 'LGPL')
depends=('freetype2' 'libpng' 'flex' 'giflib')
makedepends=('bison' 'pkgconf' 'python' 'git')
options=('!emptydirs')
source=(git+https://github.com/libming/libming.git python3.patch pull-145.patch pull-179.patch)
sha256sums=('SKIP'
            '7794e62df85c94d1ab1e9de0fc11222f52b9920a4babed4a74906f6ed1bcfb1d'
            '863be1aadbd12d957b39e731ab4b7d7c18f5d5fe38b993020f86c4e7123072ff'
            '5e8281464ba030d228d3c6a1c45c6b7ca2a4a6c698f03022a4c66999458d8d63')

pkgver() {
  cd "$srcdir"/libming
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "$srcdir"/libming
  git apply ../pull-145.patch
  git apply ../pull-179.patch
  git apply ../python3.patch

  ./autogen.sh
  PYTHON=/usr/bin/python ./configure --prefix=/usr --enable-python

  # Strange that this can fail when we let the Makefile do it
  pushd src/actioncompiler
  bison -y -p swf4 -b swf4compiler swf4compiler.y
  bison -y -p swf5 -b swf5compiler swf5compiler.y
  popd
  make
}

package() {
  cd "$srcdir"/libming
  make DESTDIR="$pkgdir" install
}

