# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.29
pkgrel=2
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm' 'deepin-metacity' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go-pie' 'python2' 'git')
replaces=('deepin-wm-switcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/startdde/archive/$pkgver.tar.gz")
sha512sums=('75a88ff08410636c3c149a02107c767029f374d0350924937f641fece1f772e13e966d2b1fcdec43d873b8c2262fdb5cc047a0537f69daf30f1a83cc453cd0db')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/cryptix/wav

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install
}
