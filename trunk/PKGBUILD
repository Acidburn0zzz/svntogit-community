# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-tenacity
pkgname=('python-tenacity' 'python2-tenacity')
pkgver=4.5.0
pkgrel=1
pkgdesc='Retry code until it succeeeds'
arch=('any')
license=('Apache')
url='https://github.com/jd/tenacity'
makedepends=('python-setuptools' 'python2-setuptools' 'python2-futures' 'python2-monotonic'
             'python-pbr' 'python2-pbr')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/tenacity/archive/$pkgver.tar.gz")
sha512sums=('7f7975eb1685a8d08c94e2a59f2ad29d7ff8422acfff434803c0987c2cdabd4fe9f8055229af4edfdf97dac7f79ab5a850b6cb902b897afd759f598d618ddeec')

prepare() {
  cp -a tenacity-$pkgver{,-py2}
  sed -i '/monotonic/d' tenacity-$pkgver/requirements.txt
  sed -i 's/from monotonic import monotonic/from time import monotonic/' tenacity-$pkgver/tenacity/{__init__,async}.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py build

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/tenacity-$pkgver
  python setup.py nosetests

  cd "$srcdir"/tenacity-$pkgver-py2
  python2 setup.py nosetests --ignore-files '.*async.py'
}

package_python-tenacity() {
  depends=('python-six')

  cd tenacity-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-tenacity() {
  depends=('python2-six' 'python2-futures' 'python2-monotonic')

  cd tenacity-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
