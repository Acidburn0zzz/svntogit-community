# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidictl
pkgver=0.5.4
pkgrel=1
pkgdesc="MIDI remote control application sending MIDI data over network using UDP/IP multicast"
arch=('x86_64')
url="https://qmidictl.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('96acf87975aba8228a2bb1d60e7423a62aafc2e1f9c2a37b40749ee384d3a87849994eacbce00f0dd73d1dfd213d99933401c1de90bc40ef117807f17556b029')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
