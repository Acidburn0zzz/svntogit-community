# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.3.12
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://git.merproject.org/mer-core/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications' 'qt5-tools')
source=("$pkgname-$pkgver.tar.bz2::https://git.merproject.org/mer-core/fingerterm/repository/archive.tar.bz2?ref=$pkgver")
sha512sums=('bc0f9595bac5ec5da408620eae08fd86b283296e1359c171570adb6b1c3a97d2b52647c029c5057eefdb4891beff575d609d412ed09ccae06e6c03022130d455')

prepare() {
  rm fingerterm-$pkgver.tar.bz2
  mv fingerterm-* fingerterm-$pkgver
}

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}
