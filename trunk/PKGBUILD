# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.2.1.2
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('lib32-glibc' 'alsa-lib')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/alsa-lib-$pkgver.tar.bz2)
sha512sums=('e8286fd55f63ee0d95513279d0885c287533de89b7af6c338413dec5d38ba4f5a15da1a4a4ce36e052614e4b730e3778782dab9979d82958283be17b48604913')

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
}
