# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=5.2.0.3
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'polkit-qt5')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-polkit-agent/archive/$pkgver.tar.gz")
sha512sums=('821df238c612799362c7bab8707ffb02d11b11f6478295f6425ff7d4e9f446365d1a63bb1184b2befa7b6faf0d732753c2e1940f4877335ef0883ad173649440')

prepare() {
  cd dde-polkit-agent-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/1721
  sed -i 's/bool is_deepin = true/bool is_deepin = false/' policykitlistener.cpp
}

build() {
  cd dde-polkit-agent-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-polkit-agent-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
