# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-spec-validator
pkgver=0.3.0
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
depends=('python-jsonschema' 'python-openapi-schema-validator' 'python-six' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-flake8' 'python-pytest-cov'
              'python-tox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('9305d402a7ad69f94e28bed6476ba03095789700c79d8bf5c9db0308be2559d444eb1130d21db10dc1700baf1ef9151933eef8b2fd9ca6cb796fe8e0f9375326')

build() {
  cd openapi-spec-validator-$pkgver
  python setup.py build
}

check() {
  cd openapi-spec-validator-$pkgver
  python setup.py pytest
}

package() {
  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
