# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=sslsplit
pkgver=0.4.11
pkgrel=3
pkgdesc="Tool for man-in-the-middle attacks against SSL/TLS encrypted network connections"
url="https://www.roe.ch/SSLsplit"
arch=('i686' 'x86_64')
license=('BSD')
depends=('libevent' 'openssl')
checkdepends=('check')
source=(https://mirror.roe.ch/rel/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.asc}
        no-ssl3.patch)
sha512sums=('c3339b4d22eb8f94ad1bc161b2c2d9efcb2f2828985712a5580437077f4c0bd74657d42274c4a9118c92721b9bd1d8150cf83d89afef6144b20aba497ac1d69a'
            'SKIP'
            '0c80471a881d29d285385e00dc1d36d27d796648b0327748263b7b78dfa6d728f0d7feaca719df3b74c0741740a350604173c9cb6f44ba5e68e34bd47a293856')
validpgpkeys=('BFF9C7D7EA0EAC7F1AA55B3EFABE3324B5D3397E') # Daniel Roethlisberger <daniel@roe.ch>

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/no-ssl3.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 test
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX="${pkgdir}/usr" install
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 NEWS.md "${pkgdir}/usr/share/doc/${pkgname}/NEWS.md"
}

# vim: ts=2 sw=2 et:
