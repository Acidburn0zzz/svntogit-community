# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=5.2.0.19
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz"
        deepin-dock-fix-build.patch
        https://github.com/linuxdeepin/dde-dock/commit/1611be8c849d19fab44de5eecc663a7c38575e70.patch)
sha512sums=('792689c44c50b1d022d8ad10b1ca9f6036d1dbeec597240401b6e8d9620893ef3b31a9bce589091a6f3910d7553c16d56774f7a77a4f4dd1e1e541e005b2ea8e'
            '38c3f683e10c70f54123d445b80831eb74830b27f265fccf7eb4037bfb0c96879bf25203e8e672ebe483e8cae291197dfac81296648f08548d61a7644c0076f9'
            'f65a066dbbd27caec29e59dcb5f0328dd462cc5b86d4d899a10f04d9164c29729575230ab1be81f47ea4f4c9ee8bdeaa0a2b4d89fe82d30f8858509ebb2e289a')

prepare() {
  cd dde-dock-$pkgver
  patch -p1 -i ../deepin-dock-fix-build.patch
  patch -p1 -i ../1611be8c849d19fab44de5eecc663a7c38575e70.patch
}

build() {
  cd dde-dock-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  ninja
}

package() {
  cd dde-dock-$pkgver
  DESTDIR="$pkgdir" ninja install
}
