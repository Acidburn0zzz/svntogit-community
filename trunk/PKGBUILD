# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: totoloco <totoloco at gmail _dot_com>
pkgname=mysql-workbench
pkgver=5.2.31a
pkgrel=1
pkgdesc="MySQL Workbench is a cross-platform, visual database design tool developed by MySQL."
arch=('i686' 'x86_64')
url="http://dev.mysql.com/workbench/"
license=('GPL2')
depends=('libgnome' 'libzip' 'libmysqlclient' 'lua' 'gtkmm' 'libglade' 'libgl' 'libsigc++' 'python-paramiko' 'python-pexpect')
makedepends=('boost' 'curl' 'mesa')
options=('!libtool' 'force')
source=(ftp://mirror.anl.gov/pub/mysql/Downloads/MySQLGUITools/$pkgname-gpl-$pkgver-src.tar.gz
        ArchLinux.xml arch.patch python27.patch)
md5sums=('e91015265c55bd12735dff759e63c374'
         '668eed06edb1ba1b58c5219a4a45ed35'
         '23430d24400fd65c9fd872bd8e2411e5'
         '47bc4cdfaa035a0132392e94c462ebf7')

build() {
  cd "$srcdir/$pkgname-gpl-5.2.31-src"

  cp "$srcdir/ArchLinux.xml" res/mysql.profiles
  patch -Np1 -i $srcdir/arch.patch

  patch -Np0 -i $srcdir/python27.patch

  #we use System provided libraries
  rm -rf ext/curl
  rm -rf ext/yassl
  rm -rf ext/boost
  rm -rf ext/libsigc++

  export CPPFLAGS=$(pkg-config --cflags sigc++-2.0)
  unset LDFLAGS

  ./autogen.sh --prefix=/usr --disable-debug --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-gpl-5.2.31-src"

  make DESTDIR="$pkgdir" install
  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" $pkgdir/usr/share/mysql-workbench/sshtunnel.py
}
