# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.4.2
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'qt5-tools')
makedepends=('cmake')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('86ffac1023646ef33d38ade4c3db855acee405ad379d5b855e1145c4db5253a02aa50a8f1b0de9381f77cea996aaa5f3b4034f8dfd282892bbc79771f06d17bc')

prepare() {
  cd dde-launcher-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=1
  make
}

package() {
  cd dde-launcher-$pkgver
  make DESTDIR="$pkgdir" install
}
