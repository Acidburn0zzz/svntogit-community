# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>
# Contributor: SÅ‚awomir Kowalski <suawekk@gmail.com>

pkgname=percona-toolkit
pkgver=3.0.13
pkgrel=1
pkgdesc="Collection of advanced command-line tools to perform a variety of MySQL and system tasks."
url="https://www.percona.com/software/database-tools/percona-toolkit"
arch=('any')
license=('GPL')
depends=('perl-term-readkey')
makedepends=('perl-dbd-mysql' 'perl-dbi')
optdepends=(
    'perl-dbd-mysql: MySQL connections'
    'perl-dbi: MySQL connections'
)
options=('!emptydirs' 'purge')
source=("https://www.percona.com/downloads/$pkgname/$pkgver/source/tarball/$pkgname-$pkgver.tar.gz")
sha256sums=('21f68d1c5204a9cad7be716fd1e53f0fe6ff7d995292b56dbc7c55e3979432b1')

build() {
    cd "$srcdir"/$pkgname-$pkgver
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null
    perl Makefile.PL INSTALLDIRS=vendor
    make
}

package() {
    cd "$srcdir"/$pkgname-$pkgver
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/lib
}
