# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: simo <simo@archlinux.org>

pkgname=tor
pkgver=0.2.1.28
pkgrel=1
pkgdesc='Anonymizing overlay network.'
arch=('i686' 'x86_64')
url='http://www.torproject.org/'
license=('BSD')
depends=('openssl' 'libevent' 'tsocks' 'bash')
backup=('etc/tor/torrc'
        'etc/tor/torrc-dist'
        'etc/tor/tor-tsocks.conf')
install='tor.install'
source=("http://www.torproject.org/dist/${pkgname}-${pkgver}.tar.gz"
        'torrc'
        'tor')
md5sums=('11e8384414623b50fe50ba7da85893db'
         '56c75d4e8a66f34167d31e38c43793dd'
         '5b0cd40f880846f8bed2d241c4eb2469')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -dm0700 "${pkgdir}/var/lib/tor"

  mv "${pkgdir}/etc/tor/torrc.sample" "${pkgdir}/etc/tor/torrc-dist"
  install -Dm0644 "${srcdir}/torrc" "${pkgdir}/etc/tor/torrc"
  install -Dm0755 "${srcdir}/tor" "${pkgdir}/etc/rc.d/tor"

  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
