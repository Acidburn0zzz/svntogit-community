# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.6.1
pkgrel=1
pkgdesc="A fast AppStream metadata generator"
arch=(i686 x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb)
makedepends=(gdc meson ninja mustache-d curl)
source=("https://github.com/ximion/appstream-generator/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz" metainfo-no-summary-warning.patch)
sha256sums=('b7d893c99e4e74c9a90f48afb79ffe671ba19bbe48f37f00755ef0ca849acb9f'
            '6ae502ac0e5b6aad3d3617e0e32b6b13752baa97b4d5f051a298db707b2cb4e6')

prepare() {
  mkdir -p build

# Don't treat 'metainfo-no-summary' hints as errors, it excludes too many packages
  cd $pkgname-$pkgver
  patch -p1 -i ../metainfo-no-summary-warning.patch
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload_js=false --prefix=/usr
  ninja -j1
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
