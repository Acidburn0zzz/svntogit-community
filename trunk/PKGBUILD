# Maintainer: David Runge <dvzrv@archlinux.org>

_name=PyMySQL
pkgname=python-pymysql
pkgver=1.0.0
pkgrel=1
pkgdesc="Pure Python MySQL Client"
arch=('any')
url="https://github.com/PyMySQL/PyMySQL/"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'mariadb')
optdepends=('mariadb: for using a local MariaDB instance'
            'python-cryptography: for RSA cryptography'
            'python-pynacl: for ed25519 cryptography')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('305d2aebeca62f766f6067ca380f18203cf1f34b37086ccaf042d827e419310d7b81e78067ac860750473fe499089257253c7f9dd493e38ebcd1592c791086c6')
b2sums=('a9991d0373402bb37cd39724113d2eec3a9c172bac66fbc5fb376e249bf4d67a32626cc7b17c13197eb01721b37a9e13fbba48f6ea7f12b5f4305e1a5d1fced0')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
