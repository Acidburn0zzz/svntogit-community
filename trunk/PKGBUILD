# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=fclabels
pkgname=haskell-fclabels
pkgver=2.0.4
pkgrel=14
pkgdesc="First class accessor labels implemented as lenses."
url="https://github.com/sebastiaanvisser/fclabels"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        fclabels-template-haskell-2.16.patch::https://github.com/sebastiaanvisser/fclabels/pull/41.patch)
sha512sums=('bd1a292f5870046d58dfcbf65497e018f3b4e3b98aef263b2d81a8e02ff89b74e647db6bef141e9c898209d9a91d44e4098da156d8ebc470553c82a21b5d36a4'
            'b861939d19bbcb895eef3e38bdf5e1faa47163a4626964f8096010cd3863c8722bda3e444e57059368a80f0395189b147f87b98fc3ffd8711b63313e38e01547')

prepare() {
    cd $_hkgname-$pkgver
    patch -p1 -i ../fclabels-template-haskell-2.16.patch
    sed -i 's/< *4.14/<5/;s/< *2.16/<3/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
