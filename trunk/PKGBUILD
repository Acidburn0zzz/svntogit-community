# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-libthai
pkgver=5.0.3
pkgrel=1
pkgdesc="Libthai Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-libthai"
license=('GPL')
depends=('fcitx5' 'libthai')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('c2f8a212e30a0cd92a2eac9e01bad5818b0fb0265dd131f5220aa37b965c89985afab7798a6165dcf53defa74923a45b2e7387b89825a8efc474b092331a8572'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$pkgver
  DESTDIR="$pkgdir" ninja install
}
