# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.3
pkgrel=2
pkgdesc="Audio session manager"
arch=('x86_64')
url="https://github.com/linuxaudio/new-session-manager"
license=('GPL3')
depends=('fltk' 'gcc-libs' 'glibc')
makedepends=('jack' 'liblo' 'meson')
provides=('non-session-manager')
conflicts=('non-session-manager')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('8242e0766b0e51786e28f2fa40cbca1a7f3026acc9decb69edc14a8ed413c39cb673b1b2b52dab25fb74b5d8b7ec2de9f2544edad7bccb76d191ba9b8fe0def4')
b2sums=('ced59f49271f145643dde88b36dc639e2c5d7bccc5c10f48370755d26779b2e853a28b4784c0ee0032092a91378edc3ca3daa20744318d3ba8a6fd549e2351e8')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

package() {
  depends+=('libjack.so' 'liblo.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
