# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=pptpd
pkgver=1.3.4
pkgrel=11
pkgdesc="Poptop server"
arch=(i686 x86_64)
url="http://poptop.sourceforge.net/"
license=('GPL')
depends=(ppp glibc)
backup=(etc/pptpd.conf)
source=(http://downloads.sourceforge.net/sourceforge/poptop/pptpd-$pkgver.tar.gz
	pptpd
	pptpd.service
	unrecognized-opt.patch)
md5sums=('b38df9c431041922c997c1148bedf591'
         'e09f81beb5d59a0fdfba94e3a60d992b'
         '144c17c7d5c1b95cfb3450eb73aa2e50'
         'cfa02c86d0413eb94fac15599986f54c')

build() {
  local _pppver=`pacman -Q ppp`
  _pppver=${_pppver%-*}
  _pppver=${_pppver#ppp }

  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/unrecognized-opt.patch
  autoreconf -i
  ./configure --prefix=/usr --libdir=/usr/lib --exec-prefix=/usr --mandir=/usr/share/man
  sed -i "s|#define VERSION.*|#define VERSION \"${_pppver}\"|" plugins/patchlevel.h
  make
  sed -i 's|install -o root|install|g' plugins/Makefile
  sed -i 's|?=|=|g' plugins/Makefile
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  mkdir -p $pkgdir/etc/
  echo -e "# Read man pptpd.conf, see samples in /usr/share/doc/pptpd\n# and write your pptpd configuration here" >$pkgdir/etc/pptpd.conf
  install -Dm0755 ../pptpd $pkgdir/etc/rc.d/pptpd
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
  mkdir -p $pkgdir/usr/share/doc/pptpd
  cp -a samples $pkgdir/usr/share/doc/pptpd/
}
