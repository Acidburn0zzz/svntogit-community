# Maintainer:
# Contributor: Uro≈° Vampl <mobile.leecher at gmail dot com>

pkgname=tigervnc
pkgver=1.2.0
pkgrel=6
_xorgver=1.12.0
pkgdesc="Suite of VNC servers and clients based on TightVNC"
arch=('x86_64' 'i686')
url="http://www.tigervnc.org"
license=('GPL')
depends=('pam' 'gnutls' 'zlib' 'libjpeg-turbo' 'fontconfig' 'libsm' 'libxtst' 'libxi' 'libxfont' 'libxmu' 'libxkbfile' 'libxrender' 'libgl' 'pixman' 'xorg-xauth' 'xorg-xsetroot' 'xkeyboard-config')
makedepends=('cmake' 'nasm' 'xorg-util-macros' 'xorg-font-util' 'inputproto' 'xextproto' 'xproto' 'bigreqsproto' 'compositeproto' 'damageproto' 'dri2proto' 'fixesproto' 'fontsproto' 'randrproto' 'resourceproto' 'scrnsaverproto' 'videoproto' 'xcmiscproto' 'xineramaproto' 'xf86driproto' 'xf86vidmodeproto' 'glproto' 'renderproto' 'mesa' 'libpciaccess' 'xtrans')
conflicts=('tightvnc')
source=(http://downloads.sourceforge.net/tigervnc/${pkgname}-${pkgver}.tar.gz
        ftp://ftp.freedesktop.org/pub/xorg/individual/xserver/xorg-server-${_xorgver}.tar.bz2
        xorg111.patch
        xorg112.patch
        gnutls.patch
        gethomedir.patch
        cmakelists.patch)
md5sums=('3a5755b4ed600a81c3a17976c6f8420d'
         '3a3c4281f32c8702749e8f129372a355'
         'b680b55f7f9f569cd26ac0b330012a8b'
         '603772248ff2a7fdc80497427ab24d3f'
         '1a5fe8867ab3e49e20aafc70c415494f'
         'fa059baabe72308bf82622bca73cb3e0'
         '6ef923c71cc0e79c4b37eb3b124eea75')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  cp -r ${srcdir}/xorg-server-${_xorgver}/* unix/xserver
  for p in ${srcdir}/*.patch; do patch -p1 < $p; done

  cmake -G "Unix Makefiles" \
	-DCMAKE_INSTALL_PREFIX=/usr -DUSE_INCLUDED_FLTK=yes
  make

  cd unix/xserver
  patch -p1 < ../xserver110.patch
  autoreconf -fiv
  ./configure --prefix=/usr \
	--disable-static --disable-xinerama --without-dtrace \
	--disable-xorg --disable-xnest --disable-xvfb --disable-dmx \
	--disable-xwin --disable-xephyr --disable-kdrive --with-pic \
	--disable-config-dbus --disable-config-hal --disable-config-udev \
	--disable-unit-tests --disable-devel-docs --disable-dri \
	--enable-dri2 --enable-glx --enable-glx-tls
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install

  cd unix/xserver/hw/vnc
  make DESTDIR=${pkgdir} install
  rm ${pkgdir}/usr/lib/xorg/modules/extensions/libvnc.la

  # start vncconfig in no-window mode, the window is annoying :)
  sed -i 's/iconic/nowin/' ${pkgdir}/usr/bin/vncserver
}
