# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=1.11.0
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
replaces=("letsencrypt-apache")
depends=('certbot' 'python-acme' 'python-augeas' 'python-mock' 'python-setuptools'
         'python-zope-component' 'python-zope-interface')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('5f2772333f6e8229ccfaecb973959752eeaae69d9d209db41ed76cb180b50a3f2b79c55ee4aae7d6fa12e6bf6a401f95840b55b4d02f3a642c1966a23ea8f701'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  python -m pytest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
