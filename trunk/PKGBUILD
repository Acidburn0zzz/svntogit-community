# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Anatol Pomozov

pkgbase=python-txaio
pkgname=(python-txaio python2-txaio)
pkgver=2.7.1
pkgrel=1
pkgdesc='Compatibility API between asyncio/Twisted/Trollius'
arch=('any')
url='https://github.com/tavendo/txaio'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-twisted' 'python2-twisted')
source=("https://pypi.io/packages/source/t/txaio/txaio-$pkgver.tar.gz")
sha512sums=('d77a8b5e8fc21be0bef43f2a6795229456a351abc1a5eb6d2b7de0da0a58e67a228cb06d1a7962efcc760099ef5df94e9421875ea5628985260911efc7deb85e')

prepare() {
  cp -a txaio-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py build

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/txaio-$pkgver
  python setup.py test

  cd "$srcdir"/txaio-$pkgver-py2
  python2 setup.py test
}

package_python-txaio() {
  depends=('python-six')

  cd txaio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm "$pkgdir"/usr/LICENSE
}

package_python2-txaio() {
  depends=('python2-six')

  cd txaio-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm "$pkgdir"/usr/LICENSE
}
