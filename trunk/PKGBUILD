# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pyaml
pkgname=(python-pyaml python2-pyaml)
pkgver=17.10.0
pkgrel=1
pkgdesc="Low-level CSS parser for Python"
url="https://pypi.python.org/pypi/pyaml"
license=('BSD')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml')
checkdepends=('python-unidecode' 'python2-unidecode')
source=("https://pypi.io/packages/source/p/pyaml/pyaml-$pkgver.tar.gz")
sha512sums=('7d7dd424308cadcbfa76c852c01dce6dc611d909d35e3bf081be7866246836b4d31aaea65aa975b162a22203da586f249e6d244dcaf6d2971123f9e396fda649')

prepare() {
  cp -a pyaml-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/pyaml-$pkgver
  python setup.py build

  cd "$srcdir"/pyaml-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pyaml-$pkgver
  python setup.py test

  cd "$srcdir"/pyaml-$pkgver-py2
  python2 -m unittest pyaml.tests.dump
}

package_python-pyaml() {
  depends=('python-yaml')
  optdepends=('python-unidecode: Needed if same-id objects or recursion is used within serialized data')

  cd pyaml-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}

package_python2-pyaml() {
  depends=('python2-yaml')
  optdepends=('python2-unidecode: Needed if same-id objects or recursion is used within serialized data')

  cd pyaml-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
