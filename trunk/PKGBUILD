# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: gmes78 <gmes.078 at gmail dot com>

pkgname=python-qasync
pkgver=0.11.0
pkgrel=1
pkgdesc="Python library for using asyncio in Qt-based applications"
arch=(any)
url="https://github.com/CabbageDevelopment/qasync"
license=('BSD')
depends=('python' 'qt5-python-bindings')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pyqt5' 'pyside2' 'xorg-server-xvfb')
source=("https://github.com/CabbageDevelopment/qasync/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('046261af47bdbc3de38bd183bfe8309602ac6f6bbaed99bac533ac9be7f8669d9302691edeb76cc7d0a524bb782386ee4392a14917c760f9b11fbb94ef0abec4')

build() {
  cd qasync-$pkgver
  python setup.py build
}

check() {
  cd qasync-$pkgver
  QT_API=PyQt5 xvfb-run python -m pytest
  QT_API=PySide2 xvfb-run python -m pytest
}

package() {
  cd qasync-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
