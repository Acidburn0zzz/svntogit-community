# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=vmpk
pkgver=0.7.1
pkgrel=1
pkgdesc="Virtual MIDI Piano Keyboard"
arch=('x86_64')
url="http://vmpk.sourceforge.net/"
license=('GPL3')
groups=('pro-audio')
depends=('drumstick' 'qt5-x11extras')
makedepends=('cmake' 'docbook-xsl' 'qt5-tools' 'libxkbcommon-x11')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver/a/}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('134e221aac195da1636182966b32c83ae7974a8ac5f023a931b9c19044b08ef27be54c2f53bac2885516f7405570ec9d51b1bef9c77bf8ade5d9ab372bf18960')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,ChangeLog,NEWS,README,TODO}
}
