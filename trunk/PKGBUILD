# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BShapr
_plugin_uri="https://www.jahnichen.de/plugins/lv2/BShapr"
pkgname=bshapr
pkgver=0.6
pkgrel=1
pkgdesc="Beat/ envelope shaper LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BShapr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint' 'sord')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('da34fba526cf6ecb14413e6f8d5f9841be0b7e1b3c3320a4588fa87f47bda3e0f8d5efbcf0ccd121b3cd813fc5b84138461b6a75610abb92e3f1b0e9773a6e27')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

check() {
  cd "$pkgname-$pkgver"
  sord_validate "${_name}.lv2/"*.ttl || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
  LV2_PATH="${PWD}" lv2lint "${_plugin_uri}" || echo "Known to fail: https://github.com/sjaehn/BShapr/issues/7"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
