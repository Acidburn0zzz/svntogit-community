# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=6.2.0
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='https://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('b12d126709b9500a08f3717e7207689b3d1c04d3ecf809d1bc46e8358796beb67c51c0c70aeff99aea1cc280cbd93907abdb2490fd06bd98e6c08f071bb4b7b3')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
