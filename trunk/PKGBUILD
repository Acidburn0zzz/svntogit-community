# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=0.0.4.1
pkgrel=1
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'kwin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz"
        new-kwin.patch)
sha512sums=('c6a11ddcf876605726e318e9e6dda609149a35cb4e2c946339c8aee5ad16d4d61b877e27f787f33bfaea0b3aa3dca8ac4d6da3f5587698f21ae2e78029576a18'
            '771ac8b0b80f15e46fa857ab5ee7c5f28ac79fc82e95999077ffbf8dfa63669a9e6c746f355438211145390ea3af108b31b7683e5193b82ccd9cc00d24abb3f2')

prepare() {
  cd dde-kwin-$pkgver
  patch -p1 -i ../new-kwin.patch
}

build() {
  cd dde-kwin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-kwin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
