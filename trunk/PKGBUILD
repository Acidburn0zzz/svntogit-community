# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Aram Drevekenin <aram@poor.dev>

pkgname=bandwhich
pkgver=0.9.0
pkgrel=1
pkgdesc='Terminal bandwidth utilization tool'
url='https://github.com/imsnif/bandwhich'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=(https://github.com/imsnif/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a5552d91dfe013669b0c60ffc6393bc808a7d758dc0ff2960f55d0fea2013a0cf5ee47d2aff7ad95126e264bb64c5e4a1ad973a2b34705ff6cf629c9651820e3')
b2sums=('48fcaf7348fee4246aec8f65d46bdc9b3a981aeb5c745e98eb064b9446f13e8764df0e50fc13204ebcd24e98b73119f40a07b4dc62cd760ee20949d8e37e9f01')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 "docs/bandwhich.1" -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
