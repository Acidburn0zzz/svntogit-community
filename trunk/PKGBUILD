# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.13.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('69a187adbb60d8ecf4e3ab37454c874895a6ed375b11573be9277821cbc3a8cf76e2a470e96633e8a9f787437923b78587a38dbdc204c095d7422e5fb2a18bbc')
b2sums=('1542e37bf6ea0620d2559ec9e709c7ded7d03c742801ab814cad408df3b3941ac9d1ae685e801dddc0b820366d93694cc85ba1ade3c3bd4f5b78dbfa089d10bd')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
