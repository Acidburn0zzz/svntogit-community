# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-pdoc
pkgver=7.1.0
pkgrel=1
pkgdesc='A simple program and library to auto generate API documentation for Python modules.'
arch=('any')
url='https://pdoc.dev/'
license=('Unlicense')
depends=('python-jinja' 'python-pygments' 'python-markupsafe')
checkdepends=('python-pytest' 'python-pytest-timeout' 'python-hypothesis')
source=("https://github.com/mitmproxy/pdoc/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('42dbb24f108c226dea9cd55247128db64af7f32cad5b0ffc48db4e1eafd17b913a704b6c86dc046f2ed89b4ef78ac86c8059b9e9fc13f71061af4efd5d24f0bc')

build() {
  cd pdoc-$pkgver
  python setup.py build
}

check() {
  cd pdoc-$pkgver

  # test #11 of test_snapshot.py currently fails
  pytest --deselect test/test_snapshot.py::test_snapshots[html-snapshot11]
}

package() {
  cd pdoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
