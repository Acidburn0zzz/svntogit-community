# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-image-viewer
pkgver=1.2.11
pkgrel=1
pkgdesc='Deepin Image Viewer'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-image-viewer"
license=('GPL3')
depends=('deepin-tool-kit' 'libexif' 'freeimage' 'libraw' 'qt5-svg' 'deepin-shortcut-viewer'
         'deepin-manual')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-image-viewer/archive/$pkgver.tar.gz")
sha256sums=('94fc8859608dcd5c4bd9221c95c64f325b817f29771784d885d1401d6da0c75f')

build() {
  cd deepin-image-viewer-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd deepin-image-viewer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
