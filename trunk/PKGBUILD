# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Richard Murri <admin@richardmurri.com>
pkgname=jython
pkgver=2.5.1
pkgrel=1
pkgdesc="An implementation of the Python language written in Java"
arch=('any')
url="http://www.jython.org/"
license=('PSF' 'APACHE' 'custom')
depends=('java-runtime')
options=('!emptydirs')
install='jython.install'
source=(http://downloads.sourceforge.net/$pkgname/${pkgname}_installer-${pkgver}.jar)
noextract=(${pkgname}_installer-${pkgver}.jar)
md5sums=('2ee978eff4306b23753b3fe9d7af5b37')
build() {
	cd "$srcdir"
	java -jar ${pkgname}_installer-${pkgver}.jar -s -t standard -d "$pkgdir"/opt/jython
	sed -i s*"${pkgdir}"**g "${pkgdir}"/opt/jython/jython
	sed -i 's#/opt/java/jre/bin/java#java#g' "${pkgdir}"/opt/jython/jython
	install -m755 -D "$pkgdir/opt/jython/jython" "$pkgdir/usr/bin/jython" || return 1
	rm "$pkgdir/opt/jython/jython" "$pkgdir/opt/jython/bin/jython" || return 1
	install -m644 -D "$pkgdir/opt/jython/LICENSE.txt" \
	    "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt" || return 1
}
