# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.2.3
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('44bf3b845c9ef93cc539645765af63b037fc9e421f0a97c848946399de13c1d47df86efe63b0bb50fbd54a76912d3bcc7f7f972e7f77c491933d20243492a7f1')
b2sums=('0e56b7443f19e0956b72c428b2cf4c741b10cb38e6cad3efa6fa130d3ac1ec84e351d049c58bac9098b85cd32d64459f96e2cd3f13ee43b6cf7190c1894b5a88')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
