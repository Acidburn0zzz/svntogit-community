# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.4.4
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'ca-certificates')
makedepends=('cmake' 'boost')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json'
        'etc/trojan/config.json')
sha512sums=('77aa2b884afa599f5b8ae5584c8a269bf3461d42ea28b7e69ed8f1473a6e5e599ab7cc44c883538c18ff9448a58a70effd8d7fc587ec82d4df268aa0efef4086')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
