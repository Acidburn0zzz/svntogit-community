# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: shild <sxp@bk.ru>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

_ghcver=`pacman -Q ghc | cut -d\  -f 2 | cut -f 1 -d -`
pkgname=haskell-x11
pkgver=1.5.0.0
pkgrel=3
pkgdesc="A Haskell binding to the X11 graphics library."
arch=(i686 x86_64)
url="http://hackage.haskell.org/cgi-bin/hackage-scripts/package/X11"
license=('BSD')
depends=("ghc=${_ghcver}" 'libx11')
options=('strip' 'force')
conflicts=(haskell-x11-extras)
install=hsmod.install
source=(http://hackage.haskell.org/packages/archive/X11/$pkgver/X11-$pkgver.tar.gz)
md5sums=('ec6670f5ce1812206903c8a0fd9df04c')

build() {
  cd $srcdir/X11-$pkgver

  runhaskell Setup.hs configure --ghc --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid || return 1

  runhaskell Setup.hs build || return 1
  runhaskell Setup haddock || return 1

  runhaskell Setup.hs register --gen-script
  runhaskell Setup.hs unregister --gen-script
  sed -i 's/ghc-pkg unregister/ghc-pkg unregister --force/' unregister.sh

  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.hs copy --destdir=$pkgdir || return 1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/$pkgname/BSD3
}
