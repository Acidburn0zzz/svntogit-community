# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-bopomofo
pkgver=0.0.0.20210124
_commit=c19f19edc4820a7743f8ea59573f0277c26a9f67
pkgrel=1
pkgdesc="Bopomofo input for rime"
arch=('x86_64')
url="https://github.com/rime/rime-bopomofo"
license=('LGPL')
# Rime will regenerate yaml&prism bin, which makes terra-pinyin required on runtime (for translator)
# rime-cangjie is dependency for reverse lookup
depends=('rime-cangjie' 'rime-terra-pinyin')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-bopomofo/archive/$_commit/rime-bopomofo-$_commit.tar.gz")
sha512sums=('0ed843307a302c61e41b0b6ef8b076b94af9bd693f1e0575af2421186deb1a9035aa6860e23416123deb63d10a07f8c88f13d6784f446324c6477df517b61265')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay rime-terra-pinyin | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/terra_pinyin.*
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
