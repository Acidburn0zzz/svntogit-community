# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=python-pycares
pkgver=3.2.3
pkgrel=1
pkgdesc='Python interface for c-ares'
arch=('x86_64')
url=https://github.com/saghul/pycares
license=('MIT')
depends=('python-cffi') # Modified c-ares is bundled
makedepends=('python-setuptools')
#checkdepends=('python-idna')
optdepends=('python-idna')
source=("https://files.pythonhosted.org/packages/source/p/pycares/pycares-$pkgver.tar.gz")
sha256sums=('da1899fde778f9b8736712283eccbf7b654248779b349d139cd28eb30b0fa8cd')
b2sums=('78cdfa981b586a2ee29c587a0a864e457a794f63a629e9bad0d5010f2be51b7f47a10ba8942a4dfa7ec1ff209d2bae932bb70404b46098b21cbb65363bdc5003')

build() {
  cd pycares-$pkgver
  python setup.py build
}

# Test failures: gethostbyaddr{,6}, gethostbyname{,_small_timeout}, test_getnameinfo
#check() {
#  cd pycares-$pkgver
#  mkdir -p temp
#  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
#  python setup.py install --skip-build --root=temp
#  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" python tests/tests.py
#}

package() {
  cd pycares-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
