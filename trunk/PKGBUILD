# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=python-pygit2
pkgver=1.6.1
pkgrel=2
pkgdesc='Python bindings for libgit2'
arch=('x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.27.0' 'python-cffi' 'python-six' 'python-cached-property')
makedepends=('python-cffi' 'python-setuptools' 'git' 'python-six')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz"::$url/archive/v$pkgver.tar.gz
        libgit2-1.2.x.patch::$url/commit/c777aa33f899d9199fb71716e312603fadef6502.patch)
sha256sums=('2e3fe066bb9a94694e6f87a54f28fffc3ff834fe3b1ff62036f4c9ec8aad1846'
            '6f05c053a9a6d8f1f091b662c58aabce85dd62f1ed22b5a3a16934e18abb448e')

prepare() {
	cd "pygit2-$pkgver"
	# Upstream Issue: https://github.com/libgit2/pygit2/pull/1089
	patch -p1 < ../libgit2-1.2.x.patch
	# Disable tests that do stuff online
	sed -i -e '/has_network/s/True/False/' test/utils.py
}

build() {
	cd "pygit2-$pkgver"
	python setup.py build
}

check() {
	cd "pygit2-$pkgver"
	local _pyver==$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
	# Disabled tests are related to the libgit2-1.2.x patch, re-enable when
	# upstream supports it properly
	python setup.py pytest --addopts "
		--deselect test/test_branch_empty.py
		--deselect test/test_remote_prune.py
		--deselect test/test_remote.py
		--deselect test/test_remote_utf8.py
		--deselect test/test_repository.py
		"
}

package() {
	cd "pygit2-$pkgver"
	LANG=en_US.UTF8 python setup.py install --root="${pkgdir}" --optimize=1
}
