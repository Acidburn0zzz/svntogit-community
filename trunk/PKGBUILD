# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=42.1.44
pkgrel=1
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-trollop')
options=('!emptydirs')
source=(https://rubygems.org/downloads/${pkgname}-${pkgver}.gem)
noextract=(${pkgname}-${pkgver}.gem)
sha512sums=('84379fdfabdcf585c2d5535659741417e5dc500b8f21b397fededdea86769922761a3a5846b837b86b7e15ceae50270fea0d385b961b83f53f751863f5c8a057')

package() {
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${pkgname}-${pkgver}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 "${pkgdir}${_gemdir}/gems/${pkgname}-${pkgver}/README.md" "${pkgdir}/usr/share/doc/${pkgname}/README"
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
