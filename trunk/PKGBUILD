# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-gtk
pkgver=0.0.0.20200607
_commit=fc335f1d6be8820d021db282cf90b746dc7d9b7c
pkgrel=1
pkgdesc="Fcitx5 gtk im module and glib based dbus client library"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-gtk"
license=('GPL')
depends=('fcitx5')
makedepends=('extra-cmake-modules' 'gobject-introspection' 'gtk2' 'gtk3' 'ninja')
source=("https://github.com/fcitx/fcitx5-gtk/archive/$_commit/fcitx5-gtk-$_commit.tar.gz")
sha512sums=('b634980a1abb465235d6c1e157403e91cf1aaf0373e67a63dfeea49bbc2383ea2c754eed731bedc34ab616f3c330b4f7a4ba3f9dcdad389f6d0528eeeae099be')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
