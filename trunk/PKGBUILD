# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.1.8
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'deepin-tool-kit' 'qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher'
         'deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('1a1c0f3a0eb3b3818153c0c318fee14128e7b4ff8b59c7eaa4526a5e815824df8a50791508cc8d413b9621e47baa2116b3c119cf59accddaedc7e2145d05ef59')

build() {
  cd dde-dock-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
