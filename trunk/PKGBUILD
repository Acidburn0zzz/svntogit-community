# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname=msgpack
pkgname="ruby-${_gemname}"
pkgver=1.4.4
pkgrel=1
pkgdesc='MessagePack implementation for Ruby'
url='https://github.com/msgpack/msgpack-ruby'
arch=('x86_64')
license=('Apache')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-yard' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('b5557c1c181bb0ea8c6e9ad4c3671da450e5e00344f47f74c860201cf3453929')
sha512sums=('ddf0d65a9293d339a5deebe27ed79f5e2f64d89ac9609ca95bae4f9f1e660c80727167b752cf2ce71faafb4264a13fb4a0d180de15f2501369a2b19f71504b70')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"
  sed 's|git ls-files|find|' -i "${_gemname}.gemspec"
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd "${_gemname}-ruby-${pkgver}"
  rake compile
  rake build
  rake clean
}

check() {
  cd "${_gemname}-ruby-${pkgver}"
  rake spec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}/${_gemdir}" \
    --bindir "${pkgdir}/usr/bin"  \
    "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/"${_gemname}-${pkgver}"/{ext,tmp,lib/*/*.so} \
    extensions/*/*/"${_gemname}-${pkgver}"/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
