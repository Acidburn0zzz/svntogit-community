# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=gobby
pkgver=0.5.0
pkgrel=6
pkgdesc="A collaborative text editor"
arch=('x86_64')
url="https://gobby.github.io/"
license=('GPL')
depends=('gtkmm' 'gtksourceview2' 'libinfinity' 'libxml++2.6')
makedepends=('gnome-doc-utils' 'intltool')
source=(http://releases.0x539.de/${pkgname}/${pkgname}-${pkgver}.tar.gz{,.sig})
sha256sums=('8ceb3598d27cfccdf9c9889b781c4c5c8e1731ca6beb183f5d4555644c06bd98'
            'SKIP')
validpgpkeys=('B63A7560B25B619CA11E219225A941E879B57591') # Armin Burgmeier <armin@arbur.net>

build() {
  cd "$srcdir"/gobby-$pkgver
  [ -x configure ] || ./autogen.sh
  CXXFLAGS+=' -std=c++11'
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/gobby-$pkgver
  make DESTDIR="$pkgdir" install
}
