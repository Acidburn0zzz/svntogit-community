# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.16.3
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base' 'gsettings-qt')
makedepends=('qt5-tools' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz")
sha512sums=('6f4e6e4daf16f6c7bac170b8879f0036f7bc5c3e42e408d9e2e1160bd807dfe181e57cd13981f74da0ec9f4a7c50ba40eb55728df6489d24c33b9cdabb04c272')

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
