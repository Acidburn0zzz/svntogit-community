# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.8.12
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-numpy' 'python2-numpy')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('4d0f59623a62eeab3c10e95b4e8153a1593219bcbcbbe5f87e75ba0a010b62021a9502faf715623faa78b4e397f1a74b41e9bffd1ede92dbebf87b0d4a59b66b')

prepare() {
  cp -a construct-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/construct-$pkgver
  python setup.py build

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct-$pkgver
  python setup.py pytest

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py pytest
}

package_python-construct() {
  depends=('python')

  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')

  cd construct-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
