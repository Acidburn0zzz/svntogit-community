# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: dracorp aka Piotr Rogoza <piotr.r.public at gmail.com>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-par
pkgver=1.016
pkgrel=3
pkgdesc='Perl Archive Toolkit'
arch=('any')
url='https://metacpan.org/release/PAR'
license=('GPL')
depends=('perl-archive-zip' 'perl-par-dist')
options=('!emptydirs' purge)
source=("https://cpan.metacpan.org/authors/id/R/RS/RSCHUPP/PAR-${pkgver}.tar.gz")
b2sums=('247e48b9de6dffc73cf78f8bc5c9114edde49634ef91412c765202529e0bcfeb5eee180c40cc04ac5bfbe02c2c12f6eaf2926f2b7a4178a102a607e36301c9e3')

build() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  perl Makefile.PL
  make
}
check(){
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  export PERL_MM_USE_DEFAULT=1
  make test
}
package() {
  cd PAR-${pkgver}
  unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
  make install INSTALLDIRS=vendor DESTDIR="${pkgdir}"
}
