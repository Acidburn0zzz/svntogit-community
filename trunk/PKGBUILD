# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=9.7
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL3)
depends=(qt6-imageformats)
makedepends=(qt6-tools)
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('98d08b4c36cc991e29293c8764a0624394f78bd6f8c5afc19a7ce9840c1684a7')

build() {
  cd $_pkgname-$pkgver
  /usr/lib/qt6/bin/lrelease gpxsee.pro
  qmake6 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  # Install appdata
  install -Dm644 pkg/appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
