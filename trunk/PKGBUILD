# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=happy
pkgver=1.19.5
pkgrel=7
pkgdesc="The Parser Generator for Haskell"
url="http://hackage.haskell.org/package/happy"
arch=('i686' 'x86_64')
license=("custom:BSD3")
depends=('gmp' 'libffi')
makedepends=('ghc=8.0.1' 'haskell-mtl')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
md5sums=('ffa56a1c312a60dfb1110afd25b28785')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --ghc-option='-dynload=deploy' --prefix=/usr --datasubdir="$pkgname"
  runhaskell Setup build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  runhaskell Setup copy --destdir="${pkgdir}"
  rm -r "$pkgdir/usr/share/doc"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
