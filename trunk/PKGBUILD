# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkwidget
pkgver=2.0.1.20171020
_commit=6ebada94f946dec59a252acee7f643f149f4de48
pkgrel=1
pkgdesc='Deepin graphical user interface library'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkwidget"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'dtkcore' 'librsvg' 'gsettings-qt' 'qt5-multimedia' 'qt5-svg'
         'qt5-x11extras' 'startup-notification')
makedepends=('qt5-tools')
replaces=('deepin-tool-kit')
conflicts=('deepin-tool-kit<0.3.4')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/dtkwidget/archive/$_commit.tar.gz")
sha512sums=('5f7c13f01fc2d41b663f67790194ac530f8ec6cd6fcbc208dfdc7b0e1cb526c67fe88871e31f585e446da7ff0026c75fee1a776673ac9afa21765622b3867dba')

prepare() {
  mv dtkwidget-{$_commit,$pkgver}
}

build() {
  cd dtkwidget-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkwidget-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
