# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>

_pkgbasename=libpulse
pkgname=lib32-$_pkgbasename
pkgdesc="A featureful, general-purpose sound server (32-bit client libraries)"
pkgver=14.2
pkgrel=1
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/PulseAudio/"
license=(LGPL)
depends=(lib32-{dbus,libasyncns,libcap,libxtst,libsm,libsndfile,systemd}
         $_pkgbasename)
makedepends=(lib32-{speexdsp,glib2,gtk3} git meson)
optdepends=('lib32-alsa-plugins: ALSA support')
provides=("lib32-pulseaudio=$pkgver")
conflicts=(lib32-pulseaudio)
replaces=(lib32-pulseaudio)
_commit=6a487c5244563f49a025088de3510c80824fc99e  # tags/v14.2^0
source=("git+https://gitlab.freedesktop.org/pulseaudio/pulseaudio.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd pulseaudio
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  cd pulseaudio

  # Freeze version before patching
  ./git-version-gen doesnt-exist >.tarball-version

  # Pick some ACP updates required by pipewire
  git cherry-pick -n \
    a28f2e7293a055588f7bba568b5049ff91b3aad0 \
    69ba5a2b58ad6e98c8899aa297afd8276dbbe052 \
    05c373d939506f31f96e58d8390b92e737e12afc \
    a73ec2a3f4f03829575761dc17546cf3226861c7 \
    71aa4720a4194e9602271634d9042b8e56a8217f \
    7ec6ee4725a72b42ddcb38550efc968a06e6be2f \
    836ba89c4a3cd15efa33f9e70ed8e4764bcb3793 \
    19e34d8d5bb9380ed70607b3f661c26df6d4836c
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pulseaudio build \
    --libdir /usr/lib32 \
    --auto-features auto \
    -D man=false \
    -D tests=false \
    -D avahi=disabled \
    -D bluez5=false \
    -D gcov=false \
    -D pulsedsp-location='/usr/\$LIB/pulseaudio' \
    -D stream-restore-clear-old-devices=true \
    -D udevrulesdir=/usr/lib/udev/rules.d
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

_pick() {
  local f d
  for f; do
    d="$pkgdir/${f#$srcdir/install/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package() {
  DESTDIR="$srcdir/install" meson install -C build

  cd install
  _pick usr/lib32/libpulse{,-simple,-mainloop-glib}.so*
  _pick usr/lib32/{cmake,pkgconfig}
  _pick usr/lib32/pulseaudio/libpulse{dsp,common-*}.so
}
# vim:set sw=2 et:
