# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kr1ss $(echo \<kr1ss+x-yandex+com\>|sed s/\+/./g\;s/\-/@/)
# Contributor: Alexander GÃ¶rtz <aur@nyloc.de>
# Contributor: Dan Beste <dan.ray.beste@gmail.com>
# Contributor: Julien Nicoulaud <julien dot nicoulaud at gmail dot com>
# Contributor: stef204 <https://aur.archlinux.org/account/stef204>

pkgname=borgmatic
pkgver=1.5.16
pkgrel=1
pkgdesc='Simple, configuration-driven backup software for servers and workstations'
arch=('any')
url=https://torsion.org/borgmatic
license=('GPL3')
depends=('borg' 'python-colorama' 'python-jsonschema' 'python-requests'
         'python-ruamel-yaml' 'python-setuptools')
checkdepends=('python-flexmock' 'python-pytest' 'vim')
optdepends=('python-llfuse: for mount action')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('8a4daef5494a2e7064518d5b33b92bc19021f39666c64b2986a25e64d14ee31a')
b2sums=('14eb511202b30b570cdf1097a68f732af09a6412e72dddaad0d01982f3375e1f0acf77546222b0166066d27ed6ac450db4e9cc9347eb7af972aa1df023ef0168')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,root/.local,usr,' sample/systemd/$pkgname.service
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  mkdir -p temp
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  python setup.py install --root=temp --optimize=1 --skip-build
  PATH="$PWD/temp/usr/bin:$PATH" PYTHONPATH="$PWD/temp/$site_packages" pytest -c /dev/null --ignore=tests/end-to-end
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system sample/systemd/*
}

# vim: ts=2 sw=2 et:
