# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Contributor: Gilbert Kennen <gilbert firewatcher org>

pkgname=elixir
pkgver=1.7.2
pkgrel=1
pkgdesc="a functional meta-programming aware language built on top of the Erlang VM"
url="http://elixir-lang.org"
arch=('any')
license=('Apache' 'custom:EPL')
depends=('erlang-nox')
checkdepends=('git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/elixir-lang/elixir/archive/v$pkgver.tar.gz")
sha256sums=('3258eca6b5caa5e98b67dd033f9eb1b0b7ecbdb7b0f07c111b704700962e64cc')

build() {
  cd elixir-"$pkgver"
  make
}

check() {
  cd elixir-"$pkgver"
  make test_erlang
}

package() {
  cd elixir-"$pkgver"
  mkdir -p "$pkgdir"/usr/share/licenses/"$pkgname"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"
  make DESTDIR="$pkgdir" PREFIX=/usr install
}
