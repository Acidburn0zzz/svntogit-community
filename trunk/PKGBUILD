# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
pkgver=0.3
pkgrel=1
_pkgname_test=vis-test
_pkgver_test=0.1
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('i686' 'x86_64')
url='http://www.brain-dump.org/projects/vis/'
depends=('ncurses' 'libtermkey' 'lua' 'lua-lpeg' 'tre' 'file')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/martanne/${pkgname}/archive/v${pkgver}.tar.gz"
        "${_pkgname_test}-${_pkgver_test}.tar.gz::https://github.com/martanne/${_pkgname_test}/archive/v${_pkgver_test}.tar.gz")
sha256sums=('bed8ed32ef4f81bc4fd28e297ea8edc80027ac6923b9d3b5bd955d24899c7077'
            '78c2c4416ef284e2362bb0e761ee9c4add106d122ac44b341c6bcf449f0b17a6')

prepare() {
	cd "${pkgname}-${pkgver}/"

	rmdir test/
	mv "../${_pkgname_test}-${_pkgver_test}" test
}

build() {
	cd "${pkgname}-${pkgver}/"

	./configure \
		--prefix=/usr

	make
}

check() {
	cd "${pkgname}-${pkgver}/"

	make -C test/
}

package() {
	cd "${pkgname}-${pkgver}/"

	make DESTDIR="${pkgdir}" install

	install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

	install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

