# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Konstantin Shalygin <k0ste@k0ste.ru>

pkgbase='ovirt-engine-sdk'
pkgname=('python-ovirt-engine-sdk' 'python2-ovirt-engine-sdk')
pkgver='4.1.3'
pkgrel='1'
pkgdesc='Python access to the oVirt Engine API'
arch=('any')
url='http://ovirt.org/develop/release-management/features/infra/python-sdk'
makedepends=('python-setuptools' 'python2-setuptools' 'libxml2')
license=('ASL 2.0')
source=("http://resources.ovirt.org/pub/ovirt-4.1/src/python-${pkgbase}4/${pkgbase}-python-${pkgver}.tar.gz")
sha512sums=('9ec4125045a69090425d944b3f76a3c282eb181d372d2f03b41a68349901bb9c6f92b722a431ada0f2abd7666a86a4c3e524dfd969a001a7d441013b32c2818f')

package_python-ovirt-engine-sdk() {
  depends=('python-pycurl' 'python-six')

  cd "${srcdir}/${pkgbase}-python-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}"
}

package_python2-ovirt-engine-sdk() {
  depends=('python2-pycurl' 'python2-six' 'python2-enum34')

  cd "${srcdir}/${pkgbase}-python-${pkgver}"
  python2 setup.py install -O1 --root="${pkgdir}"
}
