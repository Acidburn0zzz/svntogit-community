# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=pandoc
pkgver=2.2.1
pkgrel=76
pkgdesc="Conversion between markup formats"
url="http://pandoc.org"
license=("GPL")
arch=('x86_64')
depends=('ghc-libs' 'haskell-http' 'haskell-juicypixels' 'haskell-sha' 'haskell-aeson'
         'haskell-aeson-pretty' 'haskell-base-compat' 'haskell-base64-bytestring'
         'haskell-blaze-html' 'haskell-blaze-markup' 'haskell-case-insensitive' 'haskell-cmark-gfm'
         'haskell-data-default' 'haskell-doctemplates' 'haskell-exceptions' 'haskell-glob'
         'haskell-haddock-library' 'haskell-skylighting' 'haskell-hslua'
         'haskell-hslua-module-text' 'haskell-http-client' 'haskell-syb' 'haskell-http-client-tls'
         'haskell-http-types' 'haskell-safe' 'haskell-split' 'haskell-texmath'
         'haskell-network' 'haskell-pandoc-types' 'haskell-random'
         'haskell-scientific' 'haskell-tagsoup' 'haskell-temporary' 'haskell-network-uri'
         'haskell-unordered-containers' 'haskell-zip-archive' 'haskell-vector' 'haskell-xml'
         'haskell-yaml' 'haskell-zlib')
optdepends=('pandoc-citeproc: for citation rendering with pandoc-citeproc filter'
            'pandoc-crossref: for numbering figures, equations, tables and cross-references to them with pandoc-crossref filter'
            'texlive-core: for pdf output')
conflicts=('haskell-pandoc')
replaces=('haskell-pandoc')
makedepends=('ghc' 'haskell-diff' 'haskell-tasty' 'haskell-tasty-hunit' 'haskell-tasty-quickcheck'
             'haskell-tasty-golden' 'haskell-quickcheck' 'haskell-executable-path')
source=("https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        https://github.com/jgm/pandoc/commit/edcfbccf5dbef7d64b498376b457cc0e44fd275c.patch)
sha512sums=('84af51e180bcf8f0c11e67d8031d11a4b60c6061736a48c21e64bc30d0018a0161e3993167111584e384eced18632841b70027fdf29ed9e46427425096bb56b1'
            '9e19d24d98a173622a00993c0a5b57f2a8e02f9c117572fa25863dba23fd25cd9d56ccddbf4213c608747c89b17f657f0f769c777d5c12dbb99532e3ab23dabe')

prepare() {
    cd "${srcdir}/$pkgname-${pkgver}"
    patch -p1 -i ../edcfbccf5dbef7d64b498376b457cc0e44fd275c.patch

    # TODO: find a better solution
    sed -i "s|let env' = dynlibEnv ++ |let env' = dynlibEnv ++ [(\"LD_LIBRARY_PATH\", \"$PWD/dist/build\")] ++ |" test/Tests/Command.hs
    sed -i -e 's/<.*1.1/<2/' -e 's/<.*0.9/<1/' -e 's/<.*3.3/<4/' $pkgname.cabal
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgbase}" --datasubdir="$pkgname" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fnetwork-uri -f-trypandoc -f-embed_data_files -f-static
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    LC_CTYPE=en_US.UTF-8 runhaskell Setup test
}

package() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/COPYING.md"
}
