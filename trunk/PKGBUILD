# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=python-pynvim
pkgver=0.4.1
pkgrel=1
pkgdesc='Python client for Neovim'
url='https://github.com/neovim/pynvim'
arch=('any')
license=('Apache')
depends=('python' 'python-msgpack' 'python-greenlet')
provides=('python-neovim')
makedepends=('python-setuptools')
replaces=('python-neovim')
source=("https://github.com/neovim/pynvim/archive/${pkgver}.tar.gz")
sha256sums=('132ea5c226399aed45a959f1d8efe8cdd89a769a672534722760a9a67b06dfa0')

prepare() {
  sed -i "s/msgpack-python/msgpack/g" pynvim-${pkgver}/setup.py
}

build() {
  cd "${srcdir}/pynvim-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/pynvim-${pkgver}"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

# vim:set sw=2 sts=2 et:
