# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
pkgname=kgraphviewer
pkgver=2.4.3
pkgrel=1
pkgdesc="A Graphviz dot graph file viewer for KDE"
arch=('x86_64')
url="https://www.kde.org/applications/graphics/kgraphviewer"
license=('GPL' 'FDL')
depends=('graphviz' 'hicolor-icon-theme' 'kparts')
makedepends=('boost' 'cmake' 'qt5-base' 'qt5-svg' 'extra-cmake-modules' 'kdoctools')
source=("https://download.kde.org/stable/kgraphviewer/${pkgver}/kgraphviewer-${pkgver}.tar.xz"{,.sig})
sha256sums=('54e0c218453912ea82ad9a96edfb2d4dfd8cb15f6a567ef843b4ef97d07bd7c0'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E')

build(){
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}
