# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=bctoolbox
pkgver=4.4.25
pkgrel=1
pkgdesc="A library written in C that allows you to create and run audio and video streams"
arch=(x86_64)
url="https://github.com/BelledonneCommunications/bctoolbox"
license=(GPL)
depends=(mbedtls bcunit)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/BelledonneCommunications/bctoolbox/archive/$pkgver.tar.gz")
sha256sums=('38acc4b85d66b5bea57a8c7563469a271474975503b3e3ad2d7e7fd1bea8a658')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_STATIC=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
