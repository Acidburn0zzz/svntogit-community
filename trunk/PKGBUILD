# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-desktop-schemas
pkgver=3.1.7
pkgrel=2
pkgdesc='GSettings deepin desktop-wide schemas'
arch=('any')
url="https://github.com/linuxdeepin/deepin-desktop-schemas"
license=('GPL3')
depends=('dconf' 'deepin-gtk-theme' 'deepin-sound-theme' 'deepin-artwork-themes')
replaces=('deepin-default-gsettings')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-desktop-schemas/archive/$pkgver.tar.gz")
sha512sums=('a765e5a284949913c5aaa42540a3eabb978b4b750133ffb0d921a89ecc08358da814b75f02ea98d0977c7bd06a2be9c0c6f0bf8db931aab75ae3c030917d1ab6')

prepare() {
  cd deepin-desktop-schemas-$pkgver
  # fix default background url
  sed -i "s#^picture-uri.*#picture-uri='file:///usr/share/backgrounds/deepin/desktop.jpg'#" overrides/x86/com.deepin.wrap.gnome.desktop.override
  # don't override GNOME defaults
  rm overrides/x86/{org.gnome.desktop,other}.override
}

build() {
  cd deepin-desktop-schemas-$pkgver
  make ARCH=x86
}

package() {
  cd deepin-desktop-schemas-$pkgver
  make DESTDIR="$pkgdir" install
}
