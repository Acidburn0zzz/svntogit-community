# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=openttd
pkgver=1.11.1
pkgrel=1
pkgdesc='Engine for running Transport Tycoon Deluxe'
arch=(x86_64)
url='https://www.openttd.org'
license=(GPL)
depends=(fluidsynth fontconfig hicolor-icon-theme libpng sdl2)
makedepends=(cmake ninja)
optdepends=('openttd-opengfx: free graphics'
            'openttd-opensfx: free soundset')
source=("https://proxy.binaries.openttd.org/openttd-releases/$pkgver/$pkgname-$pkgver-source.tar.xz")
b2sums=('84a087c3f1209ebe58e309a9f39576feeabf12517d9a3632d45503ed61a81378e47395393e190825c8b9137442910a02d1476a55c44e14dd6706c09fd8d705e3')

build() {
  cmake \
    -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_BINDIR=bin \
    -DCMAKE_INSTALL_DATADIR=/usr/share \
    -DGLOBAL_DATA_DIR=/usr/share/openttd \
    -G Ninja \
    -S $pkgname-$pkgver
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
