# $Id$
#Maintainer: Ionut Biru <ibiru@archlinux.org>
pkgbase=virtualbox
pkgname=('virtualbox' 'virtualbox-guest-additions' 'virtualbox-guest-modules')
pkgver=4.0.0
pkgrel=2
arch=('i686' 'x86_64')
url='http://virtualbox.org'
license=('GPL' 'custom')
makedepends=('libstdc++5' 'bin86' 'dev86' 'iasl' 'libxslt' 'libxml2' 'libxcursor' 'qt' 'libidl2' 'sdl_ttf' 'alsa-lib' 'libpulse' 'libxtst'
'xalan-c' 'sdl' 'libxmu' 'curl' 'python2' 'kernel26-headers>=2.6.36' 'mesa' 'libxrandr' 'libxinerama' 'libvncserver' 'cdrkit' 'makeself' 'jdk')
[[ $CARCH == "x86_64" ]] && makedepends=("${makedepends[@]}" 'gcc-multilib' 'lib32-glibc')
source=(http://download.virtualbox.org/virtualbox/${pkgver}/VirtualBox-${pkgver}.tar.bz2
        '60-virtualbox.rules' '60-vboxguest.rules' 'LocalConfig.kmk' 'vboxdrv.sh')
build() {
    cd "$srcdir/VirtualBox-${pkgver}_OSE"

    cp "$srcdir/LocalConfig.kmk" .

    ./configure --disable-docs
    source ./env.sh
    kmk all

    make -C "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"
    make -C "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src/vboxvideo"
}

package_virtualbox() {
    pkgdesc="Powerful x86 virtualization for enterprise as well as home use"
    depends=('libxml2' 'libxcursor' 'libidl2' 'libxinerama' 'sdl' 'libxmu' 'curl' 'libvncserver' 'kernel26-headers' 'gcc' 'make')
    optdepends=('libxt: for shared clipboard'
                'python2: for python support'
                'java-runtime: java bindings'
                'qt: for VirtualBox GUI')

    replaces=('virtualbox-ose')
    conflicts=('virtualbox-ose')
    install=virtualbox.install

    source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin"
    mkdir -p "$pkgdir"/usr/{bin,lib/virtualbox/components,share/virtualbox/nls}

    #Binaries and Wrapper with Launchers
    install -m 0755 VBox.sh "$pkgdir/usr/bin/VBox"
    ln -sf VBox "$pkgdir/usr/bin/VBoxHeadless"
    ln -sf VBox "$pkgdir/usr/bin/vboxheadless"
    ln -sf VBox "$pkgdir/usr/bin/VBoxManage"
    ln -sf VBox "$pkgdir/usr/bin/vboxmanage"
    ln -sf VBox "$pkgdir/usr/bin/VBoxSDL"
    ln -sf VBox "$pkgdir/usr/bin/vboxsdl"
    ln -sf VBox "$pkgdir/usr/bin/VirtualBox"
    ln -sf VBox "$pkgdir/usr/bin/virtualbox"

    install -m 0755 VBoxTunctl "$pkgdir/usr/bin"

    #components
    install -m 0755 components/* -t "$pkgdir/usr/lib/virtualbox/components"

    #lib
    install -m 0755 *.so "$pkgdir/usr/lib/virtualbox"
    install -m 0644 *.gc *.r0  VBoxEFI*.fd "$pkgdir/usr/lib/virtualbox"

    #setuid root binaries
    install -m 4755 VBoxHeadless VBoxSDL VBoxNetDHCP VBoxNetAdpCtl VirtualBox VBoxBFE VBoxExtPackHelperApp \
        -t "$pkgdir/usr/lib/virtualbox"
    #other binaries
    install -m 0755 VBoxManage VBoxSVC VBoxXPCOMIPCD xpidl \
                    VBoxTestOGL EfiThunk -t "$pkgdir/usr/lib/virtualbox"

    #language
    install -m 0755 nls/*.qm -t "$pkgdir/usr/share/virtualbox/nls"

    #useless scripts
    install -m 0755 VBoxCreateUSBNode.sh VBoxSysInfo.sh -t "$pkgdir/usr/share/virtualbox"

    #icon
    install -D -m 0644 VBox.png "$pkgdir/usr/share/pixmaps/VBox.png"
    cp -r icons "$pkgdir/usr/share"

    #desktop
    install -D -m 0644 virtualbox.desktop "$pkgdir/usr/share/applications/virtualbox.desktop"
    install -D -m 0644 virtualbox.xml "$pkgdir/usr/share/mime/packages/virtualbox.xml"

    #install configuration
    mkdir -p "$pkgdir/etc/vbox"
    echo 'INSTALL_DIR=/usr/lib/virtualbox' > "$pkgdir/etc/vbox/vbox.cfg"

    #udev and licence
    install -D -m 0644 "$srcdir/VirtualBox-${pkgver}_OSE/COPYING" \
        "$pkgdir/usr/share/licenses/virtualbox-ose/LICENSE"
    install -D -m 0644 "$srcdir/60-virtualbox.rules" \
        "$pkgdir/lib/udev/rules.d/60-virtualbox.rules"

    #python sdk
    mkdir -p ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/
    pushd sdk/installer
    VBOX_INSTALL_PATH="/usr/lib/virtualBox" python2 vboxapisetup.py install --root "${pkgdir}"
    popd
    pushd sdk/bindings/xpcom/python
    cp -r xpcom ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/*.py
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/server/*.py
    chmod 644 ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/python/xpcom/client/*.py
    popd
    #java sdk
    pushd sdk/bindings/xpcom/
    cp -r java ${pkgdir}/usr/lib/virtualbox/sdk/bindings/xpcom/
    popd

    #sources for modules
    cp -r src ${pkgdir}/usr/lib/virtualbox
    #install compilation script
    install -D -m 755 "${srcdir}/vboxdrv.sh" "${pkgdir}/etc/rc.d/vboxdrv"
}

package_virtualbox-guest-additions(){
    pkgdesc="VirtualBox user-space tools for Linux guests"
    depends=("virtualbox-guest-modules=$pkgver" 'gcc-libs' 'libxmu')
    replaces=('virtualbox-ose-additions')
    conflicts=('virtualbox-ose-additions')

    source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions"

    install -d "$pkgdir/usr/bin"
    install -d "$pkgdir/sbin"
    install -m755 VBoxClient VBoxControl VBoxService "$pkgdir/usr/bin"
    install -m755 mount.vboxsf "$pkgdir/sbin"

    install -m644 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/50-vboxmouse.conf \
        "$pkgdir"/etc/X11/xorg.conf.d/50-vboxmouse.conf
    install -m644 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/linux/installer/70-xorg-vboxmouse.rules \
        "$pkgdir"/lib/udev/rules.d/70-xorg-vboxmouse.rules
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/98vboxadd-xclient \
        "$pkgdir"/usr/bin/VBoxClient-all
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/vboxclient.desktop \
        "$pkgdir"/etc/xdg/autostart/vboxclient.desktop
    install -D vboxmouse_drv_19.so \
        "$pkgdir/usr/lib/xorg/modules/input/vboxmouse.so"
    install -D vboxvideo_drv_19.so \
        "$pkgdir/usr/lib/xorg/modules/drivers/vboxvideo.so"
    install -d "$pkgdir/usr/lib/xorg/modules/dri"
    install -m755 VBoxOGL*.so "$pkgdir/usr/lib"
    ln -s /usr/lib/VBoxOGL.so "$pkgdir/usr/lib/xorg/modules/dri/vboxvideo_dri.so"
    install -m755 -D pam_vbox.so "$pkgdir/lib/security/pam_vbox.so"

}
package_virtualbox-guest-modules(){
    _kernver=2.6.36-ARCH
    pkgdesc="VirtualBox kernel modules for Linux guests"
    license=('GPL')
    install=virtualbox-guest-modules.install
    depends=('kernel26>=2.6.36' 'kernel26<2.6.37')
    replaces=('virtualbox-ose-additions-modules')
    conflicts=('virtualbox-ose-additions-modules')

    source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"

    cd vboxguest
    install -D -m644 vboxguest.ko \
        "$pkgdir/lib/modules/$_kernver/misc/vboxguest.ko"

    cd ../vboxsf
    install -D -m644 vboxsf.ko \
        "$pkgdir/lib/modules/$_kernver/misc/vboxsf.ko"

    cd ../vboxvideo
    install -D -m644 vboxvideo.ko \
        "$pkgdir/lib/modules/$_kernver/misc/vboxvideo.ko"

    install -D -m 0644 "$srcdir/60-vboxguest.rules" \
        "$pkgdir/lib/udev/rules.d/60-vboxguest.rules"

    sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/virtualbox-guest-modules.install"
}
md5sums=('0d8c3d2cb4924140b602bfc115c5801f'
         '641bd2d65f0183ef5897d8dd0322f7bc'
         'ed1341881437455d9735875ddf455fbe'
         'b2ef418477275ceb7bf03c158fdfd80c'
         '47da2e88de582bb2bab14580a3aa47b1')
