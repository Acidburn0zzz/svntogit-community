# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.9.10
pkgrel=1
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('desktop-file-utils' 'fftw' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'qt5-base')
makedepends=('alsa-lib' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('dd01c603ea60da09dabf032fb9ca601ca8c5ff25693999ecb764f25263aef1fc081c9532778744c113433ec746cda86394a2833e58b03763e87ed63070a51c32')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
