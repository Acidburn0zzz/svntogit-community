# Contributor: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.15.14
pkgrel=1
pkgdesc="2D graphics library with support for multiple output devices (32-bit)"
url="https://cairographics.org/"
arch=(x86_64)
license=(LGPL MPL)
depends=(lib32-libpng lib32-libxrender lib32-libxext lib32-fontconfig lib32-pixman lib32-glib2 $_pkgbasename)
makedepends=(lib32-librsvg lib32-gtk2 gtk-doc valgrind git)
_commit=d9aaea0c1e1484c632e1a6735c6ecc961c4b032b  # tags/1.15.14^0
source=("git+https://gitlab.freedesktop.org/cairo/cairo.git#commit=$_commit")
sha1sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgbasename

  # Update gtk-doc
  cp /usr/share/aclocal/gtk-doc.m4 build/aclocal.gtk-doc.m4
  cp /usr/share/gtk-doc/data/gtk-doc.make build/Makefile.am.gtk-doc

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgbasename
  ./configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-gl \
        --enable-tee \
        --enable-svg \
        --enable-ps \
        --enable-pdf \
        --enable-gobject \
        --disable-gtk-doc
        #--enable-full-testing \
        #--enable-test-surfaces
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $_pkgbasename
  # many tests in cairo-test-suite hang forever
  # xvfb-run make check
}

package() {
  cd $_pkgbasename
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{include,bin}
}
