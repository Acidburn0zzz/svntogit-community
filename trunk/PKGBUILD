# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zita-dpl1
pkgver=0.1.0
pkgrel=6
pkgdesc="A look-ahead digital peak level limiter"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2' 'custom')
groups=('pro-audio')
depends=('cairo' 'clthreads' 'clxclient' 'jack')
makedepends=('gendesk')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('e1adf94a3acec5468f5fb2e3ac24ff9671588542754e792cbb73771f3043070ce577f122d21cd7a6c756eec51789b46b9a24fbe6128c274cb4d6554b5c7b37b1')

prepare() {
  cd "${pkgname}-${pkgver}"
  # split out custom license
  head -n21 source/dplimit1.h > LICENSE
  # strip -march=native
  sed -e '/native/d' \
      -e 's/freetype-config\ --cflags/pkgconf\ --cflags\ freetype2/' \
      -i source/Makefile
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name zita-dpl1 \
          --categories "AudioVideo;Audio"
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX='/usr'
}

package() {
  cd "${pkgname}-${pkgver}/source"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  # custom LICENSE
  install -vDm 644 ../LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # XDG desktop integration
  install -vDm 644 "../${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../doc/* \
    -vDm 644 ../{AUTHORS,README}
}
# vim:set ts=2 sw=2 et:
