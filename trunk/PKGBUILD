# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python-matplotlib
pkgver=3.1.1
pkgrel=4
pkgdesc="A python plotting library, making publication quality plots"
arch=(x86_64)
url="https://matplotlib.org"
license=(custom)
depends=(freetype2 libpng python-numpy python-cycler python-dateutil python-kiwisolver python-pyparsing)
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python-pyqt5: Qt5{Agg,Cairo} backends'
            'python-gobject: for GTK3{Agg,Cairo} backend'
            'python-wxpython: WX{,Agg,Cairo} backend'
            'python-cairo: {GTK3,Qt5,Tk,WX}Cairo backends'
            'python-cairocffi: alternative for Cairo backends'
            'python-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'python-pillow: for reading/saving jpeg/bmp/tiff files'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies')
makedepends=(git rsync python-setuptools
             tk python-pyqt5 python-gobject
             python-wxpython python-cairocffi python-tornado
             python-pillow ghostscript texlive-bin)
             # qhull missing pkg-config file
             # agg missing some non-upstreamed patches?
checkdepends=(python-pytest-xdist python-pytest-runner xorg-server-xvfb
              texlive-core texlive-latexextra inkscape mencoder
              ffmpeg imagemagick python-pandas)
source=("https://github.com/matplotlib/matplotlib/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        setup.cfg
        matplotlib-python-3.8-warning.patch::"https://github.com/matplotlib/matplotlib/commit/4651a125.patch")
sha512sums=('157ced0cdcb17c9c82f897ee1621ea06a063491ce7393cda4c74a07a970c49385167b5cf32c1666a2933af50ccc53811cf871b6465b796a42abf4a358c76f6c0'
            '77f37e460b17afb5fed12eee6fe48fd48642301f45bb80eb3a02592b74beb32357b0707454aaf8d3e573105726b9bdbaac944c09fb617df4db2df5d1d33fd133'
            '443e1a8c088340c9c7d89008e770475d0935c51d84a1e7bfea56ac53f7d48fc0530c05e68b33aa07a92dd621cfc3f9b8227c65692dfeb53d72119b47236ac8fd')

prepare() {
  cp -a matplotlib-${pkgver}{,-test}

  # Configure tests (FS#48175)
  cp setup.cfg matplotlib-${pkgver}-test/

  cd matplotlib-$pkgver
  patch -p1 -i ../matplotlib-python-3.8-warning.patch # Fix deprecation warnings with python 3.8
}

build() {
  cd matplotlib-${pkgver}
  python setup.py build
}

check() {
  cd matplotlib-${pkgver}-test
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python setup.py pytest --addopts="-n auto" || warning "Tests failed" # Different font rendering details
}

package_python-matplotlib() {
  cd matplotlib-${pkgver}
  python setup.py install --root "${pkgdir}" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 doc/users/license.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
