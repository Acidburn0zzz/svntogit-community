# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Adrian Perez de Castro <aperez@igalia.com>

pkgname=mold
pkgver=1.0.0
pkgrel=4
pkgdesc='A Modern Linker'
arch=('x86_64')
url="https://github.com/rui314/mold"
license=('AGPL3')
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'tbb')
makedepends=('clang' 'cmake' 'xxhash')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('99ffd0b9e2ff7157cc8b26808675c9d3147bf88961155ae19ed9b99990ac647b7ec31ee78d05062decc6d41e66d99aa0fdc398d119803929b8dbff51eb3d077c')
b2sums=('56ebc267370548a2f91a71ebeed87871cede6f564c29dc7d44a499b95fe570f6e9c8a717baf2d9e235c7057c41e735b315493bd23d3b44574d2a44b14aaf5ef8')

build () {
  make \
    -C "$pkgname-$pkgver" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1
}

package() {
  make \
    -C "$pkgname-$pkgver" \
    PREFIX=/usr \
    LTO=1 \
    SYSTEM_MIMALLOC=1 \
    SYSTEM_TBB=1 \
    DESTDIR="$pkgdir" \
    install
}
