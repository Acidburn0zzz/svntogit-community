# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=xml-conduit
pkgname=haskell-xml-conduit
pkgver=1.7.0.1
pkgrel=6
pkgdesc="Pure-Haskell utilities for dealing with XML with the conduit package."
url="https://github.com/snoyberg/xml"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-attoparsec' 'haskell-blaze-builder' 'haskell-text'
         'haskell-blaze-markup' 'haskell-conduit' 'haskell-conduit-extra'
         'haskell-data-default-class' 'haskell-monad-control' 'haskell-resourcet'
         'haskell-blaze-html' 'haskell-xml-types')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('9c2569721711bf7b60b9a3cf8998095d5da7dc758af034a78007800be1cb1356619080a90e7393c0c986c301425be68f9f4c537e2427935b0df2446ad63e0ebb')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
