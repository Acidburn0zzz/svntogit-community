# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: toliman <altergan@hs-albsig.de>

pkgname=tipp10
pkgver=3.2.0
pkgrel=1
pkgdesc='Intelligent touch typing tutor'
arch=('x86_64')
url=https://gitlab.com/tipp10/tipp10
license=('GPL2')
depends=('qt5-multimedia')
makedepends=('cmake' 'qt5-tools')
source=("$url/-/archive/v$pkgver/tipp10-v$pkgver.tar.gz")
sha512sums=('2a5d4c7aa8967f15322309fe5630f69ca1ef89017a5c1309818cb25e54fd561edec90735468c0aea329ff8b477c1ae33d17ca878ebfb1b35940776c9fd40e9fc')

build() {
  cd tipp10-v$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd tipp10-v$pkgver
  make DESTDIR="$pkgdir" install
}
