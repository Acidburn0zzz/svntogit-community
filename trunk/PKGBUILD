# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-qiniu
pkgver=7.2.7
pkgrel=1
pkgdesc="Qiniu Resource Storage SDK for Python"
arch=('any')
url='https://github.com/qiniu/python-sdk'
license=('MIT')
depends=('python-requests' 'python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/qiniu/python-sdk/archive/v$pkgver.tar.gz")
sha512sums=('78b98fab728120e2c28ebec47a524df0c60accfb2193c7dec5c5bfd086d7c431810afcd89efd5bed8da19f15ec41530b2f065e2bdfd037d56ad4abadf8351681')

build() {
  cd python-sdk-$pkgver
  python setup.py build
}

check() {
  # Extracted from .travis.yml
  export QINIU_TEST_BUCKET="pythonsdk"
  export QINIU_TEST_DOMAIN="pythonsdk.qiniudn.com"
  export QINIU_TEST_ENV="travis"

  cd python-sdk-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd python-sdk-$pkgver
  python setup.py install -O1 --root "$pkgdir"

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
