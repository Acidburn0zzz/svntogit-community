# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.14.10
pkgrel=1
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="https://cairographics.org/"
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl' 'lib32-librsvg')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-glib2' 'lib32-mesa' 'lib32-libgl'
         ${_pkgbasename})
source=(https://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz)
options=(!libtool)
sha1sums=('28c59d85d6b790c21b8b59ece73a6a1dda28d69a')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
   
  ./configure --prefix=/usr \
      --libdir=/usr/lib32 \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --disable-static \
      --enable-tee \
      --enable-gl \
      --enable-egl
      # --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
