# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sergio A. Morales <sergiomorales@archlinux.cl>
# Contributor: soeren <nonick@posteo.de>

pkgname=gummi
pkgver=0.7.4
pkgrel=1
pkgdesc='Simple LaTex editor for GTK users'
arch=('x86_64' 'i686')
url='https://github.com/aitjcize/Gummi' # Using the GTK3 fork, for now
#url='http://gummi.midnightcoding.org/'
license=('MIT')
depends=('texlive-core' 'gtkspell3' 'gtksourceview3' 'poppler-glib' 'desktop-file-utils')
makedepends=('glib2' 'gtk3' 'pango' 'poppler' 'intltool' 'setconf' 'git')
install="$pkgname.install"
source=("git://github.com/aitjcize/Gummi#tag=$pkgver") # Using the GTK3 fork, for now
#source=("git://github.com/alexandervdm/gummi#tag=$pkgver") # 0.6.6
md5sums=('SKIP')

prepare() {
  cd "${pkgname^}"

  find . -type f -print0 | xargs -0 sed -i "s|0.6.5|$pkgver|g"
  setconf autogen.sh AM_VERSION ''
  ./autogen.sh
  setconf Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  setconf data/misc/Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  echo $pkgver > dev/latest
  autoreconf
  setconf configure PACKAGE_VERSION "'"$pkgver"'"
  setconf configure PACKAGE_STRING "'""Gummi $pkgver (GTK3 fork)""'"
  setconf configure PACKAGE_BUGREPORT "'https://bugs.archlinux.org/'"
}

build() {
  cd "${pkgname^}"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname^}"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
