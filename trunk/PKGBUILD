# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

pkgname=exploitdb
_pkgver=2018-10-05
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc="Offensive Securityâ€™s Exploit Database Archive"
arch=('any')
url="https://www.exploit-db.com/"
license=('GPL')
optdepends=('libxml2: to check nmap XML results'
            'xclip: copy paths to the clipboard')
backup=('etc/searchsploit_rc')
options=('!strip')
source=("${pkgname}-${_pkgver}.tar.gz::https://github.com/offensive-security/${pkgname}/archive/${_pkgver}.tar.gz"
        "rcfile.diff")
sha512sums=('4caa48bb8d2f002f8f61be8350064529b1e641c6259b42bd7229d51183205c0936be2b08e775fb9473d95e6da62ad2cee9b83145c801c2ed4b69728298aed92d'
            'e170da5b974975c6c23c5b09e8c1825bbb17446fb506f2d00f92f86f94394acf18a53458bd90969de38da642fc1f7abb08ba940969108002dab17415b88211d6')

prepare() {
    cd ${pkgname}-${_pkgver}

    patch -p1 -i ../rcfile.diff
}

package() {
    cd ${pkgname}-${_pkgver}

    install -Dm755 searchsploit "${pkgdir}"/usr/bin/searchsploit
    install -Dm644 .searchsploit_rc "${pkgdir}"/etc/searchsploit_rc
    mkdir -p "${pkgdir}"/usr/share/${pkgname}
    cp -a exploits/ shellcodes/ files_*.csv "${pkgdir}"/usr/share/${pkgname}
}
