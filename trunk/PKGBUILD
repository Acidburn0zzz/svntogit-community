# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-shaders-glsl
pkgver=145
pkgrel=1
pkgdesc='Collection of GLSL shaders for libretro'
arch=('any')
url='http://www.libretro.com/'
license=('GPL3')
groups=('libretro')
provides=('libretro-shaders')
conflicts=('libretro-shaders')
makedepends=('git')
_commit='c3b252c135ef04a81baf2053f545f296c3c893f4'
source=("libretro-shaders-glsl::git+https://github.com/libretro/glsl-shaders.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd libretro-shaders-glsl

  git rev-list --count HEAD
}

package() {
  install -dm 755 "${pkgdir}"/usr/share/libretro
  cp -dr --no-preserve='ownership' libretro-shaders-glsl "${pkgdir}"/usr/share/libretro/shaders
  rm -rf "${pkgdir}"/usr/share/libretro/shaders/.git
}

# vim: ts=2 sw=2 et:
