# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rofimoji
pkgver=5.1.0
pkgrel=1
pkgdesc='Character picker for rofi'
arch=('any')
url=https://github.com/fdw/rofimoji
license=('MIT')
depends=('python-configargparse')
makedepends=('python-setuptools')
optdepends=('emoji-font: for the emojis character file'
            'nerd-fonts: for the nerd_font character file'
            'rofi: for the X.Org selector'
            'xclip: for one of the X.Org clipboarders'
            'xsel: for one of the X.Org clipboarders'
            'xdotool: for the X.Org typer'
            'wofi: for the Wayland selector'
            'wl-copy: for the Wayland clipboarder'
            'wtype: for the Wayland typer')
install=$pkgname.install
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('0079ea5177fac16126b9874cca95dfe6cea2e827cc985201fe5041941dcb88e87b085dcc4228060c7c6bca473ba1171e0a96b136b7b47037a5b2156f32a7428f')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
