# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: froggie <sullivanva@gmail.com>

pkgname=unrtf
pkgver=0.21.2
pkgrel=1
pkgdesc='Command-line program which converts RTF documents to other formats'
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/unrtf/unrtf.html"
license=('GPL3')
depends=('glibc')
changelog=$pkgname.changelog
source=(http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('acb1b093dd2c7485fc43b128ccaf665d6188f568752cec96d54b693374caddb0')

build() {
  cd ${srcdir}/$pkgname-$pkgver

# bugfix FS#14043
  sed -e 's|/usr/local|/usr|' -i src/{main.h,path.h}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}
