# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-screensaver
pkgver=2.0.10
pkgrel=1
pkgdesc="Screensaver for UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-screensaver"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'libxtst' 'qt5-x11extras')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-screensaver/archive/v$pkgver.tar.gz"
        ukui-screensaver-1.patch::https://github.com/ukui/ukui-screensaver/pull/1.patch
        ukui-screensaver-fix-build.patch::https://github.com/ukui/ukui-screensaver/commit/f5b977ce5910e9d4f5eac61892f7ef833a3f4324.patch)
sha512sums=('40e3a0872b33a96d1aceb315e15d7ba6519c2eb290f585ae1571630a2cfa2f2819616f05f20f854a12ca5518ddfcb233df2ed766efcc852692af9f9fb51c031b'
            'c7e602dfb58417e7fd84011756b83d61e030c676b9bf53632ee2498568f51d4fb48663a05f4bd8825d2d60c76e2efe04181a7f66037d0f078956120a3888e4bd'
            '53d6b02b2a3487a3eadd78d43433d699b3a2751c669aaa9bcebe325a3f79b80dddf89690fc6eff2880829f955e88ac61bc9bfb8c90d1afae23eaf982a15681ee')

prepare() {
  mkdir -p build
  rmdir $pkgname-$pkgver/VirtualKeyboard
  patch -d $pkgname-$pkgver -p1 -i ../ukui-screensaver-1.patch || :
  patch -d $pkgname-$pkgver -p1 -i ../ukui-screensaver-fix-build.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
