# $Id$
# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Daniel Micay <danielmicay@gmail.com>

pkgbase=python-pygit2
pkgname=(python-pygit2 python2-pygit2)
pkgver=0.24.0
pkgrel=1
pkgdesc='Python bindings for libgit2'
arch=('i686' 'x86_64')
url="https://github.com/libgit2/pygit2"
license=('GPL2')
depends=('libgit2>=1:0.24.0')
makedepends=('python-cffi' 'python2-cffi' 'python-setuptools' 'python2-setuptools' 'git')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/libgit2/pygit2/archive/v${pkgver}.tar.gz")
md5sums=('135adc1c70129d904ec89315b91cc26a')

package_python-pygit2() {
  depends+=('python' 'python-cffi')
  replaces=('python-libgit2')
  conflicts=('python-libgit2')

  cd "pygit2-$pkgver"
  LANG=en_US.UTF8 python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pygit2() {
  depends+=('python2' 'python2-cffi')
  replaces=('python2-libgit2')
  conflicts=('python2-libgit2')

  cd "pygit2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}
