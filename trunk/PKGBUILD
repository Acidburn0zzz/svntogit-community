# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-printer
pkgver=0.5.6
pkgrel=1
pkgdesc='Printer configuration project for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-printer"
license=('GPL3')
depends=('crypto++' 'cups' 'deepin-qt5integration' 'libcups' 'smbclient')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-printer/archive/$pkgver.tar.gz")
sha512sums=('362a6a1b304c3751f4af09bc1b512ae178016fd4114334a468a4fae99dcb1dc684c1d7483b87eabedd8a3ae2861821c627f186fc132983e95f549f613585ebae')

prepare() {
  cd dde-printer-$pkgver
  sed -i '/include <QEventLoop>/a #include <QCloseEvent>' src/Printer/ui/dprintersshowwindow.h
  sed -i '/include <QList>/a #include <QFile>' src/Printer/vendor/zdrivermanager.h
  sed -i 's/-lcrypto++/-lcryptopp/' src/Printer/Printer.pro
}

build() {
  cd dde-printer-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-printer-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
