# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Albert.Zhou <albert.zhou@wiz.cn>

pkgname=wiznote
pkgver=2.6.6
_commit=cf0d102e8dd3a90668eada2c2a5883c19ba57cee
pkgrel=1
pkgdesc="Opensource cross-platform cloud based note-taking client"
arch=('x86_64')
url="http://www.wiznote.com"
license=('GPL3' 'custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'qt5-websockets' 'qt5-webengine'
         'qt5-svg') # 'crypto++')
# shared clucene/quazip triggers segfault
makedepends=('cmake' 'qt5-tools' 'boost')
source=("$pkgname-$_commit.tar.gz::https://github.com/WizTeam/WizQTClient/archive/$_commit.tar.gz")
sha512sums=('f2db596b5d84842fdf7b247d215d710d44d2e34114ba5ab9183c57a89f80fed99e234a95c72422db7bd5427de84c040a2e06931814bec04553690d6c0a4d4469')

prepare() {
  #sed -e 's/add_subdirectory(cryptopp)//' \
  #    -i WizQTClient-$_commit/lib/CMakeLists.txt
  #sed -e 's/cryptlib/cryptopp/' \
  #    -i WizQTClient-$_commit/src/CMakeLists.txt

  rm -rf build; mkdir build
}

build() {
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../WizQTClient-$_commit
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
