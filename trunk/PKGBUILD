# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=lxqt-config
pkgver=0.14.1
pkgrel=1
pkgdesc="LXQt system configuration."
arch=("x86_64")
groups=("lxqt")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("lxqt-themes" "liblxqt" "libkscreen" "libxcursor" "xf86-input-libinput" "qt5-svg")
makedepends=("lxqt-build-tools")
optdepends=(
	'xsettingsd: help GTK applications apply themes on the fly'
)
source=(
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	$pkgname-qt5.14.patch::https://github.com/lxqt/lxqt-config/commit/cd7de12f2ebe5a2ad03c2086da4c3b5317a4506e.patch
)
sha256sums=('2ffb0a8627652861f1d5c3a9f5071d7e1016071d829c0bd17be234b3ed34639a'
            'SKIP'
            'bc9c8159167e6689ec7f51297e21e100f602b2b3b3d98ece9a7ca7b37c90fa5a')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

prepare() {
	cd $pkgname-$pkgver
	patch -Np1 -i ../$pkgname-qt5.14.patch
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
