# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.7.1
pkgrel=1
pkgdesc="An easy to use and easily extensible IDE/GUI for R"
url="https://rkward.kde.org/"
arch=(x86_64)
license=(GPL)
depends=(kdewebkit ktexteditor qt5-script r)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/rkward/$pkgver/src/$pkgname-$pkgver.tar.gz")
sha256sums=('02a96450ef4275d38b9ff6fd13d98bcf353c5388399a813a111b640d7c61c834')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  rm "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax/r.xml # provided by syntax-highlighting
}
