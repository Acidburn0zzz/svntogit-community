# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=binaryen
epoch=1
pkgver=93
pkgrel=1
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
makedepends=('cmake' 'ninja' 'python' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/WebAssembly/binaryen/archive/version_${pkgver}.tar.gz"
        "binaryen.sh")
sha384sums=('08feb86dac0345d2e13443681d521c141d0e2d20dac0f1946e8aefb02f6081a6518d72c36e9152024988f9392ea60a2b'
            'fdf76d46e6ab9b915212bbca7c29e624e209de911b18ecaccc40b99a00360cd12b261f96c8655f02ec4fd28cedffbcac')

prepare() {
    mkdir -p binaryen-version_$pkgver/build
}

build() {
    cd binaryen-version_$pkgver/build

    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ..
    ninja
}

package() {
    cd binaryen-version_$pkgver/build
    DESTDIR="$pkgdir" ninja install

    install -Dm755 "$srcdir"/binaryen.sh "$pkgdir"/etc/profile.d/binaryen.sh
    install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
