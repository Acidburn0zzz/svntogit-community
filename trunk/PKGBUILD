# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.47.0
pkgrel=1
_image_pkgver=5.18.0
_podman_pkgver=3.4.4
_skopeo_pkgver=1.5.2
_storage_pkgver=1.38.0
pkgdesc="Configuration files and manpages for containers"
arch=(any)
url="https://github.com/containers"
license=(Apache)
makedepends=(go-md2man)
backup=(
  etc/containers/containers.conf
  etc/containers/mounts.conf
  etc/containers/policy.json
  etc/containers/registries.conf
  etc/containers/seccomp.json
  etc/containers/storage.conf
)
# configuration override directories need to exist
options=(emptydirs)
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('f36c272ee9effc4e0b30b8b03893f886b94d4ff5b57b2d25a4a7aa596482be5d6a290e8c863f62dfab651ede74c034b46c310941d3e834c8ec3bc278a81a292f'
            '85ed0d0d760bf45bc4cfdbb6ea75442ec44caee828771898143ac0f35ea6f49451483dee5880b1c02cadd278eb646a76022f83c635a9e64ec28a04555cf7675a'
            'cfd295bf50ce86ea70741c3e663b409ed47b1e560c962bc579f319151a0fe2b24cdd3045667660083cce89449a9c5de1508c94a9a02375165a72ce7c16166666'
            '7f1dfa41e4fa6a145187413252672b12e59a11ce445d9d5c47c78c4dbb8aa4ffe6925bea20b11034e40725fdb2ac5eacfbb646e7b8cfdd11f07bbb6fc020dda7'
            'c9be760d088ac2bf1fa7ee7b6742c9f0f54b4878ae85059bbdb059b48eb5a2cf2c217b774944b23ff175d7126351d324ed96febb77c7a6b78b6cffb51f271f13'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('471b642aadc24d9bc8bf04308c21b4810a36318cd64c36e2d76fd3a2248fc7839c0d175bb0c8ae728940ef5c00169bb2c8a2a3ac05a9b1db15ab1cf50e240e7b'
        '321aeff9a0f93a7b9ffece33e907a68096828cf8ca63409acf46320c348acee2047035decf2cce68750cb6f88048e7d918c18e510564b926882d3c1d4479358d'
        '3de69c9bc3bd1334837d21cb2817a9e9757fbb561a0b047658c7401608ecf1fbe09d5cd0c65497a82150a67a3775c77705d0dbd314a54f7b3a1953733cfb2906'
        '009f820d0784ae098d434418e126d4cc7318bbef72839b0438073eee02c4644195a6c5108334b3c1efd3e5c655035460994b151e5f4f3f30ae3046a31eab27b2'
        'b33342dd6c55d69c3c71ce17ceeb4fd4c442291b700a9a601f9b273337bda70b9f353adbac55019d3ab0a20a8b142816255af106b77127f371ee0f80fbea488b'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i "storage-${_storage_pkgver}/docs/Makefile"
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
