# Maintainer: Orhun Parmaksız <orhun@archlinux.org>
# Contributor: Wojciech Kępka (wojciech@wkepka.dev)

pkgname=helix
pkgver=0.3.0
pkgrel=2
pkgdesc="A post-modern modal text editor"
arch=('x86_64')
url="https://helix-editor.com"
license=('MPL2')
depends=('bash')
makedepends=('rust' 'git')
conflicts=("hex")
source=("git+https://github.com/helix-editor/helix.git#tag=v$pkgver"
        "$pkgname.sh")
sha256sums=('SKIP'
            'f782c93bc0f2c7b27ec62fb2ba36e11b3354c3953405f8d2017b039c9eac500a')

prepare() {
  cd "$pkgname"
  git submodule init
  git submodule update --recursive
}

build() {
  cd "$pkgname"
  cargo build --release --locked
}

check() {
  cd "$pkgname"
  cargo test --release --locked --all-features
}

package() {
  cd "$pkgname"
  install -Dm 755 "../$pkgname.sh" "$pkgdir/usr/bin/hx"
  install -Dm 755 "target/release/hx" "$pkgdir/usr/lib/$pkgname/hx"
  mkdir -p "$pkgdir/usr/lib/helix/"
  cp -r "runtime" "$pkgdir/usr/lib/helix/"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
