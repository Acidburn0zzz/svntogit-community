# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-cmd2
pkgname=(python-cmd2 python2-cmd2)
pkgver=0.7.3
pkgrel=1
pkgdesc="Extra features for standard library's cmd module"
arch=('any')
url="https://github.com/python-cmd2/cmd2"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyparsing' 'python2-pyparsing' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/python-cmd2/cmd2/archive/$pkgver.tar.gz")
sha512sums=('f329b674efc8881d6c6b978cfbe367ffafa32a4234d9fbf3731eff21a32b6e107f2072157c70a0ed4dbf5cd1239bdcbadb4ac7dcbce96326d69cb968a6bf49c3')

prepare() {
  cp -a cmd2-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py build

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/cmd2-$pkgver
  python setup.py pytest

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py pytest
}

package_python-cmd2() {
  depends=('python-pyparsing')

  cd "$srcdir"/cmd2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-cmd2() {
  depends=('python2-pyparsing')

  cd "$srcdir"/cmd2-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
