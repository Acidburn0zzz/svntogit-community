# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-boto3
pkgname=python-boto3
pkgver=1.12.37
pkgrel=1
pkgdesc='The AWS SDK for Python'
arch=('any')
license=('Apache')
url='https://github.com/boto/boto3'
depends=('python-botocore' 'python-jmespath' 'python-s3transfer')
makedepends=('python-setuptools')
# checkdepends=('python-coverage' 'python-nose' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/boto/boto3/archive/$pkgver.tar.gz")
sha512sums=('45896cb2959e7933c7bc6df4176617cd758f0a61f666d8723269f52c1ebc2a2253a64aef7b876325a9006249d47ea63c2fcd1ce810d3db24e64d3819ab53421f')

build() {
  cd boto3-$pkgver
  python setup.py build
}

check() {
  cd boto3-$pkgver
  # scripts/ci/run-tests
}

package() {
  cd boto3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
