# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hypothesmith
pkgver=0.1.3
_commit=80d28f546af3e3f3c43e8f30a2779dd6e2968de2
pkgrel=1
pkgdesc="Hypothesis strategies for generating Python programs, something like CSmith"
url="https://github.com/Zac-HD/hypothesmith"
license=('MPL')
arch=('any')
depends=('python-hypothesis' 'python-lark-parser' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-black' 'python-parso' 'python-pytest-cov')
source=("https://github.com/Zac-HD/hypothesmith/archive/$_commit/$pkgname-$_commit.tar.gz")
sha512sums=('750cf35cd7da3a28859ca94fabc4b07902e771abbfb8df19ccc825cea4509596c849acc8eaa7ebd192fd5bf2670b0f51e65abfc350fc81e33f47f6fb25c30d7b')

build() {
  cd hypothesmith-$_commit
  python setup.py build
}

check() {
  cd hypothesmith-$_commit
  PYTHONPATH="$PWD/build/lib" pytest
}

package() {
  cd hypothesmith-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}
