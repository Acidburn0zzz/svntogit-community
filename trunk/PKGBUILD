# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=3.2.0
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-six' 'python-numpydoc' 'python-scipy' 'python-sqlitedict' 'python-pycodestyle'
         'python-pydocstyle' 'python-testflo' 'python-parameterized' 'python-pyparsing'
         'python-networkx' 'python-sphinx' 'python-redbaron' 'python-mock' 'python-requests-mock'
         'python-tornado' 'python-pydoe2')
checkdepends=('python-mpi4py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver.tar.gz")
sha512sums=('5fe401de4099f50069583176caddb7974581ec61f597d54eda1674485139aa22c78beed962fe514f7c271c58027a32f36d40cc7433d8575a3f4d9e1b2e2fe2fe')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/from distutils.core/from setuptools/' \
      -e 's/==/>=/' \
      -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  PYTHONPATH="$PWD/build/lib" testflo -n 16 --timeout=120 openmdao || warning "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
