# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Veeti Paananen <veeti.paananen@rojekti.fi>

pkgname=cmark-gfm
pkgver=0.29.0.gfm.2
pkgrel=1
pkgdesc="GitHub's fork of cmark, a CommonMark parsing and rendering library and program in C"
arch=('x86_64')
url="https://github.com/github/cmark"
license=('custom:BSD2')
depends=('glibc')
makedepends=('cmake' 'python')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('4623989cc34c3d9c9596e27a8253d2ab26ecf0cbe8c2a6d802ab543c0310b90abae53364326ab8d5599d38a8cca001cf39931081bbc96c01f52283ff1cca1994')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    ../cmark-gfm-$pkgver
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
  install -Dm0644 -t "$pkgdir"/usr/share/licenses/$pkgname/ "$srcdir"/cmark-gfm-$pkgver/COPYING
}
