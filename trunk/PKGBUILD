# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Moritz Bunkus <moritz@bunkus.org>

pkgname=perl-cpan-meta-check
pkgver=0.012
pkgrel=1
pkgdesc="Verify requirements in a CPAN::Meta object"
arch=('any')
license=('PerlArtistic' 'GPL')
url='http://search.cpan.org/dist/CPAN-Meta-Check'
depends=('perl')
makedepends=('perl-test-deep')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/L/LE/LEONT/CPAN-Meta-Check-$pkgver.tar.gz")
sha512sums=('99ec6721dfb6be8fae7e5a4f6418d4d12d8d093df11f0596480ecc19a46ef3af3fc3da91ccdd7110f974e8f4d04e33166d76804b673a019bc4994f96b7078e7d')

build() {
  cd CPAN-Meta-Check-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd CPAN-Meta-Check-$pkgver
  make test
}

package() {
  cd CPAN-Meta-Check-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
