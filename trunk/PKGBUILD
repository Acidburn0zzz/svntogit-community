# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=nvchecker
pkgver=1.1
pkgrel=1
pkgdesc="New version checker for software"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-pytest-asyncio' 'python-flaky')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz")
sha512sums=('8680776c22bbb34ad531982027a173744a484a98b9a02fda4068d314bdfadefda29d4542a0dac2981ead26a0040c786c59c9239d5097a8a75aec353ace93b9fd')

build() {
  cd nvchecker-$pkgver
  python setup.py build 
}

check() {
  cd nvchecker-$pkgver
  python setup.py pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
