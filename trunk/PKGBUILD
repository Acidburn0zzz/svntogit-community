# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: fenuks

pkgname=skim
pkgver=0.9.1
pkgrel=1
pkgdesc='Fuzzy Finder in rust!'
arch=('x86_64')
url=https://github.com/lotabout/skim
license=('MIT')
depends=('ncurses')
makedepends=('rust')
optdepends=('bash: for resp. completions and key bindings, and for sk-tmux script'
            'fish: for resp. key bindings'
            'tmux: for sk-tmux script'
            'vim-plugin-runtime: for N/Vim plugin'
            'zsh: for resp. completions and key bindings')
source=("skim-$pkgver.tar.gz::https://static.crates.io/crates/skim/skim-$pkgver.crate")
b2sums=('e98e1891a52c3ea40576c8423e88a8db458290171346ad82134f29f9ff66c210a1ca83fd21f1e70168542a33807dea5192f40281fcadee18ec6cfba4e720f537')

build() {
  cd skim-$pkgver
  cargo build --release --locked
}

check() {
  cd skim-$pkgver
  cargo test --release --locked
}

package() {
  cd skim-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/sk bin/sk-tmux
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 man/man1/*
  install -Dm644 -t "$pkgdir"/usr/share/skim shell/*.bash shell/*.zsh
  install -Dm644 shell/key-bindings.fish "$pkgdir"/usr/share/fish/functions/skim_key_bindings.fish
  install -Dm644 -t "$pkgdir"/usr/share/vim/vimfiles/plugin plugin/skim.vim
  install -Dm644 -t "$pkgdir"/usr/share/licenses/skim LICENSE
}

# vim:set ts=2 sw=2 et:
