# Maintainer: Florian Pritz <bluewind@xinu.at>

_name=schema
pkgname="python-$_name"
pkgver=0.7.0
pkgrel=2
pkgdesc='Python module to validate and convert data structures.'
arch=(any)
url="https://github.com/keleshev/$_name"
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-mock python-pytest)
license=(MIT)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha256sums=('44add3ef9016c85ac4b0291b45286a657d0df309b31528ca8d0a9c6d0aa68186')

build() {
	cd "$srcdir/$_name-$pkgver"
    python setup.py build
}

check() {
	cd "$srcdir/$_name-$pkgver"
	python setup.py test
}

package() {
	cd "$srcdir/$_name-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
	install -D -m644 LICENSE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
