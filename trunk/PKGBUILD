# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Philipp Ãœberbacher <hollunder at gmx dot at>

pkgname=qtractor
pkgver=0.9.1
pkgrel=1
pkgdesc="Audio/MIDI multitrack sequencer"
arch=('x86_64')
url="http://qtractor.org/"
license=('GPL')
groups=('pro-audio')
depends=('qt5-x11extras' 'suil' 'lilv' 'libmad' 'liblo'
         'rubberband' 'hicolor-icon-theme')
makedepends=('qt5-tools' 'ladspa' 'dssi')
optdepends=('dssi-vst: win32 VST support')
source=("https://sourceforge.net/projects/${pkgname}/files/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "qt5.11.diff")
sha512sums=('c40a7535740bc8315ffc640f791b640a12a4ba135070502682681c3022923f1edb62738127cae0c523a6410cb163b4cac2828544ae629b42fc93946f3d4d8a59'
            'bf6407de7039f2c60ff082717a2da72cd5a00bb1fe368b399519d9de79f1917173f8680c3137fa819b6c50063f17e4caab92f90092dc5ee398ca3db1382d0eb9')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  # fix for qt5.11: https://github.com/rncbc/qtractor/pull/155
  patch -Np1 -i "../qt5.11.diff"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README,README.VST,TODO}
}
# vim:set ts=2 sw=2 et:
