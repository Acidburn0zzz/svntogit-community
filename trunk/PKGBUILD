# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Daniel Eklöf <daniel at ekloef dot se>

pkgbase=foot
pkgdesc='fast, lightweight, and minimalistic Wayland terminal emulator'
pkgname=($pkgbase $pkgbase-terminfo $pkgbase-themes)
pkgver=1.10.3
pkgrel=2
url=https://codeberg.org/dnkl/$pkgbase
license=(MIT)
makedepends=(fcft
             fontconfig
             libutf8proc
             libxkbcommon
             llvm
             meson
             ncurses
             ninja
             pixman
             python
             scdoc
             sway
             tllist
             wayland
             wayland-protocols)
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha256sums=('24f57d9926ead7bea491f1bdd97eaceae5fdc10c1cb3435ee588a8f9c9af805a')

build() {
	cd "$pkgbase"
	./pgo/pgo.sh \
		full-headless-sway \
		. build \
		--prefix=/usr \
		--wrap-mode=nodownload
}

check() {
	cd "$pkgbase"
	ninja -C build test
}

package_foot() {
    arch=(x86_64)
	optdepends=('foot-terminfo: alternative to ncurses’ terminfo, with additional non-standard capabilities'
	            'foot-themes: color schemes'
	            'libnotify: desktop notifications'
	            'xdg-utils: URI launching')
	depends=(fcft
	         fontconfig
	         libutf8proc
	         libxkbcommon
	         ncurses
	         pixman
	         wayland)
	cd "$pkgbase"
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_foot-terminfo() {
	pkgdesc="Terminfo for $pkgbase, a Wayland terminal emulator"
	arch=(any)
	depends=(ncurses)
	cd "$pkgbase"
	install -Dm0644 -t "$pkgdir/usr/share/terminfo/f/" build/f/*
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}

package_foot-themes() {
	pkgdesc+=' (optional color themes)'
	arch=(any)
	depends=($pkgbase)
	cd "$pkgbase"
	install -Dm0644 -t "$pkgdir/usr/share/foot/themes/" themes/*
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
