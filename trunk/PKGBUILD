# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-control-center
pkgver=2.0.1
pkgrel=1
pkgdesc="Utilities to configure the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-control-center"
groups=('ukui')
depends=('kconfig' 'kconfigwidgets' 'ki18n' 'kscreen' 'kwidgetsaddons' 'peony' 'ukui-interface'
         'ukui-panel' 'ukui-media' 'ukui-menus' 'ukui-power-manager' 'ukui-screensaver'
         'ukui-settings-daemon' 'upower')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-control-center/archive/v$pkgver.tar.gz"
        fix-build.patch)
sha512sums=('c2633eff9a7f8b916c1cb9adbac71a1a7280950fc283b6c441c4245cde1233ae9f565af24e7304650ba220b5bdb142d2516e4ec1aa57199433fef05e43841153'
            '582ecdd847c28c4c82cbf0caea0553d7cfc92d2cf9dfe2df0d4e3e721ac4291da2486332a3a4ee4f14f306c2a0f2353c78cb3e8fdcbc405164c880c68d7ed09d')

prepare() {
  cd ukui-control-center-$pkgver
  # https://github.com/ukui/ukui-control-center/issues/36
  patch -p1 -i ../fix-build.patch
}

build() {
  cd ukui-control-center-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
