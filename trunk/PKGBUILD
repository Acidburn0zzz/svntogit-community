# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=cormorant
pkgname=("otf-${pkgbase}" "ttf-${pkgbase}")
pkgver=3.601
pkgrel=2
pkgdesc="Open-source display font family"
arch=('any')
url="https://github.com/CatharsisFonts/Cormorant"
license=('custom:OFL')
source=(${pkgbase}-${pkgver}.tar.gz::"https://github.com/CatharsisFonts/Cormorant/archive/v${pkgver}.tar.gz")
sha256sums=('0cfcbf79b2079339d10b1a78a6a4f14cad799187ef5c020c51adc237e5ffc17a')

package_otf-cormorant() {
    cd ${pkgbase^}-${pkgver}
    install -Dm644 "2. OpenType Files/"*.otf -t "${pkgdir}"/usr/share/fonts/OTF/
    install -Dm644 OFL.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_ttf-cormorant() {
    cd ${pkgbase^}-${pkgver}
    install -Dm644 "1. TrueType Font Files/"*.ttf -t "${pkgdir}"/usr/share/fonts/TTF/
    install -Dm644 OFL.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
