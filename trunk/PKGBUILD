# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

pkgbase=python-zope-component
pkgname=('python-zope-component' 'python2-zope-component')
pkgver=4.4.0
pkgrel=1
pkgdesc="Represents the core of the Zope Component Architecture"
arch=('any')
url="https://github.com/zopefoundation/zope.component"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-event' 'python2-zope-event')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-persistent'
              'python2-persistent' 'python-zope-hookable' 'python2-zope-hookable'
              'python-zope-location' 'python2-zope-location' 'python-zope-proxy'
              'python2-zope-proxy' 'python-zope-security' 'python2-zope-security'
              'python-zope-configuration' 'python2-zope-configuration' 'python-zope-i18nmessageid'
              'python2-zope-i18nmessageid')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.component/archive/$pkgver.tar.gz")
sha512sums=('aecf9a6222a3ba224b650943c4200275553dfc69647c13e41bec9a22b3d08dfc279da0f91e02c95e29b1930283df7a66bbd56c61df29ebaf4cb67d8527e59813')

prepare() {
  cp -a zope.component-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.component-$pkgver
  python setup.py build

  cd "$srcdir"/zope.component-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.component-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.component-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-component() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')

  cd zope.component-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-component() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-event')

  cd zope.component-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
