# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <bruno.n.pagani@gmail.com>

pkgname=kresus
pkgver=0.13.2
_commit=0c2bb82d76d8cfa1c0d183576bd8bddd8ab14a90
pkgrel=1
pkgdesc="Self-hosted personal finance manager"
arch=('x86_64')
url="https://kresus.org"
license=('MIT')
backup=('etc/webapps/kresus/config.ini')
depends=('nodejs' 'weboob-headless')
makedepends=('npm' 'libpng' 'python2')
optdepends=('python2-pdfminer: For IBAN extraction from PDF RIB')
source=(${pkgname}-${pkgver}.tar.bz2::"https://framagit.org/bnjbvr/kresus/repository/${pkgver}/archive.tar.bz2"
        'config.ini'
        "${pkgname}.service"
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles")
sha256sums=('2594837ddb656c230e439db3c86a05743b14df1fd819ad53738e03118e61263e'
            'adc91cd0cef6b546d482ebe1e9de85a451105166c15c190caa8c6a86c023b07a'
            '9d1e05cb232fd511de31f09c93ea346b256f63b9435bff86826323d93281ef20'
            'd9d30f5470c7165e4917487b69d7ab82e463da4e1355056e1035ee501d3f1adc'
            'e58284777bb3750f52973dd021da6ba9837652975f53e383960b1c23c317edb8')

build() {
    cd ${pkgname}-${pkgver}-${_commit}
    npm install && npm run build:prod
}

package() {
    cd ${pkgname}-${pkgver}-${_commit}
    make DESTDIR="${pkgdir}" install
    # Fix python2 name
    sed -i 's:#!/usr/bin/env python:#!/usr/bin/env python2:' "${pkgdir}"/usr/lib/node_modules/kresus/server/weboob/main.py
    # Fix npm crazyness
    chmod -R o-w "${pkgdir}"/usr
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/

    cd ..
    install -Dm600 config.ini -t "${pkgdir}"/etc/webapps/kresus/
    install -Dm644 ${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
    install -Dm644 ${pkgname}.sysusers "${pkgdir}"/usr/lib/sysusers.d/${pkgname}.conf
    install -Dm644 ${pkgname}.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/${pkgname}.conf
}
