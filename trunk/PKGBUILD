# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: J.W. Birdsong  <jwbirdsongATmailDOTcom>

pkgname=teamspeak3
pkgver=3.1.1.1
pkgrel=1
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com/"
license=('custom')
depends=('qt5-base' 'libxkbcommon-x11' 'qt5-webengine')
optdepends=('libpulse')
arch=('i686' 'x86_64')
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_x86-${pkgver}.run"
        "http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_amd64-${pkgver}.run"
        'teamspeak3.desktop'
        'teamspeak3.png'
        'teamspeak3.launcher')
sha512sums=('c5e9d876badcfd9692ef2d3a5b80af85050139501b19302000c8a8b50349467f8651e4c3be57e8080c66330ab7fdb7aaa12f3da53d4b0214d6c1548d908a4cbd'
            'a9e16ec64cc7e1e683b9b23fa6198bd75d59f0a1a66958d56194432190f7acea8259615bfe01e2fbea7102430ad2da7e87c1426c748e95d9988acaec9293ee49'
            '4cac0b7446dc3e728b232fdd6c6d75be1d2a5530635eab5918a4e7dd6a4a3fc21d6a75055da919b56e5cf849f4c5ba26ece56759a45bc4850eba3eb73853759d'
            'af18ed36a3c8de90b60a7205ebf91a35d4b517f22f97d541c82debdb12697882469d9771fdf3fb8b081aeb74a01237f3afd4732daccf4b374dc6d39e7df5d444'
            '2365bf65500a7455551ca656d27d8fed54dd7151e61e369296e4fa2b5451ee3dd0c4adc67ec1d0f221de0ed7e6efa18c303b63bf2d858060671491bee51c899d')

[[ "$CARCH" == "i686" ]] && _TSARCH='x86'
[[ "$CARCH" == "x86_64" ]] && _TSARCH='amd64'

prepare() {
  mkdir archive && cd archive
  sh ../TeamSpeak3-Client-linux_${_TSARCH}-${pkgver}.run --tar -xf 2>/dev/null

  # Delete bundled Qt libs to use system-wide ones
  rm libQt5*

  # Fix FS#34190
  sed -i "/export QTDIR.*/d" ts3client_runscript.sh
  sed -i "/export QT_PLUGIN_PATH*/d" ts3client_runscript.sh

  # Fix FS#34189
  rm qt.conf

  # Fix permissions
  find -type d | xargs chmod 755
  find -type f | xargs chmod 644
  find -name *.so | xargs chmod 755
  chmod +x ts3client*
}

package() {
  install -d ${pkgdir}/{usr/bin/,opt/teamspeak3}

  cp -r archive/* ${pkgdir}/opt/teamspeak3/

  # Install Desktop File
  install -D -m644 $srcdir/teamspeak3.desktop ${pkgdir}/usr/share/applications/teamspeak3.desktop

  # Install Icon File
  install -D -m644 $srcdir/teamspeak3.png ${pkgdir}/usr/share/pixmaps/teamspeak3.png

  # Install Custom License
  #install -D -m644 ${pkgdir}/opt/teamspeak3/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Install Client Launcher
  install -D -m755 $srcdir/teamspeak3.launcher ${pkgdir}/usr/bin/teamspeak3
}
# vim:set ts=2 sw=2 et:
