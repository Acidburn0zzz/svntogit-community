# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=libdnf
pkgver=0.65.0
pkgrel=1
pkgdesc="Library providing simplified C and Python API to libsolv"
arch=('i686' 'x86_64')
url="https://github.com/rpm-software-management/$pkgname"
license=('LGPL2.1')
depends=('glib2' 'gpgme' 'json-c' 'libmodulemd' 'librepo' 'libsolv'
         'libutil-linux' 'openssl' 'rpm-tools' 'sqlite' 'zlib')
makedepends=('cmake' 'gtk-doc' 'python' 'python-sphinx' 'swig')
checkdepends=('check' 'cppunit')
optdepends=('python: for python bindings')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('b615a6f7e1d1d82c928d2c79b36242a29d04cd28e267a5e8a6996435d9f97997')

prepare() {
  cd "$pkgname-$pkgver"
  cp -p /usr/share/cmake/Modules/FindLibSolv.cmake cmake/modules/
}

build() {
  cd "$pkgname-$pkgver"

  cmake -B build \
        -DCMAKE_BUILD_TYPE=None \
        -DCMAKE_C_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_CXX_FLAGS_RELEASE='-DNDEBUG' \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DPYTHON_DESIRED=3 \
        -DWITH_ZCHUNK=OFF \
        -Wno-dev

  make -C build VERBOSE=1
}

check() {
  cd "$pkgname-$pkgver"

  # Fails because Arch Linux's /etc/os-release does not have a
  # VERSION_ID field
  make -n -C build ARGS="--output-on-failure" test
}

package() {
  cd "$pkgname-$pkgver"

  make -C build DESTDIR="$pkgdir/" install

  install -Dp -m644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
