# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Eric Lehmann <katyl@katyl.info>
# Contributor: Thomas Fanninger <thomas@fanninger.at>
# Contributor: ultraviolet <ultravioletnanokitty@gmail.com>
# Contributor: Pablo Lezeta <prflr88@gmail.com>

pkgname=iio-sensor-proxy
pkgver=2.7
pkgrel=1
pkgdesc='IIO accelerometer sensor to input device proxy'
arch=('x86_64')
url='https://github.com/hadess/iio-sensor-proxy'
license=('GPL2')
depends=('systemd' 'libgudev' 'gtk3')
source=("https://github.com/hadess/iio-sensor-proxy/releases/download/$pkgver/iio-sensor-proxy-$pkgver.tar.xz")
sha512sums=('5397b69788b7913a43de8a6baada58785f476d6887edadb8e58fef62cc4d7e75f60bf96486e64219f55ebd10a4907dc34e24b1371ae1f60dc726b51badf36b74')

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--sysconfdir=/etc \
  	--bindir=/usr/bin \
  	--sbindir=/usr/bin \
  	--libdir=/usr/lib \
  	--libexecdir=/usr/lib \
  	--with-dbuspolicydir

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

