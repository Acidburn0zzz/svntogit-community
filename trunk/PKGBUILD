# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Orivej Desh <smpuj@bk.ru>

_dir=2450
pkgname=vamp-plugin-sdk
pkgver=2.8.0
pkgrel=1
pkgdesc="The Vamp audio analysis plug-in system"
arch=('x86_64')
url="https://www.vamp-plugins.org/"
license=('BSD')
groups=('pro-audio')
depends=('libsndfile')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_dir}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('abc009dab8d1ae4549640aba4a1341942b5a868479b4a9b2a435556a1bf1ce350d8aec94ee5b403fe8fff4175f40778a8fc12ceaf602dd0f176f9215e4a3f8f8')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  # license
  install -vDm 644 COPYING \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {CHANGELOG,README}
}

# vim:set ts=2 sw=2 et:
