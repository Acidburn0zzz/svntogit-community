# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thorsten Töpper <atsutane-tu@freethoughts.de>

pkgname=vnstat
pkgver=1.11
pkgrel=3
pkgdesc="A console-based network traffic monitor"
arch=('i686' 'x86_64')
url="http://humdi.net/vnstat/"
license=('GPL')
depends=('bash')
makedepends=('gd')
optdepends=('gd: image output')
backup=(etc/vnstat.conf)
source=(http://humdi.net/vnstat/$pkgname-$pkgver.tar.gz
        http://humdi.net/vnstat/init.d/arch/vnstat
        service)
md5sums=('a5a113f9176cd61fb954f2ba297f5fdb'
         'e89a566dd7514ff0d2d3fc77b2d4ca7e'
         'fcc189e3dee616ff414dac72637c1a19')

build() {
  cd $srcdir/$pkgname-$pkgver
  make all
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make DESTDIR=$pkgdir install

  install -D -m0755 $srcdir/vnstat $pkgdir/etc/rc.d/vnstat
  install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/vnstat.service

  install -D -m0644 examples/vnstat.cron $pkgdir/usr/share/doc/vnstat/examples/vnstat.cron
  install -D -m0755 examples/vnstat.cgi $pkgdir/usr/share/doc/vnstat/examples/vnstat.cgi
  sed -i 's#root##' $pkgdir/usr/share/doc/vnstat/examples/vnstat.cron
}
