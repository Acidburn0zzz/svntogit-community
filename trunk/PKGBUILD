# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.6.10
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('bdfff6de705a214f428d1e5e58e82fedb0d58f9b398c5e9a84a0b234f6c82ed4ecef571ff23688a43b6c4aa8abdcd80e004931fcf7b7a8344998672b6c8de947')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/QMenuBar/a #include <QMouseEvent>' src/yearwindow.cpp
  sed -i '/<QQueue>/a #include <QMouseEvent>' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/draginfographicsview.cpp
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/schcedulesearchview.cpp src/daymonthview.cpp src/weekheadview.cpp src/customframe.cpp
}

build() {
  cd dde-calendar-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package() {
  cd dde-calendar-$pkgver
  make DESTDIR="$pkgdir" install
}
