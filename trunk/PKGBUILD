# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Ali H. Caliskan <ali.h.caliskan AT gmail DOT com>
# Contributor: Ryan Coyner <rcoyner@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=mixxx
pkgver=1.8.2
pkgrel=1
pkgdesc="Free, open source software for digital DJ'ing."
arch=('i686' 'x86_64')
url='http://www.mixxx.org'
license=('GPL')
depends=('fftw' 'libid3tag' 'libmad' 'libogg' 'libsndfile' 'portaudio' 'portmidi' 'qt>=4.5')
makedepends=('mesa' 'scons')
source=("http://downloads.mixxx.org/${pkgname}-${pkgver}/${pkgname}-${pkgver}-src.tar.gz")
md5sums=('f0297f4493d4d8e6ad59f72970bad7bc')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  scons qtdir=/usr/lib/qt prefix=/usr install_root="${pkgdir}/usr"
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  scons qtdir=/usr/lib/qt prefix=/usr install_root="${pkgdir}/usr" install
}
