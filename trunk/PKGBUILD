# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.7.1
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest')
# PyPI source is missing some files (e.g. test/noms.txt)
#source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz")
source=("https://gitlab.pyicu.org/main/pyicu/-/archive/v$pkgver/pyicu-v$pkgver.tar.bz2")
sha512sums=('9d6a0c713396e0391a8617e68ad5ec5410e2dbb7ea0decad02816530ecc5a5ce8dc6b0ef905107b45f0e62c9d60e1e0d565ad18aeaf92e38fefd1c123e8754b5')

prepare() {
  # Remove this when switching back to the PyPI tarball
  mv pyicu-v$pkgver PyICU-$pkgver
}

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
