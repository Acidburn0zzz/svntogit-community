# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.10.0
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/CoinUtils"
license=('EPL')
depends=('zlib' 'bzip2')
groups=('coin-or')
source=("http://www.coin-or.org/download/source/CoinUtils/CoinUtils-${pkgver}.tgz")
sha1sums=('99c897d20305e0aac87c7db7f3a2c45dbe3cf874')

build() {
  cd CoinUtils-$pkgver

  export LDFLAGS="$LDFLAGS -lz -lbz2"
  ./configure --prefix=/usr -C
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="${pkgdir}/usr/lib/pkgconfig/" \
  make DESTDIR="$pkgdir" install
}
