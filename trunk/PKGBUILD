# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>

pkgname=(python2-pylibacl python-pylibacl)
pkgbase=pylibacl
pkgver=0.5.4
pkgrel=2
pkgdesc='A python extension module that allows you to manipulate the POSIX.1e ACLs.'
arch=('x86_64')
url='https://pylibacl.k1024.org/'
license=('LGPL')
makedepends=('python' 'python2' 'python-setuptools' 'python2-setuptools')
source=("${url}/downloads/${pkgbase}-${pkgver}.tar.gz"{,.asc})
md5sums=('aba415da393e5b6dc791fd860ed9c5a9'
         'SKIP')
validpgpkeys=('CB94E3AA3B1755D61EBB19A5F66E3E419F84F4DE') # Iustin Pop <iustin@k1024.org>

build() {
	cd "${srcdir}/${pkgbase}-${pkgver}"

	python setup.py build
	python2 setup.py build
}

package_python2-pylibacl() {
	depends=('python2')
	provides=('pylibacl')
	conflicts=('pylibacl')
	replaces=('pylibacl')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python-pylibacl() {
	depends=('python')

	cd "${srcdir}/${pkgbase}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
