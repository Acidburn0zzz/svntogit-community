# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=3.0.0
pkgrel=1
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz")
sha512sums=('1f84a9ef881fe1cd5c24132b803728b89abb48ef1476fa6fb91def499362431207fabaae957aca6da5d83ffa035bffa3f44daee01dd64826356f1de50e5612f3')

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
