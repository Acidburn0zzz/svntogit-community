# $Id$
# Maintainer:
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgbase=ettercap
pkgname=('ettercap' 'ettercap-gtk')
pkgver=NG_0.7.4.2
_origver=${pkgver/_/-}
pkgrel=2
arch=('i686' 'x86_64')
url="http://ettercap.sourceforge.net/"
license=('GPL')
makedepends=('libpcap' 'libltdl' 'libnet' 'gtk2' 'hicolor-icon-theme')
options=('!libtool' '!makeflags' '!emptydirs')
source=("http://drizztbsd.github.com/${pkgbase}/dist/${pkgbase}-${_origver}.tar.gz")
md5sums=('ab2354bf9c91195f06ad0b5d058565a4')

build() {
  cd "${srcdir}"
  sed -i 's/su-to-root -c/gksu/' ${pkgbase}-${_origver}/desktop/ettercap.desktop

  cp -r ${pkgbase}-${_origver} ${pkgbase}-${_origver}-gtk

  cd ${pkgbase}-${_origver}
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --datarootdir=/usr/share \
              --enable-plugins \
              --enable-https \
              --disable-gtk
  make

  cd ../${pkgbase}-${_origver}-gtk
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --mandir=/usr/share/man \
              --datarootdir=/usr/share \
              --enable-plugins \
              --enable-https \
              --enable-gtk
  make
}

package_ettercap() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - console"
  depends=('openssl' 'libpcap' 'pcre' 'libltdl' 'libnet')
  backup=('etc/etter.conf')

  cd "${srcdir}"/${pkgbase}-${_origver}
  make DESTDIR="${pkgdir}" install
}

package_ettercap-gtk() {
  pkgdesc="A network sniffer/interceptor/logger for ethernet LANs - GTK"
  depends=('ettercap' 'gtk2' 'hicolor-icon-theme')
  optdepends=('gksu: to run ettercap from the menu')
  install=ettercap-gtk.install

  cd "${srcdir}"/${pkgbase}-${_origver}-gtk
  install -Dm755 src/ettercap "${pkgdir}"/usr/bin/ettercap-gtk
  install -Dm644 desktop/ettercap.desktop \
    "${pkgdir}"/usr/share/applications/ettercap.desktop
  install -Dm644 share/ettercap.png \
    "${pkgdir}"/usr/share/ettercap/ettercap.png
  install -Dm644 desktop/ettercap.xpm \
    "${pkgdir}"/usr/share/icons/hicolor/32x32/apps/ettercap.xpm
  install -Dm644 desktop/ettercap.svg \
    "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/ettercap.svg
}
