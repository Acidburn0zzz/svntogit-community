# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jonathan Liu <net147@gmail.com>

pkgname=python-pyicu
pkgver=2.7.2
pkgrel=1
pkgdesc="Python binding for ICU"
arch=('x86_64')
license=('MIT')
url="http://pyicu.osafoundation.org/"
depends=('python' 'icu')
makedepends=('python-setuptools')
checkdepends=('python-fonttools' 'python-pytest')
source=("https://pypi.io/packages/source/P/PyICU/PyICU-$pkgver.tar.gz"
        $pkgname-$pkgver-LICENSE::https://gitlab.pyicu.org/main/pyicu/-/raw/v$pkgver/LICENSE)
sha512sums=('45d60ec2fc7a62d4f7b9d4cc954529b2cd998161172f6017a399ebb31fff9d7da24231e461c6ecb17d77246c96d93c7bbf5cd693eb1b66c46a729c215b4134f1'
            'ac2b6d1fe6124b84249426abbf4ecdb7c40ab3101ff6b91fa98776a8f27e6b0c255ed88724d19a77e9c89628aab7a16ede1416d5e2464bfe5d8f3ace59dc67f5')

prepare() {
  # https://gitlab.pyicu.org/main/pyicu/-/issues/148#note_785
  # next release after 2.7.2 will include this in the tarball
  mv -vn $pkgname-$pkgver-LICENSE PyICU-$pkgver/LICENSE
}

build() {
  cd PyICU-$pkgver
  python setup.py build
}

check() {
  cd PyICU-$pkgver
  python setup.py test
}

package() {
  cd PyICU-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
