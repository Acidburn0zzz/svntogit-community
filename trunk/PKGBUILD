# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-panel
pkgver=3.0.2
pkgrel=1
pkgdesc="Launcher and docking facility for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-panel"
groups=('ukui')
provides=('ukui-indicators')
replaces=('ukui-indicators')
depends=('dconf' 'gsettings-qt' 'kwindowsystem' 'libqtxdg' 'peony' 'qt5-webkit' 'ukui-biometric-auth'
         'ukui-menu' 'ukui-menus')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-panel/archive/v$pkgver.tar.gz")
sha512sums=('a5749823c66a87fb3d5c5149eb41028e99b70d0d7b593997b0ab4f44e0fdc9e6b8645299f94c5455fa6638d32c00c704a4f4311eb5a8dbbce7867808814abf38')

prepare() {
  mkdir -p build
  sed -i '1i #include <QPainterPath>' $pkgname-$pkgver/ukui-flash-disk/ejectInterface.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
