# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python-pytools
pkgver=2021.2.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('3ba0540cb8fc153b915183e424abac32d181173488d0f29b612cbad5d51bdbddc9e19c54432937abb6556c4bb6db09fee46e6a4172a62edb4c9f89dfcf00f8c5')

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest
}

package(){
  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
