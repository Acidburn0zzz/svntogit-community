# $Id$
# Maintainer: Your Name <youremail@domain.com>
pkgname=japa
pkgver=0.8.4
pkgrel=2
pkgdesc="A 'perceptual' or 'psychoacoustic' audio spectrum analyser"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/index.html"
license=('GPL2')
groups=('pro-audio')
depends=('clxclient' 'clthreads' 'fftw' 'jack')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('d81652153e88394c81cc257af59b554718d3476c48062cfa7e246627171dc92842f6524a06ab8151220d4dae9d94a386f2d90efb1d238bbbc522988dfeaad32a')

prepare() {
  cd "$pkgname-$pkgver"
  # disable optimizations
  sed -e '/native/d' -i source/Makefile
}

build() {
  cd "$pkgname-$pkgver/source"
  make PREFIX='/usr'
}

package() {
  cd "$pkgname-$pkgver/source"
  make PREFIX='/usr' DESTDIR="$pkgdir/" install
  install -vDm 644 ../{AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
