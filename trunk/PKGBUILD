# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: svoufff <svoufff at gmail dot com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=linuxsampler
pkgver=1.0.0
pkgrel=1
pkgdesc="Professional-grade audio sampler alternative to Gigasampler"
arch=(i686 x86_64)
url="http://www.linuxsampler.org/"
license=('GPL' 'custom')
depends=('libgig' 'jack' 'sqlite3')
makedepends=('dssi' 'lv2core')
options=('libtool') # do NOT slay libtool; required by gigedit
source=(http://download.linuxsampler.org/packages/$pkgname-$pkgver.tar.bz2
        license.txt)
md5sums=('a97136791a3228d840b006e5481fc39d'
         '746c1421eb9f6812b5fa411bfb923c75')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure	--prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # install GPL exception statement
  install -Dm644 ../license.txt \
    "$pkgdir/usr/share/licenses/$pkgname/GPL-EXCEPTION"
}

# vim:set ts=2 sw=2 et:
