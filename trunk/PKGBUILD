# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=cockatrice
_pkgname=Cockatrice
pkgver=2.7.4
_date=2020-03-20
_tag=$_date-Release-$pkgver
pkgrel=4
pkgdesc='A cross-platform virtual tabletop for multiplayer card games'
arch=('x86_64')
url='https://cockatrice.github.io'
license=('GPL2')
depends=('qt5-base' 'protobuf' 'qt5-tools' 'qt5-svg' 'qt5-multimedia' 'qt5-websockets')
optdepends=('mariadb-libs: MySQL/MariaDB connection')
makedepends=('cmake' 'git' 'zlib' 'mariadb-libs')
checkdepends=('gtest' 'valgrind')
optdepends=('zlib: Load zipped files in Oracle')
provides=('cockatrice-client' 'cockatrice-server')
conflicts=('cockatrice-client' 'cockatrice-server')
source=("git+https://github.com/Cockatrice/Cockatrice.git#tag=$_tag"
        'fix-qt-5.15.patch::https://github.com/Cockatrice/Cockatrice/commit/7fa1936d0f4efffa16ae28d6bd3f9e727e207046.patch')
sha512sums=('SKIP'
            '1a61d9fdfa939d2d773d226f6efd0e3446f57b1571548a8b46b7dbadc13674ad9bb2099f0f8c45a1a0e01286118b026981840ccce3874252af3eb4cd35e64548')

prepare() {
  cd $_pkgname

  patch -p1 -i ../fix-qt-5.15.patch
}

build() {
  mkdir -p $_pkgname/build
  cd $_pkgname/build

  cmake .. \
  	-DCMAKE_INSTALL_PREFIX=/usr \
  	-DWITH_ORACLE=ON \
  	-DWITH_CLIENT=ON \
  	-DWITH_SERVER=ON \
  	-DTEST=ON

  make
}

#check() {
#  cd $_pkgname/build
#
#  make test
#}

package() {
  cd $_pkgname/build

  make DESTDIR="$pkgdir" install
}

