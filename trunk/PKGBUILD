# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Wesley Moore <wes@wezm.net>
pkgname=sd
pkgver=0.7.3
pkgrel=1
pkgdesc='Intuitive find & replace'
arch=('x86_64')
url="https://github.com/chmln/sd"
license=('MIT')
makedepends=('rust' 'cargo' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/chmln/sd/archive/v${pkgver}.tar.gz")
sha256sums=('8f8168b849c5da26fdd81b6de3497613631c66ba4f7ab4e86e5adf94ac925dd0')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  install -Dm755 "$pkgname-$pkgver/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
