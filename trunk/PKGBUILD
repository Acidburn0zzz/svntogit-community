# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: geekitus

pkgname=blobwars
pkgver=1.17.1
_pkgver=1.17-1
pkgrel=1
pkgdesc="Platform action game featuring a variety of different weaponry and multiple objectives"
arch=('i686' 'x86_64')
url="http://www.parallelrealities.co.uk/projects/blobWars.php"
license=('GPL')
depends=('hicolor-icon-theme' 'sdl_image' 'sdl_mixer' 'sdl_ttf' 'sdl_net')
install=$pkgname.install
source=(http://www.parallelrealities.co.uk/download/$pkgname/$pkgname-$_pkgver.tar.gz)
md5sums=('b7e639e1dddf13fe7d9de939ef0e5d6f')

build() {
  cd ${srcdir}/$pkgname-1.17

  sed -i -e 's/-g games//' -e 's/-Werror//' makefile || return 1
  make
}

package() {
  cd ${srcdir}/$pkgname-1.17

  make PREFIX=${pkgdir}/usr BINDIR=${pkgdir}/usr/bin/ install
  sed -i 's/Game;/Game;ArcadeGame;/' ${pkgdir}/usr/share/applications/$pkgname.desktop || return 1
}
