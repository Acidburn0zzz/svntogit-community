# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: rtfreedman

_pkgver=0.9.6-rc1
_name=CsoundQt
pkgname=csoundqt
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc="Csound frontend with highlighting editor, autocomplete, interactive widgets and integrated help"
arch=('x86_64')
url="https://csoundqt.github.io/"
license=('CCPL' 'FDL' 'GPL' 'LGPL2.1')
groups=('pro-audio')
depends=('csound' 'csound-doc' 'desktop-file-utils' 'hicolor-icon-theme' 'qt5-declarative' 'rtmidi')
makedepends=('shared-mime-info')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${_pkgver}.tar.gz")
sha512sums=('6ed98d28248936a860b6f92432e031c54f9584ec0b826dc6d1e69adade781d4104f613fc969196b188a413990cc1196821a9ecffc3aea5d653ec2da65be5c2f2')

prepare() {
  mv -v "${_name}-${_pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix inclusion of wrong path:
  # https://github.com/CsoundQt/CsoundQt/issues/258#issuecomment-508981111
  sed -e 's|/usr/local/rtmidi|/usr/include/rtmidi|g' \
      -i config.pri
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake-qt5 qcs.pro \
    SHARE_DIR="${pkgdir}/usr/share" \
    INSTALL_DIR="${pkgdir}/usr"
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # overwrite broken symlink:
  # https://github.com/CsoundQt/CsoundQt/issues/258#issuecomment-508982990
  ln -svf "${_name}-d-cs6" "${pkgdir}/usr/bin/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm644 {ChangeLog,README.md}
}

# vim:set ts=2 sw=2 et:
