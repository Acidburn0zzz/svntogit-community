# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.24
pkgrel=1
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/newville/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-numpy')
source=("https://github.com/newville/asteval/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('35150a0869b99e8b607297884841578bd326386b289b6c46b8d2a3df68f7b416446bda1edde9c245058c8ce8b60b4960a16dec57dcf5a9d59803d8af3869997c')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd asteval-$pkgver
  python setup.py build
}

check() {
  cd asteval-$pkgver
  python -m pytest
}

package() {
  cd asteval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
