# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>

pkgname=python-sphinx-testing
pkgver=1.0.0
pkgrel=1
pkgdesc="Testing utility classes and functions for Sphinx extensions"
arch=('any')
url="https://github.com/sphinx-doc/sphinx-testing"
license=('BSD')
makedepends=('python-setuptools')
depends=('python-six' 'python-sphinx')
checkdepends=('python-mock' 'python-sphinx')
source=("https://pypi.io/packages/source/s/sphinx-testing/sphinx-testing-$pkgver.tar.gz")
sha256sums=('2a421b42b2dae8ecad2b4c9d3953f9970e5a9c07bb2c66626338157435e5708c')

build() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py build
}

check() {
  export PYTHONPATH="$srcdir/sphinx-testing-$pkgver/src"
  cd "$srcdir/sphinx-testing-$pkgver/tests"
  # Tests fail with sphinx 2.0, see https://github.com/sphinx-doc/sphinx-testing/issues/8
  # It does not look like a major issue though.
  python -m unittest discover
}

package() {
  cd "$srcdir/sphinx-testing-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
