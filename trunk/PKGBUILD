# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.5.0alpha7+4+g813d6e7
pkgrel=3
pkgdesc='Graphical tool for merging and comparing text files'
arch=(any)
url='https://github.com/MightyCreak/diffuse'
license=(GPL)
depends=(gtk3 python-cairo python-gobject)
makedepends=(git)
source=("git+$url#commit=813d6e7bbac0dbcbf464951ba6b070562737ed8d"
        "py.patch::$url/commit/175bbd7e79278674b97b08145b13316cf42484ad.patch")
sha256sums=('SKIP'
            '8ba6a584d6fb13502501c9119fdb018bdc9e9d15c4baa59af2793f2c43cf4d35')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-alpha\./alpha/;s/-/+/g'
}

prepare() {
  cd $pkgname
  # Thanks Uffe Jakobsen: https://github.com/MightyCreak/diffuse/pull/27
  sed -i 's|success_results)))|success_results).decode\("utf-8", errors="ignore")))|g' \
    src/usr/bin/diffuse
  # Thanks Derek Scherger: https://github.com/MightyCreak/diffuse/pull/24
  patch -p1 -i "$srcdir/py.patch"
}

package() {
  cd $pkgname
  python install.py --prefix=/usr --destdir="$pkgdir"
}

# vim: ts=2 sw=2 et:
