# $Id$
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=avr-libc
pkgver=1.8.0
pkgrel=1
pkgdesc="The C runtime library for the AVR family of microcontrollers"
arch=('any')
url="http://savannah.nongnu.org/projects/avr-libc/"
license=('BSD')
depends=('avr-gcc')
options=('!strip')
source=(http://download.savannah.gnu.org/releases/$pkgname/$pkgname-$pkgver.tar.bz2{,.sig} \
        avr-libc-1.7.1-gcc46.patch)
md5sums=('54c71798f24c96bab206be098062344f'
         '4dfc58651096f56f49fb07961ea15eee'
         '5a42d423afb2e18bf882f5b0ac51019c')


build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
#  patch -p1 -i "${srcdir}/avr-libc-1.7.1-gcc46.patch"
  ./configure --build=$(./config.guess) --host=avr --prefix=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
