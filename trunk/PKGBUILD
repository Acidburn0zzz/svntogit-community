# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Slash <demodevil5[at]yahoo[dot]com>
# Contributor: J.W. Birdsong  <jwbirdsongATmailDOTcom>

pkgname=teamspeak3
pkgver=3.1.3
pkgrel=2
pkgdesc="TeamSpeak is software for quality voice communication via the Internet"
url="http://www.teamspeak.com/"
license=('custom')
depends=('qt5-base' 'libxkbcommon-x11' 'qt5-webengine')
optdepends=('libpulse')
arch=('i686' 'x86_64')
source=("http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_x86-${pkgver}.run"
        "http://teamspeak.gameserver.gamed.de/ts3/releases/${pkgver}/TeamSpeak3-Client-linux_amd64-${pkgver}.run"
        'teamspeak3.desktop'
        'teamspeak3.png'
        'teamspeak3.launcher'
        'LICENSE')
sha512sums=('cbf21cbfed1f5ff213b3ecf0a7ce68107e2d8b6bcd0b5b1821f8c90c863699a2ff1f587dc079852c28209cbb45104bc1a0e50290275a2e1f221c96c18e75d38e'
            '2a863ea493c8283160cf8d704fb789f0a8d36cace87fe3f977aa3ba7db5015759baa66635593874ad1069f2d9992c9915f456a39f129ca1f767ebf75e6cad359'
            '4cac0b7446dc3e728b232fdd6c6d75be1d2a5530635eab5918a4e7dd6a4a3fc21d6a75055da919b56e5cf849f4c5ba26ece56759a45bc4850eba3eb73853759d'
            'af18ed36a3c8de90b60a7205ebf91a35d4b517f22f97d541c82debdb12697882469d9771fdf3fb8b081aeb74a01237f3afd4732daccf4b374dc6d39e7df5d444'
            '2365bf65500a7455551ca656d27d8fed54dd7151e61e369296e4fa2b5451ee3dd0c4adc67ec1d0f221de0ed7e6efa18c303b63bf2d858060671491bee51c899d'
            'd6d03f1bda86c90390ce0032e4d30e889977f80ff76ba1e5c443109e93cbedbee92c6fc2759f58d23659c15dba4f2ac718861571239ba117dcfe11f136f556dc')

[[ "$CARCH" == "i686" ]] && _TSARCH='x86'
[[ "$CARCH" == "x86_64" ]] && _TSARCH='amd64'

prepare() {
  mkdir archive && cd archive
  sh ../TeamSpeak3-Client-linux_${_TSARCH}-${pkgver}.run --tar -xf 2>/dev/null

  # Delete bundled Qt libs to use system-wide ones
  rm libQt5*

  # Fix FS#34190
  sed -i "/export QTDIR.*/d" ts3client_runscript.sh
  sed -i "/export QT_PLUGIN_PATH*/d" ts3client_runscript.sh

  # Fix FS#34189
  rm qt.conf

  # Fix permissions
  find -type d | xargs chmod 755
  find -type f | xargs chmod 644
  find -name *.so | xargs chmod 755
  chmod +x ts3client*
  chmod +x package_inst
}

package() {
  install -d ${pkgdir}/{usr/bin/,opt/teamspeak3}

  cp -r archive/* ${pkgdir}/opt/teamspeak3/

  # Install Desktop File
  install -D -m644 $srcdir/teamspeak3.desktop ${pkgdir}/usr/share/applications/teamspeak3.desktop

  # Install Icon File
  install -D -m644 $srcdir/teamspeak3.png ${pkgdir}/usr/share/pixmaps/teamspeak3.png

  # Install Custom License
  install -D -m644 $srcdir/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Install Client Launcher
  install -D -m755 $srcdir/teamspeak3.launcher ${pkgdir}/usr/bin/teamspeak3
}
# vim:set ts=2 sw=2 et:
