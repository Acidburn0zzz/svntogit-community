# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Alois Nespor <alium@centrum.cz>
# Contributor: kikadf <kikadf.01@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: thacrazze <thacrazze|googlemail|com>

pkgname=clipgrab
pkgver=3.9.2
pkgrel=1
pkgdesc='A video downloader and converter for YouTube, Veoh, DailyMotion, MyVideo, ...'
arch=(x86_64)
url='https://clipgrab.org'
license=(GPL3)
depends=(qt5-webengine youtube-dl)
optdepends=('ffmpeg: for the conversion functionality')
source=(https://download.clipgrab.org/$pkgname-$pkgver.tar.gz
        $pkgname.desktop
        system-youtube-dl.patch)
sha256sums=('c5bfcb0f6c3bff52ea68c2b978812bcbd43718fd2bcd2dcad1d6a4c970be6cb2'
            '840910097d8c129ea12c395c4a33f562591e878e521c98fde8a7b3f9403aa23f'
            '8ce7a5309470c51bd79cbf6b5b769b302ac7b10f0bd9c7f80871b72822d3567b')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../system-youtube-dl.patch # Fix system youtube-dl detection
}

build() {
  cd $pkgname-$pkgver

  qmake clipgrab.pro
  make
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname -t "$pkgdir"/usr/bin/
  install -Dm644 icon.png "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$srcdir"/$pkgname.desktop -t "$pkgdir"/usr/share/applications/
}
