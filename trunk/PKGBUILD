# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: nut543 <kfs1@online.no>

pkgbase=freedroidrpg
pkgname=('freedroidrpg' 'freedroidrpg-data')
pkgver=0.13
pkgrel=2
arch=('any')
pkgdesc="a mature science fiction role playing game set in the future"
url="http://freedroid.sourceforge.net"
license=("GPL")
source=(http://downloads.sourceforge.net/sourceforge/freedroid/freedroidrpg-$pkgver.tar.bz2 \
	freedroidrpg.jpg \
	freedroidrpg.desktop)
md5sums=('9109ffb242021ee2074767a5bfad63d4'
         'b73d9dac44c7e83a6c80fbe4eb96ba79'
         '9a10c2a2064439cdcff5b945dfb1c3ac')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make DESTDIR=$srcdir/freedroidrpg.pkg install
}

package_freedroidrpg() {
  arch=('i686' 'x86_64')
  depends=('sdl_mixer' 'sdl_image' 'libogg' 'libvorbis' 'libgl' 'freedroidrpg-data')

  mkdir $pkgdir/usr
  mv $srcdir/freedroidrpg.pkg/usr/bin $pkgdir/usr/
  install -D -m644 $srcdir/freedroidrpg.jpg $pkgdir/usr/share/icons/freedroidrpg.jpg
  install -D -m644 $srcdir/freedroidrpg.desktop $pkgdir/usr/share/applications/freedroidrpg.desktop
}

package_freedroidrpg-data() {
  arch=('any')
  pkgdesc="a mature science fiction role playing game set in the future (data files)"
  depends=()

  mkdir $pkgdir/usr
  mv $srcdir/freedroidrpg.pkg/usr/share $pkgdir/usr/
}
