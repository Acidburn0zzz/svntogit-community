# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=2.0.0
_commit=ec46992546faf2f1829c410186808d0152d9a92a
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-appdirs' 'python2-appdirs' 'python2-enum34' 'python-pytz' 'python2-pytz'
             'python2-scandir' 'python-setuptools' 'python2-setuptools' 'python-six' 'python2-six'
             'git')
checkdepends=('python-nose' 'python2-nose' 'fuse2' 'python-mock' 'python2-mock' 'python-pyftpdlib'
              'python2-pyftpdlib')
source=("git+https://github.com/PyFilesystem/pyfilesystem2.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a pyfilesystem2{,-py2}

  sed -e '/enum34/d' -e '/scandir/d' -i pyfilesystem2/setup.py
}

build() {
  cd "$srcdir"/pyfilesystem2
  python setup.py build

  cd "$srcdir"/pyfilesystem2-py2
  python2 setup.py build
}

check_disabled() {
  # Tests hangs, disabled in 2.0.0
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyfilesystem2
  nosetests3

  cd "$srcdir"/pyfilesystem2-py2
  nosetests2
}

package_python-fs() {
  depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')

  cd pyfilesystem2
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-appdirs' 'python2-enum34' 'python2-pytz' 'python2-scandir' 'python2-setuptools'
           'python2-six')

  cd pyfilesystem2-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
