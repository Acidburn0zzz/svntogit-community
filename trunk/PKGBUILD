# Maintainer: David Runge <dave@sleepmap.de>
pkgname=ssr
pkgver=0.5.0
pkgrel=1
pkgdesc="A tool for real-time spatial audio reproduction"
arch=('x86_64')
url="http://spatialaudio.net/ssr/"
license=('GPL3')
groups=('pro-audio')
depends=('asio' 'ecasound' 'fftw' 'glu' 'qt5-base')
makedepends=('gendesk' 'help2man' 'qt5-tools')
source=("https://github.com/SoundScapeRenderer/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('89234be15d4607278a7e91935300bd60aec3a4b21f006a54059ccbdba2e3eac48e2869f58172dd4569d188aad45b04f14516c49b157f44c04482656abd103dfe')
_name="SoundScape Renderer"

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  gendesk -n \
          --pkgname ${pkgname} \
          --name ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-aap \
          --name ${pkgname}-aap \
          --pkgdesc "${_name} (Ambisonics Amplitude Panner): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-binaural \
          --name ${pkgname}-binaural \
          --pkgdesc "${_name} (Binaural): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-brs \
          --name ${pkgname}-brs \
          --pkgdesc "${_name} (Binaural Room Synthesis): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-dca \
          --name ${pkgname}-dca \
          --pkgdesc "${_name} (Distance-coded Ambisonics): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-generic \
          --name ${pkgname}-generic \
          --pkgdesc "${_name} (Generic): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-vbap \
          --name ${pkgname}-vbap \
          --pkgdesc "${_name} (Vector Based Amplitude Panning): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
  gendesk -n \
          --pkgname ${pkgname}-wfs \
          --name ${pkgname}-wfs \
          --pkgdesc "${_name} (Wave Field Synthesis): ${pkgdesc}" \
          --genericname "Spatial Audio Renderer" \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr --disable-vrpn
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 ${pkgname}{,-{aap,binaural,brs,dca,generic,vbap,wfs}}.desktop \
    -t "${pkgdir}/usr/share/applications"
}
