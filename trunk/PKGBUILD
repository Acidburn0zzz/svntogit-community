# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=3.1.12
pkgrel=1
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('any')
url="https://github.com/linuxdeepin/deepin-screenshot"
license=('GPL3')
depends=('deepin-menu' 'deepin-qml-widgets' 'pygtk' 'qt5-quickcontrols'
         'qt5-svg' 'python2-pyqt5' 'python2-wnck' 'python2-xpybutil'
         'python2-opengl')
makedepends=('deepin-gettext-tools')
optdepends=('deepin-social-sharing: for "Share" support'
            'deepin-daemon: for sound effect')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screenshot/archive/$pkgver.tar.gz")
sha512sums=('ae310f5120cf983329c4678f93a7f4856d6f5cbd6adca3119620ba103b9dc4f0e68040b1b4a21a879f8dd3930ab7fbdcdd434f3751b48655a6cdbd7208c05c22')

prepare() {
  cd deepin-screenshot-$pkgver

  # fix python version
  find src -type f | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-screenshot-$pkgver
  make
}

package() {
  cd deepin-screenshot-$pkgver
  make DESTDIR="$pkgdir" install
}

 # Local Variables:
 # sh-basic-offset: 2
 # End:
