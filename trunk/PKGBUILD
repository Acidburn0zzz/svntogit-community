# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=webhook
pkgver=2.6.11
pkgrel=1
pkgdesc="A lightweight incoming webhook server to run shell commands"
arch=('x86_64')
url="https://github.com/adnanh/webhook"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/adnanh/webhook/archive/$pkgver.tar.gz"
        webhook.service
        go.uuid.patch::https://github.com/adnanh/webhook/pull/321.patch)
sha512sums=('561bbb14674e868fc101563639c0d4f49c01a569f50fff8777905e76aceb59abcc80466029c32193ac139e6085d98153cc7d94cdbb2a239b108bc26efa943b16'
            '4b08e6348a7e51aa4df668f1541f12e08960801d2e039b480de6ad73d26b8c325012474f83a3c8e819caafc216535c0b259aed1daf2226d545f895d0bd3d8efe'
            'fd66e890167fd620aed3df57dceb251460ac13e67b1c64e13d7a2628720703cf08977d61a8a30bf19988cfd273c2aa221ca98448f280fc5c35343a08d145c46f')

prepare() {
  cd webhook-$pkgver
  patch -p1 -i ../go.uuid.patch

  mkdir -p .gopath/src/github.com/adnanh
  ln -sf "$PWD" .gopath/src/github.com/adnanh/webhook
  export GOPATH="$PWD/.gopath:/usr/share/gocode"

  rm -r vendor

  go get github.com/codegangsta/negroni github.com/gorilla/mux github.com/satori/go.uuid \
         gopkg.in/fsnotify.v1 github.com/ghodss/yaml
}

build() {
  cd "$srcdir"/webhook-$pkgver
  go build  -ldflags "-extldflags ${LDFLAGS}" -o build/webhook
}

package() {
  cd webhook-$pkgver

  install -Dm755 build/webhook "$pkgdir"/usr/bin/webhook
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  install -dm755 "$pkgdir"/etc/webhook
  install -Dm644 "$srcdir"/webhook.service "$pkgdir"/usr/lib/systemd/system/webhook.service
}
