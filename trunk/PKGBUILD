# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-filter
pkgname=python-django-filter
pkgver=2.2.0
pkgrel=2
pkgdesc="A generic system for filtering Django QuerySets based on user selections"
arch=('any')
url="https://github.com/carltongibson/django-filter"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-django-crispy-forms'
'python-django-rest-framework' 'python-mock' 'python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('16a4336d1558cabfe497dd9736a2f652c9f9d141a72c856cf13f3ca56db4da85b5926b05c772aef5fa43a75e085025244b8deaf7facbc5bfa68907bd99f4f95d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python runtests.py
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,{CHANGES,README}.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
