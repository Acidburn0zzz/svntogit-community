# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-icon-theme
pkgver=2020.05.21
pkgrel=1
pkgdesc="Deepin Icons"
arch=('any')
url="https://github.com/linuxdeepin/deepin-icon-theme"
license=('GPL3')
depends=('papirus-icon-theme')
replaces=('deepin-cursor-theme')
conflicts=('deepin-cursor-theme')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-icon-theme/archive/$pkgver.tar.gz"
        $pkgname-fix-installation.patch::https://github.com/linuxdeepin/deepin-icon-theme/pull/23.patch)
sha512sums=('790e70a7372b50a7fa97b2e36d815e551b0a6b160c31044e2dc4082a90e59800c820b9a8794b1acab1bc1f63638429c5d69dae01bef3e840c6fc079bf9e5bbac'
            'de3365adc47d824ae8f527db7b68f568d623bf5f96ab3f511146594aee57db8fd587dc6d2c5639283f620f6f19fa8dd5b789c703dfa7eaf571bd766e397cba23')

prepare() {
  patch -d $pkgname-$pkgver -p1 < $pkgname-fix-installation.patch
  sed -i 's/deepin/bloom/g' $pkgname-$pkgver/tools/hicolor.links
}

build() {
  cd $pkgname-$pkgver
  make hicolor-links
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cp -a ./Sea ./usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
}
