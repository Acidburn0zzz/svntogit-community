# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.2.0
pkgrel=2
pkgdesc='A LISP dialect for the JVM that safely supports concurrency.'
url='http://www.clojure.org'
arch=('i686' 'x86_64')
license=('CPL')
depends=('java-environment')
makedepends=('apache-ant' 'libxrender') #libxrender maybe needed due to openjdk missing dep. jre doesn't have this problem
optdepends=('clojure-contrib: user contributions and extras'
            'java-forkjoin: java concurrency library wrapped by clojure.parallel'
            'rlwrap: for a friendlier shell with readline support')
install=clojure.install
source=("http://github.com/downloads/clojure/clojure/${pkgname}-${pkgver}.zip"
        'clj'
        'clojure.sh')
md5sums=('da0cc71378f56491d6ee70dee356831f'
         'aa5846298c4b185abc09866a05078c3f'
         '7e02402b3f875ec60c77da9dbe324786')


build() {
  cd ${pkgname}-${pkgver}

  ant
}

package() {
  # launcher script
  install -D clj \
    ${pkgdir}/usr/bin/clj

  # profile
  install -D clojure.sh \
    ${pkgdir}/etc/profile.d/clojure.sh

  # clojure
  install -D -m644 ${pkgname}-${pkgver}/clojure.jar \
    ${pkgdir}/usr/share/clojure/clojure.jar

}

