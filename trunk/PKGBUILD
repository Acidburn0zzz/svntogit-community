# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_discover
pkgname=python-dephell-discover
pkgver=0.2.4
pkgrel=1
pkgdesc="Find project modules and data files (packages and package_data for setup.py)"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('be80a188e5624678812f966c8cd2e6b2db6b54239fd0fdeef1abbc1babfa1d85')
b2sums=('65af6e838de96060eadf76851216fe26e94f88723fed2ea459f676bba0fa7876e2ff3c238110efbd55a744cbdd590ec3e3cdf6737fbe6069279da25efce1832c')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # fix attr dependency, see https://github.com/dephell/dephell_discover/pull/3
    sed -i '/install_requires/{s/attr/attrs/}' setup.py

    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
