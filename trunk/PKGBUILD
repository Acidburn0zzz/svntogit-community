# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.1.0.0
pkgrel=2
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('0d090fe0d87b8cc16852783ee43336dc6a3a375c9ff94849a327b70a6534ba1384bd8395d639a21230254680d718a7150c3dedf1ad2e18c2aa10b7165fadeb3d')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of third-party commercial company's homepage
  sed -i '/www.baidu.com/i \    "https://www.archlinux.org",' connectivitychecker.cpp
}

build(){
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
