# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=5.1.0.9
pkgrel=2
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz"
         deepin-dock-qt5.15.patch)
sha512sums=('0d88e418ee21702f2786c47cce923c534cb7a564152eabb4706d54265c7675b563278949286e4fd247b06fa9b521e62b454d97ce28481b13730ad7f387e3d45f'
            '1fb3a6490e152d081f5fdff655e503dac602cb29b231df252edd664bf8b5ffcc589efd7d96b7b61e53f620670b110dcd5e1952fe5b88fe65fbf8acf8ce8812fd')

prepare() {
  patch -d dde-dock-$pkgver -p1 -i ../deepin-dock-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dde-dock-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  make
}

package() {
  cd dde-dock-$pkgver
  make DESTDIR="$pkgdir" install
}
