# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mox3
pkgname=('python-mox3' 'python2-mox3')
pkgver=1.0.0
pkgrel=2
arch=('any')
pkgdesc='Mock object framework for Python'
url='https://docs.openstack.org/developer/mox3'
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-fixtures' 'python2-fixtures')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/mox3/archive/$pkgver.tar.gz")
sha512sums=('18e4fe6514993cfae07b0b9d0a7759285803cd09035865bf8355ef3d5eb24a07294cfcdb20fdd222c6774a6fa09a8838186ce7bbfe19e0430a060041c0ca0320')

prepare() {
  # Python 3.6 fixes
  sed -i 's/, flags=4//' mox3-$pkgver/mox3/tests/test_mox.py

  cp -a mox3-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/mox3-$pkgver
  python setup.py build

  cd "$srcdir"/mox3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd mox3-$pkgver
  stestr run
}

package_python-mox3() {
  depends=('python-pbr' 'python-fixtures')

  cd mox3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-mox3() {
  depends=('python2-pbr' 'python2-fixtures')

  cd mox3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
