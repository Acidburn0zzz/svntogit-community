# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hopenpgp-tools
pkgver=0.21
pkgrel=4
pkgdesc="hOpenPGP-based command-line tools"
url="http://floss.scru.org/hopenpgp-tools"
license=("AGPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-wl-pprint' 'haskell-attoparsec'
         'haskell-base16-bytestring' 'haskell-binary-conduit' 'haskell-conduit'
         'haskell-conduit-extra' 'haskell-crypto-pubkey' 'haskell-cryptohash' 'haskell-errors'
         'haskell-fgl' 'haskell-graphviz' 'haskell-hopenpgp' 'haskell-http-client'
         'haskell-http-client-tls' 'haskell-http-types' 'haskell-ixset-typed' 'haskell-lens'
         'haskell-monad-loops' 'haskell-openpgp-asciiarmor' 'haskell-optparse-applicative'
         'haskell-prettyprinter' 'haskell-prettyprinter-ansi-terminal' 'haskell-resourcet'
         'haskell-time-locale-compat' 'haskell-yaml')
makedepends=('alex' 'happy' 'ghc')
source=(https://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz
        ghc-8.4.patch)
sha512sums=('bfea3b24fbf61ddf0da0b87728448d191921c662eead7f0de0383fc1c653d4a77a7c3babbfc55a5cb1ea7ef4215499654182622d7be98617c0b2f1d9d079f751'
            '94810a884eef875fc61f8235e4fad6c1e67d267fe3edaad4968f52110ea732b8c911068cc324b92bbdb045bf7f527eb866485f7737bd876277fc3c1ce20db8f1')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../ghc-8.4.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    runhaskell Setup configure -O --prefix=/usr --enable-executable-dynamic --docdir="/usr/share/doc/${pkgname}"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    rm "$pkgdir/usr/share/doc/hopenpgp-tools/LICENSE"
    rmdir "$pkgdir/usr/share/doc/hopenpgp-tools" "$pkgdir/usr/share/doc" "$pkgdir/usr/share"
}
