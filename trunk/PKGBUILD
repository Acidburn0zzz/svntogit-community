# Maintainer:
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vice
pkgver=3.2
pkgrel=1
pkgdesc="The Versatile Commodore 8-bit Emulator"
arch=('x86_64')
license=('GPL')
url="http://vice-emu.sourceforge.net/"
depends=('gtkglext' 'libpulse' 'giflib')
makedepends=('xorg-bdftopcf' 'xorg-mkfontdir' 'libpcap' 'libxaw' 'ffmpeg' 'xa')
source=("http://downloads.sourceforge.net/project/vice-emu/releases/${pkgname}-${pkgver}.tar.gz"
        'disable-fc-cache.patch')
sha256sums=('28d99f5e110720c97ef16d8dd4219cf9a67661d58819835d19378143697ba523'
            '8058210e092c4943a55baeff47e6d5f3661aeb3686fd1f75e07f47888da9cc98')

prepare() {
  cd ${pkgname}-${pkgver}
  rm -rf src/lib/{liblame,libx264,libffmpeg}

  patch -p1 -i "$srcdir/disable-fc-cache.patch"
  sed -i 's/lib64/lib/g' configure.ac
  autoreconf -fi

  sed -e 's|CODEC_CAP_VARIABLE_FRAME_SIZE|AV_&|g' -e 's|CODEC_FLAG_GLOBAL_HEADER|AV_&|g' -e 's|\& AVFMT_RAWPICTURE||' \
    -i src/gfxoutputdrv/ffmpegdrv.c # Fix build with ffmpeg 4
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr \
    --libdir=/usr/lib \
    --enable-gnomeui \
    --enable-external-ffmpeg
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" realdocdir="/usr/share/doc/vice" install -j1
  ln -s /usr/share/doc/vice "${pkgdir}/usr/lib/vice/doc"
}
