# Maintainer: Robin Broda <coderobe @ archlinux.org>
pkgname=nebula
pkgver=1.1.0
pkgrel=1
pkgdesc='A scalable overlay networking tool with a focus on performance, simplicity and security'
arch=('x86_64')
url='https://github.com/slackhq/nebula'
license=('MIT')
depends=()
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('ff08ce10c202a047149397b172e2effaac7f213676e99bb01293b751e73a33fd')

build() {
  cd "${pkgname}-${pkgver}"

  go build \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS} -X main.Build=${pkgver}" \
    -o ./nebula ./cmd/nebula

  go build \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS} -X main.Build=${pkgver}" \
    -o ./nebula-cert ./cmd/nebula-cert

  go build \
    -trimpath \
    -ldflags "-extldflags ${LDFLAGS} -X main.Build=${pkgver}" \
    -o ./nebula-service ./cmd/nebula-service
}

check() {
  cd "${pkgname}-${pkgver}"

  go test -v ./...
}

package() {
  cd "${pkgname}-${pkgver}"

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm755 nebula "${pkgdir}/usr/bin/nebula"
  install -Dm755 nebula-cert "${pkgdir}/usr/bin/nebula-cert"
  install -Dm755 nebula-service "${pkgdir}/usr/bin/nebula-service"
}
