# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks dot org>
# Contributor: NeoRaider

pkgname=fastd
arch=("x86_64")
pkgver=19
pkgrel=1
pkgdesc="Fast and secure tunneling daemon"
makedepends=('cmake')
depends=('libcap' 'gcc-libs' 'libuecc>=6' 'libsodium' 'json-c' 'openssl')
url="https://projects.universe-factory.net/projects/fastd"
license=('BSD')
source=("https://projects.universe-factory.net/attachments/download/86/${pkgname}-${pkgver}.tar.xz")
sha512sums=('fc15c34214c41880449fe5f47c7577e31b61dda68eb8b3aa5a0b1ad2ab1922a3c4a92afa87a318ea30bb4579b8b0f6cee510312d895c16b3f5824bbc23523a3e')

build() {
  mkdir -p "$srcdir/build-${pkgver}"
  cd "$srcdir/build-${pkgver}"
  
  cmake \
    -D CMAKE_BUILD_TYPE=RELEASE \
    -D CMAKE_AR=/usr/bin/gcc-ar \
    -D CMAKE_NM=/usr/bin/gcc-nm \
    -D CMAKE_RANLIB=/usr/bin/gcc-ranlib \
    -D CMAKE_INSTALL_PREFIX=/usr \
    -D ENABLE_SYSTEMD=ON \
    -D ENABLE_LTO=ON \
    -D ENABLE_OPENSSL=ON \
    "../${pkgname}-${pkgver}"
  make
}

package() {
  cd "$srcdir/build-${pkgver}"
  make install DESTDIR="${pkgdir}"
  
  install -dm755 "${pkgdir}/etc/fastd/"
  
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/COPYRIGHT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/examples/fastd@.service" "${pkgdir}/usr/lib/systemd/system/fastd@.service"
  install -Dm644 "$srcdir/${pkgname}-${pkgver}/doc/fastd.1" "${pkgdir}/usr/share/man/man1/fastd.1"
}
