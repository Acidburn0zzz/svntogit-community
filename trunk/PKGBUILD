# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=EasyProcess
pkgname=python-${_pkg,,}
pkgver=0.2.10
pkgrel=1
pkgdesc="Easy to use python subprocess interface"
arch=(any)
url="https://github.com/ponty/EasyProcess"
license=(BSD)
depends=(python)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-nose python-pyvirtualdisplay iputils xorg-server-xvfb)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('636b5e30222cde1e881fafc0112b35d516bce41ddee83ecbac656dec9befea1b')

prepare() {
  sed -i 's|isAlive|is_alive|' ${_pkg}-${pkgver}/easyprocess/__init__.py
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}/build/lib/" pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
