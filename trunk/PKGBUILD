# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-responses
pkgver=0.13.2
pkgrel=1
pkgdesc='A utility library for mocking out the `requests` Python library.'
arch=('any')
license=('Apache')
url='https://github.com/getsentry/responses'
depends=('python-requests' 'python-biscuits' 'python-six')
makedepends=('python-setuptools')
checkdepends=('mypy' 'python-pytest-runner' 'python-pytest-cov' 'flake8'
              'python-pytest-localserver')
source=("https://github.com/getsentry/responses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('ef6e9cd455ae68711a648034eead4dc224dc8935cc8c525d8299a6fd19880944774e68bd457e83343b6d00ffcf4e9a6d9f261c5a8cf72c3934e35c817a3b0019')

build() {
  cd responses-$pkgver
  python setup.py build
}

check() {
  cd responses-$pkgver
  python setup.py pytest
}

package() {
  cd responses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
