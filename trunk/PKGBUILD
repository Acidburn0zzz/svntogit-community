# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=lxterminal
pkgver=0.1.11
pkgrel=2
pkgdesc="VTE-based terminal emulator (part of LXDE)"
arch=('i686' 'x86_64')
license=('GPL2')
url="http://lxde.org/"
groups=('lxde')
depends=('vte')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/${pkgname}-${pkgver}.tar.gz
        shift-keybinds.patch)
md5sums=('fd9140b45c0f28d021253c4aeb8c4aea'
         '7eff715e02181b006b1912cd041b7f1a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -Np1 -i "$srcdir"/shift-keybinds.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --sysconfdir=/etc --prefix=/usr
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

