# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Maato <maato softwarebakery com>

pkgname=volumeicon
pkgver=0.5.1
pkgrel=1
pkgdesc='Volume control for your system tray'
arch=('x86_64' 'i686')
url='http://softwarebakery.com/maato/volumeicon.html'
license=('GPL3')
depends=('gtk3' 'alsa-lib' 'libnotify')
makedepends=('intltool' 'git')
install=volumeicon.install
source=('git://github.com/Maato/volumeicon.git#commit=090fe14c0f')
md5sums=('SKIP')

pkgver() {
  head -1 "$pkgname/ChangeLog" | cut -d: -f1
}

prepare() {
  cd "$pkgname"
  ./autogen.sh
}

build() {
  cd "$pkgname"
  ./configure --prefix=/usr --enable-notify
  make
}

package() {
  make -C "$pkgname" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
