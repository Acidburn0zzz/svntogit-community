# Maintainer: Ivy Foster <code@escondida.tk>

pkgname=translate-shell
pkgver=0.9.6.7
pkgrel=1
pkgdesc='A command-line interface and interactive shell for Google Translate'
arch=(any)
url='https://www.soimort.org/translate-shell/'
license=('custom:PublicDomain')
depends=(bash gawk)
optdepends=(
	'aspell: spell checking'
	'curl: openSSL support'
	'fribidi: display right-to-left languages'
	'hunspell: spell checking'
	'less: terminal paging'
	'mplayer: Text-to-Speech functionality'
	'mpv: Text-to-Speech functionality'
	'mpg123: Text-to-Speech functionality'
	'espeak: Text-to-Speech functionality'
	'rlwrap: readline-style editing and history in the interactive mode'
	'emacs: Emacs interface'
)
source=("translate-shell-$pkgver.tar.gz::https://github.com/soimort/translate-shell/archive/v$pkgver.tar.gz")
# Checksum provided by packager, not upstream; grain of salt recommended.
md5sums=(ceed584244a77a9eb3c08d9dea2405ba)

build() {
	cd "translate-shell-$pkgver"
	make
}

package() {
	cd "translate-shell-$pkgver"
	make PREFIX=/usr DESTDIR="$pkgdir" install
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/translate-shell/LICENSE"
}
