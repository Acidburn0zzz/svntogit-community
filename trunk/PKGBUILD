# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: judd <jvinet@zeroflux.org>
pkgname=exim
pkgver=4.76
pkgrel=1
pkgdesc="A Message Transfer Agent"
arch=(i686 x86_64)
url="http://www.exim.org/"
license=('GPL')
backup=(etc/mail/aliases etc/mail/exim.conf \
        etc/logrotate.d/exim etc/conf.d/exim)
install=exim.install
changelog=exim.changelog
depends=('db' 'pcre' 'pam' 'tcp_wrappers' 'openssl' 'libldap')
provides=('smtp-server')
conflicts=('smtp-server')
options=('!makeflags')
source=(ftp://mirrors.24-7-solutions.net/pub/exim/ftp/exim/exim4/exim-${pkgver}.tar.bz2
        aliases 
        exim 
        exim.logrotate 
        exim.conf.d
        exim.Makefile
        )
md5sums=('58e784b33c7a2ab335ec6400346d6362'
         '4874006f0585253ddab027d441009757'
         '9aed772e87223213e8da9ca5e7376869'
         'd788c26f86a9d72a0aebb3b849fe74f2'
         'b75fe4c6e960a59a25b5f51e8f61ba3a'
         '61e76543476f52f136c1d6c80ac1c5a1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  sed -i 's|tail -1|tail -n -1|g' scripts/Configure-config.h
  # Make some configuration changes
  cp ${srcdir}/${pkgname}.Makefile Local/Makefile
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m644 ../exim.logrotate ${pkgdir}/etc/logrotate.d/exim
  install -D -m644 ../exim.conf.d ${pkgdir}/etc/conf.d/exim
  install -D -m644 doc/exim.8 ${pkgdir}/usr/share/man/man8/exim.8
  install -D -m755 ../exim ${pkgdir}/etc/rc.d/exim
  mkdir -p ${pkgdir}/var/spool/exim/db ${pkgdir}/etc/mail \
    ${pkgdir}/var/log/exim ${pkgdir}/usr/{lib,sbin}
  chmod 770 ${pkgdir}/var/spool/exim ${pkgdir}/var/spool/exim/db ${pkgdir}/var/log/exim
  cd build-Linux-*
  for i in exicyclog exim_checkaccess exim_dumpdb exim_lock\
          exim_tidydb exipick exiqsumm exigrep exim_dbmbuild exim\
          exim_fixdb eximstats exinext exiqgrep exiwhat
  do
          install -m 0755 $i ${pkgdir}/usr/sbin
  done

  cd ${srcdir}/exim-${pkgver}/src
  sed "s|/etc/aliases|/etc/mail/aliases|g" configure.default | \
    sed "s|SYSTEM_ALIASES_FILE|/etc/mail/aliases|g" \
    >${pkgdir}/etc/mail/exim.conf

  cp ${srcdir}/aliases ${pkgdir}/etc/mail
  cd ${pkgdir}/usr/sbin
  for i in mailq rmail rsmtp runq sendmail 
  do
      ln -s exim $i
  done
  # fhs compliancy
  ln -s ../sbin/exim ../lib/sendmail

  mkdir -p ${pkgdir}/etc/rc.d
  cp ${srcdir}/exim ${pkgdir}/etc/rc.d
}
