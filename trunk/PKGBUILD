# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgbasename=glib2
pkgname=lib32-$_pkgbasename
pkgver=2.50.1
pkgrel=2
pkgdesc="Common C routines used by GTK+ 2.4 and other libs (32-bit)"
url="http://www.gtk.org/"
arch=('x86_64')
license=('LGPL')
depends=(lib32-dbus lib32-libffi lib32-pcre lib32-util-linux lib32-libelf "$_pkgbasename")
makedepends=('gcc-multilib' 'python' 'git')
_commit=e44ea516afeb41d22cebf968b3ea5d9543856df2 # tags/2.50.1^0
#options=('!docs')
source=("git://git.gnome.org/glib#commit=$_commit"
        gio-querymodules-32.hook
        revert-warn-glib-compile-schemas.patch)
sha256sums=('SKIP'
            'e36da43ac0f705860646d4aecf66f82030bb0926814b5d4ec4e3284f24fecc36'
            '049240975cd2f1c88fbe7deb28af14d4ec7d2640495f7ca8980d873bb710cc97')

pkgver() {
  cd glib
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd glib
  patch -Rp1 -i ../revert-warn-glib-compile-schemas.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd glib
  ./configure --prefix=/usr --sysconfdir=/etc \
      --libdir=/usr/lib32 --with-pcre=system --disable-fam
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd glib
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{share,include}}

  cd "${pkgdir}"/usr/bin
  mv gio-querymodules gio-querymodules-32
  rm -f gdbus glib* gobject-query gsettings gtester*
  rm -rf "$pkgdir"/usr/lib32/gdbus-2.0
  find "$pkgdir/usr/bin" -type f -not -name gio-querymodules-32 -delete

  # install hook
  install -d "$pkgdir/usr/share/libalpm/hooks/"
  install -m644 "$srcdir"/gio-querymodules-32.hook \
    "$pkgdir/usr/share/libalpm/hooks/"
}
