# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.187
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('a8e6f6270eca41d353a4d2bd954990d2311730fc9879625ce7736c23c98b3aaf87d6be9f530fb02201f5026207a911a586d36f54ed7aca1aac478a1ce462ab9b')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

package() {
  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s ../lib/node_modules/$pkgname/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/usr/lib/node_modules/$pkgname
}

# vim:set ts=2 sw=2 et:
