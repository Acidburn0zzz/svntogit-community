# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-bleach
pkgname=(python-bleach python2-bleach)
pkgver=1.5.0
pkgrel=5

pkgdesc="An easy whitelist-based HTML-sanitizing tool"
url="http://pypi.python.org/pypi/bleach"
arch=('any')
license=('Apache')

makedepends=('python'
             'python-setuptools'
             'python2'
             'python2-setuptools')

source=("python-bleach-$pkgver.tar.gz::https://pypi.org/packages/source/b/bleach/bleach-$pkgver.tar.gz"
        "html5lib-python-0.9999999.tar.gz::https://github.com/html5lib/html5lib-python/archive/0.9999999.tar.gz"
        "0001-Make-bleach-allow-for-vendored-html5lib-version.patch")

md5sums=('b663300efdf421b3b727b19d7be9c7e7'
         '2ca78b1ec5852779bc121a97da6e8d4d'
         'c7f318607346ce3f151bebd73f15864e')

prepare() {
	(cd bleach-$pkgver
	patch -p1 <"$srcdir"/0001-Make-bleach-allow-for-vendored-html5lib-version.patch)

	cp -a bleach-$pkgver{,-python2}
}

build() {
	cd "$srcdir"/bleach-$pkgver
	python setup.py build

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py build
}

package_python-bleach() {
	cd "$srcdir"/html5lib-python-0.9999999
	python setup.py install --root="$pkgdir"/usr/lib/python3.6/site-packages --install-purelib=bleach --optimize=1

	cd "$srcdir"/bleach-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-bleach() {
	cd "$srcdir"/html5lib-python-0.9999999
	python2 setup.py install --root="$pkgdir"/usr/lib/python2.7/site-packages --install-purelib=bleach --optimize=1

	cd "$srcdir"/bleach-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
}
