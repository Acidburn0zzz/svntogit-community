# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-ujson
pkgname=('python-ujson' 'python2-ujson')
_pkgname=ultrajson
pkgver=1.34
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/esnme/ultrajson"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/esnme/ultrajson/archive/v${pkgver}.tar.gz)
sha512sums=('fce4c9d8d84ac84a35ed762f120c6e8ce8c2b0d29831c3d2a7e79c72b59279441b95e35831b149c501d00d738f0b9864f720da689e5da840a064c8c0b437006e')

prepare() {
  cp -ra ${_pkgname}-${pkgver}{,-py2}
  2to3 -w ${_pkgname}-${pkgver}-py2/tests/tests.py
}

build() {
  (cd ${_pkgname}-${pkgver}
    python setup.py build
  )
  (cd ${_pkgname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  (cd ${_pkgname}-${pkgver}
    # TODO: tests are currently broken
    #local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    #PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      #python tests/tests.py
  )
  (cd ${_pkgname}-${pkgver}-py2
    # TODO: tests are currently broken
    #local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
    #PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
      #python2 tests/tests.py
  )
}

package_python-ujson() {
  depends=('python')

  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

package_python2-ujson() {
  depends=('python2')

  cd ${_pkgname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
