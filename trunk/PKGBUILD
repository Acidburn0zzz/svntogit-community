# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-text
pkgver=0.3.0
pkgrel=1
pkgdesc='Go text processing support'
arch=('any')
url="https://github.com/golang/text"
license=('BSD')
depends=('go-pie' 'golang-golang-x-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/golang/text/archive/v$pkgver.tar.gz")
sha512sums=('bd60dfaa7d9dbfca171a3ba40b51f5663c01257cf3b12b618d35689a97ab2212d316d5716ff09681bf40b099f7e20a0e0df8cfc065699cb6a07ad1dc2e2bc189')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$srcdir"/build/src/golang.org/x/text
  cd "$srcdir"/build/src/golang.org/x/text
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$pkgdir"/usr/share/gocode/src/golang.org/x/text

  install -Dm644 text-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
