# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=12
pkgrel=2
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject')
makedepends=('glib2' 'jack')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-12-remove_gconf.patch")
sha512sums=('e49a5fa901de38230911c73e2ea60ea8e9e400c5ae1ca5220181e25d5eda5506f61f8dfdaf99839e73b4c9af62d035acf2ef471c304dc1743e1f61f0444bf1f8'
            '3187b3f6d6d94259e9e291021f0bba10106379beaba902638b5de6f0ce70ace6695c9e04dd0659e3fd25e7615b4a4587b6b1a7548d4059248c3d3e3a99edfd30')
b2sums=('1d83452ba02286b069441ebb14008950bbbe1668582d6372a9ff5de4be333f0f63b4e3fea5cb5509a41f05df13a6f03e5ee5ca41688ba82b18970b569362a619'
        '1b4f68f5e5b5a3369aea778f8e10428febc702093a571429b0c8c845d68c8f3ca770a2813e18eec59ec4c4224e023969fee800e6c6c8b63e1552a400d8b73957')

prepare() {
  mv -v "$pkgname-release-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove legacy gconf integration
  # https://github.com/jack-mixer/jack_mixer/issues/2
  patch -Np1 -i "../${pkgname}-12-remove_gconf.patch"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
