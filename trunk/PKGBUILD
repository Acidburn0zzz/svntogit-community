# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.12.28
pkgrel=1
pkgdesc="HashiCorp tool for building and updating infrastructure as code idempotently"
url="https://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go")
source=(
	"$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz"
	"test-backport.patch"
)
sha512sums=('cadd2a5527b4c4ee6dd0385d962435e15ac72f99bc0d3e8193f78a49a474e491dab73f9280c77f37f34e516873b12a7f1af23cd437cb2f9d0afb2bef9966c836'
            '5c7468f47c19867598bafe07322d1c5a7314286eabab6012aa70a55967967467e620f5a56aeecadf6ebd13021f2c01a4e54c70c41ee09dfd702a082507b3ae47')
changelog="CHANGELOG.md"

prepare() {
	cd "$pkgname-$pkgver"
	patch -Np1 < "${srcdir}/test-backport.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CGO_CPPFLAGS="${CPPFLAGS}"
	export CGO_CFLAGS="${CFLAGS}"
	export CGO_CXXFLAGS="${CXXFLAGS}"
	export CGO_LDFLAGS="${LDFLAGS}"
	export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
	go build -o terraform-binary
}

check() {
	cd "${pkgname}-${pkgver}"
	go test -mod=vendor ./...
}

package() {
	cd "$pkgname-$pkgver"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
