# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: ChaosKid42 <christoph.scholz@gmail.com>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>

_name=rugged
# ideally this should match $pkgver, as package() might fail otherwise. thanks, github
_libgit2_pkgver=0.28.3
pkgname=ruby-rugged
pkgver=0.28.3
pkgrel=1
pkgdesc='A Ruby binding to the libgit2 linkable library'
arch=('x86_64')
url="https://github.com/libgit2/rugged"
license=('MIT')
depends=('ruby' 'libgit2')
makedepends=('ruby-minitest' 'ruby-rake' 'ruby-rake-compiler' 'ruby-rdoc')
checkdepends=('git')
options=(!emptydirs)
source=("${_name}-${pkgver}.tar.gz::https://github.com/libgit2/${_name}/archive/v${pkgver}.tar.gz"
        "libgit2-${_libgit2_pkgver}.tar.gz::https://github.com/libgit2/libgit2/archive/v${_libgit2_pkgver}.tar.gz")
sha512sums=('f377653fb84326968f1126177b0cc6279da5e5f74529605087af7b0c481d203c22d89ca26105dc871ce197c1f91c7fa03cae24fb20d14b65f7002ec017a0e4fe'
            '15444823b7d4885f7b8c3982f8905efc4a75913de016a9b2e0a24d5ce9746e6a549dffd5469036529557feff2ce7ece9328266eb312c80b96091ce0f65ee97ee')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd ${pkgname}-${pkgver}
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i ${_name}.gemspec
  # remove broken and useless libgit2 "version check":
  # https://github.com/libgit2/rugged/issues/698
  sed '68,88d' -i ext/rugged/extconf.rb
  # imported version is wrong:
  # https://github.com/libgit2/rugged/issues/776#issuecomment-521234453
  sed -E "s|(VERSION =) .+|\1 '${pkgver}'|" -i lib/rugged/version.rb
  # source tarball doesn't include vendored libgit2 version, which is only
  # needed for test fixtures (resources): https://github.com/libgit2/rugged/issues/801
  mv -v ../libgit2-${_libgit2_pkgver}/* vendor/libgit2
}

build() {
  cd "${pkgname}-${pkgver}"
  export CI_BUILD=true
  export RUGGED_USE_SYSTEM_LIBRARIES=true
  export CMAKE_FLAGS=" --use-system-libraries"
  rake compile
  rake gem
}

check(){
  cd "${pkgname}-${pkgver}"
  # ConfigTest#test_read_global_config_file fails, if no gitconfig is set for user...
  # https://github.com/libgit2/rugged/issues/432
  git config --global user.name "Foo Bar"
  rake test
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              pkg/${_name}-${pkgver}.gem
  install -vDm 644 "$pkgdir/$_gemdir/gems/$_name-$pkgver/LICENSE" \
    -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.md CHANGELOG.md -t \
    "${pkgdir}/usr/share/doc/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rv cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out} \
    gems/${_name}-${pkgver}/vendor
}
