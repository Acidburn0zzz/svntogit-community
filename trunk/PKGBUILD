# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Filipp "Scorp" Andjelo <scorp@mailueberfall.de>

pkgbase=lxinput
pkgname=(lxinput lxinput-gtk3)
pkgver=0.3.4
pkgrel=2
pkgdesc="Small program to configure keyboard and mouse for LXDE"
arch=('i686' 'x86_64')
url="http://lxde.org/"
license=('GPL')
groups=('lxde')
depends=('gtk2' 'gtk3')
makedepends=('intltool')
source=(http://downloads.sourceforge.net/lxde/$pkgbase-$pkgver.tar.xz
        gtk3.patch)
sha256sums=('6e37074e406a05792f6eaac029638bd02fe88926f20736d73dcb5113da825c24'
            '73504c8bebf946f9806f9d72a79691357d1b705ca4798f5597538b9ece06e7f3')

prepare() {
  cd $pkgbase-$pkgver
  patch -Np1 -i ../gtk3.patch
}

build() {
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --prefix=/usr
  make

  cd "$srcdir"
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --prefix=/usr --enable-gtk3
  make
}

package_lxinput() {
  groups=('lxde')
  depends=('gtk2')

  cd gtk2
  make DESTDIR="$pkgdir" install
}

package_lxinput-gtk3() {
  groups=('lxde-gtk3')
  pkgdesc+=' (GTK+ 3 version)'
  depends=('gtk3')
  conflicts=('lxinput')

  cd gtk3
  make DESTDIR="$pkgdir" install
}
