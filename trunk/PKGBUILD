# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>

pkgname=lib32-json-glib
pkgver=1.2.2
pkgrel=1
pkgdesc='JSON library built on GLib'
arch=('x86_64')
url='https://wiki.gnome.org/Projects/JsonGlib'
license=('GPL')
depends=('json-glib' 'lib32-glib2')
makedepends=('gobject-introspection' 'python2')
source=("http://ftp.gnome.org/pub/GNOME/sources/json-glib/${pkgver%.*}/json-glib-${pkgver}.tar.xz")
sha256sums=('ea128ab52a824fcd06e5448fbb2bd8d9a13740d51c66d445828edba71321a621')

build(){
  cd json-glib-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32'
  make
}

package(){
  cd json-glib-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
