# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-whitenoise
pkgver=5.1.0
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-brotli' 'python-django' 'python-pytest' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('2e247566ca098345d76646a2108ed63ee7216d60bd30fa5afda4aa5cce3a69d1c2c31b716b4a5d2d72284bd58d92c714440f110012b47e4a8d5cbaafa1973257')

build() {
  cd whitenoise-$pkgver
  python setup.py build
}

check() {
  cd whitenoise-$pkgver
  DJANGO_SETTINGS_MODULE=tests.django_settings pytest
}

package() {
  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
