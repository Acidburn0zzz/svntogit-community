# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris Baker <baker.chris.3@gmail.com>

pkgname=python-nltk
pkgver=2.0b9
pkgrel=3
pkgdesc='Software for natural language processing'
arch=('any')
url='http://www.nltk.org/'
license=('APACHE')
depends=('python2' 'python-yaml')
optdepends=('nltk-data: test data'
            'python-numpy: used for calculations'
            'python-matplotlib: used for plotting')
source=("http://nltk.googlecode.com/files/nltk-${pkgver}.tar.gz"
        'fixdatapath.diff')
md5sums=('7b9a083b3b37cff3b84ca9c231ab131b'
         '86b71519f4380849ba737e169b84640b')

build() {
  cd nltk-${pkgver}

  # fix FS#21743 - [python-nltk] NLTK data files not installed in a place the package knows about
  patch -Np0 -i ${srcdir}/fixdatapath.diff

  python2 setup.py build
}

package() {
  cd nltk-${pkgver}

  python2 setup.py install --root=${pkgdir} --optimize=1
}
