# Maintainer: David Runge <dave@sleepmap.de>
_name=tabulate
pkgname=python-tabulate
pkgver=0.8.4
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python-setuptools')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('1a71e8f7d4eea9c03c7ab327fc919ecb367aeaa2b075b5c9bccf394e308c74c3451a4d99f9361847499d89de751face469e981e7dce519c9a8cb658c37b3363e')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
}

package_python-tabulate() {
  cd "${_name}-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
