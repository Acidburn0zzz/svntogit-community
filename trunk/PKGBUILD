# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=biber
pkgver=2.8 # for biblatex 3.8
pkgrel=1
pkgdesc="A Unicode-capable BibTeX replacement for biblatex users"
arch=('any')
license=('PerlArtistic' 'GPL')
url="https://sourceforge.net/projects/biblatex-biber/"
# Encode::* modules are not real dependencies.
depends=('perl-autovivification'
         'perl-business-isbn'
         'perl-business-ismn'
         'perl-business-issn'
         'perl-class-accessor'
         'perl-data-compare'
         'perl-data-dump'
         'perl-data-uniqid'
         'perl-datetime-calendar-julian'
         'perl-datetime-format-builder'
         'perl-file-slurper'
         'perl-io-string' # required by Biber::Output::bblxml
         'perl-ipc-run3'
         'perl-lingua-translit'
         'perl-list-allutils'
         'perl-log-log4perl'
         'perl-lwp-protocol-https'
         'perl-mozilla-ca'
         'perl-perlio-utf8-strict'
         'perl-regexp-common'
         'perl-sort-key'
         'perl-text-bibtex'
         'perl-text-csv'
         'perl-text-roman'
         'perl-unicode-linebreak'
         'perl-xml-libxml-simple'
         'perl-xml-libxslt'
         'perl-xml-writer')
# ExtUtils::LibBuilder and Config::AutoConf are not actual makedepends
makedepends=('perl-module-build' 'perl-test-differences')
checkdepends=('perl-capture-tiny' 'perl-file-which')
options=('!emptydirs')
# The sourceforge tarball is missing bblxml.pm
source=("https://github.com/plk/biber/archive/v${pkgver}.tar.gz"
        "sortinithash-unicode-collate-1.21.patch")
sha256sums=('01640f1d3827d0f2f0572db19f5836cb58acf2d7c1a1925faa2326f0b16e8e1a'
            'd72953acd63453ec5363fcfc830137deb6cf3827d5899edb50b1b835a0ca013f')

prepare() {
  cd "${srcdir}/biber-${pkgver}"
  # We don't have Unicode::Collate 1.21
  patch -R -Np1 -i ${srcdir}/sortinithash-unicode-collate-1.21.patch
  # Disable long year tests on 32 bit
  if [[ $CARCH = "i686" ]]; then
    sed -i '/17000002/ s+.*+eq_or_diff("dummy", "dummy", "skip 32 bit");+' t/dateformats.t
  fi
}

build() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  perl Build.PL
  ./Build
}

check() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  LC_CTYPE=en_US.UTF-8 BIBER_DEV_TESTS=1 ./Build test
}

package() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install installdirs=vendor destdir="${pkgdir}"
}
