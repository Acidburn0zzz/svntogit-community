# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.112
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'python')
optdepends=('gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support')
source=("https://github.com/gnumdk/lollypop/releases/download/${pkgver}/lollypop-${pkgver}.tar.xz")
sha256sums=('be9c0123e8d2ee6771929ed2aaac8fe0ae744141a97f7741bf3123c224d4e30e')

build() {
  cd lollypop-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd lollypop-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
