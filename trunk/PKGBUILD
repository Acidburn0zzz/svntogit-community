# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgbase=python-arrow
pkgname=('python-arrow' 'python2-arrow')
pkgver=0.12.1
pkgrel=4
pkgdesc='Better dates and times for Python'
arch=('any')
url="https://arrow.readthedocs.io"
license=('Apache')
makedepends=('python-dateutil' 'python-setuptools'
             'python2-dateutil' 'python2-setuptools' 'python2-backports.functools_lru_cache')
checkdepends=('python-chai' 'python-nose' 'python-simplejson'
              'python2-chai' 'python2-nose' 'python2-simplejson')
source=("https://files.pythonhosted.org/packages/source/a/arrow/arrow-${pkgver}.tar.gz")
sha256sums=('a558d3b7b6ce7ffc74206a86c147052de23d3d4ef0e17c210dd478c53575c4cd')

prepare() {
  cp -r {,python-}arrow-${pkgver}
  mv {,python2-}arrow-${pkgver}
}

build() {
  for py in python{,2}; do
    pushd ${py}-arrow-${pkgver}
    ${py} setup.py build
    popd
  done
}

check() {
  cd python-arrow-${pkgver}
  nosetests
  nosetests2
}

package_python-arrow() {
  depends=('python' 'python-dateutil')

  cd python-arrow-${pkgver}

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-arrow() {
  depends=('python2' 'python2-backports.functools_lru_cache' 'python2-dateutil')

  cd python2-arrow-${pkgver}

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
