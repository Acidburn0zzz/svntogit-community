# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sloppybench <sloppybench@eml.cc>
# Contributor: der_FeniX <derfenix@gmail.com>
# Contributor: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gordin <9ordin @t gmail dot com>
# Contributor: Simon Lipp <aur@simon.lipp.name>
# Contributor: Chris Bolton <http://scr.im/chris-kun>
# Contributor: Sergei Lebedev <superbobry at gmail dot com>
# Contributor: flying sheep <flying-sheep@web.de>

pkgname=coffeescript
pkgver=2.2.2
pkgrel=1
pkgdesc='CoffeeScript is a little language that compiles into JavaScript'
arch=('any')
url='http://coffeescript.org'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=("coffee-script=$pkgver")
conflicts=('coffee-script')
replaces=('coffee-script')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('5499d06ee4ff9545c53de87d64fc2ea998c6db5ea783113cf6f7e3d5ad6194068209539bebde2429731548c9bd90b2936e431029cfdd3ecf400633b682130c25')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
