# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200810
_pkgver=2020-08-10
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('9da221674e4b3509b429b1bd00f063422dd5ff92bf3cecebb1aace5927453dd574a355ba0cde430c91f36f609cdae46565d45f0585b4d71addaabe5df66ef3b7')

prepare() {
  cd $pkgname-$_pkgver
  sed -i '/ensure_rustfmt()?;/d' xtask/src/lib.rs
}

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

check() {
  cd $pkgname-$_pkgver
  cargo test --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
