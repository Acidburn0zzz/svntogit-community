# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=dsdcc
pkgver=1.8.5
pkgrel=2
pkgdesc='Digital Speech Decoder (DSD) rewritten as a C++ library'
arch=('x86_64')
url='https://github.com/f4exb/dsdcc'
license=('GPL3')
depends=('mbelib' 'serialdv')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('da4aefee4e8d31507991b4a21df19858826b39b32820c8060fba21138d3216a8fe0fa2399050ab07383654eddf25696985361a853ef0cba0366957f249b24bee')

prepare() {
  cd $pkgname-$pkgver

  sed -i 's|LIBSERIAL_NAMES|LIBSERIALDV_NAMES|g' cmake/Modules/FindSerialDV.cmake
}

build() {
  mkdir -p "$srcdir"/$pkgname-$pkgver/build
  cd "$srcdir"/$pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DLIBSERIALDV_INCLUDE_DIR=/usr/include/serialdv \
    -DUSE_MBELIB=ON

  make

  # Build tests
  cd "$srcdir"/$pkgname-$pkgver/testfec

  make
}

check() {
  cd $pkgname-$pkgver/testfec

  ./qr
  ./golay20
  ./golay23
  ./golay24
  ./hamming7
  ./hamming12
  ./hamming15
  ./hamming16
  ./viterbi
  ./viterbi35
  ./crc
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}

