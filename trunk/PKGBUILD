# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jacktrip
pkgver=1.5.0
pkgrel=1
pkgdesc="Multi-machine audio network performance over the Internet"
arch=(x86_64)
url="https://github.com/jacktrip/jacktrip"
license=(LGPL3 GPL3 MIT)
groups=(pro-audio)
depends=(gcc-libs glibc hicolor-icon-theme qt5-base)
makedepends=(help2man meson jack python-jinja python-yaml)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('056f7596f528d38f7c4decc32b86775647fd4c00455dc668ee5a3f9bcfd37712085e1ddd7f24d7cac00b9020f47e36b693807f0b4b5cc13bf9da033c7f2cf1f5')
b2sums=('1f15dc09a5359bd4f2e33b61df43ee354025a5a4a869df1be99a0d0aa0f387a2374f2fb0ff409d7127fa2276c93791106807f12f014005fbfd2406ba28cde137')

build() {
  arch-meson -Drtaudio=disabled build "${pkgname}-${pkgver}"
  ninja -C build
}

check() {
  ninja -C build test
}
package() {
  depends+=(libjack.so)

  DESTDIR="${pkgdir}" meson install -C build

  # licenses
  install -vDm 644 "${pkgname}-${pkgver}/"{LICENSE.md,LICENSES/MIT.txt} -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 "${pkgname}-${pkgver}/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}
