# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Todd Musall <tmusall@comcast.net>

pkgname=apcupsd
pkgver=3.14.10
pkgrel=2
pkgdesc="power mangement and controlling most of APC's UPS models"
arch=(i686 x86_64)
url="http://www.apcupsd.org"
license=('GPL')
depends=('gcc-libs')
optdepends=('gd: for CGI'
	    'gconf: for frontend'
	    'gtk2: for frontend')
makedepends=('pkgconfig' 'gd' 'gconf' 'gtk2')
backup=(etc/apcupsd/apcupsd.conf etc/apcupsd/hosts.conf
        etc/apcupsd/multimon.conf)
source=(http://downloads.sourceforge.net/apcupsd/$pkgname-$pkgver.tar.gz
	apcupsd
	apcupsd.service)
md5sums=('5928822d855c5cf7ac29655e3e0b8c23'
         '8bc674da24188c69b0ad5d7fe7510f73'
         '3baf366b2cfabf83a206a1cb0c8f4c26')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr  --libexecdir=/usr/lib/$pkgname \
		--enable-cgi --enable-usb --enable-net \
		--with-upstype=usb --with-upscable=usb \
		--with-serial-dev=/dev/usb/hid/hiddev[0-9] \
		--enable-pthreads --enable-gapcmon
  make
  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/apcupsd" "$pkgdir/etc/rc.d/apcupsd"
  install -Dm644 "$srcdir/apcupsd.service" "$pkgdir/usr/lib/systemd/system/apcupsd.service"
  chmod 755 "$pkgdir"/sbin/*
  install -dm755 "$pkgdir/usr/lib/$pkgname/cgi-bin"
  mv "$pkgdir"/etc/apcupsd/*.cgi "$pkgdir/usr/lib/$pkgname/cgi-bin"
}
