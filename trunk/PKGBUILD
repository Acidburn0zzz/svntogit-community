# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-openstacksdk
pkgname=(python-openstacksdk python2-openstacksdk)
pkgver=0.36.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://developer.openstack.org/sdks/python/openstacksdk"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-jsonpatch'
             'python2-jsonpatch' 'python-appdirs' 'python2-appdirs' 'python-keystoneauth1'
             'python2-keystoneauth1' 'python-os-service-types' 'python2-os-service-types'
             'python-deprecation' 'python2-deprecation' 'python-requestsexceptions'
             'python2-requestsexceptions' 'python-yaml' 'python2-yaml' 'python-decorator'
             'python2-decorator' 'python-jmespath' 'python2-jmespath' 'python2-ipaddress'
             'python2-futures' 'python-iso8601' 'python2-iso8601' 'python-netifaces'
             'python2-netifaces' 'python-dogpile.cache' 'python2-dogpile.cache' 'python-munch'
             'python2-munch' 'python-cryptography' 'python2-cryptography')
checkdepends=('python-stestr' 'python-mock' 'python-requests-mock' 'python-oslotest'
              'python-jsonschema' 'python-oslo-config' 'python-prometheus_client')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/openstacksdk/archive/$pkgver.tar.gz")
sha512sums=('ac5557700cf2e8ad19767d6237bf2fc2f5e60b09143771088153213b31a7aa5bd33cf61cde392f821688613d3e307eddb98a914688f8a226ea596909c2c74ab8')

export PBR_VERSION=$pkgver

prepare() {
  echo -e "\nglobal-include *.json" >> openstacksdk-$pkgver/MANIFEST.in
  cp -a openstacksdk-$pkgver{,-py2}
  sed -i 's/assertItemsEqual/assertCountEqual/' openstacksdk-$pkgver/openstack/tests/unit/*.py openstacksdk-$pkgver/openstack/tests/unit/*/*.py
}

build() {
  cd "$srcdir"/openstacksdk-$pkgver
  python setup.py build

  cd "$srcdir"/openstacksdk-$pkgver-py2
  python2 setup.py build
}

check() {
  # From https://github.com/openstack/openstacksdk/blob/master/tox.ini
  export OS_LOG_CAPTURE=true OS_STDOUT_CAPTURE=true OS_STDERR_CAPTURE=true

  cd openstacksdk-$pkgver
  # TODO
  stestr run || :
}

package_python-openstacksdk() {
  depends=('python-six' 'python-pbr' 'python-yaml' 'python-jsonpatch' 'python-appdirs' 'python-keystoneauth1'
           'python-os-service-types' 'python-deprecation' 'python-requestsexceptions'
           'python-decorator' 'python-jmespath' 'python-iso8601' 'python-netifaces'
           'python-dogpile.cache' 'python-munch' 'python-cryptography')

  cd "$srcdir"/openstacksdk-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openstacksdk() {
  depends=('python2-six' 'python2-pbr' 'python2-yaml' 'python2-jsonpatch' 'python2-appdirs'
           'python2-keystoneauth1' 'python2-os-service-types' 'python2-deprecation'
           'python2-requestsexceptions' 'python2-decorator' 'python2-jmespath' 'python2-ipaddress'
           'python2-futures' 'python2-iso8601' 'python2-netifaces' 'python2-dogpile.cache'
           'python2-munch' 'python2-cryptography')

  cd "$srcdir"/openstacksdk-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openstack-inventory{,2}
}

# vim:set ts=2 sw=2 et:
