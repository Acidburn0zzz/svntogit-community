# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=1.9.2
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'clutter-gtk' 'cjs' 'gnome-bluetooth'
         'gnome-icon-theme' 'gnome-menus' 'cinnamon-settings-daemon' 'cinnamon-session'
         'gnome-themes-standard' 'gstreamer0.10' 'libgnome-keyring' 'librsvg'
         'networkmanager' 'muffin' 'pygtk' 'python2-dbus' 'python2-pillow'
         'python2-pyinotify' 'python2-lxml' 'webkitgtk' 'gnome-panel' 'python2' 'cinnamon-translations')
makedepends=('gnome-common' 'intltool')
optdepends=('cinnamon-control-center: extended configurations for Cinnamon'
            'cinnamon-screensaver: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nemo: the official Cinnamon file manager (forked from Nautilus)')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/archive/$pkgver.tar.gz"
        "remove_GC.patch"
        "fix-control-center-check.patch"
        "background.patch"
        "clutter_cairo_texture_invalidate.patch")
sha256sums=('0abaa8da02c4d626084aa482adc551af154796c4c35faaebebdac896ce72aa6d'
            '3d362efd15f8cfeca1713f5bcf88d4be787b39d7c7f24b73cd13f867af33a680'
            'ee5694bdc997ffa35a817f691b15bae13747137d35ec2aecd0da298d7edbe426'
            '373d80cdb23250fbde846ed493ba422672cc42b03a111c2ce044467ee782df7f'
            'b7656c138738dc600c7d9949fa4298bca8381184dd829f15c8525cf1f4c4d6d7')

prepare() {
  cd ${srcdir}/Cinnamon*

  # Python2 fix
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Fix crasher when disconnecting from wifi
  patch -Np1 -i ../remove_GC.patch

  # Check for the cc-panel path, not for the unneeded binary
  patch -Np1 -i ../fix-control-center-check.patch

  # Fix missing backgrounds
  patch -Np1 -i ../background.patch

  # Prefix 'System Settings' with 'Cinnamon' to avoid confusion with gnome-control-center
  sed -i 's/^Name\(.*\)=\(.*\)/Name\1=Cinnamon \2/' files/usr/share/applications/cinnamon-settings.desktop
}

build() {
  cd ${srcdir}/Cinnamon*

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
  make CFLAGS="${CFLAGS} -Wno-deprecated -Wno-deprecated-declarations"
}

package() {
  cd ${srcdir}/Cinnamon*
  make DESTDIR="${pkgdir}" install

  # Remove leftover files after patching
  find "$pkgdir" -type f -name *.orig | xargs rm
}
