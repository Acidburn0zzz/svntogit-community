# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: damir <damir@archlinux.org>

pkgname=gcin
pkgver=2.8.8
pkgrel=1
pkgdesc='Input method server supporting various input methods'
url='https://hyperrate.com/dir.php?eid=67'
license=('LGPL')
arch=('x86_64')
depends=('gtk2' 'libxtst')
makedepends=('qt5-base' 'qt4' 'gtk3' 'anthy')
optdepends=('qt5-base: support for qt5 input method'
            'qt4: support for qt4 input method'
            'gtk3: support for gtk3 input method'
            'anthy: support for anthy input method'
            'curl: for downloading and uploading vocabulary databases via ts-edit')
source=("https://hyperrate.com/gcin-source/${pkgname}-${pkgver}.tar.xz")
sha512sums=('6fa9955f5f4fdd67b80af9e91b65ff15488368453041dd4c71a3875fc08e9027aa9202783f20d4513f3c01528cb6f8141489546cf21be8fa5ba17060d55d09d4')

install=install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    sed 's/lib64/lib/g' -i configure
    sed '/include suffixes-rule/a \
        CFLAGS+='"${CFLAGS}"' \
        LDFLAGS+='"${LDFLAGS}"' \
        OPTFLAGS=' \
        -i Makefile
    sed \
        -e 's:QT=qt5:QT=qt:' \
        -i qt5-im/Makefile59
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix=/usr --use_i18n=Y
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
