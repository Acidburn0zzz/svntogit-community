# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-jung-kurt-gofpdf
pkgver=1.0.0.20180222
_commit=567bef933d5649777a8b39772ad57dc9ddaba8d8
pkgrel=1
pkgdesc='A PDF document generator with high level support for text, drawing and images'
arch=('any')
url="https://github.com/jung-kurt/gofpdf"
license=('MIT')
depends=('go-pie' 'golang-github-boombuler-barcode' 'golang-github-ruudk-golang-pdf417'
         'golang-golang-x-image')
source=("$pkgname-$_commit.tar.gz::https://github.com/jung-kurt/gofpdf/archive/$_commit.tar.gz")
sha512sums=('7be5d1e9e1b4b879d96b54cbf03d7515b92eb4f323ea5fbdf2d87de7c5628c9a7a33d5dda1d5808ab9cbec1e70b90e94b8e2819d11077044a6d0ddb56c33ab6b')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$_commit "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  cd "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$_commit "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt/gofpdf

  install -Dm644 gofpdf-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
