# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Wenxuan Zhang <wenxuangm@gmail.com>

pkgname=rust-script
pkgver=0.20.0
pkgrel=1
pkgdesc='Run Rust files and expressions as scripts without any setup or compilation'
arch=('x86_64')
url='https://rust-script.org'
license=('MIT' 'Apache')
depends=('gcc-libs' 'rust')
makedepends=('git')
_commit='8b6f3d13d90c712002ec94123e5c0dc814976a68'
source=("$pkgname::git+https://github.com/fornwall/rust-script#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd "$pkgname"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname"

  cargo build --frozen --release
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" target/release/rust-script

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md

  # licenses
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE*
}
