# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.190
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('5b71260f1f4411f116d1bb94c10525f26df15bd08a7861fdbbe6f628d6ef43691cd5d4c2e60c46731a2d02bf08866ab6b90cad132d9ad31b585d0b8c0a87b34b')

prepare() {
  cd $pkgname-$pkgver
  npm ci
}

build() {
  cd $pkgname-$pkgver/packages/$pkgname
  npm run build
}

package() {
  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s ../lib/node_modules/$pkgname/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/usr/lib/node_modules/$pkgname
}

# vim:set ts=2 sw=2 et:
