# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Brendan MacDonell <macdonellba at gmail dot com>
# Contributor: Linmiao Xu <linmiao.xu@gmail.com>
# Contributor: Juan Miguel Cejuela <juanmi@jmcejuela.net>

pkgbase=python-apsw
pkgname=('python-apsw' 'python2-apsw')
pkgver=3.32.2
pkgrel=1
arch=('x86_64')
url="https://github.com/rogerbinns/apsw"
license=('MIT')
depends=('sqlite')
makedepends=('python' 'python2')
source=("apsw-$pkgver-r1.tar.gz::https://github.com/rogerbinns/apsw/archive/$pkgver-r1.tar.gz")
sha512sums=('f3412846d7ae2bf27cccf5a69217bc53ac1c321e0ce4ba3e2c47a302d58d7de6816fcc2a68746407bb963fa402064e7ff22ef8cba5ae2e8a415543715ffc510a')
b2sums=('a84f273dda33edcaa8ecb9f269e2959ea133cab8f93acc35faa5eb737149a108b0aa30120b6d4b838afe08451ab9619d86441ca82ecfa54be7ed62432a0d8131')

build() {
  cd apsw-${pkgver}-r1

  python setup.py build --enable=load_extension
  python2 setup.py build --enable=load_extension
}

check() {
  cd "$srcdir"/apsw-${pkgver}-r1

  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH-"2*)
  PYTHONPATH="${pythonpaths[0]}" python setup.py test
  PYTHONPATH="${pythonpaths[1]}" python2 setup.py test
}

package_python-apsw() {
depends+=('python')
pkgdesc="Python wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apsw() {
depends+=('python2')
pkgdesc="Python2 wrapper for SQLite"
  cd "$srcdir"/apsw-${pkgver}-r1
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
