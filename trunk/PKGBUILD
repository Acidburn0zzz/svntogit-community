# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=lib32-rust
pkgver=1.29.0
pkgrel=1

pkgdesc='32-bit libraries for rust'
url='https://www.rust-lang.org/'
arch=('x86_64')
license=('MIT' 'Apache')

makedepends=('libffi' 'perl' 'python2' 'curl' 'cmake' 'ninja')
depends=('lib32-gcc-libs' "rust=1:$pkgver-1")

source=("https://static.rust-lang.org/dist/rustc-$pkgver-src.tar.gz"{,.asc}
        config.toml
        https://github.com/rust-lang/rust/commit/1b94b84ad0143ea2039610e3aec9e929a8a20733.patch)

sha256sums=('a4eb34ffd47f76afe2abd813f398512d5a19ef00989d37306217c9c9ec2f61e9'
            'SKIP'
            '5c1962458f20dd5a542db7ef31e35c20a7b42f9e74dd27ec4e9f0cf2ab1953ce'
            '8c8e79b0bac0dd1434c49f665abfa50634ffb0fbb35c9efee511e5b97960b9e2')
validpgpkeys=('108F66205EAEB0AAA8DD5E1C85AB96E6FA1BE5FE') # Rust Language (Tag and Release Signing Key) <rust-key@rust-lang.org>

prepare() {
  cd "rustc-$pkgver-src"

  patch -p1 <"$srcdir"/1b94b84ad0143ea2039610e3aec9e929a8a20733.patch
  cp "$srcdir"/config.toml config.toml
}

build() {
  cd "rustc-$pkgver-src"

  msg2 "Run build under x.py"
  python2 ./x.py build
}

package() {
  cd "rustc-$pkgver-src"

  target="$pkgdir"/"$(rustc --print sysroot)"/lib/rustlib
  install -d "$target"
  cp -r \
    build/x86_64-unknown-linux-gnu/stage2/lib/rustlib/i686-unknown-linux-gnu \
    "$target"

  for license in APACHE MIT; do install -Dm644 "LICENSE-$license" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE-$license"; done
}

# vim:set ts=2 sw=2 et:
