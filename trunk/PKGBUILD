# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-image
pkgver=0.0.20190515
_commit=61b8692d9a5c9886248d7f96e0ba50ad77baab4c
pkgrel=1
pkgdesc='Go supplementary image libraries'
arch=('any')
url="https://github.com/golang/image"
license=('BSD')
depends=('go-pie' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/image/archive/$_commit.tar.gz")
sha512sums=('54caa5d54197aff5b0bf3301cd4205aa1359a9d20e667fcd3e4b5b5c6c567ad57f58b357cb338320d001128b2c29699070da732d8b51592b99aa8ef7f042bb6a')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$srcdir"/build/src/golang.org/x/image
  cd "$srcdir"/build/src/golang.org/x/image
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/image

  install -Dm644 image-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
