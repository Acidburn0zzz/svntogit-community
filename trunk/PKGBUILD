# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: FadeMind <fademind@gmail.com>
# Contributor: Zatherz <zatherz at linux dot pl>

pkgname=qt5ct
pkgver=0.41
pkgrel=5
pkgdesc="Qt5 Configuration Utility"
arch=(x86_64)
url="https://qt5ct.sourceforge.io/"
license=(BSD)
depends=(qt5-svg)
makedepends=(qt5-tools)
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname-$pkgver.tar.bz2"
         qt5.15-palette.patch)
sha256sums=('f24030e90d8b923b60c320b2db5b4c0fcc47f9f35ac4c52060ab3c6e423057dc'
            '98e87d63bd3873decf7364fe1ba70c075eee94f561a0aad06e78bd66b61149e1')

prepare() {
  cd $pkgname-$pkgver
  patch -p3 -i ../qt5.15-palette.patch # Fix QPalette use with Qt 5.15
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 $pkgname.pro 
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
} 

