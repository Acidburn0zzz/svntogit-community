# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=2.1.2.1
pkgrel=1
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=(x86_64)
url="https://yoshimi.github.io/"
license=(GPL2)
groups=(pro-audio)
depends=(cairo gcc-libs glibc zlib)
makedepends=(alsa-lib cmake fftw fltk jack lv2 mxml ncurses readline)
checkdepends=(kxstudio-lv2-extensions lv2lint)
optdepends=('lv2-host: for LV2 plugin')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
)
sha512sums=('bb3840d25ac9012b253ee51fa003137cc5db92c5a31bbfb740c5a45d395b5728e0dda2c7ea4e4cc993633db87d6d30f54b48cb707749799c3b19e5f7f913010e')
b2sums=('c08d965b87f9a0b84cc5dd946da9d6af2705e420e82c18f5f090cbf66a7645b12b29c5060fe0adc53a71d1e0b3d021dc182a98ddc56c4e10d381d1eabe0aadda')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build\
        src
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}"
  cp -v "build/LV2_Plugin/${pkgname}_lv2.so" "src/LV2_Plugin/"
  lv2lint -Mpack -I src/LV2_Plugin/ "http://yoshimi.sourceforge.net/lv2_plugin"
  rm -v "src/LV2_Plugin/${pkgname}_lv2.so"
}

package() {
  depends+=(libasound.so libfftw3f.so libfltk.so libfltk_images.so libjack.so
  libmxml.so libncursesw.so libreadline.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" VERBOSE=1 -C build install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
