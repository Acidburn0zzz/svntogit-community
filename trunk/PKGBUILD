# Maintainer: David Runge <dave@sleepmap.de>

_name=iwlib
pkgname=python-iwlib
pkgver=1.6.2
pkgrel=3
pkgdesc='Python module to interface with iwlib'
arch=('x86_64')
url="https://github.com/nathan-hoad/python-iwlib"
license=('GPL2')
depends=('python-cffi' 'wireless_tools')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('bc7a1406586f7b6e87040cb9ab9653437f6bcd4a103b2490b9d993e0a7aa1f1a65713a9b9dbee54a2fa9a38296db3cddc7d634abb4f43c0873d6874cd5b0ab6e')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  local _py3_ver=$(python3 -c "import sys; print(str(sys.version_info.major)+'.'+str(sys.version_info.minor))")
  export PYTHONPATH="build/lib.linux-$CARCH-${_py3_ver}/:${PYTHONPATH}"
  py.test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
