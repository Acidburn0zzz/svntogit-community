# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Simon Hanna <simon dot hanna AT serve-me DOT info>

_name=nose2
pkgname=python-nose2
pkgver=0.8.0
pkgrel=1
pkgdesc="The next generation of nicer testing for python"
arch=('any')
url="https://github.com/nose-devs/nose2"
license=('BSD')
depends=('python-coverage' 'python-setuptools' 'python-six')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('db9b95a28f9b497bf14bbf1d3e5f26254f4c6b313690fed26cdacfeaeaed65ffc88df3368698cf05a5b13c7a574beb80381f7da971bf720388dd77742a1d309a')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 license.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
