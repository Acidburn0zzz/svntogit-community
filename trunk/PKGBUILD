# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.21.0
pkgrel=1
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca' 'qt5-script' 'qt5-webengine' 'libofx'
         'grantlee' 'kross' 'knewstuff' 'krunner' 'knotifyconfig'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools' 'patch' 'kdesignerplugin')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        skrooge-missing-header.patch::"https://cgit.kde.org/skrooge.git/patch?id=79aeb3f4")
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('221fec450af95c29db8314e3b7c277af12f6b12a08948cc479ad1e7ccc0016ab'
            'SKIP'
            '303e42c8de0db7a1ef7486acb68e04b317281119f84615169d34ec2f37b479e9')

prepare() {
  cd $pkgname-$pkgver
#   patch -p1 -i ../skrooge-missing-header.patch # Fix build with KF 5.61
}

build() {
  cd $pkgname-$pkgver

  cmake . -DSKG_WEBENGINE=ON \
          -DSKG_BUILD_TEST=OFF
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
