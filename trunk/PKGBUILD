# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=10.0.3
pkgrel=1
# epoch needed, because of fake semver:
# https://github.com/nextcloud/spreed/issues/1685
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
depends=('nextcloud')
source=("https://github.com/nextcloud/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('d7756c8cf9a96cf4061f836748d26f6cb2fad92c073f9ecd4cc512a6ad78303d49e6dcdbac9fa29caa9ae583de655b07be1ccfd8cfc617f93652ecfea93cd911')
b2sums=('827c7679a304632713d3434be53ae97a286417c57c633fdc352f980f610b9b26400410d5818782633bf355ff217b684a3022ceb2e13ad3feb62ff7ef2f88957b')

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
