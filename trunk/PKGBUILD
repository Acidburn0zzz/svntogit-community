# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-checkdocs
pkgver=2.3.0
pkgrel=1
pkgdesc='Check the README when running tests'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/pytest-checkdocs'
depends=('python-docutils' 'python-more-itertools')
makedepends=('python-setuptools-scm')
checkdepends=('python-jaraco.test' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8'
              'python-pytest-mypy')
source=("https://github.com/jaraco/pytest-checkdocs/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6e578b26fc5662814ac8329bf903dd223927a0dfe216a54df85e689443299d8260e22107d2e123791915fa4edfbb4fc9909bed9e7a687ba244f04e254a10ebce')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-checkdocs-$pkgver
  python setup.py build
}

check() {
  cd pytest-checkdocs-$pkgver
  python setup.py egg_info
  PYTHONPATH="$PWD" pytest
}

package() {
  cd pytest-checkdocs-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
