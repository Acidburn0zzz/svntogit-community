# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jose
pkgver=3.1.0
pkgrel=1
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
depends=('python-rsa' 'python-six' 'python-ecdsa' 'python-future')
optdepends=('python-cryptography: one option for better performance'
            'python-pycryptodome: one option for better performance')
makedepends=('python-setuptools' 'python-rsa' 'python-six' 'python-ecdsa' 'python-future')
checkdepends=('python-pytest-runner' 'python-cryptography' 'python-pycryptodome' 'python-pytest-cov')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mpdavis/python-jose/archive/$pkgver.tar.gz")
sha512sums=('c4a04bed8fc17d65cb8fc9b2cf37996edb50fa0dcdc2dedd76a97c784f8af34a9c86748ea2f9ea348955ce780035fbe3a756abc8e1eb5e9b77ad57d411622c6b')

build() {
  cd python-jose-$pkgver
  python setup.py build
}

check() {
  cd python-jose-$pkgver
  python setup.py pytest
}

package() {
  cd python-jose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
