# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.13.1
pkgrel=6
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'libwebkit3' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2')
makedepends=('intltool' 'vala>=0.17.2' 'libunique3')
install=shotwell.install
source=("http://yorba.org/download/shotwell/0.13/shotwell-${pkgver}.tar.xz"
	"no-libexec.patch"
        "0001-Port-to-GStreamer-1.0.patch"
        "regenerate-video-thumbnails.patch")
md5sums=('71eb1346093705ca2b37c12a21994d14'
         'b4124e022c98b4382530e6fbcef965e2'
         '666d28c2a1b78632df8ab5f24c3aa3cd'
         '4b70442c6d9c04f3be23426ef92adb43')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 <$srcdir/no-libexec.patch

  # Port to GSreamer 1.0 (video thumbnailer)
  # http://redmine.yorba.org/issues/6055
  patch -p1 <$srcdir/0001-Port-to-GStreamer-1.0.patch

  # Regenerate video thumbnails on lost
  # http://redmine.yorba.org/issues/6152
  patch -p1 <$srcdir/regenerate-video-thumbnails.patch

  ./configure --prefix=/usr \
	--disable-schemas-compile \
	--disable-desktop-update \
	--disable-icon-update
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
