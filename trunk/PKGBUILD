# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Karol "Kenji Takahashi" Wo≈∫niak <kenji.sx>
# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Allen Li <darkfeline at abagofapples.com>

pkgbase=flake8
pkgname=(flake8 python2-flake8)
pkgver=2.6.2
pkgrel=1
epoch=1
pkgdesc="The modular source code checker: pep8, pyflakes and co"
arch=('any')
url="http://bitbucket.org/tarek/flake8"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pyflakes' 'python2-pyflakes'
             'python-pycodestyle' 'python2-pycodestyle' 'python-mccabe' 'python2-mccabe')
checkdepends=('python-nose' 'python2-nose' 'python2-mock')
source=("https://pypi.io/packages/source/f/flake8/flake8-$pkgver.tar.gz")
md5sums=('cf4f63730fd35e736a8a99583b9158fd')

prepare() {
  sed -i -e 's/, *< *[0-9=.]*//' flake8-$pkgver/setup.py
  cp -a flake8-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py build

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/flake8-$pkgver
  python setup.py test

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py test
}

package_flake8() {
  depends=('python-pyflakes' 'python-mccabe' 'python-pycodestyle')
  provides=('flake8-python3')
  replaces=('flake8-python3')
  conflicts=('flake8-python3')

  cd "$srcdir"/flake8-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-flake8() {
  depends=('python2-pyflakes' 'python2-mccabe' 'python2-pycodestyle')
  provides=('flake8-python2')
  replaces=('flake8-python2')
  conflicts=('flake8-python2')

  cd "$srcdir"/flake8-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  mv "$pkgdir"/usr/bin/flake8{,-python2}
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
