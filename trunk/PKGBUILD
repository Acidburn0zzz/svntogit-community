# Maintainer: Anatol Pomozov

pkgname=easyjson
pkgver=0.7.1
pkgrel=1
pkgdesc='Fast JSON serializer for golang'
arch=(x86_64)
url='https://github.com/mailru/easyjson'
license=(MIT)
depends=(glibc)
makedepends=(go git)
source=(easyjson-$pkgver.zip::https://github.com/mailru/easyjson/archive/v$pkgver.zip)
sha512sums=('2f0b9f95fc88028b8858348cfd9edba18997d4c6aa877e41f5bf63999e62f784bf57db73fae738639728fcf2e7195d6a798dbc2efcb5c1fc6c2edb0961eb517f')

prepare() {
  cd $srcdir/easyjson-$pkgver
  sed -i 's/golint /#golint /g' Makefile
}

build() {
  cd $srcdir/easyjson-$pkgver
  make build
}

check() {
  cd $srcdir/easyjson-$pkgver
  make test
}

package() {
  cd $srcdir/easyjson-$pkgver
  install -Dm755 bin/easyjson $pkgdir/usr/bin/easyjson
}
