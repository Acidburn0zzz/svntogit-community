# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.6.4
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python')
optdepends=('python-six: for logilab.common.testlib')
makedepends=('python-setuptools')
checkdepends=('python-six' 'python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha512sums=('30d65b6010cc42da719771feda7e4343c8f705e49ad744342241cb6ab0a31f5f89cdda1347fdc658ee939e3a64b2a51d3df03e2d06c0d9bf69c75259706a19be')

prepare() {
  cp -a logilab-common-$pkgver{,-py2}

  sed -i 's/from cgi import escape/from html import escape/' logilab-common-$pkgver/logilab/common/ureports/html_writer.py
}

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest || :
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
