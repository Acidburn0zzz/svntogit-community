# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-file-manager
pkgver=4.5.0
pkgrel=1
epoch=1
pkgdesc='Deepin File Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-file-manager"
license=('GPL3')
depends=('deepin-qt5integration' 'gtk2' 'gsettings-qt' 'libsecret' 'qt5-svg' 'file' 'avfs'
         'polkit-qt5' 'deepin-shortcut-viewer' 'poppler' 'ffmpegthumbnailer' 'deepin-manual'
         'file-roller' 'deepin-qt-dbus-factory' 'treefrog-framework' 'deepin-terminal'
         'gst-plugins-good' 'mpv' 'deepin-anything' 'deepin-movie')
makedepends=('qt5-tools' 'deepin-dock' 'deepin-gettext-tools')
groups=('deepin')
provides=('deepin-desktop')
conflicts=('deepin-desktop')
replaces=('deepin-desktop')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-file-manager/archive/$pkgver.tar.gz")
sha512sums=('123eee16a6397d432e67de974a7f820c07c27b23aeaf609d6472a027e13d95ea60af38cb43c6123a040bc794815ed8bea004778b698251b79fff42f2e0edab63')

build() {
  cd dde-file-manager-$pkgver
  qmake-qt5 PREFIX=/usr QMAKE_CFLAGS_ISYSTEM=
  make
}

package() {
  cd dde-file-manager-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
