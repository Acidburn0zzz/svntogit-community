# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.10
pkgrel=5
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('i686' 'x86_64')
url='http://www.rutschle.net/tech/sslh.shtml'
license=('GPL2')
backup=('etc/conf.d/sslh')
source=("http://www.rutschle.net/tech/$pkgname-$pkgver.tar.gz"
        'sslh.rc'
        'sslh.conf'
        'sslh.service')
md5sums=('bc34e9a4770d634633e70589c72708cc'
         '0680f09267c33205acf16ac6d4adf926'
         '808199410bd6ab67421bf3544f805d1a'
         '5af3d2abd03ccf4e46c74fe183ddb1e1')

build() {
  cd $pkgname-$pkgver
  # fix archlinux path in manpage
  sed -i -e "s/init.d/rc.d/" sslh.pod
  sed -i -e "s+/etc/default+/etc/conf.d+" sslh.pod
  make
}

package() {
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" install
  cd ..
  # install initscripts
  install -D -m 755 sslh.rc "$pkgdir/etc/rc.d/sslh"
  install -D -m 644 sslh.conf "$pkgdir/etc/conf.d/sslh"
  # install systemd service
  install -D -m 644 sslh.service "$pkgdir/usr/lib/systemd/system/sslh.service"
}

# vim:set ts=2 sw=2 ft=sh et:
