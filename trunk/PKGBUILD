# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.1.8
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL')
depends=('glib2' 'libstemmer')
makedepends=('meson' 'gobject-introspection' 'gtk-doc')
checkdepends=('shared-mime-info')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('14a728c375f6abfae70fadc7269ad6722ba960ed7c7616b54b5e0c99c804ec40' 'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

prepare() {
    cd ${pkgname}-${pkgver}
}

build() {
    arch-meson ${pkgname}-${pkgver} build -D stemmer=true
    ninja -v -C build
}

check() {
    meson test -C build --print-errorlogs
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
}
