# $Id: PKGBUILD 174910 2016-05-12 08:52:07Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wallpapers
pkgver=1.7.2
pkgrel=1
epoch=1
pkgdesc='Deepin Wallpapers'
arch=('any')
license=('GPL')
url="https://github.com/linuxdeepin/deepin-wallpapers"
makedepends=('deepin-api')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wallpapers/archive/$pkgver.tar.gz"
        fix-blur-filename.patch)
sha512sums=('1760d0b06f2b70635d2a5339d7724c0d74ef93d53321739e38b2bbeabbe461b58dd5efb788232a0b34ef44e27fd1c923c69ea554436304a28eea0c811822afa7'
            'b90f4c008969598b300f00c4f0518a579da98de1bc35ba16d8a744bd223759fd06d893c5a7f27769269336dbc4fd5e9e0224840be7ef23e70d742aface281af0')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-blur-filename.patch
}

build() {
  cd $pkgname-$pkgver
  make -j8
}

package() {
  cd $pkgname-$pkgver
  install -dm755 "$pkgdir"/usr/share/wallpapers
  cp -r deepin "$pkgdir"/usr/share/wallpapers/

  install -dm755 "$pkgdir"/var/cache
  cp -r image-blur "$pkgdir"/var/cache/

  # Suggested by upstream
  install -dm755 "$pkgdir"/usr/share/backgrounds/deepin
  ln -s ../../wallpapers/deepin/Hummingbird_by_Shu_Le.jpg "$pkgdir"/usr/share/backgrounds/deepin/desktop.jpg
  ln -s $(echo -n /usr/share/wallpapers/deepin/Hummingbird_by_Shu_Le.jpg | md5sum | cut -d " " -f 1).jpg \
        "$pkgdir"/var/cache/image-blur/$(echo -n /usr/share/backgrounds/deepin/desktop.jpg | md5sum | cut -d " " -f 1).jpg
}
