# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: farwayer <farwayer [at] gmail [dot] com>

_pkgbase=opus
pkgname=lib32-$_pkgbase
pkgver=1.1.3
pkgrel=2
pkgdesc="Codec designed for interactive speech and audio transmission over the Internet"
arch=('x86_64')
url="http://www.opus-codec.org/"
license=('BSD')
depends=('lib32-glibc' 'opus')
makedepends=('gcc-multilib')
source=("http://downloads.xiph.org/releases/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('58b6fe802e7e30182e95d0cde890c0ace40b6f125cffc50635f0ad2eef69b633')

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  
  cd ${_pkgbase}-${pkgver}
  ./configure --prefix=/usr --enable-custom-modes --disable-static \
    --enable-intrinsics --enable-rtcd --enable-ambisonics --libdir=/usr/lib32
  make
}

check() {
  cd ${_pkgbase}-${pkgver}
  make check
}

package() {
  cd ${_pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
  install -d "${pkgdir}"/usr/share/licenses
  ln -s ${_pkgbase} "${pkgdir}"/usr/share/licenses/$pkgname
}
