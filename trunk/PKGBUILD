# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Gerhard Brauer <gerbra@archlinux.de>

pkgname=nbd
pkgver=3.1.1
pkgrel=1
pkgdesc="tools for network block devices, allowing you to use remote block devices over TCP/IP"
arch=('i686' 'x86_64')
url="http://nbd.sourceforge.net"
license=('GPL')
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
	config nbd)
backup=('etc/nbd-server/config')
depends=('glib2')
install=nbd.install
md5sums=('e0e9d96abe068ee4f6fecd58635450ac'
         '2d05d426b8c2708d5f8a0d028fcbae05'
         'e7b13a8b3369e11927d9917664557efa')

build(){
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -D -m644 "${srcdir}"/config "${pkgdir}"/etc/nbd-server/config
  install -D -m755 "${srcdir}"/nbd "${pkgdir}"/etc/rc.d/nbd

  install -D -m644 README "${pkgdir}"/usr/share/doc/nbd/README
}
