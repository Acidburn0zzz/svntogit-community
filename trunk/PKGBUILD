# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Maintainer: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=tellico
pkgver=2.3.6
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('i686' 'x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kdebase-workspace' 'yaz' 'exempi' 'libksane' 'taglib' 'kdemultimedia-kioslave' 'poppler-qt' 'qjson')
makedepends=('automoc4' 'cmake')
install=$pkgname.install
source=(http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('62bafaea733ce3b108322623e866b8659ab2644cc80ee06f0fc7fe974ec5d1c3')

build() {
  cd ${srcdir}

  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build

  make DESTDIR=${pkgdir} install

# fix python 2.7 path
  find ${pkgdir} -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
