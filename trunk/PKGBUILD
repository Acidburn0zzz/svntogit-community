# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.9.0
pkgrel=1
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('spatialindex' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Toblerity/rtree/archive/$pkgver.tar.gz")
sha512sums=('aa9049ed217302d1c3a55fb5ccc505ae0636a83391f58e4de611b3abf7810f6f241f35623578359d885e556e791daeadb55adb393653c2500ba8663b048e0740')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
