# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=megaglest-data
_pkgbasename=megaglest
pkgver=3.6.0
pkgrel=1
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world (data files)"
arch=('any')
url="http://sourceforge.net/projects/megaglest/"
license=('custom:CCBYSA3.0')
makedepends=('cmake' 'xz')
source=("http://downloads.sourceforge.net/project/${_pkgbasename}/current_release/${_pkgbasename}-data-${pkgver}.tar.xz")
md5sums=('434b4c27e98aa3ee2e61982f30799034')

build() {
  cd "${srcdir}"/"${_pkgbasename}"-"${pkgver}"

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr ..
}

package() {
  cd "${srcdir}"/"${_pkgbasename}"-"${pkgver}"

  cd build
  make DESTDIR="${pkgdir}" install
}

# vim: sw=2:ts=2 et:
