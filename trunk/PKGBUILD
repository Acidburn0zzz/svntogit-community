# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=vmaf
pkgver=1.5.2
pkgrel=1
pkgdesc='Perceptual video quality assessment algorithm based on multi-method fusion'
arch=('x86_64')
url='https://github.com/Netflix/vmaf/'
license=('BSD')
depends=('gcc-libs')
makedepends=('meson')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/Netflix/${pkgname}/archive/v${pkgver}.tar.gz")
sha256sums=('5f7785da0b0d66b2513ce11d3f81d0a9b3f79d49483c4652fde3a31c13f644da')

build() {
    arch-meson "${pkgname}-${pkgver}/libvmaf/build" "${pkgname}-${pkgver}/libvmaf"
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build"
}

check() {
    ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" test
}

package() {
    DESTDIR="$pkgdir" ninja -v -C "${pkgname}-${pkgver}/libvmaf/build" install
    install -D -m755 "${pkgname}-${pkgver}/libvmaf/build/tools"/vmaf_{feature,rc} -t "${pkgdir}/usr/bin"
    install -D -m644 "${pkgname}-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
