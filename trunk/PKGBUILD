# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2.1
pkgrel=1
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL')
depends=('python' 'gammu')
makedepends=('python-setuptools')
checkdepends=('libdbi-drivers')
source=($pkgname-$pkgver.tar.gz::https://github.com/gammu/$pkgname/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('83af2e1e6b2da3d86d16fbfc1a97ac67e474417dc0fc0074465a2e10b40ef05bddd6ce9e8abf956d1ee2db3ae7012dba45779cc1cb3ad63a2be4b3f883c17aa1')

build() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py build
}

check() {
  # tests can be flaky on slower hardware due to timing
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py test 
}

package() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
