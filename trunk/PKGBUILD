# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contribuitor: Alfredo Palhares <masterkorp@masterkorp.net>
# Contribuitor: Christian Babeux <christian.babeux@0x80.ca>

pkgname=sysdig
pkgver=0.13.0
pkgrel=2
pkgdesc="Open source system-level exploration and troubleshooting tool"
arch=('i686' 'x86_64')
url="http://www.sysdig.org/"
license=('GPL2')
depends=('dkms' 'jsoncpp' 'luajit' 'curl' 'jq')
makedepends=('cmake' 'pandoc')
source=("https://github.com/draios/sysdig/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "linux-4.9.1.patch::https://github.com/draios/sysdig/commit/ded1f7b24b1cbc1425257b1dda22c4a1d04f4971.patch")
sha256sums=('2a5e744cef11348aa36d88bff9974557727e3b632a41e4b3b5e1903d0e911d3e'
            'cb73fbe316d41a52696bdf575c5f58079dc06cf4b2dc4b6d8f4d98d177d9ceca')
install=$pkgname.install

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 -i "$srcdir"/linux-4.9.1.patch
  sed 's|${DIR_ETC}/bash_completion.d|share/bash-completion/completions/|' \
      -i scripts/CMakeLists.txt
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  rm -rf build
  mkdir build
  cd build
  cmake .. \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DSYSDIG_VERSION=$pkgver \
        -DUSE_BUNDLED_DEPS=OFF \
        -DUSE_BUNDLED_B64=ON \
        -DBUILD_DRIVER=OFF
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  cd build
  make install DESTDIR="$pkgdir"
}
