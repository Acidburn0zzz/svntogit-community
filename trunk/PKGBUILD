# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Maato <maato softwarebakery com>

pkgname=volumeicon
pkgver=0.4.6
pkgrel=4
pkgdesc='Volume control for your system tray'
arch=('x86_64' 'i686')
url="http://softwarebakery.com/maato/volumeicon.html"
license=('GPL3')
depends=('gtk2>=2.16.0' 'alsa-lib' 'libnotify')
source=("http://softwarebakery.com/maato/files/volumeicon/$pkgname-$pkgver.tar.gz"
        "glib.patch")
sha256sums=('32f94adc5f346404ae564405dd483ae4bd04212df08e46f18d369b4ba2cb6590'
            'f2ab8b01cd9f554b170a773fa2a88c2874d97eb3fe106d4692595728201abeb0')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../glib.patch
  ./configure \
    --prefix=/usr \
    --enable-notify
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
