# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Robert Emil Berge

pkgname=swh-plugins
pkgver=0.4.17
pkgrel=1
pkgdesc="Steve Harris' LADSPA plug-ins suite"
arch=('x86_64')
url="http://plugin.org.uk/"
license=('GPL2')
depends=('fftw' 'ladspa')
makedepends=('libxml2' 'perl-xml-parser')
groups=('ladspa-plugins')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/swh/ladspa/archive/v${pkgver}.tar.gz")
sha512sums=('6b566030b43c3929ce8938648abd458efa0fea0b4916410a34b4f4fa788c45e4a6ec3f46ca435f8546179ff0c4674303db22314e1c1f1d09e5f2b71e6c5aecc7')

prepare() {
  mv -v ladspa-${pkgver} "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
  export CFLAGS="$CFLAGS -fPIC"
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,NEWS,README,TODO}
}

# vim:set ts=2 sw=2 et:
