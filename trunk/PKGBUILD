# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-proxy.py
pkgver=2.3.1
pkgrel=4
pkgdesc='Lightweight HTTP, HTTPS, HTTP2 and WebSockets proxy server'
arch=('any')
url='https://github.com/abhinavsingh/proxy.py'
license=('BSD')
depends=('python' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-paramiko')
optdepends=('python-paramiko: proxy over SSH tunnel support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/refs/tags/v$pkgver.tar.gz")
sha512sums=('3b92271f3253b5c3eae9a01b864ea0ca4b4a40d131d73252aaa03b1b3e1887621085a4cd60420fcae2bae79a1b04edb5c143e4a0f19ff8a8de50acff772e04dc')

prepare() {
	cd "${pkgname#python-}-$pkgver"
	# Remove strict version dependency for typing_extensions
	sed -i 's/==.*//' requirements.txt

	# Fix building with python-setuptools >= 59
	sed -zi 's/server\s*focused/server focused/' setup.py
}

build() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py build
}

check() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py test
}

package() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"

	# Remove entry point usr/bin/proxy to avoid conflict with libproxy (FS#73325)
	rm -r "$pkgdir/usr/bin"
}
