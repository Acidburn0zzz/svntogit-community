# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-hangul
pkgver=0.0.0.20200506
_commit=497f491599c7e61f10017c6d5f5b0dcac3f7ab0a
pkgrel=1
pkgdesc="Hangul Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-hangul"
license=('GPL')
depends=('fcitx5' 'libhangul')
makedepends=('extra-cmake-modules')
source=("https://github.com/fcitx/fcitx5-hangul/archive/$_commit/fcitx5-hangul-$_commit.tar.gz")
sha512sums=('1ec6f089421ceeb08b5b648271b65a41045af8d56d06de8cc322e7695d6ef0bbbac404da3b05bf7e266bcd2c0612be7ab4b76de0a6a6dc9ff083430b7dba6e00')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
