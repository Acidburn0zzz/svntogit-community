# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: xyzzy <628208@gmail.com>

pkgname=flameshot
pkgver=0.6.0
pkgrel=4
pkgdesc="Powerful yet simple to use screenshot software"
arch=('x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-svg' 'hicolor-icon-theme')
makedepends=('make' 'qt5-tools')
source=("https://github.com/lupoDharkael/flameshot/archive/v${pkgver}.tar.gz"
	"fix311.patch::https://github.com/lupoDharkael/flameshot/commit/415b059fb248eec4b4ee3be85a1594ce99bfce67.patch"
	"pr470.patch::https://patch-diff.githubusercontent.com/raw/lupoDharkael/flameshot/pull/470.patch"
	"pr678.patch::https://patch-diff.githubusercontent.com/raw/lupoDharkael/flameshot/pull/678.patch"
	)
sha256sums=('61b3a1969d6e17d80d5d90a3fce53ca5ae78fa21f9a45e5a19b0b32ea815a589'
            'ab73202901f9f531ce7b3cc7d59c026a00ea347f9aeef1bc427b97154bb741d5'
            'cffd645935583d0b5b77b163e38063d457500f706003b4014ef5ed4620f14b49'
            '4d373238da105734ea25097c4f088bd204285b9df9e2b1a7a2f71142c49dbf00')
install="${pkgname}.install"

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ## fix https://github.com/lupoDharkael/flameshot/issues/311
  patch -Np1 -i ../fix311.patch
  ## fix https://github.com/lupoDharkael/flameshot/issues/459
  ## see https://github.com/lupoDharkael/flameshot/pull/470
  patch -Np1 -i ../pr470.patch

  ## fix issues #564 #265 #650
  ## see https://github.com/lupoDharkael/flameshot/pull/678
  patch -Np1 -i ../pr678.patch

  ## add a version number from pkgver
  sed -i "s|TAG_VERSION = .*|TAG_VERSION = v${pkgver}|" flameshot.pro 
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  qmake CONFIG+=packaging BASEDIR="${pkgdir}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make INSTALL_ROOT="${pkgdir}" install
}
