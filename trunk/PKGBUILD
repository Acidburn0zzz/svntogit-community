# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Lex Black <autumn-wind@web.de>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Corrado 'bardo' Primier <corrado.primier@mail.polimi.it>
# Contributor: Jochem Kossen <j.kossen@home.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=scite
pkgver=4.4.3
pkgrel=3
pkgdesc='Editor with facilities for building and running programs'
arch=(x86_64)
url='https://www.scintilla.org/SciTE.html'
license=('custom:HPND')
depends=(gtk3)
backup=(usr/share/scite/SciTEGlobal.properties)
source=(https://www.scintilla.org/${pkgname}${pkgver//./}.tgz
        load_so_from_library_path.patch
        hardening_flags.patch
        SciTE.appdata.xml)
sha256sums=('05e53ccb1219753b9c73b0c1b83b6e2868a8c0c91d8785db2a5f8f9c41ed7d3f'
            'f2cbc239a1fb166d411a817ad7964d06b72111fc51b5ef56795275a62ead3681'
            '8d18bef06a73d0a081659119094c6f1b2582bfc3e3ae0c55210b083820d7a514'
            '04a33885833024a450c295d384f833bbc60894d57615de2a6f89db32d3ba942d')

prepare() {
  patch -Np1 -i load_so_from_library_path.patch
  patch -Np1 -i hardening_flags.patch
  sed -i 's|pixmapdir=$(datadir)/pixmaps|pixmapdir=$(datadir)/icons/hicolor/48x48/apps|
          s|pixmapdir)/Sci48M.png|pixmapdir)/SciTE.png|' scite/gtk/makefile
  sed -i 's/Sci48M/SciTE/' scite/gtk/{SciTEGTK.cxx,SciTE.desktop}
}

build() {
  GTK3=1 make -C scintilla/gtk
  GTK3=1 make -C scite/gtk
}

package() {
  GTK3=1 make -C scite/gtk DESTDIR="$pkgdir" install
  install -Dm644 scite/License.txt "$pkgdir/usr/share/licenses/$pkgname/License.txt"
  install -Dm644 SciTE.appdata.xml "$pkgdir/usr/share/metainfo/SciTE.appdata.xml"
  ln -sf /usr/bin/SciTE "$pkgdir/usr/bin/scite"
}
