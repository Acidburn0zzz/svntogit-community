# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Jérémie Detrey <Jeremie.Detrey@loria.fr>

_name=treestyletab
pkgname=firefox-tree-style-tab
pkgver=3.6.2
pkgrel=2
pkgdesc='Firefox extension to show tabs like a tree'
arch=('any')
url=https://piro.sakura.ne.jp/xul/_treestyletab.html.en
license=('MPL' 'MPL2')
groups=('firefox-addons')
makedepends=('git' 'zip')
source=("git+https://github.com/piroor/treestyletab.git#tag=$pkgver")
b2sums=('SKIP')

prepare() {
  cd $_name/webextensions
  sed -i 's/^xpi:.*/xpi:/' Makefile
  git submodule update --init
}

build() {
  cd $_name/webextensions
  make install_extlib
  make xpi
}

package() {
  cd $_name/webextensions
  install -Dm644 $_name-we.xpi "$pkgdir"/usr/lib/firefox/browser/extensions/$_name@piro.sakura.ne.jp.xpi
}

# vim:set ts=2 sw=2 et:
