# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=0.95
pkgrel=2
pkgdesc='an application that provides lots of terminals in a single window'
arch=('i686' 'x86_64')
url='http://www.tenshu.net/terminator/'
license=('GPL2')
depends=('vte' 'pygtk' 'xdg-utils')
makedepends=('desktop-file-utils' 'gettext' 'intltool')
optdepends=('gnome-python: gnome-terminal profile support')
install=terminator.install
source=("https://launchpad.net/terminator/trunk/${pkgver}/+download/${pkgname}_${pkgver}.tar.gz")
md5sums=('ceae5c899c71b9c80e48083c17a631ad')

build() {
    cd ${pkgname}-${pkgver}

    # python2 fix
    for file in terminatorlib/{,plugins/}*.py; do
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

    python2 setup.py build
}

package() {
    cd ${pkgname}-${pkgver}

    python2 setup.py install --skip-build --root=${pkgdir}

    rm -f ${pkgdir}/usr/share/applications/${pkgname}.desktop
    rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/data/${pkgname}.desktop.in ${pkgdir}/usr/share/applications/${pkgname}.desktop
    sed -i 's|_||' ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
