# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.3.2
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=('any')
url="https://github.com/aio-libs/aiosmtpd"
license=('Apache')
depends=('python-atpublic' 'python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b5d183671b8a985dcf774fb1b35ffbdf5c058e24e7cb81d77b2b0ce00b2b4dff27ff8bb167e985d5bf84cb6816c014d7cd601aab59243136ae409975ef3da30d')
b2sums=('985684d8183c89670c362e39b81cbff187fa820bc3197a0c0c18cd61eb4c00912bf489d935303be0fedfee3dabfafaf81c7d4be3e428374624ed9a1fab6ce2c8')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v --ignore=aiosmtpd/qa/
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
