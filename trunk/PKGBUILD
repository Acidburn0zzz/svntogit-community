# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kquickimageeditor
pkgver=0.1.2
pkgrel=1
pkgdesc='QML image editing components'
license=(LGPL)
arch=(x86_64)
url='https://invent.kde.org/libraries/kquickimageeditor'
depends=(qt5-declarative)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz"{,.sig})
sha256sums=('37d54981a1c7dbb48ca45a7df37b8871bf1751e4c0484eeaddc548804ee18a04'
            'SKIP')
validpgpkeys=(B3ED0BAE371BEDA00717921D18F4795C1CE7042B) # Carl Schwan <carl@carlschwan.eu>

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
