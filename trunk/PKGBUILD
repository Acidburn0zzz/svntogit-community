# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Timofey Titovets <Nefelim4ag@gmail.com>

pkgname=systemd-swap
pkgver=4.3.0
pkgrel=1
pkgdesc="Script for creating hybrid swap space from zram swaps, swap files and swap partitions."
arch=('any')
url="https://github.com/Nefelim4ag/systemd-swap"
license=('GPL3')
depends=('systemd' 'bash')
backup=('etc/systemd/swap.conf')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/Nefelim4ag/systemd-swap/archive/$pkgver.tar.gz")
sha512sums=('88dc56d90953a154815235d11463b4ff8c155a207d7bbb2983c4df34893984af4a6fdb49d37827227282cb85ddb455edc6e748ba2f14789646bb83ad3b657981')

build() {
  cd systemd-swap-$pkgver
  make
}

package() {
  cd systemd-swap-$pkgver
  make DESTDIR="$pkgdir" install
}
