# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=deepin-metacity
pkgver=3.22.9
pkgrel=1
pkgdesc="2D window manager for Deepin"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'libcanberra' 'libgtop' 'libsm' 'deepin-desktop-schemas' 'gtk3' 'startup-notification' 'zenity' 'bamf')
makedepends=('intltool' 'itstool' 'autoconf-archive' 'yelp-tools' 'python')
url="https://github.com/linuxdeepin/deepin-metacity"
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-metacity/archive/$pkgver.tar.gz")
sha512sums=('22a46cafbfe20e955e566c63cb032415227a963f231731d40d2f634a2980e0cb19f5261bc53aaf7ba7d2d349488bbcd2a68d7e5ef8cf632bedfdd17312a925cd')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-static --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
