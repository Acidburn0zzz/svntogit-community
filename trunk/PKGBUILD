# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pjr=prjtrellis
_targets=ECP5
_commit=717478b757a702bbc7e3e11a5fbecee2a64f7922
pkgname=$_pjr-db
pkgver=r115.717478b
pkgrel=1
pkgdesc='Project Trellis (Lattice ECP5 bit-stream format) database'
arch=('x86_64')
url="https://github.com/SymbiFlow/$pkgname"
license=('custom:CC0')
makedepends=('git')
source=("git+$url.git?commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd $pkgname

  printf 'r%s.%s' "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd $pkgname

  install -dm 755 "$pkgdir"/usr/share/$_pjr/database
  cp -r --no-preserve=ownership devices.json $_targets "$pkgdir"/usr/share/$_pjr/database/

  install -Dm 644 COPYING "$pkgdir"/usr/share/$pkgname/LICENSE
}

