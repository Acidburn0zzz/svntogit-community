# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=equivalence
pkgname=haskell-equivalence
pkgver=0.3.4
pkgrel=14
pkgdesc="Maintaining an equivalence relation implemented as union-find using STT."
url="https://bitbucket.org/paba/equivalence/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' "haskell-stmonadtrans" "haskell-transformers-compat")
makedepends=('ghc' 'haskell-quickcheck' 'haskell-test-framework'
             'haskell-test-framework-quickcheck2')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('1333b0d75be8d2211bbc406b9768df3e8e94596b44dc18d8155a1a34615bce21de15176fbb76a2da2fee7d41c85caa63e03593aa3eda5d6d0fd2c865a23027ce')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/, fail//' -e '/fail/d' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
