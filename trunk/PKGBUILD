# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
pkgver=2.1.1
pkgrel=1
pkgdesc="An open-source time-series database optimized for fast ingest and complex queries"
arch=('x86_64')
url="https://www.timescale.com/"
license=('Apache')
depends=('postgresql' 'postgresql-libs')
makedepends=('gcc' 'cmake')
install=timescaledb.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/timescale/timescaledb/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('53bd8f9006741af834cc2598efe95b94e6ebf00df770ac3a4e0a087af83c8ba5ac2b1f421fd108e6609fde6bea725f05e2d590c3f341030a0f31edb6a3cf6cd5')

build() {
    cd "${pkgname}-${pkgver}"
    ./bootstrap -DWARNINGS_AS_ERRORS=OFF -DREGRESS_CHECKS=OFF
    cd build && make
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir/" install
}
