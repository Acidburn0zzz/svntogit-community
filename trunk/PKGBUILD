# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=deja-dup
pkgver=38.1
pkgrel=1
pkgdesc="Simple backup tool, that hides the complexity of backing up the Right Way and uses duplicity as the backend"
arch=('x86_64')
url="https://launchpad.net/deja-dup"
license=('GPL')
depends=('duplicity' 'gnome-online-accounts' 'gvfs' 'libpeas' 'python2-gobject')
makedepends=('appstream-glib' 'gobject-introspection' 'intltool' 'itstool' 'meson' 'nautilus' 'vala')
optdepends=('gnome-keyring: save passwords'
            'nautilus: backup extension'
            'python2-boto: Amazon S3 and Google Cloud Storage backend')
source=(https://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('A3A5C2FC56AE7341D308D8571B50ECA373F3F233') # Michael Terry
sha256sums=('6a65b3dfd84d046b7d1f1bdd9aac7dd19477972e10bdaebdfe92ea85c5f9a772'
            'SKIP')

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
