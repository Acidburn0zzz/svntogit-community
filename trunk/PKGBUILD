# Maintainer: David Runge <dave@sleepmap.de>

pkgname=python-imdbpy
pkgver=6.4
pkgrel=1
pkgdesc="Python bindings for the Internet Movie Database (IMDb)"
url="https://imdbpy.sourceforge.io/"
arch=('any')
license=('GPL2')
depends=('python-lxml' 'python-sqlalchemy')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-pytest' 'python-pytest-cov')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alberanid/${pkgname/python-/}/archive/${pkgver}.tar.gz")
sha512sums=('dc1852abffaafe495b6c22bcbd6afb999ca03efcb3a5d2e5aea56221b249169b5b0bd0b31858e22dc40ef3c9b4e43c51c43720a1aa3e35fe149a5a6f734af1d2')

prepare() {
  mv -v "${pkgname/python-/}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix install folder for docs and config
  sed -e "s/'doc'/'share\/doc'/" \
      -e "s/'etc'/'\/etc'/" -i setup.py
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH=${srcdir}/${pkgname}-${pkgver}/build:${PYTHONPATH}
  py.test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

# vim:set ts=2 sw=2 et:
