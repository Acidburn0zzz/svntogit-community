# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-settings-daemon
pkgver=1.1.7
pkgrel=1
pkgdesc="Daemon handling the UKUI session settings"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-settings-daemon"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libmatekbd' 'libmatemixer' 'libnotify' 'libpulse' 'libxklavier' 'mate-desktop' 'nss')
makedepends=('mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-settings-daemon/archive/v$pkgver.tar.gz")
sha512sums=('e707ab6b1729c2e633d0fe0a20565d947e2c649939f575412a04136f0e540d49c30a0050a8848df22bf0fd54954ec7055f115f86d02d0b66ba7b2c6d49e16c4e')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
