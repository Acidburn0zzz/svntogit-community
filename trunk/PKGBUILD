# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Dmitry Chusovitin <dchusovitin@gmail.com>

pkgname=docker-machine
pkgver=0.6.0
pkgrel=1
pkgdesc='Machine management for a container-centric world'
arch=('i686' 'x86_64')
url='https://github.com/docker/machine'
license=('Apache')
depends=('glibc')
optdepends=('net-tools: for VirtualBox support')
makedepends=('go' 'git')
optdepends=('net-tools: required for VirtualBox driver')
source=("git+https://github.com/docker/machine.git#tag=v$pkgver")
sha256sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p build/src/github.com/docker/machine
  cp -a machine/* build/src/github.com/docker/machine
}

build() {
  cd build/src/github.com/docker/machine
  USE_CONTAINER=false make build
}

package() {
  cd build/src/github.com/docker/machine
  install -Dm755 bin/docker-machine "$pkgdir"/usr/bin/docker-machine
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
