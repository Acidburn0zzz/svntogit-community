# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-inline-c
pkgver=0.80
pkgrel=2
pkgdesc="C Language Support for Inline"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Inline-C"
depends=('perl-inline' 'perl-parse-recdescent' 'perl-pegex')
makedepends=('perl-file-copy-recursive' 'perl-test-warn')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TI/TINITA/Inline-C-$pkgver.tar.gz")
sha512sums=('f68eb9478374ef13a6eb0be6c5adc12ba9c052834ef45db6acc0c794ac295d5b47ffda57dd4d0b15345be96aa21ccaab8bf7e2be0ac0bc4cef73be782a346cf0')

build() {
  cd Inline-C-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Inline-C-$pkgver
  make test
}

package() {
  cd Inline-C-$pkgver
  make DESTDIR="$pkgdir" install
}
