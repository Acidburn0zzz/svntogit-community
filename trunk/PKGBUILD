# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=sshfs
pkgver=2.9
_tag=sshfs_${pkgver/./_}
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=('i686' 'x86_64')
url="http://github.com/libfuse/sshfs"
license=('GPL')
depends=('fuse2' 'glib2' 'openssh')
source=(https://github.com/libfuse/sshfs/releases/download/sshfs-$pkgver/sshfs-$pkgver.tar.gz{,.asc})
sha256sums=('46d1e1287ce97255fcb50010355184d8c5585329f73ab1e755217419a8e6e5de'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
