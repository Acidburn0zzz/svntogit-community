# Maintainer: David Runge <dvzrv@archlinux.org>

_name=spreed
pkgname=nextcloud-app-spreed
pkgver=12.1.0
pkgrel=1
epoch=1
pkgdesc="Video & audio-conferencing using WebRTC"
arch=('any')
url="https://apps.nextcloud.com/apps/spreed"
license=('AGPL3')
groups=('nextcloud-apps')
makedepends=('nextcloud')
checkdepends=('ripgrep' 'yq')
source=("https://github.com/nextcloud-releases/${_name}/releases/download/v${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('e8107700a1dc49d72826191eb8d3dca9170264733d4263dca66e94ee52b5e15d5f20275f50f4d8acb751a1ba0ffdfede61577e8293a8344c0b1ae622bc149ce2')
b2sums=('049deda0d8dcaa228acb6d0b6b5115f352fefebc488d3b918a0dfac61899115e2e53d798dfc3db251c90dc63c1aad6616e192c4a716e167da0c9fa2df89e5bf8')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

prepare() {
  find ${_name} -type f -exec chmod 644 {} +
  find ${_name} -type d -exec chmod 755 {} +
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"

  if (("$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")"<0)) || (("$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")">0)) ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions
  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps/"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
