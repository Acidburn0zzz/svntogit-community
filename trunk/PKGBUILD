# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# AUR Maintainer: Vlad M. <vlad@archlinux.net>
# Contributor: HÃ¥vard Pettersson <mail@haavard.me>
# Contributor: Kevin MacMartin <prurigro at gmail dot com>

pkgname=qtox
_pkgname=qTox
pkgver=1.6.0
pkgrel=1
_gitver='5648106b34fc7dc3ad4e169651a0cbbd0139170a'
pkgdesc='Powerful Tox client written in C++/Qt that follows the Tox design guidelines'
arch=('i686' 'x86_64')
url='https://github.com/tux3/qTox'
license=('GPL3')
depends=('libxss'
         'openal'
         'gtk2'
         'ffmpeg'
         'qrencode'
         'qt5-svg'
         'sqlcipher'
         'toxcore')
makedepends=('qt5-tools')

source=("https://github.com/tux3/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('6ccf2c17c25ea30899356518b8f8849ed53404a8ac81180e2719c979dab1f411a93aa08ad759b3e8defea994d3baaf538ed354b733a321de3615521dcf6c8192')

build() {
  cd $_pkgname-$pkgver
  install -d build
  cd build
  qmake-qt5 ENABLE_SYSTRAY_UNITY_BACKEND=NO GIT_VERSION=$_gitver GIT_DESCRIBE=v$pkgver ..
  make
}

package() {
  # executable
  cd $_pkgname-$pkgver/build
  make INSTALL_ROOT="$pkgdir" install
}
