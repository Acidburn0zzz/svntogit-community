# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=3.53.3
pkgrel=1
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=('any')
depends=('python-six' 'python-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest' 'python-freezegun')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('6399d8fa8270fc0241969d94ce284161516b42c0fdc3fbf2dfe8a0dfcca72933d23ec9b99c4118cebbca376f5821337aacd11fcee6eee5988d6caf73d1f3112d'
            'SKIP')
b2sums=('01f0d0cf6c546855d2cac10cc1164fb68308b60e1095499f8838d7c82127860235f65d742a137db5acdce6ac50e65aff7580a6f6a344942d4c78375cc23085b4'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # don't care about custom pytest config
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  # Don't write pyc files of tests so the package is reproducible.
  PYTHONDONTWRITEBYTECODE=1 pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
