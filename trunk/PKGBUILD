# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.18.0
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('x86_64')
url="https://lib.filezilla-project.org/"
license=('GPL')
depends=('gcc-libs' 'nettle')
makedepends=(clang)
checkdepends=('cppunit')
source=("https://download.filezilla-project.org/libfilezilla/libfilezilla-$pkgver.tar.bz2")

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  export CXX=clang++
  export CC=clang
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
sha512sums=('745039ca0123516bc58a7057117386fdaed8956b7612b331ffd10f3c7b6c739c47c9c90261ccaf5f7862662775f6b0b467891731ae30c5976d36a0cfb0102455')
