# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mephisto.lv2
pkgver=0.8.8
pkgrel=2
pkgdesc="A Just-in-Time FAUST compiler embedded in an LV2 plugin"
arch=('x86_64')
url="https://open-music-kontrollers.ch/lv2/mephisto/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('fontconfig' 'glew' 'glibc' 'libevdev' 'libglvnd' 'libvterm' 'libx11'
'ttf-fira-code')
makedepends=('faust' 'glu' 'lv2' 'meson')
checkdepends=('lv2lint')
source=("https://git.open-music-kontrollers.ch/lv2/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('7c07ed64cb665da1f753a67177e0d789ea1ba229b0ea674fad6844bfd03a09c8551e568386ae5ec4192d2beb21dc8472d9cacf851831c149cd43041129d22738')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix=/usr build
  ninja -C build
}

check() {
  cd "$pkgname-$pkgver"
  _plugin_names=('audio_1x1' 'audio_2x2' 'audio_4x4' 'audio_8x8' 'cv_1x1'
  'cv_2x2' 'cv_4x4' 'cv_8x8')
  for _plugin in ${_plugin_names[@]}; do
    lv2lint -Mpack -I "build/" "http://open-music-kontrollers.ch/lv2/mephisto#${_plugin}"
  done
}

package() {
  depends+=('libfaust.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" ninja -C build install
  # devendoring ttf-fira-code
  rm -vf "${pkgdir}/usr/lib/lv2/${pkgname}/"*.ttf
  for font_type in {Bold,Light,Medium,Regular}; do
    ln -svf "/usr/share/fonts/TTF/FiraCode-${font_type}.ttf" \
      "${pkgdir}/usr/lib/lv2/${pkgname}/"
  done
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
