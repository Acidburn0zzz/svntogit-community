# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-mimeparse
pkgname=(python-mimeparse python2-mimeparse)
pkgver=1.5.1
pkgrel=1
pkgdesc='Module of basic functions for parsing mime-type names and matching them against a list of media-ranges'
arch=('any')
url="https://github.com/dbtsai/python-mimeparse"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
source=("http://pypi.python.org/packages/source/p/python-mimeparse/python-mimeparse-$pkgver.tar.gz")
md5sums=('b90629fe43e3f4ada76b938f66022721')

prepare() {
  cp -a python-mimeparse-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-mimeparse-$pkgver
  python setup.py build

  cd "$srcdir"/python-mimeparse-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-mimeparse-$pkgver
  python mimeparse_test.py

  cd "$srcdir"/python-mimeparse-$pkgver-py2
  python2 mimeparse_test.py
}

package_python-mimeparse() {
  depends=('python')

  cd python-mimeparse-$pkgver
  python setup.py install -O1 --root "$pkgdir"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-mimeparse() {
  depends=('python2')

  cd python-mimeparse-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
