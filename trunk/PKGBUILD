# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fs
pkgname=('python-fs' 'python2-fs')
pkgver=2.0.10
pkgrel=1
pkgdesc="Filesystem abstraction layer"
arch=('any')
license=('custom:BSD')
url="http://pypi.python.org/pypi/fs/"
makedepends=('python-appdirs' 'python2-appdirs' 'python2-enum34' 'python-pytz' 'python2-pytz'
             'python2-scandir' 'python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-nose' 'python2-nose' 'fuse2' 'python-mock' 'python2-mock' 'python-pyftpdlib'
              'python2-pyftpdlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyFilesystem/pyfilesystem2/archive/v$pkgver.tar.gz")
sha512sums=('ca2303ec99169d2859575762ab23543452282f9bca9d5c95e9a4867852afad0494b3e94b8228222ab49f3aabd6dd22889ff585940a48f6ae6e026cddba3d118b')

prepare() {
  cp -a pyfilesystem2-$pkgver{,-py2}

  sed -e '/enum34/d' -e '/scandir/d' -i pyfilesystem2-$pkgver/setup.py
}

build() {
  cd "$srcdir"/pyfilesystem2-$pkgver
  python setup.py build

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/pyfilesystem2-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" nosetests3 tests

  cd "$srcdir"/pyfilesystem2-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages:$PYTHONPATH" nosetests2 tests
}

package_python-fs() {
  depends=('python-appdirs' 'python-pytz' 'python-setuptools' 'python-six')

  cd pyfilesystem2-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-fs() {
  depends=('python2-appdirs' 'python2-enum34' 'python2-pytz' 'python2-scandir' 'python2-setuptools'
           'python2-six')

  cd pyfilesystem2-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
