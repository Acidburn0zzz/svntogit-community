# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-inline-c
pkgver=0.78
pkgrel=1
pkgdesc="C Language Support for Inline"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Inline-C"
depends=('perl-inline' 'perl-parse-recdescent' 'perl-pegex')
makedepends=('perl-file-copy-recursive' 'perl-test-warn')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/T/TI/TINITA/Inline-C-$pkgver.tar.gz")
sha512sums=('368e68dfb9f1700534d9b391090e340172e3adde5fe15576f883e9341047fec35cea8660751883b27e128ad91dda363d513547368097a989457d669bbbdc8ce4')

build() {
  cd Inline-C-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Inline-C-$pkgver
  make test
}

package() {
  cd Inline-C-$pkgver
  make DESTDIR="$pkgdir" install
}
