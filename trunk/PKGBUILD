# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Maintainer:Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.1.6
pkgrel=2
pkgdesc="Parallel implementation of the gzip file compressor"
arch=('i686' 'x86_64')
url="http://www.zlib.net/pigz/"
license=('custom')
depends=('zlib')
source=(http://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('cbe9030c4be3d0ef2438ee5f8b169ca4')

build() {
  cd ${srcdir}/$pkgname-$pkgver
  sed -i s/"CFLAGS=".*/"CFLAGS=${CFLAGS}"/g Makefile 
  make
}
package() {
  cd ${srcdir}/$pkgname-$pkgver
  install -Dm755 ${srcdir}/${pkgname}-${pkgver}/${pkgname} \
    ${pkgdir}/usr/bin/${pkgname} 
  pushd ${pkgdir}/usr/bin
  ln -s pigz unpigz 
  popd
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/${pkgname}.1 \
    ${pkgdir}/usr/share/man/man1/${pkgname}.1
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/${pkgname}.pdf \
    ${pkgdir}/usr/share/doc/${pkgname}/${pkgname}.pdf
  install -Dm644 ${srcdir}/${pkgname}-${pkgver}/README \
    ${pkgdir}/usr/share/licenses/${pkgname}/README
}
