# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=1.2.6.2
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'syntax-highlighting' 'kcodecs' 'polkit-qt5')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-editor/archive/$pkgver.tar.gz")
sha512sums=('ccbb423902d0ac16afdb5ce8aa58539e0b4e7b6983015080f0eb6ddde8430191547071f8d545ab1e6fa63ddd4f7c106ea94db1c4262c845b127e532cdd60cafa')

build(){
  cd deepin-editor-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd deepin-editor-$pkgver
  make DESTDIR="$pkgdir" install
}
