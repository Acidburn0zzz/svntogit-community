# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Michele Mocciola <mickele>
# Contributor: Simon Zilliken <simon____AT____zilliken____DOT____name>

pkgname=paraview
pkgver=3.14.1
pkgrel=1
pkgdesc='Parallel Visualization Application using VTK'
arch=('i686' 'x86_64')
url='http://www.paraview.org'
license=('custom')
depends=('qt' 'python2' 'libgl' 'unixodbc' 'postgresql-libs' 'libxt' 'qtwebkit'
         'libmysqlclient' 'mesa' 'openmpi' 'boost-libs' 'libcgns2'
         # export as avi support
         'ffmpeg'
         # The following is a list of libraries we explicitly tell cmake to use the system library of
         'hdf5' 'freetype2' 'libjpeg' 'libpng' 'libtiff' 'zlib' 'expat' 'libxml2' 'libtheora'
        )
makedepends=('cmake' 'desktop-file-utils' 'boost')
source=("http://paraview.org/files/v${pkgver:0:4}/ParaView-${pkgver}-Source.tar.gz"
        'paraview.png'
        'paraview.desktop'
        'fix_FTBFS_gcc-4.7.patch'
        'ffmpeg-0.8.diff')
sha1sums=('1326c6e941fb9d12c548b14ca10932b49b5e3ffd'
          'a2dff014e1235dfaa93cd523286f9c97601d3bbc'
          '9cd5b19b4072d8d5512bb97a141eb878adf73ab5'
          '9ea9290ee1fb91b2dafbf2d87629cf1437029af9'
          'ef710cfb99d77c909eb42ca17f04751191e5d536')

build() {
  cd "${srcdir}"/ParaView-${pkgver}-Source

  # fix http://www.vtk.org/Bug/view.php?id=12691
  cd VTK
  patch -Np1 -i "${srcdir}"/ffmpeg-0.8.diff

  patch -Np2 -i "${srcdir}"/fix_FTBFS_gcc-4.7.patch

  mkdir "${srcdir}"/build
  cd "${srcdir}"/build

  # flags to enable using system libs
  local cmake_system_flags=""
  for lib in HDF5 FREETYPE JPEG PNG TIFF ZLIB EXPAT LIBXML2 OGGTHEORA; do
    cmake_system_flags+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
  done

  # flags to use python2 instead of python which is 3.x.x on archlinux
  local cmake_system_python_flags="-DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 \
     -DPYTHON_INCLUDE_DIR:PATH=/usr/include/python2.7 -DPYTHON_LIBRARY:PATH=/usr/lib/libpython2.7.so"

  # enable when http://paraview.org/Bug/view.php?id=12852 gets fixed
  #-DCMAKE_SKIP_RPATH:BOOL=YES \
  # the following flags enable the feature request at https://bugs.archlinux.org/task/27525
  # -DPARAVIEW_USE_VISITBRIDGE:BOOL=ON -DVISIT_BUILD_READER_CGNS:BOOL=ON
  cmake \
    -DCMAKE_SKIP_RPATH:BOOL=YES \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX:PATH=/usr \
    -DVTK_USE_BOOST:BOOL=ON \
    -DPARAVIEW_USE_MPI:BOOL=ON \
    -DPARAVIEW_ENABLE_PYTHON:BOOL=ON \
    -DPARAVIEW_BUILD_QT_GUI:BOOL=ON \
    -DPARAVIEW_USE_VISITBRIDGE:BOOL=ON \
    -DVISIT_BUILD_READER_CGNS:BOOL=ON \
    -DVTK_USE_FFMPEG_ENCODER:BOOL=ON \
    ${cmake_system_flags} \
    ${cmake_system_python_flags} \
    ../ParaView-${pkgver}-Source

  make
}

package() {
  cd "${srcdir}"/build

  make DESTDIR="${pkgdir}" install

  # Install license
  install -Dm644 "${srcdir}"/ParaView-${pkgver}-Source/License_v1.2.txt "${pkgdir}"/usr/share/licenses/paraview/LICENSE

  # Install desktop shortcuts
  install -Dm644 "${srcdir}"/paraview.png ${pkgdir}/usr/share/pixmaps/paraview.png
  desktop-file-install --dir="${pkgdir}"/usr/share/applications "${srcdir}"/paraview.desktop
}
