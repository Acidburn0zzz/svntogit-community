# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=laz-perf
pkgver=2.0.5
epoch=1
pkgrel=1
pkgdesc="Alternative LAZ implementation for C++ and JavaScript"
arch=(any)
url="https://github.com/hobu/laz-perf"
license=('LGPL')
makedepends=('cmake' 'ninja')
source=("$pkgname-$pkgver.tar.gz::https://github.com/hobu/laz-perf/archive/${pkgver}.tar.gz")
sha512sums=('fd4f53399c01c168b977b1da101ba93ade4a2020aa76cf58f5af64a47a315bc443a71985404d62541670fef24a47357d40d21a08b4e7bbd70b562182f4bb92a1')

build() {
  cd laz-perf-$pkgver

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr
  ninja -C build
}

check() {
  cd laz-perf-$pkgver

  ninja -C build test
}

package() {
  cd laz-perf-$pkgver

  DESTDIR="$pkgdir/" ninja -C build install
}

# vim:set ts=2 sw=2 et:
