# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack_mixer
pkgver=12
pkgrel=1
pkgdesc="Jack desktop mixer application"
arch=('x86_64')
url="https://github.com/jack-mixer/jack_mixer"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'python-cairo'
'python-gobject')
makedepends=('glib2' 'jack')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jack-mixer/${pkgname}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-12-remove_gconf.patch")
sha512sums=('e49a5fa901de38230911c73e2ea60ea8e9e400c5ae1ca5220181e25d5eda5506f61f8dfdaf99839e73b4c9af62d035acf2ef471c304dc1743e1f61f0444bf1f8'
            '9a7d1ff5255110dfc07f9e9fdf06a155f305e1b9b917242b56daa46ea369cb19355e2c77a5c2fd4f839de847857169afdab3d332f75413bbdfab60e15a889727')
b2sums=('1d83452ba02286b069441ebb14008950bbbe1668582d6372a9ff5de4be333f0f63b4e3fea5cb5509a41f05df13a6f03e5ee5ca41688ba82b18970b569362a619'
        '70d0bd15d9d08593b16247844082febb802d01e2dbcd392adf6f1483ef2d823dc07e441108fa416c3b7f21fe8ab7baf6f83f4bdc25718ceeacd19be465ca14de')

prepare() {
  mv -v "$pkgname-release-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # remove legacy gconf integration
  # https://github.com/jack-mixer/jack_mixer/issues/2
  patch -Np1 -i "../${pkgname}-12-remove_gconf.patch"
  ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  depends+=('libglib-2.0.so' 'libjack.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
