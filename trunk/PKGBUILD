# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-pkcs11
pkgver=1.2.0
pkgrel=3
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('libyaml' 'openssl' 'python' 'python-cryptography' 'python-pyasn1-modules' 'python-yaml' 'sqlite'
         'tpm2-tools' 'tpm2-tss' 'libtss2-esys.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
makedepends=('cmocka')
checkdepends=('ibm-sw-tpm2' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc' 'tpm2-abrmd' 'wget' 'xxd')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        "tpm2-pkcs11-1.2.0_gcc-10-fix.patch::$url/commit/13691c677e5f3af4c1ee4e26e04dbe4bcb1e5c28.patch"
        "tpm2-pkcs11-1.2.0_ibm-sw-tpm2-1628.patch::$url/commit/7db0e907303731425b80271264cd67e8ed9c0eb4.patch")
sha512sums=('e7de454b38388c4562fc92282855c1ed9a04c7968c358d6918137257fb5a1ca785c2297e8a34464ea04eed86acd9fc5b59c837027da9d3f195ec86ff0563feeb'
            'SKIP'
            '8a52ebf13e715be87f2a103765eb45051ad9c6124cd5e6826fdf20f50c69bdd5c103d1a4168653d4b6eb4cc68974d0b5adcb8b42211181d066a772c3a416cd61'
            '4607014cee52cdedce57c5c1d53fb4c274d144c3684964673c19c12c3e4e1f7372122dacb6d4cd0db77e1287cf28a684f3e0ca9a30518c96ef13e6da630c0039')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix test suite for GCC 10 (https://github.com/tpm2-software/tpm2-pkcs11/pull/514)
	patch --strip=1 --input="$srcdir/tpm2-pkcs11-1.2.0_gcc-10-fix.patch"
	# Fix test suite for ibm-sw-tpm2 1628 (https://github.com/tpm2-software/tpm2-pkcs11/pull/519)
	patch --strip=1 --input="$srcdir/tpm2-pkcs11-1.2.0_ibm-sw-tpm2-1628.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr --enable-unit $( ((CHECKFUNC)) && echo --enable-integration)
	make
	cd tools
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
