# Maintainer: Maxime Gauduin <alucryd@archlonux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=libva1
pkgver=1.8.3
pkgrel=1
pkgdesc='Video Acceleration (VA) API for Linux'
arch=('x86_64')
url='https://01.org/linuxmedia/vaapi'
license=('MIT')
depends=('libdrm' 'libgl' 'libx11' 'libxext' 'libxfixes' 'wayland')
makedepends=('git' 'libglvnd' 'mesa')
provides=(
  'libva-drm.so' 'libva-glx.so' 'libva-wayland.so' 'libva-x11.so' 'libva.so'
)
source=("git+https://github.com/intel/libva.git#tag=${pkgver}")
sha256sums=('SKIP')

prepare() {
  cd libva

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libva

  ./configure \
    --prefix='/usr' \
    --disable-va-messaging
  make
}

package() {
  make DESTDIR="${pkgdir}" -C libva install
  rm -rf "${pkgdir}"/usr/{include,lib/{pkgconfig,*.so}}
  install -Dm 644 libva/COPYING -t "${pkgdir}"/usr/share/licenses/libva1
}

# vim: ts=2 sw=2 et:
