# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maui-clip
_pkgname=clip
pkgver=2.1.0
pkgrel=5
pkgdesc='Video player and video collection manager'
url='https://mauikit.org/'
arch=(x86_64)
license=(GPL3)
depends=(mauikit-filebrowsing taglib ffmpeg)
makedepends=(extra-cmake-modules)
groups=(maui)
source=(https://download.kde.org/stable/maui/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.xz
        ffmpeg5.patch)
sha256sums=('c09f9668c25710e4018ca1ac00fa56d7d00a5c64ffc3e967aa3abf3a46c97bef'
            '54d5c9d7f077edcfb94a6681d590d40ac9c2b83f3da2c340d477c6b2e1b2576f')

prepare() {
  patch -d $_pkgname-$pkgver -p1 < ffmpeg5.patch # Fix build with FFmpeg 5
}

build() {
  cmake -B build -S $_pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
