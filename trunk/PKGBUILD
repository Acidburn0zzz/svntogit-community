# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.6.1
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "python2" "git")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha512sums=('6effe92874c9bd2e3e6e0c7503ca778ad9f1dfe64e414ab60fb6f6a579a35dce3911677572739360e0930d1077a64b153cc44b7756bc71d7e1fbddb49fb5513b')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    PYTHON=python2 yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
