# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=fastapi
pkgname=python-$_pkgname
pkgver=0.62.0
pkgrel=1
pkgdesc='FastAPI framework, high performance, easy to learn, fast to code, ready for production'
arch=('any')
url='https://github.com/tiangolo/fastapi'
license=('MIT')
depends=('python-starlette' 'python-pydantic')
optdepends=('python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
            'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
            'python-email-validator' 'python-python-multipart')
makedepends=('python-setuptools' 'python-dephell')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'mypy'
              'python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
              'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
              'python-email-validator'  'python-pyjwt' 'python-sqlalchemy'
              'python-peewee' 'python-flask' 'python-passlib'
              'python-bcrypt' 'python-httpx' 'python-python-multipart'
              'python-databases' 'python-aiosqlite' 'python-jose')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('b20a1353128b967c5b8fb657190069d30510010472152d6d233f08c9f8550faa554c41db7584395df3ac656f0304bad80a90c1d012ddc0bd5423e1a926490445')

prepare() {
  cd $_pkgname-$pkgver

  dephell deps convert --from pyproject.toml --to setup.py

  # don't pin starlette dependency
  sed -i 's|starlette.*"|starlette"|' pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest

  mypy -p fastapi
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
