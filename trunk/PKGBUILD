# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=python-python-socks
pkgver=1.2.4
pkgrel=1
pkgdesc='Core proxy client (SOCKS4, SOCKS5, HTTP) functionality for Python'
arch=('any')
url='https://github.com/romis2012/python-socks'
license=('APACHE')
depends=('python') 
makedepends=('python-setuptools')
checkdepends=('python-async-timeout' 'python-curio' 'python-flask' 'python-pytest'
              'python-pytest-asyncio' 'python-pytest-trio' 'python-trio' 'python-yarl')
optdepends=('python-async-timeout: asyncio support'
            'python-curio: curio support'
            'python-trio: trio support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "python-python-socks-1.2.4_fix_test_suite.patch::$url/commit/9ea957a0e367563961bf6619ac64e4127be1e01b.patch")
sha512sums=('6e70d8d98b2901e97250d30b350002944d979356bb6cd7dd4022af420e166076259a7b1aa4845d771243b18ab3f06ee955f07482c527835bb0135f0622d804be'
            '6e48bc1f2b5acfa5ee5d2e17b6593e3159225bfa8ad805229daebe4deca3b71e5bef424f222d359b6a0d28bd42bc27d45496df81888dc9d053e89c593e80970e')

prepare() {
	cd "${pkgname#python-}-$pkgver"
	# Fix test suite failures due to expired certificates
	patch --forward --strip=1 --input="$srcdir/python-python-socks-1.2.4_fix_test_suite.patch"
}

build() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py build
}

check() {
	cd "${pkgname#python-}-$pkgver"
	pytest
}

package() {
	cd "${pkgname#python-}-$pkgver"
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
