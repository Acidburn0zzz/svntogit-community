# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=autorandr
pkgver=1.9
pkgrel=1
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python-setuptools' 'xorg-xrandr')
makedepends=('bash-completion')
optdepends=('bash-completion: auto-completion for autorandr'
            'xorg-xdpyinfo: For detecting the primary XRandR output')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('ca1727a1ac2a55b0ebce3051e7e976fc675633269972785b72ec0001e598cc028f7fff95bcfafa38f8dca900795818d8aaef26c3081528cf26b85f5290318c70')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
