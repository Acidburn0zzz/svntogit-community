# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.36.0
_image_pkgver=5.11.0
_podman_pkgver=3.1.0
_skopeo_pkgver=1.2.3
_storage_pkgver=1.29.0
pkgrel=1
pkgdesc="Configuration files and manpages for containers"
arch=('any')
url="https://github.com/containers"
license=('Apache')
makedepends=('go-md2man')
backup=(
  'etc/containers/containers.conf'
  'etc/containers/mounts.conf'
  'etc/containers/policy.json'
  'etc/containers/registries.conf'
  'etc/containers/seccomp.json'
  'etc/containers/storage.conf'
)
# configuration override directories need to exist
options=('emptydirs')
source=("common-${pkgver}.tar.gz::https://github.com/containers/common/archive/v${pkgver}.tar.gz"
        "image-${_image_pkgver}.tar.gz::https://github.com/containers/image/archive/v${_image_pkgver}.tar.gz"
        "podman-${_podman_pkgver}.tar.gz::https://github.com/containers/podman/archive/v${_podman_pkgver}.tar.gz"
        "skopeo-${_skopeo_pkgver}.tar.gz::https://github.com/containers/skopeo/archive/v${_skopeo_pkgver}.tar.gz"
        "storage-${_storage_pkgver}.tar.gz::https://github.com/containers/storage/archive/v${_storage_pkgver}.tar.gz"
        'mounts.conf'
)
sha512sums=('bf9929cc22138733fa6227de840e0528d56b8809cff18a9a6c106e621e63bf89eeb7506572c49f29503ab86ba899f2bfe8f157ddf38705a9887166efd3a56b3b'
            'ec91d3483f6c41e310fb4ea51593850da19e09a08b784784bfe4875ced4ea1088bab28418187fe57ac316a0d8d6293a438cd398d75320ad5c64caa5744f9dda8'
            '534a16acbfc75c515c1abaf603ecb6af636039cd8208ce415979f577895174404bcf5e75c8fe6646f0f9f3032e4a3da9b0927d36f0e7fb525e19800cf8bc610e'
            '06e3ddc17f45a7b145c44c58d67894d3fff809cd1521753027c703c44425241d4580096fd44d9156eae70336544110e2af95ab6f9d22deff88903c6e77bbe4d8'
            'dfb22b8d14244f04fce453664efc146a6f957ac0223888dc74ed0e40e15089615bd44c30577c1ed7b9984e3778af53184f477c9df4a5d21d811c1b4557338589'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('5783057b2cd3fdb9f6ce098013cf6f7d2e5fbdaa32e7c04ae4540ca8327b7ba784126236f25b097921f5cef0781ce1f9b112ffecd43873d63a5cf8376be87954'
        '95d9224e80dc505e926e2152ede7fc20ba6d230e10ff40900c45d104a2f8642d68b6b02482519a1b2161564f7c103a53dcbb2ab65c328e5beded0267ade5e7fe'
        'ead46deef10acb064ef97d1bd9148c6ed75e3132b0bd20a9b2fbfaeaf11566ccc1de51ad352773a37bf9f45457f2ba982d57fa6c3ca73cf0eb0bdf5ecdcb75e5'
        '3db447726d6a545f5225aa37a215da8a7893ea8beae08547bf93148d632a80863173b014949cc69cb033a2e68653738fe86d1c8758b12fc3c2c16ebb5582b203'
        '4ffbbb666134276cf7ef7c34d56b465a2c2f899142b203abe08e484a1f604cefa9a220d8b9827c3f7cb5c7e80f25839cc8b0abbfdd7af060ce6bc929fafe86d6'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  (
    cd "storage-${_storage_pkgver}"
    sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i docs/Makefile
  )
}

build() {
  (
    cd "common-${pkgver}/docs"
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "image-${_image_pkgver}/docs"
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}.1"
    done
    for _man_page in man5/*.md; do
      go-md2man -in "$_man_page" -out "${_man_page//.md}"
    done
  )
  (
    cd "podman-${_podman_pkgver}"
    go-md2man -in docs/source/markdown/containers-mounts.conf.5.md \
      -out containers-mounts.conf.5
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md \
      -out oci-hooks.5
  )
  (
    cd "storage-${_storage_pkgver}"
    make -C docs
  )
}

package() {
  # directories
  install -vdm 755 "${pkgdir}/etc/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/etc/containers/registries.conf.d/"
  install -vdm 755 "${pkgdir}/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "${pkgdir}/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "${pkgdir}/etc/containers/"

  (
    cd "common-${pkgver}"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "${pkgdir}/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "image-${_image_pkgver}"
    # configs
    install -vDm 644 registries.conf -t "${pkgdir}/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "podman-${_podman_pkgver}"
    install -vDm 644 *.5 -t "${pkgdir}/usr/share/man/man5/"
  )
  (
    cd "skopeo-${_skopeo_pkgver}"
    # configs
    install -vDm 644 default-policy.json "${pkgdir}/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "${pkgdir}/etc/containers/registries.d/"
  )
  (
    cd "storage-${_storage_pkgver}"
    # configs
    install -vDm 644 storage.conf -t "${pkgdir}/etc/containers/"
    install -vDm 644 storage.conf -t "${pkgdir}/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "${pkgdir}/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "${pkgdir}/usr/share/man/man5/"
  )
}
