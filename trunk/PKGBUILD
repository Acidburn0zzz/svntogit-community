# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-zconfig
pkgver=3.5.0
pkgrel=2
pkgdesc="Structured Configuration Library"
url="https://github.com/zopefoundation/ZConfig"
license=('ZPL')
arch=('any')
makedepends=('python-setuptools')
checkdepends=('python-zope-testrunner' 'python-manuel' 'python-docutils' 'python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/ZConfig/archive/$pkgver.tar.gz")
sha512sums=('0aa5ad1344ac599a10513e1189d0c990717311b39cc6dd6a44315812dce66c09760979931839e3739089d30c69c626bf8259695e1afb4691d19662f41100ecee')

build() {
  cd "$srcdir"/ZConfig-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/ZConfig-$pkgver
  nosetests3
}

package() {
  cd ZConfig-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
}
