# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=5.8.0.17
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-picker' 'deepin-qt5integration' 'freeimage' 'libexif' 'qt5-imageformats')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-draw/archive/$pkgver.tar.gz")
sha512sums=('1bc642625eb96acf6d4a93efc6c5151a7e7b7ba7ed0f4dd2ca1652d7bc17036b9535f4d3a2080a2e119cd6404d476f585319089b3383d5d055eefcc90e836169')

prepare() {
  cd deepin-draw-$pkgver
  sed -i '/include <DGraphicsView>/i #include <QFileDevice>' frame/cgraphicsview.h
  sed -i '/include <QPainter>/a #include <QMouseEvent>' widgets/ciconbutton.cpp
}

build() {
  cd deepin-draw-$pkgver

  qmake-qt5
  make
}

package() {
  cd deepin-draw-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
