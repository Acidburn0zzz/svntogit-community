# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Contributor: Mihails Strasunse <public@dicebot.lv>

pkgname='dtools'
pkgver=2.081.2
pkgrel=1
pkgdesc="Ancilliary tools for the D programming language"
license=('Boost')
groups=('dlang')
arch=('x86_64')
url="http://www.dlang.org"
makedepends=('dmd' 'libphobos')
depends=('curl')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/dlang/tools/archive/v${pkgver}.tar.gz
		"http://www.boost.org/LICENSE_1_0.txt"
		"autodetect.diff")
sha512sums=('cd91fab26e503058aa172880d3b2db74879e49d80a89c4f43f5620f454fde52e48ad4dcd6433a70c6cf180d094d6819bc790d80b849f030e0d080b7a2015bf67'
            'd6078467835dba8932314c1c1e945569a64b065474d7aced27c9a7acc391d52e9f234138ed9f1aa9cd576f25f12f557e0b733c14891d42c16ecdc4a7bd4d60b8'
            '3bce3c66cf24837fcb9bf244f2b07ed6116d8d67a75b2319010cf93956e93b43262b7d9613359503e6b0352b82684be8baeeb7b1f50e77accda334ce470bf5e3')

build() {
    DMD=dmd

    # Add $LDFLAGS and $CXXFLAGS to the compilation
    _linkflags=$(echo -ne $LDFLAGS | cut -d\" -f2 | tail -c+4 | sed "s/,/ -L=/g")

    cd tools-${pkgver}

    # rdmd is used to build others
    $DMD -od. -ofdtools-rdmd $_linkflags rdmd.d

    for tool in DustMite/dustmite ddemangle catdoc detab tolf dget; do
        ./dtools-rdmd --compiler=$DMD --build-only -od. -ofdtools-`basename $tool` $_linkflags $tool.d
    done
}

package() {
    cd tools-${pkgver}

    mkdir -p $pkgdir/usr/bin
    for tool in rdmd ddemangle catdoc detab tolf dget dustmite; do
        install -m755 -t $pkgdir/usr/bin $pkgname-$tool
    done

    cd $pkgdir
    ln -s dtools-rdmd usr/bin/rdmd
    ln -s dtools-dustmite usr/bin/dustmite
    ln -s dtools-ddemangle usr/bin/ddemangle

    install -Dm644 $srcdir/LICENSE_1_0.txt usr/share/licenses/$pkgname/LICENSE
}
