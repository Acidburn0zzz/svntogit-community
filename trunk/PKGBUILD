# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=drumkv1
pkgver=0.9.10
pkgrel=1
pkgdesc="An old-school drum-kit sampler"
arch=('x86_64')
url="https://drumkv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('desktop-file-utils' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libsndfile' 'qt5-base')
makedepends=('alsa-lib' 'jack' 'liblo' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a55b8d06a373034c99873aedf5f11447921237948fd0fcc6ac2654677b0aafec5ac288aae183e8685c68a586f73525875b171c695bce87895342673f40199c45')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
