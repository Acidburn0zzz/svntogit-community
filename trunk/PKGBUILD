# Maintainer: David Runge <dvzrv@archlinux.org>

_name=authheaders
pkgname=python-authheaders
pkgver=0.12.2
pkgrel=1
pkgdesc="Library for the generation of email authentication headers"
arch=('any')
url="https://github.com/ValiMail/authentication-headers"
license=('BSD' 'MPL2' 'ZPL')
# python-publicsuffix should actually not be in use:
# https://github.com/ValiMail/authentication-headers/issues/2
depends=('python-authres' 'python-dkim' 'python-dnspython'
'python-publicsuffix')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('418c31f4b2d8f8893f986785814860cde7188e450aeb8f5b50f97fc32a27cdf6695942586c3fe1695b01f9468dd16a25afa67310e00b537422b42b9e7911fc68'
            'SKIP')
validpgpkeys=('E7729BFFBE85400FEEEE23B178D7DEFB9AD59AF1') # Donald Scott Kitterman <scott@kitterman.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGES,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
