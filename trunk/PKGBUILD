# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-spec-validator
pkgver=0.2.8
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
depends=('python-six' 'python-yaml' 'python-jsonschema')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-pytest-flake8' 'python-pytest-cov'
              'python-tox')
source=("$pkgname-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('38b6817d00e37cf3d1e23d9a4013a0ceab680b1c6e76f431ad4b3741c405789a03f04d6d5025b4fb8c4362ec8eec9a9d3d0c2e2f1bcfb4e6e2b0d581713025a7')

build() {
  cd openapi-spec-validator-$pkgver
  python setup.py build
}

check() {
  cd openapi-spec-validator-$pkgver
  python setup.py pytest
}

package() {
  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
