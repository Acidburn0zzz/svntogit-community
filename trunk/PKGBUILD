# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.45.1
pkgrel=1
pkgdesc="Sass makes CSS fun again"
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('dart')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sass/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('cccc8cc811f022afd994ea84f82ec06602c28fd8e00d8d53de3f1d1544ff1077d7b53b72c9163b2a44d3d73d90c19380ac2e85976bfa85018477650b60c39835')
b2sums=('ec4d78510828da73a5677b2eaaf853ec3aca9844ef77360e773ebd3185d50d7c0e5fd245a1bdf3f0a5902cb080048387e4f9562f91b74599166c2f48b90b7065')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  pub get
}

build() {
  cd "$pkgname-$pkgver"
  dart compile exe -Dversion=$pkgver -o sass bin/sass.dart
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
