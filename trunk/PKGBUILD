# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org> 
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=1.22
pkgrel=1
pkgdesc="Fast and lightweight IDE"
arch=('x86_64' 'i686')
url="http://www.geany.org/"
license=('GPL')
depends=('gtk2' 'hicolor-icon-theme' 'desktop-file-utils' 'python2')
makedepends=('perlxml' 'intltool')
optdepends=("vte: for terminal support")
install=geany.install
source=("http://download.geany.org/$pkgname-$pkgver.tar.bz2")
options=('!libtool')
sha256sums=('901a35a7395ef10a80fb10b3ab63bae3871693a4e82d56388e9521a27877577e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Configuring..."
  ./configure --prefix=/usr
  msg2 "Compiling..."
  make
  msg2 "Python2 fix..."
	sed -i '0,/on/s//on2/' data/templates/files/main.py
  msg2 "Fixing FS#10318..."
  sed -i 's|MimeType=text/plain;|MimeType=|' geany.desktop
  msg2 "Enabling colors for PKGBUILD files..."
  sed -i 's|Sh=|Sh=PKGBUILD;|' data/filetype_extensions.conf
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
