# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cysignals
pkgname=(python-cysignals python2-cysignals)
pkgver=1.10.1
pkgrel=1
pkgdesc="Interrupt and signal handling for Cython"
arch=(x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(cython2 cython pari)
source=("https://github.com/sagemath/cysignals/releases/download/$pkgver/$pkgbase-$pkgver.tar.gz")
sha256sums=('dfe63bbe295324bd88e39215db8745a9f81668229e3bd931da1b0757bb45f7b8')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cysignals() {
  depends=(python pari)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"

# Keep scripts in python2 package
  rm -r "$pkgdir"/usr/{bin,share}
}

package_python2-cysignals() {
  depends=(python2 pari)
  conflicts=(cysignals)
  provides=(cysignals)
  replaces=(cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"
}
