# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: James An <james@jamesan.ca>
# Contributor: Stefano Bergamini <bergs at live dot it>
# Contributor: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>
# Contributor: MetaNova

pkgname=epson-inkjet-printer-escpr
pkgver=1.6.34
pkgrel=1
pkgdesc='Epson Inkjet Printer Driver (ESC/P-R) for Linux'
arch=('x86_64')
url='http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX'
license=('GPL2')
depends=('cups' 'ghostscript')
source=('https://download3.ebz.epson.net/dsc/f/03/00/08/97/78/75e6a1d0cac1d02c40d8809780593fed539b54e9/epson-inkjet-printer-escpr-1.6.34-1lsb3.2.tar.gz'
        'bug_x86_64.patch')
sha512sums=('8caabf66f787abc09f59e0cbc41691cdbaf98a653ffecc5f66d12febce37e6733092afacaf43867c7e42c215cd1467a2c6bfc82f7fa18895d45ced54c187eb91'
            'ff4f736ec884c4d2354a83c89ee535732cb414ef0c6443bdc8ec869f889f486b242d76e143ad59d61b182231697dfe3bdf68122cb8aae873a9776ce109a07dab')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 < ../bug_x86_64.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--with-cupsfilterdir=/usr/lib/cups/filter \
  	--with-cupsppddir=/usr/share/ppd

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

