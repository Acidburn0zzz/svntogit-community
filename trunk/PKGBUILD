# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-toolkit-gtk3
pkgver=0.113
pkgrel=2
pkgdesc="Sugar GTK library"
arch=('x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
depends=('alsa-lib' 'gtk3' 'libsm' 'python2-cairo' 'python2-dateutil' 'python2-decorator'
         'python2-telepathy' 'sugar-artwork' 'sugar-datastore' 'telepathy-mission-control' 'unzip')
makedepends=('intltool' 'gobject-introspection')
optdepends=('webkit2gtk: run sugar-activity-web')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz
        six.patch)
sha256sums=('4062d981d551f1567d073389385ffc28793b75f0ed2485e8f9250a145239cba7'
            '3adf9d6ddc817c31887f1c22e9976d9865ca17c608a5142ef97ffbd8c0de3424')

prepare() {
  cd $pkgname-$pkgver
  # Revert 'Port from Python 2 to six', it's not complete yet
  patch -RNp1 -i ../six.patch
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  # Disable hardened build until fixed upstream
  # https://bugs.sugarlabs.org/ticket/4886
  export CFLAGS=${CFLAGS/ -fno-plt}
  export LDFLAGS=${LDFLAGS/,-z,now}

  # Don't use incomplete python3 port
  sed -i 's/PYTHON=python3/PYTHON=python2/' configure
  sed -i 's/exec sugar-activity3/exec sugar-activity/' bin/sugar-activity-web
  sed -i '/dist_bin_SCRIPTS/ s/ sugar-activity3//' bin/Makefile.in

  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
