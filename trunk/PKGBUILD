# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.2.1.2
pkgrel=2
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' 'alsa-lib')
provides=('libasound.so' 'libatopology.so')
source=("https://www.alsa-project.org/files/pub/lib/alsa-lib-$pkgver.tar.bz2")
sha512sums=('e8286fd55f63ee0d95513279d0885c287533de89b7af6c338413dec5d38ba4f5a15da1a4a4ce36e052614e4b730e3778782dab9979d82958283be17b48604913')

prepare() {
  cd alsa-lib-$pkgver
  autoreconf -vfi
}

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

check() {
  cd alsa-lib-$pkgver
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
