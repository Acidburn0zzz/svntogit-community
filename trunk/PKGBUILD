# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich <tobias@archlinux.org>

pkgname=qjackctl
pkgver=0.9.2
pkgrel=2
pkgdesc="A Qt front-end for the JACK low-latency audio server"
url="https://qjackctl.sourceforge.io"
arch=('x86_64')
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'jack2' 'jack2-dbus' 'qt6-tools' 'portaudio')
optdepends=(
  'jack2-dbus: for JACK dbus support'
  'qt6-wayland: for native wayland support'
)
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('3af3190c9808f873bbefab347e2a307a0176c1ce5c92c346510b159d43c6b83dfa3e4d1b4208968e4d871b12ed56afd79f14560185485dca314381f42a58ad53')
b2sums=('281ce20be6acb26a0efa10c9a11b44bd370225b1fcecf2acaf2e6cefb807b0ba8a30fa9deb98eeebc86dbea1301d5e87a2b5979cd971a33ce4a54b4757949d0b')


build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so' 'libjack.so' 'libportaudio.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO,TRANSLATORS}
}
