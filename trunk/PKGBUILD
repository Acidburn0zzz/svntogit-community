# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-session-manager
pkgver=2.0.1
pkgrel=1
pkgdesc="Session manager of the UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-session-manager"
groups=('ukui')
depends=('kidletime' 'peony' 'qt5-multimedia' 'qt5-ukui-platformtheme' 'ukui-biometric-auth'
         'ukui-panel' 'ukui-settings-daemon' 'ukui-screensaver' 'ukwm' 'xtrans')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-session-manager/archive/v$pkgver.tar.gz")
sha512sums=('24d49e118352dbbb3639563921d7e3aad2d96426b53a8384cbc1d0c1804527eb8eb06cb8f21506e441e63e63a1957ad26df0032996522d31343ba54e2ab443db')

prepare() {
  mkdir -p build
  sed -i '/#include <QString>/i #include <QFile>' $pkgname-$pkgver/tools/main.cpp
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
