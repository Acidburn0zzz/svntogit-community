# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=2.6
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://github.com/jrnl-org/jrnl"
license=('GPL3')
depends=('python-pyxdg' 'python-cryptography' 'python-parsedatetime'
         'python-keyring' 'python-pytz' 'python-tzlocal' 'python-asteval' 'python-colorama'
         'python-dateutil' 'python-yaml' 'python-ansiwrap' 'python-setuptools')
makedepends=('python-dephell')
checkdepends=('python-behave' 'python-pytest')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e37c610048523999d994409699f8fe55a97cb971a462dc22c4e40704a959ea6ce7e252443605159e8031be468894d70712cce9680841eb4fa877775aac61058e')

prepare() {
  cd jrnl-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd jrnl-$pkgver
  python setup.py build
}

check() {
  cd jrnl-$pkgver
  python -m pytest
  behave
}

package() {
  cd jrnl-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
