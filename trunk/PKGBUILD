# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy
pkgver=2.0beta1
pkgrel=1
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('libffi')
provides=('python2')
makedepends=('python2')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')
source=(https://bitbucket.org/pypy/pypy/get/release-2.0-beta1.tar.bz2)
md5sums=('a7e3e15c51e8f42af9f0c168b6c2750e')

build() {
  cd ${srcdir}/pypy-pypy-*/pypy/translator/goal

  python2 translate.py -Ojit
}

package() {
  cd ${srcdir}/pypy-pypy-*/pypy/translator/goal

  install -Dm755 pypy-c ${pkgdir}/opt/pypy/pypy-c
  mkdir -p ${pkgdir}/opt/pypy/{lib-python,pypy}

  cd ${srcdir}/pypy-pypy-* 
  cp -r lib-python/2.7 ${pkgdir}/opt/pypy/lib-python/
  cp -r lib_pypy ${pkgdir}/opt/pypy/
  cp -r include ${pkgdir}/opt/pypy/

  mkdir -p ${pkgdir}/usr/bin
  ln -s /opt/pypy/pypy-c ${pkgdir}/usr/bin/pypy

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/pypy/LICENSE
}
# vim: ts=2 sw=2 et:
