# $Id$
# Maintainer: Jonathan Conder <jonno.conder@gmail.com>

pkgname=gnome-packagekit
pkgver=3.0.2
pkgrel=1
pkgdesc='Collection of graphical tools for PackageKit to be used in the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
depends=('dconf' 'desktop-file-utils' 'gnome-menus' 'gtk3'
         'hicolor-icon-theme' 'libnotify' 'packagekit')
makedepends=('gconf' 'gtk-doc' 'gnome-common' 'intltool' 'libcanberra' 'upower')
optdepends=('gnome-settings-daemon-updates: update and message notifications')
options=('!emptydirs' '!libtool')
install="$pkgname.install"
source=("http://ftp.gnome.org/pub/gnome/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.bz2"
        'arch.patch')
sha256sums=('faaae52d839a66d0bcb7d7cf97ef62d11bccf558e6aa9fdb0dbc8d61dbaf714f'
            '9c809ac3c8bbf870442e7bc4123c70b144930a287b28626be1b8bae95edf71ac')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/arch.patch"
  sed -i 's@python@python2@' 'python/enum-convertor.py'

  export PYTHON=/usr/bin/python2
  # TODO: remove gnome-common dependency and switch back to configure
  gnome-autogen.sh --prefix=/usr \
	      --sysconfdir=/etc \
	      --disable-gtk-doc \
	      --disable-schemas-compile \
	      --disable-scrollkeeper
  make -s
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -s DESTDIR="$pkgdir" install

  rm -rf "$pkgdir/usr/lib/gnome-settings-daemon-3.0/gtk-modules"
}
