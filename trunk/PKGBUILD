# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.15.0
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/awslabs/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-pyaml python-six python-requests python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-mock)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/awslabs/cfn-python-lint/archive/v$pkgver.tar.gz"
        fix-tests.patch)
sha256sums=('53ad1177d3c85a7c20adb38cac9cd08751d7f3f1a0fd40cce790d96dfae1cb76'
            'c36881e4d33092bdd5173e42e5b2bf4b04d521c95e4365b7eaa94dd93dbaf0ea')

prepare() {
  cd cfn-python-lint-$pkgver
  patch -Np1 -i ../fix-tests.patch
}

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  PYTHONPATH=build/lib pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
