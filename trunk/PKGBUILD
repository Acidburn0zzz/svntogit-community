# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-outcome
pkgver=1.0.0
pkgrel=3
pkgdesc='Capture the outcome of Python function calls'
arch=(any)
url=https://github.com/python-trio/outcome
license=(MIT)
depends=(
  python
  python-attrs
)
makedepends=(
  git
  python-async_generator
  python-pytest
  python-pytest-cov
  python-setuptools
)
source=(git+https://github.com/python-trio/outcome.git#tag=v${pkgver})
sha256sums=(SKIP)

build() {
  cd outcome

  python setup.py build
}

check() {
  cd outcome

  python setup.py test
}

package() {
  cd outcome

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-outcome/
}

# vim: ts=2 sw=2 et:
