# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=primus_vk
pkgver=1.0
pkgrel=1
pkgdesc="Primus-Vk Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/$pkgname"
license=('BSD')
depends=('nvidia-utils' 'bumblebee' 'vulkan-icd-loader' 'primus')
makedepends=('vulkan-validation-layers')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/felixdoerre/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('5d4bb4be4e958f039144d90c33dc94dcdfcbaf0925f1f70676ae46ea3022d4a11ba07fa23974b434188fad0c54f4df1c590d20b76186634a852a99203e3a76c1')

build() {
  cd "${pkgname}-${pkgver}"

  export CXXFLAGS+=' -DNV_DRIVER_PATH=\"/usr/lib/libGLX_nvidia.so.0\"'
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  install -D libnv_vulkan_wrapper.so "$pkgdir"/usr/lib/libnv_vulkan_wrapper.so
  install -D libprimus_vk.so "$pkgdir"/usr/lib/libprimus_vk.so
  install -Dm644 primus_vk.json "$pkgdir"/usr/share/vulkan/implicit_layer.d/primus_vk.json
  install -Dm644 nv_vulkan_wrapper.json "$pkgdir"/usr/share/vulkan/icd.d/nv_vulkan_wrapper.json
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
  install -Dm755 pvkrun.in.sh "$pkgdir"/usr/bin/pvkrun
}
