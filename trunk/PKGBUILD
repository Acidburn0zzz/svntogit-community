# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: xduugu
# Contributor: Manolis Tzanidakis
# Contributor: Jonathan Schmidt <j.schmidt@archlinux.us

pkgname=acpid
pkgver=2.0.15
pkgrel=1
pkgdesc='A daemon for delivering ACPI power management events with netlink support'
arch=('i686' 'x86_64')
url='http://tedfelix.com/linux/acpid-netlink.html'
license=('GPL')
depends=('bash')
optdepends=('perl: use perl based examples')
replaces=('acpid2')
backup=('etc/acpi/handler.sh' 'etc/acpi/events/anything' 'etc/conf.d/acpid')
source=("http://www.tedfelix.com/linux/$pkgname-$pkgver.tar.xz"
        'acpid'
        'anything'
        'handler.sh'
        'acpid.conf.d')
md5sums=('1b1c8775adab6a994a386c45af6b86dc'
         'd69203a032c4583f9abaafcf21a7ed84'
         '2d37b98d6e74bab815604b8b48c6cfd4'
         '615439f4a89716c747907d4fc3c8df75'
         '929c6d2e91295c22ed9ec6212d7eabef')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm755 ../acpid "$pkgdir/etc/rc.d/acpid"
  install -Dm644 ../anything "$pkgdir/etc/acpi/events/anything"
  install -Dm755 ../handler.sh "$pkgdir/etc/acpi/handler.sh"
  install -Dm644 ../acpid.conf.d "$pkgdir/etc/conf.d/acpid"

  chmod 755 "$pkgdir"/usr/sbin/acpid
}

# vim:set ts=2 sw=2 ft=sh et:
