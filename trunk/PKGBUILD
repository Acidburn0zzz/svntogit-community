# Maintainer: David Runge <dvzrv@archlinux.org>

_name=json
pkgname=ruby-json
pkgver=2.3.0
pkgrel=2
pkgdesc="JSON implementation for Ruby"
arch=('x86_64')
url="https://github.com/flori/json"
license=('BSD')
depends=('ruby')
makedepends=('jruby' 'ragel' 'ruby-rake' 'ruby-rdoc')
checkdepends=('git' 'ruby-bundler' 'ruby-test-unit')
optdepends=('jruby: Java integration')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flori/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('0cda44a77212748d9513e61314dfbc7c0cf82beebcc56eb8b043e4ada698bc475502100389b80fa5c9090341fd1c6d2841fc5c9332e520f1c8dd1084ca505379')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${_name}.gemspec"
}

check(){
  cd "${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
