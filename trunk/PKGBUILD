# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.6.11
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz")
sha512sums=('5ee4c05e9275c6d920763ebaa22fe1cb15116945deea42bcd7de77a561030dc50e7d7672bae27186f3c4ae9a78043f7f1f14c5e0e699e8c26eaf30745f2cc176')

prepare() {
  cd deepin-compressor-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' deepin-compressor/source/src/openwithdialog/openwithdialog.cpp deepin-compressor/source/src/logviewheaderview.cpp
}

build() {
  cd deepin-compressor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-compressor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
