# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=4.8.7
pkgrel=2
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'xorg-server' 'deepin-control-center' 'startdde' 'xorg-xrandr' 'deepin-qt-dbus-factory'
         'qt5-tools')
makedepends=('deepin-gettext-tools')
provides=('lightdm-deepin-greeter' 'deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz"
        fix-crash.patch)
sha512sums=('850e436e9f5e3fdb33c97daa37d0bb724b343a87fa7536a498211298636dd5c11dccfa237bf371d7afd47bc866572554420a19dc7913ff68558da0188c40aa49'
            '31d57a89a9716de0d26e7eddb4eed3159d4a78cd24a9991e9b86426c18c74373d9f5854128d3c1031bf06516539b1e288b8c5b1cac773eed9368c1fef536a5af')

prepare() {
  cd dde-session-ui-$pkgver
  patch -p1 -i ../fix-crash.patch
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp boxframe/*.cpp
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
