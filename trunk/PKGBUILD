# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lfs
pkgver=1.2.1
pkgrel=1
pkgdesc="Get information on your mounted disks"
arch=('x86_64')
url="https://github.com/Canop/lfs"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('a46290f7d73af225d3675ad9b72fd2c63770460d524f209da414cda3a54b2c6bc4c3d1ba0013d94bc69af5cf7a00df3934159030d2cff708c55880561814be70')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
