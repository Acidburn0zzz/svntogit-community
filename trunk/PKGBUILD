# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jose
pkgname=('python-jose' 'python2-jose')
pkgver=3.0.1
pkgrel=2
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="https://github.com/mpdavis/python-jose"
makedepends=('python-setuptools' 'python2-setuptools' 'python-rsa' 'python2-rsa'
             'python-six' 'python2-six' 'python-ecdsa' 'python2-ecdsa' 'python-future'
             'python2-future')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-cryptography'
              'python2-cryptography' 'python-pycryptodome' 'python2-pycryptodome'
              'python-pytest-cov' 'python2-pytest-cov')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mpdavis/python-jose/archive/$pkgver.tar.gz"
        'pytest5.patch')
sha512sums=('a39c3aa9acc6bcc0030e4435e499581043a820c54aa1a4b67ae478cf95f96f28755b03fa4453e484932a64865a61613a1ff4afe30249216e8f392638485693d7'
            '0406340a45309e0b9411e451a68ca353454bb10bc34309264abc9b56fe4fed1c2eda705d5a7625a35333043030b0032ad70cf7da1feea673595c2fdf8423a234')

prepare() {
  patch -Np1 -d python-jose-$pkgver <pytest5.patch
  cp -a python-jose-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py build

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-jose-$pkgver
  python setup.py pytest

  cd "$srcdir"/python-jose-$pkgver-py2
  python2 setup.py pytest
}

package_python-jose() {
  depends=('python-rsa' 'python-six' 'python-ecdsa' 'python-future')
  optdepends=('python-cryptography: one option for better performance'
              'python-pycryptodome: one option for better performance')

  cd python-jose-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jose() {
  depends=('python2-rsa' 'python2-six' 'python2-ecdsa' 'python2-future')
  optdepends=('python2-cryptography: one option for better performance'
              'python2-pycryptodome: one option for better performance')

  cd python-jose-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
