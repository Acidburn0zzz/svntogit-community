# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.5.1
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=('x86_64')
url="https://github.com/ijl/orjson"
license=('Apache' 'MIT')
depends=('python')
makedepends=('maturin' 'python-pip' 'rustup')
checkdepends=('python-arrow' 'python-pendulum' 'python-psutil' 'python-pytest'
'python-pytz' 'python-xxhash')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('5f6b0305c83a52cc9262326a7d9e27523ff19a2ef4a534d76f063cd31a456cb88557803d69b553c2897025f1b90a21a5f5835fd7b1b29376f770c5e67d06c7bc')
b2sums=('dd82f0025e266f168ada6dc9517565c4b8f6006ee72ba441d0c68899b7bcb9c22641dae32129b997642fd3378bd1c2660156afcbd02dcaa1d4ee9fa48da177f9')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  rustup override set nightly-2021-01-31
}

build() {
  cd "$pkgname-$pkgver"
  maturin build --release \
                --strip \
                --manylinux
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "$pkgname-$pkgver"
  mkdir -vp testdir
  export PIP_CONFIG_FILE=/dev/null
  pip install --isolated \
              --root="testdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  export PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PIP_CONFIG_FILE='/dev/null'
  pip install --isolated \
              --root="$pkgdir" \
              --ignore-installed \
              --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
