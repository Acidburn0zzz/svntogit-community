# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
# https://github.com/aio-libs/aiobotocore/releases
pkgver=2.0.1
pkgrel=3
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-aioitertools)
makedepends=(python-setuptools)
checkdepends=(python-moto python-pytest python-pytest-asyncio
              # moto optdepends
              python-docker python-yaml python-flask python-flask-cors)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "botocore-1.23.diff")
sha256sums=('20da488ef99b4f296274a5012145f85887041e4e2c64e33eb3c2d6e17bf919fc'
            '67d9e250f2a0b6345be7b24ccf02dabf1d6ccae07b3b4183a113789b1f89d9d5')

prepare() {
  cd $_pkgname-$pkgver

  # From https://github.com/aio-libs/aiobotocore/issues/905#issuecomment-980524777
  patch -Np1 -i ../botocore-1.23.diff

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  sed --in-place=.orig -r "s#'(botocore.*),<.*',#'\1',#" setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export PYTHONPATH="$PWD"
  # test_lambda uses moto.awslambda, which requires a running Docker service
  # See: https://github.com/spulec/moto/issues/3276
  pytest -m moto tests \
      --ignore=tests/test_patches.py \
      --ignore=tests/test_lambda.py
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
