# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=3.0.1
pkgrel=1
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz")
sha512sums=('0b478fdfec801b1dd13259fff752e5fa8fd64648f21c52472df3a6be2d42ca8a8d8739e9138dccdef188ebc4dcd357e2062617cad5b268c9cec30c1fd5a45580')

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
