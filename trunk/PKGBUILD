# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=1.0.2
pkgrel=1
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL2)
depends=(podman python-dotenv python-yaml)
makedepends=(python-setuptools)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('3a2921ba8852c20ee86a7ee89f6c170e67331c3014fa0ce90fe5d804e44a13cb2e201b14798e099c2a0b7ff9722e5bc71484d79f83e6067242bf893b3df6353b')
b2sums=('9b0473a5d753c36d61b24465f0c62376eb9d0fc5fca3ec03b730f376aea52626cd0ee5738655c53fae6e251bec805251605957a88e2ca818dceb80c4365ec5cc')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  find examples -type f -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
  install -vDm 644 {README.md,docs/*} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
