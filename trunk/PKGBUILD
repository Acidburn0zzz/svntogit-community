# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=python-cairosvg
_pkgname=CairoSVG
pkgver=2.2.1
pkgrel=1
pkgdesc='SVG converter based on Cairo. It can export SVG files to PDF, PostScript and PNG files'
arch=('any')
url='http://cairosvg.org'
license=('LGPL3')
depends=('python' 'python-setuptools' 'python-cairocffi' 'python-pillow' 'python-cssselect2' 'python-defusedxml' 'python-tinycss2')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-flake8' 'python-pytest-isort')
replaces=('cairosvg')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Kozea/CairoSVG/archive/$pkgver.tar.gz"
        'https://github.com/Kozea/CairoSVG/commit/1f403ad229f0e2782d6427a79f0fbeb6b76148b6.patch')
sha512sums=('26cf72ffb6551f54801def0694661ac7a1c911cba132108d9d52328f9946b11243c7256f76509602a17f106a82b40685038d15ee706d696d509f77022ebd4d08'
            '0f216de848e2f81701f701e21425e3d948d3bab30f94462f9bbce825066fc22ea217595cb80fe1827f60b79f2b54fc5bbdd830afcbee786b99f1b8b650ec76b1')

prepare() {
  cd $_pkgname-$pkgver

  cp -r cairosvg test_non_regression/cairosvg_reference/

  # Fix tests
  patch -p1 < "$srcdir"/1f403ad229f0e2782d6427a79f0fbeb6b76148b6.patch
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

