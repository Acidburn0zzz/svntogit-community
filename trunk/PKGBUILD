# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.6.4
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(i686 x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git libpcl libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('5972f1b40bb3253319f564dee513229f82b0dcb8eea1502ae7dc483a9c6da5a0')

build() {
  cd shards-$pkgver
  crystal build --release -o bin/shards src/shards.cr
}

check() {
  cd shards-$pkgver
  ./bin/shards install
  make test
}

package() {
  cd shards-$pkgver

  install -Dm755 bin/shards "$pkgdir/usr/bin/shards"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
