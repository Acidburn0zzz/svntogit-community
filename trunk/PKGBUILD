# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=3.0.1
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-filelock')
makedepends=('python-setuptools-scm' 'python-pip')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('1179a6110c30afbcb6540e24007382821ab18b78a5f2a26d3211838dece9ea2b6fe607ae9a5800974bd1be50babe4c0cec5850066f9b92e166024bd430e41bfb')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python -m pytest --ignore build
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # use the snapshot version, because generating a new one requires Internet access and root permission
  ln -s .tld_set_snapshot "$pkgdir"/usr/lib/python3.8/site-packages/tldextract/.tld_set
}

# vim:set sw=2 et:
