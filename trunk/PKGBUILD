# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qxgedit
pkgver=0.9.2
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=('x86_64')
url="https://qxgedit.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt6-base')
makedepends=('alsa-lib' 'cmake' 'qt6-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-0.9.2-qt6.patch::https://github.com/rncbc/qxgedit/pull/6/commits/406232e2cb836ac2e459bcd81428bf09041144e9.patch")
sha512sums=('4531fed72420ab37500d9b1f2467ef847a60fab64abc367b731c4101c574c5fc0a785f6262876563b4227dac4ba5c0d1739e37616302b03d091722785cb36f60'
            'a5919ce1748ffe4d369c540c1b8b23ee166767e97a3f63d5c511b0ca64efa5d1bf3f62a2ba3b0d3106cc91ced6ad2e5ae1f4fa459052235c009bdd6adb17ef67')
b2sums=('bb7c818fc2185ae10648d30e60a4a46bffd465945be35de38996a3ec75493453bea2edce7b26eb5f9d2730bf455c70fa1cc458c3695fc8f4c5f0ccb10f2b01cf'
        '031b51931dfa593352e2a686e24daf6a7eaab219ed5201543b3662a6dc17e99d815e90990d31510b2eeb300faa788b60e407bbb27212e23eaefdbeaac7630c61')

prepare() {
  cd "$pkgname-$pkgver"
  # do not include skulpture on qt6:
  # https://github.com/rncbc/qxgedit/pull/6
  patch -Np1 -i "../${pkgname}-0.9.2-qt6.patch"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libasound.so')
  cd "$pkgname-$pkgver"
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
