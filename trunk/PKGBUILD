# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: waterbear <st92.at.freeshell.org>

pkgname=simh
pkgver=3.8.1
_pkgver=38-1
pkgrel=3
pkgdesc="simulator for assorted historical computers, from Altair to VAX"
arch=('i686' 'x86_64')
url="http://simh.trailing-edge.com"
license=("BSD")
depends=("libpcap")
makedepends=("unzip")
install=simh.install
source=("http://simh.trailing-edge.com/sources/simhv${_pkgver}.zip"
	"LICENSE")
md5sums=('e15f65a82e21ea49e14b438326d93d5c'
         'd56eefa5d020ee2b77d21280fef80e3d')

build() {
  cd "$srcdir"
  mkdir -p BIN
  find VAX -type f -exec sed -i 's/SIM_INLINE//' {} +
  make USE_NETWORK=1 NETWORK_OPT='-DUSE_NETWORK -isystem /usr/include -lpcap'
}

package() {
  cd "$srcdir/BIN"
  for i in *; do
    install -D $i "$pkgdir/usr/bin/simh-$i"
  done

  cd "$srcdir/VAX"
  mkdir -p "$pkgdir/usr/lib/simh"
  cp *.bin "$pkgdir/usr/lib/simh"

  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
