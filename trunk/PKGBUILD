# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.4.4
pkgrel=2
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("https://github.com/lilydjwg/nvchecker/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        "$pkgname-test_regex-1.patch::https://github.com/lilydjwg/nvchecker/commit/1e1f431f9bf301ac2ac3e4f158632da8d0ac0858.patch"
        "$pkgname-test_regex-2.patch::https://github.com/lilydjwg/nvchecker/commit/6f48b774ae6bffea857e97bed04dd856c7d8aa55.patch")
sha512sums=('5fb95901641953a093a8d6c10d5b72ad9982dc8cc6161e769b30a861e4057100b861dbe9ae26c288b4fa1126b261f34c82afeb0052670429c1b7f97d1b2cf6ff'
            '1c6f42b3df56eee04aecdf09be35972aa184d82a986b942011782b65e717327757eddcc5bea690cf08ff0cf9a0f57b519c2a614f5289ac1cff98bbdf6f90175d'
            '5ff70455d2234ddadd8c2a72812b3896c9ec1fa4acf8cb529a5ceaedb2f8b5ffdd9688665f9aec8ddfdc83d3d2482bc0aa9f2a468f04848decf80527ad58f858')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-test_regex-1.patch
  patch -Np1 -i ../$pkgname-test_regex-2.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
