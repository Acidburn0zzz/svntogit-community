# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=terraform
pkgver=0.11.5
pkgrel=1
pkgdesc="Tool for building, changing, and versioning infrastructure safely and efficiently"
url="http://www.terraform.io/"
arch=("x86_64")
license=("MPL")
makedepends=("go-pie")
source=("$pkgname-$pkgver.tar.gz::https://github.com/hashicorp/terraform/archive/v$pkgver.tar.gz")
sha512sums=('22a9ce667dc45f4c24154c3323370406abf77469d7e21d58e897f82cc09bf08484907699da1968d2058d84ff494b4f8c9eff87890d298285add2bd732d7e26cd')


prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p src/github.com/hashicorp/
  mv "${pkgname}-${pkgver}" src/github.com/hashicorp/${pkgname}
}


build() {
  cd src/github.com/hashicorp/"${pkgname}"
  go build -o terraform-binary
}


package() {
  cd src/github.com/hashicorp/"${pkgname}"
	install -Dm755 terraform-binary "$pkgdir/usr/bin/terraform"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	# Zsh completion
	install -D contrib/zsh-completion/_terraform "$pkgdir/usr/share/zsh/site-functions/_terraform"
}
