# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

_plugins_pkgver=3.8.1
pkgname=nomacs
pkgver=3.12
pkgrel=1
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('desktop-file-utils' 'exiv2' 'libraw' 'opencv' 'quazip' 'qt5-svg')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${_plugins_pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${_plugins_pkgver}.tar.gz")
sha512sums=('4508354d48a6115cd40f701cc90cbbcb84490e99eaac0295db48f6fea99550d5e2b3e7365128d0c9cc4eeb621906f55e275603d84cb8b6bb164d0b5e2e192119'
            '86bc4db30b0815fd2efa272b14d3917f0e88fa95284f0afb55acca87b77b3db7668238fb3023cc9b21fe8c87cad72319d45d48633a646bf5b6acb80a6537b8c8')

prepare() {
  cd "${pkgname}-${pkgver}"
  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${_plugins_pkgver}" "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr \
                      -DUSE_SYSTEM_QUAZIP=ON \
                      -DENABLE_TRANSLATIONS=true
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
