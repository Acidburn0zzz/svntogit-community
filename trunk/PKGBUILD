# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: David Adler <d dot adler aet posteo dot de>

pkgname=yoshimi
pkgver=1.6.1
pkgrel=2
pkgdesc="A sophisticated soft-synth originally forked from ZynAddSubFX"
arch=('x86_64')
url="https://yoshimi.github.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'cairo' 'fftw' 'fltk' 'gcc-libs' 'glibc' 'libjack.so'
'libmxml.so' 'libncursesw.so' 'libreadline.so' 'zlib')
makedepends=('cmake' 'lv2' 'mesa')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('dd632eedadfeb89e5e4a41e12455fe5523f2d01c1b687a08f0f7c2696a16d09d1d6f017969828741543ee8c2a507164115b985d9cf9727c155180dbe6680e0c1')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -vp build
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -B build\
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_INSTALL_DATAROOTDIR='/usr/share' \
        -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
        src
  make -C build
}

package() {
  cd "${pkgname}-${pkgver}"
  make -C build DESTDIR="${pkgdir}" install
  install -vDm 644 {Changelog,Dependencies,README.txt,Yoshimi_Helpers} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
