# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=bash-language-server
pkgver=1.5.3
pkgrel=1
pkgdesc="Bash language server implementation based on Tree Sitter and its grammar for Bash"
arch=("x86_64")
url="https://github.com/mads-hartmann/bash-language-server"
license=("MIT")
depends=("nodejs" "acorn")
makedepends=("yarn" "typescript" "python2")
source=("https://github.com/mads-hartmann/$pkgname/archive/server-$pkgver.tar.gz")
sha512sums=('3dbaedc36c483fb990a7b54bf75d3a87bfb1b5cfe9c4ba4ece7107003102e7a6fed317c77d0f59dd3c06828d76e7dde78dd4f6104f67261fe50462034741c0dd')

build() {
    cd "$srcdir/$pkgname-server-$pkgver"
    yarn
    cd server
    yarn run compile
}

package() {
    install -d "$pkgdir/usr/lib/$pkgname"
    cd "$pkgdir/usr/lib/$pkgname"
    cp -a "$srcdir/$pkgname-server-$pkgver/server/"* .

    rm -r node_modules/acorn

    install -d "$pkgdir/usr/bin"
    ln -s "/usr/lib/$pkgname/bin/main.js" "$pkgdir/usr/bin/$pkgname"
}
