# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-anything-arch
pkgver=5.0.1
_extramodules=extramodules-ARCH
pkgrel=2
pkgdesc="Deepin Anything file search tool, kernel module for Arch kernel"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-anything"
license=('GPL3')
depends=('linux')
makedepends=('linux-headers')
provides=('DEEPIN-ANYTHING-MODULE')
replaces=('deepin-anything-module')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-anything/archive/$pkgver.tar.gz")
sha512sums=('f79b4db917cce2611bd6964d00ae0e162fc500fa7ca76a987145456a9ee81296c776d2b83cf6492a4224c4e4fd95df3ad95a25c1c14d2d4e6865f5bbd639be14')

build() {
  cd deepin-anything-$pkgver
  _kernver="$(cat /usr/lib/modules/$_extramodules/version)"
  make -C kernelmod kdir=/usr/lib/modules/$_kernver/build
}

package() {
  cd deepin-anything-$pkgver
  install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 kernelmod/*.ko
  find "$pkgdir" -name '*.ko' -exec xz {} +
}
