# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor : sebikul <sebikul@gmail.com>
# Contributor : Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=geary
pkgver=0.4.3
pkgrel=1
pkgdesc='A lightweight email client for the GNOME desktop'
arch=('i686' 'x86_64')
url='http://www.yorba.org/projects/geary/'
license=('GPL3')
depends=('desktop-file-utils' 'gmime' 'libcanberra' 'libgee' 'libnotify' 'libunique3' 'webkitgtk3')
makedepends=('cmake' 'gobject-introspection' 'intltool' 'vala')
install="${pkgname}.install"
source=("https://download.gnome.org/sources/${pkgname}/${pkgver%.?}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('58a4021bea6be813a1381597f23b646335c9e15c3a7d818c302752a7e07e37a0')

build() {
  cd ${pkgname}-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_BUILD_TYPE='Release' -DCMAKE_INSTALL_PREFIX='/usr' -D{DESKTOP_UPDATE,GSETTINGS_COMPILE{,_IN_PLACE},ICON_UPDATE}='OFF'
  make
}

package() {
  cd ${pkgname}-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
