# $Id$
# Maintainer: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=2.0.0
pkgrel=2
pkgdesc="Library with common API for various Cinnamon modules"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(gtk3 libxkbfile)
makedepends=(intltool gobject-introspection gnome-common)
url="https://github.com/linuxmint/cinnamon-desktop"
options=(!libtool)
install=cinnamon-desktop.install
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-desktop/archive/${pkgver}.tar.gz")
sha256sums=('dd4ef4c14699a0566ae8e7e060a7db8574ef65dcdac80751de350788ea99ff3c')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --libexecdir=/usr/lib/$pkgname --disable-static \
    --with-gnome-distributor="Arch Linux"
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
