# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Justin Davis <jrcd83@gmail.com>

pkgname=perl-sub-exporter
pkgver=0.986
pkgrel=1
pkgdesc="A sophisticated exporter for custom-built routines"
arch=('any')
license=('PerlArtistic' 'GPL')
options=(!emptydirs)
depends=('perl>=5.6.0' 'perl-data-optlist>=0.1' 'perl-params-util>=0.14'
         'perl-sub-install>=0.92')
url="https://metacpan.org/release/Sub-Exporter"
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Sub-Exporter-$pkgver.tar.gz")
md5sums=('bdfa41103f82ab8611af61fceed1271e')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB="" \
      PERL_AUTOINSTALL=--skipdeps            \
      PERL_MM_OPT="INSTALLDIRS=vendor"       \
      PERL_MB_OPT="--installdirs vendor"     \
      MODULEBUILDRC=/dev/null

    cd Sub-Exporter-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""

    cd Sub-Exporter-$pkgver

    make test
  )
}

package() {
    cd Sub-Exporter-$pkgver

    make DESTDIR="$pkgdir" install
}
