# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Ivan Sichmann Freitas <ivansichfreitas at gmail dot com>
# Contributor: Laszlo Papp <djszapi2 at gmail dot com>
# Contributor: Gerardo Exequiel Pozzi <vmlinuz386@yahoo.com.ar>
# Contributor: Vladimir Cerny <blackvladimir@gmail.com>
# Contributor: 4javier <4javieregATgmailDOTcom>

pkgname=avr-gdb
_pkgname=gdb
pkgver=7.8.1
pkgrel=1
pkgdesc="The GNU Debugger for AVR"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/gdb/"
license=('GPL3')
depends=('python2' 'guile' 'xz')
source=(ftp://ftp.gnu.org/gnu/gdb/gdb-$pkgver.tar.xz{,.sig})
sha256sums=('497722b58386f4428c3474281eb3ef1c872404998b6ace907edb3bba1fabdc35'
            'SKIP')
validpgpkeys=('F40ADB902B24264AA42E50BF92EDB04BFF325CF3') # Joel Brobecker

build() {
  cd ${_pkgname}-${pkgver}
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  ./configure \
    --disable-nls \
    --enable-languages=c,c++ \
    --prefix=/usr \
    --target=avr \
    --with-python=/usr/bin/python2 \
    --with-system-readline \
    --with-system-gdbinit=/etc/gdb/gdbinit

  make
}

package() {
  cd ${_pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -f ${pkgdir}/usr/lib/libiberty.a
  rm -r ${pkgdir}/usr/share/info
  rm -r ${pkgdir}/usr/share/man
  rm -r ${pkgdir}/usr/share/gdb
  rm -r ${pkgdir}/usr/include/gdb
}
