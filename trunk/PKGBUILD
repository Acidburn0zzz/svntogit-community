# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgbase=argon2_cffi
pkgbase=python-argon2_cffi
pkgname=('python2-argon2_cffi' 'python-argon2_cffi')
pkgver=18.3.0
pkgrel=1
pkgdesc="CFFI-based Argon2 Bindings for Python"
arch=('x86_64')
url="https://github.com/hynek/argon2_cffi"
license=('MIT')
depends=(argon2 python-cffi python-six python2-cffi python2-enum34 python2-six)
makedepends=(git python-setuptools python2-setuptools)
_commit=f0b0d8fc4baa820fc0d5612f9384f6c95a0ed5c1  # tags/18.3.0^0
source=("git+https://github.com/hynek/argon2_cffi#commit=$_commit"
        "https://github.com/p-h-c/phc-winner-argon2")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $_pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $_pkgbase
  git submodule init
  git config --local submodule.extras/libargon2.url "$srcdir/phc-winner-argon2"
  git submodule update
  cd ..

  cp -a $_pkgbase{,-py2}
}

build() {
  # Building Python2
  cd $_pkgbase-py2
  python2 setup.py build

  # Building Python3
  cd ../$_pkgbase
  python3 setup.py build
}

package_python2-argon2_cffi() {
  depends=(argon2 python2-cffi python2-enum34 python2-six)

  cd $_pkgbase-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-argon2_cffi() {
  depends=(argon2 python-cffi python-six)

  cd $_pkgbase
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
