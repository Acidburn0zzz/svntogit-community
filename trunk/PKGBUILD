# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.11.3
pkgrel=1
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm-libs' 'clang' 'bcc' 'libbpf')
makedepends=('cmake' 'llvm' 'git' 'linux-headers')
options=(!strip)
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz")
sha512sums=('76f6b19ce2a2f3989ebafc49a4df337c6b5f3acf39e7415846b16ee08e2fa7a59821cd18de3c9ed29420b323de8458d1411ccf801e9cd5f3ac64f2bd981e2c3c')

build() {
  cd bpftrace-$pkgver

  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd bpftrace-$pkgver/build

  make DESTDIR="$pkgdir" install
}
