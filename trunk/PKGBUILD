# Maintainer: David Runge <dvzrv@archlinux.org>

_name=toolz
pkgname=python-toolz
pkgver=0.11.0
pkgrel=1
pkgdesc="A functional standard library for Python"
arch=('any')
url="https://github.com/pytoolz/toolz/"
license=('BSD')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('6c546561848d5156ae90fc7f11b219c4aa489c96d3d0c6e703b636dc7f0096fe95b986e7e1f33a0914b324bf807f6d66bec4c8ce2031c3aae3a3086bb1cb0596')
b2sums=('5b4a1fb4503767b91262ada31026c8ce26b98bb98967043dbbd2ecfa0011e200accc65d3a3923cbb69670aa305845d6825b3f0ed08f9e5a2c4e3a3854df1c62d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
