# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.17
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm-switcher')
makedepends=('cmake' 'coffeescript' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go-pie' 'python2' 'git')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz"
        deepin-notifications.patch)
sha512sums=('33b72245a93106c388b7f9f97e106fd544792d201ae656857d4f2b7fb1c5c02d6522509e489b9e900b94e2025e03f1fde4e111a657d61fa84b278506651acec9'
            'cf684b195a9cd9e24722a211c5446f460558caf95b0f7635641c0cbab08094c6f7fb71dff17a3f705152160d82e4585212fdd280abf0005134e67ff7d9c52f9f')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify

  cd startdde-$pkgver
  patch -p1 -i ../deepin-notifications.patch
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install

  mv "$pkgdir"/lib/systemd "$pkgdir"/usr/lib/systemd
  rmdir "$pkgdir"/lib
  ln -sf ../dde-readahead.service "$pkgdir"/usr/lib/systemd/system/multi-user.target.wants/dde-readahead.service
}
