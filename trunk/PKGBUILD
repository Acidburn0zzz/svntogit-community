# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jason R Begley (jayray@digitalgoat.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=freeradius
pkgver=3.0.17
pkgrel=6
pkgdesc='The premier open source RADIUS server'
arch=('x86_64')
url="https://freeradius.org/"
license=('GPL')
depends=('krb5' 'pth' 'net-snmp' 'postgresql-libs' 'libmariadbclient' 'talloc'
         'libpcap' 'libwbclient')
makedepends=('unixodbc' 'python2' 'curl' 'json-c' 'samba')
optdepends=('unixodbc: for ODBC'
            'python2: for Python'
            'json-c: rlm_rest module'
            'curl: for REST')
options=('!makeflags')
validpgpkeys=('BF2384EC6938B9744B03E2A620E37C25995B4F85') # FreeRADIUS - Package Signing [...] <packages@freeradius.org>
source=("ftp://ftp.freeradius.org/pub/radius/freeradius-server-$pkgver.tar.bz2"{,.sig}
        'openssl-cve.patch'
        'freeradius-sysusers.conf'
        'freeradius-tmpfiles.conf'
        'freeradius.service')
sha512sums=('f4510d8e77eb7c72a21fbfad851f13460ff4b5a35f0b7bea6102076ceb71188a63b277fb7e4fcd9c3033b396b63e1bf0e455cc03608d7ab1380d1662407cb399'
            'SKIP'
            '77908c2f9e7bd526711f6057c827a0fd969dd2c9269df7a88d494112cc68c7f3ceb0fcde3d3c6358a14e4980505c57284787c8981e52856c7fc858d46a95a3dc'
            '890005b2129174568a3bf0e8963b683ab15550198b9478cc766c3ddcfd5167296cfce221c7592be354fe7dfe08e82484f826e55fd59b6291e86c8a4f78ca2d96'
            '5e196584c725885ae33b70d729729b52852f6a051445be3f9afd831564029820179f606e6c8d8554f8615e2b4b9b8d5203a32b8a81c04d4edfb96a377a213bae'
            '833bfd85218898af6f24e9356f1af60ba9e8f08a93fa93aafb53ba9ec49afdf23c7eeb897ac5939c2d7c6958076cbb3fbc0c075b741e4b9be2f70c3fef2014b6')

prepare() {
  cd "$srcdir"/freeradius-server-$pkgver

  patch -Np1 < ../openssl-cve.patch
}

build() {
  cd "$srcdir"/freeradius-server-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --libdir=/usr/lib/freeradius \
    --localstatedir=/var \
    --enable-heimdal-krb5 \
    --with-system-libltdl \
    --with-system-libtool \
    --with-udpfromto
  make
}

package() {
  cd "$srcdir"/freeradius-server-$pkgver

  make install R="$pkgdir"
  chmod o+r "$pkgdir"/etc/raddb/*
  mv "$pkgdir"/etc/raddb "$pkgdir"/etc/raddb.default
  rm -rf "$pkgdir"/var/run

  install -D -m0644 "$srcdir"/$pkgname.service "$pkgdir"/usr/lib/systemd/system/$pkgname.service
  install -D -m0644 "$srcdir"/$pkgname-sysusers.conf "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  install -D -m0644 "$srcdir"/$pkgname-tmpfiles.conf "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf
}
