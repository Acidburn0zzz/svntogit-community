# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

_pkgbasename=nss
pkgname=lib32-${_pkgbasename}
pkgver=3.30.2
pkgrel=1
pkgdesc="Network Security Services (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=(x86_64)
license=('MPL' 'GPL')
_nsprver=4.12
depends=("lib32-nspr>=${_nsprver}" 'lib32-sqlite' 'lib32-zlib' 'lib32-p11-kit' "${_pkgbasename}")
makedepends=('gcc-multilib' 'perl')
options=('!strip' '!makeflags' 'staticlibs')
source=("https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/${_pkgbasename}-${pkgver}.tar.gz"
        nss.pc.in)
sha256sums=('0d4a77ff26bcee79fa8afe0125e0df6ae9e798b6b36782fa29e28febf7cfce24'
            'b9f1428ca2305bf30b109507ff335fa00bce5a7ce0434b50acd26ad7c47dd5bd')

prepare() {
  cd nss-$pkgver

  # Respect LDFLAGS
  sed -e 's/\$(MKSHLIB) -o/\$(MKSHLIB) \$(LDFLAGS) -o/' \
      -i nss/coreconf/rules.mk
}

build() {
  cd nss-$pkgver/nss
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig
  export BUILD_OPT=1
  export NSS_USE_SYSTEM_SQLITE=1
  export NSS_ALLOW_SSLKEYLOGFILE=1
  export NSS_ENABLE_ECC=1
  export NSPR_INCLUDE_DIR="`nspr-config --includedir`"
  export NSPR_LIB_DIR="`nspr-config --libdir`"
  export XCFLAGS="${CFLAGS}"

  make -C coreconf
  make -C lib/dbm
  make
}

package() {
  cd nss-$pkgver

  install -d "$pkgdir"/usr/lib32/pkgconfig

  NSS_VMAJOR=$(grep '#define.*NSS_VMAJOR' nss/lib/nss/nss.h | awk '{print $3}')
  NSS_VMINOR=$(grep '#define.*NSS_VMINOR' nss/lib/nss/nss.h | awk '{print $3}')
  NSS_VPATCH=$(grep '#define.*NSS_VPATCH' nss/lib/nss/nss.h | awk '{print $3}')

  sed ../nss.pc.in \
    -e "s,%libdir%,/usr/lib32,g" \
    -e "s,%prefix%,/usr,g" \
    -e "s,%exec_prefix%,/usr/bin,g" \
    -e "s,%includedir%,/usr/include/nss,g" \
    -e "s,%NSPR_VERSION%,${_nsprver},g" \
    -e "s,%NSS_VERSION%,${pkgver},g" \
    > "$pkgdir/usr/lib32/pkgconfig/nss.pc"
  ln -s nss.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nss.pc"

  cd dist/*.OBJ/lib
  install -t "$pkgdir/usr/lib32" *.so
  install -t "$pkgdir/usr/lib32" -m644 libcrmf.a *.chk

  rm "$pkgdir/usr/lib32/libnssckbi.so"
  ln -s libnssckbi-p11-kit.so "$pkgdir/usr/lib32/libnssckbi.so"
}
