# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-swiftclient
pkgname=(python-swiftclient python2-swiftclient)
pkgver=3.8.0
pkgrel=1
pkgdesc="An SDK for building applications to work with OpenStack"
arch=('any')
url="https://docs.openstack.org/python-swiftclient"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-futures'
             'python-requests' 'python2-requests')
checkdepends=('python-stestr' 'python2-stestr' 'python-mock' 'python2-mock'
              'python-keystoneauth1' 'python2-keystoneauth1')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/python-swiftclient/archive/$pkgver.tar.gz")
sha512sums=('c7c71ca3536f53fd88532cf0e00045159d7ee7504c33bcf0735f38e1a2bd2c1d3508a6ba7b125c04a3d0a41a9d09825bc8597c3301c4cb65473a5a050df76e81')

prepare() {
  cp -a python-swiftclient-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py build

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-swiftclient-$pkgver
  stestr run

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-swiftclient() {
  depends=('python-six' 'python-requests')

  cd "$srcdir"/python-swiftclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-swiftclient() {
  depends=('python2-six' 'python2-futures' 'python2-requests')

  cd "$srcdir"/python-swiftclient-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/swift{,2}
  mv "$pkgdir"/usr/share/man/man1/swift{,2}.1
}

# vim:set ts=2 sw=2 et:
