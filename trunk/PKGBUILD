# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=5.1.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
         'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
         'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-openstackclient/archive/$pkgver.tar.gz")
sha512sums=('4856796e0dadbec128725e3f7648412234dc27652692b4e1c699cee0432e756ac5da48b32d2db20b84bb68712f0e307e5ae9f374db91533c113f52f77f152c00')

export PBR_VERSION=$pkgver

build() {
  cd python-openstackclient-$pkgver
  python setup.py build
}

check() {
  cd python-openstackclient-$pkgver
  stestr run || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package() {
  cd python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
