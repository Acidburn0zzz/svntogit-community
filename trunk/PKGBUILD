# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.9.11
pkgrel=5
pkgdesc='A dock based on Plasma Frameworks'
arch=(x86_64)
url='https://store.kde.org/p/1169519/'
license=(GPL)
depends=(plasma-framework knewstuff hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        latte-dock-plasma-5.22.patch::https://invent.kde.org/plasma/latte-dock/commit/078db6da.patch)
sha256sums=('513320e30f3016686ffb97f5502e513f4d21ce0f7bce75bf034a7775d448ea74'
            'SKIP'
            '87f3978d00d1c9e7df70066dfa243a246133331d742de2d663fa07e192c2472b')
validpgpkeys=(325E97C32E601F5D4EADCF3A55999050A2D9110E) # Michail Vourlakos (mvourlakos) <mvourlakos@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < latte-dock-plasma-5.22.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
