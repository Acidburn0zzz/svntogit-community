# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=motion
pkgver=3.2.12
pkgrel=3
pkgdesc="A software motion detector which grabs images from video4linux devices and/or from webcams"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.lavrsen.dk/twiki/bin/view/Motion/WebHome"
depends=('ffmpeg' 'libjpeg' 'v4l-utils')
backup=('etc/motion/motion.conf')
options=('!makeflags')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	rc.motion
	ffmpeg-0.8.patch
	linux-headers.patch)
md5sums=('1ba0065ed50509aaffb171594c689f46'
         '77be944e4466ab66836d50ea216af3b6'
         'd36687710837d69fbce4608b1345fa34'
         'd8c3c4fdded5cfbd729710475559a21d')
sha1sums=('dc59b36e45e7626baa65ce62c961af918fea76bd'
          'e092b0d880d0169feb56ed1e8a3c95729a7896f7'
          '6912448db0711f85322505ee6f8052f8f98ae634'
          '6f33dab895ae398cc5e7017cbf5dc5d728f3ca9e')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i $srcdir/ffmpeg-0.8.patch
  patch -Np1 -i $srcdir/linux-headers.patch
  ./configure --prefix=/usr \
    --without-pgsql \
    --without-mysql \
    --sysconfdir=/etc/motion
  make
}

package(){
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}/etc/motion/motion-dist.conf" "${pkgdir}/etc/motion/motion.conf"

  install -Dm755 "${srcdir}/rc.motion" "${pkgdir}/etc/rc.d/motion"
}
