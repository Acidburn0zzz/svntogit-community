# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-testfixtures
pkgver=6.12.0
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-zope-component' 'python-sybil'
              'python-pytest-django' 'python-django' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz")
sha512sums=('ba9a57c07edcb33bdecc33f4587f096351d37ed2e32b95b9b13ff4c170a0585a22533db36362fb164e9c88ef8049973fce76dad991e591cc0618a7e8171a88db')

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py pytest --addopts "--ignore=build"
}

package() {
  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
