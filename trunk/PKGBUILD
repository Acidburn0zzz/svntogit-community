# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: arkhan <arkhan@disroot.org>

pkgname=ueberzug
pkgver=18.1.8
pkgrel=1
pkgdesc='Command line util which allows to display images in combination with X11'
arch=('x86_64')
url=https://github.com/seebye/ueberzug
license=('GPL3')
depends=('libxext' 'python-attrs' 'python-docopt' 'python-pillow'
         'python-psutil' 'python-setuptools' 'python-xlib')
provides=("python-$pkgname")
replaces=("python-$pkgname")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('3718db8f824ef5f6a69dc25b3f08e0a45388dd46843c61721476bad2b64345ee')
b2sums=('153329b31ba1c1c8d494d9d2ae6ce17f336df5dbaa7d2e48328489b854085450b39321f486944a3d1d43b211b118d2da4809d5f1f3c88a102da36d28de8e7270')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
