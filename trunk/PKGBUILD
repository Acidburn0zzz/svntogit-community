# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=primecount
pkgver=5.1
pkgrel=1
pkgdesc="Fast C++ prime counting function implementation"
arch=(x86_64)
url="https://github.com/kimwalisch/primecount"
license=(BSD)
depends=(gcc-libs)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/kimwalisch/primecount/archive/v$pkgver.tar.gz")
sha256sums=('a69636ddc03788dec1b82a15e39a3bf0cd1ff38c05467471b6c61c4896c0823f')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING -t "$pkgdir"/usr/share/licenses/$pkgname/
}
