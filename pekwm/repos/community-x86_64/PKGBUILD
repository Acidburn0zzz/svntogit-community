# $Id$
# Maintainer:
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.15
pkgrel=1
pkgdesc="A small, fast, functional, and flexible window manager"
arch=('i686' 'x86_64')
license=('GPL')
url='https://www.pekwm.org/projects/pekwm'
depends=('gcc-libs' 'libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxpm' 'libxrandr')
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
	etc/pekwm/config
	etc/pekwm/config_system
	etc/pekwm/keys
	etc/pekwm/menu
	etc/pekwm/mouse
	etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
	etc/pekwm/start
        etc/pekwm/vars)
source=("http://www.pekwm.org/projects/pekwm/files/${pkgname}-${pkgver}.tar.bz2"
        'pekwm.desktop')
md5sums=('4db10ad191b77120429abd5a65c73cec'
         '5a78fc6653fbb0b7282ecf7f1f81e2c5')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  # Default settings:
  #    --enable-shape --enable-xinerama --enable-menus \
  #    --enable-harbour --disable-debug --disable-pedantic \
  #    --enable-xft --enable-image-xpm --enable-image-jpeg \
  #    --enable-image-png --enable-xrandr
  ./configure --prefix=/usr --sysconfdir=/etc

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/${pkgname}.desktop" \
                   "${pkgdir}/usr/share/xsessions/${pkgname}.desktop"
}
# vim: ts=2 sw=2 et ft=sh
