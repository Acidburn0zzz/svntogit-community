# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Eddie Lozon <almostlucky@attbi.com>

pkgname=pekwm
pkgver=0.1.16
pkgrel=1
pkgdesc='Small, fast, functional, and flexible window manager'
arch=('x86_64' 'i686')
license=('GPL')
url='https://www.pekwm.org/projects/pekwm'
depends=('gcc-libs' 'libjpeg' 'libpng' 'libxft' 'libxinerama' 'libxpm' 'libxrandr')
makedepends=('gendesk')
backup=(etc/pekwm/autoproperties
        etc/pekwm/autoproperties_typerules
        etc/pekwm/config
        etc/pekwm/config_system
        etc/pekwm/keys
        etc/pekwm/menu
        etc/pekwm/mouse
        etc/pekwm/mouse_click
        etc/pekwm/mouse_sloppy
        etc/pekwm/mouse_system
        etc/pekwm/start
        etc/pekwm/vars)
source=("http://www.pekwm.org/projects/pekwm/files/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('899eef35d5d1f472dd65a08c180b9719935cb1f42eda7fada6f9b8b551c79c96')

build() {
  cd "$srcdir"
  gendesk

  cd "$pkgname-$pkgver"
  # Default settings:
  #    --enable-shape --enable-xinerama --enable-menus \
  #    --enable-harbour --disable-debug --disable-pedantic \
  #    --enable-xft --enable-image-xpm --enable-image-jpeg \
  #    --enable-image-png --enable-xrandr
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/xsessions/$pkgname.desktop"
}
# vim: ts=2 sw=2 et
