# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: RÃ©my Oudompheng <oudomphe@clipper.ens.fr>

pkgname=zn_poly
pkgver=0.9.1
pkgrel=2
pkgdesc="Polynomial arithmetic over Z/nZ"
arch=(x86_64)
url="https://gitlab.com/sagemath/zn_poly/"
license=(GPL)
depends=(gmp)
makedepends=(python)
source=("https://gitlab.com/sagemath/zn_poly/-/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         zn_poly-python3.patch)
sha256sums=('d11aea5646cd72ea44ce8dd0d996cf2ea498e882ed96304a74481c4eb6e01093'
            'c72736011693c9d603f0f4f8e58208b7e7923747fac2d63aadc3fec13f3d9e77')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../zn_poly-python3.patch # Fix build with python3
}

build() {
  cd $pkgname-$pkgver
  python makemakefile.py --prefix=/usr --cflags="$CFLAGS -fPIC" --cxxflags="$CXXFLAGS" --ldflags="$LDFLAGS" > makefile
  make
  make libzn_poly.so
}

package() {
  cd $pkgname-$pkgver
  python makemakefile.py --prefix="$pkgdir"/usr > makefile
  make install
# Install shared library
  cp libzn_poly.so "$pkgdir"/usr/lib
}
