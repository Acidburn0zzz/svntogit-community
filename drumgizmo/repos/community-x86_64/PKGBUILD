# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: sekret, mail=$(echo c2VrcmV0QHBvc3Rlby5zZQo= | base64 -d)
pkgname=drumgizmo
pkgver=0.9.15
pkgrel=1
pkgdesc="Multichannel, multilayered, cross-platform drum plugin and stand-alone application"
arch=('x86_64')
url="https://www.drumgizmo.org"
license=('GPL')
groups=('lv2-plugins' 'pro-audio')
depends=('libsmf' 'libxext' 'expat' 'jack' 'zita-resampler')
makedepends=('lv2')
source=("https://www.${pkgname}.org/releases/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('99a08fb054b1b4152d9b723f40510aabbb2a24eed981cc139cc01ddc13d88d76b01696ed79173169ca5df43ff81bcdb7f4948f6f985f5596fa1d86d9255a6923'
            'SKIP')
validpgpkeys=('F39C94D556CCB995B1AE540E9EB89445BF071867')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --enable-lv2
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README}
}

# vim:set ts=2 sw=2 et:
