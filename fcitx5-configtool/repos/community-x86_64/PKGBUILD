# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-configtool
pkgver=0.0.0.20200902
_commit=404e72469e36f18ceb183962fb8751812881a277
pkgrel=1
pkgdesc="Configuration Tool for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-configtool"
license=('GPL')
groups=('fcitx5-im')
provides=('fcitx5-config-qt' 'kcm-fcitx5')
conflicts=('kcm-fcitx5')
replaces=('kcm-fcitx5')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews' 'qt5-x11extras')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'ninja' 'python')
source=("https://github.com/fcitx/fcitx5-configtool/archive/$_commit/fcitx5-configtool-$_commit.tar.gz")
sha512sums=('5c707a7718fb3af02cfebd7cd6b0798e18995488cdd8996db67fa006ca5f2eeb7c51de67e745aa8ff139ef3f48f96b35a3186af3164462ea497443da498cfd04')

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
