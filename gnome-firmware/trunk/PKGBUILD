# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=gnome-firmware
pkgver=3.34.0
pkgrel=1
pkgdesc="Manage firmware on devices supported by fwupd"
arch=(x86_64)
url="https://gitlab.gnome.org/hughsie/gnome-firmware-updater"
license=(GPL2)
depends=(gtk3 glib2 fwupd libxmlb libsoup systemd)
makedepends=(meson help2man)
source=(https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz
        gnome-firmware-fix-help2man-build.patch::https://gitlab.gnome.org/hughsie/gnome-firmware-updater/commit/cb93d1522e6ef927ee458e40eddc793f85c8a746.patch
        gnome-firmware-fix-headless-build.patch::https://gitlab.gnome.org/hughsie/gnome-firmware-updater/commit/c4f076f2c902080618e0c27dec924fd0019f68a3.patch)
sha256sums=('bd33a3db43b6e5ab873cbbe304ac60e6fb4f56c9a6b1135a90561c51dfaed668'
            '3250081789f57e308c073ee2b27fa8519f0be586a4f036ef046abc55b2c4c6d7'
            'a53e8f89c7915c79f9f00bb5863af363a3452f6225f2f27007179cc3f962abe4')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../gnome-firmware-fix-help2man-build.patch
    patch -p1 -i ../gnome-firmware-fix-headless-build.patch
}

build() {
	arch-meson ${pkgname}-${pkgver} build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
}
