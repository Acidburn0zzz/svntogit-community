# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=upnp-player-qt
pkgver=0.2
pkgrel=1
pkgdesc='Upnp layer build with Qt5'
url='https://gitlab.com/homeautomationqt/upnp-player-qt'
arch=(x86_64)
license=(LGPL3)
depends=(kdsoap)
makedepends=(extra-cmake-modules qt5-svg qt5-multimedia kdeclarative)
source=($pkgname-$pkgver.tar.gz::"https://gitlab.com/homeautomationqt/upnp-player-qt/repository/$pkgver/archive.tar.gz"
        upnp-player-qt-build.patch)
sha256sums=('63d0aa2dd986ea8c0cc484eeccf5fff1787ddcf85edaaf0e5c021fd6baa70179'
            '42fa449ad6284f3c29f2f2ba842409b13c0fdd59847bb5dd080c3592d4d0c042')

prepare() {
  cd $pkgname-*/
  mkdir -p build

# Fix build
  patch -p1 -i ../upnp-player-qt-build.patch
}

build() {
  cd $pkgname-*/build
  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd $pkgname-*/build
  make DESTDIR="$pkgdir" install
}
