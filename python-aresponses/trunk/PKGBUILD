# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aresponses
pkgver=2.1.1
pkgrel=1
pkgdesc="Asyncio http mocking. Similar to the responses library used for 'requests'"
url="https://github.com/circleup/aresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp' 'python-pytest-asyncio')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov')
source=("https://github.com/circleup/aresponses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f851a793db00bfee9242b858178f01a0f3addfaf99efa9448b57d570083b81c059f14424d4eca019e9147aef0632549598120c64211939e14fd19dbfa7434843')

build() {
  cd aresponses-$pkgver
  python setup.py build
}

check() {
  cd aresponses-$pkgver
  python -m pytest
}

package() {
  cd aresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
