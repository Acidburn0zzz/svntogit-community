# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aresponses
pkgver=2.1.2
pkgrel=1
pkgdesc="Asyncio http mocking. Similar to the responses library used for 'requests'"
url="https://github.com/circleup/aresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp' 'python-pytest-asyncio')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov')
source=("https://github.com/circleup/aresponses/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('015700c77f0fa80e2380d7d7413b7c3bea6eb0407f1f27aa0a511a0978ec5b608c9a01b85ebcf9e7fd9e8fed0ffce1c5ac78ef63d9c613a643451ff5fd4956bf')

build() {
  cd aresponses-$pkgver
  python setup.py build
}

check() {
  cd aresponses-$pkgver
  python -m pytest
}

package() {
  cd aresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
