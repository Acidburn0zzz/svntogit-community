# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-whitenoise
pkgname=('python-whitenoise' 'python2-whitenoise')
pkgver=4.1
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-django' 'python2-django' 'python-requests' 'python2-requests' 'python-brotli'
              'python2-brotli')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('37f9dda51face6292ec9799dd6154fb75248f9526382deaf1a100c3b00506a1c47b7b3a8c6ceb95c0505720d41bba8c7997fcf6dd2ac030c9fcefaa29bcb1729')

prepare() {
  cp -a whitenoise-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/whitenoise-$pkgver
  python setup.py build

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 setup.py build
}

check() {
  export DJANGO_SETTINGS_MODULE=tests.django_settings
  export PYTHONWARNINGS=all

  cd "$srcdir"/whitenoise-$pkgver
  python -m unittest discover

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 -m unittest discover
}

package_python-whitenoise() {
  depends=('python')

  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-whitenoise() {
  depends=('python2')

  cd whitenoise-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
