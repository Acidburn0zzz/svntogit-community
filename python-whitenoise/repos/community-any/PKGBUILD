# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-whitenoise
pkgname=('python-whitenoise' 'python2-whitenoise')
pkgver=3.3.0
pkgrel=1
pkgdesc='Radically simplified static file serving for WSGI applications'
arch=('any')
license=('MIT')
url='http://whitenoise.evans.io'
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-django' 'python2-django'
              'python-requests' 'python2-requests' 'python-brotli' 'python2-brotli')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/evansd/whitenoise/archive/v$pkgver.tar.gz")
sha512sums=('ed79f56ba41b817e002bfe872f8603e8f642217356650a1e9beb5fc89b20d153edd6fc6d4560b72bfbcd75cfcd3014c76bb3f230f123aa413d7ca8644bb59853')

prepare() {
  cp -a whitenoise-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/whitenoise-$pkgver
  python setup.py build

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 setup.py build
}

check() {
  export DJANGO_SETTINGS_MODULE=tests.django_settings

  cd "$srcdir"/whitenoise-$pkgver
  python setup.py pytest

  cd "$srcdir"/whitenoise-$pkgver-py2
  python2 setup.py pytest
}

package_python-whitenoise() {
  depends=('python')

  cd whitenoise-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-whitenoise() {
  depends=('python2')

  cd whitenoise-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
