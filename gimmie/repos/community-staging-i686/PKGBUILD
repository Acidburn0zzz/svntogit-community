# $Id: PKGBUILD 44484 2011-04-07 20:23:57Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: György Balló <ballogy@freestart.hu>

pkgname=gimmie
pkgver=0.2.8
pkgrel=16
pkgdesc="desktop organizer which allows interaction with applications, contacts, documents, etc"
arch=('i686' 'x86_64')
url="http://beatniksoftware.com/gimmie/"
license=('LGPL')
depends=('libgnomecups' 'dbus-python' 'python2-gconf' 'python-gnomeapplet'
	 'python-gnomedesktop' 'python2-libgnome' 'python-wnck' 'pyxdg'
	 'hicolor-icon-theme' 'xdg-utils')
makedepends=('findutils' 'perlxml' 'krb5')
options=(!libtool)
install=$pkgname.install
source=(http://www.beatniksoftware.com/gimmie/releases/$pkgname-$pkgver.tar.gz)
md5sums=('721b8ec80f0247e1281aeb4aa5614c2f')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' gimmie/gimmie_{threads,bar}.py

  export CFLAGS="$CFLAGS `pkg-config --cflags glib-2.0`"
  export LDFLAGS="$LDFLAGS `pkg-config --libs glib-2.0`"

  export CFLAGS="$CFLAGS `pkg-config --cflags gtk+-2.0`"
  export LDFLAGS="$LDFLAGS `pkg-config --libs gtk+-2.0`"

  export CFLAGS="$CFLAGS `pkg-config --cflags pygtk-2.0`"
  export LDFLAGS="$LDFLAGS `pkg-config --libs pygtk-2.0`"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir="/usr/lib/$pkgname" \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              PYTHON=/usr/bin/python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
