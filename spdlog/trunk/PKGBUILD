# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Luca Weiss
# Contributor: Michael Yang

pkgname=spdlog
pkgver=1.3.1
pkgrel=2
pkgdesc='Very fast, header-only/compiled, C++ logging library'
arch=('any')
url='https://github.com/gabime/spdlog'
license=('MIT')
makedepends=('cmake' 'fmt')
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/gabime/spdlog/archive/v$pkgver.tar.gz"
    rm_bundled_fmt.patch
)
sha256sums=('160845266e94db1d4922ef755637f6901266731c4cb3b30b45bf41efa0e6ab70'
            '2eee548ad3b12f48c1e6175a0af4f94d942244d4b2eb8351fccac6f53523b80c')

prepare() {
    mkdir -p build

    # Don't duplicate code from another project...
    # https://github.com/gabime/spdlog/issues/1146
    patch -d "$pkgname-$pkgver" -p1 < rm_bundled_fmt.patch
}

build() {
    cd build
    cmake ../"$pkgname-$pkgver" \
        -DSPDLOG_BUILD_BENCH=OFF \
        -DSPDLOG_BUILD_EXAMPLES=OFF \
        -DSPDLOG_FMT_EXTERNAL=ON \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

check() {
    cd build
    make test
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 ../"$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    # Prevent usage of these bundled headers
    rm -r "$pkgdir/usr/include/spdlog/fmt/bundled/"
}
