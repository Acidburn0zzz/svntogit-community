# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.5
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('x86_64')
url='https://github.com/elementary/terminal'
license=('GPL3')
depends=('appstream' 'glib2' 'gtk3' 'pango' 'vte3'
         'libgranite.so')
makedepends=('git' 'intltool' 'meson' 'vala')
groups=('pantheon')
source=("pantheon-terminal::git+https://github.com/elementary/terminal.git#tag=${pkgver}"
        'pantheon-terminal-vte0.52.patch')
sha256sums=('SKIP'
            'af30fd1c7685ed46a708b988a8844ec1bdb30ef2727a05160dfa0384b5bb76e1')

prepare() {
  cd pantheon-terminal

  patch -Np1 -i ../pantheon-terminal-vte0.52.patch
}

build() {
  arch-meson pantheon-terminal build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
