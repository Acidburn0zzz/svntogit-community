# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.4.1
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://github.com/elementary/terminal'
license=('GPL3')
depends=('desktop-file-utils' 'glib2' 'glibc' 'gtk3' 'pango' 'vte3'
         'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
groups=('pantheon')
source=("pantheon-terminal-${pkgver}.tar.gz::https://github.com/elementary/terminal/archive/${pkgver}.tar.gz")
sha256sums=('6ff5948044c392c90d5b928547136c043473d3d9c41688fa201a8e087df8b880')

prepare() {
  cd terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd terminal-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
