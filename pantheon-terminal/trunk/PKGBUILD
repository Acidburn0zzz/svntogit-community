# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-terminal
pkgver=0.4.2
pkgrel=1
pkgdesc='The Pantheon Terminal Emulator'
arch=('i686' 'x86_64')
url='https://github.com/elementary/terminal'
license=('GPL3')
depends=('glib2' 'glibc' 'gtk3' 'pango' 'vte3'
         'libgranite.so')
makedepends=('cmake' 'intltool' 'vala')
groups=('pantheon')
source=("pantheon-terminal-${pkgver}.tar.gz::https://github.com/elementary/terminal/archive/${pkgver}.tar.gz")
sha256sums=('8443c137007ff89c61b55395c99f484fbe681b47c4fbecb1608c81ea8e1d6106')

prepare() {
  cd terminal-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd terminal-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd terminal-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
