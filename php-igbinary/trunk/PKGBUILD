# Maintainer: David Runge <dvzrv@archlinux.org>

_name=igbinary
pkgbase=php-igbinary
pkgname=('php-igbinary' 'php7-igbinary')
pkgver=3.2.2
pkgrel=1
pkgdesc="A drop in replacement for the standard php serializer"
arch=('x86_64')
url="https://github.com/igbinary/igbinary"
license=('BSD')
depends=('glibc')
makedepends=('php7' 'php')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${_name}/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('d9fd460e6d62c8ca86de0e99681714ae45b2f7c66ff76279cd376582f9e8e932e8fa0ee37c25fde82390643a543fefc3ba5cbf181d5ffa31a3ab8301c092e0ff')
b2sums=('479f1b79c84b3979659a1d596f78345e6c75e9444971b547e8f1a3fe9b4aec3ba321d7a325b734f069fb1d4cb0b5c4ea5ae6586d5e50026698dc6aaa2e972bbd')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgbase-$pkgver"
  # disable the extension by default
  sed 's/extension/;extension/g' -i "$pkgbase-$pkgver/${_name}.php.ini"

  cp -av "$pkgbase-$pkgver" "${pkgname[1]}-$pkgver"
  (
    cd "${pkgbase}-$pkgver"
    phpize
  )
  (
    cd "${pkgname[1]}-$pkgver"
    phpize7
  )
}

build() {
  (
    cd "$pkgbase-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
  (
    cd "${pkgname[1]}-$pkgver"
    ./configure --prefix=/usr \
                --enable-igbinary
    make
  )
}

check() {
  (
    cd "$pkgbase-$pkgver"
    NO_INTERACTION=1 make -k test
  )
  (
    cd "${pkgname[1]}-$pkgver"
    NO_INTERACTION=1 make -k test
  )
}

package_php-igbinary() {
  depends+=('php')
  backup=("etc/php/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_php7-igbinary() {
  depends+=('php7')
  backup=("etc/php7/conf.d/${_name}.ini")

  cd "$pkgname-$pkgver"
  make INSTALL_ROOT="$pkgdir/" install
  install -vDm 644 "${_name}.php.ini" "${pkgdir}/etc/php7/conf.d/${_name}.ini"
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CREDITS,NEWS,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
