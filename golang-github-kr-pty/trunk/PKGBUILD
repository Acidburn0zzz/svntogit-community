# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-kr-pty
pkgver=1.1.3
pkgrel=1
pkgdesc='PTY interface for Go'
arch=('any')
url="https://github.com/kr/pty"
license=('MIT')
depends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kr/pty/archive/v$pkgver.tar.gz")
sha512sums=('24fbf4e59bd73e3f8ecaca6e4ffa74e94702a231c3c315c514fa786ebdd1807a1017576506c36340f2ddda33fc860ac3f7c3d6390756f5c06377bc07b40a7276')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$srcdir"/build/src/github.com/kr/pty
  cd "$srcdir"/build/src/github.com/kr/pty
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/kr/pty

  install -Dm644 pty-$pkgver/License "$pkgdir"/usr/share/licenses/$pkgname/License
}
