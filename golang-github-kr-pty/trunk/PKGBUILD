# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-kr-pty
pkgver=1.1.4
pkgrel=1
pkgdesc='PTY interface for Go'
arch=('any')
url="https://github.com/kr/pty"
license=('MIT')
depends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kr/pty/archive/v$pkgver.tar.gz")
sha512sums=('95226bdb7ebcf5b3f267f79aa4b1a21ce19fca25c1c9c4bf7acb24707aebb4888e923de7f91e44f18d61970851bfad6174a147ec09da16e5dad5cc928e7414f8')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$srcdir"/build/src/github.com/kr/pty
  cd "$srcdir"/build/src/github.com/kr/pty
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/kr
  cp -a "$srcdir"/pty-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/kr/pty

  install -Dm644 pty-$pkgver/License "$pkgdir"/usr/share/licenses/$pkgname/License
}
