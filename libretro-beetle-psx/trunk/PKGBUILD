# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=libretro-beetle-psx
pkgname=(
  libretro-beetle-psx
  libretro-beetle-psx-hw
)
pkgver=2313
pkgrel=2
pkgdesc='Sony PlayStation core'
arch=(x86_64)
url=https://github.com/libretro/beetle-psx-libretro
license=(GPL2)
groups=(libretro)
depends=(
  libretro-core-info
  zlib
)
makedepends=(
  git
  libglvnd
  mesa
  vulkan-icd-loader
)
source=(
  libretro-beetle-psx::git+https://github.com/libretro/beetle-psx-libretro.git#commit=ecdf0c3cff78a7c306ec3f25d8e5e68c70a02be6
  libretro-beetle-psx-system-libs.patch
)
sha256sums=('SKIP'
            'c92257a9ec8a77be5c9f5a3a90e268f53555bacf44336d68b338cecae855d2c4')

pkgver() {
  cd libretro-beetle-psx

  git rev-list --count HEAD
}

prepare() {
  cd libretro-beetle-psx

  patch -Np1 -i ../libretro-beetle-psx-system-libs.patch

  cd ..
  cp -r libretro-beetle-psx{,-hw}
}

build() {
  make \
    HAVE_LIGHTREC=1 \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-psx
  make \
    HAVE_HW=1 \
    HAVE_LIGHTREC=1 \
    SYSTEM_LIBCHDR=0 \
    SYSTEM_ZLIB=1 \
    -C libretro-beetle-psx-hw
}

package_libretro-beetle-psx() {
  install -Dm 644 libretro-beetle-psx/mednafen_psx_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

package_libretro-beetle-psx-hw() {
  depends+=(libgl)

  install -Dm 644 libretro-beetle-psx-hw/mednafen_psx_hw_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
