# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyterlab_widgets
pkgname=${_pipname/_/-}
pkgver=1.0.0rc1
pkgrel=1
pkgdesc="JupyterLab extension to use ipywidgets"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(jupyterlab python-ipywidgets)
makedepends=(python-setuptools python-jupyter_packaging)
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('bf8a00f37ac5e5532f84997f901fc0b5f5539527426690b2be6b1bb72f9c0e4f')

build() {
  cd $_pipname-$pkgver
  python setup.py build 
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

# These should be installed by setup.py, but they are not
  mkdir -p "$pkgdir"/usr/share/jupyter/labextensions/@jupyter-widgets/
  cp -r jupyterlab_widgets/labextension "$pkgdir"/usr/share/jupyter/labextensions/@jupyter-widgets/jupyterlab-manager

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
