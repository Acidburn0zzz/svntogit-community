# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-draw
pkgver=5.8.0.18
pkgrel=1
pkgdesc="A lightweight drawing tool for Linux Deepin"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-draw"
license=('GPL3')
depends=('deepin-picker' 'deepin-qt5integration' 'freeimage' 'libexif' 'qt5-imageformats')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-draw/archive/$pkgver.tar.gz")
sha512sums=('e8bb2be5125d40f3e5965633bef5a9c187b7b32712d165d647f55a07d1f1a8b47fa16c3d6bd8849a7e42f9d517d20e8870821227aa7933e7d4ea6cc7c49abd2e')

prepare() {
  cd deepin-draw-$pkgver
  sed -i '/include <DGraphicsView>/i #include <QFileDevice>' frame/cgraphicsview.h
  sed -i '/include <QPainter>/a #include <QMouseEvent>' widgets/ciconbutton.cpp
}

build() {
  cd deepin-draw-$pkgver

  qmake-qt5
  make
}

package() {
  cd deepin-draw-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
