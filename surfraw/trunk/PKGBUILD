# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Jeff Mickey <jeff@archlinux.org>

pkgname=surfraw
pkgver=2.2.8
pkgrel=1
pkgdesc="Shell Users' Revolutionary Front Rage Against the Web"
arch=('any')
url="http://surfraw.alioth.debian.org/"
license=('custom')
depends=('perl')
changelog=$pkgname.changelog
install=$pkgname.install
backup=('etc/xdg/surfraw/conf' 'etc/xdg/surfraw/bookmarks')
source=(http://surfraw.alioth.debian.org/dist/$pkgname-$pkgver.tar.gz)
sha256sums=('213010e9b7c8478827e8903530cf7787')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc --disable-opensearch
  make 
}

package () {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# license
  install -D -m644 COPYING ${pkgdir}/usr/share/licenses/$pkgname/COPYING 
}
sha256sums=('925075e05637e39458b00e859193aacde306aafd9a962f44f5114f81713539ec')
