# $Id$
# Maintainer: Florian Pritz <flo@xinu.at>
# Contributor: Adam Hani Schakaki (krzd) <krzd@krzd.net>
# Contributor: Det <nimetonmaili at gmail a-dot com>
# Contributor: catalyst <catalyst@xaker.ru>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: jackobw
# Contributor: Gianni Vialetto <g dot vialetto at gmail dot com>

pkgname=cairo-compmgr
pkgver=0.3.0
pkgrel=4
pkgdesc="A Cairo based composite manager"
arch=(i686 x86_64)
url="http://cairo-compmgr.tuxfamily.org/"
license=(GPL)
makedepends=(gettext gtk-doc "intltool>=0.41" gconf)
depends=("gtk2>=2.16.0" "vala>=0.11.6" libsm)
conflicts=(cairo-compmgr-git)
install=cairo-compmgr.install
options=(!libtool)
source=(http://download.tuxfamily.org/ccm/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('f8a2b31d99505ca0308f3533160d8d3b')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's!vala-1.0!libvala-0.12!' vapi/cairo-compmgr.deps configure.ac
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  mkdir -p "$pkgdir/usr/share/gconf/schemas/"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/cairo-compmgr.schemas" "$pkgdir/etc/gconf/schemas/*.schemas"
  rm -rf "$pkgdir/etc/gconf/schemas/"
}
