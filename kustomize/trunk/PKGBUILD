# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=kustomize
pkgver=3.8.1
pkgrel=1
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=('x86_64')
url="https://github.com/kubernetes-sigs/kustomize"
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("kustomize-${pkgver}.tar.gz::https://github.com/kubernetes-sigs/kustomize/archive/kustomize/v${pkgver}.tar.gz")
sha512sums=('715c8e6ce90004c4eadb9d620149391ef2f098f8a1f106d9a58345789b3c79753e63ddfd74fcff1bb2ac3e4e391c450b9dd3cd8a829abc5c20be4e61fa6508f4')

build(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -X sigs.k8s.io/kustomize/api/provenance.version=${pkgver} \
      -X sigs.k8s.io/kustomize/api/provenance.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
      -extldflags ${LDFLAGS}" \
    .
}

check(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  go test ./...
}

package(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  install -D kustomize "${pkgdir}/usr/bin/kustomize"
}
