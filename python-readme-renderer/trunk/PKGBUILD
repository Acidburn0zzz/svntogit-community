# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-readme-renderer
pkgname=(python-readme-renderer python2-readme-renderer)
pkgver=24.0
pkgrel=4
pkgdesc="Safely render long_description/README files in Warehouse"
url="https://github.com/pypa/readme_renderer"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-bleach' 'python2-bleach'
             'python-cmarkgfm' 'python2-cmarkgfm' 'python-docutils' 'python2-docutils'
             'python-pygments' 'python2-pygments' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pypa/readme_renderer/archive/$pkgver.tar.gz")
sha512sums=('d5e98215469f2335da9ee52d29e3d7e12d2b5a481274587ae8c0286a807004749ef19f57aa4475cd6e56e782bc2c6926fcc2c9bc4250aa2b6549700bf29f989b')

build() {
  cd readme_renderer-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd readme_renderer-$pkgver
  python setup.py pytest || warning "Tests failed"
  python2 setup.py pytest || warning "Tests failed"
}

package_python-readme-renderer() {
  depends=('python-bleach' 'python-docutils' 'python-pygments' 'python-six')
  optdepends=('python-cmarkgfm: for Markdown support')

  cd readme_renderer-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-readme-renderer() {
  depends=('python2-bleach' 'python2-docutils' 'python2-pygments' 'python2-six')
  optdepends=('python2-cmarkgfm: for Markdown support')

  cd readme_renderer-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
