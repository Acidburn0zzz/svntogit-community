# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=ruamel.yaml
pkgname=python2-${_pkg}
pkgver=0.16.5
pkgrel=1
pkgdesc="Cross-section and time series data analysis toolkit"
arch=(any)
url="https://bitbucket.org/ruamel/yaml"
license=(MIT)
depends=(python2)
makedepends=(python2-setuptools)
source=(https://files.pythonhosted.org/packages/source/r/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('412a6f5cfdc0525dee6a27c08f5415c7fd832a7afcb7a0ed7319628aed23d408')

prepare() {
  cd ${_pkg}-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  cd ${_pkg}-${pkgver}
  python2 setup.py build
}

package() {
  cd ${_pkg}-${pkgver}
  # https://bitbucket.org/ruamel/yaml/issues/114/setuppy-requirement-for-pip-prevents
  RUAMEL_NO_PIP_INSTALL_CHECK=1 python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
