# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-gopkg-check.v1
pkgver=0.0.20180629
_commit=788fd78401277ebd861206a03c884797c6ec5541
pkgrel=1
pkgdesc='Rich testing for the Go language'
arch=('any')
url="https://github.com/go-check/check/tree/v1"
license=('BSD')
depends=('go-pie' 'golang-github-kr-pretty')
source=("$pkgname-$_commit.tar.gz::https://github.com/go-check/check/archive/$_commit.tar.gz")
sha512sums=('3b0a79ee3dd9e4c321a549ff8b08788ea1bfad34c29cdf42976e955716f2139b3ba7c40e8bf36d8dac01711d590fb2ab26767a06222fe4645856a401219a481b')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/gopkg.in
  cp -a "$srcdir"/check-$_commit "$srcdir"/build/src/gopkg.in/check.v1
  cd "$srcdir"/build/src/gopkg.in/check.v1
  go test -v $(go list ./... | grep -v vendor)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/gopkg.in
  cp -a "$srcdir"/check-$_commit "$pkgdir"/usr/share/gocode/src/gopkg.in/check.v1

  install -Dm644 check-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
