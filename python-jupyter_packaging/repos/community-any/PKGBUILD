# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=jupyter-packaging
pkgname=python-${_pipname/-/_}
pkgver=0.7.9
pkgrel=1
pkgdesc="Jupyter Packaging Utilities"
arch=(any)
url="https://jupyter.org/"
license=(custom)
depends=(python-packaging)
makedepends=(python-setuptools)
source=(https://pypi.io/packages/source/j/$_pipname/$_pipname-$pkgver.tar.gz)
sha256sums=('2ded75723372e5ced2e1be1ee85506c88895518892f20eacc3b3a344cf59ce3a')

build() {
  cd $_pipname-$pkgver
  python setup.py build 
}

package() {
  cd $_pipname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
