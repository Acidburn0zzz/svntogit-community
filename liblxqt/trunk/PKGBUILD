# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=liblxqt
pkgver=0.11.0
pkgrel=2
pkgdesc="Common base library for LXQt components."
arch=("i686" "x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("kwindowsystem" "libqtxdg" "libxss")
makedepends=("cmake" "qt5-tools" "git" "libxss")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"bc553bf53f3083886b5456a3f22e227f68ea945d8ee0dd31dd24f765bf879fe2"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
