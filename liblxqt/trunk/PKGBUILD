# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=liblxqt
pkgver=0.14.1
pkgrel=1
pkgdesc="Common base library for LXQt components."
arch=("x86_64")
url="https://lxqt.org"
license=("LGPL2.1")
depends=("kwindowsystem" "libqtxdg" "libxss" "polkit-qt5")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	qt5.14.diff
)
sha256sums=('1aa7325654d20d3060a0ef0ffc27febfd58ee66704ee690727dc1a77ad1069bd'
            'SKIP'
            'dbde480fe0cd9393751636ec39a2282301eb3759bb0ec076d81b18d81e0fddb2')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

prepare() {
	cd $pkgname-$pkgver
	# Backport of https://github.com/lxqt/liblxqt/pull/243
	patch -Np1 -i ../qt5.14.diff
}

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
