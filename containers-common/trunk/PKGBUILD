# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=containers-common
pkgver=0.47.3
pkgrel=1
epoch=1
_image_pkgver=5.19.1
_podman_pkgver=4.0.0
_shortnames_pkgver=2022.02.08
_skopeo_pkgver=1.6.0
_storage_pkgver=1.38.2
pkgdesc="Configuration files and manpages for containers"
arch=(any)
url="https://github.com/containers"
license=(Apache)
depends=(container-network-stack)
makedepends=(go-md2man)
checkdepends=(ripgrep)
backup=(
  etc/containers/containers.conf
  etc/containers/mounts.conf
  etc/containers/policy.json
  etc/containers/registries.conf
  etc/containers/seccomp.json
  etc/containers/storage.conf
)
# configuration override directories need to exist
options=(emptydirs)
source=(
  common-$pkgver.tar.gz::https://github.com/containers/common/archive/v$pkgver.tar.gz
  image-$_image_pkgver.tar.gz::https://github.com/containers/image/archive/v$_image_pkgver.tar.gz
  podman-$_podman_pkgver.tar.gz::https://github.com/containers/podman/archive/v$_podman_pkgver.tar.gz
  skopeo-$_skopeo_pkgver.tar.gz::https://github.com/containers/skopeo/archive/v$_skopeo_pkgver.tar.gz
  storage-$_storage_pkgver.tar.gz::https://github.com/containers/storage/archive/v$_storage_pkgver.tar.gz
  shortnames-$_shortnames_pkgver.tar.gz::https://github.com/containers/shortnames/archive/refs/tags/v$_shortnames_pkgver.tar.gz
  mounts.conf
)
sha512sums=('b34620d72394ca7170e8bfe9575f4c70ba43c7fc62f4bac1c6426db5fbc7f0dbb5dff6289de24a1aff0161b8e3e4fad8570cf7871bf33eec2d31bb8638dcaa0c'
            '8e43e74ef5367734dad29b0ed3370ec2c0741defc9bf45925c8ebded3ae5a38b7c3305abab277b642bd7e6c861fef4197dae1baaf68e9112a4a2a39b651bd393'
            '2e25f303d143bc4e265f0f8998ea1c4af3d6da8b35452cf6b27a4eb204805a9b5f75f4b6430921b42c80b76a6b5c9cf35a732c265727a11261fca25cc96f57e7'
            '13a3660780ce43e31a9840811aaa7626f9d0de9999e52c469a2a58b4bf96bd5e4d67e0f0556ac2baef14c5a662afae1f2d407b260c8833d573326791a2193e48'
            '98caa95e4456e60ef676e4d9717e5f852cfa21ffe45980f63fab703bc7c369fdae14d33102dcb023908d250c3eba6f121c270203c3abd29fdfcb7e2768319c50'
            'd0f72ad6f86cc1bcb0f02d9c29d3a982c541679098e417410c8f1a3df42550753e4f491efdec09dc02fe3ab4e3f5d8971c8ab9e964293e6b4e1f1261191b3501'
            '11fa515bbb0686d2b49c4fd2ab35348cb19f9c6780d6eb951a33b07ed7b7c72a676627f36e8c74e1a2d15e306d4537178f0e127fd3490f6131d078e56b46d5e1')
b2sums=('7205bb0f52cea55bcf4ecfd1a31b150eb6b095ed749bca01542c6452d52b81c6b767ad20c6e9ccc120d43fa24c4044a189055aa2e54e76baf09a8a52041af25c'
        'b704c273ff3a31a9c2de55382112a0df6d79d76e2e7464844e079f7dd0fd7823aa25398386f6a59e4c23c90fa6549966011ad5f982e210e673bda62dde25b9e6'
        '2d68b95206ef59873c2a5c434aa3901c33a3526f840805ab42ac4dad4ed5861824a6b2142d02b500e087d56f4da3255c2b156af4d835a6427c2ebfae34278a75'
        'd2898d8f80a04340e3174179e2397f7b5e66ed50ec8c11919adf1b0620c74a3a69f8be548c5c3bf081735e1a86dc5ae3390d058ef64fc4b73d8f01074ef4a210'
        '466641eb0c810da0e6b5fe9adc47eadbc66cedb57326903fe4cbf917b12d4e937131dcf4ff4d33d0b2372384f754f471fd532aac91809a5481d74be0ce6ffe05'
        'da94f0fe514031d3f02cbc3524fb8ba2ffab1e53949bea009768663f487301c3029a026abff23771ebf73ee94868490cf1758ebee2646c95dc5406111fb12208'
        '2f4b0af3271103362a898e7fcc3ec05f06755902ad664ac3107bb8debb8b2ac0d50de311d5fc651279a817a56e3ff05864a7e77c0d8fc628ff7411bfb98c9b69')

prepare() {
  sed -r 's/(GOMD2MAN = ).*/\1 go-md2man/' -i storage-$_storage_pkgver/docs/Makefile
}

build() {
  (
    cd common-$pkgver/docs
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd image-$_image_pkgver/docs
    mkdir -vp man5
    mv -v *.5.md man5/
    for _man_page in *.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}.1
    done
    for _man_page in man5/*.md; do
      go-md2man -in $_man_page -out ${_man_page//.md}
    done
  )
  (
    cd podman-$_podman_pkgver
    go-md2man -in pkg/hooks/docs/oci-hooks.5.md -out oci-hooks.5
  )
  (
    cd storage-$_storage_pkgver
    make -C docs
  )
}

check() {
  local _podman_common_ver
  _podman_common_ver="$(rg "github.com/containers/common" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_image_ver="$(rg "github.com/containers/image" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"
  _podman_storage_ver="$(rg "github.com/containers/storage" podman-$_podman_pkgver/go.mod | cut -d ' ' -f2 | sed 's/v//')"

  printf "Common version in podman: %s\n" $_podman_common_ver
  printf "Image version in podman: %s\n" $_podman_image_ver
  printf "Storage version in podman: %s\n" $_podman_storage_ver

  # NOTE: we are comparing the major.minor version of containers/common required by podman with the one of containers/common that we are trying to build
  # https://github.com/containers/common/issues/923
  if (( $(vercmp "${_podman_common_ver%.*}" "${pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/common in version %s but we are trying to build version %s\n" $_podman_pkgver ${_podman_common_ver%.*} ${pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_image_ver%.*}" "${_image_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/image in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_image_ver%.*} ${_image_pkgver%.*}
    exit 1
  fi
  if (( $(vercmp "${_podman_storage_ver%.*}" "${_storage_pkgver%.*}") != 0 )); then
    printf "The podman package in version %s requires containers/storage in version %s but we are trying to use version %s\n" $_podman_pkgver ${_podman_storage_ver%.*} ${_storage_pkgver%.*}
    exit 1
  fi
}

package() {
  # directories
  install -vdm 755 "$pkgdir/etc/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/etc/containers/registries.conf.d/"
  install -vdm 755 "$pkgdir/usr/share/containers/oci/hooks.d/"
  install -vdm 755 "$pkgdir/var/lib/containers/"

  # configs
  install -vDm 644 mounts.conf -t "$pkgdir/etc/containers/"

  (
    cd "common-$pkgver"
    # configs
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/config/containers.conf -t "$pkgdir/usr/share/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/etc/containers/"
    install -vDm 644 pkg/seccomp/seccomp.json -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd image-$_image_pkgver
    # configs
    install -vDm 644 registries.conf -t "$pkgdir/etc/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/man5/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd podman-$_podman_pkgver
    install -vDm 644 *.5 -t "$pkgdir/usr/share/man/man5/"
  )
  (
    cd shortnames-$_shortnames_pkgver
    install -vDm 644 shortnames.conf "$pkgdir/etc/containers/registries.conf.d/00-shortnames.conf"
  )
  (
    cd skopeo-$_skopeo_pkgver
    # configs
    install -vDm 644 default-policy.json "$pkgdir/etc/containers/policy.json"
    install -vDm 644 default.yaml -t "$pkgdir/etc/containers/registries.d/"
  )
  (
    cd storage-$_storage_pkgver
    # configs
    install -vDm 644 storage.conf -t "$pkgdir/etc/containers/"
    install -vDm 644 storage.conf -t "$pkgdir/usr/share/containers/"
    # man pages
    install -vDm 644 docs/*.1 -t "$pkgdir/usr/share/man/man1/"
    install -vDm 644 docs/*.5 -t "$pkgdir/usr/share/man/man5/"
  )
}
