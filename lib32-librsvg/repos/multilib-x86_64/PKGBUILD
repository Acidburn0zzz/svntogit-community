# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-librsvg
pkgver=2.40.13
pkgrel=1
pkgdesc='A SVG viewing library (32-bit)'
arch=('x86_64')
url='https://live.gnome.org/LibRsvg'
license=('LGPL')
depends=('lib32-gdk-pixbuf2' 'lib32-libcroco' 'lib32-pango' 'librsvg')
makedepends=('gcc-multilib' 'gobject-introspection' 'intltool' 'python2' 'vala')
install='librsvg.install'
source=("https://download.gnome.org/sources/librsvg/${pkgver:0:4}/librsvg-${pkgver}.tar.xz"
        bgo759084.patch)
sha256sums=('4d6ea93ec05f5dabe7262d711d246a0a99b2311e215360dd3dcabd6afe3b9804'
            'd6cf5b318a89fb002a520c830c0666f0cb905d4d38322b2c41296fdd36d60b93')

prepare() {
  cd librsvg-$pkgver
  patch -Np1 -i ../bgo759084.patch
}

build() {
  cd librsvg-${pkgver}

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --enable-vala \
    --disable-static

  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' -e 's/    if test "$export_dynamic" = yes && test -n "$export_dynamic_flag_spec"; then/      func_append compile_command " -Wl,-O1,--as-needed"\n      func_append finalize_command " -Wl,-O1,--as-needed"\n\0/' libtool

  make
}

check() {
  cd librsvg-$pkgver
  make check
}

package() {
  cd librsvg-${pkgver}

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/{bin,include,share}}
}

# vim: ts=2 sw=2 et:
