# $Id$
# Maintainer : Daniel Wallace <danielwallace at gtmanfred dot com>

_pkgname=sh
pkgname=("python-$_pkgname" "python2-$_pkgname")
pkgver=1.07
pkgrel=3
pkgdesc="A full-fledged subprocess interface for Python that allows you to call any program as if it were a function"
arch=('any')
url='http://pypi.python.org/pypi/sh'
license=("MIT")
makedepends=('python-distribute' 'python2-distribute')
source=(
  "http://pypi.python.org/packages/source/s/sh/$_pkgname-$pkgver.tar.gz"
)

md5sums=('68edad68bd108df713f1424bc65468e9')

build() {
  cd "$srcdir"
  cp -r "$_pkgname-$pkgver" "python2-$_pkgname-$pkgver"

  cd "$_pkgname-$pkgver"
  python setup.py build

  cd "../python2-$_pkgname-$pkgver"
  python2 setup.py build
}


package_python-sh() {
  depends=('python')
  cd "$srcdir/$_pkgname-$pkgver"
  python setup.py install --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/python-sh/LICENSE"
}

package_python2-sh() {
  depends=('python2')
  cd "$srcdir/python2-$_pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/python2-sh/LICENSE"
}
