# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Stefan Tatschner <stefan@rumpelsepp.org>
# Contributor: katt <magunasu.b97@gmail.com>

pkgname=yt-dlp
pkgver=2021.11.10.1
pkgrel=1
pkgdesc='A youtube-dl fork with additional features and fixes'
arch=(any)
url='https://github.com/yt-dlp/yt-dlp'
license=(Unlicense)
depends=(python-mutagen python-pycryptodomex python-websockets)
makedepends=(python-setuptools pandoc)
optdepends=('ffmpeg: for video post-processing'
            'rtmpdump: for rtmp streams support'
            'atomicparsley: for embedding thumbnails into m4a files'
            'aria2: for using aria2 as external downloader')
source=(https://github.com/yt-dlp/yt-dlp/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('b0bfe3a1e7c3a9a1e24219c36eaf8604328b29cc3560387f75506f45b78ea695')

build() {
  cd $pkgname-$pkgver
  make pypi-files
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --skip-build --optimize=1
}
