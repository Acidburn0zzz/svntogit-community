# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Arkham <arkham@archlinux.us>
# Contributor: dirty_d <andrew2085@gmail.com>

pkgname=joyutils
pkgver=1.7.1
pkgrel=1
pkgdesc='jscal, jstest, and jsattach utilities for the Linux joystick driver'
arch=(x86_64)
license=(GPL)
url='https://packages.debian.org/sid/joystick'
conflicts=(linuxconsole)
depends=(awk sdl2)
source=("https://deb.debian.org/debian/pool/main/j/joystick/joystick_$pkgver.orig.tar.bz2")
sha256sums=('bd4d4b7e37da02fc67e47ddf20b6f1243c0a7af7b02b918d5e72138ea8727547')

prepare() {
  ln -sf "linuxconsoletools-$pkgver" $pkgname
  sed -i 's,IR)/lib,IR)$(PREFIX)/lib,g' $pkgname/utils/Makefile
}

build() {
  make -C $pkgname
}

package() {
  DESTDIR="$pkgdir" PREFIX=/usr make -C $pkgname install
}
