# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-terminal
_pkgname=Terminal
pkgver=46
pkgrel=1
pkgdesc="Terminal emulator for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Terminal"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3' 'vte3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('7c9e4456802951cfc04bd1b183c2bc1927ba56e687c82be639881e53f8f2f763')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' helpbutton.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
