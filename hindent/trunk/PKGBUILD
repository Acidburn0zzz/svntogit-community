# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hindent
pkgver=4.6.4
pkgrel=4
pkgdesc="Extensible Haskell pretty printer"
url="http://www.github.com/chrisdone/hindent"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=8.0.1" "haskell-descriptive" "haskell-src-exts" "haskell-monad-loops"
             "haskell-mtl" "haskell-text")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz"
        hse-1.18.patch)
sha256sums=('26fc1498705b8a64b03eb5b699ba6229955273d91a49a01c3c2b58436c8e4dcf'
            '4b235c97f76925918b55abce82413771619665cda018248b0704d235757ccbc8')

prepare() {
    cd $pkgname-$pkgver
    patch -p1 -i ../hse-1.18.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname"
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}

    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
