# Maintainer: David Runge <dvzrv@archlinux.org>

_name=dbus_next
pkgname=python-dbus-next
pkgver=0.2.2
pkgrel=2
pkgdesc="The next great DBus library for Python with asyncio support"
arch=('any')
url="https://github.com/altdesktop/python-dbus-next"
license=('MIT')
depends=('dbus' 'glib2' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-timeout')
# test files missing in pypi sdist tarball: https://github.com/altdesktop/python-dbus-next/issues/94
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/altdesktop/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('8efb3194f616fdb59ca5d0484ba3ed771d2d9dc0ad7114b7f46729118188c460829805174509730f3ec32573d6daa4aaa7f6e3e71bcb9cb7f687fb48ac7b276a')
b2sums=('3ecfc94b5431a836637f58aeb38eba5ac8f1b374a2072b9dd7d50035812d06074ef842ebfa7f928192aa5a5e468ceec9a6858f88b7d8afdcaa1a60aa96fb9d10')

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"

  # integration tests are not separate: https://github.com/altdesktop/python-dbus-next/issues/95
  pytest -v \
    --ignore "test/test_aio_low_level.py" \
    --ignore "test/test_big_message.py" \
    --ignore "test/test_disconnect.py" \
    --ignore "test/test_request_name.py" \
    --ignore "test/test_tcp_address.py" \
    --ignore "test/client/test_methods.py" \
    --ignore "test/client/test_properties.py" \
    --ignore "test/client/test_signals.py" \
    --ignore "test/service/test_export.py" \
    --ignore "test/service/test_methods.py" \
    --ignore "test/service/test_properties.py" \
    --ignore "test/service/test_signals.py" \
    --ignore "test/service/test_standard_interfaces.py" \
    -k "not test_sending_file_descriptor_low_level and not test_high_level_service_fd_passing and not test_sending_file_descriptor_with_proxy"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
