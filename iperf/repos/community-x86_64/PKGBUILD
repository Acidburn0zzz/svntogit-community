# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=iperf
pkgver=2.0.5
pkgrel=5
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('i686' 'x86_64')
license=('custom')
url='http://iperf.sourceforge.net'
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/iperf/iperf-$pkgver.tar.gz"
        'iperf.rc'
        'iperf.service'
        'iperf.conf'
        'iperf-udp.conf'
        'client.cpp.patch')
md5sums=('44b5536b67719f4250faed632a3cd016'
         'e5ec7e5d4389f8f3dfd9bc31a9763b3b'
         'b0c1165e21fb686e449c6bab9852be31'
         'b1ceff406ec6bf040c7917aa6b604aff'
         '502b56e39fd61daeee6906a8e1d2c8a6'
         '82ce63c87cc8bb2f0f94069857ac14a9')

build() {
  cd $pkgname-$pkgver
  # fix FS#25159
  patch -Np1 -i "$srcdir/client.cpp.patch"
  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-multicast \
              --enable-threads
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
  popd
  # install initscripts
  install -Dm755 iperf.rc "$pkgdir/etc/rc.d/iperf"
  install -Dm755 iperf.rc "$pkgdir/etc/rc.d/iperf-udp"
  install -Dm644 iperf.conf "$pkgdir/etc/conf.d/iperf"
  install -Dm644 iperf-udp.conf "$pkgdir/etc/conf.d/iperf-udp"
  install -Dm644 iperf.service "$pkgdir/usr/lib/systemd/system/iperf.service"
}

# vim:set ts=2 sw=2 ft=sh et:
