# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=iperf
pkgver=2.0.5
pkgrel=5
pkgdesc='Tool to measure maximum TCP bandwidth'
arch=('i686' 'x86_64')
license=('custom')
url='http://iperf.sourceforge.net'
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/iperf/iperf-$pkgver.tar.gz"
        'iperf.rc'
        'iperf.conf'
        'iperf.service'
        'client.cpp.patch')
md5sums=('44b5536b67719f4250faed632a3cd016'
         'e99dc0d41eb800970390dd149d70b544'
         'c75cb18434c6bf19fdc89b9caa766d7d'
         'b0c1165e21fb686e449c6bab9852be31'
         '82ce63c87cc8bb2f0f94069857ac14a9')

build() {
  cd $pkgname-$pkgver
  # fix FS#25159
  patch -Np1 -i "$srcdir/client.cpp.patch"
  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-multicast \
              --enable-threads
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
  popd
  # install initscripts
  install -Dm755 iperf.rc "$pkgdir/etc/rc.d/iperf"
  install -Dm644 iperf.conf "$pkgdir/etc/conf.d/iperf"
  install -Dm644 iperf.service "$pkgdir/usr/lib/systemd/system/iperf.service"
}

# vim:set ts=2 sw=2 ft=sh et:
