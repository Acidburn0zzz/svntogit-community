$Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: feuri

pkgname=dunst
pkgver=0.4.0
pkgrel=1
pkgdesc='Dmenu-ish notification system'
arch=('i686' 'x86_64')
url="http://knopwob.github.com/dunst"
license=(MIT)
depends=(dbus-core libxinerama libxft libxss libxdg-basedir)
makedepends=('git' 'perl')
optdepends=('libnotify: dunst is able to listen to notifications from libnotify')
provides=(notification-daemon)
source=("${pkgname}-${pkgver}.tar.bz2::http://www.knopwob.org/public/dunst-release/$pkgname-$pkgver.tar.bz2")
md5sums=('615b673011222f6ab6f5df28def329a9')

build() {
	cd "${srcdir}/$pkgname-$pkgver"
	
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd "${srcdir}/$pkgname-$pkgver"

	make DESTDIR="${pkgdir}" PREFIX=/usr install

	install -Dm644 "${srcdir}/$pkgname-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
# vim:set ts=2 sw=2 et:
