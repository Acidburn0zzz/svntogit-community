# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eric Biggers <ebiggers3 at gmail dot com>

pkgname=fscrypt
pkgver=0.2.7
pkgrel=1
pkgdesc='A tool for managing Linux filesystem encryption'
arch=('x86_64')
url='https://github.com/google/fscrypt'
license=('Apache')
makedepends=('go-pie')
depends=('pam')
source=("fscrypt-$pkgver.zip::https://github.com/google/fscrypt/archive/v$pkgver.zip"
        "pam_config")
sha256sums=('06bed0916f2374251aa3eb7337a639b4264a8a73fb490df3a6866694b8547c30'
            'ae6ceaefc6d936c95a9b7a3f925111ffb946e6fd0152373247f1d40132f05aef')

build() {
  cd "fscrypt-$pkgver"
  BUILDDATE="`date --utc --date="@${SOURCE_DATE_EPOCH:-$(date +%s)}"`" make
}

package() {
  cd "fscrypt-$pkgver"
  make PREFIX="${pkgdir}/usr" install
  install -Dm644 ../pam_config "${pkgdir}/etc/pam.d/fscrypt"
  install -Dm644 -t "$pkgdir/usr/share/licenses/fscrypt/" LICENSE

  # Remove Ubuntu-specific PAM file
  rm -rf "$pkgdir"/usr/share/pam-configs/
}
