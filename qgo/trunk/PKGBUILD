# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
pkgname=qgo
pkgver=1.5.4
pkgrel=5
pkgdesc="Go client and full featured SGF editor"
arch=('x86_64' 'i686')
url="http://qgo.sourceforge.net/"
license=('GPL')
depends=('qt3' 'libsm')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver-r3.tar.gz"
        'gcc43.patch'
        'gcc45.patch')
md5sums=('f8f9a6288b7854932bb2666a58631988'
         '1e242ff9c3b46bcebfda0d7ae3aef2fa'
         '58de47198a50dedb029acbe28bd3bb1b')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  . /etc/profile.d/qt3.sh
  patch -Np1 -i "$srcdir/gcc43.patch"
  patch -Np1 -i "$srcdir/gcc45.patch"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
