# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=vdirsyncer
pkgver=0.16.8
pkgrel=1
pkgdesc="Synchronize CalDAV and CardDAV."
arch=('any')
url="https://vdirsyncer.pimutils.org/en/stable/"
license=('BSD')
depends=('python-atomicwrites' 'python-click' 'python-click-log'
'python-click-threading' 'python-requests' 'python-requests-toolbelt'
'python-setuptools')
makedepends=('git' 'python-setuptools-scm')
optdepends=('python-requests-oauthlib: Google support'
            'python-etesync: Etesync support'
            'python-django: Etesync support')
checkdepends=('python-hypothesis' 'python-pytest' 'python-pytest-localserver'
'python-pytest-subtesthack')
source=("git+https://github.com/pimutils/vdirsyncer.git#tag=${pkgver}?signed"
        "${pkgname}-0.16.8-test_verbosity.patch")
sha512sums=('SKIP'
            '4c23669b161f4fa6f6db33648c5e47791ceaebd97e350a19ba0a1ac6eb3a4e1e753d6887bf22948d79c6e71e5685cab0d9a8a9fcb8e4b54445de2e378e01e251')
validpgpkeys=('951082781CA308E4A529DEFD6F9FFF122B98C0CD') # Hugo Osvaldo Barrera <hugo@barrera.io>

prepare() {
  cd "${pkgname}"
  # fix failing test
  # https://github.com/pimutils/vdirsyncer/issues/828
  patch -Np1 -i ../"${pkgname}-0.16.8-test_verbosity.patch"
}

build() {
  cd "${pkgname}"
  python setup.py build
}

check() {
  cd "${pkgname}"
  make DETERMINISTIC_TESTS=true test
}

package() {
  cd "${pkgname}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # systemd
  install -vDm 644 "contrib/${pkgname}."{service,timer} \
    -t "${pkgdir}/usr/lib/systemd/user/"
  # docs
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

