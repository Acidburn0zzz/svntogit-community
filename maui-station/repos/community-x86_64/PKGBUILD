# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=maui-station
_pkgname=station
pkgver=1.2.1
pkgrel=1
pkgdesc="Convergent terminal emulator"
url="https://mauikit.org/"
arch=(x86_64)
license=(GPL3)
depends=(mauikit qt5-svg)
makedepends=(extra-cmake-modules knotifications)
conflicts=(qmltermwidget)
groups=(maui)
source=(https://download.kde.org/stable/maui/$_pkgname/$pkgver/$_pkgname-$pkgver.tar.xz
        https://github.com/Swordfish90/qmltermwidget/archive/v0.1.0/qmltermwidget-0.1.0.tar.gz)
sha256sums=('4656e72aa9e40fa1500ab08c422d4e026fba9c6442f3133def911b5bd1cab5cb'
            '127f529a96cf1c96ff201c79458cff67f497b1820b04b0b30cb473423ccb30f9')

build() {
# Bundle qmltermwidget 0.1, see https://gitlab.com/nitrux/mauikit/maui-bug-tracker/-/issues/2
  (
  cd qmltermwidget-0.1.0
  qmake
  make
  )

  cmake -B build -S $_pkgname-$pkgver
  cmake --build build
}

package() {
  INSTALL_ROOT="$pkgdir" make install -C qmltermwidget-0.1.0
  DESTDIR="$pkgdir" cmake --install build
}
