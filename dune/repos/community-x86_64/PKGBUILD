# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Jakob Gahde <j5lx@fmail.co.uk>

pkgname=dune
pkgver=1.4.0
pkgrel=2
pkgdesc="A composable build system for OCaml (formerly jbuilder)"
arch=('x86_64')
url="https://github.com/ocaml/dune"
license=('Apache')
depends=('glibc' 'ocaml' 'ocaml-findlib')
provides=('jbuilder')
conflicts=('jbuilder')
replaces=('jbuilder')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('d71b484c602160c3a20453aae10ac4562878a5a8b43a8202b0f4a0f8c2648b4c')

build() {
    cd ${pkgname}-${pkgver}
    make release
}

# Tests requires a bunch of (currently) unpackaged dependencies
#check() {
#    cd ${pkgname}-${pkgver/b/-beta}
#    make test
#}

package() {
    cd ${pkgname}-${pkgver}

    # Work around the install command
    export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
    install -dm755 ${OCAMLFIND_DESTDIR}
    make INSTALL_ARGS="--prefix='${pkgdir}'/usr --libdir='${OCAMLFIND_DESTDIR}'" install

    # Fix doc and man install
    rm -r "${pkgdir}"/usr/doc
    install -dm755 "${pkgdir}"/usr/share
    mv "${pkgdir}"/usr/{man,share/}
    # FS#60288
    install -dm755 "${pkgdir}"/usr/share/man/man{1,5}
    mv "${pkgdir}"/usr/share/man/{*.1,man1}
    mv "${pkgdir}"/usr/share/man/{dune-config.5,man5}
}
