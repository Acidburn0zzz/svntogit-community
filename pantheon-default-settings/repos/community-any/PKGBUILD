# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-default-settings
pkgver=6.0.0
pkgrel=1
pkgdesc='Default settings for Pantheon'
arch=(any)
url=https://github.com/elementary/default-settings.git
license=(GPL3)
groups=(pantheon)
depends=(
  accountsservice
  dbus
  dconf
  polkit
)
makedepends=(
  git
  meson
)
_tag=cd843651b3ca56114e34ef7d9b38d5bf4291cf87
source=(pantheon-default-settings::git+https://github.com/elementary/default-settings.git#tag=${_tag})
b2sums=(SKIP)

pkgver() {
  cd pantheon-default-settings
  git describe --tags
}

build() {
  arch-meson pantheon-default-settings build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
