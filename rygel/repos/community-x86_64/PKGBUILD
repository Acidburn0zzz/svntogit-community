# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.16.3
pkgrel=1
pkgdesc="UPnP AV MediaServer and MediaRenderer that allows you to easily share audio, video and pictures, and control of media player on your home network"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Rygel"
license=('LGPL')
depends=('gupnp-av' 'gupnp-dlna' 'libgee' 'gtk3' 'libunistring' 'libtracker-sparql' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala' 'intltool')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'tracker: Share indexed media files')
backup=('etc/rygel.conf')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('a0325afabb6a91cbe958864183adce80ad88bcaa0fc8f8ed3f434ee0d09935bf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-mediathek-plugin --enable-gst-launch-plugin
  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  make check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
