# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=rygel
pkgver=0.16.1
pkgrel=1
pkgdesc="UPnP AV MediaServer and MediaRenderer that allows you to easily share audio, video and pictures, and control of media player on your home network"
arch=('i686' 'x86_64')
url="http://live.gnome.org/Rygel"
license=('LGPL')
depends=('gupnp-av' 'gupnp-dlna' 'libgee' 'gtk3' 'libunistring' 'libtracker-sparql' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('vala' 'intltool')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs'
            'tracker: Share indexed media files')
backup=('etc/rygel.conf')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('c8ffc0492c60b1c66f13f3f72cbee622307225d7e4cb20b43e299601463cd28d')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static --enable-mediathek-plugin --enable-gst-launch-plugin
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
