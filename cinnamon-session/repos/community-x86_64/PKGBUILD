# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=2.0.0
pkgrel=2
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(cinnamon-desktop upower)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
install=cinnamon-session.install
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        lightdm_userswitch.patch
        remove_sessionmigration.patch
        timeout.patch)
sha256sums=('d1eba64a5484f086f257ee75799d12afd77373fbdb0ced8d3ae48eadcd02a878'
            '20e42cbb5504f67b1da5e1867a220629bc2fe1699cfd52be177a557c4096440b'
            'f3c0bc1a3debfaa686230f19ba371b08dd4645064d90ead0c34f465c7d416736'
            'ef421a14814c7858490b3a806568ab4ec8a0dc21d390e94f801771c1261bb24a')

prepare() {
  cd $pkgname-$pkgver

  # Add support for lightdm user switching
  patch -Np1 -i ../lightdm_userswitch.patch

  # Do not try to execute session-migration script (it's specific to Ubuntu)
  patch -Np1 -i ../remove_sessionmigration.patch

  # Increase timeout, for slow machines
  patch -Np1 -i ../timeout.patch
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --libexecdir=/usr/lib/cinnamon-session \
      --disable-schemas-compile --enable-systemd --disable-gconf
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
