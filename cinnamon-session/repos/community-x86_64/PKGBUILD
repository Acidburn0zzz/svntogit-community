# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@antergos.com>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=cinnamon-session
pkgver=3.2.0
pkgrel=2
pkgdesc="The Cinnamon Session Handler"
arch=(i686 x86_64)
license=(GPL LGPL)
depends=(dbus-glib cinnamon-desktop libsm libcanberra)
makedepends=(intltool mesa json-glib xtrans gnome-common)
options=('!emptydirs')
url="https://github.com/linuxmint/cinnamon-session"
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/linuxmint/cinnamon-session/archive/${pkgver}.tar.gz
        blacklist_g-s-d.patch)
sha256sums=('d7c3d951ae0dddef96941c9f08f028a099443888db41981524f618a92624e0ca'
            'aa690a1be223d61936497f4cd7c4f8dc93d515145a75197374ab2df7fa160b35')

prepare() {
  cd $pkgname-$pkgver
  # blacklist gnome-settings-daemon plugins from auto starting (from Fedora)
  patch -Np1 -i ../blacklist_g-s-d.patch
}

build() {
  cd $pkgname-$pkgver
  
  CFLAGS="${CFLAGS} -Wno-error=format-nonliteral" ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libexecdir=/usr/lib/cinnamon-session \
    --disable-schemas-compile \
    --disable-gconf \
    --enable-qt57-theme-support
    
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
