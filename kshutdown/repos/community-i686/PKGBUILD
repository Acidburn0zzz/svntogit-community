# $Id$
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kshutdown
pkgver=3.0beta5
pkgrel=1
pkgdesc="Shutdown Utility for KDE"
arch=('i686' 'x86_64')
url="http://kshutdown.sourceforge.net/"
license=('GPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
install='kshutdown.install'
source=("http://downloads.sourceforge.net/${pkgname}/${pkgname}-source-${pkgver}.zip")
sha1sums=('108e27a7d1cef505c296656f92ab071d8c53bce3')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
