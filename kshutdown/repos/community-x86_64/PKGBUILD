# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kshutdown
pkgver=3.0beta7
pkgrel=1
pkgdesc='Shutdown Utility for KDE'
arch=('x86_64' 'i686')
url='http://kshutdown.sourceforge.net/'
license=('GPL')
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
install="$pkgname.install"
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-source-$pkgver.zip")
sha256sums=('5de50c532a9fd2e65706376232b7e641b05bbc5f54677f06d27bce0e0f9825ac')

build() {
  mkdir "$srcdir/build"
  cd "$srcdir/build"
  cmake "../$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
