# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.1.9
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.3.1
pkgrel=3
pkgdesc="Fully rewritten Sieve implementation for Dovecot v2.1"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://www.rename-it.nl/dovecot/2.1/dovecot-2.1-$pkgname-$pkgver.tar.gz"{,.sig}
        "dovecot.conf")
options=('!libtool')
sha256sums=('e95d86c5cd912c9a7e180414ffd5eee52e1b5dc8abef3d5762b950d1de86b5c7'
            '9d4699435cb1e82d3ac943c37f2ef909cd80cab707cc8b23be0d087f0e80903f'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')

build() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  ./configure --prefix=/usr --with-dovecot=/usr/lib/dovecot
  make
}

package() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}
