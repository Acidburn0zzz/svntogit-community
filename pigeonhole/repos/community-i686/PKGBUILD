# Maintainer: Peter Lewis <plewis@aur.archlinux.org>
# Contributor: Sebastian KÃ¶hler <sebkoehler@whoami.org.uk>

# This must be built against the version of dovecot being used,
# else mail delivery will fail.
# Specify the version of dovecot to be used here:
_dcpkgver=2.1.5
# Make sure to bump pkgrel if changing this.

pkgname=pigeonhole
pkgver=0.3.0
pkgrel=6
pkgdesc="Fully rewritten Sieve implementation for Dovecot v2.1"
arch=('i686' 'x86_64')
url="http://pigeonhole.dovecot.org/"
license=('LGPL')
depends=('dovecot='$_dcpkgver)
conflicts=('dovecot-sieve' 'pigeonhole-hg')
source=("http://www.rename-it.nl/dovecot/2.1/dovecot-2.1-$pkgname-$pkgver.tar.gz"{,.sig}
        "dovecot.conf")
options=('!libtool')
sha256sums=('2aeee8aa6dc700483b0a5a3d31f535abce71698ed1d467f11e931aeace2b0ada'
            '181213cd4af854966ff5bb6b5052b49b0b40c0c21999939b8451658053fa396d'
            'a457a1691cfa82495fc0503bfa4b61e54b149e63400fe0f568dff2c24a3f7858')

build() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  ./configure --prefix=/usr --with-dovecot=/usr/lib/dovecot
  make
}

package() {
  cd "$srcdir/dovecot-2.1-$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -m 644 -D "$srcdir/dovecot.conf" "$pkgdir/etc/ld.so.conf.d/dovecot.conf"
}

