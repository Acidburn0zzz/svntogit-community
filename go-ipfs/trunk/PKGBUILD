# $Id$
# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=go-ipfs
pkgver=0.4.6
pkgrel=1

pkgdesc='A peer-to-peer hypermedia distribution protocol'
url='http://ipfs.io/'
arch=(i686 x86_64)
license=(MIT)

depends=(glibc)
optdepends=('fuse2: for mounting/advanced use')
makedepends=(go git)

source=(go-ipfs-$pkgver.tar.gz::https://github.com/ipfs/go-ipfs/archive/v$pkgver.tar.gz
        ipfs.service)
sha512sums=('ae38084f8116444b36f878f620d279ead6ad3a3c49b25150f5c7f9e321a443b074167d6b4159f3d46a41fd5de51efb592a7df4932e6e14779e0ae6718970f4a7'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6')

prepare() {
  export GOPATH="$PWD"/.gopath
  mkdir -p "$GOPATH"/src/github.com/ipfs
  ln -sf "$PWD"/go-ipfs-$pkgver "$GOPATH"/src/github.com/ipfs/go-ipfs

  cd "$GOPATH"/src/github.com/ipfs/go-ipfs
  make deps
}

build() {
  export PATH="$PATH":"$PWD"/.gopath/bin
  cd "$GOPATH"/src/github.com/ipfs/go-ipfs

  make build
}

package() {
  cd go-ipfs-$pkgver

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644  ../ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 misc/completion/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
