# $Id$
# Maintainer: Anatol Pomozov
# Contributor: kpcyrd <git@rxv.cc>
# Contributor: Frederik “Freso” S. Olesen <freso.dk@gmail.com>
# Contributor: Ian Naval <ianonavy@gmail.com>

pkgname=go-ipfs
pkgver=0.4.1
pkgrel=1

pkgdesc='A peer-to-peer hypermedia distribution protocol'
url='http://ipfs.io/'
arch=(i686 x86_64)
license=(MIT)

depends=(glibc)
optdepends=('fuse: for mounting/advanced use')
makedepends=(go git)

source=(go-ipfs-$pkgver.tar.gz::https://github.com/ipfs/go-ipfs/archive/v$pkgver.tar.gz
        ipfs.service)
sha512sums=('d336deae75a54a22c0f20fb50801af02890982f9f553e4ee4f7b64bf69554a6b1bcd08e6ab1d3e7a01018d1d292f52cf54bc255fc2f5d86e8d5ff6b484e103ba'
            '1a6bc7fffc3c3640cda3f33c107424edd698c62bd8509731856b8827ea18be1a1845ef0d5ca15325d48d7d871a78daacc011ff9eacea926bc35899850990c1c6')

prepare() {
  export GOPATH="$PWD"/.gopath
  mkdir -p "$GOPATH"/src/github.com/ipfs
  ln -sf "$PWD"/go-ipfs-$pkgver "$GOPATH"/src/github.com/ipfs/go-ipfs

  cd "$GOPATH"/src/github.com/ipfs/go-ipfs
  make toolkit_upgrade
}

build() {
  export PATH="$PATH":"$PWD"/.gopath/bin
  cd "$GOPATH"/src/github.com/ipfs/go-ipfs

  make build
}

package() {
  cd go-ipfs-$pkgver

  install -Dm 755 cmd/ipfs/ipfs "$pkgdir/usr/bin/ipfs"
  install -Dm 644  ../ipfs.service "$pkgdir/usr/lib/systemd/user/ipfs.service"
  install -Dm 644 misc/completion/ipfs-completion.bash "$pkgdir/usr/share/bash-completion/completions/ipfs"
  install -Dm 644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
  install -Dm 644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}
