# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Beno√Æt Allard <benoit dot allard at gmx dot de>
# Contributor: killruana <killruana@gmail.com>

pkgname=yosys
pkgver=0.8
_abccommit=2ddc57d8760d94e86699be39a628178cff8154f8
pkgrel=1
epoch=
pkgdesc='A framework for RTL synthesis'
arch=('i686' 'x86_64')
url='http://www.clifford.at/yosys/'
license=('custom:ISC')
depends=('tcl' 'libffi')
optdepends=('graphviz: Schematics display support')
makedepends=('clang' 'python')
source=("https://github.com/cliffordwolf/yosys/archive/$pkgname-$pkgver.tar.gz"
        abc-$_abccommit.tar.gz::https://github.com/berkeley-abc/abc/archive/$_abccommit.tar.gz)
sha512sums=('92364592f755b77bca5efaa99ed8050f4278a406066601a217c567e238ad2f59f9d85b7c91295dbd7125b2f9a0da74c511e238d8ae996bba1cd4aace71dc3d10'
            '5e5c9b887b89ae40a7b82f6d3c5b28bc0c79375fd2178598d5ddcf281c98ff6d61c73c06bc74cfad1be140722d14ea8deabae6c1c9e6c3cd641b37e6f0b7e805')

prepare() {
    ln -s "$srcdir"/abc-$_abccommit $pkgname-$pkgname-$pkgver/abc
}

build() {
    cd $pkgname-$pkgname-$pkgver
    make ABCREV=default PREFIX="/usr"
}

package() {
    cd $pkgname-$pkgname-$pkgver
    make ABCREV=default PREFIX="/usr" DESTDIR="$pkgdir" install

    install -D -m 644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
