# Maintainer: Gabriel Rauter <rauter.gabriel@gmail.com>

pkgname=maxcso
pkgver=1.11.0
pkgrel=2
pkgdesc="A fast ISO to CSO compression program for use with PSP and PS2 emulators"
arch=(x86_64)
url=https://github.com/unknownbrackets/maxcso
license=(custom:ISCL)
depends=(
  gcc-libs
  glibc
  libuv
  libzopfli.so
  lz4
  zlib
)
makedepends=(git)
source=(
  git+https://github.com/unknownbrackets/maxcso.git#tag=deb79236ea47feee30359e3dc8c865ed2dc6393c
  maxcso-system-flags.patch
  maxcso-system-zopfli.patch
)
sha256sums=('SKIP'
            '9500875013db8ad18350cc0a36751fec37de80cde06e91e2999a421e53af8416'
            'ceae42c12bc686dba1dae8b0df1f32e6063ba25d7edebfaaa94af85a9e8f24f7')

prepare() {
  cd maxcso

  git cherry-pick -n 65f456a439b484e8da31831b6a3f0f96e8bb21bf
  git cherry-pick -n 942a9a62421bfa3cf62b5c0992a272f95b802179
  patch -Np1 -i ../maxcso-system-flags.patch
  patch -Np1 -i ../maxcso-system-zopfli.patch
}

build() {
  make -C maxcso
}

package() {
  make DESTDIR="${pkgdir}" PREFIX=/usr -C maxcso install
  install -Dm 644 maxcso/LICENSE.md -t "${pkgdir}"/usr/share/licenses/maxcso/
}

#vim: ts=2 sw=2 et:
