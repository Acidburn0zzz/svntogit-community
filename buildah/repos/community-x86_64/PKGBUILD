# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=buildah
pkgver=1.4
pkgrel=2
pkgdesc="A tool which facilitates building OCI images"
arch=(x86_64)
url="https://github.com/containers/buildah"
license=(Apache)
depends=(ostree runc skopeo)
makedepends=(go-pie git bash-bats btrfs-progs device-mapper gpgme libassuan
             bzip2 go-md2man runc skopeo)
_commit=608fa843cce45e7ee58ccb71a90297b645a984d3  # tags/v1.4
source=(git://github.com/containers/buildah.git#commit=$_commit
        0001-Revert-ostree-add-selinux-label-for-each-file.patch)
md5sums=('SKIP'
         'aed8c1169e73161220c49de34f1acfdf')

pkgver() {
  cd buildah
  git describe --tags | sed 's/^[vV]//;s/-/+/g'
}

prepare() {
  pushd buildah/vendor/github.com/containers/image/
  patch -p1 -i "$srcdir/0001-Revert-ostree-add-selinux-label-for-each-file.patch"
  popd

  mkdir -p src/github.com/containers
  cp -r $pkgname src/github.com/containers
}

build() {
  export GOPATH="${srcdir}"
  cd src/github.com/containers/buildah
  make PREFIX=/usr
}

package() {
  cd src/github.com/containers/buildah
  make DESTDIR="$pkgdir" PREFIX=usr install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
