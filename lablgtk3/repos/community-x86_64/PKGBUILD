# Maintainer: Konstantin Gizdov <arch at kge dot pw>
# Contributor: Baptiste Jonglez <baptiste--aur at jonglez dot org>

_pkgname=lablgtk
pkgname="${_pkgname}3"
pkgver=3.1.2
pkgrel=1
pkgdesc="An Objective Caml interface to gtk3"
arch=('x86_64')
license=('LGPL')
url="http://lablgtk.forge.ocamlcore.org/"
depends=('gtk3' 'gtksourceview3' 'gtkspell3' 'ocaml-cairo')
makedepends=('ocaml-compiler-libs>=4.13.1' 'ocaml-findlib' 'dune')
optdepends=('ocaml: for using the tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/garrigue/lablgtk/archive/refs/tags/${pkgver}.tar.gz")
sha256sums=('ad7417875f79716597cc06ccd8cae1efccb326263db1010d9f83555c185f1669')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  # gcc-10 workaround
  OCAMLPATH=/usr/lib/ocaml LABLGTK_EXTRA_FLAGS=-fcommon dune build -p lablgtk3,lablgtk3-gtkspell3,lablgtk3-sourceview3
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  DESTDIR="${pkgdir}" dune install lablgtk3 --prefix "/usr" --libdir "lib/ocaml"
  DESTDIR="${pkgdir}" dune install lablgtk3-gtkspell3 --prefix "/usr" --libdir "lib/ocaml"
  DESTDIR="${pkgdir}" dune install lablgtk3-sourceview3 --prefix "/usr" --libdir "lib/ocaml"

  install -dm755 "${pkgdir}/usr/share/"
  mv "${pkgdir}/usr/doc" "${pkgdir}/usr/share/"
}
