# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Parth Buch <parthbuch115 at gmail dot com>
# Contributor: Tom Vincent <http://tlvince.com/contact/>

pkgname=meteorjs
pkgver=0.5.8
pkgrel=1
pkgdesc="Open-source platform for building top-quality web apps in a fraction of the time."
arch=('i686' 'x86_64')
url="https://github.com/meteor/meteor"
license=('MIT')
depends=('nodejs' 'mongodb')
options=('!strip')

sha256sums=('232d7a0bf0947e29fcd6ff8e7314d5783c1b4b7e5c6b5b68ed9ef2ed8d75c6cc'
            'a3ac659c52b652676da0530f2148025e0f8bfa84bccf63431c20eb72d3bc5cb7')

if [ "$CARCH" = "x86_64" ]; then
    _arch="amd64"
else
    _arch="i386"
    sha256sums[0]='b9c45ad56b2ccd2d6fa41740b20c671ea642ce84271474c319ddc8a67a0c77ff'
fi

source=("http://d3sqy0vbqsdhku.cloudfront.net/meteor_$pkgver-1_$_arch.deb" meteor)

package() {
    tar xf data.tar.gz -C "$pkgdir" ./usr/lib/meteor

    install -Dm644 "$pkgdir/usr/lib/meteor/LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
    install -Dm755 "$srcdir/meteor" "$pkgdir/usr/bin/meteor"

    cd "$pkgdir/usr/lib/meteor"
    rm -rf LICENSE.txt lib/node lib/dtrace lib/node_modules/npm share include \
           lib/node_modules/mongodb/node_modules/bson/build/Release/obj.target/bson.node \
           lib/node_modules/websocket/build/Release/obj.target/xor.node \
           lib/node_modules/websocket/build/Release/obj.target/validation.node

    # mongodb links
    rm -rf mongodb
    install -dm755 mongodb/bin
    ln -s /usr/bin/mongod mongodb/bin/mongod
    ln -s /usr/bin/mongo mongodb/bin/mongo

    # node links
    rm -rf bin
    install -dm755 bin
    ln -s /usr/bin/node bin/node
    ln -s /usr/bin/node-waf bin/node-waf
    ln -s /usr/bin/npm bin/npm
}
