# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Peter Garceau <RockyChimp@gmail.com>

pkgname=python-sympy
pkgver=1.7
pkgrel=1
arch=('any')
pkgdesc='Symbolic manipulation package (Computer Algebra System), written in pure Python'
url='https://sympy.org/en/index.html'
license=('BSD')
depends=('python-mpmath')
optdepends=('ipython: user friendly interface for isympy')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sympy/sympy/archive/sympy-$pkgver.tar.gz")
sha512sums=('f7bdaff7432436dc888e56d4b56d023c8da5cfb54dfa6955cf7999acceedfd5175f910fb9b3c5776145f3dc3036f78e66813cdba4201be854dfa2c5c9f4e9f4d')

build() {
  cd "$srcdir"/sympy-sympy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/sympy-sympy-$pkgver
  python setup.py test
}

package() {
  cd sympy-sympy-$pkgver

  python setup.py install --root "$pkgdir" --optimize=1

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
