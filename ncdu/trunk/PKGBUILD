# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lp76 <l.peduto@gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=ncdu
pkgver=2.1
pkgrel=3
pkgdesc='Disk usage analyzer with an ncurses interface'
url='https://dev.yorhel.nl/ncdu'
license=('MIT')
depends=('ncurses')
makedepends=('zig')
arch=('x86_64')
source=("https://dev.yorhel.nl/download/${pkgname}-${pkgver}.tar.gz"{,.asc}
        # https://code.blicky.net/yorhel/ncdu/issues/194
        "ncdu-194-sudo-refresh-fix.patch::https://code.blicky.net/yorhel/ncdu/commit/5f383966a91d82394c69ea66ed64cf238bcf0ba0.patch"
        "hardening.patch")
sha256sums=('4ba8d20a64a55cc00af2d2c6138afcc9dc25e40a5ed6a8c544cb9e2285a195fe'
            'SKIP'
            '45cff6eb86382f6657d3ebd8a3b62f372bf61ed96cdc80ede462bd573706754e'
            '4b90e881d1dd5fb36b269e52952be40e050926dd6d4ce08ac7a7f0e5940e790d')
b2sums=('9dd90b63ad7edbd402b0cdab7f5aa5263c6df562afb15229b5c2b6143cf7efc7af94fabcd710593b2fd94fca02742e08b401fc0103b1154c6cd278f5709eeef8'
        'SKIP'
        'de08ba55f5a6688d10607434df8c6b5f4de46496d50a17bca4ff99c786258cccffe5f4ee7d0e23a6c173dbfc58cbd01899520675211a1523634eb48935da390b'
        '54b47c408e4144bee629d1d80410e4c97baf375d10399da3644a7a929b21732b8323723b2218890fe7d7d5b9b7b42427a8af74bc64c4002ef23e722c3ed969bf')
validpgpkeys=('74460D32B80810EBA9AFA2E962394C698C2739FA') # Yoran Heling <projects@yorhel.nl>

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch --verbose --strip=1 --input=../ncdu-194-sudo-refresh-fix.patch
    patch --verbose --strip=1 --input=../hardening.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # since RELRO is not doable in the zig.build file (https://github.com/ziglang/zig/issues/6977)
    # we are running zig build-exe directly
    #zig build -Drelease-safe -Dcpu=baseline

    zig build-exe ./src/main.zig ./src/ncurses_refs.c -lc -lncursesw -OReleaseSafe --name ncdu -target native-native -mcpu x86_64 -D _DEFAULT_SOURCE -D _XOPEN_SOURCE=600 -fPIE -z relro -z now -fno-each-lib-rpath
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    zig build test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D --mode=755 "./${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    make install-doc PREFIX="${pkgdir}/usr"

    install -D --mode=644 LICENSES/MIT.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
