# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nlopt
pkgver=2.5.0
pkgrel=4
pkgdesc="nonlinear optimization library"
arch=('x86_64')
url="http://ab-initio.mit.edu/wiki/index.php/NLopt"
license=('LGPL')
depends=('gcc-libs')
makedepends=('cmake' 'python-numpy' 'octave' 'guile' 'swig')
optdepends=('python-numpy: to use with python')
source=("https://github.com/stevengj/nlopt/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        nlopt-fix-guile.patch::"https://github.com/stevengj/nlopt/commit/96ebb33c3143de3e050e040e1fd11c6ee9055471.patch"
        nlopt-fix-octave-5.patch::"https://github.com/stevengj/nlopt/commit/18cadf72cc69334091492b6137a32471e178e2ed.patch")
sha256sums=('c6dd7a5701fff8ad5ebb45a3dc8e757e61d52658de3918e38bab233e7fd3b4ae'
            'dd0f4ea62e92ba03e9c15b5bad15bf4202d2d7f840863acfd41baca038b20bbe'
            '84afcc709721621978915c08e0d2541c03ca504ad037db5240a821af62b6f4dc')

prepare () {
    mkdir -p build
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../nlopt-fix-guile.patch
    patch -p1 -i ../nlopt-fix-octave-5.patch
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
