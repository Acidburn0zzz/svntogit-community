# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=nlopt
pkgver=2.6.1
pkgrel=2
pkgdesc="Nonlinear optimization library"
arch=(x86_64)
url="https://nlopt.readthedocs.io/en/latest/"
license=(LGPL)
depends=(gcc-libs)
makedepends=(cmake python-numpy octave guile swig)
optdepends=('python-numpy: to use with python')
source=("https://github.com/stevengj/nlopt/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('66d63a505187fb6f98642703bd0ef006fedcae2f9a6d1efa4f362ea919a02650')

prepare () {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib
    make
}

check() {
    cd build
    ctest --output-on-failure
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
}
