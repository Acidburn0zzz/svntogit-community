# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Martchus <martchus@gmx.net>

pkgname=libfilezilla
pkgver=0.9.1
pkgrel=1
pkgdesc="Small and modern C++ library, offering some basic functionality to build high-performing, platform-independent programs"
arch=('i686' 'x86_64')
url="https://filezilla-project.org/"
license=('GPL')
depends=('gcc-libs')
checkdepends=('cppunit')
source=("http://download.filezilla-project.org/libfilezilla/libfilezilla-$pkgver.tar.bz2")

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  ./configure \
    --prefix=/usr \
    --disable-static
  make
}

check() {
  cd "${srcdir}/$pkgname-$pkgver"
  # LANG needed to fix string_test::test_conversion2
  LANG=en_US.UTF-8 make check
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install
}
md5sums=('4676304b048869ab6ac6ae09a1ab29f9')
