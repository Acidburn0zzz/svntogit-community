# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Orivej Desh <masecretaire@gmx.fr>

_download_id=2434
pkgname=sonic-visualiser
pkgver=3.2.1
pkgrel=1
pkgdesc="A viewer and analyser of music audio files."
arch=('x86_64')
url="https://www.sonicvisualiser.org/"
license=('GPL2')
groups=('pro-audio')
depends=('capnproto' 'desktop-file-utils' 'hicolor-icon-theme' 'libfishsound'
'libid3tag' 'liblo' 'liblrdf' 'libmad' 'liboggz' 'libpulse' 'portaudio'
'qt5-svg' 'rubberband' 'sord')
source=("https://code.soundsoftware.ac.uk/attachments/download/${_download_id}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('65a8d1ee41373d18bb2525b46b2a4b291071e2bca0edb6700cc9bd8f399926f6280ea961d7f5bfa74a0d9ea827b9be0b2a9054500d33eb6d4d67b72f5daa2583')

prepare() {
  cd "${pkgname}-${pkgver}"
  # devendoring of rubberband required:
  # https://sourceforge.net/p/sv1/bugs/265/
  rm -rf rubberband
  sed -e '/rubberband/d' \
      -e 's/vamp-plugin-sdk \\/vamp-plugin-sdk/' \
      -i base.pri
  sed -e '/rubberband/d' \
      -e 's/PulseAudioIO.h \\/PulseAudioIO.h/' \
      -e 's/SystemRecordSource.cpp \\/SystemRecordSource.cpp/' \
      -i bq-files.pri
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"
  export LIBS="$(pkgconf --libs rubberband) ${LIBS}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  # DESTDIR still ignored
  # https://sourceforge.net/p/sv1/bugs/254
  export INSTALL_ROOT="${pkgdir}/"

  make install
  # install some things separately, because build system is broken:
  # https://sourceforge.net/p/sv1/bugs/247/

  # desktop files
  install -t "${pkgdir}/usr/share/applications/" -vDm 644 "${pkgname}.desktop"
  # samples
  install -t "${pkgdir}/usr/share/${pkgname}/samples" -vDm 644 samples/*
  # templates
  install -t "${pkgdir}/usr/share/${pkgname}/templates" -vDm 644 templates/*
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGELOG,CITATION,README.{md,OSC}}
}
