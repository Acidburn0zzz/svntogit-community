# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=intellij-idea-libs
pkgver=2016.3
_pkgver=163.7743.44
pkgrel=1
epoch=2
pkgdesc="Architecture dependent libraries needed by the Intellij Idea IDE"
arch=('i686' 'x86_64')
url="https://www.jetbrains.com/idea/"
license=('Apache')
depends=('glibc')
options=(!strip)
source=(http://download.jetbrains.com/idea/ideaIC-$pkgver-no-jdk.tar.gz)
sha256sums=('56c006e14b5eb45a8123d54bfa9600cd5c8aae1f9503af936d814c3c285e4b7a')

package() {
  cd "idea-IC-$_pkgver"

  [ $CARCH == "x86_64" ] && SUFFIX=64
  install -D -m755 bin/fsnotifier${SUFFIX} "$pkgdir"/usr/share/intellijidea-ce/bin/fsnotifier${SUFFIX}
  install -D -m644 bin/libbreakgen${SUFFIX}.so "$pkgdir"/usr/lib/libbreakgen${SUFFIX}.so
  
  # libpty
  [ $CARCH == "x86_64" ] && _ARCH=x86_64 || _ARCH=x86
  install -D -m644 lib/libpty/linux/$_ARCH/libpty.so "$pkgdir"/usr/share/intellijidea-ce/lib/libpty/linux/$_ARCH/libpty.so
}

# vim:set ts=2 sw=2 et:
