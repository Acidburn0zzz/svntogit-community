# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=xandikos
pkgver=0.2.4
pkgrel=2
pkgdesc='Lightweight yet complete CardDAV/CalDAV server that backs onto a Git repository'
arch=('any')
url=https://www.xandikos.org/
license=('GPL3')
depends=('python-aiohttp' 'python-defusedxml' 'python-dulwich'
         'python-icalendar' 'python-jinja' 'python-multidict')
makedepends=('python-setuptools')
optdepends=('python-prometheus_client: for metrics with the standalone server'
            'uwsgi-plugin-python: for the WSGI interface')
source=("https://files.pythonhosted.org/packages/source/x/xandikos/xandikos-$pkgver.tar.gz"
        'https://github.com/jelmer/xandikos/commit/eeaa408c1f66a3b5a07748825207773d2951bfa4.patch')
sha256sums=('57289299b1cc7f5002698bf00661d2481bbfe94831e59ac33da6dc5cc6153f7d'
            'cf8f5428139c8dd60b3d7269a431fc2d90f81379fa0e2158a385436b71ccbcb4')
b2sums=('3e45eca57ed1ea601bf5efebc089b0b73067cf4075564643c44090861cddf3fec4c2db6865c6e7231a185aa2f7e5110b8e2e99deee074297826834f5fb3487fc'
        '2e1118e8f59fb6876acc604fbc63835cb2d8b80cbd00781ac5531ea18d509784fba50bd6ea91715d44039c9da5c38dd2e559d06e74526935265b3c93d8939446')

prepare() {
  cd xandikos-$pkgver
  # When size=None is passed in, don't pass it onto stream.read()
  patch --forward --strip=1 --input=../eeaa408c1f66a3b5a07748825207773d2951bfa4.patch
}

build() {
  cd xandikos-$pkgver
  python setup.py build
}

check() {
  cd xandikos-$pkgver
  python -m unittest xandikos.tests.test_suite
}

package() {
  cd xandikos-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos README.rst
  install -Dm644 -t "$pkgdir"/usr/share/doc/xandikos/examples examples/*
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 xandikos.1
}

# vim:set ts=2 sw=2 et:
