# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Contributor: robertfoster
# Contributor: kurych
# Contributor: redfish
# Contributor: atommixz
# Contributor: denn
# Contributor: post-factum
# Contributor: wrdcrrtmnstr
# Contributor: r4sas

pkgname=i2pd
pkgver=2.29.0
pkgrel=2
pkgdesc='A full-featured C++ implementation of the I2P router'
arch=('x86_64')
url='https://i2pd.website/'
license=('BSD')
depends=('boost-libs' 'libminiupnpc.so' 'openssl' 'zlib')
makedepends=('cmake' 'boost')
provides=('i2p-router')
backup=('etc/i2pd/i2pd.conf'
        'etc/i2pd/tunnels.conf')
source=("${pkgname}-${pkgver}.tar.gz"::"https://github.com/PurpleI2P/i2pd/archive/${pkgver}.tar.gz"
        '010-i2pd-fix-tests.patch'
        '020-i2pd-fix-install.patch'
        '030-i2pd-fix-config.patch'
        '040-i2pd-do-not-override-config.patch'
        '050-i2pd-tunnels-d-readme.patch'
        'i2pd.sysusers'
        'i2pd.tmpfiles')
sha256sums=('fd0474c33b411593b9dc8197f3799d37d68455c11a9ee3994ec993a96388ec06'
            'c408f582d1f8d80c0ba560848520424dfa33e1b3035604d605ceccafa66a0aa9'
            '89cbd39cfe80ac0e9f60a426910b53affebd162486cd85ff3262cb941f6b3f8c'
            'f3a2afe0adc2f502dcb388f778a394a2556848fe8be014f3470e205a098e765c'
            'a9a2fabd093b952507a297c755b75ff765f658cad16ec0318b22514d1bba44d7'
            '48217794d5d520751e00efe3ea4fe0f2a340df16c669007cf08058c349746ac1'
            '88b2e709228049ba11f37863f87de75ab6cde295104852871384337cfdc906a3'
            '55005dfc1cba3e1fa3b6a2740fdbbb0cc9eb3178941ddd7008e2ef1d5c85a088')

prepare() {
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/010-i2pd-fix-tests.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/020-i2pd-fix-install.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/030-i2pd-fix-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/040-i2pd-do-not-override-config.patch"
    patch -d "${pkgname}-${pkgver}" -Np1 -i "${srcdir}/050-i2pd-tunnels-d-readme.patch"
}

build() {
    cd "${pkgname}-${pkgver}/build"
    cmake \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DBUILD_SHARED_LIBS:BOOL='ON' \
        -DWITH_UPNP:BOOL='ON' \
        -DWITH_AESNI='OFF' \
        -DWITH_AVX='OFF' \
        -Wno-dev \
        .
    make
}

check() {
    make -C "${pkgname}-${pkgver}/tests"
}

package() {
    local _dir
    local _file
    
    cd "${pkgname}-${pkgver}"
    
    make -C build DESTDIR="$pkgdir" install
    
    # config
    install -D -m644 contrib/i2pd.conf    -t "${pkgdir}/etc/i2pd"
    install -D -m644 contrib/tunnels.conf -t "${pkgdir}/etc/i2pd"
    install -d -m755 "${pkgdir}/etc/i2pd/tunnels.d"
    
    # certificates
    while read -r -d '' _file
    do
        _dir="${_file#contrib/certificates}"
        _dir="${_dir%/*}"
        install -D -m644 "$_file" -t "${pkgdir}/usr/share/i2pd/certificates/${_dir}"
    done < <(find contrib/certificates -type f -print0)
    
    # systemd
    install -D -m644 contrib/i2pd.service   -t "${pkgdir}/usr/lib/systemd/system"
    install -D -m644 "${srcdir}/i2pd.sysusers" "${pkgdir}/usr/lib/sysusers.d/i2pd.conf"
    install -D -m644 "${srcdir}/i2pd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/i2pd.conf"
    
    # tunnels.d examples
    install -D -m644 contrib/tunnels.d/README         -t "${pkgdir}/usr/share/doc/${pkgname}/tunnels.d"
    install -D -m644 contrib/tunnels.d/IRC-Ilita.conf -t "${pkgdir}/usr/share/doc/${pkgname}/tunnels.d"
    install -D -m644 contrib/tunnels.d/IRC-Irc2P.conf -t "${pkgdir}/usr/share/doc/${pkgname}/tunnels.d"
    
    # man page
    install -D -m644 debian/i2pd.1 -t "${pkgdir}/usr/share/man/man1"
    
    # license
    install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
