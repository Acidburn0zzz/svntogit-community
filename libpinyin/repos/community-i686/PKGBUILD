# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=1.6.0
_modelver=12
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
# Switching to kyotocabinet will break all existing installation. Requesting a migration tool to prevent data loss:
# https://github.com/libpinyin/libpinyin/issues/44
depends=('db' 'glib2') # 'kyotocabinet'
makedepends=('git')
source=("git+https://github.com/$pkgname/${pkgname}.git#tag=$pkgver"
        "http://downloads.sourceforge.net/libpinyin/model${_modelver}.text.tar.gz")
noextract=("model${_modelver}.text.tar.gz")
sha512sums=('SKIP'
            'a2282bb660481691d86b766fd0e1b9d0d6e58e26a055685785cfd934f8fcc2b5ca90b7c08b89d885f20869a52dceb78033eb6bb648fd16a8d36bd24a8b562814')

prepare() {
  cd ${pkgname}

  cp ../model${_modelver}.text.tar.gz data/
  sed -i "/wget.*model${_modelver}\.text\.tar\.gz/ d" data/Makefile.am
}

build() {
  cd ${pkgname}
  aclocal && libtoolize --force && autoheader && automake -a && autoconf

  ./configure --prefix=/usr # --with-dbm=KyotoCabinet
  make
}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
