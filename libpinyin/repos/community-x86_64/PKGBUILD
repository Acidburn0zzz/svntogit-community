# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Yangtse <yangtsesu@gmail.com> 
# Contributor: WU Jun <quark at lihdd dot net>

pkgname=libpinyin
pkgver=0.9.91
pkgrel=1
pkgdesc="Library to deal with pinyin"
arch=('i686' 'x86_64')
url="https://github.com/libpinyin/libpinyin"
license=('GPL')
depends=('db' 'glib2')
options=(!libtool)
source=("https://github.com/libpinyin/libpinyin/archive/${pkgver}.tar.gz"
        "http://downloads.sourceforge.net/libpinyin/model6.text.tar.gz")
noextract=("model6.text.tar.gz")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/model6.text.tar.gz" "${srcdir}/${pkgname}-${pkgver}/data/"
  sed -i '/wget.*model6\.text\.tar\.gz/ d' ${srcdir}/${pkgname}-${pkgver}/data/Makefile.am

  aclocal && libtoolize --force && autoheader && automake -a && autoconf
  ./configure --prefix=/usr && make
}

package() {
  cd "${srcdir}/libpinyin-$pkgver"
  make DESTDIR="${pkgdir}" install
}
sha512sums=('40263ff3b0e4ac6f68ed51d90b07c9431beec70d2b93ab3c0fec36a35b662380a022478bf15d81305a5e84c0eb95d662fed70c7213ff5c0d11a964b232bf1be2'
            '8719d99b2efb22438ec067c046f8d45addb3e0d336e8d6ec4b8ed22370fe81366d19a34934bb059230f86e74c379acb62822ee3458850437880d531de8ff3e0e')
