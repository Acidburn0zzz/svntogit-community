# $Id$
# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgname=mayavi
pkgver=4.2.0
_githubtag=a1e562a
pkgrel=3
pkgdesc="A 3-dimensional visualizer of scientific data"
arch=('i686' 'x86_64')
url="http://code.enthought.com/projects/mayavi/"
license=('BSD')
depends=('ipython2' 'vtk' 'python2-envisage' 'wxpython')
makedepends=('python2-distribute' 'python2-sphinx')
replaces=('python2-mayavi')
provides=('python2-mayavi')
options=(!emptydirs)

source=("$pkgname-$pkgver.tar.gz::https://github.com/enthought/mayavi/tarball/${pkgver}"
        vtk-5.10.patch)
md5sums=('b46fb048176002738de64eed8d2b447a'
         '01b8375392b5d3f587e81ba388a8089f')

build() {
  cd "$srcdir"/enthought-mayavi-${_githubtag}

  patch -p1 < ../vtk-5.10.patch

  python2 setup.py build

  cd docs

  make SPHINXBUILD=sphinx-build2 html
}

package() {
  cd "$srcdir"/enthought-mayavi-${_githubtag}

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  cp -r docs/build/* "${pkgdir}"/usr/lib/python2.7/site-packages

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find "${pkgdir}" -name '*.py')
  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    "$pkgdir/usr/lib/python2.7/site-packages/mayavi/tests/csv_files/csv_2_py"

  install -D LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

