# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=vice
pkgver=3.4
pkgrel=3
pkgdesc='The Versatile Commodore 8-bit Emulator'
arch=(x86_64)
license=(GPL)
url='https://vice-emu.sourceforge.io/'
depends=(alsa-lib giflib libjpeg-turbo libpng libpulse sdl2)
makedepends=(ffmpeg libpcap libxaw xa xorg-bdftopcf xorg-mkfontdir)
source=("https://downloads.sourceforge.net/project/vice-emu/releases/$pkgname-$pkgver.tar.gz"
        disable-fc-cache.patch)
sha256sums=('4bd00c1c63d38cd1fe01b90032834b52f774bc29e4b67eeb1e525b14fee07aeb'
            '8058210e092c4943a55baeff47e6d5f3661aeb3686fd1f75e07f47888da9cc98')

prepare() {
  cd $pkgname-$pkgver
  rm -rf src/lib/{liblame,libx264,libffmpeg}
  patch -p1 -i ../disable-fc-cache.patch
  sed -i 's/lib64/lib/g' configure.ac
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  export CFLAGS="$CFLAGS -w -Wl,--allow-multiple-definition"
  ./configure \
    --enable-external-ffmpeg \
    --enable-x64 \
    --libdir=/usr/lib \
    --prefix=/usr
  make
}

package() {
  make -C $pkgname-$pkgver \
    DESTDIR="$pkgdir" \
    realdocdir=/usr/share/doc/vice \
    install -j1
  ln -s /usr/share/doc/vice "$pkgdir/usr/lib/vice/doc"
}
