# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-openapi-spec-validator
pkgname=(python-openapi-spec-validator python2-openapi-spec-validator)
pkgver=0.2.7
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'python-yaml'
             'python2-yaml' 'python-jsonschema' 'python2-jsonschema' 'python2-pathlib')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('61e09b1fcb8526d0ccbb1b3e990dd3508924cc6eeda2c68be6d3ea6b692813015a34ae83c1800eed74a3415463c21a3118ec913092906e4c1c4abeb256313843')

prepare() {
  cp -a openapi-spec-validator-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  python setup.py build

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  PYTHONPATH="$PWD/build/lib" pytest2
}

package_python-openapi-spec-validator() {
  depends=('python-six' 'python-yaml' 'python-jsonschema')

  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openapi-spec-validator() {
  depends=('python2-six' 'python2-yaml' 'python2-jsonschema' 'python2-pathlib')

  cd openapi-spec-validator-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openapi-spec-validator{,2}
}
