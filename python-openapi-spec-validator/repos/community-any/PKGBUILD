# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-openapi-spec-validator
pkgname=(python-openapi-spec-validator python2-openapi-spec-validator)
pkgver=0.2.6
pkgrel=1
pkgdesc="A Python library that validates OpenAPI Specs against the OpenAPI 2.0 (aka Swagger) and OpenAPI 3.0.0 specification"
url="https://github.com/p1c2u/openapi-spec-validator"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six' 'python-yaml'
             'python2-yaml' 'python-jsonschema' 'python2-jsonschema' 'python2-pathlib')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/p1c2u/openapi-spec-validator/archive/$pkgver.tar.gz")
sha512sums=('37b21af0f47c15b349b6463ce63cb67fc17f63bd855cd6920361f523c8a198195ac13b4846cb8960dd6b535d7e6d778ac10800615f247307dd8fec9b48a874ac')

prepare() {
  cp -a openapi-spec-validator-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  python setup.py build

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/openapi-spec-validator-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest

  cd "$srcdir"/openapi-spec-validator-$pkgver-py2
  PYTHONPATH="$PWD/build/lib" pytest2
}

package_python-openapi-spec-validator() {
  depends=('python-six' 'python-yaml' 'python-jsonschema')

  cd openapi-spec-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-openapi-spec-validator() {
  depends=('python2-six' 'python2-yaml' 'python2-jsonschema' 'python2-pathlib')

  cd openapi-spec-validator-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/openapi-spec-validator{,2}
}
