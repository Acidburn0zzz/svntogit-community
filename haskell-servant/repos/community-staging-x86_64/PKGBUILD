# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant
pkgname=haskell-servant
pkgver=0.11
pkgrel=2
pkgdesc="A family of combinators for defining webservices APIs"
url="http://haskell-servant.readthedocs.org/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('ghc' "haskell-base-compat" "haskell-aeson" "haskell-attoparsec" "haskell-case-insensitive"
         "haskell-http-api-data" "haskell-http-media" "haskell-http-types"
         "haskell-natural-transformation" "haskell-mtl" "haskell-mmorph" "haskell-tagged"
         "haskell-text" "haskell-string-conversions" "haskell-network-uri" "haskell-vault")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha256sums=('c5b3f7af140fdafd3f646dcea6720c1b3b8a376f1f19a020b200acde64846b03')

prepare() {
    sed -i 's/, http-media            >= 0.4  && < 0.7/, http-media            >= 0.4  \&\& < 0.8/' \
        ${_hkgname}-${pkgver}/${_hkgname}.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
