# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-control-panel
pkgver=4.0.0
pkgrel=1
pkgdesc="Graphical user interface that allows the users to manage their Ubuntu One subscription and preferences"
arch=('any')
url="https://launchpad.net/ubuntuone-control-panel"
license=('GPL')
depends=('ubuntuone-client')
makedepends=('python2-distutils-extra')
source=(http://launchpad.net/ubuntuone-control-panel/stable-4-0/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('63f49118722bcb45dba5fa58918b2b92')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Remove unneeded files
  rm "$pkgdir"/usr/share/ubuntuone-control-panel/*.ttf
}
