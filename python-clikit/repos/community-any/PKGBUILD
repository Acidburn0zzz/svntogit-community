# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=clikit
pkgname=python-clikit
pkgver=0.4.1
pkgrel=2
pkgdesc="clikit allows you to create beautiful and testable command-line interfaces"
arch=('any')
url="https://github.com/sdispater/${_pkgname}"
license=('MIT')
depends=('python-pastel' 'python-pylev')
makedepends=('python-dephell')
checkdepends=('python-pytest-mock')
source=("${_pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "https://github.com/sdispater/clikit/pull/12/commits/00cb6a31be769ed8568fd296aa0b23dd6b4cbc8d.patch")
sha256sums=('e63e5f1501dbfb69edcf945de8e8eb99375d17a31dfc9651dcfa160c4927595f'
            '068170e881b13b3f7215e74c77d68d09a59b782a9bbe1ffaa8b4f6e2f6597c2d')
b2sums=('8efbb179fc8cc8bdc8ed8028d3ebac5b14dc6fce86c9f72a7c7ed8e6bb8288e2af882a17c1cb36a8b7c321d57f4bd32b4d74dd5923bb8563202409b66c342944'
        '3dba18178ff39e2128ebc74f171b40c6405c9fa536549c0807dd89dfe3efb1d3437a60e6fe403223b97f32151b973720e6a3f582d1479d2ba955bf9c6bc6924b')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # compat with latest pastel
    patch -p1 -i ../00cb6a31be769ed8568fd296aa0b23dd6b4cbc8d.patch

    # poetry-generated setup.py are fatally broken, see:
    # https://github.com/sdispater/poetry/issues/866
    dephell deps convert --from pyproject.toml --to setup.py
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    PYTHONPATH=$PWD/src python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
