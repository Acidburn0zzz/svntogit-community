# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: andy123 < ajs [at] online [dot] de >

pkgname=mari0
pkgver=1.6.2
pkgrel=2
pkgdesc="The Mario game with Portal gun mechanics"
arch=('any')
url="http://stabyourself.net/mari0/"
license=('CCPL')
depends=('love')
makedepends=('zip')
source=(${pkgname}.sh
        ${pkgname}.desktop
    	https://github.com/Stabyourself/mari0/archive/${pkgver}.tar.gz)
sha512sums=('84d237f82ba1ad5d6c52d66d27469de7044572626381af07230b46a476eb077bce94329408cf4e0c0f4ae1c90a7adf6562dc1ca795591a7687608a253af6b705'
            '703e3e4d8d69258f94b19a217986646b261be32fa26644d585fc2a2f273bda2afd77aa58b6eee3b2f0050551bc18ba4733b3597d133a29fdc84f844816f85e15'
            '3d1353f12f02fd993e309cac7ab653c3cbc8a60e8585663186870b5ea49542f46ec2c554711bba710febaf3ad73c7bf66cfba2ccbe4831aa7d5cb2fe5aab8992')

package() {
    # Make love package
    cd "${srcdir}"/${pkgname}-${pkgver}
    zip -r9 "${srcdir}"/${pkgname}.love *

	# Install Data
	install -D -m755 "${srcdir}/${pkgname}.love" "${pkgdir}/usr/share/${pkgname}/${pkgname}.love"
	install -D -m644 "${srcdir}/${pkgname}-${pkgver}/README.md" "$pkgdir/usr/share/${pkgname}/README.md"

	# Install Launcher
	install -D -m755 "${srcdir}/${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"

	# Install Desktop
	install -D -m644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
}
