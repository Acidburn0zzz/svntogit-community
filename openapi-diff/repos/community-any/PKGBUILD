# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=openapi-diff
pkgver=2.0.0
pkgrel=1
pkgdesc=" Utility for comparing two OpenAPI specifications"
arch=('any')
url="https://github.com/OpenAPITools/openapi-diff"
license=(Apache)
depends=(bash java-runtime=8)
makedepends=(maven java-environment=8 strip-nondeterminism)
source=(
  "$pkgname-$pkgver.tar.gz::https://github.com/OpenAPITools/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
  "${pkgname}.sh"
  "${pkgname}-2.0.0-remove_git_hooks.patch"
)
sha512sums=('fdccada8baa86af63ff051027cc35967993335b276c0d202749ba7c983e6e11be4e219cdbeadd9784ee46cbb6c9535321b5ca41833606a3758d9f9ad79561e78'
            'e4143cff09abc59958f50a969ed3f010ab39e640cf77081e47e77cf3e2bb785645100572fd18bc508ea441732bc13a5a811332fdbfdbd7f8a0d6865f301f978e'
            'b427cb671aef57c8364f3c987ed822a9aaa1ae15e3a756529eedf6d5df77b9ff66489753cdbc327f820a49c3ececcec58b42ef62d723331b48a537a89754d199')
b2sums=('6a252a07a862744c3431c3f215faa748e184e1df5ea0c8d46763a4f8c4e949ed64f361785ac657ec7d6198b459a2cd3b358c49d3210d0547c1fdb7e05f2b3ec5'
        'a28a194b1b5c731fd3093c22754fc7f6641441ae2acb1ad177b8ee573d8e3787af2b249740ffd1e3472f721e0274d0866c0acf1bf5c85f4a22fe1c2dcf24af7b'
        '4ea504d3765bb2428e16a097bfc6278228f09b16fa11e5cd39507bc0e64d277c47e111fa8f69013a0d53b76248ff653f29d77158e06432a7946fc1941d8ad3ec')

prepare() {
  cd "$pkgname-$pkgver"
  # remove maven plugin that requires git checkout: https://github.com/OpenAPITools/openapi-diff/issues/299
  patch -Np1 -i ../"${pkgname}-2.0.0-remove_git_hooks.patch"
}

build() {
  cd "$pkgname-$pkgver"
  mvn clean install
  # Timestamps in JAR files generated by Maven do not honour SOURCE_DATE_EPOCH
  # (https://cwiki.apache.org/confluence/pages/viewpage.action?pageId=74682318)
  find . -type f -iname "*.jar" -exec strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" {} \;
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644  cli/target/openapi-diff-cli-2.0.0-all.jar "${pkgdir}/usr/share/java/${pkgname}/${pkgname}-cli.jar"
  install -vDm 755 "../${pkgname}.sh" "${pkgdir}/usr/bin/${pkgname}"
  ln -svf "${pkgname}" "${pkgdir}/usr/bin/${pkgname}-cli"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
