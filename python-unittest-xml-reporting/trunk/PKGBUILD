# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=2.5.1
pkgrel=3
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-lxml' 'python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz"
        unittest-xml-reporting-python-3.8.patch)
sha512sums=('86e3c78d0f5f41c05214e080bf31d47bf0c33d4d6b8ad8a6d9b5286086cab4d7b6e7bea27cda94e9184afc5830709d1ae6bc72355f4a9306d842d8abc01df7ca'
            'c75efff884d8587dc34455e32f92f104827d7d9f21fc1e221e95d77911abbda41f09c9ef0247501ba4f3034ea650c236ddcae03956bd07b02711f0436b0499f7')

prepare() {
  cd unittest-xml-reporting-$pkgver
  patch -p1 -i ../unittest-xml-reporting-python-3.8.patch # Update test for python 3.8
}

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
