# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgbase=python-unittest-xml-reporting
pkgname=('python-unittest-xml-reporting' 'python2-unittest-xml-reporting')
pkgver=2.3.2
pkgrel=1
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-lxml' 'python2-lxml' 'python-mock' 'python2-mock')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver.tar.gz")
sha512sums=('a6605253095432583c3645b826044e81fa71178071880a968d3daca733f874d768f284e29068f3e1feb1c395c288e9a6be242dcd506f73571d1444555b471cc9')

prepare() {
  cp -a unittest-xml-reporting-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/unittest-xml-reporting-$pkgver
  python setup.py build

  cd "$srcdir"/unittest-xml-reporting-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/unittest-xml-reporting-$pkgver
  # https://github.com/xmlrunner/unittest-xml-reporting/issues/160
  python setup.py test || warning "Tests failed"

  cd "$srcdir"/unittest-xml-reporting-$pkgver-py2
  python2 setup.py test
}

package_python-unittest-xml-reporting() {
  depends=('python-six')

  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-unittest-xml-reporting() {
  depends=('python2-six')

  cd unittest-xml-reporting-$pkgver-py2
  python2 setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
