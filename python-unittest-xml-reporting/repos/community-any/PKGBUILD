# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-unittest-xml-reporting
pkgver=3.1.0
pkgrel=2
pkgdesc='unittest-based test runner with Ant/JUnit like XML reporting.'
arch=('any')
url='https://github.com/xmlrunner/unittest-xml-reporting'
license=('BSD')
depends=('python-lxml')
makedepends=('python-setuptools')
source=("https://github.com/xmlrunner/unittest-xml-reporting/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('27b4c017969f12879e0c7ff996834f5da94e71a290f3e61ca5fec78b27c5c70937007472fe91dc324e4e7bb1e7ce8c3cfd57e7f4ac5e3c8b1c7f185e58c58c80')

build() {
  cd unittest-xml-reporting-$pkgver
  python setup.py build
}

check() {
  cd unittest-xml-reporting-$pkgver
  python setup.py test
}

package() {
  cd unittest-xml-reporting-$pkgver
  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/

  # Broken data_files
  rm "$pkgdir"/usr/LICENSE
}
