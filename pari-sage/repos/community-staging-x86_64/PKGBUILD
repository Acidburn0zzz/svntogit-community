# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=pari-sage
_pkgver=2.9.1
pkgver=${_pkgver//-/.}
pkgrel=1
epoch=1
pkgdesc='Computer algebra system designed for fast computations in number theory. Development snapshot required by SageMath'
url='http://pari.math.u-bordeaux.fr/'
license=(GPL)
arch=(i686 x86_64)
depends=(gmp libx11)
makedepends=(perl texlive-core)
optdepends=('perl: gphelp, tex2mail')
conflicts=(pari)
provides=(pari)
source=("http://pari.math.u-bordeaux.fr/pub/pari/unix/pari-${pkgver}.tar.gz")
md5sums=('f997d6b526df6cd9145af0de5b822bf4')

build() {
  cd pari-${_pkgver}
  ./Configure --prefix=/usr --with-readline --with-gmp --mt=pthread
  make all
}

package() {
  cd pari-${_pkgver}
  make DESTDIR="$pkgdir" install
  ln -sf gp.1.gz "$pkgdir"/usr/share/man/man1/pari.1

# Intall header needed by SageMath
  install -Dm644 src/language/anal.h "$pkgdir"/usr/include/pari/anal.h
}
