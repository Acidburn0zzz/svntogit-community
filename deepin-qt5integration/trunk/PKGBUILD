# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.1.5
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('git' 'xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        "libqt5xcbqpa-dev::git+https://github.com/zccrs/libqt5xcbqpa-dev.git")
sha512sums=('3c1022eddc3eb89884df52684e80e475c8d9bb7e5239e00832f6ad5e8231e349c45018c20273fa8ffc5a46bfd25e8ff030d58407bcb4834a6899e5627c8f67b8'
            'SKIP')

prepare() {
  cd qt5integration-$pkgver
  ln -s ../../libqt5xcbqpa-dev ./platformplugin/libqt5xcbqpa-dev
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
