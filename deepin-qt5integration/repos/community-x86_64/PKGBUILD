# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.5.17
pkgrel=2
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
options=('debug')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5platform-plugins')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'gtest')
groups=('deepin')
source=("https://github.com/linuxdeepin/qt5integration/archive/$pkgver/$pkgname-$pkgver.tar.gz"
         no-versioned-targets.patch)
sha512sums=('3aadb03a333106015b4bf69cc72de7656301d11b80f5778b9b3133364306ab198eaaa638b6cb26ff82a8776f6886ad27a14cc3489cf125ab9156b55bf08b91d0'
            'aa516c5f73c4f2340b308d1724f21c82616c828653e3a38f7c1e2a80da6eeb63b52d8a62513b8584d86d81425f8b60673c0697a6d4bd12f4304d8618fbe3a851')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../no-versioned-targets.patch # Fix build with recent dtkgui
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make -j1
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  # Upstream installs the same plugin twice as Qt plugins, which breaks everything.
  find "$pkgdir"/usr/lib/qt/plugins/ -type f ! -name '*5.5.so' -delete -print
}
