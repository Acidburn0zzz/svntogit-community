# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.1.6
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('git' 'xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        "libqt5xcbqpa-dev::git+https://github.com/zccrs/libqt5xcbqpa-dev.git")
sha512sums=('39db1197eb67b532a8d919a1216cfb2f25eabd3840d3c7b319faa74446464f21d76ece7c6a3f064f09e4aa7c248d248a973acbd0f71578aa22c0aec0fd767cb8'
            'SKIP')

prepare() {
  cd qt5integration-$pkgver
  ln -s ../../libqt5xcbqpa-dev ./platformplugin/libqt5xcbqpa-dev
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
