# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.2.1
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="http://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('deepin-tool-kit' 'libqtxdg' 'gtk2')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        use-system-qt5-headers.patch qt-5.9.patch)
sha512sums=('833d9716f10a5dbe7de19f5dc6ef2b11af3baac9108b96ab09b38f2dd71471257a26a4947bd0ff2f2258b7b35c66a3c298284aba269c327f1a255f98829e9989'
            '29152204a36efc5fc2376b74be2ebe1d9cb253f9371ca8b0e9bf95c415c903f956d0d400eaffb3f708f81b19acd0a5383647af3a5a10cf6a61dfa586eb1bd57b'
            'b5c8a41184e5fd4d9bc6b7b3f1028181e1c98b5c2ca6cc072efcd49b643844f7999b13de66ffdf0260825ae1e1921437476d4865c96043dd376a4c452536379c')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../use-system-qt5-headers.patch
  patch -p1 -i ../qt-5.9.patch
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
