# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.2.5
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'qt5-styleplugins')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        qt5.7.1.patch)
sha512sums=('70127543e5b14efd1960793513889b948662128148c6eb9c6bf95f3e950d6d030f59e0f3c3707d83ad1f43599057dcd132b7fcae1d4985ad2354c2db5fd14685'
            '1f94c0925deb6659721ced265d690b5d058c8e8482cadfad572efe4a0d1888ba42bb214de5d8c29236087d96c4ab7266c5fb1c8eeeb7b30cef6c3708744e1f0a')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../qt5.7.1.patch

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri

  sed -i 's|qt5xdgiconloader/2.0.0|qt5xdgiconloader/3.1.0|' platformthemeplugin/main.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
