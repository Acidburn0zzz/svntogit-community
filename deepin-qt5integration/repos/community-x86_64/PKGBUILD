# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.2.7
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'qt5-styleplugins')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz")
sha512sums=('167a454818a547bc8cbfb3ab2caed435d8590473f1b3a9408012d5fd644f82f3a0ebf63450163441a58f9fe0ce7958a7fbdd424c644cdcbe6f08c5feec7fdaaa')

prepare() {
  cd qt5integration-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri

  sed -i 's|qt5xdgiconloader/2.0.0|qt5xdgiconloader/3.1.0|' platformthemeplugin/main.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
