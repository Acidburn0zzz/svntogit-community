# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=0.2.4.20171017
_commit=a045b17e86735779b48ea734bd18f2b2e25b4251
pkgrel=1
pkgdesc='Deepin Qt5 platform plugin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'qt5-styleplugins')
makedepends=('xcb-util-renderutil' 'deepin-file-manager' 'qt5-xcb-private-headers')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$_commit.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$_commit.tar.gz")
sha512sums=('a2c83fe53d5c1fe264c91dae14b88389d9a5404607144082013306c6ca16c122393b7ca047ec26c1618ce776ffd6174935491d972c25530b30950f36e2ff1ff3')

prepare() {
  mv qt5integration-{$_commit,$pkgver}

  cd qt5integration-$pkgver
  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri

  sed -i 's|qt5xdgiconloader/2.0.0|qt5xdgiconloader/3.1.0|' platformthemeplugin/main.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
