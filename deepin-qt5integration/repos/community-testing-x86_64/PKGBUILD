# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5integration
pkgver=5.1.0
pkgrel=1
pkgdesc='Qt platform theme integration plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5integration"
license=('GPL3')
depends=('dtkwidget' 'libqtxdg' 'deepin-qt5dxcb-plugin')
makedepends=('xcb-util-renderutil' 'deepin-file-manager')
replaces=('qt5dxcb-plugin')
conflicts=('qt5dxcb-plugin')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5integration/archive/$pkgver.tar.gz"
        deepin-qt5integration-qt5.14.patch)
sha512sums=('a550021508f968ec7501595a031dd400633216de704a48cd294a9b00b2a5aae57d88a3b9322f945d5adc4d13eb98b5eef6621e5967508e2a1a7c316cf0592feb'
            'd227f060b3dfd324c19a957e90a210256649c366645bb2e712dbdefb0b9287dc0ae97fa14e0252d168323b06e7ba526bc351600273a4afb7c3fa2b8d59e42aa0')

prepare() {
  cd qt5integration-$pkgver
  patch -p1 -i ../deepin-qt5integration-qt5.14.patch # Fix build with Qt 5.14

  sed -i 's/^.*DPlatformWindowHandle.*$/Dtk::Widget::DPlatformWindowHandle handle((QWidget *)w);/' styleplugins/dstyleplugin/config.tests/dtkwidget/main.cpp
  sed -i 's/QSpinBox/DSpinBox/' styleplugins/chameleon/chameleonstyle.cpp
}

build() {
  cd qt5integration-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5integration-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
