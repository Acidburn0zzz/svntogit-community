# Maintainer:
# Contributor: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: neodreams <yanbrodeur@gmail.com>

pkgname=giggle
pkgver=0.7
pkgrel=9
pkgdesc='Git repository viewer'
arch=(x86_64)
url='https://wiki.gnome.org/Apps/giggle'
license=(GPL)
depends=(git gtksourceview3 vte3)
makedepends=(intltool itstool gnome-common yelp-tools)
source=("https://ftp.gnome.org/pub/GNOME/sources/giggle/$pkgver/$pkgname-$pkgver.tar.xz"
        gtksourceview3.7.patch
        vte2.91.patch)
sha256sums=('ae2e2237fa0d0999d99d0a0ee9b7ec147e80bd2472d59a045b2b01eb02261f59'
            'a29cc899d17645d43a39ac94551c4af896f67983c40fab3bfc585881217f30df'
            'eec654cd3ec31144461cbfc01f4c0b6690e0cdd1b08e057cd5afe99acc17dfed')

prepare() {
  cd $pkgname-$pkgver

  patch -Np1 -i ../gtksourceview3.7.patch
  patch -Np1 -i ../vte2.91.patch
  NOCONFIGURE=1 AUTOPOINT=intltoolize gnome-autogen.sh
}

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" check
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" install
}

# getver: -u 7 git.gnome.org/browse/giggle/plain/NEWS
# vim:set ts=2 sw=2 et:
