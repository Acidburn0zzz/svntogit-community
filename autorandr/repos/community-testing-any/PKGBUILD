# Maintainer: David Runge <dave@sleepmap.de>

pkgname=autorandr
pkgver=1.8.1
pkgrel=2
pkgdesc="Auto-detect connected display hardware and load appropiate X11 setup using xrandr"
arch=('any')
url="https://github.com/phillipberndt/autorandr"
license=('GPL3')
depends=('python-setuptools' 'xorg-xrandr')
optdepends=('xorg-xdpyinfo: For detecting the primary XRandR output')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/phillipberndt/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('f0a8fb272bcc9fdd334dd0269da23988e7552a46497c012042d893f6ce9648e7c1ddacb7685a963301666444922193afa9a2a3e7fa4b4981db67c8f6fad22516')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}/"
  make DESTDIR="${pkgdir}" PREFIX=/usr install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
