# Maintainer: Anatol Pomozov
# Contributor: Leen Jewel <leenjewel@gmail.com>
# Contributor: Auguste Pop <auguste [at] gmail [dot] com>

pkgname=msgpack-c
pkgver=1.4.0
_tag=cpp-$pkgver
pkgrel=1
pkgdesc='An efficient object serialization library'
arch=(i686 x86_64)
url='http://msgpack.org/'
license=(Apache)
depends=(glibc)
checkdepends=(gtest)
source=($pkgname-$pkgver.zip::https://github.com/msgpack/msgpack-c/archive/$_tag.zip)
sha1sums=('1ce993992f44e5ebdd575b9829d6c1b48848c6d6')

build() {
  cd msgpack-c-$_tag 
  ./bootstrap
  ./configure --prefix=/usr
  make
}

check() {
  cd msgpack-c-$_tag
  make check  
}

package() {
  cd msgpack-c-$_tag
  make DESTDIR="$pkgdir" install
}
