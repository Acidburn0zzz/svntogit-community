# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidinet
pkgver=0.6.0
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="http://qmidinet.sourceforge.net/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libjack.so' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('cdf5e508a8cf384147c4a02e0378ab9b609561129a57b2e6a77c3f5322d33a39776f8d56a581b125f45c0642bc8665f6402082deec5b34513779d8db2fd17bd7')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
