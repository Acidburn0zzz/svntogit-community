# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=qmidinet
pkgver=0.6.1
pkgrel=1
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="https://qmidinet.sourceforge.io"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libjack.so'
'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('faecc5bbeb076c640dfb72a76b3e11b7d2a741ffd6416b73870422f56b8116f5c0a2ef3b68704057cd3b2cdce23bddac18e997c04a0620713bcab0b41a473e54')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
