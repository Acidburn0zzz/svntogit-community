# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qmidinet
pkgver=0.4.3
pkgrel=2
pkgdesc="A MIDI Network Gateway via UDP/IP Multicast"
arch=('x86_64')
url="http://qmidinet.sourceforge.net/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'hicolor-icon-theme' 'jack' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/project/${pkgname}/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('17492e07cbc9a40d9dae0bb4dbe0f62edbd9071de8462a728168552a8822040f305a7a921304b4adb3c13f1baf985efe609ff57c18e61e9a76261835fec614b3')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
