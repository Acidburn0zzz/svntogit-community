# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Sefa Eyeoglu <contact@scrumplex.net>

pkgname=libliftoff
pkgver=0.1.0
pkgrel=1
pkgdesc='Lightweight KMS plane library'
arch=(x86_64)
url=https://gitlab.freedesktop.org/emersion/libliftoff
license=(MIT)
depends=(libdrm)
makedepends=(
  git
  meson
  ninja
)
_tag=a58b638736ad5efe0393ace97522c49949ba2f77
source=(git+https://gitlab.freedesktop.org/emersion/libliftoff.git#tag=${_tag})
b2sums=(SKIP)

build() {
  arch-meson libliftoff build
  meson compile -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  install -Dm 644 libliftoff/LICENSE -t "${pkgdir}"/usr/share/licenses/libliftoff/
  install -Dm 644 libliftoff/README.md -t "${pkgdir}"/usr/share/doc/libliftoff/
}

# vim: ts=2 sw=2 et:
