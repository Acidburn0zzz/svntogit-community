# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.com>
# Contributor: libertylocked <libertylocked@disroot.org>

pkgname=bitwarden-cli
pkgver=1.19.0
_jslibcommit='c7a9a89e0a0d83fce6fa42c6c4be8792cda7bd30'
pkgrel=1
pkgdesc="The command line vault"
arch=('x86_64')
url="https://github.com/bitwarden/cli"
license=('GPL3')
depends=('gcc-libs')
makedepends=('git' 'npm' 'node-gyp' 'nodejs-lts-fermium')
options=('!strip')
source=("bitwarden-cli-${pkgver}.tar.gz::https://github.com/bitwarden/cli/archive/v${pkgver}.tar.gz"
        "jslib-${_jslibcommit}.tar.gz::https://github.com/bitwarden/jslib/archive/${_jslibcommit}.tar.gz")
sha512sums=('a54c35b951c0784a93db517f72b9bdb5cf1c0c49ee2af27a6947b1d0407dd1a5752f4c0edf208491b7c0748780ee7dd6f710c011640ccd00beb1bbd96606752a'
            '2c2a17fe6bf7bc26f1a92955d015e0080499810ca8949a68e806cb0732e0d337db0a93be61990cbde411b08bc254b9bc57cd34d7fd726c28f47c5c5321e75c3d')

prepare() {
	cd cli-${pkgver}
	rmdir -v jslib
	ln -sv ../jslib-${_jslibcommit} jslib
}

build() {
	cd cli-${pkgver}
	export npm_config_cache="$srcdir/npm_cache"
	pushd jslib
	npm install
	popd
	npm install
	npm run build:prod
	npm run clean
	npm run package:lin
	./dist/linux/bw completion --shell zsh > _bw
}

package() {
	cd cli-${pkgver}
	install -vDm755 dist/linux/bw -t "${pkgdir}/usr/bin"
	# package zsh completions
	install -vDm644 "${srcdir}/cli-${pkgver}/_bw" -t "${pkgdir}/usr/share/zsh/site-functions"
}
