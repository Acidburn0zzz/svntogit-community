# Maintainer: Alexander Epaneshnikov <alex19ep@archlinux.com>
# Contributor: libertylocked <libertylocked@disroot.org>

pkgname=bitwarden-cli
pkgver=1.21.0
_jslibcommit='995ad076cca1795ec78f87b81ed3cd2e727703d1'
pkgrel=1
pkgdesc="The command line vault"
arch=('x86_64')
url="https://github.com/bitwarden/cli"
license=('GPL3')
depends=('gcc-libs')
makedepends=('git' 'npm' 'node-gyp' 'nodejs-lts-gallium')
options=('!strip')
source=("bitwarden-cli-${pkgver}.tar.gz::https://github.com/bitwarden/cli/archive/v${pkgver}.tar.gz"
        "jslib-${_jslibcommit}.tar.gz::https://github.com/bitwarden/jslib/archive/${_jslibcommit}.tar.gz")
sha512sums=('6883b2ee9641066f22cb70003f164c93d12b4e02377b6287435e053b4cc4969de4f3b6524e6f5b1b19d9df231e6eb38ea21fa3e3b06be561e89ca6c80432684c'
            'ae138e86f05f6b151b66e4242f5a9c4bb1b124dd83ce3ffbf71584090bfcce21c7845b74598c84a1380575d514c5e5857e0ab2999e89abafec5e9c799299437d')

prepare() {
	cd cli-${pkgver}
	rmdir -v jslib
	ln -sv ../jslib-${_jslibcommit} jslib
}

build() {
	cd cli-${pkgver}
	export npm_config_cache="$srcdir/npm_cache"
	pushd jslib
	npm install
	popd
	npm install
	npm run build:prod
	npm run clean
	npm run package:lin
	./dist/linux/bw completion --shell zsh > _bw
}

package() {
	cd cli-${pkgver}
	install -vDm755 dist/linux/bw -t "${pkgdir}/usr/bin"
	# package zsh completions
	install -vDm644 "${srcdir}/cli-${pkgver}/_bw" -t "${pkgdir}/usr/share/zsh/site-functions"
}
