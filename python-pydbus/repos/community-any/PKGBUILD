# Maintainer: Maxim Baz <$pkgbase at maximbaz dot com>
# Contributor: Christoph Gysin <christoph.gysin@gmail.com>

_name=pydbus
pkgname=('python-pydbus' 'python2-pydbus')
pkgver=0.6.0
pkgrel=2
pkgdesc='Pythonic D-Bus library'
arch=('any')
url="https://github.com/LEW21/${_name}"
license=('LGPL')
makedepends=('python-setuptools' 'python2-setuptools')
# checkdepends=('xorg-server-xvfb' 'python-gobject' 'python2-gobject')
source=("${_name}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('dacdf0498a831c8b43a4f3d985d64f99f3093a8aaf4e2ea99981c30fdf1a42da')

prepare() {
    mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
    cp -a "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
    cd "${pkgname[0]}-${pkgver}"
    python setup.py build

    cd ../"${pkgname[1]}-${pkgver}"
    python2 setup.py build
}

# https://github.com/LEW21/pydbus/issues/85
# check() {
#     cd "${pkgname[0]}-${pkgver}"
#     # ./tests/run.sh
#     # LANG='en_US.UTF-8' xvfb-run python setup.py test

#     cd ../"${pkgname[1]}-${pkgver}"
#     # ./tests/run.sh
#     # LANG='en_US.UTF-8' xvfb-run python2 setup.py test
# }

package_python-pydbus() {
    depends=('python' 'python-gobject')

    cd "${pkgname[0]}-${pkgver}"
    python setup.py install --skip-build \
        --optimize=1 \
        --prefix=/usr \
        --root="${pkgdir}"

    install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname[0]}" README.rst
}

package_python2-pydbus() {
    depends=('python2' 'python2-gobject')

    cd "${pkgname[1]}-${pkgver}"
    python2 setup.py install --skip-build \
        --optimize=1 \
        --prefix=/usr \
        --root="${pkgdir}"

    install -Dm644 -t "${pkgdir}/usr/share/doc/${pkgname[1]}" README.rst
}

# vim:set ts=4 sw=4 et:
