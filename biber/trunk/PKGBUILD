# $Id$
# Maintainer: RÃ©my Oudompheng <remy@archlinux.org>

pkgname=biber
pkgver=2.7 # for biblatex 3.7
pkgrel=1
pkgdesc="A Unicode-capable BibTeX replacement for biblatex users"
arch=('any')
license=('PerlArtistic' 'GPL')
url="https://sourceforge.net/projects/biblatex-biber/"
# Encode::* modules are not real dependencies.
depends=('perl-autovivification'
         'perl-business-isbn'
         'perl-business-ismn'
         'perl-business-issn'
         'perl-class-accessor'
         'perl-data-compare'
         'perl-data-dump'
         'perl-data-uniqid'
         'perl-datetime-calendar-julian'
         'perl-datetime-format-builder'
         'perl-file-slurp'
         'perl-io-string' # required by Biber::Output::bblxml
         'perl-ipc-run3'
         'perl-lingua-translit'
         'perl-list-allutils'
         'perl-log-log4perl'
         'perl-lwp-protocol-https'
         'perl-mozilla-ca'
         'perl-regexp-common'
         'perl-sort-key'
         'perl-text-bibtex'
         'perl-text-csv'
         'perl-text-roman'
         'perl-unicode-linebreak'
         'perl-xml-libxml-simple'
         'perl-xml-libxslt'
         'perl-xml-writer')
# ExtUtils::LibBuilder and Config::AutoConf are not actual makedepends
makedepends=('perl-module-build' 'perl-test-differences')
checkdepends=('perl-capture-tiny' 'perl-file-which')
options=('!emptydirs')
# The sourceforge tarball is missing bblxml.pm
source=("https://github.com/plk/biber/archive/v${pkgver}.tar.gz"
        "revert-unicode-collate-update.patch")
sha256sums=('a0afc4cd97d56f4f433e476de1d4bd9bf9740eb29191bf616b1ea3f424408d9f'
            '5f87864e6e3480bdaf085d8025042f97b852a482932d699416d21a969e583208')

prepare() {
  cd "${srcdir}/biber-${pkgver}"
  # Use Unicode::Collate from system perl (impacts unit tests)
  patch -Np1 -i "${srcdir}/revert-unicode-collate-update.patch"
  # Disable long year tests on 32 bit
  [[ $CARCH = "i686" ]] && sed -i '/17000002/ s+.*+eq_or_diff("dummy", "dummy", "skip 32 bit");+' t/dateformats.t
}

build() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  perl Build.PL
  ./Build
}

check() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  LC_CTYPE=en_US.UTF-8 BIBER_DEV_TESTS=1 ./Build test
}

package() {
  cd "${srcdir}/biber-${pkgver}"
  unset PERL5LIB PERL_MM_OPT PERL_MB_OPT PERL_LOCAL_LIB_ROOT
  ./Build install installdirs=vendor destdir="${pkgdir}"
}
