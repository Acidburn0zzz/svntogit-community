# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=rime-prelude
pkgname=(rime-prelude librime-data)
pkgver=0.0.0.20200907
_commit=4dbf45a681d7859527636b07d30027e9cc91ba48
pkgrel=1
pkgdesc="Essential files for building up your Rime configuration"
arch=('any')
url="https://github.com/rime/rime-prelude"
license=('LGPL')
source=("https://github.com/rime/rime-prelude/archive/$_commit/rime-prelude-$_commit.tar.gz")
sha512sums=('3a5938a51a469e12dca0e3f4e98bb2b8b0c6e3a296c7421fbb31001cc16f237d11c7eef706518c4d5b95ec40e3a8684804e3f406e49e17f133bcfe7c61b95f7c')

package_rime-prelude() {
  pkgdesc="Essential files for building up your Rime configuration"

  cd $pkgname-$_commit
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
}

package_librime-data() {
  pkgdesc="Meta package of preset librime data"
  depends=('rime-prelude' 'rime-essay' 'rime-luna-pinyin' 'rime-bopomofo' 'rime-cangjie' 'rime-stroke' 'rime-terra-pinyin')
  replaces=('brise')
  conflicts=('brise')
}
