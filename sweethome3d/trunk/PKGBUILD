# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: jsteel <mail at jsteel dot org>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Tdy  <tdy@gmx.com>
# Contributor: Biginoz <biginoz at free.fr>
# Contributor: Archan Paul <paul.archan@gmail.com>

pkgbase=sweethome3d
pkgname=(sweethome3d-common sweethome3d)
pkgver=5.4
pkgrel=2
pkgdesc="An interior design application to draw the plan of your house in a 3D environment"
arch=('i686' 'x86_64')
url="http://www.sweethome3d.com/"
license=('GPL' 'custom')
depends=('java-runtime>=8' 'shared-mime-info' 'desktop-file-utils')
source=("${pkgbase}.sh"
        "${pkgbase}.desktop"
        "${pkgbase}.xml")
source_i686=("SweetHome3D-5.4-linux-x86.tgz"::"http://downloads.sourceforge.net/${pkgbase}/SweetHome3D-${pkgver}-linux-x86.tgz")
source_x86_64=("SweetHome3D-5.4-linux-x64.tgz"::"http://downloads.sourceforge.net/${pkgbase}/SweetHome3D-${pkgver}-linux-x64.tgz")
sha256sums=('c1a76d4131ddf04375bab3e98d646543b4967af623a6aee0e19bac7d40d7ecef'
            '5eea3337d956d773b05ddef69fe9d34b940ff550370dc92bf307f1b9a3957f9e'
            'ec0ad1a0671f708af68ced46bec1f4ab377e24ca1a0a9984867ee5fe484f57c5')
sha256sums_i686=('f5778277c11be1b8f069be1775964a9eae1f20e3d20eb030cffe16f8f5252fdf')
sha256sums_x86_64=('4f2f69495aac1cb2fd9a7c7235b596b1916fee4f77f348e8381cf136c56e5ae4')

package_sweethome3d-common() {
  pkgdesc="An interior design application to draw the plan of your house in a 3D environment (common files)"
  arch=('any')
  install="${pkgbase}.install"
  # Desktop and launchers
  cd "${srcdir}"
  install -m 755 -D "${pkgbase}.sh" "${pkgdir}/usr/bin/${pkgbase}"
  install -m 755 -D "${pkgbase}.desktop" "${pkgdir}/usr/share/applications/${pkgbase}.desktop"
  install -m 644 -D "${pkgbase}.xml" "${pkgdir}/usr/share/mime/packages/${pkgbase}.xml"
  # Install licenses
  cd "SweetHome3D-${pkgver}"
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 755 -t "${pkgdir}/usr/share/licenses/${pkgname}" THIRDPARTY-LICENSE-*
  # Install icon
  install -m 644 -D "SweetHome3DIcon.png" "${pkgdir}/usr/share/pixmaps/${pkgbase}.png"
  # Install javaws.jar from jre8
  install -m 755 -d "${pkgdir}/usr/share/java/${pkgbase}"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgbase}" jre8/lib/javaws.jar
  # Install main program and libraries
  cd "lib"
  install -m 644 -t "${pkgdir}/usr/share/java/${pkgbase}" *.jar
  # Java3D 1.5 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgbase}/java3d-1.5"
  mv -t "${pkgdir}/usr/lib/${pkgbase}/java3d-1.5" \
    "${pkgdir}"/usr/share/java/${pkgbase}/{j3dcore.jar,j3dutils.jar,vecmath.jar}
  # Java3D 1.6 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgbase}/java3d-1.6"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgbase}/java3d-1.6" java3d-1.6/*.jar
}

package_sweethome3d() {
  arch=('i686' 'x86_64')
  depends=('libxext' 'libgl' 'libxrender' 'sweethome3d-common')
  cd "${srcdir}/SweetHome3D-${pkgver}"
  # Install licenses
  install -m 755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "THIRDPARTY-LICENSE-JAVA3D.TXT"
  cd "lib"
  # Java3D 1.5 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgbase}/java3d-1.5"
  install -m 755 -t "${pkgdir}/usr/lib/${pkgbase}/java3d-1.5" libj3dcore-ogl.so
  # Java3D 1.6 libraries
  install -m 755 -d "${pkgdir}/usr/lib/${pkgbase}/java3d-1.6"
  install -m 644 -t "${pkgdir}/usr/lib/${pkgbase}/java3d-1.6" java3d-1.6/*.so
}

