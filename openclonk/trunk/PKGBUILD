# $Id$
# Maintainer: Jonathan Steel <mail at jsteel dot org>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Wesley <rudirennsau at hotmail dot com>

pkgname=openclonk
pkgver=5.3.2
pkgrel=2
pkgdesc="A multiplayer-action-tactic-skill game"
arch=('i686' 'x86_64')
url="http://openclonk.org"
license=('custom')
depends=('gtk2' 'glew' 'sdl_mixer' 'libxpm'  'hicolor-icon-theme' 'libupnp')
makedepends=('cmake' 'boost' 'imagemagick' 'mesa')
install=$pkgname.install
source=(http://hg.openclonk.org/$pkgname/archive/$pkgname-release-$pkgver-src.tar.gz)
md5sums=('0d43b03f10c8af7a9022f023b21bb1b3')

build() {
  cd "$srcdir"/$pkgname-release-$pkgver-src

  [[ -d build ]] && rm -rf build
  mkdir build && cd build

  cmake ../ -DCMAKE_INSTALL_PREFIX=/usr

  make 
}

package() {
  cd "$srcdir"/$pkgname-release-$pkgver-src/build

  make DESTDIR="$pkgdir"/ install

  install -Dm644 clonk.png "$pkgdir"/usr/share/pixmaps/clonk.png
  install -dm755 "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 ../licenses/*.txt "$pkgdir"/usr/share/licenses/$pkgname
}
