# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>

pkgname=re2
_tag='2016-09-01'
pkgver=${_tag//-/}
pkgrel=1
pkgdesc='C++ library for fast, safe, thread-friendly alternative to backtracking regular expression engines like those used in PCRE, Perl, and Python'
url='https://github.com/google/re2'
depends=(gcc-libs)
arch=(i686 x86_64)
license=(BSD)
source=(re2-$pkgver.zip::https://github.com/google/re2/archive/$_tag.zip
        gcc-61-fix.patch)
sha1sums=('fbde84ba1661ac4ff7dea657c01b0e69c1c26eb4'
          'd233c798c40dab4c9ba973316145715308df02f1')

prepare() {
  cd re2-$_tag
  # fix for https://github.com/google/re2/issues/102
  patch -p1 < ../gcc-61-fix.patch
}

build() {
  cd re2-$_tag
  make prefix=/usr
}

check() {
  cd re2-$_tag
  make prefix=/usr test
}

package() {
  cd re2-$_tag
  make prefix=/usr DESTDIR="$pkgdir" install
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
