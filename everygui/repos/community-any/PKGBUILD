# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=everygui
pkgver=0.99.b
pkgrel=6
pkgdesc="GTK+ interface for 'everything'"
arch=('any')
url="http://everygui.sourceforge.net"
license=('GPL' 'LGPL')
depends=('pygtk')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
	'setup.patch')
md5sums=('78953de66b973b97b194d679969acc9f'
         '5c0b0eee51d8c4955416c318d893574d')

build() {
  cd ${srcdir}/${pkgname}

  find -name everygui -exec sed -i 's|python /usr/src/everygui/everygui|exec python2 /usr/lib/python2.7/site-packages/everygui/everygui|' {} \;
  find -name egdesign -exec sed -i 's|python /usr/src/everygui/designer|exec python2 /usr/lib/python2.7/site-packages/everygui/designer|' {} \;
  patch -p1 -i ../setup.patch
}

package() {
  cd ${srcdir}/${pkgname}

  python2 setup.py install --root=${pkgdir} --optimize=1
  chmod 0777 ${pkgdir}/usr/share/everygui/egconfig
}
