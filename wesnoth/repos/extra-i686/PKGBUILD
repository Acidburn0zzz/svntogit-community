# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Jacobo Arvelo <unix4all@ya.com>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>

pkgname=wesnoth
pkgver=1.8.6
pkgrel=4
pkgdesc="A turn-based strategy game on a fantasy world"
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.wesnoth.org/"
depends=('sdl_ttf' 'sdl_net' 'sdl_mixer' 'sdl_image' 'fribidi' 'boost-libs' 'pango' 'lua' "wesnoth-data=${pkgver}" 'dbus-core' 'python2')
makedepends=('boost')
install=wesnoth.install
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.bz2
        wesnoth-libpng-1.4.0.patch
        wesnoth-1.8.6-boost-foreach.patch
        wesnoth.tmpfiles.conf)
md5sums=('f1c3179869b01b61e253e74aeb241034'
         '3b0d2edcb45e8ecc8c790f92bdcb0087'
         'dc414ab7e1334941548e9db6db9c2460'
         'b8122f5054e3895c9c054e87460869dc')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/wesnoth-libpng-1.4.0.patch"
  patch -Np1 -i "$srcdir/wesnoth-1.8.6-boost-foreach.patch"
  ./autogen.sh
  ./configure --prefix=/usr --localstatedir=/var --enable-tools \
      --enable-editor --enable-server --with-fifodir=/run/wesnothd
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install-exec
  install -dm755 "$pkgdir/usr/lib/tmpfiles.d/"
  install -m644 "$srcdir/wesnoth.tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/wesnoth.conf"

  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/wesnoth_addon_manager"
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/wmlindent"
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/wmlscope"
  sed -i "s|#!/usr/bin/env python|#!/usr/bin/env python2|" "$pkgdir/usr/bin/wmllint"
}
