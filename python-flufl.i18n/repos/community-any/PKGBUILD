# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.i18n
pkgname=python-flufl.i18n
pkgver=3.1.3
pkgrel=3
pkgdesc="A high level API for internationalization"
arch=('any')
url="https://gitlab.com/warsaw/flufl.i18n"
license=('Apache')
depends=('python-atpublic')
makedepends=('python-setuptools')
checkdepends=('python-sybil' 'python-pytest')
provides=('python-flufl-i18n')
replaces=('python-flufl-i18n')
# missing files to run tests: https://gitlab.com/warsaw/flufl.i18n/-/issues/9
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("https://gitlab.com/warsaw/${_name}/-/archive/${pkgver}/${_name}-${pkgver}.tar.gz")
sha512sums=('114840595ac192da7d0213cb142001d4cdce207b470cc558d42d0a234b38c226c2158f50eebe85bbb1383929bd584d1ee72394534e3841e3e2c328375677c9e7')
b2sums=('3f10ad2330664c5fd09e56c342e340a4843bd0082eabe0e7d47a02c74fdbc76ebbe575d217e0055b3dc34317f89934c2c486f3223349b6d547af7e963c77fc89')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't need code coverage during tests
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
