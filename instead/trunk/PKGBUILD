# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Kosyh <p.kosyhgmail.com>

pkgname=instead
pkgver=3.0.1
pkgrel=1
pkgdesc="a quest interpreter"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/instead/"
license=('GPL')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'lua' 'gtk2')
makedepends=('cmake')
optdepends=('instead-launcher: install and update INSTEAD games from net')
source=(http://downloads.sourceforge.net/project/instead/instead/${pkgver}/instead_${pkgver}.tar.gz)
sha256sums=('eb5b1e2dfde2c13b391fe3275e8886d202f9479a7b58b4d7daa1fd3272fdd87a')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" PREFIX=/usr install

  cd "${srcdir}"/$pkgname-$pkgver
  cp -a doc/*.{html,pdf} doc/instead.txt doc/examples "$pkgdir"/usr/share/doc/instead/
}
