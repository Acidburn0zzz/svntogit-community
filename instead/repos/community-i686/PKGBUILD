# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Peter Kosyh <p.kosyhgmail.com>

pkgname=instead
pkgver=3.1.1
pkgrel=1
pkgdesc="a quest interpreter"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/instead/"
license=('MIT')
depends=('sdl_image' 'sdl_mixer' 'sdl_ttf' 'lua' 'gtk2')
makedepends=('cmake')
optdepends=('instead-launcher: install and update INSTEAD games from net')
source=(http://downloads.sourceforge.net/project/instead/instead/${pkgver}/instead_${pkgver}.tar.gz)
sha256sums=('cac78d4fc8c10f294bd0aad78a7885b1b686e899c8c070ccdb2ff3ca8c4ef8fc')

build() {
  cd "${srcdir}"
  mkdir -p build
  cd build
  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${srcdir}"/$pkgname-$pkgver
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" PREFIX=/usr install

  cd "${srcdir}"/$pkgname-$pkgver
  cp -a doc/*.{html,pdf} doc/instead.txt doc/examples "$pkgdir"/usr/share/doc/instead/

  install -Dm0644 COPYING "$pkgdir"/usr/share/licenses/instead/COPYING
}
