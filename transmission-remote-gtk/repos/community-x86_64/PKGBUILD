# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: SÃ©bastien LEBEAU <sebcbi1 at gmail dot com>
# Contributor: Patrick Griffis <tingping@fedoraproject.org>

pkgname=transmission-remote-gtk
pkgver=1.5.0
pkgrel=1
pkgdesc="GTK remote control for the Transmission BitTorrent client"
arch=(x86_64)
url="https://github.com/transmission-remote-gtk/transmission-remote-gtk"
license=(GPL2)
depends=(curl glib2 gtk3 json-glib libappindicator-gtk3 libproxy geoip)
makedepends=(meson)
# The new maintainer does not sign releases
#source=(${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
source=(${url}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha256sums=(51d29951fa79ad721c03bf4abd31c4579a29e5c12737ffbbbe4abde9f2fff6d5)
#validpgpkeys=('108BF2212A051F4A72B18448B3C7CE210DE76DFC') # Patrick Griffis <tingping@tingping.se>

prepare() {
  # Upstream PR#182
  sed -i "s|install_dir: get_option('mandir')|install_dir: get_option('mandir') / 'man1'|" ${pkgname}-${pkgver}/src/meson.build
}

build() {
  arch-meson -D mrss=disabled ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
  # Upstream PR#180
  mv "${pkgdir}"/usr/share/meta{data,info}
}
