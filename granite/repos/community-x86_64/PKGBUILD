# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ner0 <darkelfdarkelf666@yahoo.co.uk>

pkgname=granite
pkgver=5.0
pkgrel=1
pkgdesc='Library that extends Gtk+'
arch=('x86_64')
url='https://github.com/elementary/granite'
license=('LGPL3')
groups=('pantheon')
depends=('cairo' 'gdk-pixbuf2' 'glib2' 'gtk3' 'libgee' 'pango')
makedepends=('cmake' 'gobject-introspection' 'vala')
provides=('libgranite.so')
source=("granite-${pkgver}.tar.gz::https://github.com/elementary/granite/archive/${pkgver}.tar.gz")
sha256sums=('62dd9a8ddef61d42bb77fdc6626e235d3129bd438533a1575652f7a15fd186c8')

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../granite-${pkgver} \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib'
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
