# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=piper
pkgver=0.4
pkgrel=1
pkgdesc='GTK application to configure gaming mice'
arch=('any')
url='https://github.com/libratbag/piper'
license=('GPL')
depends=('python' 'python-gobject' 'libratbag' 'python-evdev' 'python-lxml' 'python-cairo')
makedepends=('meson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('7d283d217d114d8acbafcfb358e823a307e694c7d7f5d18ac76b2d52db0704ef9873b63f31273b203f6fdf0e5ec8578adf4bd95c2881fcd01f1c5e59e6c929a6')

prepare() {
  cd $pkgname-$pkgver

  # Remove install script
  # This is handled by pacman hooks
  sed -i "/meson.add_install_script('meson_install.sh')/d" meson.build
}

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson ..

  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
