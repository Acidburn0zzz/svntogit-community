# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=piper
pkgver=0.2.901
pkgrel=1
pkgdesc='GTK application to configure gaming mice'
arch=('any')
url='https://github.com/libratbag/piper'
license=('GPL')
depends=('python' 'python-gobject' 'libratbag>=0.9.900' 'python-evdev' 'python-lxml' 'python-cairo')
makedepends=('meson')
source=("https://github.com/libratbag/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('3d1dacd0a4194a1afa4beebf76ad5d85d5a6407c613fba1fcd2274b217dde01a')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver

  # Remove install script
  # This is handled by pacman hooks
  sed -i "/meson.add_install_script('meson_install.sh')/d" meson.build
}

build() {
  mkdir "$srcdir"/$pkgname-$pkgver/build
  cd "$srcdir"/$pkgname-$pkgver/build

  meson --prefix=/usr ..

  ninja
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
