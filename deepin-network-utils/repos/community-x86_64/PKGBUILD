# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.1.0.3
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('52cdff0d58e622ce5ffeed81561d71a8fc0fa3b20d4b9a06432f96932b479c0a552f7ec92afa2a1b2e7ec7f997ef76746526a9cf9f40c6aa84f9e0a943430984')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of third-party commercial company's homepage
  sed -i '/www.baidu.com/i \    "https://www.archlinux.org/favicon.ico",' connectivitychecker.cpp
}

build(){
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
