# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-network-utils
pkgver=5.3.0.2
pkgrel=1
pkgdesc='DDE network utils'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-network-utils"
license=('GPL3')
depends=('deepin-qt-dbus-factory' 'gsettings-qt')
makedepends=('qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-network-utils/archive/$pkgver.tar.gz")
sha512sums=('dd42b257949b6d729da8cc858cf57b534ca04b439ead46e9703f9a3810bd95cfb95dfcd1e4fc0076ec84f5eba72ea5aa427d87be143c4c81368ed3f748c05970')

prepare() {
  cd dde-network-utils-$pkgver
  # Use our own url instead of commercial company's homepage
  sed -i '/www.uniontech.com/i \    "https://www.archlinux.org/favicon.ico",' connectivitychecker.cpp
}

build(){
  cd dde-network-utils-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-network-utils-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
