# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=solidity
pkgver=0.4.5
pkgrel=1
pkgdesc='Contract-Oriented Programming Language'
arch=('i686' 'x86_64')
url='https://github.com/ethereum/solidity'
license=('GPL3')
depends=('boost-libs' 'jsoncpp')
makedepends=('boost' 'cmake' 'git')
source=("git+https://github.com/ethereum/solidity.git#tag=v${pkgver}")
sha256sums=('SKIP')

prepare() {
    cd ${pkgname}
    
    sed -e '/jsoncpp\.cmake/d' -i CMakeLists.txt
}

build() {
    cd ${pkgname}
    
    mkdir build
    cd build
    cmake \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        ..
    make
}

package() {
    cd ${pkgname}/build
    
    make DESTDIR="${pkgdir}" install
}
