# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Federico Cinelli <cinelli@aur.archlinux.org>

pkgname=texmaker
pkgver=5.0.1
pkgrel=1
pkgdesc='Free cross-platform LaTeX editor'
arch=(i686 x86_64)
url="http://www.xm1math.net/texmaker/index.html"
license=(GPL)
depends=(qt5-script)
optdepends=('ghostscript: LaTeX to HTML conversion command')
source=("http://www.xm1math.net/$pkgname/$pkgname-$pkgver.tar.bz2")
sha256sums=('bdf3bcf91405719fc2ff80c2168f6bff2b3cce9a0012923efc43638b2a278b48')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 PREFIX=/usr texmaker.pro
  make
}
package() {
  cd $pkgname-$pkgver

  install -dm755 "$pkgdir"/usr/share
  make INSTALL_PATH="$pkgdir" INSTALL_ROOT="$pkgdir" PREFIX="/usr" install
}
