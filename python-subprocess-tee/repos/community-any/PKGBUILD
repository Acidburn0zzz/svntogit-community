# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.3.0
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools_scm' 'python-toml')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-xdist' 'python-enrich')
optdepends=('python-enrich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('ae917944ec98bc0219fd2cb9570a0d9e0d028e9b0797e24100af153796e71fca2a756555f6f3a7419f8c503bb7a60adfbf8194e06171c148e90625f139d8788a')
b2sums=('6b63f941b2226cd0bcc0ff208ac57b96376b2f0e7746ec1f8c589a87f68c79cadda257f1272e9197687b1f23e36919f890d8790e83bbdaebefedfdb6349835e3')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
