# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.3.4
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools-scm' 'python-setuptools-scm-git-archive' 'python-toml')
checkdepends=('molecule' 'python-mock' 'python-pytest' 'python-pytest-xdist' 'python-enrich')
optdepends=('python-enrich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('6a97ddc75b7e7954bb7f5f3bf05807e8dd892869cc13e60344c5f40363bdff4ec5441f829cfbcb8c3c4ab7f7b2697c0b1d4a871be8164acd7cfb6fea5032ae4b')
b2sums=('6a98caa111a73ef7062f26796dd3e919d05103ae4de88e2c5ce3acc04d9fbde6b31e39a1cee1984fd8d59b8168ab33faf7ff15c916d52a226ff2cb760fd8c796')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
