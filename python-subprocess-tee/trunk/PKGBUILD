# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.1.6
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools_scm')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-xdist' 'python-rich')
optdepends=('python-rich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('e8b29e95a6a8ca45780336ef5466e6d5ab28895bdfae840ab9ae5bcc4c0c545accabbf1e922ddb80818709757f054d4d702ed77c5b8a205e8a6972cfd1ebe14b')
b2sums=('86f0ba325475736d493d2e520577b74525b8af247c48aaca829ee0da52c3e39ee3f501d2bcb12f1587750e0155c41939d5f12c2cbe093d9f3e170b15163960b0')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
