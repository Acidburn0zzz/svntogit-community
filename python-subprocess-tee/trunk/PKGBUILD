# Maintainer: David Runge <dvzrv@archlinux.org>

_name=subprocess-tee
pkgname=python-subprocess-tee
pkgver=0.3.1
pkgrel=1
pkgdesc="A subprocess.run that works like tee"
arch=('any')
url="https://github.com/pycontribs/subprocess-tee"
license=('MIT')
depends=('python')
makedepends=('python-setuptools_scm' 'python-toml')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-xdist' 'python-enrich')
optdepends=('python-enrich: for rich text rendering')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7a9b60ca940728dddaa0dedb5113409586bbdabb3cf041dec2440f1060abab7ab5dc50dbe58bc8682477d533a53957cc4bd0de099089d9e6c3bec2bd4a3012a0')
b2sums=('0c021161b64d4ca0ef4d7bbb960863e845fb680cc838855324a4257f641ae6e66c43b367d3c73e31ecae166f4c8f8d079f172263e48493ad81655cdb5fb78dad')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # do not require to run with python-pytest-cov
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
