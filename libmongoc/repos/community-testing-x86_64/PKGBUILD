# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.7.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('i686' 'x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl')
checkdepends=('python-pifpaf')
checkdepends_x86_64=('mongodb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('632347c3ebfc717f9f369f8bf3d374ace9f3c139ce96342b0e91b62f3965662fe0a233cbcd0fbeef2387dacf9cc539c60ec1cf8f4efe4f240f5a262206324b5b')

prepare() {
  cd mongo-c-driver-$pkgver
  # Remove check for bundled libbson
  sed -i 's|src/libbson/autogen.sh|/usr/bin/true|' autogen.sh
}

build() {
  cd mongo-c-driver-$pkgver
  PTHREAD_LIBS=-pthread ./autogen.sh --prefix=/usr
  make
}

check() {
  cd mongo-c-driver-$pkgver
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd mongo-c-driver-$pkgver
  make DESTDIR="$pkgdir/" install
}
