# $Id$
# Maintainer:  Chris Brannon <chris@the-brannons.com>
# Maintainer:  Bartłomiej Piotrowski 
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
pkgver=2.0.0rc1
pkgrel=1
pkgdesc='A Just-In-Time Compiler for Lua'
arch=('i686' 'x86_64')
url='http://luajit.org/'
license=('MIT')
depends=('gcc-libs') 
source=(http://luajit.org/download/LuaJIT-${pkgver/rc/-rc}.tar.gz
        Makefile.patch)
md5sums=('26dc243c12cab80b8fe2d9cfa02a81a6'
         'c9dff2a84424fa0af322379a5f115fb7')

build() { 
  cd $srcdir/LuaJIT-${pkgver/rc1}
  patch -Np0 -i $srcdir/Makefile.patch
  make PREFIX=/usr
}

package() {
  cd $srcdir/LuaJIT-${pkgver/rc1}
  make install DESTDIR=$pkgdir PREFIX=/usr

  install -Dm644 $srcdir/LuaJIT-${pkgver/rc1}/COPYRIGHT \
                 $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT

  # Poor man's slot system
  ln -s /usr/share/$pkgname $pkgdir/usr/share/$pkgname-${pkgver/rc1}
}
