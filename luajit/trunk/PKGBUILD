# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Chris Brannon <chris@the-brannons.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Anders Bergh <anders1@gmail.com>

pkgname=luajit
pkgver=2.0.1
pkgrel=2
pkgdesc='Just-in-time compiler and drop-in replacement for Lua 5.1'
arch=('i686' 'x86_64')
url='http://luajit.org/'
license=('MIT')
depends=('gcc-libs') 
source=(http://luajit.org/download/LuaJIT-${pkgver/rc/-rc}.tar.gz
        http://luajit.org/download/v2.0.1_hotfix1.patch)
sha256sums=('2371cceb53453d8a7b36451e6a0ccdb66236924545d6042ddd4c34e9668990c0'
            '143898de3fe84455684fddb92947d36c1a51c6a6e3884813fe5e025bd4652368')

build() { 
  cd $srcdir/LuaJIT-$pkgver
  patch -Np1 -i $srcdir//v2.0.1_hotfix1.patch
  make amalg PREFIX=/usr
}

package() {
  cd $srcdir/LuaJIT-$pkgver
  make install DESTDIR=$pkgdir PREFIX=/usr

  install -Dm644 $srcdir/LuaJIT-$pkgver/COPYRIGHT \
                 $pkgdir/usr/share/licenses/$pkgname/COPYRIGHT
}
