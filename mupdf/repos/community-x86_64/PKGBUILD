# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu (.desktop and install files)

pkgname=mupdf
pkgver=1.2
pkgrel=3
pkgdesc='Lightweight PDF and XPS viewer'
arch=('i686' 'x86_64')
url='http://mupdf.com'
license=('GPL3')
depends=('freetype2' 'libjpeg' 'jbig2dec' 'openjpeg' 'libxext' 'desktop-file-utils' 'xdg-utils')
install=mupdf.install
source=(https://mupdf.googlecode.com/files/$pkgname-$pkgver-source.zip
        mupdf-1.2-fix-dirty-flag-handling.patch)
sha256sums=('9bc9e31ec27c091dad37f70940bd799e46ab6da4299bc58e803bff3dbb07dc3b'
            'd5c9ab3c5e9975909562230569a149a569f44fcd2b24c0b57e889b541a017f10')

prepare() {
  cd $pkgname-$pkgver-source
  patch -Np1 -i "$srcdir"/mupdf-1.2-fix-dirty-flag-handling.patch
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'

  cd $pkgname-$pkgver-source
  rm -rf thirdparty
  make build=release prefix=/usr
}

package() {
  cd $pkgname-$pkgver-source
  make build=release prefix="$pkgdir"/usr install

  sed -i 's/mupdf.xpm/mupdf/' debian/mupdf.desktop
  sed -i 's/application\/x-pdf/application\/x-pdf/' debian/mupdf.desktop
  install -Dm644 debian/mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -Dm644 debian/mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm

  chmod 644 "$pkgdir"/usr/lib/libfitz.a
}
