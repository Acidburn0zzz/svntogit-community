# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Pierre-Paul Paquin <pierrepaulpaquin@gmail.com>
# Contributor: xduugu

pkgbase=mupdf
pkgname=(libmupdf mupdf mupdf-gl mupdf-tools)
pkgver=1.11
pkgrel=5
_openjpeg_version=2.3
pkgdesc='Lightweight PDF and XPS viewer'
arch=('x86_64')
url='http://mupdf.com'
license=('AGPL3')
makedepends=('curl' 'desktop-file-utils' 'freetype2' 'glfw' 'harfbuzz'
             'jbig2dec' 'libjpeg' 'mesa-libgl' 'openjpeg2' 'libxext')
# we need static libs for zathura-pdf-mupdf
options=('staticlibs')
source=("https://mupdf.com/downloads/mupdf-${pkgver/_/}-source.tar.gz"
        '0001-mupdf-openjpeg.patch'
        '0002-Fix-698558-Handle-non-tags-in-tag-name-comparisons.patch'
        '0003-Fix-698539-Do-not-use-xps-font-if-it-could-not-be-loaded.patch'
        '0004-Check-for-integer-overflow-when-validating-new-style-xref-Index.patch'
        '0005-Fix-698540-Check-name-comment-and-meta-size-field-signs.patch'
        'mupdf.desktop'
        'mupdf.xpm')
sha256sums=('209474a80c56a035ce3f4958a63373a96fad75c927c7b1acdc553fc85855f00a'
            'e87b0911121753ab24758a8c2bd533abe347b425f0681e84c945a225c62c63be'
            'cafea4a393cc9b15fe260e8f6238c6d8fff9e5cc849ffe5f5183f9d2798fbbd9'
            '9abcaf5d2399ea67e877bc227eaea7aac9b0d4b6b635466940832eeedb2b37a1'
            '9ccc6886352160e5bb7d7434bb49c4a0042f616cae0ae3e613636f709bb3b8df'
            '69d3227aff15051b71c6d873a042a298e02b79ee2f0d71b7eb304e0c1717f459'
            '70f632e22902ad4224b1d88696702b3ba4eb3c28eb7acf735f06d16e6884a078'
            'a435f44425f5432c074dee745d8fbaeb879038ec1f1ec64f037c74662f09aca8')

prepare() {
  cd $pkgbase-${pkgver/_/}-source

  # remove bundled packages, we want our system libraries
  rm -rf thirdparty/{curl,freetype,glfw,harfbuzz,jbig2dec,libjpeg,openjpeg,zlib}

  # fix function for openjpeg
  patch -Np1 < "${srcdir}/0001-mupdf-openjpeg.patch"
  sed -i "s/__OPENJPEG__VERSION__/${_openjpeg_version}/" source/fitz/load-jpx.c

  # fix includes for jbig2dec
  sed '/^JBIG2DEC_CFLAGS :=/s|$| -I./include/mupdf|' -i Makethird

  # this does not build with openssl 1.1.0, so disable checks
  sed -i 's/pkg-config --exists \(libcrypto\|openssl\)/false/' Makerules

  # 2b16dbd8f73269cb15ca61ece75cf8d2d196ed28
  # Fix 698558: Handle non-tags in tag name comparisons.
  patch -Np1 < "${srcdir}"/'0002-Fix-698558-Handle-non-tags-in-tag-name-comparisons.patch'

  # ab1a420613dec93c686acbee2c165274e922f82a
  # Fix 698539: Don't use xps font if it could not be loaded.
  patch -Np1 < "${srcdir}"/'0003-Fix-698539-Do-not-use-xps-font-if-it-could-not-be-loaded.patch'

  # 82df2631d7d0446b206ea6b434ea609b6c28b0e8
  # Check for integer overflow when validating new style xref Index.
  patch -Np1 < "${srcdir}"/'0004-Check-for-integer-overflow-when-validating-new-style-xref-Index.patch'

  # 0f0fbc07d9be31f5e83ec5328d7311fdfd8328b1
  # Fix 698540: Check name, comment and meta size field signs.
  patch -Np1 < "${srcdir}"/'0005-Fix-698540-Check-name-comment-and-meta-size-field-signs.patch'
}

build() {
  CFLAGS+=' -fPIC'
  CXXFLAGS+=' -fPIC'
  export CFLAGS CXXFLAGS

  HAVE_GLFW='yes'
  SYS_GLFW_CFLAGS="$(pkg-config --cflags glfw3)"
  SYS_GLFW_LIBS="$(pkg-config --libs glfw3) -lGL"
  export HAVE_GLFW SYS_GLFW_CFLAGS SYS_GLFW_LIBS

  cd $pkgbase-${pkgver/_/}-source
  make build=release
}

package_libmupdf() {
  pkgdesc='Library for Lightweight PDF and XPS viewer'

  cd $pkgbase-${pkgver/_/}-source

  make build=release prefix="$pkgdir"/usr install

  rm -rf "$pkgdir"/usr/{bin,share/man}
  mv "$pkgdir"/usr/share/doc/mupdf "$pkgdir"/usr/share/doc/libmupdf

  find "$pkgdir"/usr/include "$pkgdir"/usr/share "$pkgdir"/usr/lib \
    -type f -exec chmod 0644 {} +
}

package_mupdf() {
  pkgdesc='Lightweight PDF and XPS viewer'
  depends=('curl' 'desktop-file-utils' 'freetype2' 'harfbuzz' 'jbig2dec'
           'libjpeg' 'openjpeg2' 'openssl' 'libxext')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/release/mupdf-x11-curl "$pkgdir"/usr/bin/mupdf

  install -D -m0644 docs/man/mupdf.1 "$pkgdir"/usr/share/man/man1/mupdf.1

  install -d "$pkgdir"/usr/share/doc/mupdf
  install -m0644  README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf

  install -D -m0644 ../mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -D -m0644 ../mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm
}

package_mupdf-gl() {
  pkgdesc='Lightweight PDF and XPS viewer with OpenGL backend'
  conflicts=('mupdf')
  provides=('mupdf')
  depends=('desktop-file-utils' 'freetype2' 'glfw' 'harfbuzz' 'jbig2dec'
           'libjpeg' 'openjpeg2')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/release/mupdf-gl "$pkgdir"/usr/bin/mupdf

  install -D -m0644 docs/man/mupdf.1 "$pkgdir"/usr/share/man/man1/mupdf.1

  install -d "$pkgdir"/usr/share/doc/mupdf
  install -m0644 README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf

  install -D -m0644 ../mupdf.desktop "$pkgdir"/usr/share/applications/mupdf.desktop
  install -D -m0644 ../mupdf.xpm "$pkgdir"/usr/share/pixmaps/mupdf.xpm
}

package_mupdf-tools() {
  pkgdesc='Tools for Lightweight PDF and XPS viewer'
  depends=('mupdf')
  depends=('freetype2' 'jbig2dec' 'libjpeg'
         'openjpeg2' 'harfbuzz')

  cd $pkgbase-${pkgver/_/}-source

  install -D -m0755 build/release/mutool "$pkgdir"/usr/bin/mutool
  install -D -m0755 build/release/mujstest "$pkgdir"/usr/bin/mujstest

  install -D -m0644 docs/man/mutool.1 "$pkgdir"/usr/share/man/man1/mutool.1

  install -d "$pkgdir"/usr/share/doc/mupdf-tools
  install -m0644 README COPYING CHANGES "$pkgdir"/usr/share/doc/mupdf-tools
}

