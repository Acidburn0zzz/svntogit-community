# Maintainer: Otreblan <otreblain@gmail.com>

_name=pygls
pkgname=python-pygls
pkgver=0.10.0
pkgrel=1
pkgdesc='Pythonic generic implementation of the Language Server Protocol'
arch=('any')
url=https://github.com/openlawlibrary/pygls
license=('APACHE')
depends=('python-psutil' 'python-pydantic' 'python-typeguard')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-asyncio')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('b131aa222c3a50b80bf819f8550b450b06d1525c66a57827af80b9db5f3a7b8b0a9b2435e5d2b572ddbdca71d1cb105da85ec246900f43a38fc4776e96db005f')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
