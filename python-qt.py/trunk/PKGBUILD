# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=python-qt.py
_pkgname=Qt.py
pkgver=1.2.2
pkgrel=1
pkgdesc='Python compatibility wrapper around all Qt bindings'
arch=('any')
url='https://github.com/mottosso/Qt.py'
license=('MIT')
depends=('python')
optdepends=('python-pyqt5: Qt 5 bindings'
            'pyside2: LGPL Qt 5 bindings')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('365a5a482d8571f6e78ec9d9353e0d020d1513e06ec1503956038f26fca45daeaade6fee95974aebf425f047446ed41e00ae3ccb7a24fc0c924e001b7c21f37c')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  rm "$pkgdir"/usr/LICENSE # Weird

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

