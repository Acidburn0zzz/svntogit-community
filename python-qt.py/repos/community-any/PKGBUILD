# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=python-qt.py
_pkgname=Qt.py
pkgver=1.2.1
pkgrel=1
pkgdesc='Python compatibility wrapper around all Qt bindings'
arch=('any')
url='https://github.com/mottosso/Qt.py'
license=('MIT')
depends=('python')
optdepends=('python-pyqt5: Qt 5 bindings'
	    'pyside2: LGPL Qt 5 bindings')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('34d7cc193a739ee93f1adcbfb8886afdd2999e98805ac53109da3ee3ffa4742e519b943151f944301c0c35072eb43aa29f4cb333dbc85c8e0f083f4de6676ccb')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
  rm "$pkgdir"/usr/LICENSE # Weird

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

