# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Kieslich tobias [funnychar] archlinux org

pkgbase=aubio
pkgname=('aubio' 'python-aubio' 'python2-aubio')
pkgver=0.4.8
pkgrel=1
pkgdesc="A tool for extracting annotations from audio signals"
arch=('x86_64')
url="https://aubio.org/"
license=('GPL3')
makedepends=('doxygen' 'ffmpeg' 'fftw' 'jack' 'libsamplerate' 'libsndfile'
'python-numpy' 'python-setuptools' 'python-sphinx' 'python2-numpy'
'python2-setuptools' 'python2-sphinx' 'txt2man')
checkdepends=('python-nose2' 'python2-nose2')
source=("https://${pkgbase}.org/pub/${pkgbase}-${pkgver}.tar.bz2")
sha512sums=('e58b32beda36865a5243ec7ed4647cf96e3652a25fd319fc697c08f93487f895070dc66b830700b40b879220c2e3a53b8cdc255ce2e34770ba5e34b36a4f51b4')

prepare() {
  cp -av "${pkgbase}-${pkgver}" "${pkgname[2]}-${pkgver}"
  cd "${pkgname[2]}-${pkgver}"
  # python2 shebang
  sed -e '1s/python/&2/' \
    -i "python/lib/${pkgname}/"{__init__,cmd,cut}.py \
    -i "python/tests/"*.py
  # console_scripts names
  sed -e 's/aubio =/aubio2 =/g' \
      -e 's/aubiocut/&2/' \
      -i setup.py
}

build() {
  cd "${pkgbase}-${pkgver}"
  echo "$(pwd)"
  python waf configure --prefix=/usr \
    --enable-fftw3 \
    --htmldir=/usr/share/doc/${pkgname} \
    --docdir=/usr/share/doc/${pkgname} \
    --libdir=/usr/lib
  python waf build
  python setup.py build
  cd ../"${pkgname[2]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  LD_LIBRARY_PATH="${PWD}/build/src" \
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-3.7:${PYTHONPATH}" \
  python setup.py test

  cd ../"${pkgname[2]}-${pkgver}"
  LD_LIBRARY_PATH="${PWD}/build/src" \
  PYTHONPATH="${PWD}/build/lib.linux-${CARCH}-2.7:${PYTHONPATH}" \
  python2 setup.py test
}

package_aubio() {
  pkgdesc="A tool for extracting annotations from audio signals"
  depends=('ffmpeg' 'fftw')
  optdepends=('python2-aubio: Python 2.x interface'
              'python-aubio: Python 3.x interface')
  groups=('pro-audio')
  cd "${pkgname}-${pkgver}"
  python waf --destdir="${pkgdir}" install
  # move doc, as waf configure doesn't honor the htmldir and docdir settings
  mv -v "${pkgdir}/usr/share/doc/lib${pkgname}-doc" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
}

package_python-aubio() {
  pkgdesc="Python 3.x bindings for aubio."
  depends=('aubio' 'python-numpy' 'python-setuptools')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
}

package_python2-aubio() {
  pkgdesc="Python 2.x bindings for aubio."
  depends=('aubio' 'python2-numpy' 'python2-setuptools')
  cd "${pkgname}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,README.md}
}

# vim:set ts=2 sw=2 et:
