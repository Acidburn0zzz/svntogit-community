# $Id: PKGBUILD 149613 2012-02-08 20:20:01Z pierre $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>

pkgname=krusader
pkgver=2.4.0beta3
_pkgver=2.4.0-beta3
pkgrel=2
pkgdesc="An alternative file manager for KDE resembling Midnight Commander"
arch=('i686' 'x86_64')
url="http://krusader.sourceforge.net/"
license=('GPL')
depends=('kdebase-runtime' 'kdebase-lib' 'kdebase-katepart')
makedepends=('cmake' 'automoc4' 'kdeutils-ark')
optdepends=('kdebase-keditbookmarks: to edit bookmarks'
            'kdeutils-ark: extract archive with right-click'
            'kdebase-konsole: open an embedded terminal')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$_pkgver.tar.bz2")
md5sums=('62a02db3f213bb76805ca6c958e9e263')

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"
  make DESTDIR="$pkgdir" install
}
