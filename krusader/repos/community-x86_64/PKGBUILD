# $Id: PKGBUILD 149613 2012-02-08 20:20:01Z pierre $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=krusader
pkgver=2.7.0
pkgrel=1
pkgdesc="Advanced twin panel (commander style) file manager for KDE"
arch=('x86_64')
url="https://krusader.org/"
license=('GPL')
depends=('karchive' 'kbookmarks' 'kcodecs' 'kcompletion' 'kcoreaddons' 'kconfig'
         'kdoctools' 'ki18n' 'kiconthemes' 'kitemviews' 'kio' 'knotifications'
         'kparts' 'solid' 'ktextwidgets' 'kwallet' 'kwidgetsaddons' 'kwindowsystem'
         'kxmlgui' 'kguiaddons' 'hicolor-icon-theme')
makedepends=('cmake' 'extra-cmake-modules')
optdepends=(
            # archive formats
			# look for KrServices::cmdExist calls
            'xz: LZMA and XZ archive support'
            'unzip: ZIP decompression support'
            'zip: ZIP archive support'
            'lhasa: LHA archive support'
            'cpio: cpio archive support'
            'unrar: RAR decompression support'
            'arj: ARJ archive support'
            'unarj: ARJ decompression support'
            'unace: ACE decompression support'
            'p7zip: 7z archive support'
            # additional apps
            'keditbookmarks: Manage Bookmarks functionality'
            'kde-cli-tools: manage file associations, root mode'
            'kompare: file contents comparison'
            'kdiff3: file contents comparison'
            'krename: advanced rename tool'
            'konsole: terminal'
            'ktexteditor: file editing support'
)
source=("http://download.kde.org/stable/krusader/$pkgver/$pkgname-$pkgver.tar.xz")
sha256sums=('8b41522b3b1297d5f293f4fe9becaf255bc06b16d5b19b220653da5e951e9a27')
sha1sums=('c316ff8d1dd89234b2728980c6c471ffee6b73a1')
md5sums=('899bfa000b18040e7dc39ade3ab9b638')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDESU_PATH="/usr/lib/kf5/kdesu"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
