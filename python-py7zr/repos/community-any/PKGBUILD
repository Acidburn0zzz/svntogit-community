# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-py7zr
_pyname=${pkgname#python-}
pkgver=0.18.0
pkgrel=1
pkgdesc='Pure python 7-zip library'
arch=(any)
url="https://github.com/miurahr/${_pyname}"
license=(LGPL)
depends=(python-pycryptodomex
         python-texttable
         python-multivolumefile
         python-pyppmd
         python-pyzstd
         python-brotlicffi
         python-zipfile-deflate64)
optdepends=('python-zstandard: zstd compression')
makedepends=(python-{build,installer}
             python-setuptools-scm
             python-wheel)
checkdepends=(python-pytest-benchmark
              python-pytest-timeout
              pyannotate
              p7zip
              python-py-cpuinfo)
_archive="$_pyname-$pkgver"
source=("https://files.pythonhosted.org/packages/source/${_pyname::1}/$_pyname/$_archive.tar.gz")
sha256sums=('2a7c5b30cc5ca2ac2bbd04f3b3d40c64ccbf6a9d9d60e1a7c42b30011b13b72d')

build(){
	cd "$_archive"
	python -m build -wn
}

check() {
	cd "$_archive"
	python -m pytest -rsx -m 'not benchmark and not remote_data'
}

package() {
	cd "$_archive"
	python -m installer -d "$pkgdir" dist/*.whl
}
