# Maintainer: David Runge <dvzrv@archlinux.org>

_name=wally-cli
pkgname=zsa-wally-cli
pkgver=2.0.1
pkgrel=1
pkgdesc="Flash your ZSA Keyboard the EZ way"
arch=('x86_64')
url="https://github.com/zsa/wally-cli"
license=(MIT)
depends=(glibc zsa-udev)
makedepends=(go libusb)
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsa/${_name}/archive/$pkgver-linux.tar.gz")
sha512sums=('998035e5d932892245cd6db13682501dc5e65a79b724e9ed66733c955bf6646c46c5cb59214160cd5bc5710949dcd43f054ae72b5adb757aa12e3cece92df4bc'
            'b2f9cef04644549b844bdc2a8dd50abd43394f75e13dfbb8986b52b5e2067ffa31879eb04686fe63223e8a44d5a0f2c6e5e22c328039ae9d473646eabf4a8d59')
b2sums=('876a1e237d08c43b90c89ef596ad9015a6f641d3282296b9e3c846a7bf7fa27988605902deaae9e2c95b9c8bf10a2e2304c3ad5b1812001a45b6ca93eb0210bd'
        'b1d01b5d519edaac422f238fb270b1e6d901b1fa9b2a5fbadd70afef740e088b7ae522faf25271a3cc5d9a388f37c8fd2c232cf07225309b7a00564bf27762df')

prepare() {
  mv -v "$_name-$pkgver-linux" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"
  go build -o build
}

package() {
  depends+=('libusb-1.0.so')

  cd "$pkgname-$pkgver"
  install -vDm 755 "build/${_name}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 ../50-zsa.rules -t "${pkgdir}/usr/lib/udev/rules.d/"
  install -vDm 644 license.md -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
