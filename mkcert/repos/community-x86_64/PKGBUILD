# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Simon Legner <Simon.Legner@gmail.com>

pkgname=mkcert
_package="github.com/FiloSottile/${pkgname}"
pkgver=1.4.1
pkgrel=3
pkgdesc="Simple tool for making locally-trusted development certificates"
arch=('x86_64')
url="https://github.com/FiloSottile/mkcert"
license=('BSD')
makedepends=('go-pie' 'git')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/FiloSottile/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('23b008a06ba594f7f5a82b12d00dc129d237421f776bcbf4be04e61a99fa92d49977a616846de45ef98292e31d28bbd543bec70566d14ecfceccee8bbe49038f')

build() {
  cd "${pkgname}-${pkgver}"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}"
}

check() {
  cd "${pkgname}-${pkgver}"
  go test ./...
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
