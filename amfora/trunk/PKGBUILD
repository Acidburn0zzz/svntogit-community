# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Paul Boyd <boyd.paul2@gmail.com>

pkgname=amfora
pkgver=1.9.0
pkgrel=1
pkgdesc='Terminal browser for the Gemini protocol'
arch=(x86_64)
url='https://github.com/makeworld-the-better-one/amfora'
license=(GPL3)
makedepends=(git go)
source=('https://roboticoverlords.org/amfora.png'
        "git+$url#commit=b58a520ac15df8d93251110c1257531e08f3c711") # tag: v1.9.0
b2sums=('1f93ca564c2ef8ef9a94102ac50781b740266a2413cb92ac900240c9324d2dee27a19e4b0a95f45edf933e07ed133f696b4f969e3cfa7cb26a57bfdc63a287cb'
        'SKIP')

prepare() {
  echo Icon=amfora >> $pkgname/amfora.desktop
}

build() {
  cd $pkgname
  go build -buildmode=pie \
    -ldflags "-s -w -linkmode=external -extldflags $LDFLAGS -X main.version=$pkgver -X main.builtBy=Arch_Linux -X main.commit=${source#*=}" \
    -mod=readonly -modcacherw -trimpath
}

package() {
  cd $pkgname
  install -Dm755 amfora "$pkgdir/usr/bin/amfora"
  install -Dm644 amfora.desktop "$pkgdir/usr/share/applications/amfora.desktop"
  install -Dm644 "$srcdir/amfora.png" "$pkgdir/usr/share/pixmaps/amfora.png"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
