# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=6.10.13
pkgrel=1
pkgdesc='Tree explorer plugin for navigating the filesystem'
url='https://github.com/scrooloose/nerdtree'
arch=('any')
license=('custom:WTFPL')
depends=('vim-plugin-runtime')
groups=('vim-plugins')
source=(https://github.com/scrooloose/nerdtree/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e7ac2089c08966989500c60af93fd674a2bf2e76346d64504cf0050f4eea3c5c49f9d0d06cc5ce78d70cd93177f22ea68a3e6ba98d3124dc85dfdefac4b7c00a')
b2sums=('3221a31d8f6155e19ff506edeada09b8c6f2080afd2c2057b09618500fbfe3e449bd8d45b0657dc474f850c368a178976ee8dc441a465a2fca93cc46ba11817e')

prepare() {
  cd nerdtree-${pkgver}
  sed 's|\[s:rootNERDTreePath,|["/usr/share/doc/vim-nerdtree",|' -i autoload/nerdtree.vim
}

package() {
  cd nerdtree-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax nerdtree_plugin lib
  install -Dm 644 CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
