# Contributor: Douglas Soares de Andrade <dsandrade@gmail.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=txt2tags
pkgver=2.6
pkgrel=2
pkgdesc="Txt2tags is a text formatting and conversion tool."
arch=('any')
url="http://www.txt2tags.org"
license=('GPL')
depends=('python2')
optdepends=('ruby: for running t2tmake.rb' 'sh: for running html-update.sh' \
            'vim: for using the vim-files')
makedepends=('gettext')
source=(http://txt2tags.googlecode.com/files/$pkgname-$pkgver.tgz)
md5sums=('ac09fd624f1e3a553d5f0e01271cc0ee')

build() {
  cd $srcdir/$pkgname-$pkgver

  # python2 fix
  for file in txt2tags extras/gensite; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done
}
package() {
  install -d $pkgdir/usr/share/vim/vimfiles/{plugin,syntax,compiler,macros}
  install -d $pkgdir/usr/share/man/{it,pt_BR,fr,es,ca}/man1
  install -d $pkgdir/usr/share/man/man1
  cd $srcdir/$pkgname-$pkgver
  install -Dm755 $pkgname $pkgdir/usr/bin/$pkgname
  install -Dm644 extras/pagemaker.vim \
    $pkgdir/usr/share/vim/vimfiles/syntax/pagemaker.vim
  install -Dm644 extras/$pkgname.vim \
    $pkgdir/usr/share/vim/vimfiles/syntax/$pkgname.vim
  install -Dm644 extras/gvim-menu.vim \
    $pkgdir/usr/share/vim/vimfiles/plugin/$pkgname-gvim-menu.vim
  install -Dm644 extras/$pkgname-compiler.vim \
    $pkgdir/usr/share/vim/vimfiles/compiler/$pkgname-compiler.vim
  install -Dm644 extras/unhtml.vim \
    $pkgdir/usr/share/vim/vimfiles/macros/unhtml.vim

   	# Installing other extra tools
  install -m755 extras/{t2tmake.rb,gensite,html-update.sh,dynartslides} \
    $pkgdir/usr/bin
  install -Dm644 extras/txt2tags-mode.el \
    $pkgdir/usr/share/emacs/site-lisp/txt2tags-mode.el
  
	# Handling the manpage
  cd doc
  for _language in {"it","pt","fr","es","ca"}; do
    install -Dm644 manpage-${_language}.man $pkgname.1-${_language};
    if [ ${_language} == "pt" ]; then
      cp $pkgname.1-${_language} \
	$pkgdir/usr/share/man/pt_BR/man1/$pkgname.1;
    else
      install -Dm644 $pkgname.1-${_language} \
	$pkgdir/usr/share/man/$language/man1/$pkgname.1;
    fi
  done
  
	# English manpage
  install -Dm644 manpage.man $pkgdir/usr/share/man/man1/$pkgname.1
  cd ../po
  for _i in *.po
  do
    msgfmt -o ${_i%.po}.mo ${_i}
    install -Dm644 ${_i%.po}.mo \
      $pkgdir/usr/share/locale/${_i%.po}/LC_MESSAGES/$pkgname.mo
  done
}
