# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: simo <simo@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=oidentd
pkgver=2.3.1
pkgrel=1
pkgdesc='An RFC1413 compliant ident daemon'
arch=('x86_64')
url="https://oidentd.janikrabe.com/"
license=('GPL')
depends=('glibc' 'bash')
backup=(etc/oidentd.conf)
source=(https://github.com/janikrabe/oidentd/releases/download/v$pkgver/oidentd-$pkgver.tar.xz{,.asc}
        service socket)

md5sums=('0a3d61e12f25fe2a8f5fc2540bfe82bf'
         'SKIP'
         'f54c5ed2a532a559c837af18735a28f1'
         '4442fb9fc10754914568e6ffc3d5fc42')

validpgpkeys=('EB19A17D816542635D13B19FE3E8FA19DC9A1AA9') # Ryan McCabe

build() {
	cd oidentd-$pkgver
	./configure \
		--sysconfdir=/etc \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--mandir=/usr/share/man
	make
}

package() {
	cd oidentd-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/oidentd@.service"
	install -D -m644 "$srcdir/socket" "$pkgdir/usr/lib/systemd/system/oidentd.socket"
}

