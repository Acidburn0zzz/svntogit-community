# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rstcheck
pkgver=3.4.0
pkgrel=1
pkgdesc='Checks syntax of reStructuredText and code blocks nested within it'
arch=('any')
url=https://github.com/myint/rstcheck
license=('MIT')
depends=('python-docutils')
makedepends=('python-setuptools')
checkdepends=('python-sphinx')
optdepends=('python-sphinx: Sphinx support')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('a142f780bcfa5d17407f1729c66a45ddbbb979789dafda16b19aa347ea8a2d4c')
b2sums=('f317283367fb581a8623fb3acf7265c01d895b84c23f20b3fcab2fb6d3db1a215c0c6ad9ec59bbbdc6240ffcb2533036b5605dad799a04930383afb323b152d8')

build() {
  cd rstcheck-$pkgver
  python setup.py build
}

check() {
 cd rstcheck-$pkgver
 ./test_rstcheck.py
}

package() {
  cd rstcheck-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/rstcheck LICENSE
}

# vim:set ts=2 sw=2 et:
