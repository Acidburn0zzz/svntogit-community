# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Maintainer:  Daniel Micay <danielmicay@gmail.com>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=1.27
pkgrel=2
pkgdesc='Lightweight NTP client and server'
arch=('i686' 'x86_64')
url="http://chrony.tuxfamily.org/"
options=('strip')
license=('GPL')
depends=('readline' 'libcap')
backup=('etc/chrony.conf')
source=(http://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        service)
sha256sums=('d44835ee2180a9a0f4f320fc498c39d59dec33d35c2aa05b446fe65a2a83ae9d'
            'b4f68b335820b6a8429591750da294e5a9fafc1e8f6af8d865f6604516ecb306')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm0644 $srcdir/$pkgname-$pkgver/examples/chrony.conf.example $pkgdir/etc/chrony.conf

  # Arch's kernel has enhanced RTC support, make chrony use it by default
  sed -ie '/^! \(rtconutc\|rtcfile \/etc\/chrony.rtc\)$/ s/^! //' \
    "$pkgdir/etc/chrony.conf"

  install -Dm644  $srcdir/service $pkgdir/usr/lib/systemd/system/chrony.service

  install -dm755 "$pkgdir/usr/lib/systemd/ntp-units.d"
  echo "$pkgname.service" > "$pkgdir/usr/lib/systemd/ntp-units.d/$pkgname.list"
}
