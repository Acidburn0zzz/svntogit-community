# $Id$
# Maintainer:  Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=1.26
pkgrel=5
pkgdesc='Lightweight NTP client and server'
arch=('i686' 'x86_64')
url="http://chrony.tuxfamily.org/"
options=('strip')
license=('GPL')
depends=('readline' 'libcap')
backup=('etc/chrony.conf')
source=(http://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        rc.d
        service)
md5sums=('ad6dd619ff1986e4ff780363c64e2246'
         'd8b653c8bcc76cef00aa10fbd3eeb4f7'
         'd78e6189c6f51a2af30a65bdedcdb2eb')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver 
  make DESTDIR=$pkgdir install

  install -Dm0644 $srcdir/$pkgname-$pkgver/examples/chrony.conf.example $pkgdir/etc/chrony.conf
  install -Dm0755 $srcdir/rc.d $pkgdir/etc/rc.d/chrony
  install -Dm644  $srcdir/service $pkgdir/usr/lib/systemd/system/chrony.service

  install -dm755 "$pkgdir/usr/lib/ntp-units.d"
  echo "$pkgname.service" > "$pkgdir/usr/lib/ntp-units.d/$pkgname.list"
}
