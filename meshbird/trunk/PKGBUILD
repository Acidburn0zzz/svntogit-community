# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=meshbird
pkgver=0.2
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('i686' 'x86_64')
url="https://github.com/meshbird/meshbird"
license=('Apache')
depends=('glibc')
makedepends=('git' 'go')
backup=(etc/meshbird.conf)
source=("$pkgname-$pkgver.tar.gz::https://github.com/meshbird/meshbird/archive/v$pkgver.tar.gz"
        meshbird.service meshbird.conf)
md5sums=('989a59013a4bb0da94e31ea08fe6e8ba'
         'c8947f4684023c7e9eb2a9f6634ed5a9'
         '4e7d7a2569f88a8eecda6cddac939d4a')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/codegangsta/cli github.com/meshbird/meshbird
}

build() {
  cd meshbird-$pkgver
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd meshbird-$pkgver
  install -Dm755 meshbird-$pkgver "$pkgdir"/usr/bin/meshbird
  install -Dm644 "$srcdir"/meshbird.service "$pkgdir"/usr/lib/systemd/system/meshbird.service
  install -Dm644 "$srcdir"/meshbird.conf "$pkgdir"/etc/meshbird.conf
}
