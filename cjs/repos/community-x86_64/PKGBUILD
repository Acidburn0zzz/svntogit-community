# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=cjs
pkgver=4.8.0
pkgrel=1
pkgdesc="Javascript Bindings for Cinnamon"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('gtk3' 'gobject-introspection-runtime' 'js78')
makedepends=('meson' 'samurai' 'gobject-introspection')
checkdepends=('xorg-server-xvfb')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('e5cff9f7790d2874fb1b743d823a799560c5fe82271f6cbff8b74baa70eec267fad9ac651258ee5c4de5dfce81964c73ca9a652ce3441791e44c34bcd8187770')
b2sums=('6fea53cda72fc1e13618375d636b492d9e2cbc8130ff729376fbba7504e56fd5e1fa9b1330e2696a841b3400af04832f81febdfefc6730b43e3ccb7a41c46271')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/builddir
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    meson --prefix=/usr \
          --libexecdir=/usr/lib \
          -Dinstalled_tests=false \
          ..

    samu
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    # Needs a display
    xvfb-run meson test --print-errorlogs
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/builddir

    DESTDIR="${pkgdir}" samu install
}
