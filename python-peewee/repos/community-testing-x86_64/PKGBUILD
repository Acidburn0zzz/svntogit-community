# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgbase=python-peewee
pkgname=('python-peewee' 'python2-peewee')
pkgver=3.0.10
pkgrel=1
pkgdesc="a little orm"
url="https://pypi.python.org/pypi/peewee/"
arch=('x86_64')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2')
checkdepends=('python-apsw' 'python2-apsw' 'python-flask' 'python2-flask')
             # 'python-psycopg2' 'python2-psycopg2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/coleifer/peewee/archive/$pkgver.tar.gz")
sha512sums=('3c372739c20290df03a8a769fb803a5f2783d7bde7dd9215c673d9e5920754dc0672973b48d7fef162dc5a98e9fd4ab747286caa9fab8450b33564d45ad7706a')

prepare() {
  cp -a peewee-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/peewee-$pkgver
  python setup.py build

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/peewee-$pkgver
  python runtests.py

  cd "$srcdir"/peewee-$pkgver-py2
  python2 runtests.py
}

package_python-peewee() {
  depends=('python')
  optdepends=('python-psycopg2: for PostgreSQL database support'
              'mysql-python: for MySQL database support')

  cd "$srcdir"/peewee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-peewee() {
  depends=('python2')
  optdepends=('python2-psycopg2: for PostgreSQL database support')

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/pwiz{,2}.py
}
