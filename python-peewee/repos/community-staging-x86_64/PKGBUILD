# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgbase=python-peewee
pkgname=('python-peewee' 'python2-peewee')
pkgver=3.5.2
pkgrel=2
pkgdesc="a little orm"
url="https://pypi.python.org/pypi/peewee/"
arch=('x86_64')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'sqlite')
checkdepends=('python-apsw' 'python2-apsw' 'python-flask' 'python2-flask'
              'python-psycopg2' 'python2-psycopg2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/coleifer/peewee/archive/$pkgver.tar.gz"
        https://github.com/coleifer/peewee/commit/e33c6e912e0f5db99413badeaf361ffc28df7d13.patch)
sha512sums=('50dd25e495bcb6df4f2fb91b4af9197eda9bd28c4c2bf7debfbbbcdc458a94d3b216921780513c2d92042c5523aa1f56716dfe8ffb3b6e27c588690844029af0'
            'e44727b55f5b0986d4b5504d82bd9c62257865fc7694dde748057622b0eee7a882c9107157ec575467b289d1580f5be00092cd842702e53bbe5fbd6607b6a3e8')

prepare() {
  (cd peewee-$pkgver; patch -p1 -i ../e33c6e912e0f5db99413badeaf361ffc28df7d13.patch)
  cp -a peewee-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/peewee-$pkgver
  python setup.py build

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/peewee-$pkgver
  python runtests.py

  cd "$srcdir"/peewee-$pkgver-py2
  python2 runtests.py
}

package_python-peewee() {
  depends=('python' 'sqlite')
  optdepends=('python-psycopg2: for PostgreSQL database support'
              'mysql-python: for MySQL database support')

  cd "$srcdir"/peewee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-peewee() {
  depends=('python2')
  optdepends=('python2-psycopg2: for PostgreSQL database support')

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/pwiz{,2}.py
}
