# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Blackleg <blacklg@openaliasbox.org>
# Contributor: Nidhogg
# Contributor: juantascon

pkgbase=python-peewee
pkgname=('python-peewee' 'python2-peewee')
pkgver=3.3.2
pkgrel=1
pkgdesc="a little orm"
url="https://pypi.python.org/pypi/peewee/"
arch=('x86_64')
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'cython' 'cython2' 'sqlite')
checkdepends=('python-apsw' 'python2-apsw' 'python-flask' 'python2-flask')
             # 'python-psycopg2' 'python2-psycopg2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/coleifer/peewee/archive/$pkgver.tar.gz")
sha512sums=('170656ac2355cd15e24d8fcf411fe8ee942c96a16367dd9c4a0d743026660bd573e5bb552aefac1484163585618f00c2e6477e2489c8cd247173c354a5ebc045')

prepare() {
  cp -a peewee-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/peewee-$pkgver
  python setup.py build

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/peewee-$pkgver
  python runtests.py

  cd "$srcdir"/peewee-$pkgver-py2
  python2 runtests.py
}

package_python-peewee() {
  depends=('python' 'sqlite')
  optdepends=('python-psycopg2: for PostgreSQL database support'
              'mysql-python: for MySQL database support')

  cd "$srcdir"/peewee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-peewee() {
  depends=('python2')
  optdepends=('python2-psycopg2: for PostgreSQL database support')

  cd "$srcdir"/peewee-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/pwiz{,2}.py
}
