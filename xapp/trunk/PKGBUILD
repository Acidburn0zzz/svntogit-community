# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapp
pkgver=2.2.0
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libdbusmenu-gtk3' 'libgnomekbd')
optdepends=('python: for mate-desktop status applet')
makedepends=('meson' 'samurai' 'gobject-introspection' 'python-gobject' 'vala')
provides=('xapps')
conflicts=('xapps')
replaces=('xapps')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('c1510443eb2f3f4f78ca792a9d327fa716a71116c3c5a7dff4c1313621a36768')
b2sums=('212f0e9648b88db89cc801e8873db481c2879c4623e51b693601dbf2c1c48898979dd466038cc16e35a0754f5a15dd756d259478625bcb7282c80f597e3081c9')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install

    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    while read -rd '' _file; do
        _destdir="$(dirname "${_file#${pkgdir}}")"
        python -m compileall -d "${_destdir}" "${_file}"
        python -O -m compileall -d "${_destdir}" "${_file}"
    done < <(find "${pkgdir}"/usr/lib/python3* -name '*.py' -print0)
}
