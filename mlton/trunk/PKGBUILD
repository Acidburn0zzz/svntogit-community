# $Id: PKGBUILD 120612 2014-10-13 14:26:12Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: tochiro@no.spam.mail.berlios.de
# Contributor: Andreas W. Hauser <andy-aur@splashground.de>
# Contributor: Brian De Wolf <arch@bldewolf.com>

# WIP

pkgname=mlton
pkgver=20130715
pkgrel=7
pkgdesc='Whole-program, optimizing Standard ML compiler'
arch=('x86_64' 'i686')
url='http://mlton.org/'
license=('BSD' 'MIT' 'LGPL')
depends=('gmp')
makedepends=('setconf' 'mlton')
options=('staticlibs')
source_x86_64=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.src.tgz" \
        "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-2.amd64-linux.tgz")
sha256sums_x86_64=('215857ad11d44f8d94c27f75e74017aa44b2c9703304bcec9e38c20433143d6c'
                   'f9687c2dcab7b64ea8610911d4df91f6cdc3ad778e39f76d3d452ae890f3f330')
sha256sums_i686=('215857ad11d44f8d94c27f75e74017aa44b2c9703304bcec9e38c20433143d6c'
                 '3f0437a83f83b82da2c44f1e0f6f8d638864eacc58a8a3b3de60ebf84b441702')
source_i686=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.src.tgz" \
          "http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver-2.x86-linux.tgz")

build() {
  setconf mlton-$pkgver/runtime/Makefile CFLAGS "$CFLAGS -fPIC"
  setconf mlton-$pkgver/runtime/Makefile CXXFLAGS "$CXXFLAGS -fPIC"

  #setconf mlton-$pkgver-2/bin/mlton lib="/usr/lib/mlton"
  #PATH="$PATH:$srcdir/usr/bin" make -C "$pkgname-$pkgver" -j1 all-no-docs
  make -C "$pkgname-$pkgver" -j1 all-no-docs
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" -j1 install-no-docs

  # Copy over the handful of licenses and the README as explanation
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  cp "$pkgname-$pkgver/doc/license/"* "$pkgdir/usr/share/licenses/$pkgname/"
}

# vim: ts=2 sw=2 et:
