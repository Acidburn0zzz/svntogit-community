# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=heaps
pkgname=haskell-heaps
pkgver=0.3.6
pkgrel=7
pkgdesc="Asymptotically optimal Brodal/Okasaki heaps"
url="https://github.com/ekmett/heaps"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('e6145d45be01e85fef86921ffbe3c808f7f7cbb3ef2f74747756ff0f937ae9242f98755e701301de5584b0004d19c7eb54c46c5b6dc266a4bc2b9e5fefa2d52f')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.14/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
