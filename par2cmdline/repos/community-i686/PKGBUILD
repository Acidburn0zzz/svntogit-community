# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >

pkgname=par2cmdline
pkgver=0.5.2
pkgrel=1
pkgdesc='A PAR 2.0 compatible file verification and repair tool'
url='https://github.com/eMPee584/par2cmdline'
license=('GPL2')
arch=('i686' 'x86_64')
source=("http://herecura.be/files/${pkgname}-$pkgver.tar.xz")
md5sums=('7dea4b1d433c43afa647e72efeedb55a')

build() {
	cd ${pkgname}-${pkgver}
	aclocal
	automake --add-missing
	autoconf
	./configure --prefix=/usr
	make
}

check() {
	cd ${pkgname}-${pkgver}
	make check
}

package() {
	cd ${pkgname}-${pkgver}
	make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 ft=sh et:
