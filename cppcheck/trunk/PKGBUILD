# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=cppcheck
pkgver=1.89
pkgrel=1
pkgdesc="A tool for static C/C++ code analysis"
arch=('x86_64')
url="http://cppcheck.sourceforge.net/"
license=('GPL')
depends=('python-pygments')
makedepends=('docbook-xsl' 'qt5-tools' 'qt5-base' 'python')
optdepends=('qt5-base: run cppcheck-gui')
source=($pkgname-$pkgver.tar.gz::https://github.com/danmar/cppcheck/archive/${pkgver}.tar.gz
        translations-location.patch)
sha1sums=('9114a99e629ac820f417e5a4e7c7af47cb90050f'
          '03b0888438cf92ccdcbf307dbc3c35e65c91b844')

prepare() {
   cd "${pkgname}-${pkgver}"
   patch -Np1 -i $srcdir/translations-location.patch
}

build() {
   cd "${pkgname}-${pkgver}"
   export CXXFLAGS+=" -DNDEBUG"
   # Unicode fix
   LANG='en_US.UTF-8' make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg HAVE_RULES=yes USE_MATCHCOMPILER=yes

   make DB2MAN=/usr/share/xml/docbook/xsl-stylesheets-1.79.2/manpages/docbook.xsl CFGDIR=/usr/share/cppcheck/cfg man

   cd gui
   lrelease gui.pro
   qmake HAVE_RULES=yes
   make CFGDIR=/usr/share/cppcheck/cfg USE_MATCHCOMPILER=yes HAVE_RULES=yes SRCDIR=build
}

check() {
   cd "${pkgname}-${pkgver}"

   #LANG='en_US.UTF-8' make SRCDIR=build CFGDIR=/usr/share/cppcheck/cfg HAVE_RULES=yes test
}

package() {
   cd "${pkgname}-${pkgver}"
  LANG='en_US.UTF-8' make DESTDIR="${pkgdir}" \
                          FILESDIR=/usr/share/cppcheck \
                          CFGDIR=/usr/share/cppcheck/cfg \
                          USE_MATCHCOMPILER=yes HAVE_RULES=yes install

   install -D -p -m 644 cppcheck.1 "${pkgdir}"/usr/share/man/man1/cppcheck.1

   # GUI
   install -m755 gui/cppcheck-gui "${pkgdir}"/usr/bin
   install -d "${pkgdir}/usr/share/applications/"
   install -m644 gui/cppcheck-gui.desktop "${pkgdir}/usr/share/applications/"

   install -d "${pkgdir}"/usr/share/cppcheck/cfg
   install -D ./cfg/* -t "${pkgdir}"/usr/share/cppcheck/cfg

   install -d "${pkgdir}"/usr/share/cppcheck/cfg/lang
   install -D gui/*.qm -t "${pkgdir}"/usr/share/cppcheck/cfg/lang/
}
