# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Sergej Pupykin <ps@lx-ltd.ru>

pkgname=ulogd
pkgver=2.0.0
pkgrel=5
pkgdesc='Userspace Packet Logging for netfilter'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/ulogd/index.html'
license=('GPL2')
depends=('libmnl')
backup=('etc/ulogd.conf')
makedepends=('libmysqlclient' 'postgresql-libs' 'sqlite' 'libpcap' 'libdbi'
	     'libnetfilter_acct' 'libnetfilter_log' 'libnetfilter_conntrack'
	     'linuxdoc-tools' 'groff' 'texlive-core')
optdepends=('postgresql-libs'
	    'sqlite'
	    'libpcap'
	    'libmysqlclient'
	    'libdbi'
	    'libnetfilter_acct'
	    'libnetfilter_log'
	    'libnetfilter_conntrack')
options=('!libtool')
source=("ftp://ftp.netfilter.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
	'rc'
	'logrotate'
	'ulogd.conf.min'
	'ulogd.service')
md5sums=('211e68781e3860959606fc94b97cf22e'
         'c42230c1cabf5dd9b80bf9a0fd87ca7e'
         'dd32eeee0949adff4188544419acd17e'
         'c21531312f95e790ed19aa267a17d35a'
         '13a1227052322b4c17b258ed358f055e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-mysql --with-pgsql --with-dbi
  make
  (cd doc && make)
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -dm0755 "$pkgdir/usr/share/doc/ulogd"
  install -Dm0644 ulogd.conf $pkgdir/usr/share/doc/ulogd/ulogd.conf.sample
  install -Dm0644 doc/ulogd.{dvi,html,ps,sgml,txt} "$pkgdir/usr/share/doc/ulogd"

  install -dm0755 "$pkgdir/usr/share/ulogd"
  install -m644 doc/*.{table,sql} "$pkgdir/usr/share/ulogd"

  install -D -m644 "$srcdir/logrotate" "$pkgdir/etc/logrotate.d/ulogd"
  install -D -m755 "$srcdir/rc" "$pkgdir/etc/rc.d/ulogd"

  install -Dm0644 $srcdir/ulogd.conf.min $pkgdir/etc/ulogd.conf
  install -Dm0644 $srcdir/ulogd.service $pkgdir/usr/lib/systemd/system/ulogd.service
}
