# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Sergej Pupykin <ps@lx-ltd.ru>

pkgname=ulogd
pkgver=1.24
pkgrel=8
pkgdesc='Userspace Packet Logging for netfilter'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/ulogd/index.html'
license=('GPL2')
makedepends=('mysql' 'postgresql' 'sqlite3' 'libpcap')
optdepends=('postgresql-libs'
	    'sqlite3'
	    'libpcap'
	    'libmysqlclient')
backup=('etc/ulogd.conf')
source=("ftp://ftp.netfilter.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
	'rc'
	'logrotate'
	'mac-addr.patch')
md5sums=('05b4ed2926b9a22aaeaf642917bbf8ff'
         'c42230c1cabf5dd9b80bf9a0fd87ca7e'
         'fe40b3073b7474a77e0b8b0bfd19ab63'
         'ca41a10a9f230614cd15e2bb037e057d')

build() {
  cd $pkgname-$pkgver
  export MAKEFLAGS="-j1"
  patch -p0 extensions/ulogd_BASE.c <$srcdir/mac-addr.patch
  ./configure --prefix=/usr --sysconfdir=/etc --with-mysql --with-pgsql --with-sqlite3
  make || true
  (cd mysql && ld -shared -L/usr/lib -L/usr/lib -L/usr/lib/mysql -lmysqlclient -lz -lcrypt -lnsl -lm -o ulogd_MYSQL.so ulogd_MYSQL_sh.o -lc)
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  chmod 644 "$pkgdir/etc/ulogd.conf"
  install -d -m 755 "$pkgdir/usr/share/ulogd"
  install -m644 doc/*.table "$pkgdir/usr/share/ulogd"
  install -D -m644 "$srcdir/logrotate" "$pkgdir/etc/logrotate.d/ulogd"
  install -D -m755 "$srcdir/rc" "$pkgdir/etc/rc.d/ulogd"
}
