# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Sergej Pupykin <ps@lx-ltd.ru>

pkgname=ulogd
pkgver=2.0.1
pkgrel=4
pkgdesc='Userspace Packet Logging for netfilter'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/ulogd/index.html'
license=('GPL2')
depends=('libmnl' 'libnfnetlink')
backup=('etc/ulogd.conf')
makedepends=('libmysqlclient'
             'libnetfilter_acct'
             'libnetfilter_conntrack'
             'libnetfilter_log'
             'libpcap'
             'libdbi'
             'postgresql-libs'
             'sqlite')
optdepends=("${makedepends[@]}")
options=('!libtool')
source=("ftp://ftp.netfilter.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
	      "$pkgname.logrotate"
	      "$pkgname.conf"
	      "$pkgname.service")
md5sums=('2f4b1a58955fcad4ebf22f0d00885fc9'
         'dd32eeee0949adff4188544419acd17e'
         'c21531312f95e790ed19aa267a17d35a'
         '42fc2c0568ff89ec00c2f0797280f296')


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-mysql --with-pgsql --with-dbi
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/usr/share/doc/ulogd"
  install -Dm644 ulogd.conf "$pkgdir/usr/share/doc/ulogd/ulogd.conf"
  install -m644 doc/*.{table,sql} "$pkgdir/usr/share/doc/ulogd"
  cd "$srcdir"
  install -Dm644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 $pkgname.logrotate "$pkgdir/etc/logrotate.d/$pkgname"
  install -Dm644 $pkgname.conf "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
