# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Dale Blount <dale@archlinux.org>
# Contributor: Sergej Pupykin <ps@lx-ltd.ru>

pkgname=ulogd
pkgver=2.0.0
pkgrel=2
pkgdesc='Userspace Packet Logging for netfilter'
arch=('i686' 'x86_64')
url='http://www.netfilter.org/projects/ulogd/index.html'
license=('GPL2')
depends=('libmnl')
makedepends=('libmysqlclient' 'postgresql-libs' 'sqlite' 'libpcap' 'libdbi'
	     'libnetfilter_acct' 'libnetfilter_log' 'libnetfilter_conntrack')
optdepends=('postgresql-libs'
	    'sqlite'
	    'libpcap'
	    'libmysqlclient'
	    'libdbi'
	    'libnetfilter_acct'
	    'libnetfilter_log'
	    'libnetfilter_conntrack')
options=('!libtool')
backup=('etc/ulogd.conf')
source=("ftp://ftp.netfilter.org/pub/$pkgname/$pkgname-$pkgver.tar.bz2"
	'rc'
	'logrotate')
md5sums=('211e68781e3860959606fc94b97cf22e'
         'c42230c1cabf5dd9b80bf9a0fd87ca7e'
         'dd32eeee0949adff4188544419acd17e')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --with-mysql --with-pgsql --with-dbi
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm0644 ulogd.conf $pkgdir/etc/ulogd.conf
  install -d -m 755 "$pkgdir/usr/share/ulogd"
  install -m644 doc/*.{table,sql} "$pkgdir/usr/share/ulogd"
  install -D -m644 "$srcdir/logrotate" "$pkgdir/etc/logrotate.d/ulogd"
  install -D -m755 "$srcdir/rc" "$pkgdir/etc/rc.d/ulogd"
}
