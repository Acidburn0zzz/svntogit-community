# $Id$
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=expac
pkgver=1
pkgrel=2
pkgdesc="pacman database extraction utility"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/expac"
license=('GPL')
depends=('pacman')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('be8348f18e6af9db250437b14a91e443')

build() {
  make -C "$pkgname-$pkgver"

  sed '/\*\//q' "$pkgname-$pkgver"/expac.c >LICENSE
}

package() {
  make -C "$pkgname-$pkgver" PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
