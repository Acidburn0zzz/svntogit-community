# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Michel Brabants <michel.brabants@euphonynet.be>

pkgname=libzrtpcpp
pkgver=2.0.0
pkgrel=1
pkgdesc="An extension to Real-time Transport Protocol (RTP) which describes a method of Diffie-Hellman key agreement for Secure Real-time Transport Protocol (SRTP)"
arch=('i686' 'x86_64')
url="http://zfoneproject.com"
license=('GPL')
depends=('ccrtp' 'commoncpp2')
makedepends=('pkgconfig' 'cmake')
options=('!libtool')
source=(ftp://ftp.gnu.org/gnu/ccrtp/$pkgname-$pkgver.tar.gz)
md5sums=('dc1501e7e8a46a1608fc3820c7476727')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i 's|#include <commoncpp/|#include <cc++/|g' \
	src/libzrtpcpp/TimeoutProvider.h \
	src/libzrtpcpp/ZIDRecord.h \
	src/libzrtpcpp/ZrtpCallback.h

  sed -i 's|NAMESPACE_COMMONCPP|NAMESPACE_COMMON|g' \
	src/ZrtpQueue.cpp \
	src/libzrtpcpp/ZrtpQueue.h \
	src/libzrtpcpp/zrtpccrtp.h

  cmake \
	-DCMAKE_INSTALL_PREFIX=/usr \
	-DCMAKE_EXE_LINKER_FLAGS="-lccgnu2 -lccext2" .
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR=${pkgdir} install
}
