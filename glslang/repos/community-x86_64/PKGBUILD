# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=glslang
pkgver=7.9.2888
pkgrel=1
pkgdesc='An OpenGL and OpenGL ES shader front end and validator'
arch=('x86_64')
url='https://github.com/KhronosGroup/glslang'
license=('BSD')
depends=('gcc-libs')
makedepends=('cmake')
source=("https://github.com/KhronosGroup/glslang/archive/${pkgver}.tar.gz")
sha256sums=('cb66779d0e6b5f07f0445bd58289a24e56e12693e71d75c8fae3db31ffacaf8c')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    mkdir build && cd build
    cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}/build"
    make DESTDIR="${pkgdir}" install
}
