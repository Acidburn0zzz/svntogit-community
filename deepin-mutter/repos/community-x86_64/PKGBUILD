# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-mutter
pkgver=3.20.36
pkgrel=1
pkgdesc="Base window manager for deepin, fork of gnome mutter"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-mutter"
license=('GPL3')
depends=('deepin-clutter' 'dconf' 'gobject-introspection-runtime' 'deepin-desktop-schemas'
         'libcanberra' 'startup-notification' 'zenity' 'libsm' 'gnome-desktop' 'upower'
         'libxkbcommon-x11' 'libgudev' 'deepin-cogl')
makedepends=('gnome-common' 'gnome-doc-utils' 'gobject-introspection' 'gtk-doc' 'intltool')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-mutter/archive/$pkgver.tar.gz")
sha512sums=('76f78d054376e7611e85aa0f03176bede75141b1ccb14bf5444c9ccf3f45eea0a796bab69ddb27b8d9c652bf06eb51f0cae15f6728270f70d0e7ad0e915e540d')

build() {
  cd deepin-mutter-$pkgver
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/deepin-mutter \
    --localstatedir=/var \
    --enable-gtk-doc \
    --enable-wayland \
    --enable-native-backend \
    --disable-static \
    --disable-schemas-compile \
    --enable-compile-warnings=minimum
  make
}

package() {
  cd deepin-mutter-$pkgver
  make DESTDIR="$pkgdir" install
}
