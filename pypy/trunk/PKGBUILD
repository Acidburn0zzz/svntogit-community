# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=pypy
pkgver=1.6
pkgrel=2
pkgdesc="A Python implementation written in Python, JIT enabled"
url="http://codespeak.net/pypy/"
arch=('i686' 'x86_64')
depends=('python2' 'libffi')
optdepends=('openssl: openssl module'
            'expat: pyexpat module'
            'ncurses: ncurses module'
            'zlib: zlib module'
            'bzip2: bz2 module')
license=('custom:MIT')
source=(https://bitbucket.org/pypy/pypy/get/release-${pkgver}.tar.bz2)
md5sums=('1189352effc5df7df84e6916b3b3eae3')

build() {
  cd ${srcdir}/pypy-pypy-release-${pkgver}/pypy/translator/goal

  [[ $CARCH = "x86_64" ]] &&  python2 translate.py -Ojit
  [[ $CARCH = "i686" ]] &&  python2 translate.py -Ojit --jit-backend=x86-without-sse2
}

package() {
  cd ${srcdir}/pypy-pypy-release-${pkgver}/pypy/translator/goal

  install -Dm755 pypy-c ${pkgdir}/opt/pypy/pypy-c
  mkdir -p ${pkgdir}/opt/pypy/{lib-python,pypy}

  cd ${srcdir}/pypy-pypy-release-${pkgver} 
  cp -r lib-python/2.7 ${pkgdir}/opt/pypy/lib-python/
  cp -r lib-python/modified-2.7 ${pkgdir}/opt/pypy/lib-python/
  cp -r lib_pypy ${pkgdir}/opt/pypy/
  cp -r include ${pkgdir}/opt/pypy/

  mkdir -p ${pkgdir}/usr/bin
  ln -s /opt/pypy/pypy-c ${pkgdir}/usr/bin/pypy

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/pypy/LICENSE
}
# vim: ts=2 sw=2 et:
