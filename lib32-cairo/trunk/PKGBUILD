# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.12.10
pkgrel=1
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-glib2' ${_pkgbasename})
makedepends=('gcc-multilib')
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.xz
	revert-xlib-map-to-image-requires-an-extents.patch
	revert-xlib-Simplify-source-creation-by-use-of-map-to-image.patch)
sha1sums=('be06d5aaa272bbbd08380f71ca710d5612881493'
          '8bc096dd16a885ad041cb2137a64757f32d1cc88'
          'fd8ffd9aba3679c344e4f25494f199a51b0ae62c')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  patch -Np1 -R -i ../revert-xlib-map-to-image-requires-an-extents.patch
  patch -Np1 -R -i ../revert-xlib-Simplify-source-creation-by-use-of-map-to-image.patch

  ./configure --prefix=/usr --libdir=/usr/lib32 \
      --sysconfdir=/etc  --localstatedir=/var \
      --disable-static --enable-tee --disable-xlib-xcb
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}
