# $Id$
# Maintainer: Ionut Biru <ibiru@archlinux.org>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename
pkgver=1.8.10
pkgrel=2
pkgdesc="Cairo vector graphics library (32-bit)"
arch=('x86_64')
url="http://cairographics.org/"
license=('LGPL' 'MPL')
depends=('lib32-libpng' 'lib32-libxrender' 'lib32-fontconfig'
         'lib32-pixman' 'lib32-xcb-util' ${_pkgbasename})
makedepends=('gcc-multilib')
source=(http://cairographics.org/releases/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('fd5e8ca82ff0e8542ea4c51612cad387f2a49df3')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  sed -i -e 's/libpng13/libpng14/g' configure
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --enable-xcb --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{include,share}
}
