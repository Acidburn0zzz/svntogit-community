# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=forge
pkgdesc="High Performance Visualizations"
url='https://github.com/arrayfire/forge'
pkgver=1.0.3
arch=('x86_64')
pkgrel=1
epoch=2
license=('BSD')
makedepends=('cmake' 'doxygen' 'graphviz' 'git' 'python' 'glm' 'boost')
depends=('glfw' 'freetype2' 'fontconfig' 'freeimage' 'glbinding')
source=(https://github.com/arrayfire/forge/archive/v${pkgver}.tar.gz)
sha512sums=('43f3797e09f1758f2ab79e57f460e7bbdefa2bf408ebd439fa68543054fc1cd636bd66fbbbc9c44370a13b1e3c89262362de8b413f3f2260f86a6b163db17c93')
options=('!buildflags')

build() {
  cd forge-${pkgver}

  rm -rf build
  mkdir build && cd build

  cmake .. \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DBUILD_EXAMPLES=ON \
      -DBUILD_DOCUMENTATION=ON

  make
}

package() {
  cd forge-${pkgver}

  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  cd build

  make DESTDIR="${pkgdir}/" install
}
