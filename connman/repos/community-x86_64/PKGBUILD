# Maintainer : Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Lucas De Marchi <lucas.de.marchi@gmail.com>

pkgname=connman
pkgver=1.38
pkgrel=2
pkgdesc="Intel's modular network connection manager"
url="https://01.org/connman"
arch=('x86_64')
license=('GPL2')
makedepends=('bluez' 'wpa_supplicant' 'openconnect' 'openvpn' 'ppp' 'iwd')
depends=('dbus' 'iptables' 'gnutls' 'glib2')
backup=('etc/connman/main.conf')
optdepends=('bluez: Support for Bluetooth devices'
            'wpa_supplicant: for WiFi devices'
            'pptpclient: for ppp support'
            'openvpn: for VPN Support'
            'iwd: for WiFi devices')
source=("${pkgname}-${pkgver}.tar.xz::https://www.kernel.org/pub/linux/network/${pkgname}/${pkgname}-${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.sign::https://www.kernel.org/pub/linux/network/${pkgname}/${pkgname}-${pkgver}.tar.sign"
        'allow_group_network.diff'
        'connman-1.38_CVE-2021-26676_1.patch::https://git.kernel.org/pub/scm/network/connman/connman.git/patch/?id=58d397ba74873384aee449690a9070bacd5676fa'
        'connman-1.38_CVE-2021-26676_2.patch::https://git.kernel.org/pub/scm/network/connman/connman.git/patch/?id=a74524b3e3fad81b0fd1084ffdf9f2ea469cd9b1'
        'connman-1.38_CVE-2021-26675.patch::https://git.kernel.org/pub/scm/network/connman/connman.git/patch/?id=e4079a20f617a4b076af503f6e4e8b0304c9f2cb')
sha512sums=('9c8f77c4dd62763ccb9ea5cc5d285a8a1c51bb09a82eafc73246231557a4f627ae1e5e4a9df5e104457390ebba643349d7d67800a4ee4c17c6dceff192afe8d4'
            'SKIP'
            '06dd5867d460f1c3cf6c359e650ca2ef24495493a99cd03dbd17f23e587e9066d9bc98758d85d5c690d1ae21fa77ad8da5e2fa83d0b52c95d7a535784c5c4964'
            '8ce763511847ca826a06af1cece44b28bf57e8e1acd4ad711c997262b6aad70b28cbb4833fc7ad94ab8dfda7b3c0e4b8bff728980040669698e336f953dd31f2'
            'c884996bacc842ede38ab752c2a7476f5c1c8bcff9a3d34b2a60ef77f7674e8c9e0104c85a7249de0e7bcb324c36fab9c8eac553c19d983ed0b9230f287411fc'
            'cd33cfcaab5ac5ee58309442d75fbe414d62185cf9688cc2d5cfae19ee837b076b514a3e0e03b07360332760884140d02e07bc0f365edd4509d78166840d309a')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')

prepare(){
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/allow_group_network.diff"

  # Fix CVE-2021-26675 and CVE-2021-26676 (FS#69583)
  patch -Np1 -i "${srcdir}/connman-1.38_CVE-2021-26676_1.patch"
  patch -Np1 -i "${srcdir}/connman-1.38_CVE-2021-26676_2.patch"
  patch -Np1 -i "${srcdir}/connman-1.38_CVE-2021-26675.patch"
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --bindir=/usr/bin \
      --sbindir=/usr/bin \
      --with-systemdunitdir=/usr/lib/systemd/system \
      --enable-pptp \
      --enable-openconnect \
      --enable-vpnc \
      --enable-openvpn \
      --enable-polkit \
      --enable-client \
      --enable-nmcompat \
      --enable-test \
      --enable-pie \
      --enable-iwd
  make
}

package() {
  make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
  install -Dm755 "${srcdir}/${pkgname}-${pkgver}/client/${pkgname}ctl" "${pkgdir}/usr/bin/${pkgname}ctl"
  install -Dm644 "${srcdir}/${pkgname}-${pkgver}/src/main.conf" "${pkgdir}/etc/connman/main.conf"
  find "${pkgdir}/usr" -name \*.service -exec sed -i 's/s\(bin\)/\1/' {} +
# See FS#48044
  sed -i 's/ProtectSystem=full/ProtectSystem=true/' "${pkgdir}"/usr/lib/systemd/system/connman.service
  rm -r "${pkgdir}"/usr/lib/tmpfiles.d
}
