# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Eric Bélanger <eric@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Bjorn Lindeijer <bjorn@lindeijer.nl>
# Contributor: kritoke <kritoke@nospam.gmail.com>

pkgbase=ruby-gtk2
pkgname=('ruby-atk' 'ruby-gdkpixbuf2' 'ruby-gio2' 'ruby-glib2' 'ruby-gtk2'
         'ruby-pango')
pkgver=1.2.1
pkgrel=2
arch=('x86_64' 'i686')
url='http://ruby-gnome2.sourceforge.jp/'
license=('LGPL')
makedepends=('ruby-pkgconfig' 'ruby-cairo' 'gtk2')
source=("http://downloads.sourceforge.net/ruby-gnome2/ruby-gtk2-$pkgver.tar.gz")
sha256sums=('cb6e329f64042c8d24248204e11bb4652f9b2984ac2965d82dab49634deb1125')

build() {
  cd "$srcdir/ruby-gtk2-$pkgver"
  LANG="en_US.UTF-8" \
    ruby extconf.rb atk gdk_pixbuf2 gio2 glib2 gtk2 pango --vendor
  #mkdir -p \
  #  "$pkgdir/ruby-gdkpixbuf2/usr/lib/ruby/site_ruby/1.9.1/i686-linux"
  make
}

package_ruby-atk() {
  pkgdesc='Ruby bindings for atk'
  depends=("ruby-glib2=$pkgver" 'atk')

  cd "$srcdir/ruby-gtk2-$pkgver/atk"
  make DESTDIR="$pkgdir/" install
}

package_ruby-gdkpixbuf2() {
  pkgdesc='Ruby bindings for gdkpixbuf2'
  depends=("ruby-glib2=$pkgver" 'gdk-pixbuf2')

  cd "$srcdir/ruby-gtk2-$pkgver/gdk_pixbuf2"
  make DESTDIR="$pkgdir/" install
}

package_ruby-gio2() {
  pkgdesc='Ruby bindings for gio2'
  depends=("ruby-glib2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gio2"
  make DESTDIR="$pkgdir/" install
}

package_ruby-glib2() {
  pkgdesc='Ruby bindings for glib2'
  depends=('glib2' 'ruby')

  cd "$srcdir/ruby-gtk2-$pkgver/glib2"
  make DESTDIR="$pkgdir/" install
}

package_ruby-gtk2() {
  pkgdesc='Ruby bindings for gtk2'
  depends=('gtk2' "ruby-glib2=$pkgver" "ruby-pango=$pkgver" "ruby-atk=$pkgver"
           "ruby-gdkpixbuf2=$pkgver")

  cd "$srcdir/ruby-gtk2-$pkgver/gtk2"
  make DESTDIR="$pkgdir/" install
}

package_ruby-pango() {
  pkgdesc='Ruby bindings for pango'
  depends=("ruby-glib2=$pkgver" 'ruby-cairo' 'pango')

  cd "$srcdir/ruby-gtk2-$pkgver/pango"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
