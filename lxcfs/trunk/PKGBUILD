# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Christian Brauner <christianvanbrauner@gmail.com>

pkgname=lxcfs
pkgver=2.0.5
pkgrel=1
pkgdesc="FUSE filesystem for LXC"
arch=(i686 x86_64)
url="https://linuxcontainers.org/"
license=('GPL')
depends=('lxc' 'cgmanager>=0.35-1' 'libnih' 'fuse2' 'dbus-glib')
makedepends=('help2man')
options=('emptydirs')
source=("https://linuxcontainers.org/downloads/lxcfs/lxcfs-$pkgver.tar.gz"
	"lxcfs.service")
sha256sums=('c68b660fc224732bc727e9dd6a9bf97587e42622b6d9053321dff40503cc9709'
            '37de44dc1bc645f1cadb598ec367d3055cc6813a4e25010c8521e735ddc03c4e')

build() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure \
        --prefix=/usr \
        --sbindir=/usr/bin \
        --with-pamdir=/usr/lib/security \
        --localstatedir=/var
    make
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -dm0755 "$pkgdir"/var/lib/lxcfs
    install -Dm0644 "$srcdir"/lxcfs.service "$pkgdir"/usr/lib/systemd/system/lxcfs.service
}
