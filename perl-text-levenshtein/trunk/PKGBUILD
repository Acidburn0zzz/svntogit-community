# Maintainer: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-text-levenshtein
pkgver=0.05
pkgrel=1
pkgdesc="An implementation of the Levenshtein edit distance"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
checkdepends=('perl-test-simple')
url=https://metacpan.org/release/Text-Levenshtein
source=("http://search.cpan.org/CPAN/authors/id/J/JG/JGOLDBERG/Text-Levenshtein-${pkgver}.tar.gz")
md5sums=(481bf8c903db6ba7e5c9251140e1b98f)
sha512sums=(507c47069a9402577be0ef06aaf7845d9923f6de4512401a1194c39e8311e3334b0052858c47a3fa8d2fca95ca47c73557730857706d25e060a8ed67df40d961)
_distdir="${srcdir}/Text-Levenshtein-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
