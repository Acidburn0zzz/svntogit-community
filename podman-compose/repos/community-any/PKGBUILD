# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=podman-compose
pkgver=0.1.10
pkgrel=1
pkgdesc='A script to run docker-compose.yml using podman'
arch=(any)
url='https://github.com/containers/podman-compose'
license=(GPL2)
depends=(podman python-dotenv python-yaml)
makedepends=(python-setuptools)
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('6ccd88ba532b85bddf87b1f99011ab16321918ce9f5f8e44e4163781acd7099fd2cc5d8d6df280b858275bed1ff3bf23ea35bae7582994db079601962bd3ad5a')
b2sums=('475e24e0825310d02e5dffbb02438384f54d22ccb8371c0320f16407f6cfd2625c3d404ff5f87d5d8b123542b386ab2f9b03efafb266b234fd5a1777df590990')

build() {
  cd $pkgname-$pkgver

  python setup.py build
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  find examples -type f -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
  install -vDm 644 {README.md,docs/*} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}

# vim:set ts=2 sw=2 et:
