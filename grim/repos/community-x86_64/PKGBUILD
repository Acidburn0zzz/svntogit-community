# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: emersion <contact@emersion.fr>

pkgname=grim
pkgver=1.1.0
pkgrel=1
pkgdesc='Screenshot utility that works under Wayland'
arch=(x86_64)
url='https://github.com/emersion/grim'
license=(MIT)
depends=(libjpeg-turbo pango wayland wayland-protocols)
makedepends=(git meson ninja)
source=("git+$url#tag=v${pkgver%.0}")
sha256sums=('SKIP')

build() {
  arch-meson build $pkgname
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 $pkgname/LICENSE \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/emersion/grim/releases/latest
# vim: ts=2 sw=2 et
