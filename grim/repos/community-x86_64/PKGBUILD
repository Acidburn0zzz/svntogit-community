# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: emersion <contact@emersion.fr>

pkgname=grim
pkgver=0.0.0
pkgrel=2
pkgdesc='Screenshot utility that works under Wayland'
arch=('x86_64')
url='https://github.com/emersion/grim'
license=('MIT')
depends=('libjpeg-turbo' 'pango' 'wayland' 'wayland-protocols')
makedepends=('git' 'meson' 'ninja')
source=("git+$url#commit=61df6f0a9531520c898718874c460826bc7e2b42")
sha256sums=('SKIP')

build() {
  arch-meson build "$pkgname"
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$pkgname/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ts=2 sw=2 et:
