# $Id$
# Maintainer: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: Giacomo Rizzo <alt@free-os.it>

pkgname=gpsd
pkgver=2.92
pkgrel=1
pkgdesc="GPS daemon and library to support USB/serial GPS devices"
arch=('i686' 'x86_64')
url="http://gpsd.berlios.de"
license=('GPL')
depends=('lesstif' 'libxaw' 'python>=2.6')
options=('!libtool')
source=(http://download.berlios.de/$pkgname/$pkgname-$pkgver.tar.gz
	gpsd.confd
	gpsd
	99-gpsd-usb.rules)
install=gpsd.install
md5sums=('50b60d9f6dd51e001f4dfbaeb825c988'
         'f83f1cbb96c7f3b102683940b5f39faa'
         'fd02537eb14455ddb33dd1f29b514168'
         '3b1f267ce04d977127c114dfa48eca55')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|--prefix=${prefix} # --root=$(DESTDIR)|--prefix=${prefix} --root=$(DESTDIR)|' Makefile.am Makefile.in

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR="$pkgdir" install || return 1

  sed -i -e "s/gpsd.hotplug/gpsd/g" gpsd.hotplug gpsd.usermap
  install -D gpsd.usermap "$pkgdir/etc/hotplug/usb/gpsd.usermap" || return 1
  install -m755 gpsd.hotplug "$pkgdir/etc/hotplug/usb/gpsd" || return 1

  install -D -m644 "$srcdir/gpsd.confd" "$pkgdir/etc/conf.d/gpsd" || return 1
  install -D -m755 "$srcdir/gpsd" "$pkgdir/etc/rc.d/gpsd" || return 1
  install -D -m644 "$srcdir/99-gpsd-usb.rules"  "$pkgdir/etc/udev/rules.d/99-gpsd-usb.rules" || return 1

  mkdir -p "$pkgdir/var/run/gpsd"
}

