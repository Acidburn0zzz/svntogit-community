# Maintainer: David Runge <dvzrv@archlinux.org>

_name=autoapi
pkgname=python-autoapi
pkgver=2.0.1
pkgrel=1
pkgdesc="Automatic Python API reference documentation generator for Sphinx, inspired by Doxygen"
arch=('any')
url="https://github.com/carlos-jenkins/autoapi"
license=('Apache')
depends=('python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
# tests not included in pypi sdist:
# https://github.com/carlos-jenkins/autoapi/issues/9
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/carlos-jenkins/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('543156b7649f492fcfa6208f0e7e4f8caabf6e69384598e500c0482db4a0d516c5c4c15e89a471201cc7888cdf9c9a06c4c28fd4a67ef7995d8ea026a946702a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
