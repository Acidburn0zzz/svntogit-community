# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=fcitx5-m17n
pkgver=5.0.0
pkgrel=1
pkgdesc="m17n Wrapper for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-m17n"
license=('GPL')
depends=('fcitx5' 'm17n-lib')
makedepends=('extra-cmake-modules')
source=("https://download.fcitx-im.org/fcitx5/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
sha512sums=('a9a05efe9c0cd41b2154be22c41cdfe9bd581bb91571a471c034d689b812e0892144ee564b15e78defad0c896ad02db5e53effc30f633c9adec0662fdbbc66b0'
            'SKIP')
validpgpkeys=('2CC8A0609AD2A479C65B6D5C8E8B898CBF2412F9') # Weng Xuetian <wengxt@gmail.com>

build(){
  cd $pkgname-$pkgver

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
