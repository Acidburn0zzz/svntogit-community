# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Verelst <david dot verelst at gmail com>
# Contributor: richli

pkgname=python-shapely
pkgver=1.6.4.post2
pkgrel=2
pkgdesc="Manipulation and analysis of geometric objects in the Cartesian plane"
arch=('x86_64')
url="https://pypi.python.org/pypi/Shapely"
license=('BSD')
depends=('geos' 'python')
optdepends=('python-numpy: for shapely.vectorized submodule')
makedepends=('python-setuptools' 'python-packaging' 'python-numpy' 'cython')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/S/Shapely/Shapely-${pkgver}.tar.gz")
sha512sums=('027c816ef2104f654569ca658b52b87c60e5ba6a45927fcc51c23dd1922f3f9f7fdcea3601160b0a999e39611617142833ccac790777093be8b96a0d9083f78f')

prepare() {
  rm -r Shapely-$pkgver/_vendor
  sed -i 's/from _vendor.packaging./from packaging./' Shapely-$pkgver/setup.py
}

build() {
  cd Shapely-$pkgver
  python setup.py build
}

check() {
  # TODO: make the hack less dirty...

  cd Shapely-$pkgver
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" py.test
  mv shapely.bak shapely
}

package() {
  cd Shapely-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
