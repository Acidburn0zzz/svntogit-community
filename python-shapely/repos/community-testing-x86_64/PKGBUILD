# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: David Verelst <david dot verelst at gmail com>
# Contributor: richli

pkgname=python-shapely
pkgver=1.7.0
pkgrel=1
pkgdesc="Manipulation and analysis of geometric objects in the Cartesian plane"
arch=('x86_64')
url="https://pypi.python.org/pypi/Shapely"
license=('BSD')
depends=('geos' 'python')
optdepends=('python-numpy: for shapely.vectorized submodule')
makedepends=('python-setuptools' 'python-packaging' 'python-numpy' 'cython')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/S/Shapely/Shapely-${pkgver}.tar.gz")
sha512sums=('ff7a14f20c05e3949c1e689175b80ec1f8248a0f45370a6313dbf4319ec018a8fafcfd171efbaa81bd8ceaab9c54488feca61bb03067e22a275da2b8663dc8bc')

prepare() {
  rm -r Shapely-$pkgver/_vendor
  sed -i 's/from _vendor.packaging./from packaging./' Shapely-$pkgver/setup.py
}

build() {
  cd Shapely-$pkgver
  python setup.py build
}

check() {
  # TODO: make the hack less dirty...

  cd Shapely-$pkgver
  mv shapely shapely.bak
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" py.test
  mv shapely.bak shapely
}

package() {
  cd Shapely-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
