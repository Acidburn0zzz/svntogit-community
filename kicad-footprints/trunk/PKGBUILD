# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=kicad-footprints
pkgver=6.0.0
pkgrel=1
pkgdesc="KiCad footprint libraries"
arch=('any')
url="https://www.kicad.org"
license=('custom:CC-BY-SA-4.0')
makedepends=('cmake')
conflicts=('kicad-library')
replaces=('kicad-library')
source=("https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-footprints-$pkgver.tar.bz2")
sha512sums=('d8eac8fcc380ef67ccbd3b8f2529d7023aecef674299d83aabccd41beef1934507edf3a9bdbf4b440ae550470d7f16382aba4090fe5f1d5397a8e0a0f4364ec9')
b2sums=('dd76d572b15f420de9587bea6556637ad1ad5c3e12a589950124e44fa2e0f71f6e500cf02d2cd0187e2ae92cd786407bc3fb093dd367dea9759d26c8b9d31200')

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE.md"
}

