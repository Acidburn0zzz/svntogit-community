# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=qingy
pkgver=1.0.0
pkgrel=6
pkgdesc="A DirectFB getty replacement"
arch=('i686' 'x86_64')
url="http://qingy.sourceforge.net/"
license=('GPL')
depends=('directfb' 'openssl' 'libxss')
makedepends=('emacs')
backup=('etc/qingy/settings' 'etc/qingy/welcomes')
options=('!libtool')
install=qingy.install
source=(http://downloads.sourceforge.net/sourceforge/qingy/${pkgname}-${pkgver}.tar.bz2 \
        qingy.logrotate qingy@.service)
sha1sums=('22ee505e935bf7ddcbd33e23f754a5a1ca082c81'
          '21676cf0b71334c666f7b3e4f3d8b4f319266c81'
          '55cfd0b24c1dedd8aaea42fd03f5ec39a1be5e70')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  sed -i 's|/lib/security/||' media/pam/default/qingy
  XFREE=/usr/bin/Xorg XINIT=/usr/bin/xinit \
    ./configure --prefix=/usr --sbindir=/sbin --sysconfdir=/etc \
    --disable-optimizations --enable-pam \
    --disable-static-build --enable-gpm-lock --enable-emacs
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/qingy.logrotate" "${pkgdir}/etc/logrotate.d/qingy"
  install -D -m644 "${srcdir}/qingy@.service" "${pkgdir}/usr/lib/systemd/system/qingy@.service"
}
