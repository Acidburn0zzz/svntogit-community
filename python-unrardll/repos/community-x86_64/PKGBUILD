# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Eli Schwartz <eschwartz93@gmail.com>

_pkgname=unrardll
pkgbase='python-unrardll'
pkgname=('python-unrardll' 'python2-unrardll')
pkgver=0.1.4
pkgrel=2
pkgdesc="Python wrapper for UnRAR DLL"
arch=('x86_64')
url="https://github.com/kovidgoyal/${_pkgname}"
license=('BSD')
depends=('libunrar')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"{,.asc})
sha256sums=('4149c0729cf96a0bae80360e7d94dc40af1088c8da7f6eb8d10e09b8632e92ad'
            'SKIP')
b2sums=('cbbbe53b9457a4a633f10a2bb5817685ebb75c88f6b20eee01a39610c0b5541bd86f77917efc7824277794e12b30afac4eb0237694cd7b0cc7caf76fb6dbf80e'
        'SKIP')
validpgpkeys=('3CE1780F78DD88DF45194FD706BC317B515ACE7C')

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"

  python setup.py test
  python2 setup.py test
}

package_python-unrardll() {
  depends+=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-unrardll() {
  depends+=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
