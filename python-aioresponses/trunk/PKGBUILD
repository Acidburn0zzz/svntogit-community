# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-aioresponses
pkgver=0.6.2
pkgrel=1
pkgdesc="A helper to mock/fake web requests in python aiohttp package."
url="https://github.com/pnuckowski/aioresponses"
license=('MIT')
arch=('any')
depends=('python-aiohttp')
makedepends=('python-pbr')
checkdepends=('python-pytest-runner' 'python-ddt' 'python-asynctest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pnuckowski/aioresponses/archive/$pkgver.tar.gz")
sha512sums=('424f7021ccdd69fc3a9ef332106a511ae846094ad583deccb80be20bcb560eb5dd5580b972f0782cb87d0acefd025af208ddc81851ed5607649cd44bed68dfe4')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd aioresponses-$pkgver
  python setup.py build
}

check() {
  cd aioresponses-$pkgver
  python setup.py pytest
}

package() {
  cd aioresponses-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
