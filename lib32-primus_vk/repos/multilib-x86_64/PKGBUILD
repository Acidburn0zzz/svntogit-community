# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=primus_vk
pkgname=lib32-primus_vk
pkgver=1.1
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/primus_vk"
license=('BSD')
depends=('lib32-nvidia-utils' 'bumblebee' 'lib32-vulkan-icd-loader' 'lib32-primus')
makedepends=('vulkan-validation-layers')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('37304476836c4d0cdd238d902d55d1ae772541827804eab8d4841644525c25a12f1aa24c6cfaae1729c325c6dbb731758b51a952a00c3f3c96d8f26da5d5ceda')

build() {
  cd ${_pkgname}-${pkgver}

  export CXXFLAGS+=' -m32 -DNV_DRIVER_PATH=\"/usr/lib32/libGLX_nvidia.so.0\"'
  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -D libnv_vulkan_wrapper.so -t "$pkgdir"/usr/lib32/
  install -D libprimus_vk.so -t "$pkgdir"/usr/lib32/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}
