# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=primus_vk
pkgname=lib32-primus_vk
pkgver=1.3
pkgrel=1
pkgdesc="Nvidia Vulkan offloading for Bumblebee"
arch=('x86_64')
url="https://github.com/felixdoerre/primus_vk"
license=('BSD')
depends=('lib32-nvidia-utils' 'bumblebee' 'lib32-vulkan-icd-loader' 'lib32-primus')
makedepends=('git' 'vulkan-validation-layers')
source=("${_pkgname}-${pkgver}::git+${url}.git#tag=v${pkgver}")
sha512sums=('SKIP')

_backports=(
)

_reverts=(
)

prepare() {
  cd ${_pkgname}-${pkgver}

  local _c
  
  for _c in "${_backports[@]}"; do
    git log --oneline -1 "${_c}"  
    git cherry-pick -n "${_c}"
  done

  for _c in "${_reverts[@]}"; do
    git log --oneline -1 "${_c}"
    git revert -n "${_c}"
  done
}

build() {
  cd ${_pkgname}-${pkgver}

  export CXXFLAGS+=' -m32 -DNV_DRIVER_PATH=\"/usr/lib32/libGLX_nvidia.so.0\"'
  make
}

package() {
  cd "${_pkgname}-${pkgver}"

  install -D libnv_vulkan_wrapper.so -t "$pkgdir"/usr/lib32/
  install -D libprimus_vk.so -t "$pkgdir"/usr/lib32/
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}
