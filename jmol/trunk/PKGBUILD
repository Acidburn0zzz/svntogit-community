# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: James Spencer <james.s.spencer@gmail.com>

pkgbase=jmol
pkgname=(jmol jsmol)
pkgver=14.32.13
pkgrel=1
arch=(any)
url='https://jmol.sourceforge.net'
license=(LGPL)
makedepends=(unzip)
source=(https://sourceforge.net/projects/jmol/files/Jmol/Version%20${pkgver%.*}/Jmol%20$pkgver/Jmol-$pkgver-binary.tar.gz)
sha256sums=('10d261768cca1c0a00581505f4a747365193a808e452bb856c64e400e97d24df')

package_jmol() {
  pkgdesc='A Java 3D viewer for chemical structures'
  depends=(java-runtime)

  cd $pkgbase-$pkgver

  mkdir -p "$pkgdir"/usr/share/$pkgname
  mkdir -p "$pkgdir"/usr/bin

  cp *.jar jmol.sh "$pkgdir"/usr/share/$pkgname
  ln -s /usr/share/$pkgname/$pkgname.sh "$pkgdir"/usr/bin/$pkgname
}

package_jsmol() {
  pkgdesc='A JavaScript 3D viewer for chemical structures'
  cd $pkgbase-$pkgver

  mkdir -p "$pkgdir"/usr/share
  unzip jsmol.zip -d "$pkgdir"/usr/share/
}
