# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=adrdox
pkgver=2.4.2
pkgrel=1
pkgdesc='Documentation generator for D'
arch=('x86_64')
url='https://github.com/adamdruppe/adrdox'
license=('Boost')
depends=('liblphobos' 'postgresql-libs')
makedepends=('ldc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('d7b4e3a10eb398ff491076ae0bc968956c84ddf642ef04add7ef32e7916c4c7380c51fa1271df57158f19c4cfce1e75502ab0384d9c2aec8ce3b6cf0d417c851')

prepare() {
    cd $pkgname-$pkgver
    sed -i "s/-g/-g -L=\"$LDFLAGS\"/g" Makefile
}

build() {
    cd $pkgname-$pkgver

    make ldc
}

package() {
    cd $pkgname-$pkgver

    install -Dm 755 $pkgname.main "$pkgdir"/usr/bin/$pkgname
}

