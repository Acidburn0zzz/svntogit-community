# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.5.2
pkgrel=1
pkgdesc="Audio tools running as a Jack client"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'python-zita-audiotools')
makedepends=('fftw' 'jack' 'libsndfile' 'python-setuptools' 'zita-convolver'
'zita-jclient' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('4eac0bebe9648b3d759c6e1c486ffb03ca37db9beb574d570c181e8240b9552845679e096cb91da93b557ddac29345c8da4f7dc1c172dac951d964188a4b993e')
b2sums=('58ac77b10a7ccd88357d3f64eb26dcf121a25cd62325f80c94e3eb993f9e0e005e09edfa97a11adbe34101fcf4e7495a6c217fcc9dd4b00795032fc177a3c4ff')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  depends+=('libfftw3f.so' 'libjack.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-jclient.so' 'libzita-resampler.so')

  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"

  # examples
  find examples -type f \
    \( -iname "*.py" \
    -or -iname "*README" \
    -or -iname "*presets" \
    \) \
    -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
