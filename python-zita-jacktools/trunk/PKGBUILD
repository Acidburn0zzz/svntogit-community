# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zita-jacktools
pkgname=python-zita-jacktools
pkgver=1.2.1
pkgrel=1
pkgdesc="Audio tools running as a Jack client"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio"
license=('GPL3')
depends=('gcc-libs' 'glibc' 'python-zita-audiotools')
makedepends=('fftw' 'jack' 'libsndfile' 'python-setuptools' 'zita-convolver'
'zita-jclient' 'zita-resampler')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${_name}-${pkgver}.tar.bz2")
sha512sums=('df244a653b662bf117fbb37dd680df1a90a7ac0bb11638ea352fcc17da1902552bc4e00940cbdcc7bdbb22be46c06302b60068fb4874cf06176a9089439cf294')
b2sums=('50f426ed17a53b234f1deb1ee7d26f2d573a904804ea26192b46f76b13715c1200978e4c4275a2e4543dd3fe84d28118b46913dab18a8d4fe8c92caa3f50de39')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  depends+=('libfftw3f.so' 'libjack.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-jclient.so' 'libzita-resampler.so')

  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"

  # examples
  find examples -type f \
    \( -iname "*.py" \
    -or -iname "*README" \
    -or -iname "*presets" \
    \) \
    -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
