# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>
pkgname=waf
pkgver=1.6.8
pkgrel=1
pkgdesc="General-purpose build system modelled after Scons"
url="http://code.google.com/p/waf/"
arch=('x86_64' 'i686')
license=("BSD")
depends=("python")
makedepends=("python2")
source=("http://${pkgname}.googlecode.com/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('ecaecbae140638920d1d09e5a97a10ce')
provides=("waf" "python-waf" "python2-waf")

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  # support for python3
  msg2 "Building Waf for Python 3..."
  ./waf-light configure --prefix=/usr
  ./waf-light --make-waf
  ./waf-light install -f --destdir=${pkgdir} --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8'

  # support for python2 too
  msg2 "Building Waf for Python 2..."
  sed -i 's/\/usr\/bin\/env python/\/usr\/bin\/env python2/g' waf-light
  python2 ./waf-light configure --prefix=/usr
  python2 ./waf-light --make-waf
  python2 ./waf-light install -f --destdir=${pkgdir} --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8'

  msg2 "Extracting license..."
  head waf -n 30 | tail -n 25 > LICENSE
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 waf ${pkgdir}/usr/bin/waf
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  # Force the generation of .waf.admin files
  cd demos/c
  ${pkgdir}/usr/bin/waf configure build  >& /dev/null

  # Set the right permissions
  chmod -R 0644 ${pkgdir}/usr/bin/.waf*/waflib/Tools/*
  chown -R root:root ${pkgdir}/usr/*
}

# vim:set ts=2 sw=2 et:
