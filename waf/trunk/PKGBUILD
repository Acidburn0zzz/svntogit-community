# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Sebastien Binet <binet@cern.ch>

pkgname=waf
pkgver=1.7.2
pkgrel=1
pkgdesc="General-purpose build system modelled after Scons"
url="http://code.google.com/p/waf/"
arch=('any')
license=("BSD")
depends=("python")
provides=('python-waf')
conflicts=('python-waf' 'python2-waf')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
sha256sums=('6ccbc601f4e96985af1c4e7b2512bb7c660a4bac4b4a40ffeb7bd339edcf6e71')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Building..."
  ./waf-light configure --prefix=/usr
  ./waf-light --make-waf
  ./waf-light install -f --destdir="$pkgdir" \
    --tools='compat,compat15,ocaml,go,cython,scala,erlang,cuda,gcj,boost,pep8'

  msg2 "Extracting license..."
  head -n 30 waf | tail -n 25 > LICENSE
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  msg2 "Packaging binaries..."
  install -Dm755 waf "$pkgdir/usr/bin/waf"
  # Force the generation of .waf.admin files
  cd demos/c
  "$pkgdir/usr/bin/waf" configure build >& /dev/null
  cd ../..

  msg2 "Setting permissions..."
  chmod -R 0644 "$pkgdir"/usr/bin/.waf*/waflib/Tools/*
  chown -R root:root "$pkgdir"/usr/*

  msg2 "Packaging license..."
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
