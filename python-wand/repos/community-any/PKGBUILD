# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-wand
pkgver=0.4.5
pkgrel=1
pkgdesc="Ctypes-based simple MagickWand API binding for Python"
url="https://github.com/emcconville/wand"
license=('MIT')
arch=('any')
depends=('imagemagick6' 'libxml2' 'python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'python-memory-profiler' 'python-psutil')
source=("$pkgname-$pkgver.tar.gz::https://github.com/emcconville/wand/archive/$pkgver.tar.gz")
sha512sums=('9c08fc74a81137d6e409f318a9ca814fa2876ca7e1837176f5962fc9bab03efc61e6d56e3f214e8253a7e29f01ac98918fad2e5a1bf58c4c011bf811f5e61cc1')

build() {
  cd wand-$pkgver
  python setup.py build
}

check() {
  cd wand-$pkgver
  python setup.py pytest || warning "Tests failed"
}

package() {
  cd wand-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm "$pkgdir"/usr/README.rst
}
