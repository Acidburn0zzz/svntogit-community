# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-daiquiri
pkgver=1.6.0
pkgrel=3
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
depends=('python')
optdepends=('python-json-logger: For JSON support')
makedepends=('python-pbr')
checkdepends=('python-testrepository' 'python-testtools' 'python-json-logger')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('29eb2aa70267042703307ad7776ad022620f08f9edd23b0be97e4afde1c2efa6193a928d04dfdf410220bceec4bc2a3288a614a1a7259df891751488d62bf629')

prepare() {
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py testr
}

package() {
  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
