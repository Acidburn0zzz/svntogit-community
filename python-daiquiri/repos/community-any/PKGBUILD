# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-daiquiri
pkgname=('python-daiquiri' 'python2-daiquiri')
pkgver=1.2.2
pkgrel=1
pkgdesc='Library to configure Python logging easily'
arch=('any')
license=('Apache')
url='https://github.com/jd/daiquiri'
makedepends=('python-pbr' 'python2-pbr')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-testtools'
              'python2-testtools' 'python-json-logger' 'python2-json-logger')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/daiquiri/archive/$pkgver.tar.gz")
sha512sums=('4b030f4b5efe0da596fe88b0c5ef175ae4ea82432b31d76a0298d4937a9375c26a79d730fbbba8fa583dff53d49bf46e71793ef1a5de93d36719b7fa03db8f65')

prepare() {
  cp -a daiquiri-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py build

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/daiquiri-$pkgver
  python setup.py testr

  cd "$srcdir"/daiquiri-$pkgver-py2
  python2 setup.py testr
}

package_python-daiquiri() {
  depends=('python')
  optdepends=('python-json-logger: For JSON support')

  cd daiquiri-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-daiquiri() {
  depends=('python2')
  optdepends=('python2-json-logger: For JSON support')

  cd daiquiri-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
