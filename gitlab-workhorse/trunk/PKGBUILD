# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>
# Contributor: Pavol (Lopo) Hluchy <lopo AT losys DOT eu>

pkgname=gitlab-workhorse
pkgver=7.2.0
pkgrel=1
pkgdesc="HTTP server to unload Git HTTP traffic from GitLab Rails app (Unicorn)"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/gitlab-workhorse"
license=('MIT')
depends=('glibc')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://gitlab.com/api/v4/projects/gitlab-org%2Fgitlab-workhorse/repository/archive?sha=v${pkgver}"
        "gitlab-workhorse.service")
sha512sums=('19c5259d516938c85e31fb7a1b7bb260a3180ae33c17ab69168cb4f9cc0c717a4e9915270d9454667308cc8798aeec6ac5e12852c18fed9907f0bc7f951e3e07'
            'c5ce0e47fa186cf9374e9771e99df0346c3652a20dd43257a6d50003b083e78cb2ed65abdbd45cc4062114023f7b112793c25136e2d6b118e1ba30623f16be83')

build() {
  cd "${pkgname}-v${pkgver}-"*

  make
}

package() {
  cd "${pkgname}-v${pkgver}-"*

  install -Dm755 "gitlab-workhorse" "${pkgdir}/usr/bin/gitlab-workhorse"
  install -Dm755 "gitlab-zip-cat" "${pkgdir}/usr/bin/gitlab-zip-cat"
  install -Dm755 "gitlab-zip-metadata" "${pkgdir}/usr/bin/gitlab-zip-metadata"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm0644 "${srcdir}/gitlab-workhorse.service" "${pkgdir}/usr/lib/systemd/system/gitlab-workhorse.service"
}

# vim:set ts=2 sw=2 et:
