# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuild with each new gcc version

pkgbase=libtool-multilib
pkgname=(libtool-multilib lib32-libtool)
pkgver=2.4
pkgrel=4
pkgdesc="A generic library support script for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
_gccver=4.6.1
makedepends=(sh tar "gcc-multilib=$_gccver")
options=('!libtool')
source=(ftp://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.xz)
md5sums=('4e6144439d95d7332dc50ace6dd24c55')

build() {
  cd "$srcdir"

  rm -rf libtool-64 libtool-32
  mv libtool-$pkgver libtool-64
  cp -a libtool-64 libtool-32

  msg2 "Building libtool-64..."
  cd "$srcdir/libtool-64"
  ./configure --prefix=/usr
  make

  msg2 "Building libtool-32..."
  export CC="gcc -m32"
  export CXX="g++ -m32"

  cd "$srcdir/libtool-32"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

check() {
  cd "$srcdir/libtool-64"
  #make check
  cd "$srcdir/libtool-32"
  #make check
}

package_libtool-multilib() {
  groups=('multilib-devel')
  depends=(sh tar "gcc-multilib=$_gccver" "lib32-libtool=$pkgver-$pkgrel")
  provides=("libtool=$pkgver-$pkgrel")
  conflicts=(libtool)
  install=libtool.install

  cd "$srcdir/libtool-64"
  make DESTDIR="$pkgdir" install
}

package_lib32-libtool() {
  pkgdesc="A generic library support script (32-bit)"
  depends=(lib32-glibc)

  cd "$srcdir/libtool-32"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
