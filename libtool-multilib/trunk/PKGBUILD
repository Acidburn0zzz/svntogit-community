# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

# NOTE: requires rebuilt with each new gcc version

pkgname=libtool-multilib
pkgver=2.2.10
pkgrel=5
pkgdesc="A generic library support script for multilib"
arch=('x86_64')
url="http://www.gnu.org/software/libtool"
license=('GPL')
depends=('sh' 'tar' 'texinfo' 'lib32-glibc')
makedepends=(gcc-multilib)
provides=("libtool=$pkgver-$pkgrel" "lib32-libtool=$pkgver-$pkgrel")
conflicts=(libtool lib32-libtool)
replaces=(lib32-libtool)
options=('!libtool')
install=libtool.install
source=(ftp://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.gz)
md5sums=('b745d220e88163fcd9eea0a90ccf21b0')

build() {
  cd "$srcdir"

  rm -rf libtool-64 libtool-32
  mv libtool-$pkgver libtool-64
  cp -a libtool-64 libtool-32

msg2 "Building libtool-64..."

  cd "$srcdir/libtool-64"
  ./configure --prefix=/usr
  make

msg2 "Building libtool-32..."

  export CC="gcc -m32"

  cd "$srcdir/libtool-32"
  ./configure --prefix=/usr --libdir=/usr/lib32
  make
}

package() {

msg2 "Installing libtool-32"

  cd "$srcdir/libtool-32"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}

msg2 "Installing libtool-64"

  cd "$srcdir/libtool-64"
  make DESTDIR="$pkgdir" install
}
