# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgbase=qtcurve
pkgname=('qtcurve-qt4' 'qtcurve-kde4')
pkgver=1.8.15
pkgrel=2
arch=('i686' 'x86_64')
groups=('qtcurve')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE and Gtk'
url='https://github.com/QtCurve/qtcurve-qt4'
makedepends=('cmake' 'automoc4' 'kdebase-workspace')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
md5sums=('ee0b35fe428807b0dc2b0c57e5eb4f38')

build() {
  cd "qtcurve-qt4-$pkgver"

  sed -i "s/QApplication/QCoreApplication/g" tools/gen_image_header_qt4.cpp

  mkdir build-{qt,kde}4

  cd build-qt4
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr \
           -DQTC_QT4_ENABLE_KDE=false
  make

  cd ../build-kde4
  cmake .. -DCMAKE_BUILD_TYPE=Release \
           -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package_qtcurve-qt4() {
  depends=('qt4')
  cd "qtcurve-qt4-$pkgver/build-qt4/qt4/style"
  make DESTDIR="$pkgdir" install
}

package_qtcurve-kde4() {
  depends=('kdebase-workspace')
  cd "qtcurve-qt4-$pkgver/build-kde4"
  make DESTDIR="$pkgdir" install
}
