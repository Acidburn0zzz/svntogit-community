# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=marco
pkgver=1.22.4
pkgrel=1
pkgdesc="A window manager for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('libcanberra' 'libgtop' 'mate-desktop' 'zenity' 'libxpresent')
makedepends=('intltool' 'itstool')
groups=('mate')
conflicts=('marco-gtk3')
replaces=('marco-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('63a6a079a7ce7cd34ea175a6b0ecd44550f71ff5888cdf975d350e52b925187d')

build() {
	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--sysconfdir=/etc \
        	--localstatedir=/var \
        	--enable-startup-notification

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
