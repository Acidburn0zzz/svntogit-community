# Maintainer: Tim Meusel <tim@bastelfreak.de>

_gemname='jwt'
pkgname="ruby-${_gemname}"
pkgver=2.2.3
pkgrel=1
pkgdesc='A pure ruby implementation of the RFC 7519 OAuth JSON Web Token (JWT) standard'
arch=('any')
url="https://github.com/jwt/${pkgname}"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=("${url}/archive/v${pkgver}/${_gemname}-v${pkgver}.tar.gz"
        'drop-ci-dependencies.patch'
        'disable-broken-test.patch')
sha512sums=('ebefbf8f825afc1a78329088012118ed681a61bdf9e1c6a3fa125d46105092f1f26a4bc88791788c0290009dbc0427128d655a301c034ac07caf14216744962c'
            '0fd44de4b939f5cf0e607e827ccc882f82e55f6d5ffe6b5746268b88fa478efeadc4ec44668e82ab4fd9da4d3e778fb9899a48e602a2a07e71ae475326272c09'
            '77f7f0c172c015b4b83b9a0ec179fd24517f9972f0b930de8df0fc0ef1f978545ffe187ed74b790b8f62a494b07dba018558bec8b15f0023717882620070fe8f')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 -N -i "${srcdir}/drop-ci-dependencies.patch"
  patch -p1 -N -i "${srcdir}/disable-broken-test.patch"
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  gem build "${pkgname}.gemspec"
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  rake test
}

package() {
cd "${srcdir}/${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --verbose --ignore-dependencies --no-user-install --install-dir "${pkgdir}/${_gemdir}" --bindir "${pkgdir}/usr/bin" "${_gemname}-${pkgver}.gem"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"

  install -Dm 644 README.md AUTHORS CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  rm -rf "${pkgdir}/${_gemdir}/cache"
}
