# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-polkit-agent
pkgver=5.2.0.1
pkgrel=1
pkgdesc='Deepin Polkit Agent'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-polkit-agent"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'polkit-qt5')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-polkit-agent/archive/$pkgver.tar.gz")
sha512sums=('ff8f1167996e3520616137a4d9d67c7a216e3a3f066a1083f95203ed44f7d9a105bc978ac59dc2f21c2fab47b0d79e6cfc954c9c65a0e0090c892ac2f0857c46')

prepare() {
  cd dde-polkit-agent-$pkgver
  # https://github.com/linuxdeepin/developer-center/issues/1721
  sed -i 's/bool is_deepin = true/bool is_deepin = false/' policykitlistener.cpp
}

build() {
  cd dde-polkit-agent-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-polkit-agent-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
