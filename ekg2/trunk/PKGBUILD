# $Id$
# Maintainer: Bart≈Çomiej Piotrowski
# Contributor: Mateusz Herych <heniekk@gmail.com>

pkgname=ekg2
pkgver=0.3.1
pkgrel=2
pkgdesc="ncurses based Jabber, Gadu-Gadu, Tlen and IRC client"
arch=('i686' 'x86_64')
url="http://ekg2.org/"
license=('GPL')
depends=('aspell' 'libjpeg' 'python2' 'libgadu' 'gnutls' 'gpm' 'libidn' 'giflib')
optdepends=('xosd: xosd support'
	        'sqlite: sqlite support'
			'gtk2: gtk support')
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('68fc05b432c34622df6561eaabef5a40')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|ncursesw/ncurses.h|ncurses.h|g' configure plugins/ncurses/ecurses.h

  ./configure --prefix=/usr     \
              --sysconfdir=/etc \
              --with-libgadu    \
              --with-xosd       \
              --enable-unicode  \
              --enable-shared   \
              --without-gpg
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/usr/lib/perl5/core_perl/perllocal.pod
  mv "$pkgdir"/usr/libexec/ioctld "$pkgdir"/usr/lib/ekg2/ioctld
  rm -rf "$pkgdir"/usr/libexec
  chmod -R 755 "$pkgdir"/usr/lib/perl5
}
