# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=variety
pkgver=0.6.4
_pkgver2=0-589-201704290523
pkgrel=1
pkgdesc='Changes the wallpaper on a regular interval using user-specified or automatically downloaded images.'
arch=('any')
url="http://peterlevi.com/variety/"
license=('GPL3')
depends=('gtk3' 'imagemagick' 'libgexiv2' 'libnotify' 'python2-beautifulsoup4' 'python2-cairo'
         'python2-configobj' 'python2-dbus' 'python2-gobject' 'python2-httplib2' 'python2-pillow'
         'python2-lxml' 'python2-pycurl' 'python2-requests')
makedepends=('python2-distutils-extra')
source=(https://launchpad.net/variety/trunk/${pkgver}/+download/${pkgname}_${pkgver}-${_pkgver2}.tar.gz{,.asc}
        disable-panoramio.patch
        fix-vauleerror.patch)
validpgpkeys=('53A44FA64701571F59CD8C9BA7FAEF1B4791406E') # Peter Levi
sha256sums=('b7f056afdd6da9547f8ca2ca3bbebf083e24cbab0b706cef976a6b3f1f05adc3'
            'SKIP'
            '011d4f19a17fd59a175f6fa36e282c82c333ce0e849673aeea678f2ee56db743'
            'b16842754013398eb85499137b4b2ea36748505e65eef46864847f7bb8606264')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # Disable Panoramio support (removes webkitgtk dependency)
  patch -Np1 -i ../disable-panoramio.patch

  # Fix error when appindicator is missing
  patch -Np1 -i ../fix-vauleerror.patch
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
