# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=variety
pkgver=0.6.3
pkgrel=2
pkgdesc='Changes the wallpaper on a regular interval using user-specified or automatically downloaded images.'
arch=('any')
url="http://peterlevi.com/variety/"
license=('GPL3')
depends=('desktop-file-utils' 'gtk3' 'imagemagick' 'libnotify' 'python2-beautifulsoup4'
         'python2-cairo' 'python2-configobj' 'python2-dbus' 'python2-exiv2'
         'python2-gobject' 'python2-httplib2' 'python2-pillow' 'python2-lxml'
         'python2-pycurl' 'python2-requests')
optdepends=('yelp: for reading help documents')
makedepends=('python2-distutils-extra')
source=(https://launchpad.net/variety/trunk/${pkgver}/+download/${pkgname}_${pkgver}-0-581-201611011859-ubuntu16.04.1.tar.gz{,.asc}
        disable-panoramio.patch)
validpgpkeys=('53A44FA64701571F59CD8C9BA7FAEF1B4791406E') # Peter Levi
sha256sums=('30f82e606a090dfab463f29500ec9dd9ad78fcfcd5a96c5f0f5d0b2bd9ce79eb'
            'SKIP'
            '011d4f19a17fd59a175f6fa36e282c82c333ce0e849673aeea678f2ee56db743')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  # Disable Panoramio support (removes webkitgtk dependency)
  patch -Np1 -i ../disable-panoramio.patch
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
