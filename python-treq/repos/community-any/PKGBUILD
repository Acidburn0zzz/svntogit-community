# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-treq
pkgver=20.3.0
pkgrel=1
pkgdesc="A requests-like API built on top of twisted.web's Agent"
arch=('any')
license=('MIT')
url='https://github.com/twisted/treq'
depends=('python-incremental' 'python-requests' 'python-pyopenssl' 'python-service-identity'
         'python-idna' 'python-attrs' 'python-twisted')
makedepends=('python-setuptools' 'httpbin')
checkdepends=('python-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/twisted/treq/archive/release-$pkgver.tar.gz")
sha512sums=('8daacffd94f0547c9b11e49cea9fe6f11f27493b9434333679aad0db4f7cb869890fdacc6aefad9c4590e38f522b31a45039dc37a784dd755e5cce807583d9e8')

build() {
  cd treq-release-$pkgver
  python setup.py build
}

check() {
  cd treq-release-$pkgver
  PYTHONPATH="$PWD/build/lib" trial3 treq
}

package() {
  cd treq-release-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
