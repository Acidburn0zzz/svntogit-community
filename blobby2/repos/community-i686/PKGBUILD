# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Urs Wolfer <uwolfer @ fwo.ch>

pkgname=blobby2
pkgver=0.9c
arch=('x86_64' 'i686')
pkgrel=1
pkgdesc="A beach ball game with blobs of goo"
url="http://sourceforge.net/projects/blobby/"
license=('GPL')
depends=('sdl' 'physfs' 'libgl')
makedepends=('cmake' 'boost' 'mesa' 'zip')
source=(http://downloads.sourceforge.net/project/blobby/Blobby%20Volley%202%20%28Linux%29/Beta%20$pkgver/$pkgname-linux-$pkgver.tar.gz
	$pkgname.desktop $pkgname.png)
md5sums=('d3a6d185c4acd1c5eddf0ce46b3f55bb'
         'ab05bed794ee78db693fd3036393275a'
         'c1bc427b41a0a3facd771ac83c7fb412')

build() {
  cd $srcdir/blobby-beta-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $srcdir/blobby-beta-$pkgver

  make DESTDIR=$pkgdir install

  # install .desktop file and icon
  install -dm755 $pkgdir/usr/share/{applications,pixmaps}
  install -m644 $srcdir/$pkgname.desktop $pkgdir/usr/share/applications
  install -m644 $srcdir/$pkgname.png $pkgdir/usr/share/pixmaps
}
