# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=kgraphviewer
pkgver=2.0.2
_kdever=4.2.2
pkgrel=4
pkgdesc="A Graphviz dot graph file viewer for KDE"
arch=('i686' 'x86_64')
url='http://opendesktop.org/content/show.php?content=23999'
license=('GPL' 'FDL')
depends=('kdebase-runtime' 'kdepimlibs' 'graphviz')
makedepends=('pkgconfig' 'cmake' 'automoc4')
install=${pkgname}.install
source=("http://download.kde.org/stable/${_kdever}/extragear/${pkgname}-${pkgver}.tar.bz2")
md5sums=('33248d1a8a8a77725cb94c554c6108c7')

build(){
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
