# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>
pkgname=kgraphviewer
pkgver=2.4.0
pkgrel=1
pkgdesc="A Graphviz dot graph file viewer for KDE"
arch=('i686' 'x86_64')
url="https://www.kde.org/applications/graphics/kgraphviewer"
license=('GPL' 'FDL')
depends=('graphviz' 'hicolor-icon-theme' 'kparts')
makedepends=('boost' 'cmake' 'qt5-base' 'qt5-svg' 'extra-cmake-modules' 'kcoreaddons' 'kdoctools' 'kio')
source=("https://download.kde.org/stable/kgraphviewer/${pkgver}/kgraphviewer-${pkgver}.tar.xz"{,.sig})
sha256sums=('88c2fd6514e49404cfd76cdac8ae910511979768477f77095d2f53dca0f231b4'
            'SKIP')
validpgpkeys=('2D1D5B0588357787DE9EE225EC94D18F7F05997E')

build(){
  mkdir -p "$srcdir/build"
  cd "$srcdir/build"
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}
