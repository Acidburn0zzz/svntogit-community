# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-requests-mock
pkgver=1.9.1
pkgrel=1
pkgdesc="A mock of useful classes and functions to be used with python-requests."
arch=('any')
url="https://github.com/jamielennox/requests-mock"
license=('Apache')
depends=('python-requests' 'python-six')
makedepends=('python-pbr')
checkdepends=('python-mock' 'python-purl' 'python-pytest' 'python-testrepository')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jamielennox/requests-mock/archive/$pkgver.tar.gz")
sha512sums=('5377962ff78b770fc5290abbd9b189d0247f3cd2c31d657c4e6d823e5fde503d9690200ba3955cf10ba1ec9f12ac688a4954c9885a5fc6f3ce23269373fd7f35')

export PBR_VERSION=$pkgver

build() {
  cd requests-mock-$pkgver
  python setup.py build
}

check() {
  cd requests-mock-$pkgver
  python -m pytest tests/pytest
  python setup.py testr
}

package() {
  cd requests-mock-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
