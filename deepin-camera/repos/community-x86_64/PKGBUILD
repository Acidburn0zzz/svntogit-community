# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-camera
pkgver=1.0.0.16
pkgrel=1
pkgdesc='Tool to view camera, take photo and video'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-camera"
license=('GPL3')
depends=('deepin-qt5integration' 'deepin-qt-dbus-factory' 'ffmpeg' 'ffmpegthumbnailer' 'portaudio' 'pulseaudio')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-camera/archive/$pkgver.tar.gz")
sha512sums=('91a3fec9b0a8e0dcea922b36c5e5c82a22ded81dd5263ed0c800c489af6b8309b1d067618b215e6f58fddeed0a6942e2175d5c89626dc40a76ef2271f77903c7')

prepare() {
  cd deepin-camera-$pkgver
  sed -i '/^CONFIG/a LIBS = -lpulse' libcam/libcam.pro
}

build() {
  cd deepin-camera-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-camera-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
