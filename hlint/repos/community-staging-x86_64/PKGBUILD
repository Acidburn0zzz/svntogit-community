# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=hlint
pkgver=1.9.26
pkgrel=3
pkgdesc="Source code suggestions"
url="http://community.haskell.org/~ndm/hlint/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=('gmp' 'libffi')
makedepends=("ghc=7.10.3" "haskell-ansi-terminal" "haskell-cmdargs" "haskell-cpphs"
             "haskell-extra" "haskell-src-exts" "haskell-hscolour" "haskell-uniplate"
             "haskell-refact")
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f9dcb152d05472c16572e9519494b376c12b748a886f79f74ffcfcb973c33553')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"    
    runhaskell Setup configure -O --prefix=/usr \
        --docdir="/usr/share/doc/${pkgname}" --datasubdir="$pkgname" \
            -fgpl -fthreaded
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -r "$pkgdir"/usr/{lib,share/doc}
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
