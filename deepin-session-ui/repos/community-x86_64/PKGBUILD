# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.1.0099.5
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center')
makedepends=('deepin-gettext-tools' 'qt5-tools')
provides=('deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz")
sha512sums=('d9b5e43fbac5283bdb0dfef4950eea7fdeda7516a2f61d0af4391a2fbd5a113f2e53ed725da0179355c629567fd907400965535a395e21ced4115d30bd1ac276')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
