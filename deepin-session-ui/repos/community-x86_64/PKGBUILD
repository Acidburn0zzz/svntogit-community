# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.3.0.1
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center' 'deepin-wallpapers')
makedepends=('deepin-gettext-tools' 'qt5-tools')
provides=('deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$pkgver.tar.gz"
         deepin-session-ui-qt5.15-reborn.patch::https://github.com/linuxdeepin/dde-session-ui/commit/926744b8111f30f8b40b9a8cfb4b3309babdf863.patch)
sha512sums=('b12055fe9a0d66e175419d6cebf10986182c100755c56bab31e8d03b1ec8ec9224c749a121db5168b57f94864e54f9c3487328daede7f4593b8982cb828de0fc'
            '181e2b27795450a420aed2229bdbdd0f0d47d1c80fdb016c08acc44120147aa0ea1bafc3c303a6f8918164bde764ad309ed54b5aa6082e3b1b420bf2d992231e')

prepare() {
  cd dde-session-ui-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp

  patch -p1 -i ../deepin-session-ui-qt5.15-reborn.patch # Fix build with Qt 5.15 again
}

build() {
  cd dde-session-ui-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
