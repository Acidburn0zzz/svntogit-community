# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-ui
pkgver=5.2.0.0
_tag=5.2.0.0-1
pkgrel=1
pkgdesc='Deepin desktop-environment - Session UI module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-ui"
license=('GPL3')
groups=('deepin')
depends=('gsettings-qt' 'deepin-qt5integration' 'liblightdm-qt5' 'qt5-svg' 'deepin-daemon'
         'deepin-control-center')
makedepends=('deepin-gettext-tools' 'qt5-tools')
provides=('deepin-notifications')
conflicts=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
replaces=('dde-workspace' 'dde-session-ui' 'deepin-notifications')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-ui/archive/$_tag.tar.gz"
         deepin-session-ui-qt5.15.patch)
sha512sums=('4bd87efdbf27a231e6db9f64e159c7ad69bcdacda881969805e1f4d58240f8eb7cdafa76720679fb688836e28552da0405a65ffb1e32bd33d3d1bdb9180f57f2'
            '846726964cac5005b0ac3a5043e5df914ff83faff68f98d2513d86494a9718a85ae4268776fd08ffe852e28ac0ae5878353d3c65db84fbd1509b2325e70fe8d6')

prepare() {
  cd dde-session-ui-$_tag
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' widgets/*.cpp

  patch -p1 -i ../deepin-session-ui-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd dde-session-ui-$_tag
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-session-ui-$_tag
  make INSTALL_ROOT="$pkgdir" install
}
