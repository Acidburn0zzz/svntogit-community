# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Justin Benge <justinbng36@gmail.com>

pkgname=python-remoto
_pkgbase="${pkgname//python-/}"
pkgver=1.1.0
pkgrel=3
pkgdesc="A very simplistic remote-command-executor using connections to hosts"
arch=('any')
url="https://github.com/alfredodeza/${_pkgbase}"
license=('MIT')
depends=('python' 'python-execnet')
makedepends=('git' 'python' 'python-execnet' 'python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('3e9d9898b88b5993df985bad22382df0fbd9ecb3ca8052e2443f81b5a0eb521a')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  :
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  py.test -v remoto/tests
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

