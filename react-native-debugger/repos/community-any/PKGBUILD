# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=react-native-debugger
pkgver=0.10.0
_commit=7dbaba93de83b04c6ef6136581ee3a097afa1e88
pkgrel=1
pkgdesc='The standalone app for React Native Debugger, with React DevTools / Redux DevTools'
arch=('any')
url='https://github.com/jhen0409/react-native-debugger'
license=('MIT')
depends=('electron')
makedepends=('asar' 'git' 'npm')
source=("git+https://github.com/jhen0409/react-native-debugger.git#commit=${_commit}"
        'iconPath.patch'
        'react-native-debugger.desktop'
        'react-native-debugger.js')
sha256sums=('SKIP'
            '9a6343d3d84a7847e8c6215fa562267f7016b38c2990c15f056236c72e54a2e7'
            'f63e79516db0feb2e76c366402cf266165c0f7eb282dc55703b800b91152867d'
            '8cd406c3b2d091f2a2b73e5755682dc925bb1f75d0d425b82507136b350a4ceb')

prepare() {
    cd ${pkgname}

    patch -Np1 -i ../iconPath.patch
}

build() {
    cd ${pkgname}

    npm install --ignore-scripts
    cd dist
    npm install
    node ../scripts/patch-modules.js
    cd ..
    npm run build
}

package() {
    cd ${pkgname}

    install -dm755 "${pkgdir}/usr/lib"
    rm dist/logo.png  # see iconPath.patch
    asar pack dist "${pkgdir}/usr/lib/${pkgname}.asar"

    install -Dm755 ../${pkgname}.js "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/applications" ../${pkgname}.desktop
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
    install -Dm644 electron/logo.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
