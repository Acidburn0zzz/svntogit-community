# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=react-native-debugger
pkgver=0.9.13
_commit=8d40748a9120adf5f04b6e7dae87ca2f2128f621
pkgrel=1
pkgdesc='The standalone app for React Native Debugger, with React DevTools / Redux DevTools'
arch=('any')
url='https://github.com/jhen0409/react-native-debugger'
license=('MIT')
depends=('electron')
makedepends=('asar' 'git' 'npm')
source=("git+https://github.com/jhen0409/react-native-debugger.git#commit=${_commit}"
        'iconPath.patch'
        'nodeIntegration.patch'
        'react-native-debugger.desktop'
        'react-native-debugger.js'
        'react-native-debugger.sh'
        'setZoomLevelLimits.patch')
sha256sums=('SKIP'
            '4ac46fdbfe1658ea63f28b56ceda94b06324f8476090a0d0888f99ae43dd27ad'
            '6022ea467881e17ff7af9dc94455fc13eff860bf59a7ced7abf222a003a22526'
            'f63e79516db0feb2e76c366402cf266165c0f7eb282dc55703b800b91152867d'
            '59e0c247d006a437a619917df7e76a506aadb4d751b7d3bb476a96030620a660'
            '8a217b932b1042c14930d89f56661dbdc13b1afe04680df6b2fefceb242fb05c'
            '49eceb17c8f2a2e3caa37aeb195ae3888399cf2946954f6f8f7e2d1c4050e3eb')

prepare() {
    cd ${pkgname}

    patch -Np1 -i ../iconPath.patch
    patch -Np1 -i ../nodeIntegration.patch
    patch -Np1 -i ../setZoomLevelLimits.patch
}

build() {
    cd ${pkgname}

    npm install --ignore-scripts
    cd dist
    npm install
    node ../scripts/patch-modules.js
    cd ..
    npm run build
}

package() {
    cd ${pkgname}

    install -dm755 "${pkgdir}/usr/lib/${pkgname}"
    rm dist/logo.png  # see iconPath.patch
    asar pack dist "${pkgdir}/usr/lib/${pkgname}/app.asar"

    install -Dm755 ../${pkgname}.sh "${pkgdir}/usr/bin/${pkgname}"
    install -m755 ../${pkgname}.js "${pkgdir}/usr/lib/${pkgname}/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/applications" ../${pkgname}.desktop
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
    install -Dm644 electron/logo.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
