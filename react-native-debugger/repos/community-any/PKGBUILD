# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=react-native-debugger
pkgver=0.9.7
_commit=6d05271a1f8c97ff7295a552c25618f49e2dbfbd
pkgrel=1
pkgdesc='The standalone app for React Native Debugger, with React DevTools / Redux DevTools'
arch=('any')
url='https://github.com/jhen0409/react-native-debugger'
license=('MIT')
depends=('electron')
makedepends=('asar' 'git' 'npm')
source=("git+https://github.com/jhen0409/react-native-debugger.git#commit=${_commit}"
        'Content-Security-Policy.patch'
        'iconPath.patch'
        'nodeIntegration.patch'
        'react-native-debugger.desktop'
        'react-native-debugger.js'
        'setZoomLevelLimits.patch')
sha256sums=('SKIP'
            '11695a30802fb9d559608f56abdd09d864b40311de310c08e084fa93abe39443'
            '4ac46fdbfe1658ea63f28b56ceda94b06324f8476090a0d0888f99ae43dd27ad'
            '6700efd069c5870edf0e4aa826a89860254c262f437761ec0c328273604bda83'
            'f63e79516db0feb2e76c366402cf266165c0f7eb282dc55703b800b91152867d'
            '8cd406c3b2d091f2a2b73e5755682dc925bb1f75d0d425b82507136b350a4ceb'
            '6853ab684f5eb39e32e0b75ead0cf19c0a883108888b003f0f80cfd07cb4f965')

prepare() {
    cd ${pkgname}

    patch -Np1 -i ../Content-Security-Policy.patch
    patch -Np1 -i ../iconPath.patch
    patch -Np1 -i ../nodeIntegration.patch
    patch -Np1 -i ../setZoomLevelLimits.patch
}

build() {
    cd ${pkgname}

    npm install --ignore-scripts
    cd dist
    npm install
    node ../scripts/patch-modules.js
    cd ..
    npm run build
}

package() {
    cd ${pkgname}

    install -dm755 "${pkgdir}/usr/lib"
    rm dist/logo.png  # see iconPath.patch
    asar pack dist "${pkgdir}/usr/lib/${pkgname}.asar"

    install -Dm755 ../${pkgname}.js "${pkgdir}/usr/bin/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/applications" ../${pkgname}.desktop
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE.md
    install -Dm644 electron/logo.png \
        "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
}
