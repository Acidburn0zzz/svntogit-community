# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <filipe.lains@gmail.com>
# Contributor: Michal Krenek (Mikos) <m.krenek@gmail.com>

pkgname=sdrangel
pkgver=4.0.5
pkgrel=1
pkgdesc="Qt5/OpenGL SDR and signal analyzer frontend."
arch=('x86_64')
url="https://github.com/f4exb/sdrangel"
license=('GPL3')
depends=('pkg-config' 'boost' 'log4cpp' 'opencv'
	'qt5-base>=5.9' 'qt5-tools' 'qt5-multimedia' # QT5
	'fftw' 'lz4' 'nanomsg' 'ffmpeg>=3.1'
	'cm256cc' 'dsdcc>=1.8.3'
	'pulseaudio')
makedepends=('git' 'cmake')
optdepends=('ffmpeg: DATV demodulator'
	    'libmirisdr4: SDRPlay support'
	    'rtl-sdr: RTLSDR support'
	    'hackrf: HackRF support'
	    'libad9361-iio: PlutoSDR support'
	    'limesuite: LimeSDR support'
	    'bladerf: BladeRF support'
	    'airspy: AirSPY support')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('4248aed4b127bc125e1b6caaeb715976467764514e9b9652d4f4621a950aa82da68eb0e87996b48690e6040cbf7ce0c5dab3a015c950d8e19a78c0e73a06be27')

build() {
  mkdir -p "$srcdir"/$pkgname-$pkgver/build
  cd "$srcdir"/$pkgname-$pkgver/build

  cmake .. \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLIBDSDCC_INCLUDE_DIR=/usr/include/dsdcc \
    -DCM256CC_INCLUDE_DIR=/usr/include/cm256cc

  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/build

  make DESTDIR="$pkgdir" install
}
