# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=haskell-haddock-api
pkgver=2.17.4
pkgrel=1
pkgdesc="A documentation-generation tool for Haskell libraries"
url="http://www.haskell.org/haddock/"
license=("BSD3")
arch=('i686' 'x86_64')
depends=("ghc" "haskell-ghc-paths" "haskell-haddock-library")
source=("https://hackage.haskell.org/package/haddock-api-${pkgver}/haddock-api-${pkgver}.tar.gz")
sha512sums=('c574cb7bde219f639abbaf1ca2971409dc1a77987bb919c99bbc464be4c45235f4de8ff2d1cbdcd85da23d0c16b608111dd0ac4d6175ff0e70fd2e5fa94a68a0')

build() {
    cd "${srcdir}/haddock-api-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html || warning "Failed to run haddock"
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/haddock-api-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/haddock-api"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
