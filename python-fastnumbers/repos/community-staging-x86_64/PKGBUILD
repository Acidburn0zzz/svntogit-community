# Maintainer: David Runge <dvzrv@archlinux.org>

_name=fastnumbers
pkgname=python-fastnumbers
pkgver=3.0.0
pkgrel=2
pkgdesc="Super-fast and clean conversions to numbers"
arch=('x86_64')
url="https://github.com/SethMMorton/fastnumbers"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        "${pkgname}-3.0.0-python3.9.patch")
sha512sums=('31a0e1bfd503204185b26d29172e1f6ae9f6a19328ce2b4eb93e50335e22f81802c5c3f0a42b242ce914bf403c7cfc279166c33a4843e963329b666cd949c3b0'
            'eda461a1e062c1fc1624d49ef77fb0ca4f770b461086ac283e7b7fce1fbfb29a7bbe893d0fe29b1b3d787da0b53b27aefcc2fa97bfb3fc04319ea5634b932ef3')
b2sums=('2fab831be7054eca8b1d0fd4a2ae5188d6ed3d124c0e4467fe2443ad3300380f494b6901450a12512d9ac075b832bf9dfc546be2e3d288c6b824cd9ffbb5ac90'
        'bacb2cc5d28b2e7af591022899711bc18da96477cbf4c9b9925159f6afe29e282c023cb2d3f819ccf79da7e406f83891db14aac4ac567188da2f18cc42a52b74')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # add fix for (now) non-public function:
  # https://github.com/SethMMorton/fastnumbers/issues/43
  patch -Np1 -i ../"${pkgname}-3.0.0-python3.9.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _py3_ver=$(python --version | cut -d " " -f2)
  export PYTHONPATH="build/lib.linux-$CARCH-${_py3_ver%"."*}:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
