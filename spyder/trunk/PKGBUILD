# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=1.1.7
pkgrel=1
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features (formerly pydee).'
arch=('i686' 'x86_64')
url='http://spyderlib.googlecode.com/'
license=('MIT')
depends=('python-numpy' 'python-qscintilla' 'setuptools')
optdepends=('pylint: code analysis'
            'python-matplotlib: 2D plotting'
            'python-scipy: signal/image processing')
source=("http://spyderlib.googlecode.com/files/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}.desktop")
md5sums=('e3aaab55c8ee204beda5244476603ed8'
         'aa0be394320c933089baa073bd9db1a1')

build() {
  cd ${pkgname}-${pkgver}

  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python setup.py install --prefix=/usr --root=${pkgdir} --optimize=1

  install -D -m644 spyderlib/__init__.py \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
