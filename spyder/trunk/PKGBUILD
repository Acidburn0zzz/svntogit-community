# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: TDY <tdy@gmx.com>

pkgname=spyder
pkgver=2.0.1
pkgrel=2
pkgdesc='Scientific PYthon Development EnviRonment providing MATLAB-like features.'
arch=('any')
url='http://spyderlib.googlecode.com/'
license=('MIT')
depends=('pyqt')
makedepends=('python-sphinx')
optdepends=('pylint: powerful code analysis'
            'ipython: enhanced Python interpreter'
            'pyflakes: real-time code analysis'
            'python-rope: editor code completion, calltips and go-to-definition'
            "python-sphinx: object inspector's rich text mode"
            'python-numpy: N-dimensional arrays'
            'python-scipy: signal/image processing'
            'python-matplotlib: 2D/3D plotting')
source=("http://spyderlib.googlecode.com/files/${pkgname}-${pkgver}.zip"
        "${pkgname}.desktop")
md5sums=('cb5f21841d98d8cf10e9fd3f21a39042'
         '01275d559ef56c33ab68c26340c10cef')

build() {
  cd ${pkgname}-${pkgver}

  python2 setup.py build
}

package() {
  cd ${pkgname}-${pkgver}

  python2 setup.py install --prefix=/usr --root=${pkgdir} --optimize=1

  install -D -m644 spyderlib/__init__.py \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
