# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python2-grequests
pkgver=0.1.0
pkgrel=1
_libname=${pkgname/python2-/}
pkgdesc="Allows you to use Requests with Gevent to make asyncronous HTTP Requests easily."
url="http://crate.io/packages/grequests/"
depends=('python2-requests' 'python2-gevent')
makedepends=('python2-distribute')
license=('custom: ISC')
arch=('any')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz https://github.com/kennethreitz/grequests/raw/master/LICENSE)

build() {
    cd "$srcdir/$_libname-$pkgver"
	find -type f -exec sed -i '1 s|python$|python2|' {} +
    python2 setup.py build
}

package() {
    cd "$srcdir/$_libname-$pkgver"
    python2 setup.py install -O1 --root="$pkgdir"
	install -m0644 -D "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('73fa312f3d4b62f038a44c0a56cd3b27973b57208befbaaf96db3683fa02e2b9'
            '4063882a8913e4b37457d58fe6600bd975ba053c8db2e8c6b218e7c9c8575ca8')
