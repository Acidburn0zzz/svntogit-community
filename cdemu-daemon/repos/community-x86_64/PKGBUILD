# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=3.2.0
pkgrel=1
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=(x86_64)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(dbus vhba-module libao 'libmirage>=3.1.0')
makedepends=(cmake intltool ninja)
optdepends=('alsa-lib: to enable the ALSA audio driver'
            'pulseaudio: to enable the PA audio driver')
backup=(etc/conf.d/cdemu-daemon
        etc/dbus-1/system.d/cdemu-daemon-dbus.conf)
install=cdemu-daemon.install
source=("https://downloads.sourceforge.net/cdemu/$pkgname/$pkgname-$pkgver.tar.bz2"
        0001-Patch-system-daemon-for-Arch-friendliness.patch)
sha256sums=('82d61860ff6d72fa080da48607b82af3e30ede8f792b73a477dfe468d898effb'
            'e83467d342abd979b38b3a971b97942787f5b5a0e18c569b034a636c54eb8154')

prepare() {
  mkdir build
  cd $pkgname-$pkgver

  # It's disabled by default. Should we even ship it?
  patch -Np2 -i ../0001-Patch-system-daemon-for-Arch-friendliness.patch
}

build() {
  cd build
  cmake -G Ninja ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DCMAKE_INSTALL_SYSCONFDIR=/etc \
    -DCMAKE_INSTALL_LIBEXECDIR=lib/$pkgname
  cmake --build .
}

package() {
  DESTDIR="$pkgdir" cmake --build build --target install
  echo vhba | install -Dm644 /dev/stdin "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
}

# vim:set ts=2 sw=2 et:
