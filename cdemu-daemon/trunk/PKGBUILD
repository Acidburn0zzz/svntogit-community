# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=1.3.0
pkgrel=3
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('i686' 'x86_64')
backup=('etc/conf.d/cdemud'
	'etc/dbus-1/system.d/cdemud-dbus.conf')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'dbus' 'dbus-glib' 'libdaemon' 'vhba-module' 'libmirage' 'libao')
install=cdemud.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.gz
        cdemud.conf
        cdemud.rc
        60-vhba.rules)
md5sums=('4ec07bd202ac1dcc2f352fc394268d67'
         '26b94a626c2c1d0a83ad5a7593dff70f'
         '655b2bf46802f2fa7821a649cecd1c4d'
         '93324b320e7b68d01ad9a0b548018ed5')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -m 644 -D ../cdemud.conf $pkgdir/etc/conf.d/cdemud
  install -m 755 -D ../cdemud.rc $pkgdir/etc/rc.d/cdemud

  # Install udev rule for non-root mounting of iso's 
  install -D -m 755 "${srcdir}/60-vhba.rules"  "$pkgdir/etc/udev/rules.d/60-vhba.rules"

}
