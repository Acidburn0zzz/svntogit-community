# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-daemon
pkgver=1.5.0
pkgrel=2
pkgdesc="CD/DVD-ROM device emulator daemon"
arch=('i686' 'x86_64')
backup=('etc/conf.d/cdemud'
	'etc/dbus-1/system.d/cdemud-dbus.conf')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('glib2' 'dbus' 'dbus-glib' 'libdaemon' 'vhba-module' "libmirage=$pkgver" 'libao')
install=cdemud.install
source=(http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2
        cdemud.conf cdemud.rc 60-vhba.rules cdemu-daemon-system.patch)
md5sums=('5ba780caa26d855942512b5b3c22405a'
         'd65c30651389900c46e62a9e5559b9d5'
         'ef47f06a3d4e07885bfbb57d4d6d0be4'
         'f551504552d7693ac79e8e0d302f62b9'
         'a3ae9c51c460d31a521dd00dd3f0a852')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np0 -i $srcdir/cdemu-daemon-system.patch
  ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib/$pkgname
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -m 644 -D ../cdemud.conf $pkgdir/etc/conf.d/cdemud
  install -m 755 -D ../cdemud.rc $pkgdir/etc/rc.d/cdemud

  # Install udev rule for non-root mounting of iso's 
  install -D -m 644 "${srcdir}/60-vhba.rules"  "$pkgdir/lib/udev/rules.d/60-vhba.rules"

  install -d "$pkgdir/usr/lib/modules-load.d"
  echo "vhba" > "$pkgdir/usr/lib/modules-load.d/cdemu.conf"
}
