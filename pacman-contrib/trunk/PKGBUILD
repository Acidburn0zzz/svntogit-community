# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=pacman-contrib
pkgver=4.0.3
pkgrel=1
pkgdesc='Utilities for use with the pacman package manager'
arch=('any')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig})
md5sums=('387965c7125e60e5f0b9ff3b427fe0f9'
         '1a70392526c8768470da678b31905a6e')

_scripts=(
  bacman
  paccache
  pacdiff
  paclist
  paclog-pkglist
  pacscripts
  pacsearch
  pacsysclean
)

build() {
  cd "$srcdir/pacman-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc

  make -C contrib
}

package() {
  cd "$srcdir/pacman-$pkgver/contrib"

  install -dm755 "$pkgdir/usr/bin" "$pkgdir/usr/share/vim/vimfiles/ftdetect"

  install -m755 -t "$pkgdir/usr/bin" "${_scripts[@]}"

  install -Dm644 PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    > "$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"
}
