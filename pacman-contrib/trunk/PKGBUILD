# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=pacman-contrib
pkgver=4.1.0
pkgrel=1
pkgdesc="Utilities for use with the pacman package manager"
arch=('any')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz{,.sig})
md5sums=('a0f2b3148bee4784f21cf373cf59a0bc'
         'SKIP')

_scripts=(
  bacman
  checkupdates
  paccache
  pacdiff
  paclist
  paclog-pkglist
  pacscripts
  pacsearch
  pacsysclean
  updpkgsums
)

build() {
  cd "$srcdir/pacman-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc

  make -C contrib
}

package() {
  cd "$srcdir/pacman-$pkgver/contrib"

  install -dm755 "$pkgdir/usr/bin" "$pkgdir/usr/share/vim/vimfiles/ftdetect"

  install -m755 -t "$pkgdir/usr/bin" "${_scripts[@]}"

  install -Dm644 PKGBUILD.vim "$pkgdir/usr/share/vim/vimfiles/syntax/PKGBUILD.vim"
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    > "$pkgdir/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim"
}
