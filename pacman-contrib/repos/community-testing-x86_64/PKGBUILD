# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=pacman-contrib
pkgver=1.3.0
pkgrel=1

pkgdesc="Contributed scripts and tools for pacman systems"
url="https://git.archlinux.org/pacman-contrib.git/about/"
arch=('x86_64')
license=('GPL')

depends=('fakeroot' 'pacman' 'perl')
makedepends=('asciidoc')
optdepends=('findutils: pacdiff'
            'mlocate: pacdiff'
            'sudo: privilege elevation for several scripts')

source=("https://git.archlinux.org/pacman-contrib.git/snapshot/pacman-contrib-$pkgver.tar".{gz,asc})
sha256sums=('2b8f614651dbe272c9b518c73e53fc194405c06fd8637159a8ac9012c84e9452'
            'SKIP')
validpgpkeys=('5134EF9EAF65F95B6BB1608E50FB9B273A9D0BB5') # Johannes Löthberg <johannes@kyriasis.com>

prepare() {
  cd pacman-contrib-$pkgver

  ./autogen.sh
}

build() {
  cd pacman-contrib-$pkgver

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var
  make
}

check() {
  make -C pacman-contrib-$pkgver check
}

package() {
  cd pacman-contrib-$pkgver

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
