# $Id$
# Maintainer: Allan McRae <allan@archlinux.org>

pkgname=pacman-contrib
pkgver=3.5.2
pkgrel=1
pkgdesc="Utilities for use with the pacman package manager"
arch=('any')
url="http://www.archlinux.org/pacman/"
license=('GPL')
depends=('pacman')
source=(ftp://ftp.archlinux.org/other/pacman/pacman-$pkgver.tar.gz)
md5sums=('b4f1fdbc17100923071ebe8fe9377be5')

build() {
  cd $srcdir/pacman-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
    --localstatedir=/var --enable-doc
  make -C contrib
}

package() {
  cd ${srcdir}/pacman-${pkgver}/contrib
  for script in bacman pacdiff paclist pacscripts pacsearch; do
    install -Dm755 ${script} ${pkgdir}/usr/bin/${script}
  done

  install -Dm644 PKGBUILD.vim ${pkgdir}/usr/share/vim/vimfiles/syntax/PKGBUILD.vim
  install -dm755 ${pkgdir}/usr/share/vim/vimfiles/ftdetect
  echo "au BufNewFile,BufRead PKGBUILD set filetype=PKGBUILD" \
    > ${pkgdir}/usr/share/vim/vimfiles/ftdetect/PKGBUILD.vim
}
