# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.1.1
pkgrel=3
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('ae721fbea3b6ed810411ba795480060a080f217112cd06dfb95ebb6e2c5c86acc073559e8ee7d63367832dd8aa05ae63e7a6f6868b21261a0aedd6ade8591697')

build() {
  cd mockito-python-$pkgver
  python setup.py build
}

check() {
  cd mockito-python-$pkgver
  # https://github.com/kaste/mockito-python/issues/29
  py.test build/lib || warning "Tests failed"
}

package() {
  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
