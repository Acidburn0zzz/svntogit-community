# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.2.1
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('7f9fb8d9b3c93fc7c39411188e74f7da8d9ae9e45ab431a93b79bb6147c3dc1a993b241dbbdc010b7783f95ac862b8281cbc86cc1cf11d087b5c7ceb7b2b715b')

build() {
  cd mockito-python-$pkgver
  python setup.py build
}

check() {
  cd mockito-python-$pkgver
  pytest --ignore=build
}

package() {
  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
