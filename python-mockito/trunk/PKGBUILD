# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>

pkgname=python-mockito
pkgver=1.2.0
pkgrel=1
pkgdesc='Spying framework'
arch=('any')
url='https://github.com/kaste/mockito-python'
license=('MIT')
depends=('python')
makedepends=('python-pytest' 'python-setuptools')
checkdepends=('python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kaste/mockito-python/archive/$pkgver.tar.gz")
sha512sums=('32e6391e9d837ecef24349f40a1418f45ddb90b44e1aa05e0f238aa9abb625ae33f4bde873ff36e318a639a21098152a4b3b4530d6ea5118aa6d4504257e5a7c')

build() {
  cd mockito-python-$pkgver
  python setup.py build
}

check() {
  cd mockito-python-$pkgver
  pytest --ignore=build
}

package() {
  cd mockito-python-$pkgver
  python setup.py install --root="$pkgdir" -O1
}
