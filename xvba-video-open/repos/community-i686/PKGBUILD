# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>

pkgname=xvba-video-open
pkgver=0.8.0
pkgrel=1
pkgdesc="OSS version of xvba-video lib to enable hw video acceleration on AMD's HD series of GFX cards"
arch=('i686' 'x86_64')
url="http://www.splitted-desktop.com/~gbeauchesne/xvba-video/"
license=('GPL2')
depends=('catalyst-utils' 'libva')
makedepends=('mesa')
options=(!libtool)
source=(ftp://ftp.archlinux.org/other/community/xvba-video-open/xvba-video-${pkgver}.tar.gz
        http://developer.amd.com/downloads/xvba-sdk-0.74-404001.tar.gz)
md5sums=('d9ddec2e7f02c1fa533773918e88e311'
         'b8f56bc55aa70cb19dd12857fdc184cc')

build() {
  cd "${srcdir}/xvba-video-${pkgver}"

  export CPPFLAGS="${CPPFLAGS} -I${srcdir}/include"
  
  ./configure --disable-debug --enable-libxvba-dlopen --prefix=/usr
  make PYTHON=python2
}

package() {
  cd "${srcdir}/xvba-video-${pkgver}"

  make DESTDIR="${pkgdir}" install
}
