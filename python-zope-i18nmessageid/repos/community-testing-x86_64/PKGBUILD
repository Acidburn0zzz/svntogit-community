# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-i18nmessageid
pkgver=4.3.1
pkgrel=3
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
depends=('python-setuptools')
checkdepends=('python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('0ad97114eb71c3aa25356f81c45d6f7d2278f379027e412b7671c3d605b65ea917b794cc533e238907c47c6bed7093711f370dc88aa51028fb9f4cc7c536ee63')

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"
}

package() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
