# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-i18nmessageid
pkgname=('python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
pkgver=4.3
pkgrel=1
pkgdesc="Message Identifiers for internationalization"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.i18nmessageid"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.i18nmessageid/archive/$pkgver.tar.gz")
sha512sums=('45acd6d35dcbf2a75f51da5a3b60057cd9846d8a559165a3b86054ca12ad3dd83da23762bb7eed2558baeac4045f97920d4a888039420f657ff53135e0b7250a')

prepare() {
  cp -a zope.i18nmessageid-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py build

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.i18nmessageid-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.7:$PYTHONPATH" python -m zope.testrunner --test-path=src || warning "Tests failed"

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-2.7:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-i18nmessageid() {
  depends=('python-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-i18nmessageid() {
  depends=('python2-setuptools')

  cd "$srcdir"/zope.i18nmessageid-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
