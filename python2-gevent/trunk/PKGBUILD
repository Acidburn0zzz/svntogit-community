# $Id: PKGBUILD 70305 2012-05-04 08:21:08Z mtorromeo $
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgname=python2-gevent
pkgver=0.13.7
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc='Python network library that uses greenlet and libevent for easy and scalable concurrency'
license=("MIT")
url="http://www.gevent.org/"
depends=('libevent' 'python2-greenlet')
makedepends=('python2-distribute')

source=(http://pypi.python.org/packages/source/g/gevent/gevent-$pkgver.tar.gz)

build() {
	cd "$srcdir/gevent-$pkgver"
	python2 setup.py build
}

package() {
	cd "$srcdir/gevent-$pkgver"
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm0644 LICENSE.pyevent "$pkgdir/usr/share/licenses/$pkgname/LICENSE.pyevent"
}

md5sums=('c74004730c2543260b751b0aa782987c')
sha1sums=('2b9c63b84e21a36e244a7c825b9bdbaa98b7ec66')
