# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.2.7
pkgrel=1
pkgdesc="Japanese Kana Kanji conversion library"
arch=('i686' 'x86_64')
url="https://bitbucket.org/libkkc"
license=('GPL')
options=(!libtool)
depends=('marisa' 'json-glib' 'libgee06')
makedepends=('vala' 'gobject-introspection' 'intltool' 'python2-marisa')
optdepends=('sh: to use kkc-package-data')
source=(https://bitbucket.org/libkkc/libkkc/downloads/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}

sha512sums=('2bb056cf00b6212a18462a5f952d4557944b29f49a92b0701abed5f5d6601fdb3b57c4e09a0b3db8ff39be01e91ab658656b89f576663046e53478ed07fe87af')
