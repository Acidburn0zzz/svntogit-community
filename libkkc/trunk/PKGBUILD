# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>

pkgname=libkkc
pkgver=0.2.6
pkgrel=1
pkgdesc="Japanese Kana Kanji conversion library"
arch=('i686' 'x86_64')
url="https://bitbucket.org/libkkc"
license=('GPL')
options=(!libtool)
depends=('marisa' 'json-glib' 'libgee06')
makedepends=('vala' 'gobject-introspection' 'intltool' 'python2-marisa')
source=(https://bitbucket.org/libkkc/libkkc/downloads/${pkgname}-${pkgver}.tar.gz)

build() {
  cd "${pkgname}-${pkgver}"

  PYTHON=/usr/bin/python2 \
  ./configure --prefix=/usr --disable-static --disable-silent-rules
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -d "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}"
  install -m 644 README data/rules/README.rules COPYING \
    "${pkgdir}/usr/share/doc/${pkgname}-${pkgver}/"  
}

md5sums=('b50f0341596a4e290188a7bf5195cd26')
