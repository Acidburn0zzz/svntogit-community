# $Id: PKGBUILD,v 1.8 2009/03/29 11:41:06 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgname=xmltv
pkgver=0.5.55
pkgrel=1
pkgdesc="Set of utilities to download tv listings and format them in xml"
arch=('i686' 'x86_64')
url="http://xmltv.org/wiki/"
license=('GPL')
depends=('perl-archive-zip' 'perl-date-manip' 'perl-file-slurp'
         'perl-http-cache-transparent' 'perl-http-response-encoding' 
         'perl-html-tree' 'perl-io-stringy' 'perl-lingua-en-numbers-ordinate' 
         'perl-lingua-preferred' 'perl-soap-lite' 'perl-term-progressbar'
         'perl-term-readkey' 'perl-timedate' 'perl-tk-tablematrix' 
         'perl-unicode-string' 'perl-unicode-utf8simple' 'perl-www-mechanize'
         'perl-xml-dom' 'perl-xml-libxml' 'perl-xml-libxslt' 'perl-xml-simple'
         'perl-xml-twig' 'perl-xml-writer')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('21812833b083a8e830ee1837e4060199')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  yes | perl Makefile.PL PREFIX=/usr INSTALLDIRS=vendor 

  make || return 1
  make DESTDIR=${pkgdir} PREFIX=/usr install
	 
  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}