# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-pkcs11
pkgver=1.2.0
pkgrel=1
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('openssl' 'python-cryptography' 'python-pyasn1-modules' 'python-yaml' 'sqlite' 'tpm2-tools')
checkdepends=('cmocka' 'ibm-sw-tpm2' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc' 'tpm2-abrmd' 'wget' 'xxd')
provides=('libtpm2_pkcs11.so')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('e7de454b38388c4562fc92282855c1ed9a04c7968c358d6918137257fb5a1ca785c2297e8a34464ea04eed86acd9fc5b59c837027da9d3f195ec86ff0563feeb'
            'SKIP')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
	make
	cd tools
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
