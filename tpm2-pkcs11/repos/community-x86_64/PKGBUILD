# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-pkcs11
pkgver=1.2.0
pkgrel=2
pkgdesc='PKCS#11 interface for Trusted Platform Module 2.0 hardware'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-pkcs11'
license=('BSD')
depends=('libyaml' 'openssl' 'python-cryptography' 'python-pyasn1-modules' 'python-yaml' 'sqlite'
         'tpm2-tools' 'tpm2-tss' 'libtss2-esys.so' 'libtss2-mu.so' 'libtss2-rc.so' 'libtss2-tctildr.so')
checkdepends=('cmocka' 'ibm-sw-tpm2' 'iproute2' 'java-hamcrest' 'junit' 'libp11' 'opensc' 'tpm2-abrmd' 'wget' 'xxd')
source=("$url/releases/download/$pkgver/$pkgname-$pkgver.tar.gz"{,.asc}
        "tpm2-pkcs11-1.2.0_gcc-10-fix.patch::$url/commit/198e6035a503f1ba747ad2997cac94a27a9633ad.patch"
        "tpm2-pkcs11-1.2.0_ibm-sw-tpm2-1628.patch::$url/commit/e4bb96cf4e6df883de8b9ab88e4768aa3d6621f0.patch")
sha512sums=('e7de454b38388c4562fc92282855c1ed9a04c7968c358d6918137257fb5a1ca785c2297e8a34464ea04eed86acd9fc5b59c837027da9d3f195ec86ff0563feeb'
            'SKIP'
            '52044f03027a5c3d64db2b43144d6e66aab09b3ef674fc3b65d3858154ac894eddd98473136fc3dbcba2df26cf9028c77379de0e172c7115a68200db1ae990aa'
            '6aa7cef3e74f41fbd1f853d969c64c066df00a3065e8b2113bd92544be9a209a1feaec1078d1df8cc4cf625266d6cb88dc453887e7558d22558dbcfd936bc722')
validpgpkeys=('5B482B8E3E19DA7C978E1D016DE2E9078E1F50C1') # William Roberts (Bill Roberts) <william.c.roberts@intel.com>

prepare() {
	cd "$pkgname-$pkgver"
	# Fix test suite for GCC 10 (https://github.com/tpm2-software/tpm2-pkcs11/pull/514)
	patch --strip=1 --input="$srcdir/tpm2-pkcs11-1.2.0_gcc-10-fix.patch"
	# Fix test suite for ibm-sw-tpm2 1628 (https://github.com/tpm2-software/tpm2-pkcs11/pull/519)
	patch --strip=1 --input="$srcdir/tpm2-pkcs11-1.2.0_ibm-sw-tpm2-1628.patch"
}

build() {
	cd "$pkgname-$pkgver"
	export CLASSPATH="/usr/share/java/junit.jar:/usr/share/java/hamcrest-core.jar:$CLASSPATH"
	./configure --prefix=/usr $( ((CHECKFUNC)) && echo --enable-unit --enable-integration)
	make
	cd tools
	python setup.py build
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
	cd tools
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
