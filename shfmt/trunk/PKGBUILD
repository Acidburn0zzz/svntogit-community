# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.0.1
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('751a79c0e0da298613c135be1142a0ea34973467bb7db340ec756f4bd97daf09af459d97f43911804e19b2643b9ee78c607544bca7b8561d75acb374d2f533cf')

build() {
  cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" .
}

package() {
  cd "${srcdir}/sh-${pkgver}/"
  install -Dm755 "cmd/shfmt/shfmt" "${pkgdir}/usr/bin/shfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
