# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.2.4
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
depends=('glibc')
makedepends=('git' 'go' 'scdoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('d88b4d734b84c160094f64238ef7dd3ff86ad0a49c53492b180930cc0f8b652d99b6eb6e0652b50b3a97f364b653febf66129ad815435460ff94f13d404dd3a8')

build() {
    cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"

    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
    go build -ldflags "-linkmode=external -extldflags=$LDFLAGS"

    scdoc < "${pkgname}.1.scd" | gzip > "${pkgname}.1.gz"
}

check() {
    cd "${srcdir}/sh-${pkgver}/"
    go test ./...
}

package() {
    cd "${srcdir}/sh-${pkgver}/"
    install -Dm755 -t "${pkgdir}/usr/bin/" "cmd/${pkgname}/${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/share/man/man1/" "cmd/${pkgname}/${pkgname}.1.gz"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
