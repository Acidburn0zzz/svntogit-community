# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Daniel Mart√≠ <mvdan@mvdan.cc>

pkgname=shfmt
pkgver=3.0.0
pkgrel=1
pkgdesc="Format shell programs"
url="https://github.com/mvdan/sh"
license=('BSD')
arch=('x86_64')
makedepends=('git' 'go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/mvdan/sh/archive/v${pkgver}.tar.gz")
sha512sums=('aa423656346de9df5ea2bf9d6e1ab89e23b36c70902346157d95435799c3f970bdf8d079927286289fed387c0bc553dd5496e4a5e46292e16ac38d753a678ea5')

build() {
  cd "${srcdir}/sh-${pkgver}/cmd/shfmt/"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" .
}

package() {
  cd "${srcdir}/sh-${pkgver}/"
  install -Dm755 "cmd/shfmt/shfmt" "${pkgdir}/usr/bin/shfmt"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
