# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=signon
pkgname=('signon' 'signon-doc')
pkgver=8.44
pkgrel=1
pkgdesc="Framework to provide credential storage and authentication service"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('qt' 'libproxy')
makedepends=('doxygen' 'graphviz' 'ttf-dejavu')
backup=(etc/signond.conf)
install=$pkgname.install
source=(http://accounts-sso.googlecode.com/files/$pkgbase-$pkgver.tar.bz2
        use_keyring.patch)
sha1sums=('c0fa356d36295c97879e3dce97d32a97428b007b'
          'cfdd48b494ad267654ceba23f1b81bcb8f56b9a1')

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  # Use gnome-keyring secrets storage if signon-keyring-extension is available
  patch -Np1 -i "$srcdir/use_keyring.patch"

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_signon() {
  cd "$srcdir/$pkgbase-$pkgver"

  make INSTALL_ROOT="$pkgdir" install

  # Split out documentation
  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_signon-doc() {
  pkgdesc="Developer documention of signon"
  depends=()
  options=('!emptydirs')
  backup=()
  install=

  mkdir -p "$pkgdir/usr/share"
  mv "$srcdir/doc" "$pkgdir/usr/share"
}
