# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=pdns
pkgver=3.1
pkgrel=1
pkgdesc="Modern, advanced and high performance authoritative-only nameserver"
arch=('x86_64' 'i686')
url='http://www.powerdns.com/'
license=('GPL')
depends=('gcc-libs' 'zlib' 'postgresql-libs' 'libmysqlclient' 'sqlite' 'libldap' 'lua')
makedepends=('boost')
provides=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
replaces=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
conflicts=('pdns-ldap' 'pdns-mysql' 'pdns-sqlite' 'pdns-pgsql')
install=pdns.install
options=('!makeflags' '!libtool')
backup=('etc/powerdns/pdns.conf')
source=("http://downloads.powerdns.com/releases/$pkgname-$pkgver.tar.gz"
        'pdns.rc'
        'pdns.conf')
sha256sums=('1400f7bd659207c0b1f4b8296092e559a7b7bf6a2434951970217d9af06922a1'
            '0457ffa2baa40518a03393e4f76964d9452c16aebbcb925582814f88367d62c2'
            'e3de30584436a063cdff4b4ca77044da06a0737fc333847b3da89370cf763c0c')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  touch AUTHORS NEWS
  libtoolize --force --copy
  aclocal
  autoconf
  automake --add-missing
  sed -i 's/BOOST_REQUIRE/#BOOST_REQUIRE/' configure
  sed -i 's/BOOST_FOREACH/#BOOST_FOREACH/' configure
  sed -i 's/BOOST_PROGRAM_OPTIONS/#BOOST_PROGRAM_OPTIONS/' configure
  sed -i 's/BOOST_SERI/#BOOST_SERI/' configure
  ./configure --prefix=/usr \
    --sysconfdir=/etc/powerdns \
    --with-boost \
    --libexecdir=/usr/lib \
    --libdir=/usr/lib/powerdns \
    --mandir=/usr/share/man \
    --with-modules="" \
    --with-dynmodules="ldap pipe gmysql gpgsql gsqlite3 geo" \
    --disable-recursor \
    --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/etc/rc.d"
  rm "$pkgdir/etc/powerdns/pdns.conf-dist"
  install -m644 "$srcdir/pdns.conf" "$pkgdir/etc/powerdns/"
  install -m755 "$srcdir/pdns.rc" "$pkgdir/etc/rc.d/pdns"
  install -Dm644 "$srcdir/pdns.service" \
    "$pkgdir/usr/lib/systemd/system/pdns.service"
}

# vim:set ts=2 sw=2 et:
