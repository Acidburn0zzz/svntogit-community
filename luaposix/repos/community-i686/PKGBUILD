# $Id$
# Maintainer: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=luaposix
pkgname=('lua-posix' 'lua51-posix')
pkgver=29
pkgrel=1
pkgdesc='A POSIX library for Lua programming language'
arch=('i686' 'x86_64')
url='https://github.com/luaposix/luaposix'
license=('GPL' 'LGPL')
makedepends=('lua' 'lua51' 'lua51-bitop' 'git')
options=('!libtool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/luaposix/luaposix/archive/v$pkgver.tar.gz")
md5sums=('5c3e49c51caeee6eb5f9dcb2bb082bed')

prepare() {
  # do bootstrap in prepare, as it clone external git
  (cd $pkgbase-$pkgver && ./bootstrap)
  cp -r $pkgbase-$pkgver $pkgbase-$pkgver-51
}

build() {
  msg2 'Build with lua 5.2'
  cd $pkgbase-$pkgver
  ./configure \
      LUA=/usr/bin/lua \
      --prefix=/usr \
      --libdir=/usr/lib/lua/5.2 \
      --datadir=/usr/share/lua/5.2 \
      --docdir=/usr/share/doc/lua-posix
  make
  msg2 'Build with lua 5.1'
  cd "$srcdir/$pkgbase-$pkgver-51"
  ./configure \
      LUA=/usr/bin/lua5.1 \
      LUA_INCLUDE=-I/usr/include/lua5.1 \
      --prefix=/usr \
      --libdir=/usr/lib/lua/5.1 \
      --datadir=/usr/share/lua/5.1 \
      --docdir=/usr/share/doc/lua51-posix
  make
}

package_lua-posix() {
  depends=('lua')

  cd $pkgbase-$pkgver
  make DESTDIR="$pkgdir/" install
}

package_lua51-posix() {
  depends=('lua51' 'lua51-bitop')
  conflicts=('luaposix')
  replaces=('luaposix')

  cd $pkgbase-$pkgver-51
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
