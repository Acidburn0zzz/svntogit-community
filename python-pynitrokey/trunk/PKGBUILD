# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pynitrokey
pkgname=python-pynitrokey
pkgver=0.4.2
pkgrel=1
pkgdesc="A command line interface for the Nitrokey FIDO2 and Nitrokey Start"
arch=('any')
url="https://github.com/Nitrokey/pynitrokey/releases"
license=('Apache')
depends=('python-cbor' 'python-cffi' 'python-click' 'python-cryptography'
'python-ecdsa' 'python-fido2' 'python-intelhex' 'python-pygments' 'python-pyserial'
'python-pyusb' 'python-requests')
makedepends=('python-dephell' 'python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4abd5e660aa63aef2a74449bef6de3388bc299b6645581bb8534046e89659fcbada59c30ffca4b9f922f37eb3bf35624ad78496abe5783be914d349ae34a657d')
b2sums=('497875071c583c3dfbc7689b08d461f48f07e888fc8925074111736b92b05b2a3243fbd3af1b2601a0e7ced1de5dcb7251acad7bc5cae50a778f12e0ecad5bc7')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't deal with such strict version constraints
  sed -e 's/ == 0.8.1//' -i pyproject.toml
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

package() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 "${_name}/VERSION" "${pkgdir}/${_site_packages}/${_name}/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
