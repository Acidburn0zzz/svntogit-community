# Maintainer: Felix Yan <felixonmars@archlinux.org
# Contributor: Pablo Arias <pabloariasal@gmail.com>
# Contributor: John Jenkins <twodopeshaggy@gmail.com>

pkgname=nnn
pkgver=2.6
pkgrel=1
pkgdesc="The fastest terminal file manager ever written."
arch=('x86_64')
depends=('bash')
url="https://github.com/jarun/nnn"
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jarun/nnn/archive/v${pkgver//_/-}.tar.gz")
sha256sums=('17fd3e517308e41065594ffe8dcde348b4d10dea4240699f4708337db48b3e25')

prepare() {
  sed -i 's/install: all/install:/' $pkgname-$pkgver/Makefile
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm644 misc/auto-completion/fish/nnn.fish "$pkgdir"/usr/share/fish/vendor_completions.d/nnn.fish
  install -Dm644 misc/auto-completion/bash/nnn-completion.bash "$pkgdir"/usr/share/bash-completion/completions/nnn
  install -Dm644 misc/auto-completion/zsh/_nnn "$pkgdir"/usr/share/zsh/site-functions/_nnn

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
