# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
pkgname=ldc
pkgver=20120209
pkgrel=1
pkgdesc="A D Compiler based on the LLVM Compiler Infrastructure including D runtime and libphobos2"
arch=('i686' 'x86_64')
url="http://www.dsource.org/projects/ldc"
license=('BSD')
depends=('llvm' 'libconfig')
makedepends=('git' 'cmake')
source=()
md5sums=()

_gitroot=git://github.com/ldc-developers/ldc.git
_gitname=ldc

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone --recursive "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"
	
  mkdir build && cd build
  cmake \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINCLUDE_INSTALL_DIR=/usr/include/d/std-ldc \
      ..
  make
}

package() {
  cd "$srcdir/$_gitname-build"
  
  cd build
  make DESTDIR=$pkgdir install
  cd ..

  install -Dm644 LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
}
