# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=0.4.11
pkgrel=2
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/greenlet"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/g/greenlet/greenlet-${pkgver}.zip")
sha512sums=('d3efee31b2854c2d4891332e6666a736526635de1ba4d78c57bcbb0fcbeff98b7cb9cc16c6c4f24325f47b8d4c8f137bb064becf6b10f2764969f7510d569328')

prepare() {
    cp -a greenlet-$pkgver{,-py2}
}

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    python setup.py test

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py test
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
