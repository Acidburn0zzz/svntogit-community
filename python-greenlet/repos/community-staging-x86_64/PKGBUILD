# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Ralf Schmitt <ralf@systemexit.de>

pkgbase=python-greenlet
pkgname=(python-greenlet python2-greenlet)
pkgver=0.4.13
pkgrel=3
pkgdesc="Lightweight in-process concurrent programming"
license=("MIT")
arch=('x86_64')
url="http://pypi.python.org/pypi/greenlet"
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://files.pythonhosted.org/packages/source/g/greenlet/greenlet-${pkgver}.tar.gz"
        port-to-py3.7-handle-tstate-exc_info.patch)
sha512sums=('625aaa8875b408442139191e1a63eca71c668675229907335f5caee623487aba5305c1d53b51e1764ad982b8edd07d04f984e08a75318a0a5672805667917a61'
            '6fac24423f680a3676f722a02a93d611a0f0194ec7c6c6a013d158c46afe6887a6d2a0ca27256fce2ddc18044e2115bbc209bfbcbc2ed7020ca89c6639e1d55d')

prepare() {
    # https://github.com/python-greenlet/greenlet/issues/131
    patch -d greenlet-$pkgver -Np1 < port-to-py3.7-handle-tstate-exc_info.patch
    cp -a greenlet-$pkgver{,-py2}
}

build() {
	cd "$srcdir"/greenlet-$pkgver
	python setup.py build

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py build
}

check() {
    cd "$srcdir"/greenlet-$pkgver
    python setup.py test

    cd "$srcdir"/greenlet-$pkgver-py2
    python2 setup.py test
}

package_python-greenlet() {
    depends=('python')

    cd greenlet-$pkgver
    python setup.py install -O1 --root="$pkgdir"
    install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}

package_python2-greenlet() {
    depends=('python2')

	cd greenlet-$pkgver-py2
	python2 setup.py install -O1 --root="$pkgdir"
	install -Dm0644 LICENSE.PSF "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.PSF
}
