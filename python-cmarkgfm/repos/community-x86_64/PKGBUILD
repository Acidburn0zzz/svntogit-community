# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cmarkgfm
pkgver=0.5.1
pkgrel=1
pkgdesc="Minimal bindings to GitHub's fork of cmark"
url="https://github.com/theacodes/cmarkgfm"
license=('MIT')
arch=('x86_64')
depends=('cmark-gfm' 'python-cffi')
makedepends=('cmark-gfm' 'python-setuptools' 'python-cffi')
checkdepends=('python-pytest-runner')
source=("https://github.com/theacodes/cmarkgfm/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('15d17f6e73aaf564b4d1ca5bad9a5993594f50cf2076b638c90cc39f45f6b73499b4c8eeacd7bd7ae8876f8e9d894b2391b0cb91f7fd789909444a069ab93f89')

prepare() {
  cd cmarkgfm-$pkgver
  sed -e '/include_dirs=/a libraries=["cmark-gfm", "cmark-gfm-extensions"],' \
      -i src/cmarkgfm/build_cmark.py
}

build() {
  cd cmarkgfm-$pkgver
  python setup.py build
}

check() {
  cd cmarkgfm-$pkgver
  python setup.py pytest
}

package() {
  cd cmarkgfm-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
