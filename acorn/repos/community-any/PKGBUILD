# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=acorn
pkgver=8.6.0
epoch=1
pkgrel=1
pkgdesc='A tiny, fast JavaScript parser, written completely in JavaScript.'
arch=('any')
url='https://github.com/ternjs/acorn'
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('535ae2211fa5052362dc86f1b5a1ce20a747f2c2c5bf73587cdbfcb20ed9b0d85c7e5e07176fbe05faaaacd031a022d05b58a2ca53a3efa79c9da531cfecfdcb')

package() {
  npm install -g --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"
}
