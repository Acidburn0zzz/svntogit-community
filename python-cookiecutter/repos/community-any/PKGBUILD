# Maintainer: David Runge <dvzrv@archlinux.org>

_name=cookiecutter
pkgname=python-cookiecutter
pkgver=1.7.2
pkgrel=5
pkgdesc="A command-line utility that creates projects from project templates"
arch=('any')
url="https://github.com/cookiecutter/cookiecutter"
license=('BSD')
depends=('python-binaryornot' 'python-click' 'python-future' 'python-jinja'
'python-jinja-time' 'python-poyo' 'python-requests' 'python-slugify'
'python-whichcraft')
makedepends=('python-setuptools')
checkdepends=('git' 'python-freezegun' 'python-pytest' 'python-pytest-cov'
'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"
        allow-jinja-3.0.0.patch
        drop-unneeded-markupsafe-constraints.patch)
sha512sums=('8fb6db75703a2c6276d275817b81c0e5d5f15226236e8c5442fbda6198aaec19c3fd68c030f79f6bca55258c0c248660b32d1344f7d6b06ecf5563b43474d877'
            'd83168b30cdec8d3cff6db05ea1e1cd27046d6e296be056571f5ddbcf7dc4574280444ca30dad224a1aee6dfa13e2fa81331f209f8940f71470e99808149c45e'
            'ec7ff08abed877afd163703dbc362c0cb47bb97539ac464daeaef9b63445a03340a1f35b541977ab4b39f832c13c3846e81890a24b19a624266917abb92a21ee')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  patch -Np1 -d "$pkgname-$pkgver" <allow-jinja-3.0.0.patch
  patch -Np1 -d "$pkgname-$pkgver" <drop-unneeded-markupsafe-constraints.patch
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
