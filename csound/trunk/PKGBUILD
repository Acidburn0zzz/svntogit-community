# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ainola
# Contributor: speps
# Contributor: Bernardo Barros

pkgname=csound
pkgver=6.10.0
pkgrel=2
pkgdesc="A programming language for sound rendering and signal processing."
arch=('x86_64')
url="http://csound.com"
license=('LGPL')
depends=('curl' 'fltk' 'fluidsynth' 'hdf5' 'liblo' 'luajit' 'portaudio' 'portmidi' 'python2' 'stk' 'tk')
# add faust (after fix), gmm (after fix), wiiuse and
makedepends=('boost' 'cmake' 'dssi' 'eigen' 'java-environment' 'ladspa' 'luajit' 'pd' 'swig')
optdepends=('csound-doc: The canonical Csound Reference Manual'
            'csoundqt: Qt frontend'
            'java-environment: Java Wrapper'
            'neovim-csound: Syntax highlighting and bindings for neovim'
            'vim-csound: Syntax highlighting and bindings for vim')
source=("https://github.com/csound/csound/archive/${pkgver}.tar.gz"
        "csound.sh")
sha512sums=('1ccf0a2d5273da016ec585e788e4af0b91f55c3be081a51c53dc39b0fbcfba0cf1308828784a1e5dfc39250dedbd3f4b48afbb5c4140c11fd469ca6753702ed4'
            '26e92e7168961686b3d89e9d552f4b086922469f7fc3e8b4b967d32bb7e41fddf014fd52a0aef30f229db57f7eac2980efe7c54b3fc3a7c321ae7e20adbb3995')

prepare() {
  cd "${pkgname}-${pkgver}"
  # requires out-of-tree build
  [ -d bld ] || mkdir bld
}

build() {
  cd "${pkgname}-${pkgver}/bld"
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr \
           -DJAVA_MODULE_INSTALL_DIR=/usr/lib/csound/java \
           -DLUA_MODULE_INSTALL_DIR=/usr/lib/lua/5.1/ \
           -DPYTHON_INCLUDE_DIR=/usr/include/python2.7 \
           -DPYTHON_LIBRARY=/usr/lib/libpython2.7.so \
           -DPYTHON_MODULE_INSTALL_DIR=/usr/lib/python2.7/site-packages
  make
}

package() {
  cd "${pkgname}-${pkgver}/bld"
  make DESTDIR="${pkgdir}/" install

  # export vars in profile.d
  install -Dm755 "${srcdir}/${pkgname}.sh" \
    "${pkgdir}/etc/profile.d/${pkgname}.sh"
}

# vim:set ts=2 sw=2 et:
