# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-google
pkgver=0.31.0
pkgrel=1
pkgdesc="Google Cloud DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-mock' 'python-google-api-python-client'
         'python-oauth2client' 'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('29ec9657c47390e92b3a5885691892ed3799a03987e26837a9ef0271692745bb24838723a4dbba603048e4397790bd160e59dca9cbaf497ebeb04d4e5ec8bbeb'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
