# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-google
pkgver=0.37.0
pkgrel=1
pkgdesc="Google Cloud DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=('certbot' 'python-acme' 'python-mock' 'python-google-api-python-client'
         'python-oauth2client' 'python-setuptools' 'python-zope-interface')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('7aa8fa4c7756fdc16c8c1619452f3ba2f95d5eb8e04df7afc1b9d08862b2d49f6e61c05244caf908262ec6d4f7f8d4972680b175270c2eef7558962efc2fed31'
            'SKIP')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py test
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  python setup.py install --root="$pkgdir"
}
