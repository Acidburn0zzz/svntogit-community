# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=certbot-dns-google
pkgver=1.25.0
pkgrel=1
pkgdesc="Google Cloud DNS Authenticator plugin for Certbot"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
depends=(
  'certbot'
  'python-acme'
  'python-google-api-python-client'
  'python-oauth2client'
  'python-httplib2'
)
makedepends=(
  'python-build'
  'python-wheel'
  'python-installer'
)
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('86379B4F0AF371B50CD9E5FF3402831161D1D280'
              '148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('e173d6d69ca7ca47d2f14c0d8f16436b20c732ed872bc89e3140f6f94a569e18fd659d44234ab08e8426fa90dc5e59e97ca7cf782804416dc641f65683ee1143'
            'SKIP')
b2sums=('071e7ba95740be8756268a8d1f09ababc9ab33a1bdacf05345adcd0f75ddfaff22d6941ad2cb9c14a98f82f0060394662ca2a1d30057de1b288d933d22ee4c1c'
        'SKIP')

prepare() {
  cd "$pkgname-$pkgver"

  # nuke setuptools from orbit ^W install_requires
  sed \
    -e '/setuptools>=/d' \
    -i setup.py
}

build() {
  cd "$pkgname-$pkgver"

  python -m build --wheel --no-isolation
}

check() {
  cd "$pkgname-$pkgver"

  python -m pytest
}

package() {
  cd "$pkgname-$pkgver"

  python -m installer --destdir="$pkgdir" dist/*.whl
}
