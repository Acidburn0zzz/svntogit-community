# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gwibber
pkgver=3.6.0
pkgrel=1
pkgdesc="Microblogging client for GNOME, which supports Facebook, Identi.ca, Twitter, Flickr, Foursquare, Sina and Sohu"
arch=('i686' 'x86_64')
url="http://gwibber.com/"
license=('GPL')
depends=('libgee' 'account-plugins' 'libnotify' 'libsoup' 'dee' 'json-glib' 'gtkspell3' 'python2-dbus' 'python2-gobject' 'python2-gobject2' 'python2-httplib2' 'python2-oauth' 'python2-imaging' 'python2-xdg' 'dconf')
makedepends=('intltool' 'vala')
options=('!libtool')
install=$pkgname.install
source=(http://launchpad.net/$pkgname/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz)
md5sums=('5988e36e9f592c6eca6537ba6878307a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Use the standalone accounts configuration panel
  sed -i 's/"gnome-control-center", "credentials", "application=gwibber"/"credentials-preferences"/' client/gwibber-client.vala
  sed -i 's/"gnome-control-center", "credentials"/"credentials-preferences"/' gwibber/microblog/dispatcher.py

  # Remove unneeded dependency on libdbusmenu
  sed -i '/Dbusmenu-0.4/ d' client/Makefile.{am,in}

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-compile --disable-unity
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
