# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
pkgname=libnsbmp
pkgver=0.0.3
pkgrel=3
pkgdesc="Decoding library for BMP and ICO image file formats"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/projects/libnsbmp/"
license=('MIT')
depends=('glibc')
source=("http://www.netsurf-browser.org/projects/releases/$pkgname-$pkgver-src.tar.gz")
md5sums=('52eef940713a4c8a7afbb6888583057c')

build() {
  cd "$srcdir/$pkgname-$pkgver-src"

  make PREFIX=/usr COMPONENT_TYPE="lib-shared" 
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver-src"

  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"   
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
