# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.6.16
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz")
sha512sums=('5fe0c45efac72df388163e73c9a699ec16220d395892bf6485a6e97bbd7d648f9b7618468a8b98a925903c0a2baf5b18b9d8235c9626444314ab2158b28caab6')

prepare() {
  cd deepin-compressor-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' deepin-compressor/source/src/openwithdialog/openwithdialog.cpp deepin-compressor/source/src/logviewheaderview.cpp
}

build() {
  cd deepin-compressor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-compressor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
