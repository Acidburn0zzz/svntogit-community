# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-compressor
pkgver=5.8.0.8
pkgrel=1
pkgdesc='A fast and lightweight application for creating and extracting archives'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-compressor"
license=('GPL3')
depends=('deepin-qt5integration' 'disomaster' 'karchive' 'kcodecs' 'libarchive' 'libzip' 'minizip' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-compressor/archive/$pkgver.tar.gz")
sha512sums=('c7d852dc079b9840f93de10e4194c1d1d5228c795546183a9a4ff25afe09214b30c811a4954c2659d0ebb716a36cdced01de4d1551674065aa4b16ddfb75bbf0')

prepare() {
  cd deepin-compressor-$pkgver
  sed -i '/include <QPainter>/a #include <QPainterPath>' deepin-compressor/source/src/openwithdialog/openwithdialog.cpp deepin-compressor/source/src/logviewheaderview.cpp
}

build() {
  cd deepin-compressor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-compressor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
