# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=python-meilisearch
pkgver=0.14.2
pkgrel=1
pkgdesc='Python client for MeiliSearch API'
arch=(any)
license=('MIT')
url='https://pypi.org/project/meilisearch/'
depends=(python)
makedepends=(python-requests python-setuptools)
checkdepends=('python-pytest' 'python-pytest-ordering')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/meilisearch-python/archive/v${pkgver}.tar.gz)
sha512sums=('69713c97983f064abf72cfc281042a74dfec257d7f8afde24db4984dc1e91575938dc793ec08b2de31e41e146d5707a5f38d1e4a15695cfb4fe785f31e6241c0')

build() {
  cd "meilisearch-python-${pkgver}"
  python setup.py build
}

check() {
  # requires running meilisearch https://github.com/meilisearch/meilisearch-python/blob/master/.github/workflows/test.yml#L20
  cd "meilisearch-python-${pkgver}"
  #pytest .
}

package() {
  cd "meilisearch-python-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
