# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Mladen Pejakovic <pejakm@gmail.com>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=lightdm-kde-greeter
pkgver=0.3.2.1
pkgrel=1
pkgdesc="KDE greeter for LightDM"
arch=('i686' 'x86_64')
url="https://projects.kde.org/projects/playground/base/lightdm"
license=('GPL')
depends=('kdelibs' 'liblightdm-qt4')
makedepends=('cmake' 'automoc4')
backup=('etc/lightdm/lightdm-kde-greeter.conf'
        'etc/dbus-1/system.d/org.kde.kcontrol.kcmlightdm.conf')
source=("ftp://ftp.kde.org/pub/kde/unstable/${pkgname%-*}/src/${pkgname%-*}-${pkgver}.tar.bz2"
        'lightdm-kde-greeter.conf')
sha256sums=('65b2a1bab2c88bf55a07e62cbe0eb8ac3660638636c5825e5925b28d03f12188'
            'a8fe10ef68d553781b91b1377106f95915c3ca14d835bd66910e08f922ef64c8')

build() {
  cd "${srcdir}"/lightdm-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/lightdm-${pkgver}/build

  make DESTDIR="${pkgdir}" install
  install -dm 755 "${pkgdir}"/etc/lightdm
  install -m 644 ../../lightdm-kde-greeter.conf "${pkgdir}"/etc/lightdm/lightdm-kde-greeter.conf
}

# vim ts=2 sw=2 et:
