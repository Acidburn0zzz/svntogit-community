# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-window-switch
pkgver=2.0.1
pkgrel=2
pkgdesc="Front of the window switcher in UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-window-switch"
groups=('ukui')
depends=('libwnck3' 'qt5-x11extras' 'ukwm')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-window-switch/archive/v$pkgver.tar.gz")
sha512sums=('da34d2d16a3b10eee9109b7f2534087d59d214f6e4ee9bf94bde779641f396d7a6507ab055326325c62a981ca86b689bb9dc037bd6eed3bdb2d0260acadd1e90')

build() {
  cd ukui-window-switch-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-window-switch-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
