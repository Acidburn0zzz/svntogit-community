# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-window-switch
pkgver=2.0.3
pkgrel=2
pkgdesc="Front of the window switcher in UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-window-switch"
groups=('ukui')
depends=('libwnck3' 'qt5-x11extras' 'ukwm')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-window-switch/archive/v$pkgver.tar.gz"
         ukui-window-switch-qt5.15.patch)
sha512sums=('8fc15f32d5929031a3542115c14702087a29b7425fa808581bf292629e6d23418de99d850e520d39233ada188ac677105cb5b3e1df59e2a7ee38789da2c40568'
            '00f573025911b0acf77262893aa429b597b1602b737cd00908b14d505eec8c1ff3f04956213c859f67b9b733dd59f33f24692446ecd1f56ba72c086539a90122')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../ukui-window-switch-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd ukui-window-switch-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-window-switch-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
