# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-window-switch
pkgver=1.1.1
pkgrel=1
pkgdesc="Front of the window switcher in UKUI desktop environment"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-window-switch"
groups=('ukui')
depends=('qt5-base' 'ukwm')
makedepends=('python' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-window-switch/archive/v$pkgver.tar.gz"
        ukui-window-switch-gdbus.patch::https://github.com/ukui/ukui-window-switch/commit/1b2dcfa8137b207f06fb879f4f0f0694b049c06b.patch)
sha512sums=('eee429ddbcab1a6d93eda1d1f84b89740b3bf3053bf29ded7488b7b1236eb015c9690e06ccb9da1ad89917daf3a74b3743b6c6753c658a124bbd2706a2b71868'
            '0ced8cd9c8ab0b98759f992719f1e46e88dfe3b6a2bd7a85ce8ce76f2b61ceefb31fa271425f8724708acd57942d1924057499dbc053b8d1e597b2f4af2a57bd')

prepare() {
  cd ukui-window-switch-$pkgver
  patch -p1 -i ../ukui-window-switch-gdbus.patch
}

build() {
  cd ukui-window-switch-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-window-switch-$pkgver
  install -Dm755 ukui-window-switch -t "$pkgdir"/usr/bin/
  install -Dm644 data/ukui-window-switch.desktop -t "$pkgdir"/etc/xdg/autostart/
}
