# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: tikysal <tikysal@gmail.com>
# Derived from Daniel Ehlers'

pkgname=coin-or-coinutils
pkgver=2.11.0
pkgrel=1
pkgdesc="COIN-OR collection of utility classes"
arch=(x86_64)
url="https://projects.coin-or.org/CoinUtils"
license=(EPL)
depends=(zlib bzip2 glpk lapack)
makedepends=(gcc-fortran doxygen)
groups=(coin-or)
source=("https://www.coin-or.org/download/source/CoinUtils/CoinUtils-$pkgver.tgz")
sha256sums=('621505d5e71ebb01f4f0568479ab1bd90e552caf890b68c479f26e89f4be5d9a')

build() {
  cd CoinUtils-$pkgver

  ./configure --prefix=/usr --with-blas-lib='-lblas' --with-lapack-lib='-llapack' --with-glpk-lib='-lglpk' --enable-dependency-linking
  make
}

check() {
  cd CoinUtils-$pkgver
  make test
}

package() {
  cd CoinUtils-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir" install
}
