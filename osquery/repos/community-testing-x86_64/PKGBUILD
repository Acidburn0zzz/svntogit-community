# Maintainer: Anatol Pomozov

pkgname=osquery
pkgver=4.2.0
pkgrel=1
pkgdesc='SQL powered operating system instrumentation, monitoring, and analytics'
arch=(x86_64)
url='https://osquery.io'
license=(Apache GPL2)
depends=(augeas boost-libs gflags google-glog aws-sdk-cpp librdkafka rocksdb gtest device-mapper rpm-tools dpkg lvm2 libelfin lldpd rapidjson sleuthkit ssdeep thrift yara lmdb iptables)
makedepends=(cmake ninja python boost linenoise-ng)
# patched version of smartools that we need to build statically to avoid collision with the upstream version
_smartmontools_commit=5f7669fba545f32a6d0c07e2be67df16f3eb354b
source=(osquery-$pkgver.zip::https://github.com/osquery/osquery/archive/$pkgver.zip
        third-party-smartmontools.zip::https://github.com/osquery/third-party-smartmontools/archive/$_smartmontools_commit.zip
        devendorize.patch::https://github.com/anatol/osquery/compare/4743cb94207023f76e2b4c3cd702f550d496d585...b5cc91eb9e9c8a1193d96a85f1eb88eba90058ae.patch)
sha256sums=('a2a613619577afcf743153ccdc8d6bcbb54926ec6b6bc8296366ea89582740cb'
            '891f11acd804aa0d53dbec599e6a32ad9c1e5392a3eb4022714871f3a1ce95ee'
            'a4bdc8de4edf847fd49b721cc978a264d9bd63f329bdb6b76c38bc3397afa19d')

prepare() {
  cd osquery-$pkgver
  patch -p1 < ../devendorize.patch

  rm -rf libraries/cmake/source/smartmontools/src
  ln -sf `pwd`/../third-party-smartmontools-$_smartmontools_commit libraries/cmake/source/smartmontools/src
}

build() {
  cd osquery-$pkgver

  cmake -DCMAKE_BUILD_TYPE=Release -DOSQUERY_STATIC_BUILD=OFF -DCMAKE_INSTALL_PREFIX=/usr -DOSQUERY_VERSION=$pkgver -S . -B build
  cmake --build build
}

package() {
  cd osquery-$pkgver

  cmake --build build --target install -- DESTDIR="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  rm -r "$pkgdir/etc/init.d/"
}
