# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=store
pkgname=haskell-store
pkgver=0.4.3.1
pkgrel=36
pkgdesc="Fast binary serialization"
url="https://github.com/fpco/store"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-async' 'haskell-base-orphans' 'haskell-base64-bytestring'
         'haskell-conduit' 'haskell-contravariant' 'haskell-cryptohash' 'haskell-free'
         'haskell-hashable' 'haskell-hspec' 'haskell-hspec-smallcheck' 'haskell-lifted-base'
         'haskell-monad-control' 'haskell-mono-traversable' 'haskell-network' 'haskell-primitive'
         'haskell-resourcet' 'haskell-safe' 'haskell-semigroups' 'haskell-smallcheck'
         'haskell-streaming-commons' 'haskell-store-core' 'haskell-syb' 'haskell-text'
         'haskell-th-lift' 'haskell-th-lift-instances' 'haskell-th-orphans' 'haskell-th-reify-many'
         'haskell-th-utilities' 'haskell-unordered-containers' 'haskell-vector' 'haskell-void')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        ghc-8.2.1.patch)
sha512sums=('fde163ef8ccc6a71c4db07681235be7dcebc5f37e2a222b194f1c34fdeef0fb9814383af56ded876a59bcb53bf2126b04a0272db1b05f28d093851cbc56a9f35'
            '6b1b27bd398c2f2571ff7ee34cefb71cc00b22fab4ad1357ddba9b8a02f09f13be6d7f6ce3bf4f48202ffb5e0a81361fa6b8e1d5fdc612d22e4efed90951c4a4')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    patch -p1 -i ../ghc-8.2.1.patch
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-comparison-bench -f-small-bench
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
