# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>

pkgname=sigil
_pkgname=Sigil
pkgver=0.3.3
pkgrel=1
pkgdesc="A WYSIWYG ebook editor"
arch=('i686' 'x86_64') 
url="http://code.google.com/p/sigil/"
license=('GPL3')
depends=('qt')
makedepends=('cmake')
install=sigil.install
source=(http://${pkgname}.googlecode.com/files/${_pkgname}-${pkgver}-Code.zip
        ${pkgname}.desktop)
md5sums=('a8573e877f3bcf250ee29bd934939e35'
         'fef6aa492af487ccccd6b133635cee5a')

build() {
  cd ${_pkgname}-${pkgver}-Code

  mkdir build
  cd build
  cmake -G "Unix Makefiles" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS:STRING="${CFLAGS}" \
    -DCMAKE_CXX_FLAGS:STRING="${CXXFLAGS}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=ON .. || return 1

    make || return 1
}

package() {
  cd ${_pkgname}-${pkgver}-Code/build

  make install DESTDIR="${pkgdir}" || return 1

  install -d -m 0755 "${pkgdir}"/usr/share/applications || return 1
  install -m 0644 "${srcdir}"/${pkgname}.desktop "${pkgdir}"/usr/share/applications || return 1

  for _pic in 16 32 48 128 256; do
    install -D -m 0644 ../src/Sigil/Resource_Files/icon/app_icon_${_pic}.png \
      "${pkgdir}"/usr/share/icons/hicolor/${_pic}x${_pic}/apps/${pkgname}.png || return 1
  done
}
