# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=fillets-ng
pkgver=1.0.1
pkgrel=2
pkgdesc='Port of the wonderful puzzle game Fish Fillets'
url="http://fillets.sf.net/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('fillets-ng-data' 'lua' 'fribidi' 'gcc-libs' 'libx11' 'sdl_mixer' 'sdl_ttf' 'sdl_image' 'desktop-file-utils')
makedepends=('gendesk')
install=$pkgname.install
changelog=$pkgname.changelog
source=("http://downloads.sourceforge.net/fillets/$pkgname-$pkgver.tar.gz"
        $pkgname.png::http://img.1mobile.com/market/i/4/9/49ec5e5520844b66c189c7e0ef6eedfc.png)
sha256sums=('329a4d9515d60bebdb657d070824933b993b85864b9d3e302e6361accab992da'
            '4d96eded48e536d02e35727c36dc20844c2e44654e81baf78e10aee4eb48e837')

build() {
  cd "$srcdir"
  gendesk
  
  cd "$pkgname-$pkgver"

  ./configure --prefix=/usr --datadir=/usr/share/$pkgname
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

  # desktop and icon files
  install -D -m644 ${srcdir}/$pkgname.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop
}
