# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jonathan Wiersma <archaur at jonw dot org>

pkgbase=virt-manager
pkgname=(virt-install virt-manager)
pkgver=1.4.2
pkgrel=1
pkgdesc="Console user interface for managing virtual machines"
arch=('any')
url="http://virt-manager.org/"
license=('GPL')
depends=('python2' 'libvirt-python2' 'libxml2' 'libvirt'
	 'libosinfo' 'python2-ipaddr' 'python2-gobject' 'python2-requests'
	 'cpio')
makedepends=('intltool>=0.35.0'
	     'dbus-python' 'gtk-vnc' 'rarian'
	     'yajl' 'librsvg' 'libuser' 'python2-cairo'
	     'python2-ipy' 'newt-syrup' 'openbsd-netcat' 'x11-ssh-askpass'
	     'graphite' 'spice-gtk3'
	     'libvirt-glib' 'vte3')
source=("https://virt-manager.org/download/sources/virt-manager/virt-manager-$pkgver.tar.gz")
sha256sums=('43e440bb099facf59b59c27c2fc4eb2c42ef0d4ed8d67d93c9e3d98538b6d574')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py build
}

package_virt-install() {
  cd "$srcdir/$pkgbase-$pkgver"
  python2 setup.py --no-update-icon-cache --no-compile-schemas install --root "$pkgdir"
  python2 -m compileall "${pkgdir}/usr/share/virt-manager"
  python2 -O -m compileall "${pkgdir}/usr/share/virt-manager"
  rm "${pkgdir}/usr/bin/virt-manager"
}

package_virt-manager() {
  pkgdesc="Desktop user interface for managing virtual machines"
  depends=("virt-install=$pkgver"
	    'python2' 'libvirt-python2' 'libxml2' 'libvirt' 'urlgrabber'
	    'dbus-python' 'gtk-vnc' 'rarian'
	    'yajl' 'librsvg' 'libuser' 'python2-cairo'
	    'python2-ipy' 'newt-syrup' 'openbsd-netcat' 'x11-ssh-askpass'
	    'graphite' 'spice-gtk3'
	    'libvirt-glib' 'vte3')
  cd "$srcdir/$pkgbase-$pkgver"
  install -Dm 755 build/virt-manager -t "${pkgdir}/usr/bin"
}
