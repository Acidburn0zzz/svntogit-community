# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>

pkgname=clojure
pkgver=1.2.1
pkgrel=1
pkgdesc='A LISP dialect for the JVM that safely supports concurrency.'
url='http://www.clojure.org'
arch=('any')
license=('CPL')
depends=('java-environment')
makedepends=('apache-ant' 'libxrender') #libxrender maybe needed due to openjdk missing dep. jre doesn't have this problem
optdepends=('clojure-contrib: user contributions and extras'
            'java-forkjoin: java concurrency library wrapped by clojure.parallel'
            'rlwrap: for a friendlier shell with readline support')
install=clojure.install
source=("http://github.com/downloads/clojure/clojure/clojure-${pkgver}.zip"
        'clj'
        'clojure.sh')
md5sums=('c5724c624fd6ce6a1d00252c27d53ebe'
         '396de3edaf31e7a0d58f6dd8aa10ecab'
         '7e02402b3f875ec60c77da9dbe324786')

build() {
  cd clojure-${pkgver}

  ant
}

package() {
  # launcher script
  install -D clj \
    ${pkgdir}/usr/bin/clj

  # profile
  install -D clojure.sh \
    ${pkgdir}/etc/profile.d/clojure.sh

  # clojure
  install -D -m644 clojure-${pkgver}/clojure.jar \
    ${pkgdir}/usr/share/clojure/clojure.jar

}
