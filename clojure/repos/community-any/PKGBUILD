# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Chris O'Donnell <christopher.p.odonnell@gmail.com>
# Contributor: Sam May <sam.m4y@gmail.com>
# Contributor: Pierre-Yves Ritschard <pyr@spootnik.org>
# Contributor: Vladimir Tsanev <tsachev@gmail.com>

pkgname=clojure
pkgver=1.10.3.833
pkgrel=1
pkgdesc='Lisp dialect for the JVM'
url='https://clojure.org/'
arch=(any)
license=(EPL)
depends=(java-environment)
optdepends=('leiningen: automate clojure projects'
            'rlwrap: friendly shell with readline support')
makedepends=('setconf')
install=clojure.install
source=("https://download.clojure.org/install/clojure-tools-$pkgver.tar.gz"
        clojure.sh)
b2sums=('629480b2560d0c10d15bd5ee7af1f0415cb3e6d0d9f232a949bea3477259903a36d0ae7794499775c0aab77b452a08e1747be15b80b55bb34468dbb6ce509a48'
        'c3d7f294fbc239e42e83f3833199b44c446a3ca3be00a9f8b27f57ee3427b5f39d6adc274e15355f0688296cabb59204cea5a3fd732f91313d30d02a6dd55664')

prepare() {
  sed -i -e 's,PREFIX,/usr/share/clojure,g' clojure-tools/clojure
  setconf clojure-tools/clj install_dir=/usr
}

package() {
  cd clojure-tools
  install -Dm644 deps.edn "$pkgdir/usr/share/clojure/deps.edn"
  install -Dm644 example-deps.edn "$pkgdir/usr/share/clojure/example-deps.edn"
  install -Dm644 clojure-tools-$pkgver.jar \
    "$pkgdir/usr/share/clojure/libexec/clojure-tools-$pkgver.jar"
  install -Dm755 clojure "$pkgdir/usr/bin/clojure"
  install -Dm755 clj "$pkgdir/usr/bin/clj"
  install -Dm644 clojure.1 "$pkgdir/usr/share/man/man1/clojure.1"
  install -Dm644 clj.1 "$pkgdir/usr/share/man/man1/clj.1"
  install -Dm755 "$srcdir/clojure.sh" "$pkgdir/etc/profile.d/clojure.sh"
  install -Dm755 exec.jar "$pkgdir/usr/share/clojure/libexec/exec.jar"
}

# getver: raw.githubusercontent.com/clojure/brew-install/1.10.3/stable.properties
