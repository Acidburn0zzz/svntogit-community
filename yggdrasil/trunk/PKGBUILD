# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=yggdrasil
pkgver=0.3.12
pkgrel=2
pkgdesc="An experiment in scalable routing as an encrypted IPv6 overlay network"
arch=('x86_64')
url="https://github.com/yggdrasil-network/yggdrasil-go"
license=('LGPL3')
depends=('glibc')
makedepends=('go-pie')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/yggdrasil-network/yggdrasil-go/archive/v${pkgver}.tar.gz"
        'yggdrasil.sysusers')
sha512sums=('7277f08c49bf23f30f579f3c983076a2e53731efe0ca7df987448728a2272e36fca6b14825389d2512b6914e36f2316c2dd769aa1008de9cd108740ec93df294'
            'b78d1f5efeeba184588ba7bdb2249d976aec160daa59742e032983da1aedad062d15c7c97cba3eba69412a0f7904ee123d98b58f859892d71188c25624295c32')
b2sums=('6c801dfb9cae33d6637c1aaa8d2e48a997dcb4315ad0b854dae2dac7528d8c8a7fc2b64567efea05385a2717bbacf8a738cd7e07f81b1bda1fde661fcd084f9c'
        '3cf5bfb3c09908f841b39f7206bb472e50e60f10d1b87ab6def97a7212e27627f1e795bbaffd964d2d27a80fbbd1a3be63d63c59793ec1967c02f48b8462c08b')

build() {
  cd "${pkgname}-go-${pkgver}"
  PKGNAME="${pkgname}" PKGVER="${pkgver}" \
    CGO_LDFLAGS="${LDFLAGS}" \
    GOFLAGS="-trimpath" \
    ./build
}

package() {
  cd "${pkgname}-go-${pkgver}"
  install -Dm755 "yggdrasil" "${pkgdir}/usr/bin/yggdrasil"
  install -Dm755 "yggdrasilctl" "${pkgdir}/usr/bin/yggdrasilctl"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm644 contrib/systemd/yggdrasil.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm644 "${srcdir}/yggdrasil.sysusers" "${pkgdir}/usr/lib/sysusers.d/yggdrasil.conf"
}

# vim: ts=2 sw=2 et:
