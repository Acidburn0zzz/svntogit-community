# Maintainer: David Runge <dvzrv@archlinux.org>

_name=orjson
pkgname=python-orjson
pkgver=3.6.6
pkgrel=1
pkgdesc="Fast, correct Python JSON library supporting dataclasses and datetimes"
arch=(x86_64)
url="https://github.com/ijl/orjson"
license=(Apache MIT)
depends=(python)
makedepends=(maturin python-pip rust)
checkdepends=(python-arrow python-pendulum python-psutil python-pytest
python-pytz python-xxhash)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ijl/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('71fadcef5c4251fc9e56996a25001c71eeb8f82d446def3c4cb208043a51c1e61440fa3f772e1852fe4ef0d1ed2a5f588bc325011ec763ca1638f78527aa0472')
b2sums=('75e4d545d0b8bb930e731218fd205267269f9a9b1d79673965d1cc94b4f4da3c42b0bd6f9a348be7e2b56d780ba58c1012cff7f1a6723ab4b45c436d08c3fa85')

build() {
  cd "${_name}-${pkgver}"
  maturin build --no-sdist --release --strip
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  cd "${_name}-${pkgver}"
  mkdir -vp testdir

  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="testdir" --ignore-installed --no-deps target/wheels/*.whl
  PYTHONPATH="testdir/usr/lib/python${python_version}/site-packages:${PYTHONPATH}" pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  PIP_CONFIG_FILE=/dev/null pip install --isolated --root="$pkgdir" --ignore-installed --no-deps target/wheels/*.whl
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE-* -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
