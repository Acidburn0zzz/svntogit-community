# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.20.0
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('gtk-vnc' 'libarchive' 'libosinfo' 'libsoup' 'libvirt-glib'
         'spice-gtk3' 'libtracker-sparql' 'qemu' 'cdrkit' 'mtools' 'dconf'
         'libusb' 'libgudev')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
install=$pkgname.install
source=(https://download.gnome.org/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
sha256sums=('fd9a72c39e2ab0cae39385830270dc88b5254667a4b95ac7ccf322a55b4677fb')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile --enable-smartcard
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
