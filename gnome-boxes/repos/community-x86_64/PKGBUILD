# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=gnome-boxes
pkgver=3.22.4
pkgrel=1
pkgdesc="Simple GNOME application to access remote or virtual systems"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Apps/Boxes"
license=('LGPL')
depends=('cdrkit' 'gtk-vnc' 'libarchive' 'libgudev' 'libosinfo' 'libsecret' 'libtracker-sparql'
         'libvirt-glib' 'mtools' 'qemu' 'spice-gtk3')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'spice-protocol' 'vala')
source=(https://download.gnome.org/sources/$pkgname/${pkgver::4}/$pkgname-$pkgver.tar.xz)
sha256sums=('585a1c292010ebb479cb60e02621d01d450f84cdad82a61b7ce58a0218e1cff2')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
