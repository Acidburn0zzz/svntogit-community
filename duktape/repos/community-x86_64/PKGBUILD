# $Id: PKGBUILD 162811 2016-02-20 15:24:54Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.0.2
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=('x86_64' 'i686')
url='http://duktape.org/'
makedepends=('setconf')
depends=('glibc')
license=('MIT')
source=("http://duktape.org/duktape-$pkgver.tar.xz" # no https available
        'duktape.pc')
sha256sums=('863204c27a18adc86795616b64b3bc3975a32b38af0f68f46d7a4adedf38089e'
            'bb69bc836a639eaca2fd774a364ff2302d265f74ef511ee06a63f73c0c42763d')

prepare() {
  cd "$pkgname-$pkgver"

  mv Makefile.sharedlibrary Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version "$pkgver"
}

build() {
  make -C "$pkgname-$pkgver"
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 "$srcdir/duktape.pc" "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim:set ts=2 sw=2 et:
