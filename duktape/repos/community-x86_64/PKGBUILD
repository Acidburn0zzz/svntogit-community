# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>

pkgname=duktape
pkgver=2.5.0
pkgrel=1
pkgdesc='Embeddable Javascript engine'
arch=(x86_64)
url='https://duktape.org/'
license=(MIT)
makedepends=(setconf)
source=("https://duktape.org/duktape-$pkgver.tar.xz"
        duktape.pc)
sha256sums=('83d411560a1cd36ea132bd81d8d9885efe9285c6bc6685c4b71e69a0c4329616'
            'ffc4b62d6574dba798164d2e492b32592cb64fd841bff60ba6a4a13a39818be1')

prepare() {
  cd $pkgname-$pkgver

  mv Makefile.sharedlibrary Makefile
  sed 's/-Wall -Wextra/$(CFLAGS)/g' -i Makefile
  setconf Makefile INSTALL_PREFIX="$pkgdir/usr"
  setconf "$srcdir/duktape.pc" Version="$pkgver"
}

build() {
  CFLAGS="$CFLAGS -D DUK_USE_FASTINT -w" make -C $pkgname-$pkgver
}

package() {
  cd $pkgname-$pkgver

  install -d "$pkgdir/usr/lib" "$pkgdir/usr/include"
  make install
  install -Dm644 "$srcdir/duktape.pc" "$pkgdir/usr/lib/pkgconfig/duktape.pc"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: github.com/svaarala/duktape
# vim: ts=2 sw=2 et:
