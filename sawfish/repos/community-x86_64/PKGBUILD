# $Id: PKGBUILD 98412 2010-11-08 20:05:27Z andrea $
# Maintainer:
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=sawfish
pkgver=1.7.1
pkgrel=1
pkgdesc="An extensible window manager using a Lisp-based scripting language"
arch=('i686' 'x86_64')
url="http://sawfish.wikia.com/wiki/Main_Page"
license=('GPL')
depends=('rep-gtk' 'libsm' 'hicolor-icon-theme')
install=${pkgname}.install
source=(http://download.tuxfamily.org/${pkgname}/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
sha1sums=('d4bd4cad90ece608cef51427994ca5667ee6f939')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -Dm644 sawfish-session.desktop \
    "${pkgdir}/etc/X11/sessions/${pkgname}.desktop"
}
