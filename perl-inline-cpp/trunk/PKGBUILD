# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-inline-cpp
pkgver=0.79
pkgrel=1
pkgdesc="Write Perl subroutines and classes in C++"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Inline-CPP"
depends=('perl-inline' 'perl-inline-c' 'perl-parse-recdescent')
makedepends=('perl-extutils-cppguess')
checkdepends=('perl-inline-filters')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DA/DAVIDO/Inline-CPP-$pkgver.tar.gz")
sha512sums=('7f287cbfbb067b663ee1dad3e31f052f105a9a6de9f641cd2986c7c941e91ca436d92a9636edf9cce15522575d8333b0b50c8a11ea5b4bce5e014e2ec0b8be90')

build() {
  cd Inline-CPP-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Inline-CPP-$pkgver
  make test
}

package() {
  cd Inline-CPP-$pkgver
  make DESTDIR="$pkgdir" install
}
