# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>

pkgname=duplicity
pkgver=0.8.09
pkgrel=3
pkgdesc='A utility for encrypted, bandwidth-efficient backups using the rsync algorithm.'
arch=('x86_64')
url='https://www.nongnu.org/duplicity/'
license=('GPL')
depends=('gnupg' 'librsync' 'python-paramiko' 'python-fasteners')
makedepends=('python-setuptools')
optdepends=('lftp: backend for ftp, ftps, fish'
            'ncftp: alternative backend for ftp'
            'python-boto: Amazon S3 and Google Cloud Storage backend'
            'python-dropbox: Dropbox backend'
            'python-gobject: GIO backend'
            'gvfs: GIO backend'
            'python-pydrive: Google Drive backend'
            'python-requests-oauthlib: OneDrive backend'
            'rsync: rsync backend')
source=("https://launchpad.net/$pkgname/0.8-series/${pkgver}/+download/$pkgname-$pkgver.tar.gz"{,.asc})
md5sums=('8039afce2b0eba01718e3641bed6561f'
         'SKIP')
validpgpkeys=('9D95920CED4A8D5F8B086A9F8B6F8FF4E654E600')

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  python setup.py install --root="$pkgdir" --optimize=1

  # fix broken documentation permissions until upstream does (FS#27301)
  chmod 644 "$pkgdir/usr/share/man/man1/$pkgname.1"
  chmod 644 "$pkgdir/usr/share/man/man1/rdiffdir.1"
  chmod 644 "$pkgdir/usr/share/doc/$pkgname-$pkgver"/*
}
