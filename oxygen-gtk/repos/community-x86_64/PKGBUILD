# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: birdflesh <antkoul at gmail dot com>

pkgname=oxygen-gtk
pkgver=1.0.1
pkgrel=1
pkgdesc="Port of the default KDE widget theme (Oxygen) to GTK"
arch=('i686' 'x86_64')
url="http://kde-look.org/content/show.php/?content=136216"
license=('LGPL')
depends=('gtk2')
makedepends=('cmake')
source=(ftp://ftp.kde.org/pub/kde/stable/${pkgname}/${pkgver}/src/${pkgname}-${pkgver}.tar.bz2)
md5sums=('39b3b951e6adfb5aebeebb23c073d42c')

build() {
  cd ${srcdir}
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/build
  make DESTDIR=${pkgdir} install
}
