# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=backuppc
pkgver=3.2.1
pkgrel=7
pkgdesc='Enterprise-grade system for backing up Linux, Windows and MacOS PCs'
url='http://backuppc.sourceforge.net/'
license=('GPL2')
arch=('any')
depends=(
  'openssh'
  'par2cmdline'
  'perl-archive-zip'
  'perl-file-listing'
  'perl-time-modules'
  'perl-xml-rss'
  'smbclient'
  'start-stop-daemon'
)
optdepends=(
  'rsync: used by rsync transfert method'
  'perl-file-rsyncp: used by rsync transfert method'
  'perl-io-dirent'
  'apache: used by web admin interface'
  'mod_perl: used by web admin interface'
)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/BackupPC-$pkgver.tar.gz"
        "$pkgname.conf"
        "$pkgname.rc"
        "$pkgname.service"
        "$pkgname.profile.sh"
        "$pkgname.profile.csh"
        '01-fix-parenthesis-warnings.patch'
        '02-fix-cve-2011-4923.patch')
install=$pkgname.install
backup=("etc/conf.d/$pkgname"
        "etc/$pkgname/config.pl"
        "etc/$pkgname/hosts"
        "etc/httpd/conf/extra/$pkgname.conf"
        "etc/$pkgname/$pkgname.users")
md5sums=('2334fafb8e03284225a9b8a7fb230012'
         '0fb4c20f7f4872b6d3994cd1e56080c0'
         'e35545a597bd3e7ecc204b05a65863f9'
         'fa5da622f87d8801065ba312f4d547b6'
         '67a939aa63740c52d12bbdca72d37891'
         'ef09e4dae5b4197998f5c3a74e0ec86d'
         'dff7ae1530929ae49f8b34a240a04131'
         'aca8392c5dea60c3cceeb02ebcc63497')

package() {
  cd BackupPC-$pkgver
  # fix warnings
  patch -p1 -N -i "$srcdir/01-fix-parenthesis-warnings.patch"
  # fix XSS (CVE-2011-4923)
  patch -p1 -N -i "$srcdir/02-fix-cve-2011-4923.patch"
  perl configure.pl \
    --batch \
    --no-set-perms \
    --uid-ignore \
    --hostname __HOSTNAME__ \
    --compress-level 5 \
    --backuppc-user=backuppc \
    --dest-dir "$pkgdir" \
    --fhs \
    --install-dir /usr/share/$pkgname \
    --config-dir /etc/$pkgname \
    --cgi-dir /usr/share/$pkgname/cgi-bin \
    --html-dir /usr/share/$pkgname/html \
    --html-dir-url /backuppc \
    --data-dir /var/lib/$pkgname \
    --log-dir /var/log/$pkgname
  # install apache http
  sed -i "s/BackupPC.users/$pkgname.users/" httpd/BackupPC.conf
  install -D -m 644 httpd/BackupPC.conf "$pkgdir/etc/httpd/conf/extra/backuppc.conf"
  install -D -m 640 /dev/null "$pkgdir/etc/$pkgname/$pkgname.users"
  # install rc script
  cd "$srcdir"
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd unit
  install -D -m 644 $pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # install shell profiles
  install -D -m 755 $pkgname.profile.sh "$pkgdir/etc/profile.d/$pkgname.sh"
  install -D -m 755 $pkgname.profile.csh "$pkgdir/etc/profile.d/$pkgname.csh"
  # set correct permissions
  cd "$pkgdir"
  chown 91 -R etc/$pkgname
  chown 91 -R var/{log,lib}/$pkgname
  chmod 755 etc
  chmod 640 etc/$pkgname/{config.pl,hosts}
  chmod 755 -R usr/share/$pkgname/{cgi-,}bin var
}

# vim:set ts=2 sw=2 ft=sh et:
