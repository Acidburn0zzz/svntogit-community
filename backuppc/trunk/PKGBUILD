# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=backuppc
pkgver=3.2.1
pkgrel=1
pkgdesc='Enterprise-grade system for backing up Linux, Windows and MacOS PCs'
url='http://backuppc.sourceforge.net/'
license=('GPL2')
arch=('any')
depends=(
  'net-tools'
  'perl-archive-zip'
  'perl-time-modules'
  'perl-xml-rss'
  'smbclient'
  'start-stop-daemon'
)
optdepends=(
  'rsync: used by rsync transfert method'
  'perl-file-rsyncp: used by rsync transfert method'
  'openssh: used by ssh transfert method'
  'par2cmdline: used by archive'
  'perl-io-dirent'
)
source=("http://downloads.sourceforge.net/project/$pkgname/$pkgname/$pkgver/BackupPC-$pkgver.tar.gz"
        "$pkgname.conf"
        "$pkgname.rc"
        "$pkgname.service")
install=$pkgname.install
backup=("etc/$pkgname/config.pl"
        "etc/$pkgname/hosts")
md5sums=('2334fafb8e03284225a9b8a7fb230012'
         '0fb4c20f7f4872b6d3994cd1e56080c0'
         'ab1ebc8ae2cd19737f6e711846bd4eb3'
         'b4488bbb50a83dca6610e379b5c54e29')

package() {
  cd BackupPC-$pkgver
  #install -d -m 755 ${pkgdir}/var/opt
  perl configure.pl \
    --batch \
    --no-set-perms \
    --uid-ignore \
    --hostname __HOSTNAME__ \
    --compress-level 5 \
    --backuppc-user=backuppc \
    --dest-dir "$pkgdir" \
    --fhs \
    --install-dir /usr \
    --config-dir /etc/$pkgname \
    --cgi-dir /usr/lib/$pkgname/cgi-bin \
    --html-dir /srv/http/$pkgname \
    --html-dir-url /backuppc \
    --data-dir /var/lib/$pkgname \
    --log-dir /var/log/$pkgname

  # set correct permissions
  chown 91 -R "$pkgdir"/etc/$pkgname
  chown 91 -R "$pkgdir"/var/{log,lib}/$pkgname
  chmod 640 "$pkgdir"/etc/$pkgname/{config.pl,hosts}
  chmod 755 "$pkgdir/etc"
  chmod 755 -R "$pkgdir/usr/bin/" "$pkgdir/var/" "$pkgdir/usr/lib/$pkgname"

  # install rc script
  install -D -m 755 ../$pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 755 ../$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"

  # install systemd unit
  install -D -m 644 ../$pkgname.service "${pkgdir}/lib/systemd/system/$pkgname.service"
}

# vim:set ts=2 sw=2 ft=sh et:
