# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>

pkgname=python-queuelib
pkgver=1.6.0
pkgrel=1
pkgdesc="Collection of persistent (disk-based) queues."
arch=('any')
license=('BSD')
url="https://scrapy.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/queuelib/archive/v$pkgver.tar.gz")
sha512sums=('a7ec95ec0dc5fd19b356076294e5d52c41896ea393cac6734a67023cec13f51a42c07685bb1a48442b1036acd5c348a45b3960e206140ba9718063bea9bf77b3')

build() {
  cd queuelib-$pkgver
  python setup.py build
}

check() {
  cd queuelib-$pkgver
  py.test
}

package() {
  cd queuelib-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -D -m644 README.rst -t "$pkgdir"/usr/share/doc/$pkgname
}
