# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ring-daemon
pkgver=20190103
pkgrel=1
epoch=2
_pjprojectver=2.8
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (daemon component)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=("ring")
depends=('opendht' 'yaml-cpp' 'alsa-lib' 'libpulse' 'jack' 'jsoncpp'
         'dbus-c++' 'ffmpeg' 'gnutls'
         'expat' 'libupnp' 'libnatpmp' 'libva' 'libvdpau' 'restbed'
         'libsecp256k1' 'util-linux' 'opus')
makedepends=('git' 'boost' 'msgpack-c' 'autoconf-archive')
#checkdepends=('cppunit')
_commit=981c9df98e4610efb2f144ead121d5ec98126876
#source=("git+https://git.ring.cx/savoirfairelinux/ring-daemon#commit=${_commit}" # Certificate issues
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-daemon#commit=${_commit}"
        "https://github.com/pjsip/pjproject/archive/${_pjprojectver}/pjproject-${_pjprojectver}.tar.gz")
noextract=("pjproject-${_pjprojectver}.tar.gz")
sha256sums=('SKIP'
            '1fcdf6e295e3b8d99c9e738d3e980d8e7da34c76bd5e773c8ed9ad26b119d0eb')

prepare() {
  cd ${pkgname}
  cp ../pjproject-${_pjprojectver}.tar.gz contrib/tarballs
  mkdir contrib/native
  autoreconf -fvi
}

build() {
  cd ${pkgname}/contrib/native
  ../bootstrap \
      --disable-downloads \
      --disable-all \
      --enable-pjproject
  make DEPS_pjproject=

  cd ../..
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc \
    --enable-ipv6
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

# Disabled because some tests (TURN) use the network.
#check() {
#  cd "${pkgname}"
#  make -k check
#}

package() {
  cd ${pkgname}
  make DESTDIR="${pkgdir}" install
}
