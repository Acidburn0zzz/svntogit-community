# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.10
pkgbase=mate-panel
pkgname=(${pkgbase})
pkgver=${_ver}.0
pkgrel=1
pkgdesc="The MATE Panel (GTK2 version)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.10' 'dbus-glib' 'dconf' 'gtk2' 'libwnck' 'libcanberra' 'libice'
         'libmateweather>=1.9.1' 'librsvg' 'libsm' 'libsoup' 'libxau' 'marco>=1.10'
         'mate-menus>=1.10' 'mate-desktop>=1.10' 'mate-session-manager>=1.10')
makedepends=('gobject-introspection' 'mate-common' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
groups=('mate')
conflicts=("${pkgbase}-gtk3")
sha1sums=('632d7127fba8c5fa76f5dd951db89325c6ca3f89')
install=${pkgbase}.install

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection \
        --disable-static
    make
}

package_mate-panel() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
