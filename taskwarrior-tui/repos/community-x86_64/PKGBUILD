# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=taskwarrior-tui
pkgver=0.14.4
pkgrel=1
pkgdesc="A terminal user interface for taskwarrior"
arch=('x86_64')
url="https://github.com/kdheepak/taskwarrior-tui"
license=('MIT')
depends=('task')
makedepends=('rust' 'git' 'pandoc')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "git+https://github.com/kdheepak/taskwarrior-testdata")
sha256sums=('7e8c561b6d45c7b11545fbdb47bdc2c048cfda088e9de77a37191839acdd6d83'
            'SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --all-features
  pandoc "docs/$pkgname.1.md" --standalone --to=man -o "docs/$pkgname.1"
}

# https://github.com/kdheepak/taskwarrior-tui/issues/277
#check() {
#  cd "$pkgname-$pkgver"
#  export TASKRC="../taskwarrior-testdata/.taskrc"
#  export TASKDATA="../taskwarrior-testdata/.task"
#  cargo test --frozen --all-features
#}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "docs/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
  install -Dm 644 "completions/$pkgname.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
