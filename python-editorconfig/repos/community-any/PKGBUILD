# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-editorconfig
pkgname=('python-editorconfig' 'python2-editorconfig')
pkgver=0.12.0
pkgrel=2
pkgdesc='EditorConfig File Locator and Interpreter for Python'
arch=('any')
license=('PSF')
url='https://github.com/editorconfig/editorconfig-core-py'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('cmake')
source=("git+https://github.com/editorconfig/editorconfig-core-py.git#tag=v$pkgver"
        "git+https://github.com/editorconfig/editorconfig-core-test.git")
md5sums=('SKIP'
         'SKIP')

prepare() {
  cd editorconfig-core-py
  git submodule init
  git config submodule."tests".url "$srcdir"/editorconfig-core-test
  git submodule update

  cp -a "$srcdir"/editorconfig-core-py{,-py2}
}

build() {
  cd "$srcdir"/editorconfig-core-py
  python setup.py build

  cd "$srcdir"/editorconfig-core-py-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/editorconfig-core-py
  ctest -VV --output-on-failure .

  cd "$srcdir"/editorconfig-core-py-py2
  ctest -DPYTHON_EXECUTABLE=/usr/bin/python2 -VV --output-on-failure .
}

package_python-editorconfig() {
  depends=('python')

  cd editorconfig-core-py
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-editorconfig() {
  depends=('python2')

  cd editorconfig-core-py-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt

  mv "$pkgdir"/usr/bin/editorconfig{,2}
}

# vim:set ts=2 sw=2 et:
