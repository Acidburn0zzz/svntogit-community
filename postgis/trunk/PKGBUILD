# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=postgis
pkgver=2.4.2
pkgrel=2
pkgdesc="Adds support for geographic objects to PostgreSQL"
arch=('x86_64')
url="http://postgis.net/"
license=('GPL')
depends=('postgresql' 'gdal' 'json-c' 'proj' 'protobuf-c')
changelog=$pkgname.changelog
options=('!makeflags')
source=(http://download.osgeo.org/postgis/source/${pkgname}-${pkgver}.tar.gz
        json-0.13-compatibility.patch)
sha256sums=('23625bc99ed440d53a20225721095a3f5c653b62421c4d597c8038f0d7a321d9'
            'a7d3a6005cf776d69dc6df0fc4f31a366049487fcde7939a1849e724d0d78c36')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i $srcdir/json-0.13-compatibility.patch
}

build() {
  cd ${pkgname}-${pkgver}

  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
