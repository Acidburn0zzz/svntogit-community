# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>

pkgname=skrooge
pkgver=2.16.2
pkgrel=3
pkgdesc="A personal finances manager for KDE"
arch=('x86_64')
url="https://skrooge.org/"
license=('GPL')
depends=('qt5-base' 'qca' 'qt5-script' 'qt5-webkit' 'libofx'
         'grantlee' 'kross' 'knewstuff' 'krunner' 'knotifyconfig'
         'sqlcipher' 'python' 'hicolor-icon-theme')
makedepends=('cmake' 'kdoctools' 'extra-cmake-modules' 'qt5-tools')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        sqlcipher4-compliance1.patch sqlcipher4-compliance2.patch)
validpgpkeys=('103DC6D087B3A4A2ED975D7EF0C8CED3382A5C4D') #Stephane MANKOWSKI (Perso) <stephane@mankowski.fr>
sha256sums=('124e0928595be6c1ae0a879f4f1cd4631e7aced8c5273ccb92e128a4f2c2bb45'
            'SKIP'
            '35e2555a404d8ee170c17664fe44864dcf47e9d70307b02af8455da09a26a61a'
            'cc85c191033576accefe8686be22de3a6a7cb8425c987ae853f70facd4823e85')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

# https://bugs.archlinux.org/task/60989
  patch -Np1 -i "${srcdir}"/sqlcipher4-compliance1.patch
  patch -Np1 -i "${srcdir}"/sqlcipher4-compliance2.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

#   export
  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DKDE_INSTALL_LIBDIR=/usr/lib \
          -DSKG_DESIGNER=OFF
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
