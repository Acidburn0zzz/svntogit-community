# $Id$
# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=2.0.2
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'cinnamon-settings-daemon' 'cinnamon-session'
         'cinnamon-translations' 'cjs' 'clutter-gtk' 'gconf' 'gnome-icon-theme'
         'gnome-menus' 'gnome-themes-standard' 'gstreamer0.10' 'libgnome-keyring'
         'librsvg' 'networkmanager' 'muffin' 'pygtk' 'python2-dbus' 'python2-pillow'
         'python2-pam' 'python2-pexpect' 'python2-pyinotify' 'python2-lxml' 'webkitgtk')
makedepends=('gnome-common' 'intltool')
optdepends=('cinnamon-control-center: extended configurations for Cinnamon'
            'cinnamon-screensaver: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nemo: the official Cinnamon file manager (forked from Nautilus)')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/archive/$pkgver.tar.gz"
        "fix-control-center-check.patch"
        "background.patch"
        "keyboard_applet.patch"
        "input_keybindings.patch"
        "org.archlinux.pkexec.cinnamon-settings-users.policy")
sha256sums=('3b7043a603c19d96bf1e5ccaafde24ca507c93ee51e81e8c5c8754457a18a5ac'
            'ee5694bdc997ffa35a817f691b15bae13747137d35ec2aecd0da298d7edbe426'
            '373d80cdb23250fbde846ed493ba422672cc42b03a111c2ce044467ee782df7f'
            '6acb07393105ddced8a4c3c869a596350d1a7d81a808ca5307d2ad770653a9d3'
            'e28c40eb844105154fa6106f5b4de3151a22805b3a7b2f84be9ea6c15cec3de6'
            '371beac9e55d36f7629d2fc5cb40d6a3e6c0f4aac014f6fefdcd6743b5194b23')

prepare() {
  cd ${srcdir}/Cinnamon*

  # Python2 fix
  sed -i 's:/usr/bin/python :/usr/bin/python2 :' files/usr/bin/cinnamon-menu-editor
  find -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'

  # Check for the cc-panel path, not for the unneeded binary
  patch -Np1 -i ../fix-control-center-check.patch

  # Fix missing backgrounds
  patch -Np1 -i ../background.patch

  # Fix keyboard applet
  patch -Np1 -i ../keyboard_applet.patch

  # Add input keybindings
  patch -Np1 -i ../input_keybindings.patch

  # Prefix 'System Settings' with 'Cinnamon' to avoid confusion with gnome-control-center
  sed -i 's/^Name\(.*\)=\(.*\)/Name\1=Cinnamon \2/' files/usr/share/applications/cinnamon-settings{,-users}.desktop

  # fix for the python2 PAM module  
  sed -i 's:import PAM:import pam:' files/usr/lib/cinnamon-settings/modules/cs_user.py

  # Use pkexec instead of gksu
  sed -i 's/gksu/pkexec/' files/usr/bin/cinnamon-settings-users
}

build() {
  cd ${srcdir}/Cinnamon*

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
  make CFLAGS="${CFLAGS} -Wno-deprecated -Wno-deprecated-declarations"
}

package() {
  cd ${srcdir}/Cinnamon*
  make DESTDIR="${pkgdir}" install

  # Remove leftover files after patching
  find "$pkgdir" -type f -name *.orig | xargs rm

  # Install policy file
  install -Dm644 "${srcdir}/org.archlinux.pkexec.cinnamon-settings-users.policy" \
                 "${pkgdir}/usr/share/polkit-1/actions/org.archlinux.pkexec.cinnamon-settings-users.policy"
}
