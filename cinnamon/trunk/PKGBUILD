# $Id$
# Contributor: M0Rf30
# Contributor: unifiedlinux
# Contributor: CReimer

pkgname=cinnamon
pkgver=1.7.1
pkgrel=1
pkgdesc="Linux desktop which provides advanced innovative features and a traditional user experience"
arch=('i686' 'x86_64')
url="http://cinnamon.linuxmint.com/"
license=('GPL2')
depends=('accountsservice' 'caribou' 'gjs' 'gnome-bluetooth' 'gnome-icon-theme'
         'gnome-menus' 'gnome-settings-daemon' 'gnome-session' 'gnome-themes-standard'
         'gstreamer0.10' 'networkmanager' 'muffin' 'python2-dbus'
         'python2-gconf' 'python2-imaging' 'python2-lxml')
makedepends=('gnome-common' 'intltool')
optdepends=('gnome-control-center: open settings from various applets'
            'gnome-screensaver: lock screen'
            'gucharmap: show character table from Keyboard applet'
            'libgnomekbd: show keyboard layout from Keyboard applet'
            'nautilus: the official Gnome file manager')
options=('!libtool' '!emptydirs')
install=${pkgname}.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxmint/Cinnamon/tarball/$pkgver"
        "cinnamon-screensaver.desktop"
        "cinnamon2d-screensaver.desktop"
        "gnome-autogen.sh"
        "keyboard_applet.patch")
md5sums=('13daa8fde1480bbee25eddc2e2630319'
         'd55c079cdc8bec606f510f87b2a6f776'
         '5c6af851ab1cf7e9219f11d86353196a'
         'a925691c9b57a6a884dcf07da057fd1f'
         '2b1ece84416c3e4de030be15f1774d82')

build() {
  cd ${srcdir}/linuxmint-Cinnamon*

  # Fix Keyboard applet
  # https://github.com/linuxmint/Cinnamon/issues/1337
  patch -Np1 -i ${srcdir}/keyboard_applet.patch

  cp ${srcdir}/gnome-autogen.sh .
  sed -i 's/\ --warn-all\ --warn-error//' src/Makefile.am
  sed -i 's/gnome-autogen.sh/.\/gnome-autogen.sh/g' autogen.sh
  chmod +x gnome-autogen.sh

  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon \
               --localstatedir=/var \
               --disable-static \
               --disable-schemas-compile \
               --enable-compile-warnings=yes \
               --with-session-tracking=systemd
 
  make
}

package() {
  cd ${srcdir}/linuxmint-Cinnamon*
  make DESTDIR="${pkgdir}" install
  
  # Python2 fix
  sed -i 's/bin\/python/bin\/python2/g' \
    "${pkgdir}/usr/bin/$pkgname-launcher" \
    "${pkgdir}/usr/bin/$pkgname-menu-editor" \
    "${pkgdir}/usr/bin/$pkgname-settings" \
    "${pkgdir}/usr/share/$pkgname/applets/panel-launchers@$pkgname.org/$pkgname-add-panel-launcher.py"
    
  sed -i 's/env python/env python2/g' \
    "${pkgdir}/usr/lib/$pkgname-settings/$pkgname-settings.py"

  # Screensaver gnome 3.6 fix
  mkdir -p $pkgdir/etc/xdg/autostart
  cp $srcdir/cinnamon*-screensaver.desktop $pkgdir/etc/xdg/autostart
}


