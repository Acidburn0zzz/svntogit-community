# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=budgie-desktop
_helper_pkgname=budgie-helper
pkgver=10.4+123+gf2dbc215
_helper_gitrev=3b52434b6d1cfe00c5bac1046d323813ce531a55
pkgrel=2
pkgdesc="Modern desktop environment from the Solus Project"
arch=('x86_64')
url="https://budgie-desktop.org/"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gnome-bluetooth' 'gnome-menus' 'gnome-session' 'gnome-themes-standard' 'libibus' 'libpeas' 'libwnck3' 'mutter')
makedepends=('autoconf-archive' 'git' 'gobject-introspection' 'intltool' 'meson' 'sassc' 'vala')
optdepends=('gnome-backgrounds: Default background'
            'gnome-control-center: System settings'
            'gnome-screensaver: Lock screen'
            'network-manager-applet: Network management')
# Tempolary use the GNOME 3.30 + GNOME 3.32 compatibility patch
# https://github.com/solus-project/budgie-desktop/pull/1591
#_commit=f2dbc215fdb43de88e5b0876413b4a9dc77b2153  # master
#source=("git+https://github.com/budgie-desktop/budgie-desktop.git#commit=$_commit"
_commit=36b793bcc35bb406c325f553ac8127141dc3b14f  # master
source=("git+https://github.com/UbuntuBudgie/budgie-desktop.git#commit=$_commit"
        "git+https://gitlab.gnome.org/GNOME/libgnome-volume-control.git"
        "$_helper_pkgname-$_helper_gitrev.tar.gz::https://codeload.github.com/City-busz/$_helper_pkgname/tar.gz/$_helper_gitrev")
validpgpkeys=('8876CC8EDAEC52CEAB7742E778E2387015C1205F') # Ikey Doherty (Solus Project Founder)
sha256sums=('SKIP'
            'SKIP'
            '3a4d7dd7c95ccba4e2916adf4a14769ffe54e8f86ed302d0268cd312b2a85c0e')

#pkgver() {
#	cd $pkgname
#	git describe --tags | sed 's/^v//;s/-/+/g'
#}

prepare() {
	cd $pkgname
	git config --local submodule.subprojects/gvc.url "$srcdir/libgnome-volume-control"
	git submodule update --init

	# Provide better compatibility for GNOME
	# https://github.com/solus-project/budgie-desktop/issues/261
	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	NOCONFIGURE=1 ./autogen.sh
}

build() {
	arch-meson $pkgname build -D with-desktop-icons=none
	ninja -C build

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile --disable-Werror
	make
}

package() {
	DESTDIR="$pkgdir" meson install -C build

	cd "$srcdir/$_helper_pkgname-$_helper_gitrev"
	make DESTDIR="$pkgdir" install
}
