# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_pkgname=MeiliSearch
pkgname=meilisearch
pkgver=0.11.0
pkgrel=1
pkgdesc="Lightning Fast, Ultra Relevant, and Typo-Tolerant Search Engine"
arch=(x86_64)
url="https://docs.meilisearch.com/"
license=('MIT')
depends=(gcc-libs)
makedepends=(rust)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/meilisearch/MeiliSearch/archive/v${pkgver}.tar.gz
        ${pkgname}.service ${pkgname}.tmpfiles ${pkgname}.sysusers ${pkgname}.conf)
sha512sums=('d77cd7b85b41649b9a447efaaa9847bfe76b5e153e485394be6c41ba000f79186df4793192ef246543d865cc3e80b3af6df887cd6d5ac1af868f97628766b839'
            '823dbed638775250e3591fba8e047931b2f950af8f28839c6d49929ceebdeecbfe57dbacb7e866456ba004e78abc3d930731b0d6cc8bbae0cdac918f940c7a3c'
            '76284bf4c436a6b3365edd6bd12ffcb186b721d284b29933e77ddc841d333be9eeb2701c89fba1567a8eedda52787be8d3c5f55809a051488b7295fbb6842bce'
            'b83518bd61cb587baac784b1b9bbe4a6a12be91c0a5b6cc0b3b631dde9cee1d59d6e43800a0d32b909fb2d90fe3f64e1ab63c3c8028428be3333d39f6c05d60c'
            '9d63a9cca6cafb248fffcd4a6e5bac0905d4b87f3eb9793576003d3965e35e217c4c1d6b3206b25ef47143e7d538e71c746bc0352bcdf248ed19e12f58aedc12')
validpgpkeys=()

build() {
  cd "$_pkgname-$pkgver"
  cargo build --release --locked --all-features
}

check() {
  cd "$_pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"

  install -D -m644 "${srcdir}/meilisearch.service"  "${pkgdir}/usr/lib/systemd/system/meilisearch.service"
  install -D -m644 "${srcdir}/meilisearch.sysusers" "${pkgdir}/usr/lib/sysusers.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/meilisearch.conf"
  install -D -m644 "${srcdir}/meilisearch.conf" "${pkgdir}/etc//meilisearch.conf"
}
