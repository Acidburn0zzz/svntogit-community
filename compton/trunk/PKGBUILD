# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=compton
pkgver=7.2
pkgrel=1
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/compton'
license=(MIT)
makedepends=(asciidoc git mesa meson ninja setconf uthash)
depends=(hicolor-icon-theme libconfig libdbus libev libgl libxdg-basedir pcre
         pixman xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/compton.conf)
optdepends=('dbus:          For controlling compton via D-Bus'
            'xorg-xwininfo: For compton-trans'
            'xorg-xprop:    For compton-trans'
            'python:        For running compton-convgen.py')
source=(compton.conf
        "git+https://github.com/yshui/compton#tag=v${pkgver%.0}")
sha256sums=('c25e5b741a3a76928d1866f0097c482844e272b41823c24486a91cb39a97341d'
            'SKIP')

prepare() {
  cd $pkgname
  setconf $pkgname.desktop Exec compton
  setconf $pkgname.desktop Comment 'Compositor for X11'
  setconf $pkgname.desktop TryExec /usr/bin/compton
}

build() {
  mkdir -p build
  arch-meson build $pkgname -D build_docs=true -D b_ndebug=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 compton.conf "$pkgdir/etc/xdg/compton.conf"
  cd $pkgname
  install -Dm644 compton.sample.conf "$pkgdir/etc/xdg/compton.conf.example"
  install -Dm644 LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# getver: github.com/yshui/compton/releases
# vim: ts=2 sw=2 et
