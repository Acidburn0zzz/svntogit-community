# $Id: PKGBUILD $
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=mp3splt-gtk
pkgver=0.6
pkgrel=1
pkgdesc="Split mp3 and ogg files without decoding"
arch=('i686' 'x86_64')
url="http://mp3splt.sourceforge.net/"
license=('GPL')
depends=('gtk2' 'libmp3splt')
makedepends=('pkgconfig' 'desktop-file-utils')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/mp3splt/$pkgname-$pkgver.tar.gz \
        $pkgname.desktop)
md5sums=('0a88bf1a8928a36842a35a174ab18aa3'
         '9e5a5748d40594e0090e7ceed903c674')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  ./configure --prefix=/usr --disable-mp3splttest --disable-audacious --disable-gstreamer
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver
  make DESTDIR=${pkgdir} install

#install .desktop file
  install -D -m644 ${srcdir}/$pkgname.desktop \
    ${pkgdir}/usr/share/applications/$pkgname.desktop
}
