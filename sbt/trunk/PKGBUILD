# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Leif Warner <abimelech@gmail.com>

pkgname=sbt
epoch=1
pkgver=0.13.13
pkgrel=1
pkgdesc='A build tool for Scala, Java, and more'
arch=('any')
url='http://www.scala-sbt.org/'
license=('BSD')
depends=('java-runtime-headless' 'sh')
IFS='.' read -a _pkgver <<< "${pkgver}"
source=(https://dl.bintray.com/${pkgname}/native-packages/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tgz{,.asc}
        https://raw.githubusercontent.com/${pkgname}/${pkgname}/${_pkgver[0]}.${_pkgver[1]}/LICENSE)
validpgpkeys=('2EE0EA64E40A89B84B2DF73499E82A75642AC823') # sbt build tool
sha256sums=('40d03d21a260c5a6a43f8349298f41c9d047f97972057d9d915afd8945faf979'
            'SKIP'
            '70b7b72694a56a3e922deaf5482273e7c33bab2c00c0c67fc0ccc5d33927ffed')

package() {
  cd "${srcdir}/${pkgname}-launcher-packaging-${pkgver}"

  mkdir -p "${pkgdir}"/usr/share/${pkgname}/bin
  install -m 644 bin/sbt-launch.jar -t "${pkgdir}"/usr/share/${pkgname}/bin
  install -m 755 bin/sbt -t "${pkgdir}"/usr/share/${pkgname}/bin
  mkdir -p "${pkgdir}"/usr/bin
  ln -s /usr/share/${pkgname}/bin/sbt "${pkgdir}"/usr/bin/sbt
  install -m 644 bin/sbt-launch-lib.bash -t "${pkgdir}"/usr/share/${pkgname}/bin

  install -D -m 644 conf/sbtopts -t "${pkgdir}"/usr/share/${pkgname}/conf
  mkdir -p "${pkgdir}"/etc/${pkgname}
  ln -s /usr/share/${pkgname}/conf/sbtopts "${pkgdir}"/etc/${pkgname}/sbtopts

  install -D "${srcdir}"/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
