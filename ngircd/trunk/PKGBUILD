# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>

pkgname=ngircd
pkgver=18
pkgrel=1
pkgdesc="Next Generation IRC Daemon"
arch=('i686' 'x86_64')
backup=(etc/ngircd.conf)
url="http://ngircd.barton.de/"
license=('GPL')
depends=('openssl' 'libident')
source=(http://ngircd.barton.de/pub/ngircd/ngircd-$pkgver.tar.gz ngircd.sh)
md5sums=('4958c8b2d128cf3e9888af3f782892a1'
         '6d2e9f1bbb4a3a7349da0dcd3f6d8dc3')

build() {
	cd $srcdir/$pkgname-$pkgver
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--with-ident \
		--with-openssl
	make
}

package() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
	install -D -m 755 ../ngircd.sh $pkgdir/etc/rc.d/ngircd
}
