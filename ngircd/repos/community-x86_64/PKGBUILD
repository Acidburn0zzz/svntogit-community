# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ngircd
pkgver=19.1
pkgrel=1
pkgdesc="Next Generation IRC Daemon"
arch=('x86_64' 'i686')
backup=(etc/ngircd.conf)
url="http://ngircd.barton.de/"
license=('GPL')
depends=('openssl' 'libident')
source=("http://ngircd.barton.de/pub/ngircd/ngircd-$pkgver.tar.gz"
        ngircd.sh)
sha256sums=('3e5cf62ab5164a310aa47f384387803a4c2b5221005ad1eb648ae2c5f0796f60'
            'f8c28546090af8ce67471b9b4d577f6926007615356dd11afd2bc16f61d698d5')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
  	--mandir=/usr/share/man \
  	--with-ident \
  	--with-openssl \
  	--enable-ipv6
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm 755 ../ngircd.sh "$pkgdir/etc/rc.d/ngircd"
}

# vim:set ts=2 sw=2 et:
