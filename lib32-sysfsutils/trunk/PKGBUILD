# $Id: PKGBUILD 38987 2009-05-11 06:21:37Z tpowa $
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

_pkgbasename=sysfsutils
pkgname=lib32-$_pkgbasename
pkgver=2.1.0
pkgrel=6
pkgdesc="System Utilities Based on Sysfs (32-bit)"
arch=(x86_64)
license=('GPL' 'LGPL')
url="http://linux-diag.sourceforge.net/Sysfsutils.html"
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
source=(http://downloads.sourceforge.net/sourceforge/linux-diag/$_pkgbasename-$pkgver.tar.gz)
md5sums=('14e7dcd0436d2f49aa403f67e1ef7ddc')
options=(!libtool)

build() {
  cd $srcdir/$_pkgbasename-$pkgver 

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr --mandir=/usr/share/man --libdir=/usr/lib32
  make 
}

package() {
  cd $srcdir/$_pkgbasename-$pkgver 
  make DESTDIR=$pkgdir install
  rm -rf "${pkgdir}"/usr/{include,share,bin}
}

