# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgbase=snes9x
pkgname=(snes9x snes9x-gtk)
pkgver=1.58
pkgrel=1
pkgdesc='Portable Super Nintendo Entertainment System (TM) emulator'
arch=(x86_64)
url='http://www.snes9x.com/'
license=(custom)
makedepends=(
  alsa-lib
  cairo
  gdk-pixbuf2
  git
  glib2
  gtk3
  intltool
  libepoxy
  libpng
  libpulse
  libx11
  libxext
  libxml2
  libxrandr
  libxv
  minizip
  nasm
  sdl2
  zlib
)
source=(git+https://github.com/snes9xgit/snes9x.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  pushd snes9x/unix

  autoreconf -fiv

  popd
  pushd snes9x/gtk

  ./autogen.sh

  popd
}

build() {
  pushd snes9x/unix

  ./configure \
    --prefix='/usr' \
    --enable-netplay
  make

  popd
  pushd snes9x/gtk

  ./configure \
    --prefix='/usr' \
    --with-netplay \
    --with-opengl
  make

  popd
}

package_snes9x() {
  depends=(
    libpng
    libx11
    libxext
    libxinerama
    libxv
    minizip
    zlib
  )

  cd snes9x/unix

  install -Dm 755 snes9x -t "${pkgdir}"/usr/bin/
  install -Dm 644 ../docs/{control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x
  install -Dm 644 ../LICENSE "${pkgdir}"/usr/share/licenses/snes9x/LICENSE

}

package_snes9x-gtk() {
  depends=(
    alsa-lib
    cairo
    gdk-pixbuf2
    glib2
    gtk3
    hicolor-icon-theme
    libepoxy
    libpng
    libpulse
    libx11
    libxext
    libxml2
    libxrandr
    libxv
    minizip
    sdl2
    zlib
  )

  cd snes9x/gtk

  make DESTDIR="${pkgdir}" install
  install -Dm 644 ../docs/{control-inputs.txt,controls.txt,snapshots.txt} -t "${pkgdir}"/usr/share/doc/snes9x-gtk
  install -Dm 644 ../LICENSE "${pkgdir}"/usr/share/licenses/snes9x-gtk/LICENSE
}

# vim: ts=2 sw=2 et:
