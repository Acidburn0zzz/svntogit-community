# $Id$
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>  
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=0.19.1
pkgrel=2
pkgdesc="A fast and lightweight IDE"
arch=('i686' 'x86_64')
url="http://www.geany.org"
license=('GPL')
depends=('gtk2>=2.8.0')
makedepends=('perlxml' 'intltool')
optdepends=("vte: for terminal support")
install=geany.install
source=(http://download.geany.org/$pkgname-$pkgver.tar.gz)
options=('!libtool')
md5sums=('962d8cdd8c3fad6ab0b172c2aede262a')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' data/templates/files/main.py

  ./configure --prefix=/usr
  make

  # FS#10318
  sed -i 's|MimeType=text/plain;|MimeType=|' geany.desktop
  # Add colors for PKGBUILD file
  sed -i 's|Sh=|Sh=PKGBUILD;|' data/filetype_extensions.conf

  make DESTDIR=${pkgdir} install
}
