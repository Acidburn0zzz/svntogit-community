# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Ionut Biru  <ibiru@archlinux.ro>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Allan McRae <mcrae_allan@hotmail.com>

pkgname=geany
pkgver=1.34
pkgrel=1
pkgdesc='Fast and lightweight IDE'
arch=('x86_64')
url='https://www.geany.org/'
license=('GPL')
depends=('gtk3' 'python')
makedepends=('doxygen' 'intltool' 'python-lxml')
optdepends=('geany-plugins: additional functionality'
            'vte3: embedded terminal support')
source=("https://download.geany.org/$pkgname-${pkgver/.0}.tar.bz2")
sha256sums=('63b93d25d037eaffa77895ae6dd29c91bca570e4053eff5cc8490f87e6021f8e')

build() {
  cd "$pkgname-${pkgver/.0}"

  ./configure --prefix=/usr --enable-gtk3 --enable-gtkdoc-header
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  make -C "$pkgname-${pkgver/.0}" DESTDIR="$pkgdir" install
}

# getver: geany.org
# vim: ts=2 sw=2 et:
