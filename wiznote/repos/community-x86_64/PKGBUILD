# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Albert.Zhou <albert.zhou@wiz.cn>

pkgname=wiznote
pkgver=2.8.4
_commit=ce110ef42d48c73160cb59f5e9b026c5b42617e4
pkgrel=1
pkgdesc="Opensource cross-platform cloud based note-taking client"
arch=('x86_64')
url="http://www.wiznote.com"
license=('GPL3' 'custom')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'xdg-utils' 'qt5-websockets' 'qt5-webengine'
         'qt5-svg') # 'crypto++')
# shared clucene/quazip triggers segfault
makedepends=('cmake' 'qt5-tools' 'boost')
source=("$pkgname-$_commit.tar.gz::https://github.com/WizTeam/WizQTClient/archive/$_commit.tar.gz")
sha512sums=('25ee69993924e9744e9e4e06b174f65aa6b49031f003932386e121dd8d53435af814d78eb6471a194c395fc3b3551081025de2918a0be068cb623415e7c47157')

prepare() {
  #sed -e 's/add_subdirectory(cryptopp)//' \
  #    -i WizQTClient-$_commit/lib/CMakeLists.txt
  #sed -e 's/cryptlib/cryptopp/' \
  #    -i WizQTClient-$_commit/src/CMakeLists.txt

  # https://github.com/WizTeam/WizQTClient/issues/610
  sed -i 's/= nPosition;/= (Json::UInt64)nPosition;/' WizQTClient-$_commit/src/share/WizObject.cpp

  sed -i '/#include <QPainter>/a #include <QPainterPath>' WizQTClient-$_commit/src/sync/WizAvatarHost.cpp

  rm -rf build; mkdir build
}

build() {
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    ../WizQTClient-$_commit
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
