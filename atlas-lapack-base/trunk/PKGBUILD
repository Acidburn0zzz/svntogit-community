# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giuseppe Borzi <gborzi___AT___ieee___DOT___org>
# Contributor: mickele <mimocciola___AT___yahoo___DOT___ com>
# Contributor: iztok pizorn <pizorn___AT___gmail___DOT___com>
# Contributor: olivier medoc <o_medoc___AT___yahoo___DOT___fr>

pkgname=atlas-lapack-base
pkgver=3.10.3
_lapackver=3.6.1
pkgrel=1
pkgdesc="Complete LAPACK and BLAS implementation using optimized ATLAS routines"
url="http://math-atlas.sourceforge.net/"
depends=(gcc-libs)
makedepends=(gcc-fortran)
arch=(i686 x86_64)
conflicts=(blas cblas lapack)
provides=(blas cblas lapack atlas-lapack)
license=(custom:blas custom:lapack custom:atlas)
options=(!makeflags)
source=(http://www.netlib.org/lapack/lapack-$_lapackver.tgz 
        http://downloads.sourceforge.net/project/math-atlas/Stable/$pkgver/atlas$pkgver.tar.bz2 
        blas-license.txt atlas-license.txt Makefile disable-throttling-check.patch)
noextract=(lapack-$_lapackver.tgz)
install=$pkgname.install
md5sums=('421b2cb72e15f237e144428f9c460ee0'
         'd6ce4f16c2ad301837cfb3dade2f7cef'
         'e8e512ee7a2d550139dcd765f127816b'
         '4903eb06072dfbf94710691ccb6660bf'
         'ffb0b384708b5aacc22715d04f3dde45'
         '54f80b9a56f13f960017460c36b725ce')

prepare() {
  mkdir -p ATLAS/build

# override throttling check (Fedora)
  patch -p0 -i disable-throttling-check.patch
}

build() {
  if [ "$CARCH" = "x86_64" ]; then
     ARCHITECTURE_BUILD_OPTS="-b 64 -A x86SSE2 -V 448" # for x86_64
  else
     ARCHITECTURE_BUILD_OPTS="-b 32 -A x86x87 -V 384" # for i686
  fi

  cd ATLAS/build
  ../configure --prefix=/usr/ $ARCHITECTURE_BUILD_OPTS -Fa alg -fPIC -Si latune 0 -t 0 \
     --with-netlib-lapack-tarfile="$srcdir"/lapack-$_lapackver.tgz
  make build

# build shared libraries
  cd lib
  cp "$srcdir"/Makefile .
  make
}

package() {
  cd ATLAS/build

  make DESTDIR="$pkgdir"/usr install
  cp -d lib/*.so* "$pkgdir"/usr/lib
  ln -sr "$pkgdir"/usr/lib/libblas.so{,.3}
  ln -sr "$pkgdir"/usr/lib/liblapack.so{,.3}

  install -Dm644 "$srcdir"/blas-license.txt "$pkgdir"/usr/share/licenses/$pkgname/blas-license.txt
  install -Dm644 "$srcdir"/ATLAS/build/src/lapack/reference/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/lapack-license.txt
  install -Dm644 "$srcdir"/atlas-license.txt "$pkgdir"/usr/share/licenses/$pkgname/atlas-license.txt
}
