# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=tilda
pkgver=1.5.0
pkgrel=1
pkgdesc="A Gtk based drop down terminal for Linux and Unix"
arch=('x86_64')
url="https://github.com/lanoxx/tilda"
license=('GPL')
depends=('vte3' 'confuse')
makedepends=('python')
changelog=$pkgname.changelog
source=(https://github.com/lanoxx/$pkgname/archive/$pkgname-$pkgver.tar.gz)
sha256sums=('f664c17daca2a2900f49de9eb65746ced03c867b02144149ef21260cbcd61039')

build() {
  cd $pkgname-$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
