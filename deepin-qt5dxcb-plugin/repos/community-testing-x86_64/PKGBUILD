# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.26.1
pkgrel=2
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        qt-5.12.patch)
sha512sums=('2ea04b16b24f8284df7a76bf15c361a134bc326ac674d5e85fc9a5aaa34cf4026746e5d62374c0381a6e30fb87d5c4ce051ff42195a73f5b7707cf6b9282db0f'
            '0fc5d30bd9541e79db8991a2332ad0db6640d95067416f7b11c4cd7b6c1be2788e8f13f1ef989ddcd5ca57e3b798ac76d15d0f3081a811b6a7b6af7e0d39b691')

prepare() {
  cd qt5dxcb-plugin-$pkgver
  patch -p1 -i ../qt-5.12.patch

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
