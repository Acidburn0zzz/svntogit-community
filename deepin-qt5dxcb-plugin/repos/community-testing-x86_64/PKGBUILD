# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.2.2
pkgrel=3
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        deepin-qt5dxcb-plugin-qt5.13.patch)
sha512sums=('1889804d45fbb325be0dec6f40150a7190c1850a8343b8e3964c51fe1354455a88707ca684d1db28baa910aef9e5218129b6159a079d82f29adbb1a24e754daa'
            '6ff5dc34d32a923a390b65e01270e7fa186dac3dd1562a1d9be3a292666ef2392574105483c8f8150a7ab9a98ffcaa21c0b65ad44f2e8314804bed28aa0b8073')

prepare() {
  cd qt5dxcb-plugin-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
  patch -p1 -i ../deepin-qt5dxcb-plugin-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
