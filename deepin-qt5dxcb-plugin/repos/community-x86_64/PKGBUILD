# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.26
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        qt-5.12.patch)
sha512sums=('8f796366d235437360c021c66c3532c2bb7e0f388acd89e1d4c20fbadf4734661d0aec19fdcf5ed4e299826d63cbdfb90def42305709ece10ba6efe55684e159'
            'cd7ee42ab6b46ecf13b1f6e14bb33f0c1f241017307f716d6e38f3d330bd56ce0d78690e9d606a1da6a32aa4ab3684d3a792abea3dc7b8fe8d43a1bc43d6f24e')

prepare() {
  cd qt5dxcb-plugin-$pkgver
  patch -p1 -i ../qt-5.12.patch

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
