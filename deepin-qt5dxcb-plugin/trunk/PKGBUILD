# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=5.0.11
pkgrel=2
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd' 'libxcb')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
         deepin-qt5dxcb-plugin-qt5.15.patch)
sha512sums=('54b401c24334e2d20db734b31f2d9e89595269a8679aba93362038d4af109581d261e79cef6e1fe030854e8e95ab109f6fe0c979f2f07684412e0de8507d0c7d'
            '15a9edb6a1df30c68622260bad799dcb56514f1e4336ab8c12b2984c81ac710037ac00584b76a50aea0b7cf311dc6dc05df2441ec175d6041c1d054c2982de92')

prepare() {
  cd qt5platform-plugins-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri

  sed -i 's/active = VtableHook::overrideVfptrFun.*/active = 1;/' platformplugin/dhighdpi.cpp

  patch -p1 -i ../deepin-qt5dxcb-plugin-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd qt5platform-plugins-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5platform-plugins-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
