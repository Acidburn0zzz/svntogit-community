# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.8.6
pkgrel=2
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        qt-5.11.patch)
sha512sums=('c8fd99d299e4867a69c4c846ea444861a5bdf8c711d7d9015ec8626f3cd2d3471b7cf78c60aa22a93d318bf82bc42598ca59f784cc29793b70c9f49ec5c7a0b2'
            'e076c590a15f43df729eb28d92c510d80ffbdd312b769179b53ca0b364d9b8232d0f14927b11a6626bcd14bea952d07a24505fb82bd0867174a76f9c937a2c32')

prepare() {
  cd qt5dxcb-plugin-$pkgver
  patch -p1 -i ../qt-5.11.patch

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
