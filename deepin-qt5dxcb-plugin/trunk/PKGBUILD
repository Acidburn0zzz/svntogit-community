# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.2.0
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz")
sha512sums=('e924e60a6e3b80ba40b7ca2b4c38ac07f3b23bfa7f9357893a826e545bd87f230b366162f9aaa4e7b4ca2493483e7b51771bd6fa4040f96356a005c021ff69c5')

prepare() {
  cd qt5dxcb-plugin-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
