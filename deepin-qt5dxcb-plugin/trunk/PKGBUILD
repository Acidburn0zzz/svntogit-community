# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.2.5
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz"
        deepin-qt5dxcb-plugin-qt5.13.patch)
sha512sums=('3dfcf74ed749ccffb2986430908fba93cd77a7626c649a7c51b0b32e407571b1b7d82ebf9866777070ff25357e4378f041da0ce7481ee4d4e63a08b583685feb'
            '6ff5dc34d32a923a390b65e01270e7fa186dac3dd1562a1d9be3a292666ef2392574105483c8f8150a7ab9a98ffcaa21c0b65ad44f2e8314804bed28aa0b8073')

prepare() {
  cd qt5dxcb-plugin-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
  patch -p1 -i ../deepin-qt5dxcb-plugin-qt5.13.patch # Fix build with Qt 5.13
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
