# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.10
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('cairo' 'qt5-x11extras')
makedepends=('qt5-xcb-private-headers' 'libglvnd')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz")
sha512sums=('18cf0f9b2c9bc27000ba7868316d8c6107a256ed2c43282c4df8aab28db6c7df7bed2cdd616e866e080e35a46b15df2740c693fc6cab212ddc0fb7445f17b2f9')

prepare() {
  cd qt5dxcb-plugin-$pkgver
  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
