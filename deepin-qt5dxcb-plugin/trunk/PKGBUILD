# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-qt5dxcb-plugin
pkgver=1.1.4
pkgrel=1
pkgdesc='Qt platform plugins for DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/qt5dxcb-plugin"
license=('GPL3')
depends=('qt5-x11extras')
makedepends=('qt5-xcb-private-headers')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/qt5dxcb-plugin/archive/$pkgver.tar.gz")
sha512sums=('06934134f8bcb51c2ca8893fa328bd0e71732af7a638d7acb30163aa570a8cc3564df6ced1198dede445aa68657b760e9e7e8ec089feb1bde67ce7c23f03ee9d')

prepare() {
  cd qt5dxcb-plugin-$pkgver

  rm -r platformplugin/libqt5xcbqpa-dev
  sed -i 's|error(Not support Qt Version: .*)|INCLUDEPATH += /usr/include/qtxcb-private|' platformplugin/linux.pri
}

build() {
  cd qt5dxcb-plugin-$pkgver
  qmake-qt5 QMAKE_CFLAGS_ISYSTEM= PREFIX=/usr
  make
}

package() {
  cd qt5dxcb-plugin-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
