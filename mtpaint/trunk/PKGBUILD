#$Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64.at.gmail.dot.com>
pkgname=mtpaint
pkgver=3.31
pkgrel=6
pkgdesc="A simple GTK2 painting program designed for creating icons and pixel based artwork"
arch=('x86_64' 'i686')
url="http://mtpaint.sourceforge.net/"
license=('GPL3')
depends=('gtk2' 'giflib' 'openjpeg' 'bzip2' 'desktop-file-utils' 'glib2' 'freetype2')
install=mtpaint.install
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2"
        "libpng14.patch"
        "mtpaint.install")
md5sums=('fbe34eb25f96b6092403115f1f6ab387'
         'f84245ae1e26bd734740306b09024bb8'
         '1bd546a8efeae01e8fd8a5c7f213012e')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	patch -Np1 -i ${srcdir}/libpng14.patch
	./configure --prefix=/usr --mandir=/usr/share/man/man1 man intl gif jpeg tiff
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make DESTDIR=${pkgdir} install
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:

