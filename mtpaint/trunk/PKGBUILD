# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64@gmail.com>

pkgname=mtpaint
pkgver=3.49.33
pkgrel=1
pkgdesc='Paint program for creating icons and pixel art'
arch=(x86_64)
url='https://github.com/wjaguar/mtPaint'
license=(GPL3)
makedepends=(git)
depends=(giflib gtk2 openjpeg2)
optdepends=('gifsicle: export GIF files from mtPaint')
source=("git+$url#commit=5272e2b1e773c8e02ac3506b2d3bde82ad946b21") # version: 3.49.33
sha256sums=('SKIP')

build() {
  cd mtPaint
  export CFLAGS="-w $(pkg-config openjp2 --cflags) $CFLAGS"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    man intl GIF jpeg tiff cflags lcms2 jp2v2
  make
}

package() {
  make -C mtPaint DESTDIR="$pkgdir" install
}

# getver: github.com/wjaguar/mtPaint/commits/master
