# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: yosh64 <yosh64@gmail.com>

pkgname=mtpaint
pkgver=3.49.20
pkgrel=1
pkgdesc='Paint program for creating icons and pixel art'
arch=(x86_64)
url='https://github.com/wjaguar/mtPaint'
license=(GPL3)
makedepends=(git)
depends=(gtk2 giflib openjpeg lcms2)
optdepends=('gifsicle: export GIF files from mtPaint')
source=("git+$url#commit=e55eb7d3d14bc17ea92f5e5f00ecc538dced0aeb") # This commit says: Version 3.49.20 
sha256sums=('SKIP')

build() {
  cd mtPaint

  export CFLAGS="-w $(pkg-config libopenjpeg --cflags) $CFLAGS"
  ./configure --prefix=/usr --mandir=/usr/share/man \
    man intl GIF jpeg tiff cflags lcms2 jp2
  make
}

package() {
  make -C mtPaint DESTDIR="$pkgdir" install
}

# getver: github.com/wjaguar/mtPaint/commits/master
# vim:ts=2 sw=2 et:
