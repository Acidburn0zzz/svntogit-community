# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-about
pkgver=0.11.0
pkgrel=5
pkgdesc="LXQt about dialog."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt")
makedepends=("cmake" "qt5-tools" "git")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"26bd91a1ca559353c2bcb82378918419b8e059a47956c2f89e533b3fac2881b6"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
