# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BSlizr
pkgname=bslizr
pkgver=1.2.4
pkgrel=1
pkgdesc="LV2 audio effect plugin for sequenced slicing of stereo audio input signals"
arch=('x86_64')
url="https://github.com/sjaehn/bslizr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11')
makedepends=('lv2')
checkdepends=('lv2lint' 'sord')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('e08a7fa4792b2944e35884a94e22456ff18655132da11c084ec25dadba34e89c08dc187ecd256831efe514a3dc140db7cc34844f3b8f73d254deb884907ae357')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  LV2_PATH="${PWD}" lv2lint "https://www.jahnichen.de/plugins/lv2/BSlizr" || echo "Known to fail: https://github.com/sjaehn/BSlizr/issues/8"
  sord_validate "${PWD}/${_name}.lv2/"*.ttl || echo "Known to fail: https://github.com/sjaehn/BSlizr/issues/8"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
