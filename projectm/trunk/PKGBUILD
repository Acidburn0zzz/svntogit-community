# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgbase=projectm
pkgname=('projectm' 'libvisual-projectm')
pkgver=2.1.0
pkgrel=1
arch=('x86_64' 'i686')
url='http://projectm.sourceforge.net/'
license=('LGPL')
makedepends=('cmake' 'ftgl' 'glew' 'gtkglext' 'libvisual' 'sdl' 'libxext')
source=("http://downloads.sourceforge.net/$pkgname/projectM-complete-$pkgver-Source.tar.gz")
sha256sums=('513204f033006bd3dcdf8aada196d816d6b7187266ddcbb1594d0285cc9406ee')

build() {
  cd $srcdir/projectM-complete-$pkgver-Source/src
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DINCLUDE-PROJECTM-QT=OFF \
    -DINCLUDE-PROJECTM-PULSEAUDIO=OFF -DINCLUDE-PROJECTM-TEST=OFF
  make
}

package_projectm() {
  pkgdesc='Music visualizer which uses 3D accelerated iterative image based rendering'
  depends=('ftgl' 'glew' 'gtkglext')

  cd $srcdir/projectM-complete-$pkgver-Source/src/libprojectM
  make DESTDIR=$pkgdir install
  install -m644 fonts/Vera.ttf $pkgdir/usr/share/projectM/fonts/Vera.ttf
}

package_libvisual-projectm() {
  pkgdesc='ProjectM XMMS plugin'
  depends=("projectm>=$pkgver" 'libvisual' 'sdl')

  cd $srcdir/projectM-complete-$pkgver-Source/src/projectM-libvisual
  make DESTDIR=$pkgdir install
}

# vim:set ts=2 sw=2 et:
