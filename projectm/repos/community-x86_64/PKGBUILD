# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=projectm
pkgver=2.0.1
pkgrel=3
pkgdesc='A music visualizer which uses 3D accelerated iterative image based rendering.'
arch=('i686' 'x86_64')
url='http://projectm.sourceforge.net/'
license=('LGPL')
depends=('gcc-libs' 'ftgl' 'glew' 'gtkglext')
makedepends=('pkgconfig' 'cmake')
source=("http://downloads.sourceforge.net/${pkgname}/projectM-${pkgver}-Source.tar.gz"
        'libprojectM-2.0.1-pkg-config.patch')
md5sums=('f8bf795878cdbbef54784cf2390b4c89'
         'c271ec8c541eb281f1294287bdbc3488')

build() {
  cd "${srcdir}/projectM-${pkgver}-Source"

  patch -p0 -i ../libprojectM-2.0.1-pkg-config.patch

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/projectM-${pkgver}-Source"
  make DESTDIR="${pkgdir}" install
}
