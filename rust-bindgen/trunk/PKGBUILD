# Maintainer : Felix Yan <felixonmars@archlinux.org>

pkgname=rust-bindgen
pkgver=0.47.0
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs')
makedepends=('rust')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz")
sha512sums=('41e22e50b2ca1218d6004545b00fd25c30ec771e146660a0dfcf98da347e724502c35027e623cb751b94178fd3565fe31e98faf7f25c86b886fb4e6661045731')

build() {
  cd $pkgname-$pkgver
  cargo build --release
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
