# Maintainer : Felix Yan <felixonmars@archlinux.org>

pkgname=rust-bindgen
pkgver=0.44.0
pkgrel=1
pkgdesc='Automatically generates Rust FFI bindings to C (and some C++) libraries'
url='https://github.com/rust-lang/rust-bindgen'
depends=('gcc-libs')
makedepends=('rust')
arch=('x86_64')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rust-lang/rust-bindgen/archive/v$pkgver.tar.gz")
sha512sums=('9d28ce5c9faeb8c3b4936161ce2e636cf8eb9f1becf707971f4b1daa24e52ed209f099c049c7bcf5aaf5de4a0ef9458192f9da0b0431e884cfacf19dcf3eba3c')

build() {
  cd $pkgname-$pkgver
  cargo build --release
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 target/release/bindgen "$pkgdir"/usr/bin/bindgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
