# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=python-neovim
_realname=pynvim
pkgver=0.3.2
pkgrel=3
pkgdesc='Python client for Neovim'
url='https://github.com/neovim/pynvim'
arch=('any')
license=('Apache')
depends=('python' 'python-msgpack' 'python-greenlet')
makedepends=('python-setuptools')
source=("https://github.com/neovim/python-client/archive/${pkgver}.tar.gz")
sha256sums=('0a44c540dd5790acfbc7e411b0ed4fb6c44914d866226ebebf370c24c2723dc2')

prepare() {
  sed -i "s/msgpack-python/msgpack/g" ${_realname}-${pkgver}/setup.py
}

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"
  python setup.py install --optimize=1 --prefix=/usr --root="${pkgdir}" --skip-build
}

# vim:set sw=2 sts=2 et:
