# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=ted
pkgver=2.22
pkgrel=2
pkgdesc="An easy rich text processor (with footnote support)"
arch=('i686' 'x86_64')
url="http://www.nllgg.nl/Ted/"
depends=('bash' 'libtiff' 'libxmu' 'libxpm' 'libpng' 'gtk2')
license=('GPL')
source=(ftp://ftp.nluug.nl/pub/editors/$pkgname/$pkgname-$pkgver.src.tar.gz)
md5sums=('08162c9164dfdde1d46e41ae64df16be')

build() {
  cd "$srcdir/Ted-$pkgver"

  make CONFIGURE_OPTIONS=--with-GTK
  make package CONFIGURE_OPTIONS=--with-GTK


}

package() {
  cd "$srcdir/Ted-$pkgver/tedPackage"
  ./installTed.sh COMMON

  # unpack package
  mkdir -p $pkgdir/usr/share
  cd "$pkgdir/usr/share"
  tar xzf $srcdir/Ted-$pkgver/tedPackage/*.tar.gz
  # move bin/* and share/*
  mv bin ../
  mv share/* ./
  rmdir share
  # move man pages
  cd "$pkgdir/usr/share/Ted/"
  mkdir -p "$pkgdir/usr/share/man/man1"
  mv Ted.1 rtf2pdf.1 rtf2ps.1 ../../share/man/man1/
  # fix shell script and move them to /usr/bin
  sed -i \
    -e "s|usr/local/afm|usr/share/Ted/afm|g" \
    -e "s|usr/local/Ted|usr/share/Ted|g" \
    gsafm.sh \
    config/Ted.ad.sample
  mv *.sh "$pkgdir/usr/bin/"
}
