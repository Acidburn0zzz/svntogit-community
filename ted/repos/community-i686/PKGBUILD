# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=ted
pkgver=2.21
pkgrel=4
pkgdesc="An easy rich text processor (with footnote support)"
arch=('i686' 'x86_64')
url="http://www.nllgg.nl/Ted/"
depends=('bash' 'libtiff' 'libxmu' 'libxpm' 'libpng' 'gtk2')
license=('GPL')
source=(ftp://ftp.nluug.nl/pub/editors/$pkgname/$pkgname-$pkgver.src.tar.gz 
        ted-2.21-libpng15.patch)
md5sums=('34ae855938a3b364eb587cca504ba356'
         '0c339b0fa7aec6b150e07e9027cd360c')

build() {
  cd "$srcdir/Ted-$pkgver"
  sed -i "s|--with-GTK|--with-GTK --prefix=/usr/share|g" Makefile
  patch -p0 -i ../ted-2.21-libpng15.patch
  make compile.shared
  make package.shared
}

package() {
  cd "$srcdir/Ted-$pkgver"
  mkdir -p "$pkgdir/usr/share/"
  cd "$pkgdir/usr/share/"
  tar -zxvf "$srcdir/Ted-$pkgver/tedPackage/ted-$pkgver-linux-$CARCH.tar.gz"
  mv bin ../
  mkdir -p "$pkgdir/usr/share/man/man1"
  cd "$pkgdir/usr/share/Ted/"
  mv Ted.1 rtf2pdf.1 rtf2ps.1 ../../share/man/man1/
  sed -i "s|usr/local/afm|usr/share/Ted/afm|g" gsafm.sh
  mv *.sh "$pkgdir/usr/bin/"
}
