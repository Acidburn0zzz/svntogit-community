# Maintainer:
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgbase=mutagen
pkgname=(python2-mutagen python-mutagen mutagen-tools)
pkgver=1.42.0
pkgrel=2
arch=(any)
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest' 'python-hypothesis' 'python2-hypothesis' 'python-pyflakes' 'python2-pyflakes'
              'python-pycodestyle' 'python2-pycodestyle')
source=("https://github.com/quodlibet/mutagen/releases/download/release-$pkgver/$pkgbase-$pkgver.tar.gz"{,.sig}
        mutagen-indent.patch::"https://github.com/quodlibet/mutagen/commit/0ee86ef9.patch")
sha256sums=('bb61e2456f59a9a4a259fbc08def6d01ba45a42da8eeaa97d00633b0ec5de71c'
            'SKIP'
            '4c02028ea27e150de8f011e809fe96ba726825732233d186c7edb3269fffdce3')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC')

prepare() {
  pushd $pkgbase-$pkgver
    patch -p1 -i ../mutagen-indent.patch # Fix tests
  popd
  cp -r $pkgbase-$pkgver{,-py2}
}

check() {
  cd $pkgbase-$pkgver
  LANG=en_US.UTF8 python setup.py test
  cd ../$pkgbase-$pkgver-py2
  LANG=en_US.UTF8 python2 setup.py test
}

package_python2-mutagen() {
  pkgdesc="An audio metadata tag reader and writer (Python 2 libraries)"
  depends=(python2)
  conflicts=(mutagen)
  provides=(mutagen)
  replaces=(mutagen)
 
  cd $pkgbase-$pkgver-py2
  python2 setup.py install --root="$pkgdir"
  rm -r "$pkgdir"/usr/{bin,share}
}

package_python-mutagen() {
  pkgdesc="An audio metadata tag reader and writer (Python 3 libraries)"
  depends=(python)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"
  rm -r "$pkgdir"/usr/{bin,share}
}

package_mutagen-tools() {
  pkgdesc="An audio metadata tag reader and writer (tools)"
  depends=(python-mutagen)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"
  rm -r "$pkgdir"/usr/lib
}

