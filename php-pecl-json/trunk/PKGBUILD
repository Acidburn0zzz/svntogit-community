# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=php-pecl-json
pkgver=1.2.1
pkgrel=1
pkgdesc="PHP json extension"
arch=('i686' 'x86_64')
url="http://pecl.php.net/package/json"
license=('PHP')
depends=('php>=5.3.2')
backup=('etc/php/conf.d/json.ini')
source=(http://pecl.php.net/get/json-$pkgver.tgz)
md5sums=('d8904d2f004ceec85eeacf524cd25539')

build() {
  cd "$srcdir/json-$pkgver"
  phpize
  ./configure --prefix=/usr
  sed -i 's|function_entry json_functions|zend_function_entry json_functions|' json.c
  make
  echo ';extension=json.so' >json.ini
}

package() {
  cd "$srcdir/json-$pkgver"
  make INSTALL_ROOT="$pkgdir" install
  install -Dm0644 json.ini "$pkgdir/etc/php/conf.d/json.ini"
}
