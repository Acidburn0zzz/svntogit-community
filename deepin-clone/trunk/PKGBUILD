# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=1.1.0
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('b4b8f3e1762b3d63b16545d9a2cff141aa4f9bedd960bc4c247ce348e4d30d9eae44e32d01dd85759febb5b41e938f956cf7caa0c029689687a39f2cbfef6ab8')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' app/{deepin-clone-app.pro,deepin-clone-ionice,deepin-clone-pkexec,com.deepin.pkexec.deepin-clone.policy.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
