# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=1.1.2.1
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('390e8b6c5cb4d3454e76d3f890f5573a196f889d4086b3262c717d95664987ff42f1823ebb25120ac70364a8845202d23fd5112299000b93419878979b62377f')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' app/{deepin-clone-app.pro,deepin-clone-ionice,deepin-clone-pkexec,com.deepin.pkexec.deepin-clone.policy.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
