# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=0.0.8.5
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('d463da4fa91fe78fba176e8e16ccdad67113d6ba07db8bfd20ead60b0581d8fe36818a5c9c3ebb32e822019f3889e75f2971d8e6c42297ef674fff1642dccc30')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' app/{deepin-clone-app.pro,deepin-clone-ionice,deepin-clone-pkexec,com.deepin.pkexec.deepin-clone.policy.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
