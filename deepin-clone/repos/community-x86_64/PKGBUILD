# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-clone
pkgver=1.1.3
pkgrel=1
pkgdesc='Disk and partition backup/restore tool'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-clone"
license=('GPL3')
depends=('deepin-qt5integration' 'partclone' 'e2fsprogs' 'jfsutils' 'xfsprogs')
makedepends=('qt5-tools' 'deepin-gettext-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-clone/archive/$pkgver.tar.gz")
sha512sums=('09e9dc85ca1483e13c89acdb41f7a52164ad1fee48b4922cc4065d58fe0c94a1229ab42bf559a081d4e9844458df9500e9ba9b045fa09f7b0a9cf4965adfdc45')

prepare() {
  cd deepin-clone-$pkgver
  sed -i 's|/usr/sbin|/usr/bin|' app/{deepin-clone-app.pro,deepin-clone-ionice,deepin-clone-pkexec,com.deepin.pkexec.deepin-clone.policy.tmp}
}

build() {
  cd deepin-clone-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-clone-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
