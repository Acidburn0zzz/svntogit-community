# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyproj
pkgname=('python-pyproj' 'python2-pyproj')
pkgver=1.9.0
pkgrel=1
pkgdesc="Python interfaces to PROJ.4 library"
arch=('i686' 'x86_64')
url="http://code.google.com/p/pyproj"
license=('custom')
source=(http://pyproj.googlecode.com/files/pyproj-${pkgver}.tar.gz)
makedepends=('python' 'python2')
sha1sums=('9a49ff24dbf88d725adddd1ba255609b558e6b03')

build() {
   cd "${srcdir}"

   cp -a ${pkgname}-${pkgver}{,-python2}

   # Build python 3 module
   cd ${pkgname}-${pkgver}
   python setup.py build

   # Build python 2 module
   cd ../${pkgname}-${pkgver}-python2
   python2 setup.py build
}

package_python-pyproj() {
   depends=('proj' 'python>=3.2')

   cd "${srcdir}/${pkgname}-${pkgver}"
   python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
   
   install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
   install -m644 LICENSE_proj4 ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyproj() {
   depends=('proj' 'python2>=2.7')
   replaces=('pyproj')

   cd "${srcdir}/${pkgname}-${pkgver}-python2"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
   
   install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
   install -m644 LICENSE_proj4 ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
