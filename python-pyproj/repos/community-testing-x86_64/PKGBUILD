# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pyproj
pkgname=('python-pyproj' 'python2-pyproj')
pkgver=2.2.0
pkgrel=1
pkgdesc="Python interfaces to PROJ.4 library"
arch=('x86_64')
url="https://github.com/pyproj4/pyproj"
license=('custom')
makedepends=('python-setuptools' 'python2-setuptools' 'proj' 'cython' 'cython2')
source=($pkgname-$pkgver.tar.gz::https://github.com/pyproj4/pyproj/archive/v${pkgver}rel.tar.gz)
sha256sums=('71ff90b9c2baf6088f54343963b82662758ccfe95c4524d0524c9599a3c7dca5')

prepare() {
  cd "${srcdir}"

# Remove RPATH
  sed -i '/runtime_library_dirs=/d' pyproj-${pkgver}rel/setup.py

  cp -a pyproj-${pkgver}rel{,-python2}
}

build() {
  cd "${srcdir}"

  export PROJ_DIR=/usr

# Build python 3 module
  cd pyproj-${pkgver}rel
  python setup.py build

# Build python 2 module
  cd ../pyproj-${pkgver}rel-python2
  python2 setup.py build
}

package_python-pyproj() {
  depends=('proj' 'python')

  cd "${srcdir}"/pyproj-${pkgver}rel
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

package_python2-pyproj() {
  depends=('proj' 'python2')

  cd "${srcdir}"/pyproj-${pkgver}rel-python2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE_proj4 "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
