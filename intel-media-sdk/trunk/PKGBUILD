# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgbase=intel-media-sdk
pkgname=('intel-media-sdk' 'libmfx')
pkgver=19.4.0
pkgrel=4
pkgdesc='API to access hardware-accelerated video on Intel Gen graphics hardware platforms'
arch=('x86_64')
url='https://software.intel.com/en-us/media-sdk/'
license=('MIT')
makedepends=('libdrm' 'libva' 'wayland' 'intel-media-driver'
             'cmake' 'libpciaccess' 'libx11' 'libxcb' 'python'
             'opencl-headers' 'ocl-icd' 'intel-compute-runtime')
source=("https://github.com/Intel-Media-SDK/MediaSDK/archive/intel-mediasdk-${pkgver}.tar.gz")
sha256sums=('289a40ffe73dc291461d97edf452bdc622f76e52666483fd1eb4f131d2921cd1')

build() {
    cmake -B build -S "MediaSDK-intel-mediasdk-${pkgver}" \
        -DBUILD_ALL:BOOL='ON' \
        -DBUILD_TOOLS:BOOL='ON' \
        -DCMAKE_BUILD_TYPE:STRING='None' \
        -DCMAKE_INSTALL_PREFIX:PATH='/usr' \
        -DENABLE_ITT:BOOL='OFF' \
        -DENABLE_OPENCL:BOOL='ON' \
        -DENABLE_WAYLAND:BOOL='ON' \
        -DENABLE_X11_DRI3:BOOL='ON' \
        -Wno-dev
    make -C build
}

check() {
    make -C build test
}

package_intel-media-sdk() {
    depends=('gcc-libs' 'libdrm' 'libva' 'wayland' "libmfx=${pkgver}" 'intel-media-driver')
    optdepends=('ocl-icd: for rotate_opencl plugin'
                'intel-compute-runtime: for rotate_opencl plugin')
    
    make -C build DESTDIR="$pkgdir" install
    
    ln -s ../share/mfx/samples/libcttmetrics.so "${pkgdir}/usr/lib/libcttmetrics.so"
    
    # remove core component libmfx
    [ -d 'libmfx' ] && rm -rf libmfx
    mkdir -p libmfx/lib/pkgconfig
    mv "${pkgdir}/usr/include" libmfx
    mv "${pkgdir}/usr/lib/libmfx.so"* libmfx/lib
    mv "${pkgdir}/usr/lib/pkgconfig/"{,lib}mfx.pc libmfx/lib/pkgconfig
    
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_libmfx() {
    pkgdesc='Intel Media SDK dispatcher library'
    depends=('gcc-libs')
    
    # install core component libmfx into a separated package
    mkdir -p "${pkgdir}/usr"
    mv libmfx/{include,lib} "${pkgdir}/usr"
    
    install -D -m644 "MediaSDK-intel-mediasdk-${pkgver}/LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
