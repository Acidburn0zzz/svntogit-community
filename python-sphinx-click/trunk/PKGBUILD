# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=2.6.0
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=('any')
url="https://github.com/click-contrib/sphinx-click"
license=('MIT')
depends=('python-pbr' 'python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-click')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f9dec3c4c274dc6e83b03a527449777ab84b3b04131d2740a24350c4641eea37275d0c32733a9c9ccbba7999abfa0c65b57a9ecfc17bcba85ad0f8e4b136f01e')
b2sums=('7cefe2bb1e11f22e24ca7940a6c26c62e327bacdea810dba39adbca04b484d685d6715021c1ecbb040dcd31f66bb32933006fe97afa9be826c566cc42ffe2123')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}/docs/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
