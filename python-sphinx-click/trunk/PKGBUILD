# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=2.7.1
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=('any')
url="https://github.com/click-contrib/sphinx-click"
license=('MIT')
depends=('python-pbr' 'python-sphinx')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-click')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('55749ab1f48569225558eda243c77991623418a2185fbda266d75d3a52f4551fb619b3024bb368d8332e55c0755da7a9da5b5da97158c9d8eefec3a2007f201e')
b2sums=('4fe15f3e8ad413f36a9aa6125f973fdb8d150a0996a373bc877ccec1c40132d81235abaf28a1ab90e647cbdb6a9571d782078e1de19c98e76818c6a957490bec')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}/docs/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
