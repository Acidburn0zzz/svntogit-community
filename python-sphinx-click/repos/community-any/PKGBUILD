# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=4.0.0
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(python-pbr python-sphinx)
makedepends=(python-build python-installer python-setuptools python-wheel)
checkdepends=(python-pytest python-click)
source=(https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz)
sha512sums=('19e20037d086a050f861e61185d46cd85e64ff76308e7bff541441d7b5c4b8925f86822675e08e2963ec9d27a4625ed98052061e591bd759e68bb810927d1521')
b2sums=('b461645d97d4552d2e3b591f0bc9853b7b971c7a67c72cd20abb3164a65e037fa94e9abb1c5561269ae64e99f16e0770e82ceb073a759e4fc383b812e42a1099')

build() {
  cd $_name-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd $_name-$pkgver
  pytest -vv
}

package() {
  cd $_name-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 docs/*.rst -t "$pkgdir/usr/share/doc/$pkgname/docs/"
  install -vDm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
