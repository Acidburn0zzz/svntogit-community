# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-click
pkgname=python-sphinx-click
pkgver=3.0.3
pkgrel=1
pkgdesc="Sphinx plugin to automatically document click-based applications"
arch=(any)
url="https://github.com/click-contrib/sphinx-click"
license=(MIT)
depends=(python-pbr python-sphinx)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-click)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('6792eedb21b51161df33ee0e30d704e8d5efc3b1502765e58e0a863d3160d0b95e69cc3c257d83966c9632d5e8ebe37c445d25673d1f59d91184a0026bded392')
b2sums=('3551662421b8b6625915894e6a43a454f1d5ef235ed3911bb0c14e9cfc29ac46ae16a028577534d6a98173558d8ae19b1bb35b4ca1f0f953e8ee9e018e4ec4da')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,ChangeLog,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 docs/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}/docs/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
