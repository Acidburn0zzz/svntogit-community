# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Philippe Proulx <pproulx@efficios.com>
# Contributor: Manuel Mendez <mmendez534 at gmail dot com>

pkgbase='lttng-ust'
pkgname=('lttng-ust' 'python-lttngust' 'python2-lttngust')
pkgver=2.10.3
pkgrel=1
arch=('x86_64')
url="https://lttng.org/"
license=('LGPL2.1' 'GPL2' 'MIT')
makedepends=('asciidoc' 'xmlto' 'libxml2' 'python' 'python2' 'liburcu>=0.7.2' 'util-linux')
source=("https://lttng.org/files/${pkgbase}/${pkgbase}-${pkgver}.tar.bz2"{,.asc})
sha512sums=('6f646de3a12dbad096014f3069c3e702fb54b824b78770e777f6f877c76ffc48fae863b10c432bff1bba29caafdb5b76cb2b9cb88eb7340d5121300d2c0ff65d'
            'SKIP')
validpgpkeys=('2A0B4ED915F2D3FA45F5B16217280A9781186ACF')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # build lttng-ust
    ./configure \
        --prefix='/usr' \
        --enable-man-pages
    make
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make check
}

package_lttng-ust() {
    pkgdesc="LTTng user space tracing libraries for LTTng"
    depends=('liburcu>=0.7.2' 'util-linux' 'python')
    optdepends=('babeltrace: trace viewer')
    cd "${srcdir}/${pkgbase}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

_python_package() {
    # build with $1 python interpreter
    cd "${srcdir}/${pkgbase}-${pkgver}/python-lttngust"
    make DESTDIR="${pkgdir}" PYTHON="/usr/bin/${1}" install

    # add license to the respective packages
    cd "${srcdir}/${pkgbase}-${pkgver}"
    install -D -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python-lttngust() {
    pkgdesc='LTTng-UST Python Bindings'
    depends=('lttng-ust' 'python')
    _python_package python
}

package_python2-lttngust() {
    pkgdesc='LTTng-UST Python2 Bindings'
    depends=('lttng-ust' 'python2')
    _python_package python2
}
