# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.2.0
pkgrel=1
pkgdesc="A forward proxy with several protocols support"
arch=('i686' 'x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz"
        glider@.service)
md5sums=('e03f93d8e93c1d2eb1456e84a1f7b212'
         '2d9bc2bd97a4dbf7b34076ced8d2877f')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 glider.conf.sample "$pkgdir"/etc/glider/glider.conf.sample
  install -Dm644 "$srcdir"/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
