# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.4.2
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('bc8aaae08455bcc2e60052be1523ddc44a4022b2578cc58656b1aa30fdad4e3f295496ce81a07a97d7556ae9846660fefc391115c3fcf15cb30610fde815b435')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
