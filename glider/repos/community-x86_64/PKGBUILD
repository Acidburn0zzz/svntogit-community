# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.6.11
pkgrel=2
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('c6488dd309d3df5c578f8fe54c3d4e79e1ef437d5d49fd9149e97275e2199f7586092194e11399a70262c8108d24b0fbae475c3ec1c86c4ceb1657dfe051c8b6')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core github.com/sun8911879/shadowsocksR
}

build() {
  cd glider-$pkgver
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}"

}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
