# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.8.3
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('f0d9e0eb710bd550c5b03141c1338ae8d9d63a011233ede075a64a1a2222ce642e4197e95200b709ded195c8334d483b9557f1aa2f38609b1765bcab9f1eff24')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  go get github.com/nadoo/conflag github.com/nadoo/go-shadowsocks2
}

build() {
  cd glider-$pkgver
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}"

}

package() {
  cd glider-$pkgver
  install -Dm755 glider "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
