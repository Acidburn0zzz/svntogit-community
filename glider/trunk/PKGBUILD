# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.6.0
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('351b4a77172527659e29be6092a10daa3fcb5e712b19a7536bd969fe195c2e85a4bca410772880318c2a7868105a6d078a04a9c4fa5946c436edfd0310701334')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  go get github.com/nadoo/conflag github.com/shadowsocks/go-shadowsocks2/core github.com/sun8911879/shadowsocksR
}

build() {
  cd glider-$pkgver
  go build
}

package() {
  cd glider-$pkgver
  install -Dm755 glider-$pkgver "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
