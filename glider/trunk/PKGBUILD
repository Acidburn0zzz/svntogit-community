# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=glider
pkgver=0.8.2
pkgrel=1
pkgdesc="A forward proxy with multiple protocols support"
arch=('x86_64')
url="https://github.com/nadoo/glider"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nadoo/glider/archive/v$pkgver.tar.gz")
sha512sums=('846c61eb55e0a0d726fcd718b6fefe33c977b53c98d710ac39e5a338455058053526452951f61ce4b5478371ad5bc7b973dbb21c306f1312ebb34ef7c6ae4113')

prepare() {
  mkdir .gopath
  export GOPATH="$srcdir"/.gopath
  mkdir -p "$GOPATH"/src/github.com/nadoo
  ln -sf "$srcdir"/glider-$pkgver "$GOPATH"/src/github.com/nadoo/glider

  echo 'replace gitlab.com/yawning/chacha20.git => github.com/Yawning/chacha20 latest' >> "$srcdir"/glider-$pkgver/go.mod

  go get github.com/nadoo/conflag github.com/nadoo/go-shadowsocks2
}

build() {
  cd glider-$pkgver
  go build \
    -gcflags "all=-trimpath=${PWD}" \
    -asmflags "all=-trimpath=${PWD}" \
    -ldflags "-extldflags ${LDFLAGS}"

}

package() {
  cd glider-$pkgver
  install -Dm755 glider "$pkgdir"/usr/bin/glider
  install -Dm644 config/glider.conf.example "$pkgdir"/etc/glider/glider.conf.example
  install -Dm644 systemd/glider@.service "$pkgdir"/usr/lib/systemd/system/glider@.service
}
