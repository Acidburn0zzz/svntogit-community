# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Neil Santos <nsantos16+aur@gmail.com>

_name=wsgi_intercept
pkgname=python-wsgi-intercept
pkgver=1.9.1
pkgrel=1
pkgdesc='Intercept socket connection to wsgi applications for testing.'
arch=('any')
url="https://github.com/cdent/wsgi-intercept"
license=('MIT')
depends=('python-six')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-httplib2' 'python-pytest' 'python-requests'
'python-urllib3')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('243a89900992c4643477599a232b87a639857172ac59626f3489e2b8fa596292ee88e0342faa2cbb5ae156d7379c6350612008fe3315e412d420e7cdc0a5289f')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
  cd docs
  make man
}

check() {
  cd "${_name}-${pkgver}"
  py.test
}

package_python-wsgi-intercept() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README -t "${pkgdir}/usr/share/${pkgname}/"
  install -vDm 644 docs/_build/man/${_name}.1 \
    -t "${pkgdir}/usr/share/man/man1/"
}
