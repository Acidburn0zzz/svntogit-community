# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alois Nespor <alois.nespor@gmail.com>

pkgname=qstardict
pkgver=1.0
pkgrel=1
pkgdesc="Qt4 clone of StarDict with full support of StarDict dictionaries"
arch=('i686' 'x86_64')
url="http://qstardict.ylsoftware.com/index.php"
license=('GPL2')
depends=('qt')
optdepends=('festival-awb-arctic: for pronouncing words, Scottish English male speaker'
            'festival-don: for pronouncing words, British English RP male speaker'
            'festival-kallpc16k: for pronouncing words, British English RP male speaker'
            'festival-rablpc16k: for pronouncing words, British English RP male speaker')
provides=('stardict')
changelog=$pkgname.changelog
source=(http://qstardict.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
sha256sums=('9cb20f8829be0983176bc52728d3701b0862522741cac1a9967222c86aaaa9fb')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  qmake ENABLED_PLUGINS="stardict web swac"
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make INSTALL_ROOT=${pkgdir} install
}
