# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=filesystem_spec
pkgname=python-fsspec
pkgver=0.7.4
pkgrel=2
pkgdesc="Specification that python filesystems should adhere to."
arch=(any)
url="https://github.com/intake/filesystem_spec"
license=(BSD)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(
    python-pytest
    python-numpy
)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('35aa9775ed20b35f82de6d38fa28b6cd53f9e56e8dad727c0192f33c79b72959')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  # https://github.com/intake/filesystem_spec/issues/253
  sed -i '/assert getpass.getuser() in stripped/d' fsspec/implementations/tests/test_local.py
  pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build
  # Install license file
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
