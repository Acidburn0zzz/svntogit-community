# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-randomly
pkgver=3.1.0
pkgrel=1
pkgdesc='Pytest plugin to randomly order tests and control random.seed'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-randomly'
depends=('python-pytest')
checkdepends=('python-entrypoints' 'python-faker' 'python-numpy' 'python-factory-boy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-randomly/archive/$pkgver.tar.gz")
sha512sums=('dc5272f343a660125241d20cfe6cd98aff59749b491b5f86f9acddfe2c464d5f03a378365e30e385db043131ea03f47d620e8370611e9c5157eae7295fe59850')

prepare() {
  cd pytest-randomly-$pkgver
  sed -i 's/pytest.py: error: argument --randomly-seed/pytest: error: argument --randomly-seed/' test_pytest_randomly.py
}

build() {
  cd pytest-randomly-$pkgver
  python setup.py build
}

check() {
  cd pytest-randomly-$pkgver
  python setup.py egg_info
  pytest
}

package() {
  cd pytest-randomly-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
