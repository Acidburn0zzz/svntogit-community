# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-semver
pkgver=2.11.0
pkgrel=1
pkgdesc="Python helper for Semantic Versioning"
url="https://github.com/k-bx/python-semver"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/k-bx/python-semver/archive/$pkgver.tar.gz")
sha512sums=('833073fe4c245666c9d5298bb82718be82b891488b73968c656899b002296758db9d43fc58187aeed59eb0c235b0ef1837a58df75ffa5451f8808467f8b2f905')

prepare() {
  sed -i '/tests_require/d' python-semver-$pkgver/setup.py
}

build() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py pytest
}

package() {
  cd python-semver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
