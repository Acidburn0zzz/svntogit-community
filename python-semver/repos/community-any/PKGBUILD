# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-semver
pkgver=2.9.0
pkgrel=1
pkgdesc="Python helper for Semantic Versioning"
url="https://github.com/k-bx/python-semver"
license=('BSD')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov')
source=("$pkgname-$pkgver.tar.gz::https://github.com/k-bx/python-semver/archive/$pkgver.tar.gz")
sha512sums=('5ae9a4a66474fc82cf7e4612796a6c0e0280ad9969ff8c509e99cb266154f39a2c03c68f6167f8e17fda1ac16ce17a838e63d81bc2e78debfd07adbb7cbc098d')

prepare() {
  sed -i '/tests_require/d' python-semver-$pkgver/setup.py
}

build() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/python-semver-$pkgver
  python setup.py pytest
}

package() {
  cd python-semver-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
