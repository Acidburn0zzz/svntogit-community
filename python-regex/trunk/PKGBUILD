# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Nikola MilinkoviÄ‡ <nikmil@gmail.com>
# Submitter: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

_pkgbase=regex
pkgname=python-regex
pkgver=2021.3.17
pkgrel=1
pkgdesc="Alternative python regular expression module."
arch=('x86_64')
url="https://bitbucket.org/mrabarnett/mrab-regex"
license=('Python' 'Apache')
depends=('python')
makedepends=('python-setuptools')
options=(!emptydirs)
source=("https://files.pythonhosted.org/packages/source/r/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
sha256sums=('4b8a1fb724904139149a43e172850f35aa6ea97fb0545244dc0b805e0154ed68')
b2sums=('9388038519cc4c11a332dd1fc9b4f742af3f69453890e148971105bb4a5e73f562f7eaa4e2533548066e87aade2f64b507a6e65cd4b0070a26733fa2675190ea')

build() {
  cd "regex-${pkgver}"

  python setup.py build
}

check() {
  cd "regex-${pkgver}"

  cd build/lib.linux-${CARCH}-3*/
  python -m unittest regex/test_regex.py
}

package() {
  cd "regex-${pkgver}"

  python setup.py install --root="${pkgdir}/" --optimize=1 --skip-build
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
