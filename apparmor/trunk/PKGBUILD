# Maintainer: David Runge <dave@sleepmap.de>
pkgname=apparmor
pkgver=2.13.1
pkgrel=1
pkgdesc="Mandatory Access Control (MAC) using Linux Security Module (LSM)"
arch=('x86_64')
url="https://gitlab.com/apparmor/apparmor"
license=('GPL')
depends=('audit' 'pam' 'python')
makedepends=('swig' 'chrpath' 'ruby')
checkdepends=('dejagnu' 'python-pyflakes')
optdepends=('perl: perl bindings'
            'ruby: ruby bindings')
provides=('libapparmor')
replaces=('apparmor-libapparmor' 'apparmor-pam' 'apparmor-parser'
'apparmor-profiles' 'apparmor-utils' 'apparmor-vim')
backup=('etc/apparmor/easyprof.conf'
        'etc/apparmor/logprof.conf'
        'etc/apparmor/notify.conf'
        'etc/apparmor/parser.conf'
        'etc/apparmor/subdomain.conf'
        'etc/apparmor/severity.db')
source=("https://launchpad.net/${pkgname}/${pkgver%.[0-9]}/${pkgver}/+download/${pkgname}-${pkgver%.[0-9]}.tar.gz"{,.asc})
sha512sums=('f98914713153d4c823a3ea7e96291cc4528bf7c8d3a139286ae0ecd806613e9c34b0ad81f2b258df2193cf6f3157d3252ef72d32d339427948a3fd8ba5651827'
            'SKIP')
# AppArmor Development Team (AppArmor signing key) <apparmor@lists.ubuntu.com>
validpgpkeys=('3ECDCBA5FB34D254961CC53F6689E64E3D3664BB')
_core_perl="/usr/bin/core_perl"

prepare() {
  mv -v "${pkgname}-${pkgver%.[0-9]}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix PYTHONPATH for aa-logprof based check
  local _py3_ver=$(python --version | cut -d " " -f2)
  local path="${PWD}/libraries/libapparmor/swig/python/build/lib.linux-${CARCH}-${_py3_ver%"."*}/LibAppArmor"
  local libs="${PWD}/libraries/libapparmor/src/.libs"
  sed -e "/PYTHONPATH/ s|utils\ |utils:$path\ LD_LIBRARY_PATH=$libs\ |" \
      -i profiles/Makefile
  # fix sandbox.py (utils check)
  sed -e '722i\ \ \ \ \ \ \ \ print(e)' -i utils/apparmor/sandbox.py
  # remove test-aa.py (utils check), producing too many fails
  rm -v utils/test/test-aa.py
  # fix bin directory for parser install target
  # fix lib dir location
  # fix missing executable bit on apparmor.systemd script
  sed -e 's/sbin/usr\/bin/g' \
      -e 's/\}\/lib\/apparmor/\}\/usr\/lib\/apparmor/' \
      -e 's/644 apparmor.systemd/755 apparmor.systemd/' \
      -i parser/Makefile
  # fix default installation path for vim syntax file
  sed -e 's/share\/apparmor/share\/vim\/vimfiles\/syntax/' \
      -i utils/vim/Makefile
  cd libraries/libapparmor/
  autoreconf -vfi
}

build() {
  # export required perl executable locations
  export MAKEFLAGS+=" POD2MAN=${_core_perl}/pod2man"
  export MAKEFLAGS+=" POD2HTML=${_core_perl}/pod2html"
  export MAKEFLAGS+=" PODCHECKER=${_core_perl}/podchecker"
  export MAKEFLAGS+=" PROVE=${_core_perl}/prove"
  cd "${pkgname}-${pkgver}/libraries/libapparmor/"
  ./configure --prefix=/usr \
              --sbindir=/usr/bin \
              --with-perl \
              --with-python \
              --with-ruby
  make
  cd ../..
  make -C binutils
  make -C parser
  make -C profiles
  make -C utils
  make -C changehat/pam_apparmor
  make -C utils/vim
}

check() {
  cd "$pkgname-$pkgver"
  make -C libraries/libapparmor check
  make -C binutils check
# parser checks don't run on non-apparmor enabled kernel
#  make -C parser check
# profiles check need to be run on a system with aa installed?
#  make -C profiles check
  make -C utils check
}

package() {
  cd "$pkgname-$pkgver"
  make -C libraries/libapparmor DESTDIR="${pkgdir}" install
  make -C changehat/pam_apparmor DESTDIR="${pkgdir}/usr" install
  make -C binutils DESTDIR="${pkgdir}" install
  make -C parser DESTDIR="${pkgdir}" install
  make -C parser DESTDIR="${pkgdir}" install-systemd
  make -C profiles DESTDIR="${pkgdir}" install
  make -C utils DESTDIR="${pkgdir}" BINDIR="${pkgdir}/usr/bin" install
  # strip perl library and remove empty rpath
  find "${pkgdir}/usr/lib/perl5/" \
    -type f -iname "*.so" \
    -exec strip --strip-unneeded {} \; \
    -exec chrpath -d {} \;
  # removing empty core_perl directory
  rm -rv "${pkgdir}"/usr/lib/perl5/*/core_perl
  # move ruby bindings to vendor_ruby
  mv -v "${pkgdir}/usr/lib/ruby/site_ruby/" \
    "${pkgdir}/usr/lib/ruby/vendor_ruby/"
  # adding files below /etc/apparmor.d to backup array
  cd "${pkgdir}"
  # tricking extract_function_variable() in makepkg into not detecting the
  # backup array modification
  [[ /usr/bin/true ]] && backup=( ${backup[@]} $(find "etc/${pkgname}.d/" -type f) )
}
