# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.10409
pkgrel=2
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses spirv-tools zlib)
makedepends=(git cmake clang lld llvm python libunwind spirv-headers vc-intrinsics)
options=(!emptydirs !lto)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-fix-lld-prebuilt-and-limits.patch::${url}/commit/4369c970d4e02258b3c53e854faaa34197124a33.patch
        ${pkgname}-fix-missing-limits.patch::${url}/pull/220.patch
        ${pkgname}-allow-external-vc-intrinsics.patch::${url}/commit/35c947c2631ef4948532eb65c4a62434a580dd3d.patch
        ${pkgname}-allow-external-spirv-tools.patch::${url}/commit/d790a8f8571c51aeccac1a320b4fcf7ed71a2a26.patch
        ${pkgname}-allow-external-spirv-tools-fix.patch::${url}/commit/86a854d3aa529e92dc5f5f9f45c0f2ca174d1c49.patch
        ${pkgname}-allow-external-spirv-tools-fix-p2.patch::${url}/pull/234.patch)
sha256sums=('233992605e1fd7e337e1457559ee6648a822f4b6e5c4894c5b11638d41f7e730'
            'd56f8537ffb25f67e29ae0fbd018247f0b7735c9ea68b7347a0dad3a7f182d5a'
            '4505488fa483446ff50d041609fca63762e5f82711ada1321a94f97165115c88'
            'fea59a67ebba1debed6d4948139184ba8677989c1b966794ac60161936846153'
            '714b452501cf2ca798f2bc915caf20225bb8cb3fdd63fb29810b69e712b0df1f'
            '6a124e2c11c88607a02d9aa16e7efda9637a0fb67bd7d189833e44edac4bfa59'
            '4a3054d4938b083b8ffefd45bc1f8764fcb685516c7c10622607284e768fdaa0')

prepare() {
  cd ${pkgname}-igc-${pkgver}
  patch -p1 < ../${pkgname}-fix-lld-prebuilt-and-limits.patch
  patch -p1 < ../${pkgname}-fix-missing-limits.patch
  patch -p1 < ../${pkgname}-allow-external-vc-intrinsics.patch
  patch -p1 < ../${pkgname}-allow-external-spirv-tools.patch
  patch -p1 < ../${pkgname}-allow-external-spirv-tools-fix.patch
  patch -p1 < ../${pkgname}-allow-external-spirv-tools-fix-p2.patch
}

build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='13.0.1' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__USE_PREINSTALLED_SPRIV_HEADERS=ON \
    -DIGC_OPTION__SPIRV_TOOLS_MODE=Prebuilds \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DIGC_OPTION__VC_INTRINSICS_MODE=Prebuilds \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
