# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.10409
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang ncurses zlib)
makedepends=(git cmake clang lld llvm python libunwind vc-intrinsics)
options=(!emptydirs !lto)
source=(${url}/archive/igc-${pkgver}.tar.gz
        ${pkgname}-fix-lld-prebuilt-and-limits.patch::${url}/commit/4369c970d4e02258b3c53e854faaa34197124a33.patch
        ${pkgname}-fix-missing-limits.patch::${url}/pull/220.patch
        git+https://github.com/intel/vc-intrinsics.git#commit=a7270625195501019507017aab17e50f5dac6466
        git+https://github.com/KhronosGroup/SPIRV-Tools.git#commit=b1877de5cd776117050bd42f08d04b52bce16099
        git+https://github.com/KhronosGroup/SPIRV-Headers.git#commit=b42ba6d92faf6b4938e6f22ddd186dbdacc98d78)
sha256sums=('233992605e1fd7e337e1457559ee6648a822f4b6e5c4894c5b11638d41f7e730'
            'd56f8537ffb25f67e29ae0fbd018247f0b7735c9ea68b7347a0dad3a7f182d5a'
            '4505488fa483446ff50d041609fca63762e5f82711ada1321a94f97165115c88'
            'SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd ${pkgname}-igc-${pkgver}
  patch -p1 < ../${pkgname}-fix-lld-prebuilt-and-limits.patch
  patch -p1 < ../${pkgname}-fix-missing-limits.patch
}

build() {
  cmake -B build -S ${pkgname}-igc-${pkgver} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
    -DIGC_OPTION__CLANG_MODE=Prebuilds \
    -DIGC_OPTION__LLD_MODE=Prebuilds \
    -DIGC_OPTION__LLVM_PREFERRED_VERSION='13.0.1' \
    -DIGC_OPTION__LLVM_MODE=Prebuilds \
    -DIGC_OPTION__LINK_KHRONOS_SPIRV_TRANSLATOR=ON \
    -DIGC_OPTION__SPIRV_TRANSLATOR_MODE=Prebuilds \
    -DVC_INTRINSICS_SRC="${srcdir}/vc-intrinsics" \
    -DINSTALL_GENX_IR=ON \
    -Wno-dev
  make -C build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
  mv "${pkgdir}"/usr/lib/igc/NOTICES.txt "${pkgdir}"/usr/share/licenses/${pkgname}
}
