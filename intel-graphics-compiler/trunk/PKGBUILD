# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.2805
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang)
makedepends=(cmake clang llvm zlib python)
options=(!emptydirs)
source=("${url}/archive/igc-${pkgver}.tar.gz")
sha256sums=('55320302fa6e09d415ff10b1ab2231a547b2d54a2ee32cea756fe6261e3497a5')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='9.0.0' \
        -Wno-dev
    make
    
    # License
    sed -n '2,20p' IGC/AdaptorOCL/igc.opencl.h > LICENSE # create file
    sed -i '1,22s/^.\{,3\}//' LICENSE # erase C comments
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m755 build/IGC/Release/Tools/GenX_IR -t "${pkgdir}"/usr/bin
    install -D -m644 build/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
