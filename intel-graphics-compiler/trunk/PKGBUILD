# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.3864
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang)
makedepends=(cmake clang llvm zlib python)
options=(!emptydirs)
source=("${url}/archive/igc-${pkgver}.tar.gz"
        "010-igc-fix-build.patch")
sha256sums=('ec4595c56e0ed7287c1847b3ea07b0053873b20b4d44a3fa23f8d8a63919242d'
            'ce2df5488a57a84e33a1a9eae8d8198b6b21c589e2a3d9deabae141e34a095aa')

prepare() {
    patch -d ${pkgname}-igc-${pkgver} -Np1 -i "${srcdir}"/010-igc-fix-build.patch
}

build() {
    cmake -B build -S ${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='10.0.0' \
        -DINSTALL_GENX_IR=ON \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="${pkgdir}" install
    install -D -m644 ${pkgname}-igc-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
