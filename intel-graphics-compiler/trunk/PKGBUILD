# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=intel-graphics-compiler
epoch=1
pkgver=1.0.2652
pkgrel=1
pkgdesc="Intel Graphics Compiler for OpenCL"
arch=(x86_64)
url="https://github.com/intel/intel-graphics-compiler"
license=(MIT)
depends=(llvm-libs intel-opencl-clang)
makedepends=(cmake clang llvm zlib python)
options=(!emptydirs)
source=("${url}/archive/igc-${pkgver}.tar.gz"
        igc-${pkgver}-build-fix.patch::"${url}/commit/028414b376d12d7d6fbb4939bca2a31a02b6a18f.patch")
sha256sums=('32aa20abe6dbc66dbeffc86aab0db96420b12eb93aec28ad2c98da9f93c1189f'
            'f3ded012337547f730a162596c701f902ec156c69f1949f7ae0f9cd0d6b2e5a7')

prepare() {
    mkdir -p build
    
    # fix build error
    # https://github.com/intel/intel-graphics-compiler/issues/112
    patch -d ${pkgname}-igc-${pkgver} -Np1 -i ../igc-${pkgver}-build-fix.patch
}

build() {
    cd build
    cmake ../${pkgname}-igc-${pkgver} \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DIGC_OPTION__ARCHITECTURE_TARGET='Linux64' \
        -DIGC_PREFERRED_LLVM_VERSION='9.0.0' \
        -Wno-dev
    make
    
    # License
    sed -n '2,20p' IGC/AdaptorOCL/igc.opencl.h > LICENSE # create file
    sed -i '1,22s/^.\{,3\}//' LICENSE # erase C comments
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
