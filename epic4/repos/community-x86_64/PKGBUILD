# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Chris Brannon <cmbrannon (at) cox.net>

pkgname=epic4
pkgver=2.10.1
pkgrel=3
pkgdesc="It's a new direction in ircII development"
arch=('i686' 'x86_64')
url="http://www.epicsol.org/"
license=('custom')
depends=('openssl' 'ncurses' 'tcl' 'perl')
source=(ftp://ftp.epicsol.org/pub/epic/EPIC4-PRODUCTION/$pkgname-$pkgver.tar.gz)
md5sums=('6f08a04494d0f5543b5f8f3c1ac56d11')

build() {
 cd ${srcdir}/$pkgname-$pkgver

 ./configure --prefix=/usr \
       --mandir=/usr/share/man \
       --with-ssl \
       --with-ipv6 \
       --with-perl \
       --with-tcl

 make
}

package() {
 cd ${srcdir}/$pkgname-$pkgver

 make prefix=${pkgdir}/usr install

 install -D -m 644 COPYRIGHT ${pkgdir}/usr/share/licenses/$pkgname/COPYRIGHT
 mkdir -p ${pkgdir}/usr/lib/$pkgname
 mv ${pkgdir}/usr/libexec/wserv4 $pkgdir/usr/lib/$pkgname
 rm -rf ${pkgdir}/usr/libexec
}

