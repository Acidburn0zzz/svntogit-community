# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbconvert
_name=nbconvert
pkgver=5.0.0
pkgrel=1
pkgdesc="A language-agnostic web-based interactive shell/notebook server."
arch=('any')
url="https://pypi.python.org/pypi/nbconvert"
license=('BSD')
depends=('jupyter' 'python-pygments' 'python-mistune'
         'python-jinja' 'python-entrypoints' 'python-traitlets')
# really depends on jupyter-client, not jupyter
# really depends on jupyter-core, not jupyter
# really depends on jupyter-nbformat, not jupyter
makedepends=('python-setuptools')
optdepends=('pandoc: non-html conversion output')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/nbconvert/archive/$pkgver.tar.gz")
md5sums=('ce3771220c3abd323347a4fe1be08a68')

package() {
  cd "$srcdir/nbconvert-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0

  install -Dm644 "COPYING.md" "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}
