# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbconvert
_name=nbconvert
pkgver=6.4.5
pkgrel=5
pkgdesc="Jupyter Notebook Conversion"
arch=('any')
url="https://pypi.python.org/pypi/nbconvert"
license=('BSD')
depends=(
  'jupyter-nbformat'
  'jupyter-nbclient'
  'jupyterlab_pygments'
  'python-beautifulsoup4'
  'python-bleach'
  'python-defusedxml'
  'python-entrypoints'
  'python-jinja'
  'python-pandocfilters'
  'python-mistune1'
  'python-pygments'
  'python-testpath'
  'python-traitlets'
)
makedepends=('python-pyppeteer' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest' 'python-ipywidgets')
optdepends=('pandoc: non-html conversion output'
            'python-pyppeteer: for webPDF conversion support')
source=("https://github.com/jupyter/nbconvert/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('7e61f0fe3b7c84acc403dd8e81cb300abb274f8026c3437842bc813621602599')

build() {
  cd nbconvert-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbconvert-$pkgver
  export PYTHONPATH="$PWD" # Make sure system nbconvert is not used
  # disable tests that download chromium and (probably) require a graphical session
  pytest -vv -k 'TestWebPDFExporter and not (test_export or test_webpdf_without_pyppeteer)'
  pytest -vv -k 'TestNbConvertApp and not test_webpdf_with_chromium'
  # run all remaining tests
  pytest -vv -k 'not TestWebPDFExporter and not TestNbConvertApp'
}

package() {
  cd nbconvert-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
