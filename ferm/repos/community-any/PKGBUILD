# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Marti Raudsepp <marti@juffo.org>
# Contributor: Manuel Mazzuola <origin.of@gmail.com>

pkgname=ferm
pkgver=2.1
pkgrel=4
pkgdesc='Tool to maintain complex firewalls'
arch=('any')
url='http://ferm.foo-projects.org/'
license=('GPL2')
depends=('iptables')
optdepends=('ebtables' 'arptables' 'ipset')
backup=('etc/ferm.conf' 'etc/conf.d/ferm')
source=(
  "http://ferm.foo-projects.org/download/${pkgver:0:3}/$pkgname-$pkgver.tar.gz"
  'ferm.rc'
  'ferm.service'
  'ferm.conf'
   )
md5sums=('ac5d11554a50e514cbad8f354b14e564'
         '6dea3504cec17e059d3340349fc4e1dc'
         '5af6db9ad8d6c016b4cd2e2b44ec9f44'
         'b365f63649b080990ec851b2cccf8adf')

package() {
  # software setup
  cd $pkgname-$pkgver
  make PREFIX="${pkgdir}/usr" install
  cd ..
  # install initscripts
  install -Dm 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -Dm 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd service
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # setup default config
  install -D -m 644 "$pkgdir/usr/share/doc/ferm/examples/workstation.ferm" \
    "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
