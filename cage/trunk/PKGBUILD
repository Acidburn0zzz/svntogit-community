# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=cage
pkgver=0.1.2
pkgrel=1
pkgdesc="A kiosk compositor for Wayland"
arch=('x86_64')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('glibc' 'libxkbcommon' 'polkit' 'pixman' 'wayland')
makedepends=('git' 'meson' 'scdoc' 'wayland-protocols' 'wlroots'
'xorg-server-xwayland')
optdepends=('xorg-server-xwayland: X11 support')
source=("${pkgname}::git+https://github.com/Hjdskes/${pkgname}.git#tag=v${pkgver}?signed"
        "${pkgname}-0.1.2-scdoc.patch")
sha512sums=('SKIP'
            '20cb1ccfd14c6e27df6487c42d98fde672fec084bcd221717aeeceed7739705cc7d8ac5261bfa526c8f20f37305f2c5827f023d620aaebba1d83ea2385d29d9a')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

prepare() {
  cd "$pkgname"
  patch -Np1 -i "../${pkgname}-0.1.2-scdoc.patch"
}

build() {
  cd "$pkgname"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             -Dxwayland=true \
             build
  ninja -C build
}

package() {
  depends+=('libwlroots.so')
  cd "$pkgname"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
