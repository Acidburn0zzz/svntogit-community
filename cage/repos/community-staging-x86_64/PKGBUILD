# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=cage
pkgver=0.1.1.r40.gcc1f975
pkgrel=1
pkgdesc="A kiosk compositor for Wayland"
arch=('x86_64')
url="https://www.hjdskes.nl/projects/cage/"
license=('MIT')
depends=('glibc' 'libxkbcommon' 'pixman' 'wayland')
makedepends=('git' 'meson' 'wayland-protocols' 'wlroots' 'xorg-server-xwayland')
optdepends=('xorg-server-xwayland: X11 support')
# cage 0.1.1 not compatible with wlroots > 0.8.1
# therefore pinning current HEAD to be able to upgrade wlroots
# https://github.com/Hjdskes/cage/issues/107
# source=("$pkgname-$pkgver.tar.gz::https://github.com/Hjdskes/${pkgname}/archive/v${pkgver}.tar.gz"
#         "https://github.com/Hjdskes/cage/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
source=("${pkgname}-${pkgver}::git+https://github.com/Hjdskes/${pkgname}.git#commit=cc1f975?signed")
sha512sums=('SKIP')
validpgpkeys=('13B6A35E9FD677D02CD68DBE37C445296EBC43B1') # Jente Hidskes <dev@hjdskes.nl>

pkgver() {
  cd "$pkgname-$pkgver"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             -Dxwayland=true \
             build
  ninja -C build
}

package() {
  depends+=('libwlroots.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  #docs
  install -vDm 644 README.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
