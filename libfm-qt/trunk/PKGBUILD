# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libfm-qt
pkgver=0.13.1
pkgrel=1
pkgdesc="Core library of PCManFM-Qt (Qt binding for libfm)"
arch=("x86_64")
url="http://lxqt.org"
license=("GPL2")
depends=("libfm" "qt5-x11extras")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('29b8f686950a7b0ea86468d01ca64a6ff8316ccd5f0144808a4332692cf96199431470880a9b560780efeba7add833ab6a0d9b4b24220f8fb7091cc5ef151dc8'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build

	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
