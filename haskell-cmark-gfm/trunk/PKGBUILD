# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=cmark-gfm
pkgname=haskell-cmark-gfm
pkgver=0.1.4
pkgrel=1
pkgdesc="Fast, accurate GitHub Flavored Markdown parser and renderer"
url="https://github.com/kivikakk/cmark-gfm-hs"
license=("custom:BSD3")
arch=('x86_64')
depends=('ghc-libs' 'cmark-gfm')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('34e914039b385b66859b9438330619c9d0895ed8a71b4de38c66a3bbcc8bf1fafb663afb5e3c8d2ae897351c5fba204ddf44017644860578721946f1a184ecd3')

prepare() {
    # https://github.com/kivikakk/cmark-gfm-hs/issues/8
    sed -i 's/pkgconfig-depends: libcmark-gfm/Extra-Libraries: cmark-gfm cmark-gfmextensions/' $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -fpkgconfig
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
