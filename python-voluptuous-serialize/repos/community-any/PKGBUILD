# Maintainer: Jameson Pugh <imntreal@gmail.com>

pkgname=python-voluptuous-serialize
pkgver=2.3.0
pkgrel=2
pkgdesc='Convert voluptuous schemas to JSON'
arch=(any)
url=https://github.com/balloob/voluptuous-serialize
license=(APACHE)
depends=(python-voluptuous)
makedepends=(
  git
  python-setuptools
)
checkdepends=(python-pytest)
source=(git+https://github.com/balloob/voluptuous-serialize.git#tag=054be2486b9f12ac4bf29ae5b25ef784fa6da6a5)
sha256sums=(SKIP)

pkgver() {
  cd voluptuous-serialize

  git describe --tags
}

prepare() {
  cd voluptuous-serialize

  git cherry-pick -n dc55dcc38a7ca0a49571a0133f423b0547cbbad6
}

build() {
  cd voluptuous-serialize

  python setup.py build
}

check() {
  cd voluptuous-serialize

  python setup.py test
}

package() {
  cd voluptuous-serialize

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
