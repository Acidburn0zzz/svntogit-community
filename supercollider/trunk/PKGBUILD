# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: osc <farid at archlinux-br.org>

_name=SuperCollider
pkgname=supercollider
pkgver=3.11.0
pkgrel=1
pkgdesc="Environment and programming language for real time audio synthesis and algorithmic composition"
arch=('x86_64')
url="https://supercollider.github.io"
license=('GPL3')
groups=('pro-audio')
depends=('gcc-libs' 'glibc'  'libx11' 'qt5-base' 'qt5-svg' 'qt5-webengine'
'qt5-websockets')
makedepends=('alsa-lib' 'avahi' 'boost' 'cmake' 'emacs' 'fftw' 'libsndfile'
'link' 'qt5-tools' 'readline' 'systemd-libs' 'yaml-cpp')
checkdepends=('xorg-server-xvfb')
optdepends=('emacs: emacs interface'
            'gedit: gedit interface'
            'sc3-plugins: additional extension plugins for scsynth')
provides=("libscsynth.so" "sclang=${pkgver}" "scsynth=${pkgver}")
source=("https://github.com/${pkgname}/${pkgname}/releases/download/Version-${pkgver}/${_name}-${pkgver}-Source-linux.tar.bz2"
        "${pkgname}-3.11.0-devendor_ableton_link.patch")
install="${pkgname}.install"
sha512sums=('8c93395231caf3617c3dae07e7c94e2253e24ab1a822b64904d5bc67f62fb12e9837ed0cd7210fbbe10cb5a7ff277e80e5f2ec85d6b51026c920851d4fda59a4'
            'f42f2eb9c61b20b6313f9351e0528c397718bccafd094095d7d1d810eee55efd7b47126be8ef3ba56d6e752dbd270d11111f188c4a88282405af489993b3051d')


prepare() {
  mv -v "${_name}-Source" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # devendoring Ableton Link:
  # https://github.com/supercollider/supercollider/issues/4818
  # https://github.com/supercollider/supercollider/issues/4818
  patch -Np1 -i "../${pkgname}-3.11.0-devendor_ableton_link.patch"
  # removing macOS hidden files (due to release tarball issues):
  # https://github.com/supercollider/supercollider/issues/4545
  find . -type f -iname "*\._*" -delete
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DBoost_NO_BOOST_CMAKE=ON \
        -DLIBSCSYNTH=ON \
        -DFORTIFY=ON \
        -DSYSTEM_ABLETON_LINK=ON \
        -DSYSTEM_BOOST=ON \
        -DSYSTEM_YAMLCPP=ON \
        -DSC_VIM=OFF \
        -B build \
        .
  make VERBOSE=1 -C build
}

check() {
  cd "${pkgname}-${pkgver}/build"
  xvfb-run make test ARGS="-V" -C build || echo "Known failing tests: https://github.com/supercollider/supercollider/issues/3555"
}

package() {
  depends+=('libasound.so' 'libavahi-common.so' 'libavahi-client.so'
  'libboost_filesystem.so' 'libboost_program_options.so' 'libboost_regex.so'
  'libboost_thread.so' 'libfftw3f.so' 'libjack.so' 'libsndfile.so'
  'libreadline.so' 'libudev.so' 'libyaml-cpp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {AUTHORS,{CHANGELOG,README,README_LINUX}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
