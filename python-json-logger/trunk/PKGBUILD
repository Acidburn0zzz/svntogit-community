# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-json-logger
pkgver=2.0.0
pkgrel=1
pkgdesc='A python library adding a json log formatter'
arch=('any')
license=('BSD')
url='https://github.com/madzak/python-json-logger'
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/madzak/python-json-logger/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('e784fc306ede22e88bcd3bf6e124d93c9e078b6e2560f468a2bc439cf2f8397af995938f85dc38f292d742c5e6e7fc691ce2245cfec9a1f029a99be83070e1fe')

build() {
  cd python-json-logger-$pkgver
  python setup.py build
}

check() {
  cd python-json-logger-$pkgver
  python setup.py test
}

package() {
  cd python-json-logger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
