# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Joost Bremmer <contact at madeofmagicandwires dot online>
# Contributor: Eliezio Oliveira <eliezio@pm.me>
# Contributor: Matthew Gamble <internet@matthewgamble.net>

pkgname=sad
pkgver=0.4.11
pkgrel=1
pkgdesc="Space Age seD"
arch=('x86_64')
url="https://github.com/ms-jpq/sad"
license=('MIT')
makedepends=('rust')
optdepends=(
  'fzf: for tui usage'
  'diff-so-fancy: for diff colourizer'
  'git-delta: for diff colourizer'
)
source=(
  "$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
  'Cargo.lock'
)
b2sums=('8f47c2066bc101fa7f9e60c37ab56b9161c1a1b0bf35ae00dab7f99d2a41da1d3e431b60737ca8c6435194b82279ee8f74a7a440f1bf37ba087d53b888ef3237'
        'a803c115c06dd3738b2f1fd4dd625214f683bab0184775c75807025fad890de0835cb74d4c76cead4dba7c5f4ba99fc0a17c7f58916e0ab111ab1d62f6a1a365')

prepare() {
  cd "$pkgname-$pkgver"

  # lockfile is out of sync again
  cp -f "$srcdir/Cargo.lock" Cargo.lock

  # download dependencies
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --frozen --release --all-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --all-features
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim: set ts=2 sts=2 sw=2 et :
