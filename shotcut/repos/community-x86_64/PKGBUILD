# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: nfnty <arch@nfnty.se>

pkgname=shotcut
pkgdesc='Cross-platform Qt based Video Editor'
pkgver=22.04.22
pkgrel=1
arch=('x86_64')
url='https://www.shotcut.org'
license=('GPL3')
depends=('qt5-base' 'qt5-declarative' 'qt5-graphicaleffects' 'qt5-multimedia'
         'qt5-quickcontrols' 'qt5-quickcontrols2' 'qt5-websockets' 'qt5-x11extras'
         'mlt' 'movit' 'ffmpeg' 'libx264' 'libvpx' 'lame' 'frei0r-plugins' 'ladspa')
optdepends=('swh-plugins: Several audio filters')
makedepends=('qt5-tools')
options=('debug')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mltframework/shotcut/archive/v$pkgver.tar.gz")
sha512sums=('37074a79d9ded0ec6d635ae67462b9e823a832b64b6234064a88aa564e58e62976c567778f311137d6416251ff4033db567bd8e4c410ea0f51fc8fff973b7880')

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  qmake .. \
    PREFIX='/usr' \
    QMAKE_CFLAGS_RELEASE="$CFLAGS" \
    QMAKE_CXXFLAGS_RELEASE="$CXXFLAGS" \
    SHOTCUT_VERSION=ARCH-$pkgver \
    DEFINES+=SHOTCUT_NOUPGRADE

  make

  lrelease ../translations/*.ts
}

package() {
    cd $pkgname-$pkgver/build

    make INSTALL_ROOT="$pkgdir" install
}

