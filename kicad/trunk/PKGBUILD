# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Marq Schneider <queueRAM@gmail.com>

pkgname=kicad
pkgver=5.1.0
pkgrel=1
pkgdesc="Electronic schematic and printed circuit board (PCB) design tools"
arch=('x86_64')
#url="http://iut-tice.ujf-grenoble.fr/kicad/"
url="http://kicad-pcb.org/"
license=('GPL')
#wxgtk3 is still not supported yet (FS#54839)
depends=('wxgtk2' 'python2' 'boost-libs' 'glew' 'curl' 'glm' 'ngspice' 'opencascade')
# the AUR git package uses python3 but this release does not support it
# can ngspice/opencascade be an optdep?
# opencascade vs community edition?
makedepends=('cmake' 'zlib' 'mesa' 'boost' 'swig')
optdepends=('kicad-library: for footprints and symbols'
            'kicad-library-3d: for 3d models of components')
source=("https://launchpad.net/kicad/5.0/$pkgver/+download/kicad-$pkgver.tar.xz"
        "kicad-i18n.$pkgver.tgz::https://github.com/KiCad/kicad-i18n/archive/$pkgver.tar.gz")
sha512sums=('644865932d7e18e446c222cd85e969bb3462bca7fbf6c411e955cd460f5df45ed5221c0948ecd34262a2f5355e586c8fe04c1cb28fa51e4fd09d6f3f78c54761'
            'c1d971958376d68fc7ea95551b8407344494b6babf807063efc0e392713a4807a3dbda42dc0d3c85035c7cfe19b2d5a139efb0a6323db642fe29852d0bdfccea')

prepare() {
  cd "$srcdir/kicad-$pkgver"
  find -name '*.py' -exec sed -i '1s/python$/&2/' {} +
}

build() {
  cd "$srcdir/kicad-$pkgver"

  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_BUILD_TYPE=Release    \
              -DCMAKE_INSTALL_PREFIX=/usr   \
              -DCMAKE_INSTALL_LIBDIR=lib    \
              -DKICAD_USE_OCE=OFF           \
              -DKICAD_USE_OCC=ON            \
              -DKICAD_SCRIPTING=ON          \
              -DKICAD_SCRIPTING_MODULES=ON  \
              -DKICAD_SCRIPTING_WXPYTHON=OFF \
              -DKICAD_SCRIPTING_ACTION_MENU=ON \
              -DBUILD_GITHUB_PLUGIN=ON
  make

  cd "$srcdir/kicad-i18n-$pkgver"
  mkdir -p build/Release
  cd build/Release
  cmake ../.. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir/kicad-$pkgver/build/Release"

  make DESTDIR="$pkgdir" install

  # copy updated linux icons
  #cp -r -n "$srcdir/$pkgname-icons/resources/linux/mime/icons" "$pkgdir/usr/share/"

  cd "$srcdir/kicad-i18n-$pkgver/build/Release"
  make DESTDIR="$pkgdir" install
}
