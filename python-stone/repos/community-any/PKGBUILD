# Maintainer:

pkgname=python-stone
_name=${pkgname#python-}
pkgver=3.3.0
pkgrel=1
pkgdesc='The Official API Spec Language for Dropbox API V2'
arch=(any)
url='https://github.com/dropbox/stone'
license=(MIT)
depends=(python python-ply python-six)
makedepends=(python-setuptools python-pytest-runner)
checkdepends=(python-mock python-coverage)
source=(https://github.com/dropbox/stone/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('48b1a29b271fed237a6a1087bad3cca938f58c1650c69c35322ab14a2969c01b')

prepare() {
  sed -e 's|== 5\.2\.0|>= 5.2.0|' -i $_name-$pkgver/setup.py # Unpin pytest-runner version
}

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  pytest
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
