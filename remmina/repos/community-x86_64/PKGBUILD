# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.0.0
pkgrel=8
pkgdesc="Remmina is a remote desktop client written in GTK+. "
arch=(i686 x86_64)
url="http://remmina.sourceforge.net/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
	     'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib')
optdepends=('libxkbfile'
	    'freerdp'
	    'telepathy-glib'
	    'gnome-keyring')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=(https://github.com/downloads/FreeRDP/Remmina/Remmina-$pkgver.tar.gz
	'https://github.com/FreeRDP/Remmina/commit/569d9bb702d06c061dfbbf3c590d57ea83a04369.diff'
	'https://github.com/FreeRDP/Remmina/commit/f7d1038ba358a295fb46c681db5c47e4ead97306.diff'
	'https://github.com/FreeRDP/Remmina/commit/1aebfc027507607d5f061ba9072eb952e07214d1.diff')
md5sums=('701c540acaab961bc3adf130a2ddb8b1'
         'a979c898d5e53446cec9d01912ad9243'
         'b2a9c0e80ac7b0c8a26935bbe289aa3b'
         'd7db32f63996df1ea661c7fff24262d4')

build() {
  cd ${srcdir}/FreeRDP-Remmina-*/
  patch -p1 -i "${srcdir}/569d9bb702d06c061dfbbf3c590d57ea83a04369.diff"
  patch -p1 -i "${srcdir}/f7d1038ba358a295fb46c681db5c47e4ead97306.diff"
  export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread -lgnutls"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/FreeRDP-Remmina-*/
  make DESTDIR=${pkgdir} install
}
