# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.3.7
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(x86_64)
url="https://www.remmina.org/"
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3' 'libsodium')
makedepends=('cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol' 'telepathy-glib'
	     'harfbuzz')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'libxkbfile: NX plugin'
            'nxproxy: NX plugin'
            'spice-gtk: Spice plugin'
            'telepathy-glib: Telepathy plugin'
            'xorg-server-xephyr: XDMCP plugin')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/Remmina/Remmina/-/archive/v${pkgver/rc/-rc}/Remmina-v${pkgver/rc/-rc}.tar.bz2")
sha256sums=('0412dd8d405a52c644a2465c261e333471079c9b5645b84942752a09cbedeb4b')

prepare() {
  cd Remmina-v${pkgver/rc/-rc}
  sed -e 's|ssh_threads|ssh|' -i cmake/FindLIBSSH.cmake # Fix build with libssh 0.8
  sed -i 's|include_directories(.)|include_directories(.)\ninclude_directories(/usr/include/harfbuzz)|' CMakeLists.txt
}

build() {
  cd "$srcdir"/Remmina-v${pkgver/rc/-rc}/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd "$srcdir"/Remmina-v${pkgver/rc/-rc}/
  make DESTDIR="$pkgdir" install
}
