# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.2.0rcgit.8
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://www.remmina.org/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib'
             'libvncserver')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support'
            'libvncserver: VNC client/server support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/${pkgver/rc/-rc}.tar.gz")
md5sums=('8010dd04dee689d77e7b7c3e956b0205')

build() {
  cd ${srcdir}/Remmina-${pkgver/rc/-rc}/
  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/Remmina-${pkgver/rc/-rc}/
  make DESTDIR=${pkgdir} install
  rm -rf $pkgdir/build
}
