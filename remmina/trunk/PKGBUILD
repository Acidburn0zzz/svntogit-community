# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
epoch=1
pkgver=1.4.14
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(x86_64)
url="https://www.remmina.org/"
license=('GPL')
depends=('avahi' 'libgcrypt' 'libssh' 'vte3' 'libsodium' 'libappindicator-gtk3')
makedepends=('cmake' 'freerdp' 'libvncserver' 'spice-gtk' 'spice-protocol' 'telepathy-glib'
             'harfbuzz' 'xorgproto' 'gobject-introspection')
optdepends=('freerdp: RDP plugin'
            'libsecret: Secret plugin'
            'libvncserver: VNC plugin'
            'libxkbfile: NX plugin'
            'nxproxy: NX plugin'
            'spice-gtk: Spice plugin'
            'telepathy-glib: Telepathy plugin'
            'xorg-server-xephyr: XDMCP plugin'
            'gnome-terminal: external tools')
replaces=('remmina-plugins')
provides=('remmina-plugins')
source=("$pkgname-$pkgver.tar.bz2::https://gitlab.com/Remmina/Remmina/-/archive/v${pkgver/rc/-rc}/Remmina-v${pkgver/rc/-rc}.tar.bz2")
sha512sums=('5eb97087bac95793d3d94d503525d94f6b3186aeae849c139068e9726b8d4cc17d8e237887d93710a9f49be43cfc5e7278bbe8ba0d1383ef97d1116795357bf6')

build() {
  cmake -S Remmina-v${pkgver/rc/-rc} -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=/usr/lib \
    -DWITH_APPINDICATOR=ON \
    -DWITH_NEWS=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
