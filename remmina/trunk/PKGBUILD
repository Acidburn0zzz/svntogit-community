# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=remmina
pkgver=1.1.1
pkgrel=1
pkgdesc="remote desktop client written in GTK+"
arch=(i686 x86_64)
url="http://freerdp.github.io/Remmina/"
license=('GPL')
depends=('gtk3' 'zlib' 'libjpeg' 'libssh' 'libunique' 'avahi' 'vte3' 'libgcrypt' 'libxdmcp')
makedepends=('intltool' 'pkgconfig' 'cmake' 'libgnome-keyring'
             'avahi' 'libxkbfile' 'freerdp' 'telepathy-glib')
optdepends=('libxkbfile'
            'freerdp'
            'telepathy-glib'
            'gnome-keyring'
            'nxproxy'
            'xorg-server-xephyr: XDMCP support')
replaces=('remmina-plugins')
provides=('remmina-plugins')
install=remmina.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/FreeRDP/Remmina/archive/v1.1.1.tar.gz"
	"vte2.91.patch")
md5sums=('481dd96ebf7b14e4fd4df8648040b725'
         'a784e86b570e68afffe6ad4191f1bd00')

prepare() {
  cd ${srcdir}/Remmina-$pkgver/

  for i in "${source[@]}"; do
    if [[ $i == *.patch ]]; then
      patch -F100 -p1 -i "${srcdir}/${i}"
    fi
  done
}

build() {
  cd ${srcdir}/Remmina-$pkgver/

#  export LDFLAGS="-lX11 -lgdk_pixbuf-2.0 -lgio-2.0 -lgobject-2.0 -lgmodule-2.0 -lgthread-2.0 -lpthread -lgnutls"
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DWITH_APPINDICATOR=OFF .
  make
}

package() {
  cd ${srcdir}/Remmina-$pkgver/
  make DESTDIR=${pkgdir} install
}
