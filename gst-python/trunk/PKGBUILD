# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gst-python
pkgname=('gst-python' 'gst-python2')
pkgver=1.6.2
pkgrel=2
pkgdesc="Python bindings for GStreamer"
arch=('i686' 'x86_64')
license=('LGPL')
url="http://gstreamer.freedesktop.org/"
makedepends=('pkg-config' 'python-gobject' 'python2-gobject' 'gst-plugins-base')
source=(http://gstreamer.freedesktop.org/src/gst-python/gst-python-${pkgver}.tar.xz)
md5sums=('14b6cfe1a0db5cc01c6a58f7b4af6184')

build() {
  cp -a "${srcdir}/gst-python-${pkgver}" "${srcdir}/gst-python2-${pkgver}"

  cd "${srcdir}/gst-python-${pkgver}"
  ./configure --prefix=/usr
  make

  cd "${srcdir}/gst-python2-${pkgver}"
  export PYTHON=python2
  ./configure --prefix=/usr
  make
}

package_gst-python() {
  depends=('python-gobject' 'gst-plugins-base')

  cd "${srcdir}/gst-python-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

package_gst-python2() {
  depends=('python2-gobject' 'gst-plugins-base')

  cd "${srcdir}/gst-python2-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
