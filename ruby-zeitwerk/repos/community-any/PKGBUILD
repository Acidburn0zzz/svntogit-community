# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.5.2
pkgrel=1
pkgdesc="Efficient and thread-safe code loader for Ruby"
arch=(any)
url="https://github.com/fxn/zeitwerk"
license=(MIT)
depends=(ruby)
makedepends=(ruby-rake ruby-rdoc)
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=(ruby-minitest ruby-minitest-focus ruby-minitest-reporters)
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('0e8be7500163761a75518b002eb8ca9e41d0d073f0d03443af4a57fef07ee647de3faafaf79791328c4b2b9a6f5898e1038b310b697485a363af486f36509393')
b2sums=('d4277dc79729e39bd63b066b12ad4d91d9457e8e7a836c2e24606f6d343c85ce59febaa0425ab10f96d8d327935beede285a5ae52cfeff37210b36b0bcaf22db')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" -n "${pkgdir}/usr/bin" ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
