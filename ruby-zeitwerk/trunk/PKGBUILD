# Maintainer: David Runge <dvzrv@archlinux.org>

_name=zeitwerk
pkgname=ruby-zeitwerk
pkgver=2.3.0
pkgrel=1
pkgdesc='Efficient and thread-safe code loader for Ruby'
arch=('any')
url="https://github.com/fxn/zeitwerk"
license=('MIT')
depends=('ruby')
makedepends=('ruby-rake' 'ruby-rdoc')
# TODO: package ruby-minitest-focus
# TODO: package ruby-minitest-reporters
# checkdepends=('ruby-minitest' 'ruby-minitest-focus' 'ruby-minitest-reporters')
options=(!emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/fxn/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('e891b2fda9375f1e459dce2adc2f1b2128bd20a2f7e617ae27967b3bd0927689f5134aa42b098fdd077c089bf2debcf0a88b7b3fd89a97ac44dc9c9fbc72f14e')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "$pkgname-$pkgver"
  gem build "${_name}.gemspec"
}

# check() {
#   cd "$pkgname-$pkgver"
#   rake test
# }

package() {
  cd "$pkgname-$pkgver"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 MIT-LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache gems/${_name}-${pkgver}/{ext,lib/*/*.so} \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out}
}
