# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-pie
_pkgname=Gnome-Pie
pkgver=0.7.0
pkgrel=1
pkgdesc="Circular application launcher for GNOME"
arch=('i686' 'x86_64')
url="http://gnome-pie.simonschneegans.de/"
license=('GPL')
depends=('libarchive' 'libgee' 'libwnck3' 'gnome-menus')
makedepends=('cmake' 'vala')
source=($_pkgname-$pkgver.tar.gz::https://github.com/Simmesimme/$_pkgname/archive/v$pkgver.tar.gz)
md5sums=('90d205a8768bf57138c420385b21b9cc')

build() {
  cd $_pkgname-$pkgver
  ./resources/locale/compile-po.sh
  [[ -d build ]] || mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd $_pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
