# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

pkgname='matrix-appservice-irc'
pkgver=0.9.0
pkgrel=1
pkgdesc="Node.js IRC bridge for Matrix"
arch=('any')
url="https://github.com/matrix-org/matrix-appservice-irc"
license=('Apache')
depends=('nodejs')
makedepends=('npm' 'git')
optdepends=('python: for maintenance scripts')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('6260bd2b6434d31cbe5b5972d19d50652141ae585a37f41b549efe28719282df')

package() {
  cd ${pkgbase}-${pkgver}
  npm install --cache "${srcdir}"/npm-cache -g --user root --prefix "${pkgdir}"/usr

  # Apparently it now symlinks to the source folderâ€¦
  rm -f "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  mkdir -p "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc
  cp -r * "${pkgdir}"/usr/lib/node_modules/matrix-appservice-irc/
  chmod -R go-w "${pkgdir}"/usr/lib/node_modules/
  chmod go-w "${pkgdir}"/usr/bin
}
