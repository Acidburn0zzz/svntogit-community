# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-devel-patchperl
pkgver=0.70
pkgrel=1
pkgdesc="Patch perl source a la Devel::PPPort's buildperl.pl"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0' 'perl-file-pushd>=1.00')
url=https://metacpan.org/release/Devel-PatchPerl
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Devel-PatchPerl-$pkgver.tar.gz")
md5sums=(9fb18ee1ba0e35b7efe532c2317c6ec5)
sha512sums=(8017ceb64d66c5790ec5d2e27443f974efc2501cf02ad928a3b06de56491f6886d3e97cbda5db7f40e75e00490c092964d1108625021427a4916158ccf41a7de)
_dir="$srcdir/Devel-PatchPerl-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
