# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: D. Can Celasun <dcelasun[at]gmail[dot]com>
# Contributor: Felix Hanley <felix@seconddrawer.com.au>
# Contributor: SÅ‚awomir Kowalski <suawekk@gmail.com>

pkgname=percona-toolkit
pkgver=3.1.1
pkgrel=3
_commit=8abdc070501191bd4688a9e5baf28ccac97020e8
pkgdesc="Collection of advanced command-line tools to perform a variety of MySQL and system tasks."
url="https://www.percona.com/software/database-tools/percona-toolkit"
arch=('x86_64')
license=('GPL')
depends=('perl-term-readkey')
makedepends=('perl-dbd-mysql' 'perl-dbi' 'go-pie' 'dep' 'git')
optdepends=(
    'perl-dbd-mysql: MySQL connections'
    'perl-dbi: MySQL connections'
)
options=('!emptydirs' 'purge')
# source=("https://github.com/percona/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
source=("https://github.com/percona/$pkgname/archive/$_commit/$pkgname-$_commit.tar.gz")
sha256sums=('19699f01e34fbbd276f6557c6acab296ca2f340899106bfbea3b73870f4a1985')

prepare() {
    mkdir -p "$srcdir"/src/github.com/percona
    cd "$srcdir"/src/github.com/percona
    mv "$srcdir"/$pkgname-$_commit $pkgname
    cd $pkgname

    sed -ri src/go{,/*}/Makefile \
        -e "s#\\$\\(shell git describe --abbrev=0\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-(parse|list).* HEAD\\)#v$pkgver#" \
        -e "s#\\$\\(shell git rev-parse --show-toplevel\\)#$srcdir/src/github.com/percona/$pkgname#" \
        -e "s#\\$\\(shell basename \`git rev-parse --show-toplevel\`\\)#$pkgname#" \
        -e "s#\\bLDFLAGS\\b#PTLDFLAGS#" \
        -e 's#^PTLDFLAGS="#PTLDFLAGS="-extldflags \\"$(LDFLAGS)\\" #'
}

build() {
    export GOPATH="$srcdir"
    export PATH="$GOPATH/bin:$PATH"

    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps MODULEBUILDRC=/dev/null
    perl Makefile.PL INSTALLDIRS=vendor
    make

    cd src/go
    make linux-amd64
}

package() {
    cd "$srcdir"/src/github.com/percona/$pkgname
    unset PERL5LIB PERL_MM_OPT PERL_LOCAL_LIB_ROOT
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/lib

    cd bin
    for bin in *; do
        if [ ! -f "$pkgdir/usr/bin/vendor_perl/$bin" ]; then
            install -Dm755 "$bin" "$pkgdir/usr/bin/$bin"
        fi
    done
}
