# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=aiohttp-apispec
pkgname=python-$_pkgname
pkgver=1.5.0
pkgrel=1
pkgdesc='Build and document REST APIs with aiohttp and apispec'
arch=('any')
url='https://github.com/maximdanilchenko/aiohttp-apispec'
license=('MIT')
depends=('python-aiohttp' 'python-apispec' 'python-webargs' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-aiohttp')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('e6c271abd0664da55fa4300b5de90c6585af17de2701b366d06c6a611401353445e8abb94e19dea640571fa071d688745aeadaa82fbccf6659e92409ade7276f')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
