# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-power-manager
pkgver=2.0.1
pkgrel=1
pkgdesc="Power management tool for the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-power-manager"
groups=('ukui')
depends=('dbus-glib' 'gtk3' 'libgnome-keyring' 'libnotify' 'upower')
makedepends=('intltool' 'mate-common' 'yelp-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-power-manager/archive/v$pkgver.tar.gz")
sha512sums=('ec8010ce428bd488a432deb47175d5c1ad7b016e4e175e46d2de65e226c5ef0d1025b6ebec317eb5f5ed51c2a52818b213442cb0a8bee60789297eab51f02f8f')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sbindir=/usr/bin --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
