# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgname=opensmtpd
pkgver=201307311702p1
pkgrel=1
pkgdesc='Free implementation of the server-side SMTP protocol'
arch=('i686' 'x86_64')
url='http://www.opensmtpd.org/'
license=('custom')
depends=('libevent')
provides=('smtp-server' 'smtp-forwarder')
conflicts=('smtp-server' 'smtp-forwarder')
backup=('etc/smtpd/smtpd.conf' 'etc/smtpd/aliases')
options=('emptydirs')
install=opensmtpd.install
source=("http://www.opensmtpd.org/archives/$pkgname-$pkgver.tar.gz"
        'smtpd.service'
        'smtpd.socket')
md5sums=('25bdf0f590a34f8ee4735d80677bace9'
         'a278f272d97a9fe5a8aac784a7c98d67'
         'c2c01e9ca78df3f65efe40a7c0e17ee0')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's,etc/aliases,etc/smtpd/aliases,' smtpd/smtpd.conf
}

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc/smtpd \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib/smtpd \
    --with-maildir=/var/spool/mail \
    --with-privsep-path=/var/empty \
    --with-sock-dir=/run \
    --with-privsep-user=smtpd \
    --with-queue-user=smtpq \
    --with-pam
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
  # license
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  # working directory
  cd "$pkgdir"
  # install systemd units
  install -Dm644 "$srcdir/smtpd.service" usr/lib/systemd/system/smtpd.service
  install -Dm644 "$srcdir/smtpd.socket" usr/lib/systemd/system/smtpd.socket
  # empty aliases file (used by default config)
  install -Dm644 /dev/null etc/smtpd/aliases
  # create pool directory
  install -dm711 var/spool/smtpd
  # fix bad symlinks
  ln -fs smtpctl usr/bin/mailq
  ln -fs smtpctl usr/bin/sendmail
  ln -fs makemap usr/bin/newaliases
  ls -l usr/bin/
}

# vim:set ts=2 sw=2 et:
