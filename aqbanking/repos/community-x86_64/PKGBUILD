# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=aqbanking
pkgver=6.1.4
_verid=283
pkgrel=1
pkgdesc="A library for online banking and financial applications"
arch=('x86_64')
url="https://www.aquamaniac.de/rdm/projects/aqbanking"
license=('GPL')
depends=('gwenhywfar' 'ktoblzcheck' 'libofx')
options=('!makeflags')
source=(https://www.aquamaniac.de/rdm/attachments/download/${_verid}/$pkgname-$pkgver.tar.gz
        https://www.aquamaniac.de/rdm/attachments/download/282/$pkgname-$pkgver.tar.gz.asc)
validpgpkeys=('42400AF5EB2A17F0A69BB551E9899D784A977416') #AqBanking Package Key <packages@aqbanking.de>
sha256sums=('f3c07a466de96e38b14df7b096304886c8b2b3921f41fd6ab47d3a6cd36431d9'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr --enable-gwenhywfar \
              --with-backends="aqhbci aqofxconnect aqpaypal"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
