# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: David Moore <davidm@sjsoft.com>

pkgname=aqbanking
pkgver=5.8.1
_dnrel=105
pkgrel=1
pkgdesc="A library for online banking and financial applications"
arch=('x86_64')
url="https://www.aquamaniac.de/rdm/projects/aqbanking"
license=('GPL')
depends=('gwenhywfar' 'ktoblzcheck' 'libofx')
options=('!makeflags')
source=(https://www.aquamaniac.de/rdm/attachments/download/${_dnrel}/$pkgname-$pkgver.tar.gz)
# validpgpkeys=('88491B1E68624A5AE5068262895C619C3CA35783') #Martin Preuss <martin@libchipcard.de>
sha256sums=('af063a75ba401de3538f41cca0c7001950086b3e2d4f190ffe3ddfeea1b08454')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export PKG_CONFIG=/usr/bin/pkg-config
  ./configure --prefix=/usr --enable-gwenhywfar \
              --with-backends="aqhbci aqofxconnect"
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
