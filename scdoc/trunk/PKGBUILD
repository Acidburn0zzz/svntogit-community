# Maintainer: Ainola
# Contributor: Drew DeVault

pkgname=scdoc
pkgver=1.6.0
pkgrel=2
license=('MIT')
pkgdesc='tool for generating roff manual pages'
arch=('x86_64')
url='https://git.sr.ht/~sircmpwn/scdoc/'
source=("$pkgname-$pkgver.tar.gz::https://git.sr.ht/~sircmpwn/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('18dade6bb27dbe6d112478bbbc08239b384c2334f37418b94d5f5b25713697d9d854ebb08cc62ed6e68b55a01326ea207614e4f79976536ec800029cc55f8b4b')

build() {
    cd "$pkgname-$pkgver"
    make PREFIX=/usr
}

check() {
    cd "$pkgname-$pkgver"
    make check
}

package() {
    cd "$pkgname-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}
