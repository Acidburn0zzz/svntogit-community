# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Simo Leone <neotuli@gmail.com>

pkgname=t1utils
pkgver=1.41
pkgrel=4
pkgdesc="Utilities for manupulating Adobe Type 1 font software "
arch=('x86_64')
url="https://github.com/kohler/t1utils"
license=('custom')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kohler/${pkgname}/archive/v${pkgver}.tar.gz"
        "${pkgname}-1.41-memmem-decl.patch::https://github.com/kohler/${pkgname}/commit/3f1ddda424353f0f926dd28efa47b0ac61556ce8.patch")
sha512sums=('155406eb56721ed2c3298c9ddd0a1bcac0f782f37dc209ed993fee356d4455b8462970b8da70f189bfff55ab60522cd9e4d1f505429f8008e57a15a5f75e1381'
            '573a15133e8227100820039aaf5dea12d038674dd486510a38647e9f500c9de2c16034d99c8b26444f6c7253fe788833aa6dbd3f46be389bc1ca1b031deb51c8')
b2sums=('6c6e20d5816a2333e5cc1fe619e28d0930343b58837ce839f076360993b37e40672d69858947368f375e2105c72fe8d2613a8e7b940f8dba05efbd024f142d98'
        '571231de362261aa9f7cda5c0180e26027177703d40e8ad30d3a1edc51cd1190c93d9930db51ebf54dd2745a59feab7721232ebb3614d1568a47e439c240500a')

prepare() {
  cd "${pkgname}-${pkgver}"
  # check for memmem declaration
  # FS#57999 and https://github.com/kohler/t1utils/issues/8
  patch -p1 -i "${srcdir}/${pkgname}-1.41-memmem-decl.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  install -vDm 644 {NEWS,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

