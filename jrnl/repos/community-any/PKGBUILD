# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jrnl
pkgver=2.8.2
pkgrel=1
pkgdesc="Collect your thoughts and notes without leaving the command line"
arch=('any')
url="https://jrnl.sh/"
license=('GPL3')
depends=('python-ansiwrap' 'python-asteval' 'python-colorama' 'python-cryptography' 'python-keyring'
         'python-parsedatetime' 'python-dateutil' 'python-pyxdg' 'python-yaml' 'python-pytz'
         'python-tzlocal')
makedepends=('python-dephell')
checkdepends=('python-behave' 'python-pytest' 'python-pytest-bdd')
source=("https://github.com/jrnl-org/jrnl/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c1b3188dde10ae5243d366afc89cc3dcbea60eaa3b9684706f26dc3699fe95658f70a4c54e7f87e1f9bd2ce96fd07db63bee70afd4f8760c7457605e874848b1')

prepare() {
  cd jrnl-$pkgver

  # poetry-generated setup.py is badly broken in several ways, including
  # distribution of tests in the built package as well as using distutils for
  # bad metadata. See https://github.com/sdispater/poetry/issues/866
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd jrnl-$pkgver
  python setup.py build
}

check() {
  cd jrnl-$pkgver
  python -m pytest
  behave
}

package() {
  cd jrnl-$pkgver
  python setup.py install -O1 --prefix=/usr --root="$pkgdir"
}

# vim:set ts=2 sw=2 et:
