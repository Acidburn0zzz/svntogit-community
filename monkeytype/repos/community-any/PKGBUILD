# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=MonkeyType
pkgname=monkeytype
pkgver=20.4.2
pkgrel=1
pkgdesc='A system for Python that generates static type annotations by collecting runtime types'
arch=('any')
url='https://github.com/instagram/MonkeyType'
license=('BSD')
depends=('python-mypy_extensions' 'python-libcst')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-django')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('911f34b8c67f53a7684bdadf83655793516c89960f4bbf18e7454faa87e08df0cfb8189bfc15682ac34648f01cbab53ebbbac5dd8c2dc2ce878d1e1a5cbb0383')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
