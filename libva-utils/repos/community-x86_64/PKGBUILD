# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sean V Kelley <seanvk@posteo.de>

pkgname=libva-utils
pkgver=2.9.0
pkgrel=1
pkgdesc="Intel VA-API Media Applications and Scripts for libva"
arch=(x86_64)
url="https://github.com/intel/libva-utils"
license=(custom)
depends=(libva)
makedepends=(mesa meson)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d363210a42cd2667f6f7cff0e57db1d527feb067ffebe3ba233bc80b1bdcd9b4')

build() {
    arch-meson ${pkgname}-${pkgver} build
    meson compile -C build
}

package() {
    DESTDIR="${pkgdir}" meson install -C build
    install -Dm644 ${pkgname}-${pkgver}/COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
