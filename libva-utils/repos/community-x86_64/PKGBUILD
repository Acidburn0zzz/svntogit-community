# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Sean V Kelley <seanvk@posteo.de>

pkgname=libva-utils
pkgver=2.6.0
pkgrel=1
pkgdesc="Intel VA-API Media Applications and Scripts for libva"
arch=(x86_64)
url="https://github.com/intel/libva-utils"
license=(custom)
depends=(libva)
makedepends=(mesa)
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('6ce7a534539c9ae554ace0bfa6689d871145cc172bb3f66ceb3fe2f65466fd90')

build() {
    cd ${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
