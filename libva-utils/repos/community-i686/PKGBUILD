# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Sean V Kelley <seanvk@posteo.de>

pkgname=libva-utils
pkgver=1.8.2
pkgrel=1
pkgdesc="Intel VA-API Media Applications and Scripts for libva"
arch=('i686' 'x86_64')
url="https://github.com/01org/libva-utils"
license=('custom')
depends=('libva')
makedepends=('mesa')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz")
sha256sums=('ccae885d9f3b4511081d287816c56bd7dda98e5947e6254ebe60d08e86c6ba4a')

build() {
    cd ${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="$pkgdir" install
    install -Dm644 COPYING "$pkgdir"/usr/share/licenses/${pkgname}/COPYING
}
