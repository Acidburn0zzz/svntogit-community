# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Armin 'IsSuE' Praher [issue.at.gmx.dot.at]
# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=teeworlds
pkgver=0.5.2
_bamver=0.2.0
pkgrel=2
pkgdesc="A multiplayer 2D shooter"
arch=('i686' 'x86_64')
url="http://teeworlds.com"
license=('custom')
depends=('alsa-lib' 'mesa' 'sdl')
makedepends=('python2')
replaces=('teewars')
source=(http://www.teeworlds.com/files/${pkgname}-${pkgver}-src.tar.gz
        http://www.teeworlds.com/files/bam-${_bamver}.tar.gz
        teeworlds.desktop
        teeworlds.png
        fix_datadir_search.patch)
md5sums=('f605f6df9f1714dcda4cba1d281cc757'
         'd4efe74591a73c1cec8b34d76ffd1049'
         '3944aaf7a8c19a83510b2b39c4cddf5f'
         '44f39e398409232a0e0a431ed46d8cc8'
         '673791ecb74274346456f0fae53b4428')

build() {
	# Build bam (used to build teewars)
	# Now it is released separately I should make a separate package...
	cd ${srcdir}/bam-${_bamver}
	./make_unix.sh

	# Build teeworlds
	cd ${srcdir}/${pkgname}-${pkgver}-src

	# fix for FS 13118 which will be applied in next release
	patch -p1 -i ../fix_datadir_search.patch

	# Use Python 2
	sed -i 's/python /python2 /' default.bam

	../bam-${_bamver}/src/bam server_release client_release
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}-src

	# Install data files
	mkdir -p ${pkgdir}/usr/share/${pkgname}/data
	cp -r ${srcdir}/${pkgname}-${pkgver}-src/data/* \
		${pkgdir}/usr/share/${pkgname}/data

	install -Dm755 ${srcdir}/${pkgname}-${pkgver}-src/${pkgname} \
		${pkgdir}/usr/bin/${pkgname}
	install -Dm755 ${srcdir}/${pkgname}-${pkgver}-src/${pkgname}_srv \
		${pkgdir}/usr/bin/${pkgname}_srv

	install -Dm644 ${srcdir}/${pkgname}.desktop \
		${pkgdir}/usr/share/applications/${pkgname}.desktop
	install -Dm644 ${srcdir}/${pkgname}.png \
		${pkgdir}/usr/share/pixmaps/${pkgname}.png
	install -Dm644 ${srcdir}/${pkgname}-${pkgver}-src/license.txt \
		${pkgdir}/usr/share/licenses/${pkgname}/license.txt
}
