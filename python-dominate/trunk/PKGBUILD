# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-dominate
pkgver=2.5.1
pkgrel=1
pkgdesc="A Python library for creating and manipulating HTML documents using an elegant DOM API"
url="https://github.com/Knio/dominate"
license=('LGPL')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Knio/dominate/archive/$pkgver.tar.gz")
sha512sums=('431209e6431ab00c9352be6eeab7d25d05ae923670f1ade1398f5af247047b3be458c06bf0d68ab90fb4c93b59acb47a10e6a1f020b5c6234bdb1d2cac50ef71')

build() {
  cd dominate-$pkgver
  python setup.py build
}

check() {
  cd dominate-$pkgver
  python setup.py pytest
}

package() {
  cd dominate-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
