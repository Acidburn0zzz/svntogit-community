# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=mc
pkgver=4.8.17
pkgrel=2
pkgdesc="Midnight Commander is a text based filemanager/shell that emulates Norton Commander"
arch=('i686' 'x86_64')
url="http://www.ibiblio.org/mc/"
license=('GPL')
depends=('e2fsprogs' 'glib2' 'gpm' 'libssh2' 'slang')
makedepends=('libxt' 'libx11' 'unzip')
optdepends=('aspell: spelling corrections'
            'cabextract: ucab extfs'
            'cdparanoia: audio extfs'
            'cdrkit: iso9660 extfs'
            'cvs: CVS support'
            'gawk: hp48+ extfs'
            'mtools: a+ extfs'
            'p7zip: support for 7zip archives'
            'perl: needed by several extfs scripts'
            'python2-boto: s3+ extfs'
            'python2-pytz: s3+ extfs'
            'samba: VFS support'
            'unace: uace extfs'
            'unarj: uarj extfs'
            'unrar: urar extfs'
            'zip: uzip extfs')
backup=('etc/mc/edit.indent.rc'
        'etc/mc/filehighlight.ini'
        'etc/mc/mc.ext'
        'etc/mc/mc.keymap'
        'etc/mc/mc.menu'
        'etc/mc/mcedit.menu'
        'etc/mc/sfs.ini')
options=('!emptydirs')
source=("http://ftp.midnight-commander.org/${pkgname}-${pkgver}.tar.xz"
        "0001-Ticket-3639-fix-window-resizing-when-panels-are-hidd.patch"
        "0002-Do-not-botch-SIGWINCH-delivery-to-the-subshell.patch")
sha256sums=('0447bdddc0baa81866e66f50f9a545d29d6eebb68b0ab46c98d8fddd2bf4e44d'
            '9a3639ff068dfddee0342d631b56d8df6c0240105802fb00edb0fa3a265e95d6'
            '2a5ab735226ce41b0ebbb4bcedb0e04e82ea3cb14e2776acede1e58816f3d787')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ${srcdir}/0001-Ticket-3639-fix-window-resizing-when-panels-are-hidd.patch
  patch -Np1 < ${srcdir}/0002-Do-not-botch-SIGWINCH-delivery-to-the-subshell.patch
}

build() {
  export PYTHON=/usr/bin/python2

  cd ${pkgname}-${pkgver}
  ./configure \
      --prefix=/usr \
      --libexecdir=/usr/lib \
      --sysconfdir=/etc \
      --enable-vfs-smb \
      --with-x
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
