# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: mutantmonkey <aur@mutantmonkey.in>

pkgname=spice-vdagent
pkgver=0.19.0
pkgrel=2
pkgdesc="Spice agent for Linux guests"
arch=('x86_64')
url="https://www.spice-space.org/"
license=('GPL')
depends=('alsa-lib' 'dbus' 'glib2' 'gtk3' 'libdrm' 'libpciaccess' 'libxfixes' 'libxinerama' 'libxrandr' 'systemd')
makedepends=('spice-protocol')
backup=('etc/conf.d/spice-vdagentd')
source=("https://www.spice-space.org/download/releases/$pkgname-$pkgver.tar.bz2"
        "gnome-3.34.patch")
sha256sums=('63a77c611c84f8120519a78a30256e43e159085831ac82de71643db643972f65'
            'cde285aba68b2949bc2ae4b5351691a3fd0b6d5a618623ea6d1a420c7ec56c7d')

prepare() {
	cd $pkgname-$pkgver

	# Fix session lookup for new GNOME versions
	patch -Np1 -i ../gnome-3.34.patch

	sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|
	        s|/usr/sbin/spice-vdagentd|/usr/bin/spice-vdagentd|' data/spice-vdagentd.service
	sed -i 's|/etc/sysconfig/spice-vdagentd|/etc/conf.d/spice-vdagentd|' data/spice-vdagentd.1.in
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --sbindir=/usr/bin \
	            --with-session-info=systemd --with-init-script=systemd
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	rm -r "$pkgdir/var"
	install -dm755 "$pkgdir/etc/conf.d/"
	echo 'SPICE_VDAGENTD_EXTRA_ARGS=""' > "$pkgdir/etc/conf.d/spice-vdagentd"
}
