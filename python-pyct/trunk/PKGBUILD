# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pyct
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=0.4.6
pkgrel=1
pkgdesc="Python packaging Common Tasks"
arch=(any)
url="https://pyct.pyviz.org/"
license=(BSD)
makedepends=(python-setuptools python-param python2-setuptools python2-param)
source=("https://files.pythonhosted.org/packages/source/p/${_pkg}/${_pkg}-${pkgver}.tar.gz")
sha256sums=('df7b2d29f874cabdbc22e4f8cba2ceb895c48aa33da4e0fe679e89873e0a4c6e')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

package_python-pyct() {
    depends=(python-param)
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-pyct() {
    depends=(python2-param)
    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1

    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
