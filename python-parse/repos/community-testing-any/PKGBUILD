# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Aaron DeVore <aaron.devore@gmail.com>

pkgname=python-parse
pkgver=1.16.0
pkgrel=1

pkgdesc='Parse strings using a specification based on the Python format() syntax.'
url='https://pypi.python.org/pypi/parse'
arch=('any')
license=('BSD')

depends=('python')
makedepends=('python-setuptools')

source=("https://pypi.org/packages/source/p/parse/parse-$pkgver.tar.gz")

sha256sums=('cd89e57aed38dcf3e0ff8253f53121a3b23e6181758993323658bffc048a5c19')

build() {
  cd "$srcdir"/parse-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/parse-$pkgver
  python test_parse.py
}


package() {
  cd "$srcdir"/parse-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
