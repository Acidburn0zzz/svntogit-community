# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: John D Jones III <j[nospace]n[nospace]b[nospace]e[nospace]k[nospace]1972 -_AT_- the domain name google offers a mail service at ending in dot com>
# Generator  : CPANPLUS::Dist::Arch 1.25

pkgname=perl-crypt-openssl-dsa
pkgver=0.18
pkgrel=1
pkgdesc="Digital Signature Algorithm using OpenSSL"
arch=('i686' 'x86_64')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('openssl')
url='http://search.cpan.org/dist/Crypt-OpenSSL-DSA'
source=("http://search.cpan.org/CPAN/authors/id/K/KM/KMX/Crypt-OpenSSL-DSA-$pkgver.tar.gz")
sha512sums=('00d7026158a96fcd276aa61ddd5f16b110d223723d9e8823659febdf55be9f1cae7ae0c4c0c7e9314148cc042fb57c0f62b6d34b56e2fa1c41832bf6d72d3b3d')

build() {
  cd Crypt-OpenSSL-DSA-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Crypt-OpenSSL-DSA-$pkgver
  make test
}

package() {
  cd Crypt-OpenSSL-DSA-$pkgver
  make DESTDIR="$pkgdir" install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
# template start; name=perl-binary-module-dependency; version=1;
if [[ $(find "$pkgdir/usr/lib/perl5/" -name "*.so") ]]; then
	_perlver_min=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]);')
	_perlver_max=$(perl -e '$v = $^V->{version}; print $v->[0].".".($v->[1]+1);')
	depends+=("perl>=$_perlver_min" "perl<$_perlver_max")
fi
# template end;
}

# vim:set ts=2 sw=2 et:
