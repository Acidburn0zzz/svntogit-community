# $Id$
# Contributor: Francois Boulogne <fboulogne at april dot org>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=subsurface-marble
_pkgname=marble-subsurface-branch
pkgver=4.6.2
pkgrel=1
pkgdesc='Virtual Globe'
url='https://git.subsurface-divelog.org/'
license=('GPL' 'LGPL' 'FDL')
arch=('i686' 'x86_64')
makedepends=('cmake')
depends=('qt5-'{script,svg,tools,webkit} 'marble-data')
source=("https://subsurface-divelog.org/downloads/${_pkgname}-${pkgver}.tgz")
sha256sums=('e3c2df82c9616c3b01baffe4710970371b01744b60761cdaf5b8259506455f5b')

prepare() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	sed '/<QPrint/d' -i src/lib/marble/TinyWebBrowser.cpp
}

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	cmake \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_BUILD_TYPE=Release \
		-DQT5BUILD=ON \
		-DQTONLY=ON \
		-DBUILD_MARBLE_APPS=OFF \
		-DBUILD_MARBLE_EXAMPLES=OFF \
		-DBUILD_MARBLE_TESTS=OFF \
		-DBUILD_MARBLE_TOOLS=OFF \
		-DBUILD_TESTING=OFF \
		-DWITH_DESIGNER_PLUGIN=OFF \
		-DBUILD_WITH_DBUS=OFF \
		-DMOBILE=OFF \
		.
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install

	cd "${pkgdir}/usr"
	mv include subsurface
	install -d include
	mv subsurface include
        rm -fr share/{appdata,icons}

	# Provided by marble-data, avoids conflicts.
	rm share/marble/cmake/FindMarble.cmake
	rm -fr share/marble/data

	# Conflicts with libastro
	rm lib/libastro.so{,.1,.0.17.20}
}
