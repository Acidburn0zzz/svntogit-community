# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sagemath-doc
pkgname=(sagemath-doc sagemath-src)
pkgver=7.6
pkgrel=1
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
makedepends=(sagemath sage-notebook python2-pyzmq python2-pillow python2-docutils tachyon
             python2-jupyter_client python2-pkgconfig cython2 thebe python2-sphinx python2-ipywidgets)
source=("$pkgbase-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
	package.patch disable-fes.patch docbuild_main.patch is-package-installed.patch ecm-7.patch increase-rtol.patch
        r-no-readline.patch sagemath-planarity3.patch sagemath-singular-4.1.0.p2.patch sagemath-sphinx-1.5.patch)
sha256sums=('e6e0729ab25446e7c922058db0eee4b0b294b32c196e34842e843ea9fae41423'
            '4a2297e4d9d28f0b3a1f58e1b463e332affcb109eafde44837b1657e309c8212'
            '7ab53d30b877dabb6835fbe67a208bef65fa0aaf40221b4d0b58e4b7086bc47a'
            '187b75d3ce202126012ff20b6316277b0a003ffdd7f15b820d3b88459d2753c8'
            'd60fb0fbd27991ce9496ca035a54b03334b5b53f244227a8d6e13f3327ce75d2'
            '65ad84c83d5f956fde8eab961f2caad56d64044324c4abc0e56bcd9f44061cb6'
            'be925eefeac3f28e639794ddc20d64e6faafd7181804e29417a510224d19ccc7'
            'ef9f401fa84fe1772af9efee6816643534f2896da4c23b809937b19771bdfbbf'
            'a1c562ebe4538d672404ca3ac2e954a3c955afeb7463f7b4fe6eaa6fa74fe5c7'
            'e1912afec58b834fac3b9161c2d5a1794bb618126c6bd7a4be85c834231360e7'
            '0ffbccdd65ba8dfe84222e62d9e43663735d4f33276f7ec7b2e44ed53d1f24d0')

prepare() {
  cd sage-$pkgver

# Arch-specific patches
# assume all optional packages are installed
  patch -p0 -i ../package.patch
# fix regressions with ECM 7
  patch -p1 -i ../ecm-7.patch
# increase numerical tolerance, needed by scipy 0.18
  patch -p1 -i ../increase-rtol.patch
# fix freezes in R interface with readline 7 (Debian)
  patch -p1 -i ../r-no-readline.patch
# fix build with planarity 3 (Debian)
  patch -p2 -i ../sagemath-planarity3.patch
# fix build with Singular 4.1.0-p2
  patch -p0 -i ../sagemath-singular-4.1.0.p2.patch

# Upstream patches
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
#  patch -p0 -i ../fes02.patch
# disable fes module, fails to build
  patch -p0 -i ../disable-fes.patch
# replace is_package_installed usage http://trac.sagemath.org/ticket/20377
  patch -p1 -i ../is-package-installed.patch
# fix doc build with sphinx 1.5 https://trac.sagemath.org/ticket/22252
  patch -p1 -i ../sagemath-sphinx-1.5.patch

# fix relative imports
  patch -p0 -i ../docbuild_main.patch

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py

  mkdir -p local-python
  ln -sr src/sage_setup local-python
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages:$srcdir/sage-$pkgver/local-python"
  python2 sage_setup/docbuild --no-pdf-links -k all html
}

package_sagemath-doc() {
  pkgdesc="HTML documentation and inline help for SageMath"
  depends=(sage-notebook python2-sphinx gap-doc thebe)
  conflicts=(sage-mathematics-doc)
  replaces=(sage-mathematics-doc)

  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
        rm -r $_dir
        ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}

package_sagemath-src() {
  pkgdesc="Source files for SageMath"
  conflicts=(sage-mathematics-src)
  replaces=(sage-mathematics-src)
  
  mkdir -p "$pkgdir"/usr/share/sage/source
  cp -r sage-$pkgver/src/{sage,sage_setup} "$pkgdir"/usr/share/sage/source
}
