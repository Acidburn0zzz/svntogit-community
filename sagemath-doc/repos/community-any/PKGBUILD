# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=sagemath-doc
pkgname=(sagemath-doc sagemath-src)
pkgver=8.1
pkgrel=1
arch=(any)
url="http://www.sagemath.org"
license=(GPL)
makedepends=(sagemath sage-notebook python2-pyzmq python2-pillow python2-docutils tachyon
             python2-jupyter_client python2-pkgconfig cython2 thebe python2-sphinx python2-ipywidgets)
source=("$pkgname-$pkgver.tar.gz::https://github.com/sagemath/sage/archive/$pkgver.tar.gz"
        sagemath-env.patch package.patch latte-count.patch jupyter-path.patch sagemath-python3-notebook.patch test-optional.patch
        r-no-readline.patch fes02.patch sagemath-threejs.patch sagemath-detect-igraph.patch
        sagemath-networkx2.patch sagemath-linbox-1.5.patch sagemath-pynac-0.7.14.patch docbuild_main.patch)
sha256sums=('a42b55c2c8ac244535dc9b87171b7d4dd7e1d498099518ab253354fbe2eaaa7b'
            '6dc9b101310f80381c61c0312a3e5c648771bfe0de2e94ac46fafe7282a3e0ee'
            'c41ae665499c6cd775d40bbe178f8786830b0931ee26bf11ee02f7d83bcc8107'
            '0b680e674c11c47afa86162d8b49645620b8912722e08133d23357c29ca9310a'
            '2cad308f8adbb6c54e6603fa22b2f0eb60f6f09248d5d015000c3932ac14f646'
            '962ce805c87147212b21fc2ab0ac9af9bd0033942c7a6905b9906645b48e8a4f'
            'ef94908d4ab28d13af622e6e58ec191aa78817d17e4466c7bb6f64ee72a813b9'
            'afd0952b9bb8f52fd428eae36cf719a58ff85a894baae88cbb2124e043768cc7'
            '7fcb52e96935dccb0f958d37c2f4e3918392480b9af53e08562f6cba6c68cb94'
            '514135b920a43f999571a15e97b41e14f5bed59f65b19643864dc23555a7b830'
            '28d7789b8d777922ab8871ca43b6afab751428cae875c0343d3962e6a2030b88'
            '37c5c1e694a2aca06c0f1c7d99622ff81fd2bc6a51e8745762294889fa4673f6'
            'a52d03e04c9d64bb957a1f8dcdae3280ebb9450a7fd76aaf5ae5de5c6f74774f'
            '538f7b279d72f4b67edb445d386d267f3f7022c1079031ca7ea06f6ce392c906'
            'ea5e17bb7a7cb36a22e5e3872fcc2585852bc971c4b139b0b2cd69a36c1b009b')

prepare() {
  cd sage-$pkgver

# Arch-specific patches
# assume all optional packages are installed
  patch -p0 -i ../package.patch
# set env variables
  patch -p0 -i ../sagemath-env.patch
# don't list optional packages when running tests
  patch -p0 -i ../test-optional.patch
# set jupyter path
  patch -p0 -i ../jupyter-path.patch
# fix freezes in R interface with readline 7 (Debian)
  patch -p1 -i ../r-no-readline.patch
# use correct latte-count binary name
  patch -p1 -i ../latte-count.patch
# make 'sage -notebook=jupyter' work with our python3 jupyter-notebook package
  patch -p1 -i ../sagemath-python3-notebook.patch
# fix Cremona database detection
  sed -e "s|is_package_installed('database_cremona_ellcurve')|os.path.exists('/usr/share/cremona/cremona.db')|" \
   -i src/sage/databases/cremona.py
# fix python-igraph detection
  patch -p1 -i ../sagemath-detect-igraph.patch
# adapt to networkx 2 changes
  patch -p1 -i ../sagemath-networkx2.patch
# fix three.js plotting backend
  patch -p1 -i ../sagemath-threejs.patch

# Upstream patches  
# fix build against libfes 0.2 http://trac.sagemath.org/ticket/15209
  patch -p1 -i ../fes02.patch
# fix build with linbox 1.5 https://trac.sagemath.org/ticket/24214
  patch -p1 -i ../sagemath-linbox-1.5.patch
# fix build with pynac 0.7.14 https://trac.sagemath.org/ticket/24329
  patch -p1 -i ../sagemath-pynac-0.7.14.patch

# fix relative imports
  patch -p0 -i ../docbuild_main.patch

# use python2
  sed -e 's|cython %s %s|cython2 %s %s|' -e 's|python setup.py|python2 setup.py|' -i src/sage/misc/cython.py

  mkdir -p local-python
  ln -sr src/sage_setup local-python
}

build() {
  cd sage-$pkgver/src

  export SAGE_LOCAL="/usr"
  export SAGE_ROOT="/usr"
  export SAGE_SRC="$PWD"
  export SAGE_DOC_SRC="$SAGE_SRC"/doc
  export SAGE_DOC="$SAGE_SRC"/doc
  export SAGE_DOC_MATHJAX=yes
  export PYTHONPATH="/usr/lib/sage/site-packages:$srcdir/sage-$pkgver/local-python"
  python2 sage_setup/docbuild --no-pdf-links -k all html
}

package_sagemath-doc() {
  pkgdesc="HTML documentation and inline help for SageMath"
  depends=(sage-notebook python2-sphinx gap-doc thebe)
  conflicts=(sage-mathematics-doc)
  replaces=(sage-mathematics-doc)

  cd sage-$pkgver/src/doc
 
  mkdir -p "$pkgdir"/usr/share/doc/sage/en
  cp -r en/introspect "$pkgdir"/usr/share/doc/sage/en
  cp -r common "$pkgdir"/usr/share/doc/sage
  cp -r html "$pkgdir"/usr/share/doc/sage

# Replace duplicated files by symlinks (Gentoo)
  cd "$pkgdir"/usr/share/doc/sage
  mv html/en/_static{,.tmp}
  for _dir in `find -name _static` ; do
        rm -r $_dir
        ln -s /usr/share/doc/sage/html/en/_static $_dir
  done
  mv html/en/_static{.tmp,}
}

package_sagemath-src() {
  pkgdesc="Source files for SageMath"
  conflicts=(sage-mathematics-src)
  replaces=(sage-mathematics-src)
  
  mkdir -p "$pkgdir"/usr/share/sage/source
  cp -r sage-$pkgver/src/{sage,sage_setup} "$pkgdir"/usr/share/sage/source
}
