# Maintainer: Ivy Foster <escondida@iff.ink>
# Reference: PKGBUILD(5)

pkgname=inter-font
pkgver=3.13
pkgrel=1
pkgdesc='A typeface specially designed for user interfaces'
url=https://github.com/rsms/inter/
license=(custom:OFL)

arch=(any)
source=("https://github.com/rsms/inter/releases/download/v$pkgver/Inter-$pkgver.zip")
# Warning: checksums provided by packager, not upstream
sha256sums=(789d34210229d814afc5bd7d0b4623bb89c8d8073fbe7b0a24fbf77248564da6)
noextract=("Inter-$pkgver.zip")

# Upstream dumps a *bunch* of stuff into the top level of the archive
prepare() {
	mkdir "inter-$pkgver"
	cd "inter-$pkgver"
	bsdtar xf ../Inter-$pkgver.zip
}

package() {
	cd "inter-$pkgver"
	install -d "$pkgdir/usr/share/fonts/inter"
	install -m644 -t "$pkgdir/usr/share/fonts/inter" 'Inter Desktop'/*.otf
	install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/inter-font/LICENSE.txt"
}
