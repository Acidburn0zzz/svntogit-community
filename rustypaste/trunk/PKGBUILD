# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=rustypaste
pkgver=0.6.5
pkgrel=1
pkgdesc='A minimal file upload/pastebin service'
arch=('x86_64')
url='https://github.com/orhun/rustypaste'
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
backup=('etc/rustypaste/config.toml')
options=(!lto)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/orhun/rustypaste/archive/refs/tags/v${pkgver}.tar.gz
  rustypaste.service
  rustypaste.env
  rustypaste.sysusers
  rustypaste.tmpfiles)
b2sums=('d0cdd1afa85e9ca96bdd79bcf174e3fa4c771b872d3582efbe9813c5d4f33757d964c31c749a0cd0814579c95b4d480f90e848c48e773f9363f3f63225b0f9b8'
        '0a97e61cc4bbe00d7e395bbee1893ae21444559dc01a1598144c9e20b198ed4b467aa72c2989449b0d055bab9c5d8d75ecb06f874332d8e444a4b0580915b359'
        '540a8523154c1847c93b9332da2a1ce84336ca043bc551540649a2dfab4bb2bee42ef11d25d9287ac1b256c3862f703b3302e36b11e627a45ff72a10ff5f9a8e'
        '47dd34688a8cfab88f6be13830e3030445f0378247a69f8efc8c54d221d28b871a6226920b12c7fcb1e1bf1659c7e0aa2df691dbbfa0ff64e58c7680149027ee'
        'f08a802529a7afb77950496d500a12b1332b6b56202db60e4d76765cca79d8b44ce658f433294221696172a8c34ceac0121c75673b9b9de72a873f451617d0c0')

prepare() {
  cd ${pkgname}-${pkgver}
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target
  cargo build --release --frozen
}

check() {
  cd ${pkgname}-${pkgver}
  export RUSTUP_TOOLCHAIN=stable
  cargo test --frozen
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 target/release/${pkgname} -t "${pkgdir}"/usr/bin
  install -Dm644 config.toml -t "${pkgdir}"/etc/rustypaste
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}

  install -Dm644 "${srcdir}"/rustypaste.env -t "${pkgdir}"/etc/rustypaste
  install -Dm644 "${srcdir}"/rustypaste.service -t "$pkgdir"/usr/lib/systemd/system/
  install -Dm644 "${srcdir}"/rustypaste.sysusers "${pkgdir}"/usr/lib/sysusers.d/rustypaste.conf
  install -Dm644 "${srcdir}"/rustypaste.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/rustypaste.conf
}
