# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sam Burgos <santiago.burgos1089@gmail.com>
# Contributor: Kyle Laker <kyle@laker.email>

pkgname=warpinator
pkgver=1.1.2
pkgrel=2
pkgdesc='LAN file sender, send and receive files across the network'
arch=('any')
url='https://github.com/linuxmint/warpinator'
license=('GPL3')
depends=('gtk3' 'python-cryptography' 'python-gobject' 'python-grpcio' 'python-packaging'
         'python-protobuf' 'python-pynacl' 'python-setproctitle' 'python-xapp' 'python-zeroconf'
         'xapp')
makedepends=('meson' 'polkit')
optdepends=('ufw: Open a firewall port for Warpinator')
source=("https://github.com/linuxmint/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('c689cd26192198b787329c68d26ecc8d0ed229aa761971baa4e7d6823ebe5f5a')

prepare() {
  cd $pkgname-$pkgver

  # Fix hardcoded libexec dir
  sed -i 's|libexec/warpinator|lib/warpinator|' \
    bin/warpinator.in \
    data/org.x.warpinator.policy.in.in

  # Fix icon name
  sed -i 's/icon_name="warpinator"/icon_name="org.x.Warpinator"/' src/util.py
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
