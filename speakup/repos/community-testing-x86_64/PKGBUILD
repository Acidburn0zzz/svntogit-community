# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.33-ARCH'
pkgname=speakup
pkgver=3.1.4
pkgrel=3
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("ftp://linux-speakup.org/pub/speakup/$pkgname-$pkgver.tar.bz2"
            mksound.patch)
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.33' 'kernel26<2.6.34')
makedepends=('kernel26-headers>=2.6.33')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  # A change in 2.6.33 broke Speakup.  So we need to apply the following
  # patch.  This same patch is in upstream's git repo.
  # It'll go away with the next version.
  patch -p2 < "$srcdir/mksound.patch" || return 1
  make KERNELDIR="/lib/modules/${_kernver}/build" || return 1
  install -d "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" \
    || return 1
  install -d "$pkgdir/usr/share/doc/$pkgname" || return 1
  install -m 0644 speakup*.ko \
    "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" || return 1
  install -m 0644 ../doc/* "$pkgdir/usr/share/doc/$pkgname" || return 1
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" \
    "$startdir/speakup.install"
}

md5sums=('7b50da8061db01527c15dd9331fa1d32'
         'a1fd811bf98d17297a3774427ca1b810')
