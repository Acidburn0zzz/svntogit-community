# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.36-ARCH'
pkgname=speakup
pkgver=3.1.6
pkgrel=3
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("ftp://linux-speakup.org/pub/speakup/$pkgname-$pkgver.tar.bz2"
        k2.6.36.patch
        no_free.patch)
md5sums=('d5698267098f63ea166e06c0bd769a03'
         '4581784d381e1ebb792ee21f51916b7d'
         'a23fa2ee2f2e655cfe14a97b8f9c1e0a')
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.36' 'kernel26<2.6.37')
makedepends=('kernel26-headers>=2.6.36')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # The following patch is necessary in order to build against the 2.6.36
  # kernel.  It is in upstream's git repo.
  patch -N -p1 < "$srcdir/k2.6.36.patch"

  # Fix a possible double-free.  It is not legal to call input_free_device
  # after input_unregister_device.  The former function should only be
  # used for cleanup in case of errors.

  patch -N -p1 < "$srcdir/no_free.patch"

  cd src

  make KERNELDIR="/lib/modules/${_kernver}/build"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  install -d "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m 0644 speakup*.ko \
    "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname"
  install -m 0644 ../doc/* "$pkgdir/usr/share/doc/$pkgname"
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" \
    "$startdir/speakup.install"
}
