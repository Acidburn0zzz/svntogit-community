# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.32-ARCH'
pkgname=speakup
pkgver=3.1.3
pkgrel=3
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("ftp://linux-speakup.org/pub/speakup/$pkgname-$pkgver.tar.bz2"
        speakup-2.6.32-hdrs.patch)
md5sums=('395ba6e883d4e211caa9902040ecf9b2'
         '7f2231a6ac5a129e272668b5cabf5bb8')
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.32' 'kernel26<2.6.33')
makedepends=('kernel26-headers>=2.6.32' 'kernel26-headers<2.6.33')

build() {
  cd "$srcdir"
  patch -p0 < "$srcdir/speakup-2.6.32-hdrs.patch" || return 1
  cd "$srcdir/$pkgname-$pkgver/src"
  make KERNELDIR="/lib/modules/${_kernver}/build" || return 1
  install -d "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" \
    || return 1
  install -d "$pkgdir/usr/share/doc/$pkgname" || return 1
  install -m 0644 speakup*.ko \
    "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname" || return 1
  install -m 0644 ../doc/* "$pkgdir/usr/share/doc/$pkgname" || return 1
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" \
    "$startdir/speakup.install"
}

