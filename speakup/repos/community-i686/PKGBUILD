# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.30-ARCH'
pkgname=speakup
pkgver=3.1.1
pkgrel=1
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("http://members.cox.net/cmbrannon/$pkgname-$pkgver.tar.bz2")
md5sums=('f048b61ae9b5a49155e8722b8201effc')
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.30-1' 'kernel26<2.6.31')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"
  make || return 1
  install -d "${pkgdir}/lib/modules/${_kernver}/drivers/char/${pkgname}" || return 1
  install -d "${pkgdir}/usr/share/doc/${pkgname}" || return 1
  install -m 0644 speakup*.ko "${pkgdir}/lib/modules/${_kernver}/drivers/char/${pkgname}" || return 1
  install -m 0644 ../doc/* "${pkgdir}/usr/share/doc/${pkgname}" || return 1
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION=${_kernver}/" "${startdir}/speakup.install"
}

