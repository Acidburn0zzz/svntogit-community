# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
_kernver='2.6.35-ARCH'
pkgname=speakup
pkgver=3.1.6
pkgrel=1
pkgdesc='Kernel-based screenreader for the Linux console'
arch=('i686' 'x86_64')
url="http://linux-speakup.org/"
license=('GPL2')
source=("ftp://linux-speakup.org/pub/speakup/$pkgname-$pkgver.tar.bz2")
md5sums=('d5698267098f63ea166e06c0bd769a03')
install='speakup.install'
depends=('speakup-utils>=3.1.0' 'kernel26>=2.6.35' 'kernel26<2.6.36')
makedepends=('kernel26-headers>=2.6.35')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  make KERNELDIR="/lib/modules/${_kernver}/build"
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"
  install -d "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname"
  install -d "$pkgdir/usr/share/doc/$pkgname"
  install -m 0644 speakup*.ko \
    "$pkgdir/lib/modules/${_kernver}/kernel/drivers/$pkgname"
  install -m 0644 ../doc/* "$pkgdir/usr/share/doc/$pkgname"
  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" \
    "$startdir/speakup.install"
}
