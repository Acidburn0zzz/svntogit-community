# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=browserify
pkgdesc='browser-side require() the node way'
pkgver=16.2.0
pkgrel=1
arch=('any')
url='http://browserify.org/'
license=('MIT' 'BSD')
depends=('nodejs' 'acorn')
makedepends=('npm' 'git')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
install=browserify.install
sha512sums=('ca8b5d024a7f65b8037ac1d0058537ef38dcdbd2431f88974fc863cccd5f754556a208c0457d12d5c29e5f6e028bacd9fa31be0039824d1b7ac6fb6626723e05')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done

  # Fix npm security derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
