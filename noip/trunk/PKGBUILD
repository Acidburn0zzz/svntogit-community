# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daenyth
# Contributor: Lyle Putnam <lcputnam@amerytel.net>

pkgname=noip
pkgver=2.1.9
pkgrel=4
pkgdesc='Dynamic DNS Client Updater for no-ip.com services'
arch=('x86_64' 'i686')
url='http://www.no-ip.com/downloads.php?page=linux'
license=('GPL')
depends=('bash')
#backup=('etc/no-ip2.conf')
install=noip.install
source=('http://www.no-ip.com/client/linux/noip-duc-linux.tar.gz'
        'noip-rc.d'
        'noip2.service')
sha256sums=('82b9bafab96a0c53b21aaef688bf70b3572e26217b5e2072bdb09da3c4a6f593'
            '95bbebf584e0bc33186fb95f76a85bbd103fcea8ca073c7e4d36a811c3ac5d5d'
            '624553d92d69bb76cb457a056a7722dc051b5bbd17ea0e622b5cc08909019ea5')

build() {
  cd $srcdir/$pkgname-$pkgver-1

  # Their Makefile is fucking horrid, do it by hand instead
  sed -i '/^#define CONFIG_FILEPATH/s/PREFIX//' noip2.c
  sed -i '/^#define CONFIG_FILENAME/s/PREFIX//' noip2.c
  cc -Wall $CLFAGS $LDFLAGS -g -Dlinux -DPREFIX=/usr noip2.c -o noip2
}

package() {
  cd $srcdir/$pkgname-$pkgver-1

  install -Dm755 noip2 $pkgdir/usr/bin/noip2
  install -Dm755 ../noip-rc.d $pkgdir/etc/rc.d/noip
  install -Dm644 ../noip2.service $pkgdir/usr/lib/systemd/system/noip2.service
}

# vim:set ts=2 sw=2 et:
