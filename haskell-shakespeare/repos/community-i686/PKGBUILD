# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shakespeare
pkgname=haskell-shakespeare
pkgver=2.0.12
pkgrel=3
pkgdesc="A toolkit for making compile-time interpolated templates"
url="http://www.yesodweb.com/book/shakespearean-templates"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-aeson" "haskell-blaze-html" "haskell-blaze-markup"
         "haskell-exceptions" "haskell-parsec" "haskell-scientific" "haskell-text"
         "haskell-unordered-containers" "haskell-vector")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz"
        issue201.patch)
sha256sums=('9f07d26f00bba94ea47c35197132d22d8f6cb56de53bad818c5db8382c7cae91'
            '01d6550508d2651cf5d7fdceaa929a6749ab1340977dbf3a0462325eb5a317c3')

prepare() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    # https://github.com/yesodweb/shakespeare/pull/201
    patch -p1 -i ../issue201.patch
}


build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-test_roy -f-test_coffee -f-test_export
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
