# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Aleksandar TrifunoviÄ‡ <akstrfn at gmail dot com>

_pkgname=GSL
pkgname=microsoft-gsl
pkgver=3.0.0
pkgrel=1
pkgdesc="C++ Core Guidelines Support Library"
arch=('any')
url="https://github.com/Microsoft/GSL"
license=('MIT')
makedepends=('cmake' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Microsoft/GSL/archive/v${pkgver}.tar.gz")
sha256sums=('767b6246eecd0b2a915e2b5774ba6d4796579a5e15dc562d93ec80f1f2c9c889')

prepare() {
    mkdir "$_pkgname-$pkgver"/build
}

build() {
    cd "$_pkgname-$pkgver"/build
    cmake .. \
        -DGSL_TEST=ON \
        -DCMAKE_CXX_FLAGS="${CXXFLAGS}" \
        -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release

    make
}

check() {
    cd "$_pkgname-$pkgver"/build
    make test
}

package() {
    cd "$_pkgname-$pkgver"/build

    make DESTDIR="$pkgdir/" install

    cd ..
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
