# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: sickhate <sickhate@tux-linux.net>

pkgname=gnome-subtitles
pkgver=0.9.1
pkgrel=3
pkgdesc="Subtitle editor for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnome-subtitles.sourceforge.net/"
depends=('gnome-sharp' 'glade' 'gstreamer0.10-base' 'gtkspell')
makedepends=('gnome-doc-utils' 'intltool')
optdepends=('gstreamer0.10-base-plugins: video playback (or good, bad, ugly plugins)')
install=gnome-subtitles.install
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz fix-use-ldadd.patch use-libtool-shared.patch )

build() {
  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/${pkgname}-${pkgver}
  #backport from git remove when update to 0.9.2
  patch -Np1 -i $srcdir/fix-use-ldadd.patch || return 1
  patch -Np1 -i $srcdir/use-libtool-shared.patch || return 1
  autoreconf -f -i || return 1

  ./configure --prefix=/usr --sysconfdir=/etc \
  	--disable-schemas-install --disable-scrollkeeper

  make || return 1
  make DESTDIR=${pkgdir} install

  install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain $pkgname ${pkgdir}/etc/gconf/schemas/*.schemas || return 1
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas

  rm -rf "${MONO_SHARED_DIR}"
}
md5sums=('1c5f01aaf14dbd91581b286bf92f08d7'
         'cd2c78fedd06dbdf48f281eae135202b'
         '9a63d2e6e7f684a9b1dff851d43a64c1')
