# Maintainer: Allan McRae <mcrae_allan@hotmail.com>
# Contributor: sickhate <sickhate@tux-linux.net>

pkgname=gnome-subtitles
pkgver=0.8
pkgrel=6
pkgdesc="Subtitle editor for the GNOME Desktop"
arch=('i686' 'x86_64')
license=('GPL')
url="http://gnome-subtitles.sourceforge.net/"
depends=('gnome-sharp' 'glade' 'sublib' 'gstreamer0.10-base' 'gtkspell')
makedepends=('gnome-doc-utils' 'intltool')
optdepends=('gstreamer0.10-*-plugins: video playback')
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('9590389ba91f9cfd94b6b36454dc2420')

build() {
  export MONO_SHARED_DIR=${srcdir}/.wabi
  mkdir -p "${MONO_SHARED_DIR}"
  
  cd ${srcdir}/${pkgname}-${pkgver}
  
  sed -i "s#<property name=\"title\">Gnome Subtitles</property>#<property name=\"title\">Gnome Subtitles</property><property name=\"visible\">True</property>#" src/Glade/MainWindow.glade
  sed -i "s#window.Visible = true;##" src/GnomeSubtitles/Core/GUI.cs
  
  ./configure --prefix=/usr --sysconfdir=/etc
  make || return 1
  make DESTDIR=${pkgdir} install
  
  rm -rf "${MONO_SHARED_DIR}"
}
