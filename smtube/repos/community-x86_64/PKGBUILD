# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Andreas Schnaiter <rc.poison@gmail.com>

pkgname=smtube
pkgver=20.1.0
pkgrel=1
pkgdesc="An application that allows to browse, search and play YouTube videos"
arch=('x86_64')
url="https://www.smtube.org/"
license=('GPL2')
depends=('gcc-libs' 'glibc' 'qt5-script' 'qt5-webkit' 'hicolor-icon-theme')
makedepends=('qt5-tools')
optdepends=('mplayer: play videos with MPlayer'
            'smplayer: play videos with SMPlayer'
            'gnome-mplayer: play videos with GNOME MPlayer'
            'dragon: play videos with Dragon Player'
            'totem: play videos with Totem'
            'vlc: play videos with VLC'
            'mpv: play videos with MPV'
            'youtube-dl: download videos')
source=("https://downloads.sourceforge.net/project/${pkgname}/SMTube/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('91a3b53c7bf23b567cc9e35f6c12cfc37e4286c0b60938d4ea9e275f1f459db6ff149f3b316b05a4810a62b0f224ee360ecf10f1e5c1d101178a949280eb8e2f')

build() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "${pkgname}-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}/" install
}
