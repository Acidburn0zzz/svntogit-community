# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Knut Ahlers <knut at ahlers dot me>

pkgname=dust
pkgver=0.6.1
pkgrel=2
pkgdesc="A more intuitive version of du in rust"
arch=('x86_64')
url="https://github.com/bootandy/dust"
license=('Apache')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('639421a6935a79ff0c8656456e5aa13fb557c17f779d1d8d37dfd799ad785259f1df20221c7e42c215dbf6b5db273e55ceb015a514270679415dbc2fd6c781c6')

build() {
  cd "$pkgname-$pkgver"
  RUSTUP_TOOLCHAIN=stable cargo build --release --locked --all-features --target-dir=target
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
}

# vim:set ts=2 sw=2 et:
