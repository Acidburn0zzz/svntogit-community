# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>
# Contributor: vande198
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=supertux
pkgver=0.6.1
pkgrel=4
pkgdesc="A classic 2D jump'n'run sidescroller game in a style similar to the original SuperMario games"
arch=('x86_64')
url="https://github.com/supertux/supertux"
license=('GPL')
depends=('sdl2_image' 'curl' 'physfs' 'openal' 'libvorbis' 'glew' 'physfs' 'freetype2' 'boost-libs' 'hicolor-icon-theme')
makedepends=('cmake' 'boost' 'mesa' 'optipng')
source=("https://github.com/SuperTux/supertux/releases/download/v${pkgver}/SuperTux-v${pkgver}-Source.tar.gz")
sha512sums=('38a1da2a0546aa7fb98ece307b35a7e03a154b018d4b732592d3a718fd6c8ba827f3f8d99f0df777b7f438c0294c1838185ea7e60cc08f8cdb6f9a46b440e860')

build() {
  cd "SuperTux-v${pkgver}-Source"

  cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DINSTALL_SUBDIR_BIN=bin \
      -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "SuperTux-v${pkgver}-Source"

  make DESTDIR="${pkgdir}" install
}
