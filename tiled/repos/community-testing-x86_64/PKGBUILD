# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Collaborator: Jonathan Fine (were.Vire AT gmail DOT com)
pkgname=tiled
pkgver=1.2.5
pkgrel=2
pkgdesc='A general purpose tile map editor, built to be flexible and easy to use'
arch=('x86_64')
url='http://www.mapeditor.org'
license=('GPL')
replaces=('tiled-qt')
depends=('qt5-base' 'libgl' 'gtk-update-icon-cache' 'shared-mime-info' 'desktop-file-utils' 'libxkbcommon-x11')
makedepends=('mesa' 'qt5-tools' 'python3')
optdepends=('python3: Python plugin')
source=($pkgname-$pkgver.tar.gz::https://github.com/bjorn/tiled/archive/v${pkgver}.tar.gz)
sha512sums=('2b0009a8aa785d4289d6759369ff579e62e4fb969090ee6ba2b3fae4dd42d4e9562bd6d2a74ae372c07d9bbfd108390c79a48b49e05f0881e87497f6a727cb3a')

build() {
  cd $srcdir/$pkgname-$pkgver
  qmake -r INSTALL_HEADERS=yes RPATH=no PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install INSTALL_ROOT=$pkgdir
}
