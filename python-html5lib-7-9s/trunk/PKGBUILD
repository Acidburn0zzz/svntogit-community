# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Erol V. Aktay <e.aktay@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Philipp A. <flying-sheep@web.de>

pkgbase=python-html5lib-7-9s
pkgname=(python-html5lib-7-9s python2-html5lib-7-9s)
pkgver=0.9999999
pkgrel=3

pkgdesc="Python HTML parser/tokenizer based on the WHATWG HTML5 spec (v0.9999999)"
url="https://github.com/html5lib"
arch=('any')
license=('MIT')

makedepends=('python'
             'python-setuptools'
             'python-webencodings'
             'python2'
             'python2-setuptools'
             'python2-webencodings')
checkdepends=('python-six'
              'python-pytest'
              'python-mock'
              'python-lxml'
              'python2-lxml')

source=("python-html5lib-$pkgver.tar.gz::https://github.com/html5lib/html5lib-python/archive/$pkgver.tar.gz")

md5sums=('2ca78b1ec5852779bc121a97da6e8d4d')

prepare() {
	cp -a html5lib-python-$pkgver{,-python2}
}

package_python-html5lib-7-9s() {
	depends=('python-webencodings')
	conflicts=("python-html5lib")

	cd html5lib-python-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
	install -Dm755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-html5lib-7-9s() {
	depends=('python2-webencodings')
	conflicts=("python2-html5lib")

	cd html5lib-python-$pkgver-python2
	python2 setup.py install --root="$pkgdir" --optimize=1
	install -Dm755 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
