# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openmdao
pkgver=3.7.0
pkgrel=1
pkgdesc="An open-source high-performance computing platform"
url="https://github.com/OpenMDAO/OpenMDAO"
license=('Apache')
arch=('any')
depends=('python-six' 'python-numpydoc' 'python-scipy' 'python-sqlitedict' 'python-pycodestyle'
         'python-pydocstyle' 'python-testflo' 'python-parameterized' 'python-pyparsing'
         'python-networkx' 'python-sphinx' 'python-redbaron' 'python-mock' 'python-requests-mock'
         'python-tornado' 'python-pydoe2')
checkdepends=('python-mpi4py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/OpenMDAO/OpenMDAO/archive/$pkgver.tar.gz")
sha512sums=('605a3d2b954d9912c205aad6b12bbe5fe451a82a3489707bbc9421ad77d91c3e5c0ab2341d2dbb482e2a143816c7d321390394ba79b5ccf648f72bd0921e9a8e')

prepare() {
  cd OpenMDAO-$pkgver
  sed -e 's/from distutils.core/from setuptools/' \
      -e 's/==/>=/' \
      -i setup.py
}

build() {
  cd OpenMDAO-$pkgver
  python setup.py build
}

check() {
  cd OpenMDAO-$pkgver
  PYTHONPATH="$PWD/build/lib" testflo -n 16 --timeout=120 openmdao || echo "Tests failed"
}

package() {
  cd OpenMDAO-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
