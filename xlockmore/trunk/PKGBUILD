# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: dorphell <dorphell@archlinux.org>

pkgname=xlockmore
pkgver=5.47
pkgrel=1
pkgdesc="screen saver / locker for the X Window System"
arch=(i686 x86_64)
license=('BSD')
depends=(freetype2 gtk2 libxpm lesstif pam libxmu glu)
optdepends=('fortune-mod')
makedepends=(mesa)
url="http://www.tux.org/~bagleyd/xlockmore.html"
options=('!makeflags')
install=xlockmore.install
validpgpkeys=('1B79C8478F236DCDADC0D6ACCF159707377919AE')
source=(http://www.tux.org/~bagleyd/xlock/xlockmore-$pkgver.tar.xz{,.asc}
	LICENSE)
md5sums=('d059df32d1673ce5a434a98a49606f3b'
         'SKIP'
         'a64afab4283f53972a6702c2e59850d7')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --disable-setuid \
	      --enable-appdefaultdir=/usr/share/X11/app-defaults \
	      --enable-pam --with-gtk2 --without-esound --without-ftgl
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  make xapploaddir="$pkgdir"/usr/share/X11/app-defaults \
       mandir="$pkgdir"/usr/man/man1 \
       prefix="$pkgdir"/usr install
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/
}
