# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: Eric Bélanger <eric@archlinux.org>

pkgname=xaos
pkgver=3.7
pkgrel=1
pkgdesc='Interactive fractal zoomer'
arch=(x86_64)
url='https://xaos-project.github.io/'
license=(GPL)
makedepends=(gendesk git)
depends=(gsl gtk3)
#options=(!makeflags)
source=("git+https://github.com/xaos-project/XaoS#commit=1709b93d92eef541c66134db9776935a87b70f05") # tag: release-3.7
md5sums=('SKIP')

prepare() {
  gendesk -f -n \
    --pkgname=XaoS \
    --pkgdesc="$pkgdesc" \
    --exec=xaos \
    --icon=xaos \
    --categories='Application;Graphics'
}

build() {
  cd XaoS
  autoreconf
  ./configure \
    --prefix=/usr \
    --with-long-double=yes \
    --with-mitshm=no \
    --with-pthread=yes \
    --with-png=yes \
    --with-gsl=yes \
    --with-x11-driver=no \
    --with-gtk-driver=yes \
    --with-win32-driver=no \
    --with-aa-driver=yes
  make
}

package() {
  make -C XaoS DESTDIR="$pkgdir" install
  install -Dm644 "XaoS/src/ui/ui-drv/qt/images/xaosbig.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 XaoS.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim: ts=2 sw=2 et:
