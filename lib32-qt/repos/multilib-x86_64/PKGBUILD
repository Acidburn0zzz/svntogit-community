# $Id$
# Maintainer: Florian Pritz <flo@xssn.at>

_pkgbasename=qt
pkgname=lib32-$_pkgbasename
pkgver=4.6.3
pkgrel=2
pkgdesc='A cross-platform application and UI framework (32-bit)'
arch=('x86_64')
url='http://qt.nokia.com/'
license=('GPL3' 'LGPL')
depends=('lib32-libpng' 'lib32-fontconfig' 'lib32-libtiff' 'lib32-libmng' 'lib32-sqlite3' 'xdg-utils' 'ca-certificates' $_pkgbasename 'hicolor-icon-theme' 'lib32-alsa-lib' 'lib32-glib2' 'lib32-dbus-core' 'lib32-libxrender' 'lib32-libgl' 'lib32-libsm' lib32-openssl)
optdepends=('postgresql-libs' 'libmysqlclient' 'unixodbc')
makedepends=('lib32-mesa' 'inputproto' 'postgresql-libs' 'mysql' 'unixodbc' 'cups' 'libxfixes' 'gtk2' gcc-multilib)
options=('!libtool')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("ftp://ftp.qt.nokia.com/qt/source/${_pkgfqn}.tar.gz"
        'ca-certificates.patch')
md5sums=('5c69f16d452b0bb3d44bc3c10556c072'
         'd5c4cad7af93f1d736d4569a207bd739')

build() {
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  unset QMAKESPEC
  export QT4DIR=$srcdir/$_pkgfqn
  export PATH=${QT4DIR}/bin:${PATH}
  export LD_LIBRARY_PATH=${QT4DIR}/lib:${LD_LIBRARY_PATH}

  cd $srcdir/$_pkgfqn

  # see http://cvs.fedoraproject.org/viewvc/rpms/qt/devel/
  # use system ssl cert bundle
  patch -p1 -i $srcdir/ca-certificates.patch

  sed -i "s|-O2|$CXXFLAGS -m32|" mkspecs/common/g++.conf
  sed -i "/^QMAKE_RPATH/s| -Wl,-rpath,||g" mkspecs/common/g++.conf
  sed -i "/^QMAKE_LFLAGS\s/s|+=|+= $LDFLAGS|g" mkspecs/common/g++.conf

  ./configure -confirm-license -opensource -v -platform linux-g++-32 \
    -prefix /usr \
    -sysconfdir /etc \
    -plugindir /usr/lib32/qt/plugins \
    -translationdir /usr/share/qt/translations \
    -datadir /usr/share/qt \
    -docdir /usr/share/doc/qt \
    -examplesdir /usr/share/doc/qt/examples \
    -demosdir /usr/share/doc/qt/demos \
    -largefile \
    -system-sqlite \
    -xmlpatterns \
    -no-phonon \
    -no-phonon-backend \
    -svg \
    -webkit \
    -scripttools \
    -system-zlib \
    -system-libtiff \
    -system-libpng \
    -system-libmng \
    -system-libjpeg \
    -openssl-linked \
    -nomake demos \
    -nomake examples \
    -nomake docs \
    -nomake tools \
    -no-rpath \
    -optimized-qmake \
    -dbus \
    -no-separate-debug-info \
    -reduce-relocations \
    -gtkstyle \
    -opengl \
    -no-openvg \
    -glib \
    --libdir=/usr/lib32
  make
}

package() {
  cd $srcdir/$_pkgfqn
  make INSTALL_ROOT=$pkgdir install

  # Fix wrong path in pkgconfig files
  find ${pkgdir}/usr/lib32/pkgconfig -type f -name '*.pc' \
    -exec perl -pi -e "s, -L${srcdir}/?\S+,,g" {} \;
  # Fix wrong path in prl files
  find ${pkgdir}/usr/lib32 -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d;s/\(QMAKE_PRL_LIBS =\).*/\1/' {} \;

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
