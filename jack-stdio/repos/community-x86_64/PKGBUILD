# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=jack-stdio
pkgver=1.5
pkgrel=2
pkgdesc="Unix pipe audio-data from and to JACK"
arch=('x86_64')
url="https://github.com/x42/jack-stdio"
license=('GPL2')
groups=('pro-audio')
depends=('glibc' 'libjack.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/x42/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b3cb97ad46c0e875c57be1906eb3c51d3362e44080ef965107f5036650445edc1487ca86284db7553aeb5326e0d11d65062a6f4f8613d6bd5f27e3f3642ae4ab')

build() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
