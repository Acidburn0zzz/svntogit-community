# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.7.2
pkgrel=1
pkgdesc='A dock based on Plasma Frameworks'
arch=(i686 x86_64)
url='https://github.com/psifidotos/Latte-Dock'
license=(GPL)
depends=(plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
source=("https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz")
sha256sums=('8d3127c7e45b1573b6d5cda9450c1adab5d3dc213f3da5e96229ba55074a9b1c')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
