# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Michael Straube <straubem@gmx.de>

pkgname=latte-dock
pkgver=0.7.1
pkgrel=2
pkgdesc='A dock based on Plasma Frameworks'
arch=(i686 x86_64)
url='https://github.com/psifidotos/Latte-Dock'
license=(GPL)
depends=(plasma-framework hicolor-icon-theme)
makedepends=(extra-cmake-modules python)
source=("$pkgname-$pkgver.tar.gz::https://github.com/psifidotos/Latte-Dock/archive/v$pkgver.tar.gz"
         latte-dock-qt5.9.2.patch)
sha256sums=('b57a5495370fea6ae61f8045590d958c477efbe454690ea52d992d8ec76b80ea'
            '95f4b2437cad2e7f5bea993dc8b957ad1a501112a636e7f0ab69995b0ca0121d')

prepare() {
  mkdir -p build

  cd Latte-Dock-$pkgver
  patch -p1 -i ../latte-dock-qt5.9.2.patch # Fix crash with Qt 5.9.2
}

build() {
  cd build

  cmake ../Latte-Dock-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
