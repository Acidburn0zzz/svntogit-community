# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zsh-autosuggestions
pkgver=0.6.3
pkgrel=1
pkgdesc="Fish-like autosuggestions for zsh "
arch=('any')
url="https://github.com/zsh-users/zsh-autosuggestions"
license=('MIT')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('bafeca5c6ef3d315310976762fdc5db5ae3db3960c4ac17c1ac139a4d09c2733c24884e7f708881e3f3cacf96c39d9ddeac0b1ca6c8720d2febb41c9894d4ad3')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ${pkgname}{,.plugin}.zsh \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
