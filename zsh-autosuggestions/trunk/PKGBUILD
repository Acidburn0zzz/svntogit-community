# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zsh-autosuggestions
pkgver=0.6.1
pkgrel=1
pkgdesc="Fish-like autosuggestions for zsh "
arch=('any')
url="https://github.com/zsh-users/zsh-autosuggestions"
license=('MIT')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('bb3990ebafad7ea8ebcfea9de71b8290f855ec4f38f23f79bbb0928e8b10eea4305939d5f16449e1dcfa4023f35d5eb61080457e6e8ad1e9e5ccfe129551dc13')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ${pkgname}{,.plugin}.zsh \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
