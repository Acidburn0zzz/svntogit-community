# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zsh-autosuggestions
pkgver=0.4.2
pkgrel=2
pkgdesc="Fish-like autosuggestions for zsh "
arch=('any')
url="https://github.com/zsh-users/zsh-autosuggestions"
license=('MIT')
depends=('zsh')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zsh-users/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('67aae94211e6666d3a0f539509142d94b03ceb3d171e92b26fbf1a5a86185af515af07b9c244e258da321f1116624e6ed85eb77df73bd89e76246d415a18852b')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 644 ${pkgname}{,.plugin}.zsh \
    -t "${pkgdir}/usr/share/zsh/plugins/${pkgname}/"
  # docs
  install -vDm 644 {CHANGELOG,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
