# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Kevin Guan <KevinGuan.gm@gmail.com>

pkgname=iptux
pkgver=0.8.0
pkgrel=1
pkgdesc='LAN messenger and file sender, an IP Messenger client'
arch=('x86_64')
url='https://github.com/iptux-src/iptux/'
license=('GPL2')
depends=('google-glog' 'gtk3' 'jsoncpp' 'libsigc++')
makedepends=('appstream' 'gflags' 'meson')
checkdepends=('xorg-server-xvfb')
source=("https://github.com/iptux-src/$pkgname/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        'appstream.patch')
sha256sums=('ecaf1ff2b9486db753419a63aea52062fd46ecbdf32784ae96fae51eabc74407'
            '515b839bda7e67aedb3561ca86bac0d9354cfbd1d4613d9ac306c32b034e4de0')

prepare() {
  patch -d $pkgname-$pkgver -Np1 -i ../appstream.patch
}

build() {
  arch-meson $pkgname-$pkgver build
  meson compile -C build
}

check() {
  xvfb-run meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
