# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=5.0.10
pkgrel=2
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/ansible/ansible-lint"
license=('MIT')
depends=('python' 'ansible-base' 'python-ruamel-yaml' 'python-pyaml' 'python-rich' 'python-packaging'
          'python-wcmatch' 'python-enrich' 'python-tenacity')
checkdepends=('python-pytest')
optdepends=('yamllint: check for yaml syntax mistakes'
            'ansible: check official ansible collections')
source=(https://github.com/ansible/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        disable_use_scm_version.patch
        fix_32_excludes_paths_limit.patch::https://github.com/ansible-community/ansible-lint/commit/281402e4e28a67da9d4075d9faec03da2386c0ba.patch)
b2sums=('59924ea6298839bba9e26fdc550e2ab103e2577535d1da5428b1595ab077daf893cb13cfd924fefc4069e5c85f596d7a9069d3e410a243448b3bceac2a4c23a7'
        'a090862e657bfc29c89b23b2ecc8c585e15f098310b997e39600b6bdf0cd433e1045e2da348ce27dc2d3c0f9ef01950cf0ec5e7b071bc43d85161faabd8a1f60'
        '0293b57a5cf78d0adcef9d3bd44c4542a659104ad4d8d39000a61e7135cef2190e7f99fffdfa6fc2bdb85ef348db41ccb527b79814312b0682baf048f69a4adf')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  patch -Np1 < "${srcdir}"/disable_use_scm_version.patch
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg

  # fix infrastructure.git pls
  patch -Np1 < "${srcdir}"/fix_32_excludes_paths_limit.patch
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  PYTHONHASHSEED=0 python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
