# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Sander Boom <sanderboom@gmail.com>

pkgname=ansible-lint
pkgver=4.3.6
pkgrel=1
pkgdesc="Checks playbooks for practices and behaviour that could potentially be improved."
arch=('any')
url="https://github.com/willthames/ansible-lint"
license=('MIT')
depends=('python' 'ansible' 'python-ruamel-yaml' 'python-pyaml' 'python-rich')
makedepends=('git')
checkdepends=('python-pytest')
source=(https://github.com/willthames/ansible-lint/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ede70a1ddc95ecab5e486b79b0e90d6d1aa4b4da28a1887cb02ebb73a98edc23')

prepare() {
  cd ${pkgname}-${pkgver}
  # stop setuptools from using the scm version which doesn't exist in this context
  sed -i "/^\[metadata\]/a version = ${pkgver}" setup.cfg
  sed -i 's/use_scm_version/#use_scm_version/' setup.cfg
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
