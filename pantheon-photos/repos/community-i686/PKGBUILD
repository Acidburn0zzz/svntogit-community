# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-photos
pkgver=0.2
pkgrel=1
pkgdesc='The Pantheon Photos Manager'
arch=('i686' 'x86_64')
url='https://launchpad.net/pantheon-photos'
license=('GPL3')
depends=('cairo' 'desktop-file-utils' 'gdk-pixbuf2' 'glib2' 'glibc'
         'gst-plugins-base-libs' 'gstreamer' 'gtk3' 'hicolor-icon-theme'
         'libexif' 'libgee' 'libgexiv2' 'libgphoto2' 'libraw' 'pango' 'sqlite'
         'libgranite.so' 'libgudev-1.0.so')
makedepends=('cmake' 'intltool' 'vala' 'webkit2gtk')
optdepends=('webkit2gtk: Publishing plugins')
conflicts=('shotwell')
source=("https://launchpad.net/pantheon-photos/0.2.x/${pkgver}/+download/pantheon-photos-${pkgver}.tar.xz")
sha256sums=('4e2a14ffa955706ece4391c999d4bf9e353a16461ce3fefc3735d1975b595862')

prepare() {
  cd pantheon-photos-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd pantheon-photos-${pkgver}/build

  cmake .. \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBDIR='/usr/lib' \
    -DGSETTINGS_COMPILE='OFF'
  make
}

package() {
  cd pantheon-photos-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
