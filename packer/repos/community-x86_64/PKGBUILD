# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
pkgname=packer
replaces=('packer-io')
pkgver=1.3.1
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('487baf6515b0d85444f49b0a62ec29a4fbcf65d4c68c4b8a6119d4675a4ca65c13325aa972e22c8d6860d7fef5afe67a45633144ba4a9770dae90844edcd0026')

prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/hashicorp"
  mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}
build() {
  cd "src/github.com/hashicorp/${pkgname}"
  go build -o packer-binary
}

package() {
  cd "src/github.com/hashicorp/${pkgname}"
  install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
}

# vim:set et sw=2 ts=2 tw=79:
