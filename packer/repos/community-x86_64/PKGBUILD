# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
pkgname=packer
replaces=('packer-io')
pkgver=1.6.0
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('1ae912ea6f4906dff988b59d4b3e964fabd7fc13b8b936af02b5ed9963557cb057b26aabdc542091a7d31a162780b1c536fbc38f4ec3b42800f9167f37f814b1')
changelog=CHANGELOG.md

prepare() {
	export GOPATH="${srcdir}"
	export PATH="$PATH:$GOPATH/bin"
	mkdir -p "src/github.com/hashicorp"
	mv "${pkgname}-${pkgver}" "src/github.com/hashicorp/${pkgname}"
	sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${pkgname}/Makefile"
}

build() {
  cd "src/github.com/hashicorp/${pkgname}"
	go build \
  -trimpath \
  -mod=readonly \
  -buildmode=pie \
  -modcacherw \
  -ldflags "-extldflags \"${LDFLAGS}\"" \
  -o packer-binary
}

check() {
  cd "src/github.com/hashicorp/${pkgname}"
  go test ./...
}

package() {
	cd "src/github.com/hashicorp/${pkgname}"
	install -Dm755 packer-binary "${pkgdir}/usr/bin/packer"
	install -Dm644 contrib/zsh-completion/_packer "${pkgdir}"/usr/share/zsh/site-functions/_packer
}

# vim:set et sw=2 ts=2 tw=79:
