# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=clevis
pkgver=12
pkgrel=1
pkgdesc='Automated Encryption Framework'
arch=('x86_64')
url='https://github.com/latchset/clevis'
license=('GPL3')
depends=('bash' 'jose')
makedepends=('meson' 'asciidoc' 'bash-completion' 'dracut' 'libpwquality'
             'luksmeta' 'tpm2-tools' 'udisks2')
checkdepends=('tang')
optdepends=('cryptsetup: LUKS unlocker support'
            'curl: Tang pin support'
            'dracut: dracut unlocker support'
            'libpwquality: LUKS unlocker support'
            'luksmeta: LUKS and UDisks2 unlocker support'
            'nmap: dracut unlocker support'
            'tpm2-tools: TPM2 pin support'
            'udisks2: UDisks2 unlocker support')
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz")
sha512sums=('c71144590cf2528d20892d3ef49b2b1ea948286410385b15f6a3eda01539846c7154b6396b78c0ed8a28f3056b4c3f88ff043978e5483b2ac360ee0f156f78e7')

build() {
	cd "$pkgname-$pkgver"
	meson --prefix=/usr --libexecdir=/usr/lib --buildtype=plain build
	ninja -C build
}

check() {
	cd "$pkgname-$pkgver"
	# The LUKS tests are skipped when not running as root but work fine without
	# actual root privileges, so use fakeroot to bypass the root check
	fakeroot ninja -C build test
}

package() {
	cd "$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
}
