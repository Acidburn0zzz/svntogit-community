# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: uastasi <uastasi@archlinux.us>
pkgname=gnote
pkgver=0.7.3
pkgrel=4
pkgdesc="Experimental port of Tomboy to C++ "
arch=('i686' 'x86_64')
url="http://live.gnome.org/Gnote"
license=('GPL')
depends=('gtkmm' 'gconf' 'boost-libs' 'gtkspell' 'libxslt')
makedepends=('intltool' 'gnome-doc-utils' 'boost')
options=('!libtool')
install=gnote.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.7/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('4853315711baf7647cdae4c2f7d23e22e2b13322605efe3dd98796a06f928321')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    export CXXFLAGS="${CXXFLAGS} -DBOOST_FILESYSTEM_VERSION=2"
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/gnote \
        --disable-scrollkeeper \
        --disable-schemas-install \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
    install -m755 -d "${pkgdir}/usr/share/gconf/schemas"
    gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas" --domain gnote ${pkgdir}/etc/gconf/schemas/*.schemas
    rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
