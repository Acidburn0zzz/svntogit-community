# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Jeff Mickey <j@codemac.net>

pkgname=amsn
pkgver=0.98.4
pkgrel=2
pkgdesc="MSN client written in Tcl/Tk"
arch=('i686' 'x86_64')
url="http://amsn.sourceforge.net/"
license=('GPL2')
depends=('tk' 'tls')
makedepends=('libjpeg' 'libpng' 'farsight2' 'libv4l')
optdepends=('farsight2: for video conferencing')
changelog=$pkgname.changelog
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}-src.tar.bz2 amsn-0.98.4-libpng15.patch amsn-0.98.4-v4l2.patch)
md5sums=('96fd20e8709ced4443432d125a5e0a5a'
         '182eef321e78632499c24b3009c085ad'
         '02d4ec8faed521387d6e44481f43c91a')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # patch for libpng15
  patch -Np0 -i $srcdir/amsn-0.98.4-libpng15.patch

  # patch for linux kernel header changes
  patch -Np0 -i $srcdir/amsn-0.98.4-v4l2.patch
  # python2 fix
  for file in lang/missing.py plugins/music/infosongbird; do
      sed -i 's_/usr/bin/env python_/usr/bin/env python2_' ${file}
  done

  ./configure --prefix=/usr
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
