# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Matthew Sloan matthew@sloan.cc

pkgname=python-pint
pkgver=0.10.1
pkgrel=1
pkgdesc="A unit library for Python"
arch=('any')
license=('BSD')
url="https://pint.readthedocs.org"
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-runner' 'python-numpy' 'python-uncertainties')
source=("https://pypi.io/packages/source/P/Pint/Pint-$pkgver.tar.gz")
sha512sums=('e2c4c3e87eed997c58a45df5091064f495759c49bf365cd571e4e7d7aefd2b5fdf6699e8f9c03dc867dfdc57a39da03f63e2edb052595d9814d1981239641285')

prepare() {
  cd Pint-$pkgver
  sed -i 's/pkg_resources.extern.//' pint/quantity.py
}

check() {
  cd Pint-$pkgver
  python setup.py pytest
}

package() {
  cd Pint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
