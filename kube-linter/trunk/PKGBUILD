# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=kube-linter
pkgver=0.2.1
pkgrel=1
pkgdesc='Static analysis tool that checks Kubernetes YAML files and Helm charts'
url='https://github.com/stackrox/kube-linter'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/stackrox/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('36c46240057730941457012898daaa19df8242a0634fb345e61681d7f9ff6132799653c9ee55371dd10298239610cd399487128e7d8dbe73e373ae64f4bcbfc6')
b2sums=('974a4bab3878804f27c7b08b55d27efa65687f86269440627d1916128c98d88eda82c422f99f94f1094efe6eaa3ba4d19467f9278f7925fc7ef9c4a1e4cdc7ca')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    ./cmd/kube-linter
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 kube-linter -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
