# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=mypaint
pkgver=0.8.2
pkgrel=2
pkgdesc="A fast and easy painting application for digital painters, with brush dynamics"
arch=('i686' 'x86_64')
url="http://mypaint.intilinux.com/"
license=('GPL' 'LGPL')
depends=('pygtk' 'python-numpy' 'protobuf-python')
makedepends=('scons>=0.97' 'swig>=1.3.39')
source=(http://download.gna.org/$pkgname/$pkgname-$pkgver.tar.bz2)
md5sums=('4382912b5d70574a61dc53a2a10fe566')

build() {
  cd $pkgname-$pkgver

  # python2 fix
  sed -i 's_python generate.py_python2 generate.py_' brushlib/SConscript
  for file in $(find . -name '*.py' -print); do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' $file
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' $file
  done

  scons
  scons prefix="$pkgdir"/usr install
}
