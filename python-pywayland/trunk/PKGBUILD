# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.4
pkgrel=2
pkgdesc="Python bindings for the libwayland library"
arch=('x86_64')
url="https://github.com/flacjacket/pywayland"
license=('Apache')
depends=('python-cffi' 'wayland')
makedepends=('python-setuptools' 'wayland-protocols')
checkdepends=('python-pytest')
# TODO: remove provides/conflicts/replaces after 2022-01-01
provides=('pywayland')
conflicts=('pywayland')
replaces=('pywayland<=0.4.4-1')
# files required for tests missing in pypi sdist tarball: https://github.com/flacjacket/pywayland/issues/27
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flacjacket/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('2801d8e2f579fe59c3ecd7a61a95dac32931ff821836ad9dcc2a3ac347cce3d5ab3b45097b9104464028c8168ae1872a3809d1f8432c094f1ab6f8d0dbdd20a1')
b2sums=('8d90c6acac609dc507245fa57745897508d8cd0e2cda0879a9086712fc1abb4e5f661437d87b78d053b091642c9ceeac49536daf883b75eb3ad957d18e018f99')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp temp
}

build() {
  cd "$pkgname-$pkgver"
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  export XDG_RUNTIME_DIR="${PWD}/temp"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
