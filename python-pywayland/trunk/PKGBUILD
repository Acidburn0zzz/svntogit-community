# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pywayland
pkgname=python-pywayland
pkgver=0.4.5
pkgrel=1
pkgdesc="Python bindings for the libwayland library"
arch=('x86_64')
url="https://github.com/flacjacket/pywayland"
license=('Apache')
depends=('python-cffi' 'wayland')
makedepends=('python-setuptools' 'wayland-protocols')
checkdepends=('python-pytest')
# TODO: remove provides/conflicts/replaces after 2022-01-01
conflicts=('pywayland')
provides=('pywayland')
replaces=('pywayland')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b71266c704f94709d00369927189b3c5be3206e3695f29e02a2fba006ad4f2550e67a72e26d878fd798f03d66d8e9d5a00427fb57e01f589ba35b2e5c1ca481d')
b2sums=('8eb76edffff465bfb912df1e999dced9aa3fb986ec06b9f521737bedb2f070c318dc618003c99446d5fcb5681365b68c11ae382503f71e93bd14600d2e2ec3ab')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp temp
}

build() {
  cd "$pkgname-$pkgver"
  python pywayland/ffi_build.py
  python -m pywayland.scanner
  python setup.py build
}

check() {
  local _py_ver=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  cd "$pkgname-$pkgver"
  export PYTHONPATH="build/lib.linux-${CARCH}-${_py_ver}:${PYTHONPATH}"
  export XDG_RUNTIME_DIR="${PWD}/temp"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
