# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=tree-sitter
pkgver=0.20.1
pkgrel=1
pkgdesc='An incremental parsing system for programming tools'
arch=('x86_64')
url='https://github.com/tree-sitter/tree-sitter'
license=('MIT')
provides=('libtree-sitter.so')
depends=('gcc-libs')
makedepends=('git' 'rust')
source=("$url/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('172e9b2fba5d4610f663934dff1cf4580d09631a3d8d5d7249b6a80288aa9fdda9f5eac50a501935a87414563b46f843c3d98a1f7e45302a5c61dd3c08159606')

prepare() {
  cd $pkgname-$pkgver

  cargo fetch --locked
}

build() {
  cd $pkgname-$pkgver

  make

  cd cli

  cargo build --release --locked --offline --all-features
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" PREFIX=/usr install

  install -Dm 755 target/release/$pkgname -t "$pkgdir"/usr/bin

  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

