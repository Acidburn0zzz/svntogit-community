# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=1.7.1
pkgrel=1
pkgdesc="Adds caching support to your Flask application"
url="https://github.com/sh4nks/flask-caching"
license=('BSD')
arch=('any')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-xprocess' 'python-pylibmc'
              'python-redis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sh4nks/flask-caching/archive/v$pkgver.tar.gz")
sha512sums=('84d2e9de3d7a5416cfbb453e900923f3221c3c073eb1354885f43a28ea6a669a8f2cee753064394f325ed5e1901ea0b56f778cd3c766dcff217fc6953e1e235e')

prepare() {
  cd flask-caching-$pkgver
  sed -i 's/< *2.6/<3/' setup.py
}

build() {
  cd flask-caching-$pkgver
  python setup.py build
}

check() {
  cd flask-caching-$pkgver
  python setup.py pytest
}

package() {
  cd flask-caching-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
