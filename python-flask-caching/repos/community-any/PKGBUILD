# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-caching
pkgver=1.10.0
pkgrel=1
pkgdesc="Adds caching support to your Flask application"
url="https://github.com/sh4nks/flask-caching"
license=('BSD')
arch=('any')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-pytest-xprocess' 'python-pylibmc'
              'python-redis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sh4nks/flask-caching/archive/v$pkgver.tar.gz")
sha512sums=('84054ca748420574b645ba90ce625195c4d17cbf1fa7a56b8ade618db79375b902cb8e467c1486823dc2a0874e0730e73d5009c8a2f45caf2d0ad1d6e2c14eda')

prepare() {
  cd flask-caching-$pkgver
  sed -i 's/< *2.6/<3/' setup.py
}

build() {
  cd flask-caching-$pkgver
  python setup.py build
}

check() {
  cd flask-caching-$pkgver
  python setup.py pytest
}

package() {
  cd flask-caching-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
