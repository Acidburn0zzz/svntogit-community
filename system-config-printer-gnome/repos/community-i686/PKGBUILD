# $Id: PKGBUILD,v 1.6 2009/05/03 10:51:35 BaSh Exp $
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=system-config-printer-gnome
pkgver=1.1.7
pkgrel=3
pkgdesc="Pygtk CUPS Configuration"
url="http://cyberelk.net/tim/software/system-config-printer/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('system-config-printer>=1.1.7' 'python-notify' 'gnome-python')
makedepends=('xmlto' 'docbook-xsl' 'perlxml' 'desktop-file-utils' 'intltool')
source=(http://cyberelk.net/tim/data/system-config-printer/1.1/system-config-printer-$pkgver.tar.bz2)
md5sums=('a77bde5adc5ee8822b0764ff786ccc09')

build() {
  cd "$srcdir/system-config-printer-$pkgver"

  ./configure --prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc
  make || return 1
  make DESTDIR="$pkgdir" install

  # remove system-config-printer files conflicts
  rm -r ${pkgdir}/usr/share/system-config-printer/{config,debug,smburi}.py || return 1
  rm -r ${pkgdir}/usr/lib/python2.6/site-packages/cupshelpers/ || return 1
}
