# $Id$
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Valmantas Palikša <walmis@balticum-tv.lt>
# Contributor: blasse <koralik@gmail.com>
# Contributor: Maciej Wilczyński <wiqert@gmail.com>
# Contributor: Abhishek Dasgupta <abhidg@gmail.com>


pkgname=blueman
pkgver=1.20
pkgrel=1
pkgdesc="GTK+ bluetooth manager"
arch=('i686' 'x86_64')
license=("GPL")
url="http://blueman-project.org"
depends=('python-notify' 'bluez>=4.25' 'dbus-python' 'gtk2>=2.12' 'glib2>=2.12'\
         'pygobject>=2.12' 'startup-notification>=0.9' 'obex-data-server>=0.4.3'\
         'polkit-gnome' 'notification-daemon' 'xdg-utils' 'hicolor-icon-theme')
makedepends=('pyrex>=0.9.8.0' 'pkgconfig' 'intltool')
optdepends=("dnsmasq: For creating PAN network" "gconf>=2.0: For gconf configuration support" \
"pulseaudio: To use the pulseaudio plugin")
conflicts=(bluez-gnome)
options=(!libtool)
install=$pkgname.install
source=(http://download.tuxfamily.org/blueman/${pkgname}-${pkgver}.tar.gz)
build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
# Arch is still using NM 0.7.1, so we have to enable HAL.
	./configure --prefix=/usr --sysconfdir=/etc --enable-hal \
	   --libexecdir=/usr/lib/blueman/ --with-no-runtime-deps-check
	make clean
	make || return 1
	make DESTDIR="${pkgdir}" install || return 1
}

md5sums=('5ea6d6710783e27ad9fcc8db7fda7ace')
