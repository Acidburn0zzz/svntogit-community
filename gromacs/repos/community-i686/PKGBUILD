# $Id$
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Ricardo <rikardo.horo@gmail.com>

pkgname=gromacs
pkgver=4.5.5
pkgrel=1
pkgdesc='A fast Molecular Dynamics program based on the GROMOS force field'
url='http://www.gromacs.org/'
license=("GPL")
arch=('i686' 'x86_64')
depends=('fftw' 'lesstif' 'perl' 'libxml2' 'libsm' 'libx11')
options=('!libtool')
source=(ftp://ftp.gromacs.org/pub/gromacs/${pkgname}-${pkgver}.tar.gz)
md5sums=('6a87e7cdfb25d81afa9fea073eb28468')
sha1sums=('ce4b4f9a0453dd2ffea72f28ea0bc7bb7a72f479')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [ "$CARCH" == "i686" ] && SSE="--disable-ia32-sse"
  ./configure --prefix=/usr --enable-shared $SSE

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

# installing completions in correct location
  install -D -m644 scripts/completion.bash "${pkgdir}/etc/bash_completion.d/gromacs"
  install -D -m644 scripts/completion.zsh "${pkgdir}/usr/share/zsh/site-functions/gromacs"

# Cleaning up, kept the csh completion at default location
  rm "${pkgdir}"/usr/bin/{completion.bash,completion.zsh,GMXRC,GMXRC.bash,GMXRC.zsh}
}
