# $Id$
# Maintainer: Abhishek Dasgupta <abhidg@gmail.com>
# Contributor: Ricardo <rikardo.horo@gmail.com>

pkgname=gromacs
pkgver=4.5.3
pkgrel=1
pkgdesc='A fast Molecular Dynamics program based on the GROMOS force field'
url='http://www.gromacs.org/'
license=("GPL")
arch=('i686' 'x86_64')
depends=('fftw' 'lesstif' 'perl' 'libxml2' 'libsm' 'libx11')
options=('!libtool')
install=${pkgname}.install
source=("ftp://ftp.gromacs.org/pub/gromacs/${pkgname}-${pkgver}.tar.gz")
md5sums=('d5911585cd0e0b996dbbdcfb4c3bcf6b')
sha1sums=('c4845e5e62d1f53ee443301dad00138feba6d275')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  [ "$CARCH" == "i686" ] && SSE="--disable-ia32-sse"
  ./configure --prefix=/usr --enable-shared $SSE

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -d "${pkgdir}/usr/share/gromacs/shell-specific"
  mv "${pkgdir}"/usr/bin/GMXRC* "${pkgdir}/usr/share/gromacs/shell-specific"
  mv "${pkgdir}"/usr/bin/completion* "${pkgdir}/usr/share/gromacs/shell-specific"
}
