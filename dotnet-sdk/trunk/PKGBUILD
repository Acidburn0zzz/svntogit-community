# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Max Liebkies <mail@maxliebkies.de>

pkgname=dotnet-sdk
pkgver=2.1.200
pkgrel=1
pkgdesc='The .NET Core SDK'
arch=('any')
url='https://www.microsoft.com/net/core'
license=('MIT')
depends=('dotnet-runtime')
options=('staticlibs')
conflicts=('dotnet-sdk-2.0')
provides=('dotnet-sdk-2.0')
source=('https://download.microsoft.com/download/3/7/1/37189942-C91D-46E9-907B-CF2B2DE584C7/dotnet-sdk-2.1.200-linux-x64.tar.gz')
sha256sums=('58977b4b232f5fe97f9825340ce473cf1ec1bad76eb512fe6b5e2210c76c09de')

package() {
  install -dm 755 "${pkgdir}"/{opt/dotnet,usr/share/licenses}
  cp -dr --no-preserve='ownership' sdk "${pkgdir}"/opt/dotnet/
  ln -s dotnet-runtime "${pkgdir}"/usr/share/licenses/dotnet-sdk
}

# vim: ts=2 sw=2 et:
