# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgname=libzen
pkgver=0.4.37
pkgrel=1

pkgdesc="shared library for libmediainfo and mediainfo"
url="http://mediaarea.net"
arch=('x86_64')
license=('BSD')

depends=('gcc-libs')
makedepends=('libtool' 'automake' 'autoconf')

source=(https://mediaarea.net/download/source/libzen/$pkgver/libzen_$pkgver.tar.bz2)

sha256sums=('132cdc8b994e01de86436851d5b1c930f7a6cb683be3d94e5b46ddbfd45b78b6')

build() {
  cd ZenLib/Project/GNU/Library

  ./autogen.sh
  ./configure --prefix=/usr --enable-shared
  make
}

package() {
  cd ZenLib/Project/GNU/Library

  make DESTDIR="$pkgdir" install

  install -dm 755 "$pkgdir"/usr/include/ZenLib
  install -m 644 "$srcdir"/ZenLib/Source/ZenLib/*.h "$pkgdir"/usr/include/ZenLib

  for i in HTTP_Client Format/Html Format/Http; do
    install -dm0755 "$pkgdir"/usr/include/ZenLib/$i
    install -m0644 "$srcdir"/ZenLib/Source/ZenLib/$i/*.h "$pkgdir"/usr/include/ZenLib/$i
  done

  install -dm 755 "$pkgdir"/usr/lib/pkgconfig
  install -m 644 "$srcdir"/ZenLib/Project/GNU/Library/libzen.pc "$pkgdir"/usr/lib/pkgconfig

  sed -i -e 's|Version: $|Version: '$pkgver'|g' "$pkgdir"/usr/lib/pkgconfig/libzen.pc

  install -D -m 644 "$srcdir"/ZenLib/License.txt "$pkgdir"/usr/share/licenses/libzen/License.txt
}
