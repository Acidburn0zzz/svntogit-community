# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=weboob
pkgname=weboob-headless
pkgver=1.5
pkgrel=3
pkgdesc="Provides several applications to interact with a lot of websites. Headless version, for use in e.g. Cozy/Kresus."
arch=(any)
url="https://weboob.org/"
license=(AGPL3)
depends=(python-dateutil
         python-feedparser
         python-lxml
         python-prettytable
         python-requests
         python-cssselect
         python-html2text
         python-unidecode
         python-pillow
         python-simplejson
         python-yaml
         python-google-api-python-client
         python-six)
makedepends=(python-setuptools)
conflicts=(weboob)
source=("https://files.pythonhosted.org/packages/source/w/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('7ac1851281a127182f8e8630853c8ab2e635a534928b35150b75d5d0e24b3fb1')

build() {
    cd ${_pkgname}-${pkgver}
    python setup.py build --no-qt
}

package() {
    cd ${_pkgname}-${pkgver}
    python setup.py install --no-xdg --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
