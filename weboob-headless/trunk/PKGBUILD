# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=weboob
pkgname=weboob-headless
pkgver=1.5
pkgrel=1
pkgdesc="Provides several applications to interact with a lot of websites. Headless version, for use in e.g. Cozy/Kresus."
arch=(any)
url="https://weboob.org/"
license=(LGPL3)
depends=(python2-dateutil
         python2-feedparser
         python2-lxml
         python2-prettytable
         python2-requests
         python2-cssselect
         python2-html2text
         python2-unidecode
         python2-pillow
         python2-simplejson
         python2-yaml
         python2-google-api-python-client
         python2-futures
         python2-six)
makedepends=(python2-setuptools)
conflicts=(weboob)
source=("https://files.pythonhosted.org/packages/source/w/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('7ac1851281a127182f8e8630853c8ab2e635a534928b35150b75d5d0e24b3fb1')

build() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py build --no-qt
}

package() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py install --no-xdg --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
