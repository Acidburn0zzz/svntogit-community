# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkgname=weboob
pkgname=weboob-headless
pkgver=1.4
pkgrel=1
pkgdesc="Provides several applications to interact with a lot of websites. Headless version, for use in e.g. Cozy/Kresus."
arch=('any')
url="https://weboob.org/"
license=('AGPL3')
depends=('python2-dateutil'
         'python2-feedparser'
         'python2-lxml'
         'python2-prettytable'
         'python2-requests'
         'python2-cssselect'
         'python2-html2text'
         'python2-unidecode'
         'python2-pillow'
         'python2-simplejson'
         'python2-yaml'
         'python2-google-api-python-client'
         'python2-futures'
         'python2-six')
makedepends=('python2-setuptools')
conflicts=('weboob')
source=("https://files.pythonhosted.org/packages/source/w/${_pkgname}/${_pkgname}-${pkgver}+1.tar.gz")
sha256sums=('d377027a7c1cb7c43903cce15757e4eff3ced5ab0542dfc2752abe9e6d498b71')

build() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py build --no-qt
}

package() {
    cd ${_pkgname}-${pkgver}
    python2 setup.py install --no-xdg --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
}
