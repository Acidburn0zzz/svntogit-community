# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.0.2
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('ebdc511692cf8588a2188ce765a1f0d7238075eb924af04284df664f068d869882215036211082e0aa7609e4c967c160d0000223f5f75f317477b6a37f072dcb')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver/doc/examples"
  # extract AVL drumkit
  tar xvJf AVL-Drumkit.tar.xz
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DGKICK_REDKITE_SDK_PATH=/usr \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 doc/examples/*.{gkick,gkit} \
    -t "${pkgdir}/usr/share/${pkgname}/presets"
  for directory in doc/examples/*; do
    if [ -d "$directory" ]; then
      install -vDm 644 "$directory/"*.{gkick,gkit} \
        -t "${pkgdir}/usr/share/${pkgname}/presets/$(basename "$directory")"
    fi
  done
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
