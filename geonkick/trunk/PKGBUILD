# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.2.3
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('45faaddc1941d923d8103eb3eb8cce6fd3f2f475be2696aa87876bed1e40409870cbcc626443fcfb3912c92423a2523996b5dc00a53c2736cf6102209a53d1e6')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DGKICK_REDKITE_SDK_PATH='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -Mpack -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
