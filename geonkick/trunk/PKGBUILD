# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.0.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('acb0f17a7114fd1c791ffb9e2026c3385d065d3d992513cbd7f0d2f49b2ff1cf7db0916c14b7582b1d410d5e536032d49c0c6e65583892178d5b0b155fcf74e6')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DGKICK_REDKITE_SDK_PATH=/usr \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick" \
          || echo "Known to fail: https://gitlab.com/geontime/geonkick/issues/144"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 presets/*.gkick \
    -t "${pkgdir}/usr/share/doc/${pkgname}/presets"
  for directory in presets/*; do
    if [ -d "$directory" ]; then
      install -vDm 644 "$directory/"*.{gkick,gkit} \
        -t "${pkgdir}/usr/share/doc/${pkgname}/presets/$(basename "$directory")"
    fi
  done
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
