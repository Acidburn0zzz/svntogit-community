# Maintainer: David Runge <dave@sleepmap.de>
pkgname=geonkick
pkgver=1.9.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'desktop-file-utils' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libsndfile' 'libx11' 'shared-mime-info')
makedepends=('cmake' 'jack' 'lv2' 'sord' 'rapidjson' 'redkite')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('4ebaa9cbb8d22d618b6085d7d1d86223ae6deffef2e8c296baf27d55d613a51ae4a80d015efc7c97e651a78fa33cdb6d53c2b0782fe1637fc55b6674aad0e4b2')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -v build
}

build() {
  cd "$pkgname-$pkgver"
  (
    cd build
    cmake -DCMAKE_INSTALL_PREFIX=/usr \
          -DGKICK_REDKITE_SDK_PATH=/usr \
          ..
    make VERBOSE=1
  )
}

# lv2_validate still fails on itself:
# https://gitlab.com/lv2/lv2/issues/27
# check() {
#   cd "$pkgname-$pkgver"
#   (
#     cd build
#     lv2_validate "../plugin/lv2/${pkgname}.lv2/"*.ttl
#   )
# }

package() {
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 examples/*.gkick \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  for directory in examples/*; do
    if [ -d "$directory" ]; then
      install -vDm 644 "$directory/"*.gkick \
        -t "${pkgdir}/usr/share/doc/${pkgname}/examples/$(basename "$directory")"
    fi
  done
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
