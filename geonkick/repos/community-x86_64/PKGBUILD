# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.2.0
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('0e6e479785db0ced40f6abc05190856ab3f90575c56606e73e70aa30e75596ad36d3cdc2f6e5f3a15ea0a72f4fd6f9a2b8d7343a31e46faac98bb67ab42d966e')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DGKICK_REDKITE_SDK_PATH='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  # creating non-XDG_DATA_DIRS conforming directory, required for testing
  # https://gitlab.com/iurie-sw/geonkick/-/issues/155
  mkdir -vp "$HOME/.local/geonkick"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -Mpack -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
