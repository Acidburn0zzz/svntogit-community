# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.8.1
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=(x86_64)
url="https://github.com/free-sm/geonkick"
license=(GPL3)
groups=(pro-audio)
depends=(cairo gcc-libs glibc hicolor-icon-theme libx11)
makedepends=(cmake jack libsndfile lv2 rapidjson)
checkdepends=(lv2lint)
optdepends=(
  'lv2-host: for LV2 plugin'
  'jack: for standalone application'
)
source=("$pkgname-v$pkgver.tar.gz::https://github.com/free-sm/${pkgname}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('35c22d20067d2f636978a1ca4393f246ac3bbfe236e162f2123fd9b9f10bf3c23449af9b68091bba3224a2c5cd3c839b90c76fabeb215b09b0b05f6130b68906')
b2sums=('fb84a8e242f1c86cc468358dd3d75fedd1e502f6f131488f9f839cf57dba4e5f5c32ae3bb02bb97788fec8afebb130b43b2fb61244e71ee1d6575187676dd538')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -Wno-dev \
        -B build \
        -S "$pkgname-$pkgver"
  make VERBOSE=1 -C build
}

check() {
  cp -av build/plugin/lv2/*.so "$pkgname-$pkgver/plugin/lv2/${pkgname}.lv2/"
  lv2lint -Mpack -I "$pkgname-$pkgver/plugin/lv2/${pkgname}.lv2/" "http://geontime.com/geonkick" || echo "Known to fail: https://github.com/free-sm/geonkick/issues/164"
  rm -v "$pkgname-$pkgver/plugin/lv2/${pkgname}.lv2/"*.so
}

package() {
  depends+=(libsndfile.so)

  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -vDm 644 "$pkgname-$pkgver/data/${pkgname}.desktop" -t "${pkgdir}/usr/share/applications"
  install -vDm 644 "$pkgname-$pkgver/README.md" -t "${pkgdir}/usr/share/doc/${pkgname}"
}
