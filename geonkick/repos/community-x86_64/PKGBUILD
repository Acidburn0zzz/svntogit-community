# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=geonkick
pkgver=2.3.1
pkgrel=1
pkgdesc="A free software percussion synthesizer"
arch=('x86_64')
url="https://gitlab.com/iurie/geonkick"
license=('GPL3')
groups=('pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libx11')
makedepends=('cmake' 'jack' 'libsndfile' 'lv2' 'rapidjson' 'redkite')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
source=("https://gitlab.com/iurie/geonkick/-/archive/v${pkgver}/$pkgname-v$pkgver.tar.gz")
sha512sums=('073653a3c0d7019706557731c717e6842833d9b1c6686f989a269e887216c021cce6c34de7fcd73eaa380e49aae929699ca16a630e0584f309e7d33f29269816')
b2sums=('4e1cce9b3f32cd0bce01338a0d53b01e0587ee11d76b97f50b4a58b7c1b159f896e18812e5c4401ab2cce5f62e1ef220a22e5d3dbc270f8e7df3132781ab4f62')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DGKICK_REDKITE_SDK_PATH='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  cp -av build/plugin/lv2/*.so plugin/lv2/${pkgname}.lv2/
  lv2lint -Mpack -I "${PWD}/plugin/lv2/${pkgname}.lv2/" \
          "http://geontime.com/geonkick"
  rm -v plugin/lv2/${pkgname}.lv2/*.so
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make -C build DESTDIR="$pkgdir/" install VERBOSE=1
  install -vDm 644 "data/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
