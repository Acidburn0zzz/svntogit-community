# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testfixtures
pkgname=('python-testfixtures' 'python2-testfixtures')
pkgver=4.14.3
pkgrel=1
pkgdesc="A collection of helpers and mock objects that are useful when writing unit tests or doc tests"
arch=('any')
license=('MIT')
url="https://github.com/Simplistix/testfixtures"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-zope-component'
              'python2-zope-component' 'python-manuel' 'python2-manuel' 'python-nose-fixes'
              'python2-nose-fixes' 'python-django' 'python2-django')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Simplistix/testfixtures/archive/$pkgver.tar.gz")
sha512sums=('SKIP')

prepare() {
  cp -a testfixtures-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/testfixtures-$pkgver
  python setup.py build

  cd "$srcdir"/testfixtures-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/testfixtures-$pkgver
  nosetests3

  cd "$srcdir"/testfixtures-$pkgver-py2
  nosetests2
}

package_python-testfixtures() {
  depends=('python')

  cd testfixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}

package_python2-testfixtures() {
  depends=('python2')

  cd testfixtures-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 docs/license.txt "$pkgdir"/usr/share/licenses/$pkgname/license.txt
}
