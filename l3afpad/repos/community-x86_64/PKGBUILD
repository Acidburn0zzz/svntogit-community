# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Steven Honeyman <stevenhoneyman at gmail com>
# Contributor: nbvcxz (b4283)

pkgname=l3afpad
pkgver=0.8.18.1.11
pkgrel=4
pkgdesc="Simple plain text editor for GTK+ 3"
arch=('x86_64')
url="https://github.com/stevenhoneyman/l3afpad"
license=('GPL2')
depends=('gtk3')
makedepends=('intltool')
source=($pkgname-$pkgver.tar.gz::https://github.com/stevenhoneyman/l3afpad/archive/v$pkgver.tar.gz
        l3afpad.appdata.xml)
sha256sums=('86f374b2f950b7c60dda50aa80a5034b8e3c80ded5cd3284c2d5921b31652793'
            'f6a49d9bbe4406ecfc1859592742af530e5f9ae29e590b7a164e729034304f03')

prepare() {
  cd $pkgname-$pkgver
  ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
  rm -r "$pkgdir/usr/share/pixmaps/"
}
