# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Norberto Lopes <shelika@mail.telepac.pt>
# Contributor: Kao Dome <kaodome@gmail.com>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: Mathias Rohnstock <linksoft@gmx.de>

pkgname=libmicrohttpd
pkgver=0.9.57
pkgrel=1
pkgdesc="a small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
depends=('gnutls')
optdepends=('file' 'curl')
validpgpkeys=('289FE99E138CF6D473A3F0CFBF7AC4A5EAC2BAF4') # Evgeny Grin (Karlson2k) <k2k@yandex.ru>
source=(ftp://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('dec1a76487d7e48ad74b468a888bfda1c05731f185ff950f1e363ca9d39caf4e'
            'SKIP')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --disable-dependency-tracking --enable-largefile \
	--enable-curl --enable-messages --with-pic
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
  install -Dm644 "$srcdir"/${pkgname}-${pkgver}/src/include/platform.h \
	"$pkgdir"/usr/include/$pkgname/platform.h
  sed -i 's#Cflags: -I${includedir}#Cflags: -I${includedir} -I${includedir}/libmicrohttpd#' \
	"$pkgdir"/usr/lib/pkgconfig/libmicrohttpd.pc
}
