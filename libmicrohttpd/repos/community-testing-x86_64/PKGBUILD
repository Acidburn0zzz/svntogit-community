# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Norberto Lopes <shelika@mail.telepac.pt>
# Contributor: Kao Dome <kaodome@gmail.com>
# Contributor: Dmitrij D. Czarkoff <czarkoff@gmail.com>
# Contributor: Mathias Rohnstock <linksoft@gmx.de>

pkgname=libmicrohttpd
pkgver=0.9.60
pkgrel=1
pkgdesc="a small C library that is supposed to make it easy to run an HTTP server as part of another application."
arch=('x86_64')
url="http://www.gnu.org/software/libmicrohttpd/"
license=('LGPL')
depends=('gnutls')
checkdepends=('curl')
validpgpkeys=('289FE99E138CF6D473A3F0CFBF7AC4A5EAC2BAF4'  # Evgeny Grin (Karlson2k) <k2k@yandex.ru>
              'D8423BCB326C7907033929C7939E6BE1E29FC3CC') # Christian Grothoff <christian.grothoff@bfh.ch>
source=("https://ftp.gnu.org/gnu/libmicrohttpd/$pkgname-$pkgver.tar.gz"{,.sig}
        '0001-fix-tests-with-curl.patch')
sha256sums=('cd0e5d3f95a9e55ef7cdf4d0530e997ba00b8411af9149d9287db785d729c471'
            'SKIP'
            '750224c97e9a7f504869bd1e54d58dcb447e35bc831812adeb3f8f8edfb88cbe')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix tests with curl
  patch -Np1 < ../0001-fix-tests-with-curl.patch  
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-dependency-tracking \
    --disable-examples \
    --enable-curl \
    --enable-https \
    --enable-largefile \
    --enable-messages \
    --with-pic
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="$pkgdir" install
}
