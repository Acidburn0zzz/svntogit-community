# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pyalsa
pkgname=python-pyalsa
pkgver=1.1.6
pkgrel=7
pkgdesc="Python binding for the ALSA library"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
groups=(alsa)
depends=(alsa-lib python)
makedepends=(python-setuptools)
source=(
  "https://www.alsa-project.org/files/pub/pyalsa/pyalsa-1.1.6.tar.bz2"{,.sig}
  "${pkgname}-1.1.6-python3.10.patch::https://github.com/alsa-project/alsa-python/commit/5ea2f8709b4d091700750661231f8a3ddce0fc7c.patch"
)
sha512sums=('6c6aee1ad32c2208ab3f91defee5651cca8bb414479ff80e8a1bd6791ec1df2218a61dd8e7c01f95bd5d51807776034ff96cca2b935e9e4e23d13b6c3c40a0dd'
            'SKIP'
            '50175627a6791f41da235126d945374676fe3e49b5c70dede91e8a9d0a4cc7ca5f3cbaf7ce07b29274728fc92cb2471b2749ef6b27fe5905e2ac6d3e4013b637')
b2sums=('3a23dde67ec807d09b2e9ef0f893b8ec2f5902b0fa88f0f02f8127b7a5a8bdf3fa09397dd01ae1cafbce6b9c733a0eca65fda3cd6fd18e533dd4fbace284caa3'
        'SKIP'
        'e2740075e304796984e0d9f2549818567b1bb5478ee8c1a48607f6e04bb55f75523811515fd0f689b46c05e8a8cb61ec4331f3930de7d7d5fec67c744553fa74')
validpgpkeys=(F04DF50737AC1A884C4B3D718380596DA6E59C91)  # ALSA Release Team (Package Signing Key v1) <release@alsa-project.org>

prepare() {
  cd "${_name}-${pkgver}"
  # https://github.com/alsa-project/alsa-python/issues/2
  patch -Np1 -i ../"${pkgname}-1.1.6-python3.10.patch"
}

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
}
