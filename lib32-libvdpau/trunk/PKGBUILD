# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libvdpau
pkgver=1.2
pkgrel=1
pkgdesc='Nvidia VDPAU library'
arch=(x86_64)
url=https://gitlab.freedesktop.org/vdpau/libvdpau
license=(custom)
depends=(
  lib32-gcc-libs
  lib32-libxext
  libvdpau
)
makedepends=(
  dri2proto
  git
)
source=(git+https://gitlab.freedesktop.org/vdpau/libvdpau.git#tag=libvdpau-${pkgver})
sha256sums=(SKIP)

prepare() {
  cd libvdpau

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd libvdpau

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --sysconfdir='/etc'
  make
}

package() {
  cd libvdpau

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/{etc,usr/include}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s libvdpau "${pkgdir}"/usr/share/licenses/lib32-libvdpau
}

# vim: ts=2 sw=2 et:
