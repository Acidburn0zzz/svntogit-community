# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=ubuntu-sso-client
pkgname=('ubuntu-sso-client' 'ubuntu-sso-client-gtk' 'ubuntu-sso-client-qt')
pkgver=3.0.2
pkgrel=1
pkgdesc="Desktop service to allow applications to sign into Ubuntu services via SSO"
arch=('any')
url="https://launchpad.net/ubuntu-sso-client"
license=('GPL')
makedepends=('python2-distutils-extra' 'python2-pyqt')
source=(http://launchpad.net/$pkgname/stable-3-0/$pkgver/+download/$pkgname-$pkgver.tar.gz
        http://pkgbuild.com/~bgyorgy/sources/$pkgname-translations-20120513.tar.gz
        drop-lazr.patch)
md5sums=('362374e140d7330b64d90acdcb36769e'
         '527e137cfd24d0e52c0be1a6bd1fe250'
         '4ed4788f4baf7a657e5843aca6aca304')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Remove lazr.restfulclient dependency
  # https://code.launchpad.net/~ballogy/ubuntu-sso-client/drop-lazr/+merge/107153
  patch -Np0 -i "$srcdir/drop-lazr.patch"

  # Install language files
  echo 'ace af am ar ary ast be bg bn bs ca ca@valencia ckb cy cs da de el en_AU en_CA en_GB eo es et eu fa fi fo fr fy gd gl he hi hr hu hy id is it ja ka kk km kn ko ky lt lv ml ms my nb ne nl nn oc pl pt pt_BR ro ru shn si sk sl sq sr sv ta te th tr ug uk vec vi zh_CN zh_HK zh_TW' >po/LINGUAS
  rename $pkgname- '' ../po/$pkgname-*.po
  mv -f -t po ../po/*

  sed -i 's@^#!.*python$@#!/usr/bin/python2@' bin/*

  python2 setup.py build
}

package_ubuntu-sso-client() {
  depends=('python2-dbus' 'python2-httplib2' 'python2-oauth' 'python2-pyopenssl' 'twisted' 'python2-xdg' 'python2-gobject' 'libsoup-gnome' 'gsettings-desktop-schemas')
  install=$pkgname.install

  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1

  # Split GTK+ stuff
  [[ -d $srcdir/gtk ]] && rm -r "$srcdir/gtk/"
  mkdir "$srcdir"/gtk{,/{lib,share}}
  mv "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso/gtk" \
     "$pkgdir"/usr/lib/ubuntu-sso-client/ubuntu-sso-*-gtk \
     "$srcdir/gtk/lib/"
  mv "$pkgdir/usr/share/ubuntu-sso-client/gtk" \
     "$srcdir/gtk/share/"

  # Split Qt stuff
  [[ -d $srcdir/qt ]] && rm -r "$srcdir/qt/"
  mkdir "$srcdir"/qt{,/{lib,share}}
  mv "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso/qt" \
     "$pkgdir"/usr/lib/ubuntu-sso-client/ubuntu-sso-*-qt \
     "$srcdir/qt/lib/"
  mv "$pkgdir/usr/share/ubuntu-sso-client/qt" \
     "$srcdir/qt/share/"

  # Remove unneeded files
  rm -r "$pkgdir/usr/share/ubuntu-sso-client"
}

package_ubuntu-sso-client-gtk() {
  pkgdesc="GTK+ frontend for Ubuntu Single Sign-On client"
  depends=("ubuntu-sso-client=$pkgver" 'libwebkit3')

  cd "$srcdir/gtk"
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso" \
           "$pkgdir/usr/lib/ubuntu-sso-client" \
           "$pkgdir/usr/share/ubuntu-sso-client" \

  mv lib/gtk "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso"
  mv lib/ubuntu-sso-*-gtk "$pkgdir/usr/lib/ubuntu-sso-client"
  mv share/gtk "$pkgdir/usr/share/ubuntu-sso-client"
}

package_ubuntu-sso-client-qt() {
  pkgdesc="Qt frontend for Ubuntu Single Sign-On client"
  depends=("ubuntu-sso-client=$pkgver" 'python-imaging' 'python2-pyqt')

  cd "$srcdir/qt"
  mkdir -p "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso" \
           "$pkgdir/usr/lib/ubuntu-sso-client" \
           "$pkgdir/usr/share/ubuntu-sso-client" \

  mv lib/qt "$pkgdir/usr/lib/python2.7/site-packages/ubuntu-sso-client/ubuntu_sso"
  mv lib/ubuntu-sso-*-qt "$pkgdir/usr/lib/ubuntu-sso-client"
  mv share/qt "$pkgdir/usr/share/ubuntu-sso-client"
}
