# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: iom <pizorn@gmail.com>

pkgbase=python-pychm
pkgname=(python-pychm python2-pychm)
pkgver=0.8.5
pkgrel=1
pkgdesc="Python bindings for chmlib"
arch=('x86_64')
url="https://github.com/dottedmag/pychm"
license=('GPL')
depends=('chmlib')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=($pkgname-$pkgver.tar.gz::https://github.com/dottedmag/pychm/archive/v$pkgver.tar.gz)
sh256sums=('730c484583c14350e55dba1aa1b03360')
b2sums=('4671ccca56153ee493f239de46a01c9e878448f46ecb7e1a39156457562d7f7ac951007e04bd534ea26455da68607eab9b2e2266eed71a7b14c90f103888887e')

build() {
  cd "$srcdir/pychm-$pkgver"

  python setup.py build
  python2 setup.py build
}

check() {
  cd "$srcdir/pychm-$pkgver"

  # do glob expansion in variable assignment
  pythonpaths=("$PWD/build/lib.linux-$CARCH"-3* "$PWD/build/lib.linux-$CARCH-"2*)

  PYTHONPATH="${pythonpaths[0]}" pytest
  PYTHONPATH="${pythonpaths[1]}" pytest2
}

package_python-pychm() {
  depends+=('python')

  cd "$srcdir/pychm-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pychm() {
  depends+=('python2')

  cd "$srcdir/pychm-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
