# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Cilyan Olowen <gaknar@gmail.com>
# Contributor: fill <mailbox@fillo.ru>
# Contributor: Anton Shestakov <engored*ya.ru>

pkgname=flashrom
pkgver=1.0.1
pkgrel=1
pkgdesc="Utility for reading, writing, erasing and verifying flash ROM chips"
arch=(x86_64)
url="https://www.flashrom.org/"
license=(GPL)
depends=(libftdi pciutils libusb-compat)
optdepends=('dmidecode: for SMBIOS/DMI table decoder support')
source=("https://download.flashrom.org/releases/${pkgname}-v${pkgver}.tar.bz2"{,.asc})
sha256sums=(d579f8dda5b69eeab55a658d59ffbe185443e4e8477e751aaf38c36468cede44 SKIP)
validpgpkeys=(58A4868B25C7CFD662FB0132A3EB95B8D9780F68) # David Hendricks (packaging key) <david.hendricks@gmail.com>

prepare() {
  cd ${pkgname}-v${pkgver}
  sed -i 's:sbin:bin:g' Makefile
}

build() {
  cd ${pkgname}-v${pkgver}
  make
}

package() {
  cd ${pkgname}-v${pkgver}
  make PREFIX="${pkgdir}"/usr install
}
