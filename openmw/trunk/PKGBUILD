# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>

pkgname=openmw
pkgver=0.40.0
pkgrel=4
pkgdesc="An open-source engine reimplementation for the role-playing game Morrowind"
arch=('i686' 'x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz"
        "bullet-251.patch")
sha1sums=('a69aa56374c4e991950fa1b7bfec92ef790cbbed'
          'bef2eaf6135c58354395ef07b5ac07dbdd2387e3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  patch -Np1 < ${srcdir}/bullet-251.patch
}

build() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DDESIRED_QT_VERSION=5
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
