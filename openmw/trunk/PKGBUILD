# $Id:
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sandy Carter <bwrsandman@gmail.com>
pkgname=openmw
pkgver=0.43.0
pkgrel=4
pkgdesc="Open-source engine reimplementation for the role-playing game Morrowind"
arch=('x86_64')
url="http://www.openmw.org"
license=('GPL3' 'MIT' 'custom')
depends=('openal' 'openscenegraph' 'mygui' 'bullet' 'qt5-base' 'ffmpeg' 'sdl2' 'unshield' 'libxt')
makedepends=('cmake' 'boost' 'doxygen' 'ninja')
source=("https://github.com/OpenMW/openmw/archive/openmw-${pkgver}.tar.gz"
        'qt-5.11.patch')
sha512sums=('e902dd2274c05838bb1171f07d7a673fc7b85d1290a9e01c8ab89f1c279bd236332ecdc0f2b7944ec7209a0e6022ff472639b5724bf26904e3a2d338c7be401a'
            'a53775dc3dbdc69ed213fed86382a71768545f9950c59429410c731242d6b43b7e61d4be539c99b5ac87bf92e28be7eb43552c16d30103d1ce50edc853c987de')

prepare() {
  mkdir -p build
  cd $pkgname-$pkgname-$pkgver
  patch -Np1 -i ../qt-5.11.patch
}

build() {
  cd build
  cmake "../$pkgname-$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DDESIRED_QT_VERSION=5 \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim: ts=2 sw=2 et:
