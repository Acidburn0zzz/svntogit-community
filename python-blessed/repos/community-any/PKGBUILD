# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgname=python-blessed
pkgver=1.17.8
pkgrel=1
pkgdesc='A thin, practical wrapper around terminal styling, screen positioning, and keyboard input'
arch=(any)
url=https://github.com/jquast/blessed
license=(GPL)
depends=(
  python-six
  python-wcwidth
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-tox
)
source=(git+https://github.com/jquast/blessed.git#tag=83748bd9e97f9f2fd849588e677d541e162e3fc6)
sha256sums=(SKIP)

pkgver() {
  cd blessed

  git describe --tags
}

build() {
  cd blessed

  python setup.py build
}

check() {
  cd blessed

  tox -e py38
}

package() {
  cd blessed

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
