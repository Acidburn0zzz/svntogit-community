# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>
pkgname=lib32-json-c
pkgver=0.13
pkgrel=1
pkgdesc='A JSON implementation in C (32-bit)'
url='https://github.com/json-c/json-c/wiki'
license=(MIT)
arch=('x86_64')
depends=('lib32-glibc' 'json-c')
makedepends=(gcc-multilib libtool-multilib)
source=('https://github.com/json-c/json-c/archive/json-c-0.13-20171207.tar.gz')
sha256sums=('26e642456caab38aa9459279b9712ffec52f751e9f46641d28461c244bd6bae6')

build() {
  cd json-c-json-c-$pkgver-20171207
  export CC="gcc -m32" PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS="$CFLAGS -Wno-error"
  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

check() {
  cd json-c-json-c-$pkgver-20171207
  make check
}

package() {
  cd json-c-json-c-$pkgver-20171207
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s json-c "$pkgdir/usr/share/licenses/$pkgname"
}
