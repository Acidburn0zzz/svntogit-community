# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: congyiwu <congyiwu AT gmail DOT com>

_pkgbasename=json-c
pkgname=lib32-$_pkgbasename
pkgver=0.10
pkgrel=2
pkgdesc="A JSON implementation in C (32-bit)"
url="https://github.com/json-c/json-c/wiki"
license=("MIT")
arch=('x86_64')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib' 'libtool-multilib')
source=(https://github.com/downloads/$_pkgbasename/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz)
md5sums=('a4edc79410eb894f08d7d52ca9f88732')
options=(!libtool)
build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ## from http://pkgs.fedoraproject.org/cgit/json-c.git/tree/json-c.spec
  # Hack to get json_object_iterator.c compiled
  sed -e 's/json_object.c/json_object.c json_object_iterator.c/' \
      -e 's/json_object.h/json_object.h json_object_iterator.h/' \
      -e 's/json_object.lo/json_object.lo json_object_iterator.lo/' \
      -i Makefile.in

  ./configure --prefix=/usr --disable-static --libdir=/usr/lib32
  make
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  rm -r "$pkgdir/usr/include"

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
