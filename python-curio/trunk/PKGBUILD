# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=python-curio
pkgver=1.0
pkgrel=1
pkgdesc='Concurrent I/O'
arch=(any)
url=https://github.com/dabeaz/curio
license=(BSD)
depends=(python)
makedepends=(
  git
  python-pytest
  python-setuptools
  python-sphinx
)
provides=(python-multio-provider)
source=(git+https://github.com/dabeaz/curio.git#tag=639bd8e602eda07cad34f4eedcaed901fc546c7a)
sha256sums=(SKIP)

pkgver() {
  cd curio

  git describe --tags
}

build() {
  cd curio

  python setup.py build
}

check() {
  cd curio

  python setup.py test
}

package() {
  cd curio

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/python-curio/
}

# vim: ts=2 sw=2 et:
