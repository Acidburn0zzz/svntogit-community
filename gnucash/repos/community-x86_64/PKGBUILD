# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=3.3
pkgrel=2
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gmock libdbi-drivers libmariadbclient postgresql-libs)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        0001-Fixes-to-the-appdata-file.patch
        0001-Appdata-remove-old-property.patch)
sha256sums=('491735df36cb7ba9d04de28e28df53c3e5d928bda52009708d2f83ab9a42f820'
            'c3a198a51ec2da93353e1b3082f3d5f32b07abc8eab178a9c4747b19e9710dca'
            '483e9c868db591996edcd061b28ff60db3bba369a28c23142434d774b75a940c')

prepare() {
  cd $pkgname-$pkgver

  # Fixes to the appdata file
  # https://github.com/Gnucash/gnucash/pull/440
  # https://github.com/Gnucash/gnucash/pull/444
  patch -Np1 -i ../0001-Fixes-to-the-appdata-file.patch
  patch -Np1 -i ../0001-Appdata-remove-old-property.patch
}

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DHAVE_GWEN_GTK3=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
