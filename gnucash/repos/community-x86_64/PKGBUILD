# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Mark Schneider <queueRAM@gmail.com>

pkgname=gnucash
pkgver=3.3
pkgrel=1
pkgdesc='Personal and small-business financial-accounting application'
arch=(x86_64)
url='https://gnucash.org/'
license=(GPL)
depends=(aqbanking boost-libs guile libdbi webkit2gtk)
makedepends=(boost cmake gmock libdbi-drivers libmariadbclient postgresql-libs)
optdepends=('gnucash-docs: documentation'
            'libdbi-drivers: open and save data into SQL database'
            'perl-date-manip: stock information lookups'
            'perl-finance-quote: stock information lookups')
options=(!emptydirs)
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('491735df36cb7ba9d04de28e28df53c3e5d928bda52009708d2f83ab9a42f820')

build() {
  cd $pkgname-$pkgver

  # Do not treat warnings as errors
  sed -e 's|-Werror||g' -i CMakeLists.txt

  cmake	-DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DWITH_PYTHON=ON \
        -DHAVE_GWEN_GTK3=ON \
        -DCOMPILE_GSCHEMAS=OFF
  make
}

check() {
  cd $pkgname-$pkgver
  make -k check || : # 2 tests failed
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Delete the gnucash-valgrind executable because the source files
  # are not included with the package and the executable is hardlinked
  # to the location that it was built at.
  rm -f "$pkgdir/usr/bin/gnucash-valgrind"
}
