# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
#Contributor: Pete Morris <pete dot morris at gmail dot com>
pkgname=perl-crypt-des
pkgver=2.05
pkgrel=6
pkgdesc="Perl DES encryption module"
arch=('i686' 'x86_64')
url="http://search.cpan.org/~dparis/Crypt-DES"
license=("GPL" "PerlArtistic")
depends=('glibc')
source=("http://www.cpan.org/authors/id/D/DP/DPARIS/Crypt-DES-$pkgver.tar.gz"
	"perl-5.18.patch")
md5sums=('a8a0bea7064e11d2af434f3e468c17bb'
         '836d1a954281257efe00503d98b42dc8')

prepare() {
  cd $srcdir/Crypt-DES-$pkgver
  patch -p1 <$srcdir/perl-5.18.patch
}

build() {
  cd $srcdir/Crypt-DES-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  cd $srcdir/Crypt-DES-$pkgver
  make pure_install doc_install DESTDIR=${pkgdir}
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}
