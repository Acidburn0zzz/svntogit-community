# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.13.3
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-dock' 'deepin-polkit-agent' 'deepin-file-manager' 'deepin-wm'
         'deepin-metacity' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'golang-github-linuxdeepin-go-dbus-factory' 'golang-deepin-gir'
             'golang-deepin-lib' 'golang-deepin-dde-api' 'go-pie' 'git' 'jq'
             'golang-golang-x-net' 'golang-github-linuxdeepin-go-x11-client')
replaces=('deepin-wm-switcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz")
sha512sums=('7378e291017d29d881931e274f48419bcdd989a41ebe1fea09909d48cef42e4f071ce5d5b1c860a4d4d49433b4e55dd9d59b5e419d7156a34f261fd5a56e37f4')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/cryptix/wav

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install

  # Fix env file permission
  chmod +x "$pkgdir"/etc/X11/Xsession.d/*

  # Don't rely on deepin-session's location
  install -dm755 "$pkgdir"/etc/X11/xinit/xinitrc.d
  mv "$pkgdir"/etc/X11/Xsession.d/* "$pkgdir"/etc/X11/xinit/xinitrc.d/
  rmdir "$pkgdir"/etc/X11/Xsession.d
}
