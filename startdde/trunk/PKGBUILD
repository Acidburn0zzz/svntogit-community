# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.2
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm-switcher') # 'gcc-libs>=5.3.0-3')
makedepends=('cmake' 'coffee-script' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go' 'python2' 'git') # 'gcc-go')
groups=('deepin')
source=("git+https://cr.deepin.io/dde/startdde.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify
}

build() {
  cd "$srcdir/startdde"
  make # USE_GCCGO=1
}

package() {
  cd startdde
  make DESTDIR="${pkgdir}" install

  mv "$pkgdir"/lib/systemd "$pkgdir"/usr/lib/systemd
  rmdir "$pkgdir"/lib
  ln -sf ../dde-readahead.service "$pkgdir"/usr/lib/systemd/system/multi-user.target.wants/dde-readahead.service
}
