# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.12.0
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm' 'deepin-metacity' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'deepin-go-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go-pie' 'python2' 'git' 'jq' 'deepin-dbus-factory'
             'golang-github-burntsushi-xgb')
replaces=('deepin-wm-switcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz")
sha512sums=('453abbf5430892f4b8d439441cf5af0519c2230b0189fbf95198f713c7c36f416ff1e4c9547c551329ec68bc68beeb6c365762161f69eba07a276e37ad3999fe')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/cryptix/wav golang.org/x/net/context github.com/linuxdeepin/go-x11-client

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install

  # Fix env file permission
  chmod +x "$pkgdir"/etc/X11/Xsession.d/*

  # Don't rely on deepin-session's location
  install -dm755 "$pkgdir"/etc/X11/xinit/xinitrc.d
  mv "$pkgdir"/etc/X11/Xsession.d/* "$pkgdir"/etc/X11/xinit/xinitrc.d/
  rmdir "$pkgdir"/etc/X11/Xsession.d
}
