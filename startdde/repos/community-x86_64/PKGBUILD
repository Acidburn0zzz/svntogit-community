# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=startdde
pkgver=3.1.27
pkgrel=1
pkgdesc="starter of deepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/startdde"
license=('GPL3')
depends=('deepin-daemon' 'deepin-wm-switcher' 'libgnome-keyring')
makedepends=('cmake' 'coffeescript' 'deepin-dbus-factory' 'deepin-gir-generator' 'deepin-go-lib'
             'deepin-api' 'go-pie' 'python2' 'git')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/startdde/archive/$pkgver.tar.gz")
sha512sums=('12b3aecf23aaa779e5116d4d015db7adbf7418b6ec6f77bbe31d1653231a936563e8c6892c70d31b6fc6f76d456310405e72361e232bd9d15a8a30e9ea9e6f1c')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/cryptix/wav

  sed -i 's/sbin/bin/' startdde-$pkgver/Makefile
}

build() {
  cd startdde-$pkgver
  make
}

package() {
  cd startdde-$pkgver
  make DESTDIR="$pkgdir" install
}
