# Maintainer : Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=traefik
pkgver=2.1.3
pkgrel=1
pkgdesc="Simple tool for making locally-trusted development certificates"
arch=('x86_64')
url="https://containo.us/traefik/"
license=('MIT')
makedepends=('go-pie' 'git' 'go-bindata')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/traefik/releases/download/v${pkgver}/traefik-v${pkgver}.src.tar.gz")
sha512sums=('13daf6a7e90b34240c54949243df829843424fa87bc4bba65b294c84c6fc6b337941dc11849ec3fd16ab9e5d21f12d8913d91a938fa61047c711dcf6e498490f')

build() {
  go generate
  cd cmd/traefik
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}"
}

package() {
  install -Dm755 cmd/traefik/"${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 contrib/systemd/traefik.service "${pkgdir}/usr/lib/systemd/system/traefik.service"
}
