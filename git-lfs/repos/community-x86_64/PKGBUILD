# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=git-lfs
pkgver=2.4.0
pkgrel=1
pkgdesc="Git extension for versioning large files"
arch=('x86_64')
url="https://git-lfs.github.com"
license=('MIT')
makedepends=('go-pie' 'ruby-ronn' 'glide')
depends=('git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/git-lfs/git-lfs/archive/v${pkgver}.tar.gz")
sha256sums=('3f26cd020a965f61c8e8f124a02794d86f46f2fc4627288afe70ed43f398b121')

prepare(){
  export GOPATH="${srcdir}"
  mkdir -p src
  mv "${pkgname}-${pkgver}"/vendor/* src/
  mkdir -p src/github.com/git-lfs
  ln -rTsf "${pkgname}-${pkgver}" src/github.com/git-lfs/git-lfs

  cd "${pkgname}-${pkgver}"
  glide install
}

build(){
  cd "${pkgname}-${pkgver}"
  export GOPATH="${srcdir}"
  go build -o "${pkgname}" .
  ./script/man
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 $pkgname "$pkgdir"/usr/bin/$pkgname
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -d "$pkgdir"/usr/share/man/man1
  install -Dm644 man/*.1 "$pkgdir"/usr/share/man/man1
}
