# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=1.8.2
pkgrel=1
pkgdesc="A new approach to API documentation in Sphinx."
arch=('any')
url="https://github.com/readthedocs/sphinx-autoapi"
license=('MIT')
depends=('python-astroid' 'python-jinja' 'python-sphinx' 'python-unidecode'
'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# TODO: package python-go and python-dotnet
# optdepends=('python-go' 'python-dotnet')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('38a78aa2d578fc0a46143f176e5720b4572aac869a98571c3db3163dd73526bb')
b2sums=('4468c5f0415f1a575a9c562fdb1c8f235ee28502babce5ac8a71728687cd6dbefd4965cd1171ebd5db8c9551008e10c764d82a2520ad5977db6b488e8b50ee77')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
