# Maintainer: David Runge <dvzrv@archlinux.org>

_name=sphinx-autoapi
pkgname=python-sphinx-autoapi
pkgver=1.2.1
pkgrel=1
pkgdesc=" A new approach to API documentation in Sphinx."
arch=('any')
url="https://github.com/readthedocs/sphinx-autoapi"
license=('MIT')
depends=('python-astroid' 'python-jinja' 'python-sphinx' 'python-unidecode'
'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest')
# TODO: package python-go and python-dotnet
# optdepends=('python-go' 'python-dotnet')
# tests not in sdist tarballs on pypi:
# https://github.com/readthedocs/sphinx-autoapi/issues/192
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/readthedocs/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('1086b074a0ac09b6b7481bae3f7a06f1c6960bfead87b15223b12210a3de4ed14b241f8bda8ca754feef9fd543e07eec8f97c92936bbc301470f1bc49e096161')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v || echo "Tests will fail without python-{go,dotnet}."
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
