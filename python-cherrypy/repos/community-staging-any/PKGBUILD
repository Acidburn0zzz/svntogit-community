# Maintainer:  Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Laurent Soest <laurent.soest@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: Armando M. Baratti <amblistas@ajato.com.br>
# Contributor: Florian Richter <Florian_Richter@gmx.de>

pkgbase='python-cherrypy'
_pkgbase="${pkgbase#python-}"
pkgname=("python-${_pkgbase}" "python2-${_pkgbase}")
pkgver=17.0.0
pkgrel=2
pkgdesc='A pythonic, object-oriented web development framework'
arch=('any')
url='https://cherrypy.org'
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python-six' 'python2-six' 'python-cheroot'
  'python2-cheroot' 'python-portend' 'python2-portend')
checkdepends=('python-tox' 'python2-tox')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/${_pkgbase}/${_pkgbase}/archive/v$pkgver.tar.gz")
sha512sums=('a80cbca34a1fe66a0dd83dce1d241f9b4b0c949fa05c9f982f8785c164debf969f4f222ec9e05a8e37fa272ea459e13bb06c50850dcfb87c0a281a67dd0673e6')

prepare() {
  cp -r "${srcdir}/${_pkgbase}-${pkgver}" "${srcdir}/${_pkgbase}-${pkgver}-py2"

  # fix python interpreter for python2 stuff
  find "${srcdir}/${_pkgbase}-${pkgver}-py2" -type f -name '*.py' \
    -exec sed -i 's|^#!/usr/bin/python$|#!/usr/bin/env python2|1' {} +
}

build() {
  # setuptools wont find version from git tag
  export SETUPTOOLS_SCM_PRETEND_VERSION="${pkgver}"
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python ./setup.py build

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  python2 ./setup.py build
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  tox

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"
  tox2
}

package_python-cherrypy() {
  depends=('python' 'python-six' 'python-cheroot' 'python-portend' 'python-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}"

  python ./setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}

package_python2-cherrypy() {
  depends=('python2' 'python2-six' 'python2-cheroot' 'python2-portend' 'python2-jaraco')

  cd "${srcdir}/${_pkgbase}-${pkgver}-py2"

  python2 ./setup.py install --root="${pkgdir}" --optimize=1
  mv "${pkgdir}/usr/bin/cherryd" "${pkgdir}/usr/bin/cherryd2"

  install -Dm644 LICENSE.md \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md"
}
