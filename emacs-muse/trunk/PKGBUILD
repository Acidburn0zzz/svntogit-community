# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor:  Gour <gour@mail.inet.hr>

pkgname=emacs-muse
_pkgname=muse
pkgver=3.20
pkgrel=5
pkgdesc="Publishing environment for Emacs"
url="http://mwolson.org/projects/EmacsMuse.html"
arch=('any')
license=('GPL3')
depends=('emacs' )
install=${pkgname}.install
validpgpkeys=('C7A3A125887C60A1A074B510FB51E8D8F3A8D319')
source=(http://download.gna.org/muse-el/muse-$pkgver.tar.gz{,.sig})
sha256sums=('080990e105050fc963bb1938209cc843f259488ae5c8373c5487f99a8dc383b6'
            'SKIP')

prepare() {
  cd "$srcdir"/muse-$pkgver
  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' experimental/muse-mathml.el
  cp Makefile.defs.default Makefile.defs
  sed -i -e "s|\(PREFIX.*= \)/usr/local|\1/usr|" Makefile.defs
}

build() {
  cd "$srcdir"/muse-$pkgver
  make
}

package() {
  cd "$srcdir"/muse-$pkgver
  make DESTDIR="$pkgdir" install
}
