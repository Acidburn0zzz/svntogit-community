# $Id$
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: danst0 <danst0@west.de>

pkgname=avr-gcc
pkgver=4.6.2
pkgrel=1
pkgdesc="The GNU avr Compiler Collection"
arch=('i686' 'x86_64')
license=('GPL' 'LGPL' 'custom')
url="http://gcc.gnu.org/"
depends=('binutils-avr>=2.21' 'cloog' 'ppl' 'gcc-libs>=4.6.0' 'libmpc')
provides=("gcc-avr=$pkgver")
replaces=('gcc-avr')
options=('!ccache' '!distcc' '!emptydirs' '!libtool' '!strip')
source=(http://ftp.gnu.org/gnu/gcc/${pkgname/avr-}-${pkgver}/gcc-{core,g++}-${pkgver}.tar.bz2)
md5sums=('0c0e7e35d2215e19de9c97efba507553'
         '0d75ca7ca35b1e7f252223f9d23a6ad1')

build() {
	export CFLAGS="-O2 -pipe"
  	export CXXFLAGS="-O2 -pipe"

  	cd "${srcdir}/${pkgname/avr-}-${pkgver}" 

  	mkdir build
  	cd build
  	../configure --disable-libssp \
        	--disable-nls \
		--enable-languages=c,c++ \
		--infodir=/usr/share/info \
               	--libdir=/usr/lib \
               	--libexecdir=/usr/lib \
               	--mandir=/usr/share/man \
               	--prefix=/usr \
               	--target=avr \
               	--with-gnu-as \
               	--with-gnu-ld \
               	--with-as=/usr/bin/avr-as \
               	--with-ld=/usr/bin/avr-ld

  	make
}

package() {
	cd "${srcdir}/${pkgname/avr-}-${pkgver}"

  	cd build
  	make -j1 DESTDIR=${pkgdir} install

  	install -Dm644 "${srcdir}/${pkgname/avr-}-${pkgver}/COPYING.RUNTIME" \
		"${pkgdir}/usr/share/licenses/${pkgname}/RUNTIME.LIBRARY.EXCEPTION"

  	rm -f ${pkgdir}/usr/lib/libiberty.a
  	rm -rf ${pkgdir}/usr/share/man/man7
  	rm -rf ${pkgdir}/usr/share/info
}

md5sums=('780f614ab18c7a9066dec6387d7490b2'
         '87ecd60431e41096419dd8a10f76e46b')
