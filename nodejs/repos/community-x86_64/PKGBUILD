# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: James Campos <james.r.campos@gmail.com>
# Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
# Contributor: Dongsheng Cai <dongsheng at moodle dot com>
# Contributor: Masutu Subric <masutu.arch at googlemail dot com>
# Contributor: TIanyi Cui <tianyicui@gmail.com>

pkgname=nodejs
pkgver=0.4.12
pkgrel=2
pkgdesc='Evented I/O for V8 javascript'
arch=('i686' 'x86_64')
url='http://nodejs.org/'
license=('MIT')
depends=('python2')
checkdepends=('curl') # curl used for check()
optdepends=('openssl: TLS support')
source=("http://nodejs.org/dist/node-v${pkgver}.tar.gz")
sha256sums=('c01af05b933ad4d2ca39f63cac057f54f032a4d83cff8711e42650ccee24fce4')

build() {
  cd node-v${pkgver}

  # python2 fix
  for file in $(find . -name '*.py' -print) wscript tools/waf-light tools/node-waf
  do
    sed -i 's_^#!.*/usr/bin/python_#!/usr/bin/python2_' ${file}
    sed -i 's_^#!.*/usr/bin/env.*python_#!/usr/bin/env python2_' ${file}
  done
  sed -i "s|cmd_R = 'python |cmd_R = 'python2 |" wscript
  sed -i "s|python |python2 |" Makefile

  ./configure \
    --prefix=/usr

  make
}

check() {
  cd node-v${pkgver}

  make test
}

package() {
  cd node-v${pkgver}

  make DESTDIR=${pkgdir} install

  install -D -m644 LICENSE \
    ${pkgdir}/usr/share/licenses/nodejs/LICENSE
}

# vim:set ts=2 sw=2 et:
