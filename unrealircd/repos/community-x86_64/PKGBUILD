# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Zerial <fernando@zerial.org>

pkgname=unrealircd
pkgver=4.0.0
pkgrel=1
pkgdesc="Open Source IRC Server"
arch=('i686' 'x86_64')
url="http://unrealircd.com"
license=('GPL2')
depends=('openssl' 'curl' 'c-ares')
conflicts=('ircd')
provides=('ircd')
backup=('etc/unrealircd/unrealircd.conf')
install=unrealircd.install
source=(https://www.unrealircd.org/unrealircd4/unrealircd-$pkgver.tar.gz
	unrealircd.service
	unrealircd.tmpfiles.d
	config.settings
	install.pl
	arch-fixes.patch)
md5sums=('ea5922554cda40e39a2d6fd7520284f1'
         '677d8de0bae770488a2c1730f9475a51'
         '3ec519ea7dbe99696eb6c51dfc0d382a'
         'fad1a30c32bf0c75b23e6813d062e19a'
         '3a23792b03420fc578cf6688b1574ae3'
         '5f4bf9f99f6c78bc9e6ee4ec66b021fa')

prepare() {
  cd $srcdir/unrealircd-$pkgver
  cp $srcdir/config.settings .
}

build() {
  cd $srcdir/unrealircd-$pkgver
  ./Config -quick
  make
}

package() {
  cd $srcdir/unrealircd-$pkgver

  export pkgdir
  make INSTALL=$srcdir/install.pl install
  rm -rf $pkgdir/tmp $pkgdir/usr/unrealircd

  install -Dm0644 $srcdir/unrealircd.service $pkgdir/usr/lib/systemd/system/unrealircd.service
  install -Dm0644 $srcdir/unrealircd.tmpfiles.d $pkgdir/usr/lib/tmpfiles.d/unrealircd.conf
}
