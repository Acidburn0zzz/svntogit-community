# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=0.15.0
pkgrel=3
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-yaml')
optdepends=('python-yaml: for YAML support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bw2/ConfigArgParse/archive/$pkgver.tar.gz")
sha512sums=('480bd0ef404d19c43fa9c5dffe6e177731011206aca4347a7b4b286783a6590fffe2fabdbf296cda515ccac56c81ef0856faa15bb5fc0b5b2b38f768441f505c')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  python setup.py test || warning 'Ignoring TestWrappingMetavar failure'
}

package() {
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
