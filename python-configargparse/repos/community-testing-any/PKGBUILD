# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.0
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-tests' 'python-yaml')
optdepends=('python-yaml: for YAML support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bw2/ConfigArgParse/archive/$pkgver.tar.gz")
sha512sums=('8e2af22dfa08bcf4665289285cc8f94aca00b650cd9521e9c02d4d83b546f4d79f2fd2cae2818ad0f2d38a16907e145ca7165071b78c557fe0be1797609e1cae')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  python setup.py test || warning 'Ignoring TestWrappingMetavar failure'
}

package() {
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
