# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-configargparse
pkgver=1.2
pkgrel=1
pkgdesc='A drop-in replacement for argparse that allows options to also be set via config files and/or environment variables'
arch=('any')
url='https://github.com/zorro3/ConfigArgParse'
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-tests' 'python-yaml')
optdepends=('python-yaml: for YAML support')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bw2/ConfigArgParse/archive/$pkgver.tar.gz")
sha512sums=('50a125e2c03f5ec0ccd843a44109bd065c39cb3a5b72cf0ca0ab361cc1a97de56f2eef0bf04aac2cda4b302ae67cd422bf04c8e09b1bbedca3ae19a28a5f2eb9')

build() {
  cd ConfigArgParse-$pkgver
  python setup.py build
}

check() {
  cd ConfigArgParse-$pkgver
  python setup.py test || warning 'Ignoring TestWrappingMetavar failure'
}

package() {
  cd ConfigArgParse-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
