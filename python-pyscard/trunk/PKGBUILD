# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>
# Maintainer: Bruno Parmentier <dev@brunoparmentier.be>

pkgname=python-pyscard
_pyname=pyscard
pkgver=1.9.4
pkgrel=2
pkgdesc='Framework for building smart card aware applications in Python'
arch=('i686' 'x86_64')
url='http://pyscard.sourceforge.net/'
license=('LGPL')
depends=('python' 'pcsclite')
makedepends=('swig' 'python-setuptools')
validpgpkeys=('535E3696356979F4932CB7443F4A8A8FE07F1CF9') # Ludovic Rousseau <rousseau@debian.org>
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/LudovicRousseau/${_pyname}/archive/release-${pkgver}.tar.gz"
	"${pkgname}-${pkgver}.tar.gz.asc::https://github.com/LudovicRousseau/${_pyname}/releases/download/release-${pkgver}/${_pyname}-release-${pkgver}.tar.gz.asc")
sha256sums=('c3c7d0667909cd3884fe9550c63647311fc3299439caba440e87dd3b3f4d88e8'
            'SKIP')

package() {
	cd "${srcdir}/${_pyname}-release-${pkgver}/"

	python setup.py build_ext install --root="${pkgdir}/" --optimize=1
	install -D -m0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

