# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=11.3.4
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="https://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("https://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('2d93baa2a1c6c7477113dc71413dedf74bcc8d9477427c3d1097be68fa30be39')

prepare() {
  cd $pkgname-$pkgver/src
  rm -r libtool-origin
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=x86 \
    LIBTOOL=libtool CXXFLAGS="${CXXFLAGS}" LDFLAGS="${LDFLAGS}"
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
