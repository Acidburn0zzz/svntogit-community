# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=11.4.1
pkgrel=2
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="https://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("https://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz"
         ntl-gf2x-1.3.patch)
sha256sums=('a30687c4fbb8f114200426d2b1ece840bd024f64e2c5c6920b2d11ebcd82620e'
            '45d08b12ba4d0906d6bcdafb466a0c55c1ea13bb4af87b76ec9d400458ab4a3c')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ntl-gf2x-1.3.patch # Fix build with gf2x 1.3

  cd src
  rm -r libtool-origin
}

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=x86 \
    LIBTOOL=libtool CXXFLAGS="${CXXFLAGS}" LDFLAGS="${LDFLAGS}"
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
