# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Brad Conte <brad AT bradconte.com>

pkgname=ntl
pkgver=11.1.0
pkgrel=1
pkgdesc="A Library for doing Number Theory"
arch=(x86_64)
url="http://www.shoup.net/ntl/"
license=(LGPL)
depends=(gf2x gmp)
makedepends=()
source=("http://www.shoup.net/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('bb4ae214886f95a044ef16fdfd909f8d3181b288470ea7077c9f23d14047302f')

build() {
  cd $pkgname-$pkgver/src
  ./configure DEF_PREFIX=/usr SHARED=on NTL_GF2X_LIB=on NATIVE=off TUNE=generic
  make
}

check() {
  cd $pkgname-$pkgver/src
  make -k check
}

package() {
  cd $pkgname-$pkgver/src
  make PREFIX="$pkgdir/usr" install
}
