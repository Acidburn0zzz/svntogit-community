# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-test-fatal
pkgver=0.006
pkgrel=1
pkgdesc="incredibly simple helpers for testing code with exceptions"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-carp' 'perl-exporter>=5.57' 'perl-test-simple'
         'perl-try-tiny>=0.07')
makedepends=('perl-extutils-makemaker>=6.30')
checkdepends=('perl' 'perl-test-simple')
url=https://metacpan.org/release/Test-Fatal
source=("http://search.cpan.org/CPAN/authors/id/R/RJ/RJBS/Test-Fatal-${pkgver}.tar.gz")
md5sums=(a984aa4d4088892c0454cbfce35e54d9)
sha512sums=(1e38a0048f26b8fd9dff4edcf7be5471c1a356d65b49f5096a1ab7215f970b0a90388dd607b36b0872e7cff85b72c0e02dbf42fd39262f978f349fadd67cefa5)
_distdir="${srcdir}/Test-Fatal-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
