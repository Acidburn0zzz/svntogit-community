# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-hacking
pkgver=3.0.1
pkgrel=1
arch=('any')
pkgdesc='OpenStack Hacking Guideline Enforcement'
url='https://docs.openstack.org/developer/hacking'
license=('Apache')
depends=('flake8')
makedepends=('python-pbr' 'python-setuptools')
checkdepends=('python-oslotest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/hacking/archive/$pkgver.tar.gz")
sha512sums=('721b99f729536063d25a604fca6e3366e757f0499cb738b76134c6e82b86d90fc2feb4a78e09b06072cd35b4eaad5027cf1a7fac455b7b59f6620aba9f1c3c6d')

export PBR_VERSION=$pkgver

prepare() {
  cd hacking-$pkgver
  sed -i 's/<.*//' requirements.txt
}

build() {
  cd "$srcdir"/hacking-$pkgver
  python setup.py build
}

check() {
  cd hacking-$pkgver
  stestr run
}

package_python-hacking() {
  cd hacking-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
