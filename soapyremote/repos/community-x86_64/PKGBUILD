# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=soapyremote
_pkgname=SoapyRemote
pkgver=0.5.0
_gitver=soapy-remote-$pkgver
pkgrel=1
pkgdesc='Use any Soapy SDR remotely'
arch=('x86_64')
url='https://github.com/pothosware/SoapyRemote'
license=('custom:Boost')
depends=('soapysdr' 'avahi')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$_gitver.tar.gz")
sha512sums=('5c5ce76c90dfbfc3b6846902f5dbf0c0906d92ae193a50d79733ce47403f1ec27dcf78d07fc198e8099272431c313984eb32c26306fb2afee129339e6501f3d2')

build() {
  mkdir -p $_pkgname-$_gitver/build
  cd $_pkgname-$_gitver/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release

  make
}

package() {
  cd $_pkgname-$_gitver/build

  make DESTDIR="$pkgdir" install

  install -Dm 644 "$srcdir"/$_pkgname-$_gitver/LICENSE_1_0.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
