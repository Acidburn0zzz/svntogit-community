# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=pathological
pkgver=1.1.3
pkgrel=10
pkgdesc="A puzzle game with the same feel as frozen bubble"
arch=('x86_64')
url="http://pathological.sourceforge.net/"
license=('GPL')
depends=('python2-pygame')
makedepends=('netpbm')
install=pathological.install
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        pathological.desktop pygame_181.patch encoding.patch)
sha512sums=('a64e0ca9a3bf86ade4a1abdd4f471c341838f690b2471f21c488471b7800da55f89f42c62a51c9be380cb9f265432db904a5de6f6bb3a9a7dde835da94baf566'
            'a2e65a8f005f5575e99305e71cbed2e5955b4564fa9377d228c4f54fccbda6aa148b7b506b21b4b04be0ee66754e9c8f2c2d6e37495da9f15dcc7664584fe669'
            '0ee715036f1264a1e552396b874899d93eb8b44ddf5bbc898f5fa38588cc58ce1da134904afb8c58c799422d7be151cad397f85cb4960cf5c7a9eaa5590ea429'
            '343282b22ecbb42243cf59492527336acd39eee584e19c1c53e9b9be5e0f75a8546548efb2d5a5fb70b32d8cb9d393f986d4ec07ed7fe584c144e2ab51e360a6')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p0 < ../encoding.patch
  patch -p1 < ../pygame_181.patch
  sed -i 's|#!/usr/bin/python|#!/usr/bin/python2|' pathological.py
  sed -i 's|X11R6/include/X11|share|' Makefile
  sed -i 's|usr/games|usr/bin|' Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 "${srcdir}/pathological.desktop" "${pkgdir}/usr/share/applications/pathological.desktop"
  chown root:games "${pkgdir}/var/games"
  chmod 775 "${pkgdir}/var/games"
  rm "${pkgdir}/var/games/pathological_scores"
}
