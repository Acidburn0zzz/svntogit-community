# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Llewelyn Trahaearn <WoefulDerelict at GMail dot com>
# Contributor: kokoko3k <kokoko3k at gmail dot com>
# Contributor: Ionut Biru <ibiru at archlinux dot org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski at archlinux dot org>

pkgname=lib32-libva
pkgver=1.7.2
pkgrel=2
pkgdesc='Video Acceleration (VA) API for Linux (32-bit)'
arch=('x86_64')
url='http://freedesktop.org/wiki/Software/vaapi'
license=('MIT')
depends=('lib32-libdrm' 'lib32-libgl' 'lib32-wayland' 'lib32-libxext' 'lib32-libxfixes' 'libva')
makedepends=('mesa' 'gcc-multilib')
optdepends=('lib32-libva-vdpau-driver: vdpau back-end for nvidia'
            'lib32-libva-intel-driver: back-end for intel cards')
source=(http://freedesktop.org/software/vaapi/releases/libva/libva-${pkgver}.tar.bz2
        not-finding-wl-drm-interface-with-libglvnd.patch)
sha512sums=('4a1da09416361b69813a6fa77ee1de3d546eb1bb0d8beb7933f2c00cf9fbb8ab4e74019fa432239ee618282fdc42cb1f7f1ae2781c1d851e461994288b888a10'
            '9df446b63d864b2a75a8d984b903b767793d4c23cb73a08433465b481840e4702dd66d98c408f5142e2cb4a9c6aedcb8c8a014e9ae28cf88fd2582d77e7becd5')

prepare() {
  cd "${pkgname#*-}-${pkgver}"
  patch -Np1 -i ../not-finding-wl-drm-interface-with-libglvnd.patch
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd "${pkgname#*-}-${pkgver}"
  ./configure --prefix=/usr --libdir='/usr/lib32' 
  make
}

package() {
  cd "${pkgname#*-}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rfv "${pkgdir}/usr/"{include,bin}
  install -m644 -D COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
