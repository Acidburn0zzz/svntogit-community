# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Konsta Kokkinen <kray@tsundere.fi>

pkgname=minetest
pkgver=0.4.6
pkgrel=1
pkgdesc='An Infiniminer/Minecraft inspired game'
arch=('i686' 'x86_64')
url='http://minetest.net/'
license=('GPL')
depends=('sqlite' 'libgl' 'libxxf86vm' 'openal' 'libvorbis' 'curl' 'hicolor-icon-theme')
makedepends=('cmake' 'irrlicht' 'mesa' 'libpng' 'zlib')
install=minetest.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}/tarball/${pkgver}
	${pkgname}_game-${pkgver}.tar.gz::https://github.com/minetest/${pkgname}_game/tarball/${pkgver}
        ${pkgname}_common-${pkgver}.tar.gz::https://github.com/minetest/common/tarball/${pkgver})
md5sums=('a8cacba57659c397fb8098881eb0c5f9'
         '1023ac25a83d1431c861d7947e962675'
         '54a805066c509478f6983b359f2e91fc')

build() {
  cd "$srcdir"/minetest-minetest-*

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "$srcdir"/minetest-minetest-*

  make DESTDIR="$pkgdir" install

  mv -v "$srcdir"/minetest-minetest_game-* "$pkgdir"/usr/share/minetest/games/minetest
  mv -v "$srcdir"/minetest-common-* "$pkgdir"/usr/share/minetest/games/common
  
  # small cleanup
  rm "$pkgdir"/usr/share/minetest/games/{minetest,common}/.gitignore
}
