# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

_name=jack2
pkgname=lib32-jack2
pkgver=1.9.14
pkgrel=2
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-celt' 'lib32-gcc-libs' 'lib32-glibc' 'jack2')
makedepends=('celt' 'lib32-dbus' 'lib32-expat' 'lib32-libsamplerate'
'libsamplerate' 'waf')
provides=('lib32-jack' 'libjack.so')
conflicts=('lib32-jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('ca9738fd5aadcb7e125f98a358437905959afebe0b792cab316fc31410e89ea3aefdb85154792a9d0a80fa988aa7eff379ff5c7ac6c4bfa1d57ad3d7de7b5fdd')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  touch __init__.py
  mkdir -vp tools
  cp -v waflib/extras/xcode*.py tools
  rm -rv waflib
  sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
      -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf configure --prefix=/usr \
                --libdir=/usr/lib32 \
                --classic \
                --mixed \
                --dbus
  waf build
}

package() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}:${PYTHONPATH}"
  waf install --destdir="${pkgdir}"
  # remove all non-32bit and those provided by jack2
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
  rm -rf "${pkgdir}/usr/lib32/jack/"
  rm -rf "${pkgdir}/usr/lib32/libjack"{net,server}*
}
# vim:set ts=2 sw=2 et:
