# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lib32-jack2
pkgver=1.9.11_RC1
pkgrel=1
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL')
depends=('lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'jack2')
makedepends=('celt' 'doxygen' 'gcc-multilib' 'lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'libffado' 'libsamplerate' 'opus' 'python')
provides=('lib32-jack')
conflicts=('lib32-jack' 'jack2-multilib' 'jack2-dbus-multilib')
replaces=('jack2-multilib' 'jack2-dbus-multilib')
source=("${pkgname#lib32-}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname#lib32-}/archive/v${pkgver//_/-}.tar.gz"
        'tests-fix-compilation-with-gcc7.patch'
        'jackdebugclient-keep-client-fsynchrotable.patch'
        'example-clients-fix-compilation-of-tw.c.patch')
sha512sums=('4fd7d82ab6536b8c6061023858ae5b978903608b149498818971481da75c6e9e0e7e7aef5e1343730c259f4378aebfbf25916b9736e0ad8aa19584a44f894436'
            '79e44bae409fc47af8b9b227464a3a88c223f103bf59a2aaaf2ade99108c27aeca8f4f8d60ba919a3aa83a0072f9ffef03e7259c28a61d70caf9cc18a23afcad'
            '4c3290c51bd289e36edddb55a6e4cab981d9d3a3932da8c0a06652a22cce35c7858ff4afa99c309ed0e331224274bdb9dfe8e4fed46d486e1b2b44e6b971c484'
            'ec5f46c8fffda5f90c7597c8a8058246adefec95c0565ccc72afb7f57cb159fdf368107d3160284f1b2cf1c8e5e5c490f53151039d69423ca48da0d3d676e1e0')

prepare() {
  cd "${pkgname#lib32-}-${pkgver//_/-}"
  # https://github.com/jackaudio/jack2/issues/253
  patch -Np1 -i "${srcdir}/tests-fix-compilation-with-gcc7.patch"
  patch -Np1 -i "${srcdir}/example-clients-fix-compilation-of-tw.c.patch"
  patch -Np1 -i "${srcdir}/jackdebugclient-keep-client-fsynchrotable.patch"

}

build() {
  cd "${pkgname#lib32-}-${pkgver//_/-}"

  python waf configure --prefix=/usr \
                        --libdir=/usr/lib \
                        --libdir32=/usr/lib32 \
                        --mixed
  python waf build
}

package() {
  cd "${pkgname#lib32-}-${pkgver//_/-}"

  python waf install --destdir="${pkgdir}"
  # remove all non-32bit
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
}

# vim:set ts=2 sw=2 et:
