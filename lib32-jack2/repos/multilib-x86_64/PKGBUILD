# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=lib32-jack2
pkgver=1.9.12
pkgrel=3
pkgdesc="The next-generation JACK with SMP support (32 bit)"
arch=('x86_64')
url="http://jackaudio.org/"
license=('GPL2' 'LGPL2.1')
depends=('lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'jack2')
makedepends=('celt' 'doxygen' 'gcc-multilib' 'lib32-celt' 'lib32-gcc-libs' 'lib32-opus' 'libffado' 'libsamplerate' 'opus' 'waf')
provides=('lib32-jack')
conflicts=('lib32-jack')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/jackaudio/${pkgname#lib32-}/archive/v${pkgver}.tar.gz")
sha512sums=('f0271dfc8f8e2f2489ca52f431ad4fa420665816d6c67a01a76da1d4b5ae91f6dad8c4e3309ec5e0c159c9d312ed56021ab323d74bce828ace26f1b8d477ddfa')

prepare() {
  cd "${pkgname#lib32-}-${pkgver}"
  # copying relevant custom waf scripts and loading them specifically using
  # wscript
  touch __init__.py
  mkdir -vp tools
  cp -v waflib/extras/xcode*.py tools
  rm -rv waflib
  sed -e "s/load('xcode'/load('xcode', tooldir='tools'/g" \
      -e "s/load('xcode6'/load('xcode6', tooldir='tools'/g" \
      -i wscript
}

build() {
  cd "${pkgname#lib32-}-${pkgver}"

  waf configure --prefix=/usr \
                --libdir=/usr/lib \
                --libdir32=/usr/lib32 \
                --freebob=no \
                --classic \
                --dbus \
                --mixed
  waf build
}

package() {
  cd "${pkgname#lib32-}-${pkgver}"

  waf install --destdir="${pkgdir}"
  # remove all non-32bit and those provided by jack2
  rm -rf "${pkgdir}"/usr/{bin,lib,include,share}
}
# vim:set ts=2 sw=2 et:
