# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=pynormaliz
pkgname=(python2-pynormaliz python-pynormaliz)
pkgver=1.0
pkgrel=1
pkgdesc="A Python interface to Normaliz"
arch=(i686 x86_64)
url="https://github.com/sebasguts/PyNormaliz"
license=(GPL2)
makedepends=(python-setuptools python2-setuptools normaliz)
source=("https://pypi.io/packages/source/P/PyNormaliz/PyNormaliz-$pkgver.tar.gz"
        pynormaliz-fix-i686-build.patch::"https://github.com/sebasguts/PyNormaliz/commit/11f260a4.patch")
md5sums=('27a5f7d3fa0d96766d4f811495b0f5ce'
         '056b82fc649f36ca643bd6a5b794e787')

prepare() {
  cd PyNormaliz-$pkgver
  patch -p1 -i ../pynormaliz-fix-i686-build.patch
  cd ..

  cp -r PyNormaliz-$pkgver PyNormaliz-$pkgver-py2
}

build() {
  cd PyNormaliz-$pkgver
  python setup.py build

  cd ../PyNormaliz-$pkgver-py2
  python2 setup.py build
}

package_python2-pynormaliz() {
  depends=(python2 normaliz)
  cd PyNormaliz-$pkgver-py2

  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python-pynormaliz() {
  depends=(python normaliz)
  cd PyNormaliz-$pkgver
  
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
}

