# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-classy-tags
pkgname=python-django-classy-tags
pkgver=3.0.0
pkgrel=1
pkgdesc="Class based template tags for django"
arch=(any)
url="https://github.com/django-cms/django-classy-tags"
license=(BSD)
depends=(python-django python-six)
makedepends=(python-setuptools)
checkdepends=(python-coverage python-pytest)
# tests not in pypi.org sdist tarball
# https://github.com/django-cms/django-classy-tags/issues/69
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/divio/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('9273322b36668b41706457ff29a471b647bcafa843cd0dfc2e4f9a94c931f5b7e85e508a93a9a97386a3aebb4a7064e6ea2132347986704e3f8a1d21ab4d53cf')
b2sums=('066b2c8a1cf8b3d5cd40a1a8df7736e5cbd7b83b557d283b9861b1d14437b2c72ac4ffa0f71ed7818c1e9fb33be8c39e06e088561dd134190639696a825297f6')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
