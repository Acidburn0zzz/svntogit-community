# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-classy-tags
pkgname=python-django-classy-tags
pkgver=0.9.0
pkgrel=1
pkgdesc="Class based template tags for django"
arch=('any')
url="https://github.com/divio/django-classy-tags"
license=('BSD')
depends=('python-django' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-pytest')
# LICENSE and tests not in pypi tarball in python-django-classy-tags <= 0.9.0
# https://github.com/divio/django-classy-tags/issues/52
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/divio/${_name}/archive/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}-cleanup.patch::https://github.com/divio/django-classy-tags/pull/45.patch"
        "${pkgname}-${pkgver}-fix_py38_django3.patch::https://github.com/divio/django-classy-tags/pull/50.patch")
sha512sums=('a9ee28868e558da70c5b63d979e89dd0912a775587042f39986861c6a5b621509593a0e2c9af76f90b6cc6697e28d3aa49271f308349fd3a06dc198ac8da9153'
            '5b9db95fbdfe1d9403a0f4f704f85ebaafe2d389e424731bb6fdea0df01134deb6060c6b7bf92b9862cc3fc78c5d4953738acc8ba16bd556b8b0c1d7f14f1450'
            '7293ca34f617b0670fe3faff7b8756b74c560a8ab0c4c792499c259167fc91ba34cba924fd5b25d28b32bed122293de8392748684a296be55428c5800d9bac16')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  patch -Np1 -i "../${pkgname}-${pkgver}-cleanup.patch"
  patch -Np1 -i "../${pkgname}-${pkgver}-fix_py38_django3.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
