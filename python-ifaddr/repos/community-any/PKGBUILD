# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=ifaddr
pkgname=python-ifaddr
pkgver=0.1.5
pkgrel=1
pkgdesc="Enumerates all IP addresses on all network adapters of the system"
arch=('any')
url="https://github.com/pydron/ifaddr"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=(https://github.com/pydron/ifaddr/archive/${pkgver}.tar.gz)
sha512sums=('a7c5ae08943b2b16c3759f29bfd60473edd609602bbcfbe2433495b395e004852b2209c5a01949327e0364dfd195e9fce31ae3f60b8bfdda358040dce4a4cbdc')

build() {
  cd "${_name}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${_name}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

}

check() {
  cd "${_name}-${pkgver}"
  nosetests ${_name}/test_ifaddr.py
}

# vim:set ts=2 sw=2 et:
