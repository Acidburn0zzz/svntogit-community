# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=tcsh
pkgver=6.17.00
pkgrel=4
pkgdesc='Enhanced version of the Berkeley C shell.'
arch=('i686' 'x86_64')
url='http://www.tcsh.org/Welcome'
license=('BSD')
depends=('ncurses')
backup=('etc/csh.cshrc'
        'etc/csh.login')
install='tcsh.install'
source=("ftp://ftp.astron.com/pub/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'csh.cshrc'
        'tcsh-6.17.00-ls-colors-var.patch'
        'csh.login')
md5sums=('c47de903e3d52f6824c8dd0c91eeb477'
         '7ca0fe6d1a1b9a0093f632499d4fb112'
         '0ed594ef77bc689265632682e18462eb'
         '4869b9da87c79854e2cc97241f125853')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p0 < ../tcsh-6.17.00-ls-colors-var.patch

  ./configure --prefix=/ --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" mandir=/usr/share/man install install.man

  install -Dm0644 "${srcdir}/csh.cshrc" "${pkgdir}/etc/csh.cshrc"
  install -Dm0644 "${srcdir}/csh.login" "${pkgdir}/etc/csh.login"
  install -Dm0644 Copyright "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  ln -s tcsh "${pkgdir}/bin/csh"
}
