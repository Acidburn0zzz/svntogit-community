# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gordin <9ordin @t gmail dot com>

pkgname=lolcat
pkgver=42.1.44
pkgrel=2
pkgdesc='Okay, no unicorns. But rainbows!!'
url='https://github.com/busyloop/lolcat'
arch=('any')
license=('custom')
depends=('ruby' 'ruby-paint' 'ruby-trollop')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/busyloop/lolcat/archive/v${pkgver}.tar.gz)
sha512sums=('6bda0ffcc55e3be12163617f3edcd85b3e27b82dfec71044c5918fdeed5670c6df8cedd25e7c77659bd05f59fcecef88efd251545f718a2784425618a1a2f58e')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -r 's|~>|>=|g' -i lolcat.gemspec # don't give a fuck about rubys bla bla, we will handle it
  sed 's|git ls-files -- bin/\*|find bin -type f|' -i lolcat.gemspec
  sed 's|git ls-files -- {test,spec,features}/*|echo|' -i lolcat.gemspec
  sed 's|git ls-files|find -type f|' -i lolcat.gemspec
}

build() {
  cd ${pkgname}-${pkgver}
  gem build lolcat.gemspec
}

package() {
  cd ${pkgname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${pkgname}-${pkgver}.gem
  rm "${pkgdir}/${_gemdir}/cache/${pkgname}-${pkgver}.gem"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: ts=2 sw=2 et:
