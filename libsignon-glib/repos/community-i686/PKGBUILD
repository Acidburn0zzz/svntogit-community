# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libsignon-glib
pkgver=1.7
pkgrel=1
pkgdesc="GLib-based client library for applications handling account authentication through the Online Accounts Single Sign-On service"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('signon')
makedepends=('python2-gobject')
options=('!libtool')
source=(http://accounts-sso.googlecode.com/files/$pkgname-$pkgver.tar.gz
        fix-docdir.patch
        make-tests-optional.patch)
sha1sums=('e461c952c015e5b375ca3f12fac5a8ae1ba4646e'
          '56ffd1502dcb754797dc95c0eb80ab23374aee2d'
          '327dde1944c8adc27d8bd413399a4366b3560901')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Use the standard docdir location
  # http://code.google.com/p/accounts-sso/issues/detail?id=123
  patch -Np1 -i "$srcdir/fix-docdir.patch"

  # Build tests optionally
  # http://code.google.com/p/accounts-sso/issues/detail?id=124
  patch -Np1 -i "$srcdir/make-tests-optional.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static \
              PYTHON=python2
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  # Install GI overrides for python 3 as well
  install -dm755 "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides"
  ln -s ../../../../python2.7/site-packages/gi/overrides/Signon.py "$pkgdir/usr/lib/python3.3/site-packages/gi/overrides/Signon.py"
}
