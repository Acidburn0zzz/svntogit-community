# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.7.0
pkgrel=2
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go-pie')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('5be7bc27af6e37e092f76d8d770ba415ec0241eccd39328cc21aa20eb078e8621ac23b7bd2b48c93f285d3e43dffa43fabaf697593439bbaf9aab813011b97cf')

build() {
  cd "${pkgname}-${pkgver}"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" ./cmd/yaegi
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
