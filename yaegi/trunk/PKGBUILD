# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.8.8
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go-pie')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('15e6334c935640f22913100e9c154392fd81a1734a26a1cf47f3eeccaf2ddd9993f71995996c67d0d7b4d0dcb763a415df7c2c4679f9fa45568ce7672ec848e6')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  go build ./cmd/yaegi
}

# These checks fail right now :(
#check() {
#  cd "${pkgname}-${pkgver}"
#  go test ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
