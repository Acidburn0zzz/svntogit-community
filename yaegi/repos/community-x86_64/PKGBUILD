# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.7.6
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go-pie')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('d59921cfad3ec9be1b6ba0a4813b768cc0c9c14d9edcb2075ba9c8179946b9c83c447411e9cdc38d624e45a84c90fb1b3d1238a8e83c41528ad34d40185db1eb')

build() {
  cd "${pkgname}-${pkgver}"
  go build -trimpath -ldflags "-extldflags ${LDFLAGS}" ./cmd/yaegi
}

# These checks fail right now :(
#check() {
#  cd "${pkgname}-${pkgver}"
#  go test ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
