# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: navigaid <navigaid@gmail.com>
pkgname=yaegi
pkgver=0.9.16
pkgrel=1
pkgdesc='Yet another Elegant Go Interpreter'
arch=('x86_64')
url='https://github.com/containous/yaegi'
license=('Apache')
makedepends=('go')
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/containous/yaegi/archive/v${pkgver}.tar.gz")
sha512sums=('107a5dcb8cd8be647d526c8037161b6fdf5ca64510bb9a60ee097d9c25f5865a4a167cf274f14d3559398b5d8f20b1c96def2253d2d4d42ad038a6219f0a364b')

build() {
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v ./cmd/yaegi
}

# disabling tests...
#check() {
#  cd "${pkgname}-${pkgver}"
#  sed -i 's|"-race", ||' ./cmd/yaegi/yaegi_test.go
#  GOROOT="/usr/lib/go" go test -v ./...
#}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 yaegi "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
