# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Daniel Bermond < yahoo-com: danielbermond >

pkgname=intel-media-driver
pkgver=18.4.0
pkgrel=1
pkgdesc="Intel Media Driver for VAAPI â€” Broadwell+ iGPUs"
arch=('x86_64')
url="https://github.com/intel/media-driver/"
license=('MIT' 'BSD')
depends=(gcc-libs intel-gmmlib libva libpciaccess)
makedepends=(cmake)
source=("https://github.com/${pkgname/-//}/archive/${pkgname%-*}-${pkgver}.tar.gz")
sha256sums=('57a5c0d4dc7e34d7940d23346bbdee2b6cdfbfa81d64f5b77506da8b4298df43')

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../${pkgname#*-}-${pkgname%-*}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_DRIVER_SYSCONF=OFF \
        -Wno-dev
    make
}

package() {
    cd build
    make DESTDIR="$pkgdir" install
    install -Dm644 ../${pkgname#*-}-${pkgname%-*}-${pkgver}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
