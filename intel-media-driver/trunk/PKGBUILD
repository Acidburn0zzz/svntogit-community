# Maintainer: Bruno Pagani <archange@archlinux.org>
# Maintainer: Daniel Bermond <dbermond@archlinux.org>

pkgname=intel-media-driver
pkgver=19.4.0.r
pkgrel=1
pkgdesc="Intel Media Driver for VAAPI â€” Broadwell+ iGPUs"
arch=(x86_64)
url="https://github.com/intel/media-driver/"
license=(MIT BSD)
depends=(gcc-libs intel-gmmlib libva libpciaccess)
makedepends=(cmake)
source=("https://github.com/${pkgname/-//}/archive/${pkgname%-*}-${pkgver/.r/r}.tar.gz")
sha256sums=('a03bd75eefe9cb0245e3aab2723b3fef555d9f180a180b2c29d7b12d483d9ec2')

build() {
    cmake -B build -S ${pkgname#*-}-${pkgname%-*}-${pkgver/.r/r} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DINSTALL_DRIVER_SYSCONF=OFF \
        -Wno-dev
    make -C build
}

package() {
    make -C build DESTDIR="$pkgdir" install
    install -Dm644 ${pkgname#*-}-${pkgname%-*}-${pkgver/.r/r}/LICENSE.md -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
