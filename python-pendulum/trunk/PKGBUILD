# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=2.0.5
pkgrel=1
pkgdesc="Python datetimes made easy"
arch=('any')
url="https://pendulum.eustace.io/"
license=('MIT')
depends=('python' 'python-dateutil' 'python-pytzdata')
makedepends=('python-dephell')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytz' 'python-babel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sdispater/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('c358d029c68a4bd45fb5014855f074f89618f841e08612bba3c6e99a78805f731979ad58342174ef820cf7ffcf090dde88870f61d540f3c5d56bbedc1f5cdd58')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
