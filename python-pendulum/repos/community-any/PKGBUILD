# Maintainer: David Runge <dvzrv@archlinux.org>

_name=pendulum
pkgname=python-pendulum
pkgver=2.1.0
pkgrel=1
pkgdesc="Python datetimes made easy"
arch=('any')
url="https://pendulum.eustace.io/"
license=('MIT')
depends=('python' 'python-dateutil' 'python-pytzdata')
makedepends=('python-dephell')
checkdepends=('python-pytest' 'python-pytest-cov' 'python-pytz' 'python-babel')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sdispater/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('b31e342d6ca4159f84b8a3fdf2019e4e4dd91e32c1a3b67fe0bf413c2009c78f341eaf3ed33d3b51e1e8d2ffa453b14c89e3f45a40659f7c21861d4b5c948745')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG.md,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
