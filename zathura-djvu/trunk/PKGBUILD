# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: karnath <karnathtorjian@gmail.com>

pkgname=zathura-djvu
pkgver=0.2.8
pkgrel=1

pkgdesc="DjVu support for Zathura"
url="https://pwmt.org/projects/zathura-djvu/"
arch=('x86_64')
license=('custom')

depends=('djvulibre' 'zathura' 'desktop-file-utils')
makedepends=('meson' 'ninja')

source=(zathura-djvu-$pkgver.tar.gz::https://github.com/pwmt/zathura-djvu/archive/$pkgver.tar.gz)

sha1sums=('891e30df71c3f6a840f48aa3aef5456e6ae9584b')

build(){
  cd zathura-djvu-$pkgver
  arch-meson build
  ninja -C build
}

package(){
  DESTDIR="$pkgdir" ninja -C zathura-djvu-$pkgver/build install
  install -Dm0644 zathura-djvu-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

