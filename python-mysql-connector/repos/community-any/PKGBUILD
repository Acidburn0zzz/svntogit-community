# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: RaphaÃ«l Doursenaud <rdoursenaud at free dot fr>

pkgname=python-mysql-connector
_pkgname=mysql-connector-python
pkgver=8.0.18
pkgrel=3
pkgdesc="A standardized database driver for Python platforms and development"
arch=('any')
url="https://dev.mysql.com/downloads/connector/python/"
license=('GPL')
depends=('python')
makedepends=('python-setuptools')
source=("https://cdn.mysql.com/Downloads/Connector-Python/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('cb441a6388de8609b56e4d9885ae2491d207c7b1e3bb387341d55853d383eff6e268e34feb32b42797c7b210d35f9d6dbc302ea4c9ed2441a76b91ea66d504b2')

build() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py build
}

package() {
	cd "${srcdir}/${_pkgname}-${pkgver}"
	python setup.py install --root="${pkgdir}" --optimize=1
}
