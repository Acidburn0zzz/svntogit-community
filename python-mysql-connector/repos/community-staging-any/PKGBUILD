# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: RaphaÃ«l Doursenaud <rdoursenaud at free dot fr>

pkgbase=python-mysql-connector
pkgname=('python-mysql-connector' 'python2-mysql-connector')
_pkgname=mysql-connector-python
pkgver=8.0.11
pkgrel=2
pkgdesc="A standardized database driver for Python platforms and development"
arch=('any')
url="http://dev.mysql.com/downloads/connector/python/"
license=('GPL')
depends=('python' 'python2')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://cdn.mysql.com/Downloads/Connector-Python/${_pkgname}-${pkgver}.tar.gz")
sha512sums=('9fd08f6ea7d9c60a9b9e21628d77ff41e967242441651ff46e7eaaa7dda727357f83e134e21a2bbc82a10b973abe3c56af411ae5ee68715378dc70ed9f25e27f')
changelog=ChangeLog

prepare() {
  cp -r "${_pkgname}-${pkgver}" "${_pkgname}-${pkgver}-py2"
  # change python to python2
  find "${_pkgname}-${pkgver}-py2" -name '*.py' -type f -exec \
      sed -i 's|env python|env python2|' {} \;
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py build

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py build
}

package_python-mysql-connector() {
  depends=('python')

  cd "${srcdir}/${_pkgname}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1
}

package_python2-mysql-connector() {
  depends=('python2')

  cd "${srcdir}/${_pkgname}-${pkgver}-py2"
  python2 setup.py install --root="${pkgdir}" --optimize=1
}
