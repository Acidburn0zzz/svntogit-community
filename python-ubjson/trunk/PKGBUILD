# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-ubjson
pkgver=0.12.0
pkgrel=1
pkgdesc='Universal Binary JSON draft-12 serializer for Python'
arch=(x86_64)
url='https://github.com/Iotic-Labs/py-ubjson'
license=(Apache)
makedepends=(python-setuptools python-pypandoc)
depends=(python)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/Iotic-Labs/py-ubjson/archive/v$pkgver.tar.gz")
sha256sums=('f3ea0aeda4871b51e39566df0d6200a9950c5edb704e6830e22fe5989981069b')

build() {
  cd py-ubjson-$pkgver
  python setup.py build
}

check() {
  cd py-ubjson-$pkgver
  python -m unittest discover -v test
}

package() {
  cd py-ubjson-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
