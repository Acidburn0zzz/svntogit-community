# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-requests
pkgver=1.1.0
pkgrel=2
_libname=${pkgname/python-/}
pkgdesc="Python HTTP for Humans."
url="http://python-requests.org"
depends=('python')
makedepends=('python-distribute')
optdepends=('python-certifi: SSL support')
license=('custom: ISC')
arch=('any')
source=(http://pypi.python.org/packages/source/${_libname:0:1}/$_libname/$_libname-$pkgver.tar.gz
        certs.patch)

build() {
    cd "$srcdir/$_libname-$pkgver"
    patch -p0 -i "$srcdir/certs.patch"
    sed -r 's#(\W|^)requests/cacert\.pem(\W|$)##' -i MANIFEST.in
    rm -f requests/cacert.pem
    python setup.py build
}

check() {
    cd "$srcdir/$_libname-$pkgver"
    test -f "$(python -m requests.certs)"
}

package() {
    cd "$srcdir/$_libname-$pkgver"
    python setup.py install --root="$pkgdir"
	install -m0644 -D "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('21a81ddf1a3c2f956524538966ae19c38cae251f5629821588cdc8246a1335f7'
            '97d5ee56c0e03d6c5c9791be3d9f4756252cfd74f1ab4bb6593fa2555fe3760e')
