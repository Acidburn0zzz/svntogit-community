# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)

pkgname=dkms
pkgver=2.2.0.3
pkgrel=5
pkgdesc='Dynamic Kernel Modules System'
arch=('any')
url='http://linux.dell.com/dkms/'
license=('GPL2')
depends=('linux-headers' 'kmod')
backup=('etc/dkms/framework.conf')
source=("http://linux.dell.com/$pkgname/permalink/$pkgname-$pkgver.tar.gz"
        'dkms.rc'
        'dkms.conf'
        '01-broken-uninstall.patch')
md5sums=('11a8aaade2ebec2803653837c7593030'
         '70f31e94c660f515be91b6c544dae703'
         'fba3c6e01c87d65e0ffa9b5c528a6da3'
         '2dfd8656dc79afd229c1481a90fb645b')

build() {
  patches=("$srcdir"/*.patch)
  cd $pkgname-$pkgver
  for p in "${patches[@]}"; do
    msg2 "Apply patch: ${p##*/}"
    patch -p1 -i "$p"
  done
}

package() {
  # upstream installer
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  popd
  # fix hardcoded paths
  sed -i "s|/sbin/depmod|depmod|" $pkgdir/usr/sbin/dkms
  # remove debian specific
  rm -r "$pkgdir/etc/kernel"
  # move bash completion in right place
  install -d -m 755 "$pkgdir/usr/share/bash-completion"
  mv "$pkgdir/etc/bash_completion.d" "$pkgdir/usr/share/bash-completion/completions"
  # add initscripts
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
}

# vim:set ts=2 sw=2 ft=sh et:
