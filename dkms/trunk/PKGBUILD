# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Balwinder S "bsd" Dheeman (bdheeman AT gmail.com)

pkgname=dkms
pkgver=2.2.0.3
pkgrel=3
pkgdesc='Dynamic Kernel Modules System'
arch=('any')
url='http://linux.dell.com/dkms/'
license=('GPL2')
depends=('linux-headers')
backup=('etc/dkms/framework.conf')
source=("http://linux.dell.com/$pkgname/permalink/$pkgname-$pkgver.tar.gz"
        'dkms.rc')
md5sums=('11a8aaade2ebec2803653837c7593030'
         '79866650cc69d355c34e19d0b016632f')

package() {
  # add initscripts
  install -D -m 755 $pkgname.rc "${pkgdir}/etc/rc.d/$pkgname"

  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # fix hardcoded paths
  sed -i "s|/sbin/depmod|depmod|" $pkgdir/usr/sbin/dkms

  # remove debian specific
  rm -r "$pkgdir/etc/kernel"
}

# vim:set ts=2 sw=2 ft=sh et:
