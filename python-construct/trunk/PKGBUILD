# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.10.59
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-cloudpickle' 'python-numpy' 'python-pytest-runner'
              'python-pytest-benchmark' 'python-ruamel-yaml')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol'
            'python-ruamel-yaml: for KaitaiStruct (KSY) exporter')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('bf5867efdb9e1a27ddede6844f5f3466714e38dd97aae09c6990582989801605eebf90099fb3a0ab78deaed4f3b6be38421b635f6332461efd033d5fb8c2c1af')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
