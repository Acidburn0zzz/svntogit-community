# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-construct
pkgname=('python-construct' 'python2-construct')
pkgver=2.9.31
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="http://construct.readthedocs.org"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-numpy' 'python2-numpy'
              'python-pytest-benchmark' 'python2-pytest-benchmark' 'python2-enum34')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('e578f895b7e24cf9f9e530b7b7384758387b25b4edb4686ab3a003eef4dadecda5dc374679c18d2e883a181d733fbfa74bac7dd2084d78ef68b9e02ea62dc36d')

prepare() {
  cp -a construct-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/construct-$pkgver
  python setup.py build

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/construct-$pkgver
  python setup.py pytest

  cd "$srcdir"/construct-$pkgver-py2
  python2 setup.py pytest || warning "TODO: figure out benchmark related failures"
}

package_python-construct() {
  depends=('python')

  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-construct() {
  depends=('python2')
  optdepends=('python2-enum34: for Enum / EnumFlags')

  cd construct-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
