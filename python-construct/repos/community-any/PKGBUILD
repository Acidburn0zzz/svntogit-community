# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.10.54
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-numpy' 'python-pytest-runner' 'python-pytest-benchmark')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('c7f55397fc509c5763c49277d1748c522b38cebafb7b43085e54a17618953f6e0affba85417787d05bdaba200ab1aafbfc44775bd19cac336d23987756779484')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
