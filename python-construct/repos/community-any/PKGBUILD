# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-construct
pkgver=2.9.51
pkgrel=1
pkgdesc="A powerful declarative parser/builder for binary data"
arch=('any')
license=('MIT')
url="https://construct.readthedocs.org"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-arrow' 'python-numpy' 'python-pytest-runner' 'python-pytest-benchmark')
optdepends=('python-arrow: for Timestamp'
            'python-numpy: for array serialization with Numpy protocol')
source=("$pkgname-$pkgver.tar.gz::https://github.com/construct/construct/archive/v$pkgver.tar.gz")
sha512sums=('045b9050d435e966595bce895399811bf2bd78767ae2f5dd6e8cf1a6a444d0087932a2e4b4dcd60b39ff461e39ce99c462d01a77f235db35a4723f51804f6a02')

build() {
  cd construct-$pkgver
  python setup.py build
}

check() {
  cd construct-$pkgver
  python setup.py pytest
}

package() {
  cd construct-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
