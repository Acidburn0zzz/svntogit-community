# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cymem
pkgver=2.0.4
pkgrel=3
pkgdesc="Cython memory pool for RAII-style memory management"
url="https://github.com/explosion/cymem"
license=('MIT')
arch=('x86_64')
depends=('cython' 'python')
makedepends=('python-pip' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest')
source=("https://github.com/explosion/cymem/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('3f3b3ac227ba882788342d9f2e37f11847a69fa684c0393feccbca7808994058ea8b11d3c2ed0c3b61e637f555be5c6475ece2f43d66f0136bbcca5122cede74')

build() {
  cd cymem-$pkgver
  python setup.py build
}

check() {
  cd cymem-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" pytest
}

package() {
  cd cymem-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
