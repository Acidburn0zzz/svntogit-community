# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=watchexec
pkgver=1.15.1
pkgrel=1
pkgdesc="Executes commands in response to file modifications"
url='https://github.com/watchexec/watchexec'
depends=('gcc-libs')
makedepends=('cargo')
arch=('x86_64')
license=('Apache')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('80ae278a8972e73ba77719d81b14c0c2f6ea6f66613b9ba8e8bf928ff6ec1c2e')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 "completions/zsh" "$pkgdir/usr/share/zsh/site-functions/_watchexec"
  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
