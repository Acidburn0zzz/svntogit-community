# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_npmname=emojione
pkgname=nodejs-$_npmname
pkgver=3.1.7
pkgrel=1
pkgdesc="A complete set of emojis designed for the web"
arch=('any')
url="http://www.emojione.com"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz")
noextract=($_npmname-$pkgver.tgz)
sha512sums=('2136f4aebc7a8ae24a0674e1454134ba21a4c2b8b09d8fbdd7dbf1b0017e12a0475e1ca309301c528fe73cd5a874768e26f286748d667a5da8e93cb2c418e32f')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$_npmname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
