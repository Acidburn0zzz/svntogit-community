# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_npmname=emojione
pkgname=nodejs-$_npmname
pkgver=3.1.0
pkgrel=1
pkgdesc="A complete set of emojis designed for the web"
arch=('any')
url="http://www.emojione.com"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz")
noextract=($_npmname-$pkgver.tgz)
sha512sums=('a9b3e6e9ec5efb87b01c95eeaf0e280fd29bf8a634df234e15cc760895649fe1c3a3a1df8add55272c2fdbccb3a8f99d3a3f3794753d1f98e26ea27e7b675270')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$_npmname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
