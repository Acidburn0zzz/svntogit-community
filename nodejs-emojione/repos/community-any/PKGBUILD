# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_npmname=emojione
pkgname=nodejs-$_npmname
pkgver=3.0.1
pkgrel=1
pkgdesc="A complete set of emojis designed for the web"
arch=('any')
url="http://www.emojione.com"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$_npmname/-/$_npmname-$pkgver.tgz")
noextract=($_npmname-$pkgver.tgz)
sha512sums=('e89ae97fd0b16c38206e5357cc25ebcdd066c19f6dd56d14f0cae6eda67896a715bbd7028459882fcc18de79e79399fe09bfbe39fcc345be58196ecc889d3b0a')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$_npmname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$_npmname/LICENSE.md" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
