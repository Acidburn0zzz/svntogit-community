# $Id$
# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Andrew Wright <andreww@photism.org>

pkgname=nicotine
pkgver=1.2.16
pkgrel=4
pkgdesc="Soulseek music-sharing client, written in python"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/nicotine-plus/"
license=('GPL')
depends=('pygtk' 'mutagen')
optdepends=('python2-geoip: to enable Geographical blocker')
source=(http://downloads.sourceforge.net/project/nicotine-plus/nicotine-plus/${pkgver}/${pkgname}+-${pkgver}.tar.bz2)
md5sums=('ac7433c21ef619a37e079a7ae68da94a')

build() {
  cd "${srcdir}/${pkgname}+-${pkgver}"
  sed -i -e "s/Icon=nicotine-plus/Icon=nicotine-plus-32px/" files/nicotine.desktop # Fixing FS#22476
  sed -i -e 's|/usr/bin/env python|/usr/bin/env python2|' \
    -e 's|/usr/bin/python|/usr/bin/python2|' pynicotine/{,gtkgui/}*.py

  python2 setup.py install --root="${pkgdir}"
  cd trayicon
  python2 autogen.py
  make 
}

package() {
  cd "${srcdir}/${pkgname}+-${pkgver}/trayicon"
  make DESTDIR="${pkgdir}" install 
  ln -s nicotine.py "${pkgdir}/usr/bin/nicotine"
}
