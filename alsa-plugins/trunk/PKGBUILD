# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>

pkgname=alsa-plugins
pkgver=1.0.23
pkgrel=2
pkgdesc="Extra alsa plugins"
arch=(i686 x86_64)
url="http://www.alsa-project.org"
license=(GPL)
depends=(alsa-lib)
makedepends=(pulseaudio jack ffmpeg)
optdepends=('pulseaudio: PulseAudio plugin'
            'jack: Jack plugin'
            'ffmpeg: libavcodec resampling plugin'
            'libsamplerate: libsamplerate resampling plugin'
            'speex: libspeexdsp resampling plugin')
options=('!libtool')
source=(ftp://ftp.alsa-project.org/pub/plugins/$pkgname-$pkgver.tar.bz2
        Fix_invalid_buffer_pointer_return_value.patch
        Add_handle_underrun_option.patch)
md5sums=('a671f8102366c5b388133e948e1c85cb'
         '067a51a5157a42989f60078d5225b6c1'
         '56fb0e9cbef9714504ecaa1bb62d7acf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Improve pulse underrun handling
  patch -Np1 -i "$srcdir/Fix_invalid_buffer_pointer_return_value.patch"
  patch -Np1 -i "$srcdir/Add_handle_underrun_option.patch"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR=$pkgdir install
}
