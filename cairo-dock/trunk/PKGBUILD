# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tofe <chris.chapuis@gmail.com>
# Contributor: erm67 <erm67@yahoo.it>

pkgname=cairo-dock
pkgver=3.4.0
pkgrel=1
pkgdesc='Light eye-candy fully themable animated dock'
arch=('i686' 'x86_64')
url='http://glx-dock.org/'
license=('GPL')
depends=('curl' 'dbus-glib' 'glu' 'gtk3' 'librsvg')
makedepends=('bzr' 'cmake')
optdepends=('cairo-dock-plugins: Plugins for Cairo-Dock')
source=("cairo-dock-${pkgver}.tar.gz::https://github.com/Cairo-Dock/cairo-dock-core/archive/${pkgver}.tar.gz")
sha256sums=('e92448c7d03d158c01c55c4e2e9c204c1d39a54288d8418a03c27f7b1739e0cc')

build() {
  cd cairo-dock-core-${pkgver}

  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build && cd build

  cmake .. -DCMAKE_INSTALL_PREFIX='/usr'
  make
}

package() {
  cd cairo-dock-core-${pkgver}/build

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
