# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-path
pkgver=13.1.0
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path'
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-black' 'python-pytest-cov' 'python-pytest-flake8')
replaces=('python-path.py')
conflicts=('python-path.py')
provides=('python-path.py')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path/archive/v$pkgver.tar.gz")
sha512sums=('db27420ad117f36bf42e54284e931f92b18a6db71fab3a66cba65bb2c78c02747db637e5ea7f69d9fc2864c0dfc0aa4dede0c7183b49699ffc4416d6662f9185')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path-$pkgver
  python setup.py build
}

check() {
  cd path-$pkgver
  # https://github.com/jaraco/path/issues/184
  python -m pytest || :
}

package() {
  cd path-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
