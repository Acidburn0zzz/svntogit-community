# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=release-cli
pkgver=0.9.0
pkgrel=1
pkgdesc="Gitlab Release CLI"
arch=('x86_64')
url="https://gitlab.com/gitlab-org/release-cli"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/gitlab-org/${pkgname}/-/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha512sums=('9717fea2d3d68debffad59332487ebe6bcca7beffea152c18bd1e0427acd595d658eed52c96fceb11ed9991a6c700fca74729d123e9614d9ebddd5fa9c228de3')
b2sums=('db249a458e3f138beffa954928ea47db32d96b80c7cb75642aee8966ddf2972641dbdde7e3b8bcc6f8f0beaf5d1f0beaa476dec99ec48456c6d514fd47a19cb2')

prepare() {
  mv -v "$pkgname-v$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -vp build
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  go build -o "build/${pkgname}" "./cmd/${pkgname}"
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "build/${pkgname}" -t "${pkgdir}/usr/bin"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {CHANGELOG,README}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  cd docs
  find . -type f -iname "*.md" -exec install -vDm 644 {} "${pkgdir}/usr/share/doc/${pkgname}/"{} \;
}
