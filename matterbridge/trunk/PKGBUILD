# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=matterbridge
pkgver=1.23.0
pkgrel=1
pkgdesc="Multi-protocols (IRC/XMPP/Mattermost/Slack/Matrix/etc) bridge"
arch=(x86_64)
url="https://github.com/42wim/matterbridge"
license=(Apache)
depends=(glibc gcc-libs)
makedepends=(go git)
backup=(etc/matterbridge.toml)
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz"
        ${pkgname}-${pkgver}.tar.gz.asc::"${url}/releases/download/v${pkgver}/v${pkgver}.tar.gz.asc"
        matterbridge.service)
sha512sums=('8750d600ec657c537ffb09e7fca6203b243b444569b9aec01ecdfab0d4b27575047a72d2a4db2a43aac4efca5d27d425d4bdcd8f0810c9ce7ed78045ae8521cd'
            'SKIP'
            '86cfa4818c4f02eb35c23263de71049cb900dc6362d04287dc1c1a5bcb16d7ef34c8b4e92ea89bb7421dbd1bce0a7c37a3ad51ad71379b74f4a174e17dbf0247')
validpgpkeys=(CC7D978417C1AEA1E4CDD7240E41AB4BF4C610B4) # wim <wim@42.be>

prepare() {
  cd ${pkgname}-${pkgver}
  go mod vendor
}

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw -ldflags=-linkmode=external"
  go build -v -o ${pkgname} .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 ${pkgname} -t "${pkgdir}"/usr/bin/
  install -Dm644 matterbridge.toml.sample "${pkgdir}"/etc/matterbridge.toml
  install -Dm644 ../${pkgname}.service -t "${pkgdir}"/usr/lib/systemd/system/
}
