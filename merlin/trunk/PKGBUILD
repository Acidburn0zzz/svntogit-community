# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=merlin
pkgver=3.3.2
pkgrel=1
pkgdesc="Context sensitive completion for OCaml in Vim and Emacs"
arch=('x86_64')
url="https://github.com/ocaml/merlin"
license=('MIT')
options=('!strip')
depends=('glibc' 'ocaml-findlib')
makedepends=('ocaml-yojson' 'dune' 'opam')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/v${pkgver}.tar.gz")
sha256sums=('f0d9f51d529657f5b830ad6b2cd1fdd5ab6f229d28e521d33197a5b11fb88f9f')

build() {
    cd ${pkgname}-${pkgver}
    dune build -p merlin
}

package() {
    cd ${pkgname}-${pkgver}
    
    DESTDIR="${pkgdir}" dune install merlin --prefix=/usr

    install -Dm644 LICENSE_MIT.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt

    # Fix path
    install -d "${pkgdir}"/usr/share/vim
    mv "${pkgdir}"/usr/share/{merlin/vim,vim/vimfiles}
    rmdir "${pkgdir}"/usr/share/merlin

    # Remove useless files
    rm -r "${pkgdir}"/usr/{doc,lib}
}
