# Maintainer: Johannes Löthberg <johannes@kyriasis.com>
# Contributor: Alexander F Rødseth <xyproto@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Kristoffer Fossgård <kfs1@online.no>
# Contributor: clonejo <clonejo@shakik.de>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgname=terminus-font
pkgver=4.49
pkgrel=2

pkgdesc='Monospace bitmap font (for X11 and console)'
url='http://terminus-font.sourceforge.net/'
arch=('any')
license=('GPL2' 'custom:OFL')
makedepends=('xorg-bdftopcf' 'python')
conflicts=('terminus-font-otb')
replaces=('terminus-font-otb')
source=("https://downloads.sourceforge.net/project/terminus-font/terminus-font-$pkgver/terminus-font-$pkgver.tar.gz"
        fix-75-yes-terminus.patch)
sha256sums=('db0a74551a23d2229db6ffea469ca8e390625ace4069dd1c1a3d05a16ed2634d'
            'ddd86485cf6d54e020e36f1c38c56e8b21b57c23a5d76250e15c1d16fed9caa5')

prepare() {
  cd "terminus-font-$pkgver"
  patch -p1 <"$srcdir"/fix-75-yes-terminus.patch
}

build() {
  cd "terminus-font-$pkgver"

  ./configure \
    --prefix=/usr \
    --x11dir=/usr/share/fonts/misc \
    --otbdir=/usr/share/fonts/misc \
    --psfdir=/usr/share/kbd/consolefonts
  make all otb
}

package() {
  make -C "terminus-font-$pkgver" DESTDIR="$pkgdir" install install-otb

  install -Dm644 "$srcdir/terminus-font-$pkgver/75-yes-terminus.conf" \
    "$pkgdir/etc/fonts/conf.avail/75-yes-terminus.conf"
  install -Dm644 "$srcdir/terminus-font-$pkgver/OFL.TXT" \
    "$pkgdir/usr/share/licenses/terminus-font/LICENSE"
  install -d "$pkgdir/etc/fonts/conf.d"

  cd "$pkgdir/etc/fonts/conf.d"
  ln -s ../conf.avail/75-yes-terminus.conf .
}

# vim:set ts=2 sw=2 et:
