# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_srcname=opencl-clang
pkgname=intel-${_srcname}
pkgver=8.0.1
pkgrel=2
pkgdesc="A wrapper library around clang that can compile OpenCL C kernels to SPIR-V modules"
arch=(x86_64)
url="https://github.com/intel/opencl-clang/"
license=(custom)
depends=(clang llvm-libs)
makedepends=(cmake llvm spirv-llvm-translator git)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-0001-update-revision-of-spirv-llvm-translator.patch"::'https://github.com/intel/opencl-clang/commit/a6e69b30a6a2c925254784be808ae3171ecd75ea.patch'
        "${pkgname}-0002-align-with-modified-llvm-writespirv-api.patch"::'https://github.com/intel/opencl-clang/commit/94af090661d7c953c516c97a25ed053c744a0737.patch')
sha256sums=('f24880407214c0d726829392cfe30a54f8259700d17e9c3d5e7bfa6f237e1153'
            '2f8f6b0fdc10e83670704b98638fc2117863d277486add942cc07484c4274de5'
            '1c8dd6951df94ab4e73a07b27e158b24d8f81142fa2e33720e7a2a8a13322af8')

prepare() {
    mkdir -p build
    
    # fixes for spirv-llvm-translator 8.0.1.2
    cd ${_srcname}-${pkgver}
    patch -Np1 -i "${srcdir}/${pkgname}-0001-update-revision-of-spirv-llvm-translator.patch"
    patch -Np1 -i "${srcdir}/${pkgname}-0002-align-with-modified-llvm-writespirv-api.patch"
}

build() {
    cd build
    cmake ../${_srcname}-${pkgver} \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLLVMSPIRV_INCLUDED_IN_LLVM=OFF \
        -DSPIRV_TRANSLATOR_DIR=/usr \
        -DLLVM_NO_DEAD_STRIP=ON \
        -Wno-dev
    make
}

package() {
    cd build
    make DESTDIR="${pkgdir}" install
    install -Dm644 ../${_srcname}-${pkgver}/LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
