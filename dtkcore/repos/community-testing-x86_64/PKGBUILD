# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.9.10
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base' 'gsettings-qt')
makedepends=('qt5-tools' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz"
        fix-symbol.patch)
sha512sums=('9ee89a80070ce1d21c565d5511254dcd77e1e9c7836c66e760316e8ff1393e4f81670eae663355bc6250a49061d2b0e957ed1e8f6540c19887443e10e0fa40ae'
            '0b3e8e673136b755029e926c7acabb033fea7d89c4b80d85d1c3419faffdcfb6f8ed75214ed9a4226ae2ee7925c797e73e5acbb6f679d6d98879c15965f0b773')

build() {
  cd dtkcore-$pkgver
  patch -p1 -i ../fix-symbol.patch

  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
