# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=0.1.0
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz")
sha512sums=('06ed9fe21eaa8edd192f0bcb64e7ec9d6a8bd6687c61f6e4cbe65d6b7b18a7db59cb167021fdcab03e0e5f1ef20d8f4b59ac5522b316d155feba00a47ed7bddd')

prepare() {
  cd dtkcore-$pkgver
  sed -e '/link_pkgconfig/d' -e '/gsettings-qt/d' -i src/filesystem/filesystem.pri
}

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
