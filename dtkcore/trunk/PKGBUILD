# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=dtkcore
pkgver=2.0.9.17
pkgrel=1
epoch=1
pkgdesc='DTK core modules'
arch=('x86_64')
url="https://github.com/linuxdeepin/dtkcore"
license=('GPL3')
depends=('qt5-base' 'gsettings-qt')
makedepends=('qt5-tools' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dtkcore/archive/$pkgver.tar.gz")
sha512sums=('751405973075d2f10e4f1e8f9902925079e4b1d4a5fb101fbc3fa6e2e8b8be0c2543ecc2b1a53be986e918e6973d56e942fb649ee0d4b6bd4c7ba18bd144005a')

build() {
  cd dtkcore-$pkgver
  qmake-qt5 PREFIX=/usr DTK_VERSION=$pkgver
  make
}

package() {
  cd dtkcore-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
