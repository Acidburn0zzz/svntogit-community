# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=1.5.0
pkgrel=2
pkgdesc='Simple command-line client for controlling cdemu-daemon'
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('python2' 'dbus-python' 'cdemu-daemon')
makedepends=('intltool')
conflicts=('cdemu')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2")
md5sums=('11141138f2b91a7100445d9c32b31181')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/cdemu

  # bash completion dir change
  sed -e 's,bashcompdir = $(sysconfdir)/bash_completion.d,bashcompdir = $(datadir)/bash-completion/completions,' -i data/Makefile.am data/Makefile.in

  ./configure --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
