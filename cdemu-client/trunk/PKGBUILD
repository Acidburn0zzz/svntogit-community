# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=cdemu-client
pkgver=3.0.4
pkgrel=2
pkgdesc="Simple command-line client for controlling cdemu-daemon"
arch=(any)
url="http://cdemu.sourceforge.net/"
license=(GPL)
depends=(python-gobject cdemu-daemon)
makedepends=(intltool cmake)
conflicts=('cdemu')
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        build-fixes.diff)
sha256sums=('e415a98672ac1ed908dab2ba43a94bc0af7653c470a87d6d9338a59d9856fd86'
            '908f919e779832eb05013cb0e3ecb49795afddaa4aa8cb946ec78c3218acf534')

prepare() {
  mkdir build
  cd $pkgname-$pkgver
  patch -Np2 -i ../build-fixes.diff
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
