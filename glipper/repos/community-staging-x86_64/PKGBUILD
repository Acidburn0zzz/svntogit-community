# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Pbl Gomez <pblgomez@gmail.com>

pkgname=glipper
pkgver=1.0
pkgrel=10
pkgdesc="Clipboard for gnome desktop"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/glipper/"
license=("GPL")
depends=(pygtk  gnome-python gnome-python-desktop gnome-python-extras gnome-desktop pygobject)
makedepends=(pkgconfig perlxml gnome-doc-utils)
install=glipper.install
options=(!emptydirs)
source=(http://downloads.sourceforge.net/sourceforge/glipper/$pkgname-$pkgver.tar.bz2)
md5sums=('95315e62b950877611d4645a0b47e1ba')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  
  make DESTDIR=$pkgdir install
  
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/lib/glipper/glipper

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas $pkgdir/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
