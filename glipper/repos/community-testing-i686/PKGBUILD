# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Pbl Gomez <pblgomez@gmail.com>

pkgname=glipper
pkgver=1.0
pkgrel=12
pkgdesc="Clipboard for gnome desktop"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/glipper/"
license=("GPL")
depends=('python2-gconf' 'python-gnomeapplet' 'python2-libgnome' 'hicolor-icon-theme')
makedepends=('pkgconfig' 'perlxml' 'gnome-doc-utils')
optdepends=('pycrypto: encrypted connection support in Network plugin')
install=glipper.install
options=(!emptydirs !libtool)
source=(http://downloads.sourceforge.net/sourceforge/glipper/$pkgname-$pkgver.tar.bz2)
md5sums=('95315e62b950877611d4645a0b47e1ba')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=$pkgdir install

  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    $pkgdir/usr/lib/glipper/glipper

  install -dm755 ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas $pkgdir/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
