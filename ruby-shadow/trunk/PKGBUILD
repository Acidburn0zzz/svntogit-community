# Maintainer: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <greg.sutcliffe@gmail.com>>

pkgname=ruby-shadow
pkgver=2.5.0
pkgrel=5
pkgdesc="Shadow password module"
arch=('x86_64')
url="https://github.com/apalmblad/ruby-shadow"
license=('CPL')
depends=('ruby')
source=($pkgname-$pkgver.tar.gz::https://github.com/apalmblad/$pkgname/archive/$pkgver.tar.gz
        ruby-shadow-2.5.0-cflags.patch)
md5sums=('e9c35620f6c25233b7b54dcab8ee955e'
         '8a501c170f5721881ae35b9d0acc619d')

prepare() {
  cd $pkgname-$pkgver

  # Respect CFLAGS and fix build with Ruby 3.0.0
  # https://github.com/apalmblad/ruby-shadow/issues/26
  patch -Np0 -i ../ruby-shadow-2.5.0-cflags.patch
}

build() {
  cd $pkgname-$pkgver

  ruby extconf.rb
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir"/ sitedir='$(rubylibprefix)/vendor_ruby' \
    sitehdrdir='$(rubyhdrdir)/vendor_ruby' install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
