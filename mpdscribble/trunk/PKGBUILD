# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: evr <evanroman at gmail>
# Contributor: Luiz Ribeiro <luizribeiro@gmail.com>

pkgname=mpdscribble
pkgver=0.21
pkgrel=1
pkgdesc='An mpd client which submits track info to last.fm'
url='http://mpd.wikia.com/wiki/Client:Mpdscribble'
arch=('i686' 'x86_64')
license=('GPL')
depends=('libsoup' 'glib2' 'libmpdclient')
backup=('etc/mpdscribble.conf')
install="${pkgname}.install"
source=("http://downloads.sourceforge.net/musicpd/mpdscribble-${pkgver}.tar.bz2"
        'mpdscribble'
        'mpdscribble.conf.example')
md5sums=('3dc96f9179b8d7efd02cbbceb345e529'
         'f54c937c7b3ab8e4d39e4b903c2993fc'
         '4fc5e58b38cf8da215c791001dc42094')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-http-client=soup

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/mpdscribble \
    ${pkgdir}/etc/rc.d/mpdscribble
  install -D -m644 ${srcdir}/mpdscribble.conf.example \
    ${pkgdir}/etc/mpdscribble.conf
  install -d ${pkgdir}/var/cache/mpdscribble
  touch ${pkgdir}/var/cache/mpdscribble/mpdscribble.cache
}
