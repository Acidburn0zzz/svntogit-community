# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-release
pkgver=0.13.8
pkgrel=2
pkgdesc='Cargo subcommand "release": everything about releasing a rust crate'
url='https://github.com/sunng87/cargo-release'
arch=('x86_64')
license=('MIT' 'APACHE')
depends=('openssl' 'zlib')
makedepends=('cargo')
source=(https://github.com/sunng87/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
b2sums=('0dc3f2177920d12bda2ab209340960aa13ae2e20a113ffabf671f9d5bc274dbbb5ba5e93c8e82c0c5cb478dcb507b8f61e68af43cf8f893514d198ae51489a16')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} "${pkgdir}/usr/bin/${pkgname}"
  install -Dm 644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
