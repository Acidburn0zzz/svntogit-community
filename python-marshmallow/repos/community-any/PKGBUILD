# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-marshmallow
pkgname=(python-marshmallow python2-marshmallow)
pkgver=2.16.2
pkgrel=1
pkgdesc="A lightweight library for converting complex datatypes to and from native Python datatypes."
url="https://github.com/marshmallow-code/marshmallow"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-simplejson'
              'python2-simplejson' 'python-pytz' 'python2-pytz' 'python-dateutil'
              'python2-dateutil')
source=("https://pypi.io/packages/source/m/marshmallow/marshmallow-$pkgver.tar.gz")
sha512sums=('aa46955c543de6930862c119b7bd905b4d09bb9bc07990c2ce6834cf63a3d7777add5a48ef0c2b756b76688c10baf8ec46ce255fc2512cc526abf1b380ebfa09')

prepare() {
  cp -a marshmallow-$pkgver{,-py2}
  rm -r marshmallow-$pkgver-py2/tests/test_py3
}

build() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py build

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/marshmallow-$pkgver
  python setup.py pytest

  cd "$srcdir"/marshmallow-$pkgver-py2
  python2 setup.py pytest
}

package_python-marshmallow() {
  depends=('python')

  cd marshmallow-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-marshmallow() {
  depends=('python2')

  cd marshmallow-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
