# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=golang-deepin-gir
pkgver=2.0.0
pkgrel=1
pkgdesc='Generate static golang bindings for GObject'
arch=('any')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gtk3' 'libgudev')
makedepends=('go-pie' 'gobject-introspection')
checkdepends=('golang-gopkg-check.v1')
provides=('deepin-gir-generator')
conflicts=('deepin-gir-generator')
replaces=('deepin-gir-generator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-gir-generator/archive/$pkgver.tar.gz"
        SettingsBackendLike.patch)
sha512sums=('859b11160be3172e9ffea9120af218808edfb4d2dc5afeb17858e77be9f0a2053b90d3ed50a946251d53ede48f0a4735756fa78babc180a3b1c3392f811b25f7'
            'bd97770e2a345bc1fe4248238f13bd741c157629c5e097c56039326fe7fa4d550c8030272c18c2adc1c0dce35dd72c8d4e6fc394bf4d659076794e6a375d045a')

prepare() {
  mkdir -p "$srcdir"/build/src/pkg.deepin.io

  # Should be fixed upstream
  mkdir -p go-gir-generator-$pkgver/out/src/pkg.deepin.io/gir/{glib-2.0,gobject-2.0,gio-2.0,gudev-1.0}

  # Our gobject-introspection is too new
  # https://cr.deepin.io/#/c/16880/
  patch -d go-gir-generator-$pkgver -p1 -i ../SettingsBackendLike.patch
}

build() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  cd go-gir-generator-$pkgver
  rm -r gogtk-demo
  make

  mv out/src/pkg.deepin.io/gir "$srcdir"/build/src/pkg.deepin.io/
}

check() {
  # https://github.com/linuxdeepin/developer-center/issues/955
  export GOPATH="$srcdir/build:/usr/share/gocode"
  cd "$srcdir"/build/src/pkg.deepin.io/gir
  go test -v $(go list ./...) || warning "Tests failed"

  cd "$srcdir"/go-gir-generator-$pkgver
  go run test/memory.go
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/pkg.deepin.io
  cp -a "$srcdir"/build/src/pkg.deepin.io/gir "$pkgdir"/usr/share/gocode/src/pkg.deepin.io/
}
