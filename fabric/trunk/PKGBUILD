# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=fabric
pkgver=2.4.0
pkgrel=1
pkgdesc="Python library and command-line tool designed to streamline deploying applications or performing system administration tasks via the SSH protocol"
url="https://www.fabfile.org/"
license=('BSD')
arch=('any')
depends=('python-setuptools' 'python-paramiko' 'python-invoke')
optdepends=('python-patchwork: Common deployment/sysadmin operations')
source=("https://github.com/fabric/fabric/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('5da238fd0f76705177d9d026db154c73cea2c5341b9d68b539884bf520f488bbd7c025a0d48438cac3613f6ff587b1755444f61898ada7fbaf511adf0f1eebd4')

build() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py build
}

package() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}"

  install -Dm644 README.rst "${pkgdir}"/usr/share/doc/${pkgname}/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
