# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=fabric
pkgver=2.1.3
pkgrel=2
pkgdesc="Python library and command-line tool designed to streamline deploying applications or performing system administration tasks via the SSH protocol"
url="http://www.fabfile.org/"
license=('BSD')
arch=('any')
depends=('python-setuptools' 'python-paramiko' 'python-invoke')
optdepends=('python-patchwork: Common deployment/sysadmin operations')
source=("https://github.com/fabric/fabric/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('fa29be01f15c905d8f9dcb84e16aaee920f0098d34532e282eea2d1a2ffc316ac2d3e7904a8da7c317c7e0850e061935b682272cc4f5d4050abe01a9a7a54051')

build() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py build
}

package() {
  cd "${srcdir}"/fabric-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}"

  install -Dm644 README.rst "${pkgdir}"/usr/share/doc/${pkgname}/README.rst
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
