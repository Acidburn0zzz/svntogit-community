# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-applet-dock
_pkgname=mate-dock-applet
pkgver=0.88
pkgrel=1
pkgdesc="Application dock for the MATE panel"
arch=('any')
url="https://github.com/robint99/dock-applet"
license=('GPL3')
depends=('bamf' 'libnotify' 'mate-panel' 'python-cairo' 'python-dbus' 'python-gobject'
         'python-pillow' 'python-xdg' 'python-xlib')
source=("$pkgname-$pkgver.tar.gz::https://github.com/robint99/$_pkgname/archive/V$pkgver.tar.gz")
sha256sums=('63acc79d55e3c740a3230747a6498d3a6901fcc9412fae453e270ff8e4116023')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
