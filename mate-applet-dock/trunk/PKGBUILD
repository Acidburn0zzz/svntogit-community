# $Id$
# Contriburor: Balló György <ballogyor+arch at gmail dot com>

pkgname=mate-applet-dock
_pkgname=mate-dock-applet
pkgver=0.78
pkgrel=1
pkgdesc="Application dock for the MATE panel"
arch=('any')
url="https://github.com/robint99/dock-applet"
license=('GPL3')
depends=('mate-panel' 'python-cairo' 'python-gobject' 'python-pillow' 'python-xdg' 'libkeybinder3')
source=("$pkgname-$pkgver.tar.gz::https://github.com/robint99/$_pkgname/archive/V$pkgver.tar.gz")
sha256sums=('b15d99bd5479f8c56942d52b9a0ac96796c2f01694bfc81542bc80c1ee2594e7')

prepare() {
  cd $_pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $_pkgname-$pkgver
  ./configure --prefix=/usr --with-gtk3
  make
}

package() {
  cd $_pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
