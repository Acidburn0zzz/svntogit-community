# $Id$
# Maintainer: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libvisual-projectm
pkgver=2.0.1
pkgrel=3
pkgdesc='ProjectM XMMS plugin.'
arch=('i686' 'x86_64')
url='http://projectm.sourceforge.net/'
license=('GPL')
depends=('projectm>=2.0.1' 'libvisual' 'sdl')
makedepends=('pkgconfig' 'cmake' 'libxext')
options=('!libtool')
source=("http://downloads.sourceforge.net/projectm/projectM_libvisual-${pkgver}-Source.tar.gz"
        'libvisual-projectm-gcc-4.6.patch')
md5sums=('35e09b09210d48b437e3574bd00b15a8'
         'a1fa2fd4fc85b1f27e32d400e17a3196')

build() {
  cd "${srcdir}"

  # fix build failure with GCC 4.6 (taken from SVN, revision 1355)
  (cd "projectM_libvisual-${pkgver}-Source" && patch -p3 -i ../libvisual-projectm-gcc-4.6.patch)

  mkdir build && cd build

  cmake "../projectM_libvisual-${pkgver}-Source" \
    -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "${srcdir}/build"
  make DESTDIR="${pkgdir}" install
}
