# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_hkgname=tz
pkgname=haskell-tz
pkgver=0.1.3.4
pkgrel=2
pkgdesc='Efficient time zone handling'
arch=(x86_64)
url='https://hackage.haskell.org/package/tz'
license=(BSD)
depends=(ghc-libs haskell-vector haskell-data-default haskell-tzdata)
checkdepends=(haskell-test-framework-th haskell-hunit haskell-quickcheck
              haskell-test-framework-quickcheck2 haskell-test-framework-hunit)
makedepends=(ghc uusi)
source=("https://hackage.haskell.org/packages/archive/$_hkgname/$pkgver/$_hkgname-$pkgver.tar.gz")
sha512sums=('7b6cf227b06765685c15712a070a37494c1cee4d5abaefa7ddca9fdf07f9a014eee3ca13eebe63f48cf52d80e441be3f1dd7c7b7d5cdbc914207b7ae2ab9f48c')

prepare() {
  uusi -u tzdata $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
  cd $_hkgname-$pkgver
    
  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --enable-tests \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
  cd $_hkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $_hkgname-$pkgver

  install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
  install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}

# vim: ts=2 sw=2 et:
