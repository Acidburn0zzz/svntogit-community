# $Id$
# Maintainer: judd <jvinet@zeroflux.org>
_pkgbasename=tcp_wrappers
pkgname=lib32-$_pkgbasename
pkgver=7.6
pkgrel=12
pkgdesc="Monitors and Controls incoming TCP connections (32-bit)"
arch=(x86_64)
url="ftp://ftp.porcupine.org/pub/security/index.html"
license=('custom')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
source=(ftp://ftp.porcupine.org/pub/security/${_pkgbasename}_$pkgver.tar.gz
        shared_lib_plus_plus-1.patch
        01_all_redhat-bug11881.patch
        02_all_redhat-bug17795.patch
        03_all_wildcard.patch
        04_all_fixgethostbyname.patch
        07_all_sig.patch
        08_all_strerror.patch
        09_all_gcc-3.4.patch
        10_all_more-headers.patch
        11_inet6_fixes.patch
        tcp-wrappers-7.6-ipv6-1.14.patch)
md5sums=('e6fa25f71226d090f34de3f6b122fb5a'
         '99345104130b91cb151af9d87eee1449'
         'e7ac7ae271703eacf175d597d329e11a'
         '2790301fbf1b4711e75d5b799b6d4ac8'
         'd286da9fca993f8afa89631133312151'
         '97d4d81faaecf0958eeb52c45df71e34'
         '374b8179b8d5c71979008c6a775d658e'
         '801292cacf14a92e2784b925e72a1db1'
         '0978932f49aae33834a46e189ace7d77'
         '68b1c7f82fed60b446b00f6de27c3b9f'
         'bd0af9dedcacd594e0715de800fc1b57'
         '9154c38e3fb69d12b5bfb2fc5284314f')

build() {
  cd $srcdir/${_pkgbasename}_$pkgver
  # add gentoo / fedora / redhat patches
  patch -Np1 -i ../shared_lib_plus_plus-1.patch
  patch -Np1 -i ../01_all_redhat-bug11881.patch
  patch -Np0 -i ../02_all_redhat-bug17795.patch
  patch -Np0 -i ../03_all_wildcard.patch
  patch -Np1 -i ../04_all_fixgethostbyname.patch
  patch -Np1 -i ../07_all_sig.patch
  patch -Np1 -i ../09_all_gcc-3.4.patch
  patch -Np1 -i ../10_all_more-headers.patch
  patch -Np2 -i ../tcp-wrappers-7.6-ipv6-1.14.patch

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  sed -i 's:usr/lib:usr/lib32:g' Makefile

  make REAL_DAEMON_DIR=/usr/sbin STYLE=-DPROCESS_OPTIONS linux
}

package() {
  cd $srcdir/${_pkgbasename}_$pkgver

  # dumb makefile
  mkdir -p $pkgdir/usr/{include,lib32,share/man/man3}

  make DESTDIR=$pkgdir install-lib install-dev

  rm -rf "${pkgdir}"/usr/{include,share}

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
