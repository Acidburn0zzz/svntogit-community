# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Joakim Hernberg <jhernberg at alchemy dot lu>

pkgname=rt-tests
pkgver=1.6
pkgrel=1
pkgdesc="A collection of latency testing tools for the linux(-rt) kernel"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/utils/rt-tests/rt-tests.git/about/"
license=('GPL2')
groups=('realtime')
depends=('glibc')
makedepends=('libnuma.so' 'python')
optdepends=('bash: for determine_maximum_mpps.sh and get_cpuinfo_mhz.sh'
            'libnuma.so: for cyclictest'
            'python: for hwlatdetect')
source=("https://git.kernel.org/pub/scm/utils/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e8cc75d686a1480a4ce50673173a0fd04eb12d68f071d2d61588faaebec1be7735fd39374a768282f04d8262e9a3476c92eb0cd1938303f71077e25d228069a1')

build() {
  cd "${pkgname}-${pkgver}"
  export PYLIB=$(python -c 'import distutils.sysconfig;  print(distutils.sysconfig.get_python_lib())')
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make prefix=/usr DESTDIR="${pkgdir}" install
  install -vDm 644 {MAINTAINERS,README.markdown} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
