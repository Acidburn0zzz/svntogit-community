# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-mongoengine
pkgver=0.18.2
pkgrel=3
pkgdesc="A Python Object-Document-Mapper for working with MongoDB"
url="https://github.com/MongoEngine/mongoengine"
license=('MIT')
arch=('any')
depends=('python-pymongo' 'python-six')
optdepends=('python-dateutil: for DateTimeField'
            'python-pillow: for ImageField / ImageGridFsProxy')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-blinker' 'python-pillow' 'python-mongomock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/MongoEngine/mongoengine/archive/v$pkgver.tar.gz")
sha512sums=('36b2c58bf005b3b04abc7c04d30ecb4bcecbd38f6418e95768eb1bbe5084b8c75288b19553808c951e45285434f1d9e0136dc473f3e0d604a19ef9de6809f6a8')

prepare() {
  sed -i 's/coverage==/coverage>=/' mongoengine-$pkgver/setup.py
  sed -e 's|mongodb://|mongomock://|g' \
      -e "s|connect(db='mongoenginetest')|connect(db='mongoenginetest', is_mock=True)|" \
      -i mongoengine-$pkgver/tests/*.py mongoengine-$pkgver/tests/*/*.py
}

build() {
  cd "$srcdir"/mongoengine-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/mongoengine-$pkgver
  python setup.py nosetests || warning "Tests failed"
}

package() {
  cd mongoengine-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
