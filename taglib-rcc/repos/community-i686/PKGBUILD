# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=taglib-rcc
pkgver=1.5
pkgrel=2
pkgdesc="taglib with librcc patch"
arch=('i686' 'x86_64')
url="http://ktown.kde.org/~wheeler/taglib.html"
depends=('gcc-libs' 'zlib' 'bash' libxml2 db librcc)
makedepends=(patch)
license=('GPL')
provides=("taglib=$pkgver")
conflicts=("taglib")
options=('!libtool')
source=(http://ktown.kde.org/~wheeler/files/src/taglib-$pkgver.tar.gz \
	http://downloads.sourceforge.net/rusxmms/taglib-csa6.tar.bz2)
md5sums=('7b557dde7425c6deb7bbedd65b4f2717'
         '0868d633843ecd87526c77d788b3d3b6')

build() {
  cd $startdir/src/taglib-$pkgver
  patch -Np1 <../taglib/taglib-1.5-ds-rusxmms.patch || return 1
  sed -i 's|#include <string>|#include <string.h>|' taglib/toolkit/rccpatch.cpp

  ./configure --prefix=/usr
  make || return 1
  make DESTDIR=$startdir/pkg install || return 1
}
