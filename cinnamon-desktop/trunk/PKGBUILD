# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=4.6.2
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gtk3' 'libxkbfile' 'libpulse')
makedepends=('meson' 'samurai' 'gobject-introspection' 'intltool')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "config.patch")
sha256sums=('1d04cdccde9f0c2f261fdd2dec64a2ed4c7b2c4ef4f727f1fbb9136437d80549'
            '45f403e6fe4ec701a0f3d16e990bb36598dd4a58fac5a374f85ccecdf74dd98a')
b2sums=('b51428a7a3236dfb81d4c120b44e568e73b5779a3d0af7bdf8dfeeed6ce7c5128e4aef7b7e49a84d0ef83940410a63cedf2b8d50391139f54d0415e4ec2a6467'
        '40efc9c24e1011239f68734c6c0a9459dab95d4330d8c712e674c7d1e99dd01b60833667b52d0ad8c58b31469eee349757ed4c44e4589b6fbfd7236c1499d28b')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    # Adjust configuration
    patch -Np1 -i ../config.patch
}

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr --buildtype=plain ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
