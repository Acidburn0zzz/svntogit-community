# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueria <alexfilgueira@antergos.com>
# Contributor:  Jan de Groot <jan@archlinux.org>

pkgname=cinnamon-desktop
pkgver=3.6.2
pkgrel=1
pkgdesc="Library with common API for various Cinnamon modules"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL' 'LGPL')
depends=('accountsservice' 'gtk3' 'libxkbfile' 'libpulse')
makedepends=('gobject-introspection' 'intltool' 'python')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz"
        "config.patch")
sha256sums=('76b053eea02c61df22d35ccec6ba83de25246b794cbc024dce636dcb1e74c812'
            '45f403e6fe4ec701a0f3d16e990bb36598dd4a58fac5a374f85ccecdf74dd98a')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    # Adjust configuration
    patch -Np1 -i ../config.patch

    NOCONFIGURE=1 autoreconf -fi
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr \
                --sysconfdir=/etc \
                --localstatedir=/var \
                --libexecdir=/usr/lib/$pkgname \
                --disable-static

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
    # rm legacy migration script (cinnamon 2.4) and its package dependency on python
    rm -rf "${pkgdir}"/usr/bin
}
