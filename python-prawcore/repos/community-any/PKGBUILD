# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-prawcore
pkgname=('python-prawcore' 'python2-prawcore')
pkgver=0.11.0
pkgrel=1
pkgdesc="Low-level communication layer for PRAW 4+."
arch=('any')
license=('BSD')
url="https://github.com/praw-dev/prawcore"
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-matchers' 'python2-betamax-matchers'
              'python-betamax-serializers' 'python2-betamax-serializers' 'python-testfixtures'
              'python2-testfixtures')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/prawcore/archive/v$pkgver.tar.gz")
sha512sums=('2f49abca35515836be6e27938f7a7bbdf3b8d8e7c4ffb305f646ecf068968478d724fa7839071110b7960a2313b0ab11f1a0d13e8378ed1b616317612b83aeb9')

prepare() {
  sed -i 's/testfixtures >4.13.2, <5/testfixtures >4.13.2, <6/' prawcore-$pkgver/setup.py
  cp -a prawcore-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/prawcore-$pkgver
  python setup.py test

  cd "$srcdir"/prawcore-$pkgver-py2
  python2 setup.py test
}

package_python-prawcore() {
  depends=('python-requests')

  cd prawcore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-prawcore() {
  depends=('python2-requests')

  cd prawcore-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
