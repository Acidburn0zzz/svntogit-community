# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-prawcore
pkgname=('python-prawcore' 'python2-prawcore')
pkgver=0.10.1
pkgrel=1
pkgdesc="Low-level communication layer for PRAW 4+."
arch=('any')
license=('BSD')
url="https://github.com/praw-dev/prawcore"
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests')
checkdepends=('python-mock' 'python2-mock' 'python-betamax-matchers' 'python2-betamax-matchers'
              'python-betamax-serializers' 'python2-betamax-serializers')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/praw-dev/prawcore/archive/v$pkgver.tar.gz")
sha512sums=('db7a2276dfa06d4f5424e25eec6f5e3370970e6a0249b69db7338bce9f298db5ef3fe5733735fa86219e66b0311b9c580a24f24e544bbcb5dd0bf8f673a18f87')

prepare() {
  cp -a prawcore-$pkgver{,-py2}
}

check() {
  cd "$srcdir"/prawcore-$pkgver
  python setup.py test

  cd "$srcdir"/prawcore-$pkgver-py2
  python2 setup.py test
}

package_python-prawcore() {
  depends=('python-requests')

  cd prawcore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-prawcore() {
  depends=('python2-requests')

  cd prawcore-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
