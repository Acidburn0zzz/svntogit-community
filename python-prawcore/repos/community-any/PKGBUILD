# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-prawcore
pkgver=2.0.0
pkgrel=1
pkgdesc="Low-level communication layer for PRAW 4+."
arch=('any')
license=('BSD')
url="https://github.com/praw-dev/prawcore"
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-betamax-matchers' 'python-betamax-serializers'
              'python-testfixtures')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/prawcore/archive/v$pkgver.tar.gz")
sha512sums=('ff1d0adffa96e0f68032b14555f5e8e7268ce7d3615d071bc22f0019c37032b6a35f76b85f14ee18f7da8b051d08fe17baca6d3a3448c67576f9bf6febab5af6')

check() {
  cd prawcore-$pkgver
  python setup.py test
}

package() {
  cd prawcore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
