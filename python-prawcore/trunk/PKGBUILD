# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-prawcore
pkgver=1.4.0
pkgrel=1
pkgdesc="Low-level communication layer for PRAW 4+."
arch=('any')
license=('BSD')
url="https://github.com/praw-dev/prawcore"
depends=('python-requests')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-betamax-matchers' 'python-betamax-serializers'
              'python-testfixtures')
source=("$pkgname-$pkgver.tar.gz::https://github.com/praw-dev/prawcore/archive/v$pkgver.tar.gz")
sha512sums=('4fda3cb7e403a958ec8ed25faf7d9ca23c8bfdf6fed7c47a8cfafac37314cf821dcba3523b0d2b8a879b4f010287c2c6eb627c18af0c61952b70b12cd335ed10')

check() {
  cd prawcore-$pkgver
  python setup.py test
}

package() {
  cd prawcore-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
