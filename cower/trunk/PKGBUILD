# $Id$
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower
pkgver=2.9.9
pkgrel=1
pkgdesc="A simple AUR agent with a pretentious name"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('GPL')
depends=('curl' 'yajl' 'pacman')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('557c52ccca82c89118d892bd7a3e5786')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  if readelf -s /usr/lib/libalpm.so | grep -q alpm_find_satisfier; then
    PMCHECK=-D_HAVE_ALPM_FIND_SATISFIER
  fi

  make PMCHECK=$PMCHECK
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make PREFIX=/usr DESTDIR=${pkgdir} install
}

# vim: ft=sh syn=sh
