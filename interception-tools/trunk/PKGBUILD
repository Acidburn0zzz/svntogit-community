# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Francisco Lopes

pkgname=interception-tools
pkgver=0.2.2
pkgrel=2
pkgdesc='A minimal composable infrastructure on top of libudev and libevdev'
arch=('x86_64')
license=('GPL3')
url='https://gitlab.com/interception/linux/tools'
depends=('libevdev' 'yaml-cpp' 'systemd-libs')
makedepends=('cmake' 'systemd')
# Until https://gitlab.com/interception/linux/tools/-/merge_requests/10 is
# merged, include a .service file ourselves
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/interception/linux/tools/-/archive/v$pkgver/tools-v$pkgver.tar.gz"
        'udevmon.service')
sha256sums=('56fc110917b05b7ec00b440436fd536a30275449e574978a72bb1f102e9a731a'
            '862b629a8b3f3b1a5ce7e916994fbcbc3d8891d41707c3e2497c434bdc238ee9')

build() {
    cmake -B build \
          -S "tools-v${pkgver}" \
          -DCMAKE_INSTALL_PREFIX=/usr \
          -DCMAKE_BUILD_TYPE=None \
          -Wno-dev
    cmake --build build
}

package() {
    install -dm755 "$pkgdir/etc/interception/udevmon.d"
    install -Dm644 udevmon.service -t "$pkgdir/usr/lib/systemd/system"
    install -Dm644 "tools-v${pkgver}/README.md" -t "$pkgdir/usr/share/doc/$pkgname"

    cd build
    make DESTDIR="$pkgdir/" install
}
