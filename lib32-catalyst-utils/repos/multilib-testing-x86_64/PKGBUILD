# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vi0L0, wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert

pkgname=lib32-catalyst-utils
pkgver=12.9
pkgrel=0.1
pkgdesc="AMD Catalyst drivers libraries (32 bits)"
arch=('x86_64')
url="http://www.amd.com"
license=('custom')
source=(#"http://www2.ati.com/drivers/linux/amd-driver-installer-${pkgver/./-}-x86.x86_64.zip"
        https://launchpad.net/ubuntu/quantal/+source/fglrx-installer/2:9.000-0ubuntu1/+files/fglrx-installer_9.000.orig.tar.gz)
md5sums=('2bb6e8193fdddff9bbd2e3d9f9e8f39b')
provides=('lib32-libgl')
conflicts=('lib32-libgl' 'lib32-nvidia-utils')

#build() {
#  cd ${srcdir}
#  
#  sh ./amd-driver-installer*.run --extract fglrx-install
#}

package() {
  depends=("catalyst-utils=${pkgver}")

  install -dm755 "${pkgdir}"/usr/bin
  install -dm755 "${pkgdir}"/usr/lib32/{dri,xorg/modules/{dri,extensions/fglrx}}
  
  # binaries
  cd ${srcdir}
  install -m755 arch/x86/usr/X11R6/bin/fgl_glxgears "${pkgdir}/usr/bin/fgl_glxgears32"
  install -m755 arch/x86/usr/X11R6/bin/fglrxinfo "${pkgdir}/usr/bin/fglrxinfo32"
  
  # dri/gl/... drivers
  cd ${srcdir}/xpic/usr/X11R6/lib/modules
  install -m755 *.so "${pkgdir}/usr/lib32/xorg/modules/"
  cd ${srcdir}/arch/x86/usr/X11R6/lib
  install -m755 modules/dri/fglrx_dri.so "${pkgdir}/usr/lib32/xorg/modules/dri/"
  ln -s /usr/lib32/xorg/modules/dri/fglrx_dri.so "${pkgdir}/usr/lib32/dri/"
  install -m755 fglrx/fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/"
  install -m755 *.so* "${pkgdir}/usr/lib32/"
  install -m644 libAMDXvBA.cap "${pkgdir}/usr/lib32/"
  cd ${srcdir}/arch/x86/usr/lib
  install -m755 *.so* "${pkgdir}/usr/lib32/"
  # needed symlinks
  ln -s libatiuki.so.1.0 "${pkgdir}/usr/lib32/libatiuki.so.1"
  ln -s fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so.1"
  ln -s fglrx-libGL.so.1.2 "${pkgdir}/usr/lib32/libGL.so"
  ln -s libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/libAMDXvBA.so.1"
  ln -s libAMDXvBA.so.1.0 "${pkgdir}/usr/lib32/libAMDXvBA.so"
  ln -s libfglrx_dm.so.1.0 "${pkgdir}/usr/lib32/libfglrx_dm.so.1"
  
  # useful for 32 bits ?
  rm "${pkgdir}"/usr/lib32/lib{amdocl*,OpenCL}.so* 

  # licenses
  install -Dm644 "${srcdir}/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
