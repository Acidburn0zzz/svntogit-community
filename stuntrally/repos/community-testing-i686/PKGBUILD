# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Sven Schneider <archlinux.sandmann@googlemail.com>
# Contributor: Jason Melton <jason.melton@gmail.com>

pkgname=stuntrally
pkgver=1.9
pkgrel=3
pkgdesc='Stunt Rally game with track editor, based on VDrift'
arch=('x86_64' 'i686')
license=('GPL3')
url='http://code.google.com/p/vdrift-ogre'
depends=('libvorbis' 'mygui' 'sdl' 'enet' 'hicolor-icon-theme' 'libxcursor' 'stuntrally-data')
makedepends=('cmake' 'boost')
install=stuntrally.install
source=("$pkgname-$pkgver.tar.gz::https://nodeload.github.com/$pkgname/$pkgname/tar.gz/$pkgver")
sha256sums=('651d10aa00cf73b275198f08b0360a34ff98e6b520013041863854babeb20da7')

build() {
  cd "$srcdir/stuntrally-$pkgver/"

  rm -rf build
  mkdir build && cd build
  cmake -DCMAKE_INSTALL_PREFIX="/usr" -DSHARE_INSTALL="share/stuntrally" ..
  make
}

package() {
  cd "$srcdir/stuntrally-$pkgver/build/"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/stuntrally/"
}

# vim:set ts=2 sw=2 et:
