# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
pkgver=0.11.6
pkgrel=1
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('i686' 'x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('gconf' 'libgee' 'libgphoto2' 'libunique' 'libwebkit' 'libgexiv2' 'libraw'
	 'librsvg' 'json-glib' 'desktop-file-utils' 'dconf')
makedepends=('intltool')
# 'vala'
install=shotwell.install
source=("http://yorba.org/download/shotwell/0.11/shotwell-${pkgver}.tar.bz2"
	"http://ftp.gnome.org/pub/gnome/sources/vala/0.12/vala-0.12.1.tar.bz2"
	"fix-with-libraw-0.4.patch")
md5sums=('bbbf2843eb98b6f0ef1dfcfb934e15cf'
         'bf35262cc611de447147d01cbac33767'
         '488787b6d2cbc7348bc88348b78b6005')

build() {
  cd "${srcdir}/vala-0.12.1"
  ./configure --prefix=$srcdir/vala --enable-vapigen
  make
  make install
  export PATH=$srcdir/vala/bin:$PATH

  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ../fix-with-libraw-0.4.patch

  ./configure --prefix=/usr \
	--with-gconf-schema-file-dir=/usr/share/gconf/schemas \
	--disable-schemas-install \
	--disable-desktop-update \
	--disable-icon-update

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -f $pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled
}
