# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Joeny Ang <ang(dot)joeny(at)gmail(dot)com>

pkgname=shotwell
epoch=2
pkgver=0.30.8
_basever=${pkgver%.*}
pkgrel=2
pkgdesc="A digital photo organizer designed for the GNOME desktop environment"
arch=('x86_64')
url="http://yorba.org/shotwell/"
license=('LGPL2.1')
depends=('libgee' 'webkit2gtk' 'libgexiv2' 'libraw' 'rest'
	 'desktop-file-utils' 'dconf' 'hicolor-icon-theme' 'json-glib'
	 'gst-plugins-base' 'libgphoto2' 'libgudev'
	 'gcr' 'libgdata')
makedepends=('intltool' 'vala>=0.17.2' 'itstool' 'meson' 'ninja')
# odd versions are unstable!
source=("https://download.gnome.org/sources/shotwell/${_basever}/shotwell-$pkgver.tar.xz"
        0115-fix_meson_build.patch)
sha256sums=('722d2ed2ffc843b150490d5e346395398b0b5f40dd706034fa3a70f30e00f5b8'
            'ccc6127df325df5d36732e670e938a88dfb986f46ef860baaf5a23867db4158b')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../0115-fix_meson_build.patch
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}
