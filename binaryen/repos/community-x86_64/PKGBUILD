# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=binaryen
epoch=1
pkgver=1.39.1
pkgrel=1
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
makedepends=(cmake ninja python git)
source=("$pkgname-$pkgver.tar.gz::https://github.com/WebAssembly/binaryen/archive/${pkgver}.tar.gz"
        "binaryen.sh")
sha384sums=('4c92a4d6e798cf6f4c472ff7370eea2b4ea17fb7355acf34f2bf17901aa689ad208f1ce4930f28323c8c030b43f68366'
            'fdf76d46e6ab9b915212bbca7c29e624e209de911b18ecaccc40b99a00360cd12b261f96c8655f02ec4fd28cedffbcac')

prepare() {
    mkdir binaryen-${pkgver}/build
}

build() {
    cd binaryen-${pkgver}/build

    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ..
    ninja
}

package() {
    cd binaryen-${pkgver}/build
    DESTDIR=${pkgdir} ninja install

    install -Dm755 $srcdir/binaryen.sh $pkgdir/etc/profile.d/binaryen.sh
    install -Dm644 ../LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
