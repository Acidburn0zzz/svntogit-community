# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=binaryen
epoch=1
pkgver=94
pkgrel=1
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
makedepends=('cmake' 'ninja' 'python')
source=("$pkgname-$pkgver.tar.gz::https://github.com/WebAssembly/binaryen/archive/version_${pkgver}.tar.gz"
        "binaryen.sh")
sha384sums=('629fd6390db86d592ec8264f89466d14185d86a796d690b25591acb8844621c935152b083dca845ff166bdf03d1326c2'
            '8ccff587d33aa2f8e66cc167ab887087993cea0899921d2e7cc69372c8b659770a4f4a00e9da61c75394af002c8c7660')

build() {
    cd binaryen-version_$pkgver

    cmake . \
        -Bbuild \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr
    ninja -C build
}

package() {
    cd binaryen-version_$pkgver
    DESTDIR="$pkgdir" ninja -C build install

    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    install -Dm755 "$srcdir"/binaryen.sh "$pkgdir"/etc/profile.d/binaryen.sh
}
