# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Dario Ostuni <dario.ostuni@gmail.com>

pkgname=binaryen
epoch=1
pkgver=90
pkgrel=2
pkgdesc="Compiler infrastructure and toolchain library for WebAssembly, in C++"
arch=('x86_64')
url="https://github.com/WebAssembly/binaryen"
license=('MIT')
makedepends=(cmake ninja python git)
source=("$pkgname-$pkgver.tar.gz::https://github.com/WebAssembly/binaryen/archive/version_${pkgver}.tar.gz"
        "https://github.com/WebAssembly/binaryen/pull/2578.patch"
        "https://github.com/WebAssembly/binaryen/pull/2580.patch"
        "binaryen.sh")
sha384sums=('0e041c75c870ab0d1462b6dc4b9460daa3a7949e8eba1f931a7b8c0ae1d016158b442089de8a34f2738586107e7e394d'
            'b3b46b1f1e79e680a55a774e7e7e2990901c2ab214ce5f89ac1b5da079fee48357292fff757441487e1c637254e8415d'
            '1efdb51108341c2f896a2552c03e8cf377eae9d27261b573aef26df3d861ed74606c48985c23828890b6938520908df3'
            'fdf76d46e6ab9b915212bbca7c29e624e209de911b18ecaccc40b99a00360cd12b261f96c8655f02ec4fd28cedffbcac')

prepare() {
    mkdir -p binaryen-version_$pkgver/build
    cd binaryen-version_$pkgver/
    patch -p1 < "$srcdir"/2578.patch
    patch -p1 < "$srcdir"/2580.patch
}

build() {
    cd binaryen-version_$pkgver/build

    cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr ..
    ninja
}

package() {
    cd binaryen-version_$pkgver/build
    DESTDIR="$pkgdir" ninja install

    install -Dm755 "$srcdir"/binaryen.sh "$pkgdir"/etc/profile.d/binaryen.sh
    install -Dm644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
