# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jonathan Conder <jonno.conder@gmail.com>

pkgbase='packagekit'
pkgname=('packagekit' 'python2-packagekit' 'python-packagekit')
pkgver=1.0.3
pkgrel=8
pkgdesc='A system designed to make installation and updates of packages easier'
arch=('i686' 'x86_64')
url='http://www.packagekit.org/'
license=('GPL')
makedepends=('dbus-glib' 'gobject-introspection' 'gtk-doc' 'intltool'
	'networkmanager' 'pacman>=4.2.0'  'pm-utils' 'polkit' 'python2'
	'shared-mime-info' 'sqlite' 'python2' 'python' 'bash-completion')
source=("http://www.freedesktop.org/software/PackageKit/releases/PackageKit-${pkgver}.tar.xz"
	'0001-alpm-versioning-8.2.0.patch::https://github.com/hughsie/PackageKit/commit/50cf2f652b9bfd89f153e4071474851bd93b83ef.patch'
	'0002-pacman-4.2.0.patch::https://github.com/hughsie/PackageKit/commit/99c7d79f3626fe79f8225464b7601f55d4063f20.patch'
	'0003-alpm-versioning.patch::https://github.com/hughsie/PackageKit/commit/19903a4a90028a9c7e27f953877e2654a7dd3375.patch'
	'0004-alpm-versioning-9.0.0.patch::https://github.com/eworm-de/PackageKit/commit/7d34f8da09fd2cb14daccf960e69c19efae67f7a.patch'
	'0005-fix-sigsegv.patch::https://github.com/hughsie/PackageKit/commit/e75924e9ec361b564233b335d76d832e5d592131.patch'
	'0006-fix-columns.patch::https://github.com/hughsie/PackageKit/commit/0aee338b8e5466573b526cb08a1813b2f40b7c48.patch'
	'0007-honor-simulation-when-installing-packages.patch::https://github.com/hughsie/PackageKit/commit/df1003d58b6466be0932f97c21b999e24d57527b.patch'
	'0008-alpm-fix-bad-logic-in-pk_backend_resolve_name.patch::https://github.com/hughsie/PackageKit/commit/ed43f18b5fb7764d446bed3b08bdf178abfab3a7.patch'
	'0009-alpm-pk_backend_sync_thread.patch::https://github.com/hughsie/PackageKit/commit/3c58cb37f167f1e8c3daac4ff58699d6f7264a93.patch'
	'0010-alpm-clean-logic-in-pk_alpm_transaction_packages.patch::https://github.com/hughsie/PackageKit/commit/3b414bed88d8a4865ff41e074949262b6de7c8e1.patch')

prepare() {
	cd "${srcdir}/PackageKit-${pkgver}"

	# alpm 9.0.0
	patch -Np1 < ${srcdir}/0001-alpm-versioning-8.2.0.patch
	patch -Np1 < ${srcdir}/0002-pacman-4.2.0.patch
	patch -Np1 < ${srcdir}/0003-alpm-versioning.patch
	patch -Np1 < ${srcdir}/0004-alpm-versioning-9.0.0.patch

	# fix SIGSEV when asking for package files
	patch -Np1 < ${srcdir}/0005-fix-sigsegv.patch

	# pkcpn: fix columns
	patch -Np1 < ${srcdir}/0006-fix-columns.patch

	# honor simulation when installing packages
	patch -Np1 < ${srcdir}/0007-honor-simulation-when-installing-packages.patch

	# fix package update
	patch -Np1 < ${srcdir}/0008-alpm-fix-bad-logic-in-pk_backend_resolve_name.patch
	patch -Np1 < ${srcdir}/0009-alpm-pk_backend_sync_thread.patch
	patch -Np1 < ${srcdir}/0010-alpm-clean-logic-in-pk_alpm_transaction_packages.patch

	# copy after patching but before fixing for python2
	cp -r "${srcdir}/PackageKit-${pkgver}" "${srcdir}/PackageKit-${pkgver}-python3"

	sed -i 's|python |python2 |' 'lib/python/packagekit/Makefile.in'
	sed -i 's|bin/python|bin/python2|' 'lib/python/packagekit/'*.py
}

build() {
	cd "${srcdir}/PackageKit-${pkgver}"

	export PYTHON=/usr/bin/python2
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--enable-strict \
		--disable-static \
		--disable-gtk-doc \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-dummy \
		--enable-alpm \
		--with-default-backend=alpm
	make

	cd "${srcdir}/PackageKit-${pkgver}-python3"

	export PYTHON=/usr/bin/python
	./autogen.sh --prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--libexecdir=/usr/lib/PackageKit \
		--enable-strict \
		--disable-static \
		--disable-gtk-doc \
		--disable-tests \
		--disable-local \
		--disable-browser-plugin \
		--disable-gstreamer-plugin \
		--disable-gtk-module \
		--disable-command-not-found \
		--disable-cron \
		--disable-debuginfo-install \
		--enable-pm-utils \
		--disable-dummy \
		--enable-alpm \
		--with-default-backend=alpm
	make
}

package_packagekit() {
	depends=('dbus-glib' 'pacman' 'polkit' 'shared-mime-info' 'sqlite')
	optdepends=('networkmanager: detect connection status'
		'bash-completion: command completion in bash')
	backup=('var/lib/PackageKit/transactions.db'
		'etc/PackageKit/alpm.d/pacman.conf'
		'etc/PackageKit/alpm.d/repos.list')
	install='packagekit.install'

	cd "${srcdir}/PackageKit-${pkgver}"

	make DESTDIR="${pkgdir}" install
}

package_python2-packagekit() {
	depends=('packagekit' 'python2')
	pkgdesc=('Python 2 bindings for PackageKit')
	conflicts=('packagekit-python')
	replaces=('packagekit-python')

	cd "${srcdir}/PackageKit-${pkgver}/lib/python/"

	make DESTDIR="${pkgdir}" install
}

package_python-packagekit() {
	depends=('packagekit' 'python')
	pkgdesc=('Python 3 bindings for PackageKit')

	cd "${srcdir}/PackageKit-${pkgver}-python3/lib/python/"

	make DESTDIR="${pkgdir}" install
}

sha256sums=('88c652e0bb4e39e0e0d89ad50cff3cd91c67c923250199d16419606ee79b5eef'
            '03bb902dafb9d44c960dead9dbff29830daeea948526af98634319466e809b44'
            '21790e44b0a8489896281c7664a50a3c88794e669af0f47e8185c54db448ac1b'
            '804fc47e3f371200db088c2d7d768ff464bfbd7d7691663528dd0604da840389'
            'c6f5a2f85a062d38fb500d18eb17b9cdecd8525f8c1136a2949c3070849641de'
            '7f33ec3c0455bdcafd0a5ee26c70e8368a3268413d3ee8c6cdad52f4ed7cc78b'
            '934f767148ec567b078426534060e9958182f0077cc53fa997a4d9a49865524a'
            '5cfe5c75721d3f57bf520bf211b510c771f888b11c6396d1df10f7d439fb4a46'
            'd90762bbdc893c833863ab9a59f7084cef8f44b3ba99089b64a7f12c26c501fb'
            '73723f1717849ff171d9a1a3a9a46a353b23e674db33909cc22e167d6dc0fc3a'
            '30ec77f05210bc37e0d75f476bc679c67f98622be70f89801a847a506476f6ff')
