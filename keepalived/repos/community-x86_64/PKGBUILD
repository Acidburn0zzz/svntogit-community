# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Andrea Zucchelli <zukka77@gmail.com>

pkgname=keepalived
pkgver=1.2.2
pkgrel=4
pkgdesc='Failover and monitoring daemon for LVS clusters'
arch=('i686' 'x86_64')
url='http://www.keepalived.org/'
license=('GPL2')
backup=('etc/keepalived/keepalived.conf' 'etc/conf.d/keepalived')
depends=('openssl' 'popt' 'libnl1')
source=("http://www.keepalived.org/software/$pkgname-$pkgver.tar.gz"
        'keepalived.rc'
        'keepalived.service'
        'keepalived.conf'
        'ip_vs_moved.patch')
md5sums=('f68327ca142616a8463d2d105db122cd'
         'baeb62a682c2b59d732cea84bf52cc87'
         '7a327df915700dcd45a049d099c9c42b'
         '9c8142da8730ad27a345587dea83ced7'
         '87ee19c85adaedd3eb11bea8a5865d47')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i ../ip_vs_moved.patch
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # clean pacakge init scripts
  rm -r "$pkgdir/etc/rc.d/init.d" "$pkgdir/etc/sysconfig"
  # move examples to /usr/share
  install -d -m 755 "$pkgdir/usr/share/$pkgname"
  mv "$pkgdir/etc/keepalived/samples" "$pkgdir/usr/share/$pkgname/samples"

  # add initscripts
  cd ..
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  install -D -m 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"

}

# vim:set ts=2 sw=2 ft=sh et:
