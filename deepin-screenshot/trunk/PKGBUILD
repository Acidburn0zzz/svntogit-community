# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=3.1.15
pkgrel=1
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('any')
url="https://github.com/linuxdeepin/deepin-screenshot"
license=('GPL3')
depends=('deepin-menu' 'deepin-qml-widgets' 'pygtk' 'qt5-quickcontrols'
         'qt5-svg' 'python2-pyqt5' 'python2-wnck' 'python2-xpybutil'
         'python2-opengl')
makedepends=('deepin-gettext-tools')
optdepends=('deepin-social-sharing: for "Share" support'
            'deepin-daemon: for sound effect')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screenshot/archive/$pkgver.tar.gz")
sha512sums=('ecef43deb19ee5c063ad065851d7480647dadbf329a78852dccaab866d9a8907697b15987d2106212524003e39fe3b561f1865df0f9a2ece1687689d97279b56')

prepare() {
  cd deepin-screenshot-$pkgver

  # fix python version
  find src -type f | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-screenshot-$pkgver
  make
}

package() {
  cd deepin-screenshot-$pkgver
  make DESTDIR="$pkgdir" install
}

 # Local Variables:
 # sh-basic-offset: 2
 # End:
