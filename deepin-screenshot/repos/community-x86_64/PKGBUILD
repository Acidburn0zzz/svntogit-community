# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-screenshot
pkgver=5.0.0
pkgrel=2
pkgdesc="Easy-to-use screenshot tool for linuxdeepin desktop environment"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-screenshot"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'deepin-turbo')
makedepends=('cmake' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-screenshot/archive/$pkgver.tar.gz"
        deepin-screenshot-no-notification.patch)
sha512sums=('ad073961deaa78a47739af6cf8b0d0fa77a92ee6668c57f9a5bbf51ad2be1d2028681cffabdc26548be1bdbe24c7ccc7e7ff3d49b86907f794495bfbea62aa30'
            'ffd0b306017e54edd6c910e2eca8881d000d4ef6f879683b2d14a8c685dbd4d5a9e13db2358b1622f6bcff4e0ebf90e15d14c6f084e3ae9c0b3857a0ddafde58')

prepare() {
  cd deepin-screenshot-$pkgver
  patch -p1 -i ../deepin-screenshot-no-notification.patch
}

build() {
  cd deepin-screenshot-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd deepin-screenshot-$pkgver
  make DESTDIR="$pkgdir" install
}
