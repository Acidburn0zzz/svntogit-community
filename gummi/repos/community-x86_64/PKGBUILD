# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sergio A. Morales <sergiomorales@archlinux.cl>
# Contributor: soeren <nonick@posteo.de>

pkgname=gummi
# FS#63649
epoch=2
pkgver=0.6.6
pkgrel=1
pkgdesc='Simple LaTex editor for GTK2 users'
arch=(x86_64)
url='https://github.com/alexandervdm/gummi'
license=(MIT)
depends=(gtksourceview2 gtkspell poppler-glib texlive-core)
makedepends=(git intltool setconf)
source=("git+$url#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  cd $pkgname

  setconf autogen.sh AM_VERSION ''
  ./autogen.sh
  setconf Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  setconf data/misc/Makefile.in PACKAGE_VERSION "'"$pkgver"'"
  echo $pkgver > dev/latest
  autoreconf
  setconf configure PACKAGE_VERSION "'"$pkgver"'"
  setconf configure PACKAGE_STRING "'""Gummi $pkgver""'"
  setconf configure PACKAGE_BUGREPORT "'https://bugs.archlinux.org/'"
  setconf po/Makefile.in.in DATADIRNAME 'share'
}

build() {
  cd $pkgname
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname
  DESTDIR="$pkgdir" make install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
