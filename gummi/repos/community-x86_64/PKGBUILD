# $Id$

# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sergio A. Morales <sergiomorales@archlinux.cl>
# Contributor: soeren <nonick@posteo.de>

pkgname=gummi
pkgver=0.6.2
pkgrel=1
pkgdesc='Simple LaTex editor for GTK users'
arch=('x86_64' 'i686')
url='http://gummi.midnightcoding.org/'
license=('MIT')
depends=('texlive-core' 'gtkspell' 'gtksourceview2' 'poppler-glib')
makedepends=('glib2' 'gtk2' 'pango' 'poppler' 'intltool')
source=("http://dev.midnightcoding.org/attachments/download/258/$pkgname-$pkgver.tar.gz")
sha256sums=('1d886e6036a9a480662f9cf3ae8afa4d1129929c16cb78081755ad1d10be8277')
 
build() {
  cd "$pkgname-$pkgver"

  find . -type f -exec touch {} \;
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 et:
