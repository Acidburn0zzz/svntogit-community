# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sergio A. Morales <sergiomorales@archlinux.cl>
# Contributor: soeren <nonick@posteo.de>

pkgname=gummi
pkgver=0.6.3
pkgrel=3
pkgdesc='Simple LaTex editor for GTK users'
arch=('x86_64' 'i686')
url='http://gummi.midnightcoding.org/'
license=('MIT')
depends=('texlive-core' 'gtkspell' 'gtksourceview2' 'poppler-glib' 'desktop-file-utils')
makedepends=('glib2' 'gtk2' 'pango' 'poppler>=0.20.0' 'intltool')
install=$pkgname.install
source=("http://dev.midnightcoding.org/projects/$pkgname/latest.tar.gz")
sha256sums=('d44196e56ed61e125bba01e5949be0928cf09919fdb80e62e6a41999a478f0ed')
 
build() {
  cd "$pkgname-$pkgver"

  find . -type f -exec touch {} \;
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"

  make prefix="$pkgdir/usr" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
