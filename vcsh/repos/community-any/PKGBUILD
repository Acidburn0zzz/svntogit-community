# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Evan Pitstick <nerdx00 at gmail dot com>
# Contributor: Chet Gray <chetgray@gmail.com>
# Contributor: joni <kljohann@gmail.com>
# Contributor: Dieter Plaetinck <dieter@plaetinck.be>

pkgname=vcsh
pkgver=2.0.3
pkgrel=1
pkgdesc='Version Control System for $HOME that manages multiple Git repositories'
arch=(any)
url="https://github.com/RichiH/$pkgname"
license=(GPL)
depends=(git)
checkdepends=(perl
              perl-shell-command
              perl-test-most)
optdepends=('myrepos: manage multiple vcsh repos through mr')
source=("$pkgname::git+$url.git")
_archive="$pkgname-$pkgver"
source=("$url/releases/download/v$pkgver/$_archive.tar.xz")
sha256sums=('e772596111fb26750bc688d9c836fcd73770b1f24bad08b5ad23189666736204')

build() {
	cd "$_archive"
	./configure --prefix /usr
	make
}

check() {
	cd "$_archive"
	make test
}

package() {
	cd "$_archive"
	make DESTDIR="$pkgdir" install
}
