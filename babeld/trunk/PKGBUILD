# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: Yann Lopez <arch at lonewulf dot net>

pkgname=babeld
pkgver=1.8.1
pkgrel=2
pkgdesc="A loop-avoiding distance-vector routing protocol"
arch=('x86_64')
url="https://www.irif.fr/~jch/software/babel/"
license=('MIT')
depends=('glibc')
backup=('etc/babeld.conf')
source=("https://www.irif.fr/~jch/software/files/$pkgname-$pkgver.tar.gz"{,.asc}
        'babeld.conf' 'babeld.service'
        '0001-Fix-parsing-of-source-length-in-filters.patch')
validpgpkeys=('F8E70AAD74D32CAC27B9AFD62652C881EADB5526')
sha512sums=('ffc6907ae2762ae772da5582a8cd9edb942f57b77ba87d551bf8ec42b61a4413971618d47fc5cca3466198623d1cc41cccc92153b5f7656003e47f23b4683a1c'
            'SKIP'
            '1232684447b4409d6e3813041f4ab1158faa844b5ac65441b5f299f1e353f48679aacbce9cc79e6714e9a3e1b17d2cfa4963c1388dba225ee342ea3cdb7f1194'
            '01e69d19a6176a6bf26ee703939c5bdea3f1245464ed5d3863dc5867a76b8b6a6fc7324912aee17458a0c6275ccdfa5abe63b88f7503c8d4dfd37d3f15f42700'
            'a1043a5b0d46759fd41ef2c970d28e67b0aa8db83cf9d760d1168cc0ad64bfbf56a69ba08c920ef200b7910e28b6b8a4441eb8c6b156c2c58d2edbcba97a3cf8')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < "$srcdir"/0001-Fix-parsing-of-source-length-in-filters.patch
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX="/usr"
}

package() {
  cd "$pkgname-$pkgver"
  make PREFIX="/usr" TARGET="$pkgdir" install

  install -Dm644 ../babeld.conf "$pkgdir"/etc/babeld.conf
  install -Dm644 ../babeld.service "$pkgdir"/usr/lib/systemd/system/babeld.service
  install -D -m644 LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
