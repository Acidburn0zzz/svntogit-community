# $Id$
# Maintainer: Alexander RÃ¸dseth
# Contributor: Alois Nespor <alois.nespor@gmail.com>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
pkgname=clucene
pkgver=2.3.3.4
pkgrel=3
pkgdesc="C++ port of the high-performance text search engine Lucene"
arch=('x86_64' 'i686')
url="http://clucene.sourceforge.net/"
license=('APACHE' 'LGPL')
depends=('gcc-libs' 'zlib' 'boost-libs')
makedepends=('cmake' 'boost')
source=("http://downloads.sourceforge.net/$pkgname/$pkgname-core-$pkgver.tar.gz")
sha256sums=('ddfdc433dd8ad31b5c5819cc4404a8d2127472a3b720d3e744e8c51d79732eab')

build() {
  cd "$srcdir"

  sed 's:core:core -lclucene-shared:' \
    -i "$pkgname-core-$pkgver/src/core/libclucene-core.pc.cmake"

  mkdir build
  cd build
  cmake "../$pkgname-core-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_ASCII_MODE=OFF \
    -DENABLE_PACKAGING=OFF \
    -DDISABLE_MULTITHREADING=OFF
  make
}

package() {
  cd "$srcdir/build"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
