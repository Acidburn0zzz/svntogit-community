# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Dale Blount <archlinux@dale.us>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=fortune-mod
pkgver=3.10.0
pkgrel=1
pkgdesc="The Fortune Cookie Program from BSD games"
arch=('x86_64')
url="https://www.shlomifish.org/open-source/projects/fortune-mod/"
license=('BSD')
depends=('glibc' 'recode')
makedepends=('cmake' 'rinutils')
source=("https://github.com/shlomif/fortune-mod/releases/download/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz"
        'not-a-game.patch')
sha512sums=('cb4d171405324a8ebac55ca5c7c8d8f5c06b594c2ded1b1c97fb599a4eaf638317f613fbeee4b3ca9ba7060ba4eeff7e678acddac12d49555a33d33017a90f4d'
            'f12bfed15648bf7e53143bfa5c626e0e614d64c64c4329119e7b1ad8a5d79c89d3e8f2c1336d489abecbf6a8ec0ad9acf3080b142d498f8b3f36cea77c82a23d')
b2sums=('e2dcfccee0c8d897971e742e4a5e345f9e825ba73479e2dc056891e16f005dc1d1f843573565741d1b35e4e1d1020cd382db9706f34b0caa4000d265a2c4dd30'
        '3af374f348d42bf649199a79afc18a8f0dace50d2b4c8aa090a1a54ccffcb65bf069d26f0d1d417a395c5a3574f7376c5ae1ec8b97dd2385d6b4b944643b1215')


prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 -i ../not-a-game.patch
}

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DNO_OFFENSIVE=OFF \
    -DLOCALDIR=/usr/share/fortune \
    -DLOCALODIR=/usr/share/fortune/off \
    -DCOOKIEDIR=/usr/share/fortune \
    -DOCOOKIEDIR=/usr/share/fortune/off

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm -f "$pkgdir/usr/share/fortune/"{,off/}*.u8

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/COPYING.txt"
}
