# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
_hkgname=pango
pkgname=gtk2hs-pango
pkgver=0.12.0
pkgrel=1
pkgdesc="Binding to the pango library for Gtk2Hs."
url="http://hackage.haskell.org/package/pango"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc=6.12.3' 'pango' 'gtk2hs-glib' 'gtk2hs-cairo')
makedepends=('gtk2hs-buildtools')
options=('strip')
install=gtk2hs-pango.install
source=(http://hackage.haskell.org/packages/archive/pango/$pkgver/pango-$pkgver.tar.gz)
md5sums=('0260ed21d6ca2a52ebaa0aa212390cf7')

build() {
    cd ${srcdir}/${_hkgname}-$pkgver
    runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
      --prefix=/usr --docdir=/usr/share/doc/${pkgname}
    runhaskell Setup build
    runhaskell Setup haddock
    runhaskell Setup register   --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd ${srcdir}/${_hkgname}-${pkgver}
    install -D -m744 register.sh   ${pkgdir}/usr/share/haskell/${pkgname}/register.sh
    install    -m744 unregister.sh ${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh
    install -d -m755 ${pkgdir}/usr/share/doc/ghc/html/libraries
    ln -s /usr/share/doc/${pkgname}/html ${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}
    runhaskell Setup copy --destdir=${pkgdir}
}
