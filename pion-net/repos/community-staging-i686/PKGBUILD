# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: David Watzke <david@watzke.cz>
pkgname=pion-net
pkgver=4.0.12
pkgrel=1
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="http://www.pion.org/projects/pion-network-library"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
source=("http://www.pion.org/files/$pkgname-$pkgver.tar.bz2" \
        gcc_4.7_fix.diff boost_1.50_support.diff)
md5sums=('baf0b6a006660dc4051c824e4660cb9d'
         'd6c5314678709184e983b301cea11566'
         'd8196bd0c73868990df384422d5b3b7a')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure
  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile.in
  # fix compilation with new versions of gcc
  patch -Np1 < "$srcdir"/gcc_4.7_fix.diff || true
  # fix build with boost 1.50 - port to filesystem v3
  patch -Np1 < "$srcdir"/boost_1.50_support.diff || true

  ./configure --prefix=/usr --disable-doxygen-doc

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
