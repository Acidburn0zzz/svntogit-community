# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: David Watzke <david@watzke.cz>

pkgname=pion-net
pkgver=4.0.7
pkgrel=2
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="http://www.pion.org/projects/pion-network-library"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
source=("http://www.pion.org/files/$pkgname-$pkgver.tar.bz2"
        "$pkgname-4.0.5-build.patch")
md5sums=('761c8af8dc54102b637f8d91d162f987'
         'd75ea2af90163a81d2bc71fda00f0c00')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	patch -p1 -i "$srcdir/$pkgname-4.0.5-build.patch"

	# override configure bug (forces debug even when nobody asked for it)
	sed -i "s/ -ggdb//g;s/ -DNDEBUG//g" configure

	./configure --prefix=/usr --disable-doxygen-doc --with-log4cpp

	find -name Makefile -print0 | xargs -0 \
	sed -i -r '/^C(XX)?FLAGS/ s/$/ -DBOOST_FILESYSTEM_VERSION=2/' Makefile

	# override Makefile bug
	sed -i "/docs:/ s/doxygen-doc//" Makefile

	make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install

	# install license
	install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
