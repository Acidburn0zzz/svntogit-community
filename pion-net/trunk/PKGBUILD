# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: David Watzke <david@watzke.cz>

pkgname=pion-net
pkgver=4.0.11
pkgrel=1
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="http://www.pion.org/projects/pion-network-library"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
source=("http://www.pion.org/files/$pkgname-$pkgver.tar.bz2")
md5sums=('e5cf42a02681c5d73e7be6784d99fece')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure

  ./configure --prefix=/usr --disable-doxygen-doc

  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
