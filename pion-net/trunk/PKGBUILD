# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: David Watzke <david@watzke.cz>
pkgname=pion-net
pkgver=4.0.12
pkgrel=1
pkgdesc="A C++ development library for implementing lightweight HTTP interfaces"
url="http://www.pion.org/projects/pion-network-library"
arch=('i686' 'x86_64')
license=('custom:Boost')
depends=('boost-libs' 'openssl' 'log4cpp')
makedepends=('boost')
source=("http://www.pion.org/files/$pkgname-$pkgver.tar.bz2" \
        boost_1.50_detection.diff)
md5sums=('baf0b6a006660dc4051c824e4660cb9d'
         '2ed396b15ff77dcc5c82ba5d8fea4726')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # override configure bug (forces debug even when nobody asked for it)
  sed -i "s/ -ggdb//g" configure
  # override Makefile bug
  sed -i "/docs:/ s/doxygen-doc//" Makefile.in
  # fix boost::thread detection with boost 1.50
  patch -Np1 < "$srcdir"/boost_1.50_detection.diff || true

  ./configure --prefix=/usr --disable-doxygen-doc

  make
}

check() {
  cd "$srcdir/$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  # install license
  install -D -m0755 COPYING "$pkgdir/usr/share/licenses/$pkgname/Boost"
}
