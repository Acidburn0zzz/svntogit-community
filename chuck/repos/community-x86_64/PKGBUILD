# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=chuck
pkgver=1.3.1.0
pkgrel=1
pkgdesc="Concurrent, on-the-fly audio programming language"
arch=('x86_64' 'i686')
url="http://chuck.cs.princeton.edu/"
license=('GPL')
depends=('gcc-libs' 'libsndfile')
# base-devel
#makedepends=('bison' 'flex') 
source=("http://chuck.cs.princeton.edu/release/files/$pkgname-$pkgver.tgz")
sha256sums=('43af7e06f06932cd3afde9facb17c83530e0fa45826b1f3ea04496f10f009d0a')

build() {
  cd "$srcdir/$pkgname-$pkgver/src"

  msg2 "Configuring..."
	CFLAGS+=' -fno-strict-aliasing -lpthread'
	CXXFLAGS+=' -fno-strict-aliasing -fpermissive -lpthread'

  msg2 "Compiling..."
  # Alternatives to linux-alsa are: linux-jack linux-oss osx win32
  make linux-alsa
}

package() {
  cd "$srcdir/$pkgname-$pkgver/src"

  install -Dm755 chuck $pkgdir/usr/bin/chuck
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"
  cp -r ../examples "$pkgdir/usr/share/doc/$pkgname/"
}

# vim:set ts=2 sw=2 et:
