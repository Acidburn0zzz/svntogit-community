# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: Jose Valecillos <valecillosjg@gmail.com>
# Contributor: Kaos <gianlucaatlas@gmail.com>
# Contributor: Christopher Rogers <slaxemulator@gmail.com>

pkgname=wordpress
pkgver=4.8.3
pkgrel=1
pkgdesc='Blog tool and publishing platform'
arch=('any')
url='https://wordpress.org/'
license=('GPL')
depends=('php')
optdepends=('nginx: Web server for serving Wordpress'
            'apache: Web server for serving Wordpress'
            'mariadb: Database server')
options=('emptydirs')
install="$pkgname.install"
source=("https://wordpress.org/$pkgname-$pkgver.tar.gz"
        'password_reset_exploit.patch')
sha256sums=('166fee52dff183b5bb08a5256aca3c5341a0eeb769fb171ff08023d4f50e2326'
            '68244b5bd534b21c88dc25074bf1fbc7a26d601cee69d78a467b1287f9eb2eed')

prepare() {
  cd "$pkgname"

  patch -p1 -i "$srcdir/password_reset_exploit.patch"
}

package() {
  install -d "$pkgdir/usr/share/webapps"
  cp -r "$pkgname" "$pkgdir/usr/share/webapps"
}

# getver: wordpress.org
# vim: ts=2 sw=2 et:
