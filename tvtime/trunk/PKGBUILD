# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:thefatprecious <marty@loomar.com>

pkgname=tvtime
pkgver=1.0.10
pkgrel=2
pkgdesc="A high quality television application for use with video capture cards"
arch=('i686' 'x86_64')
url='http://linuxtv.org/'
license=('GPL' 'LGPL')
depends=('gcc-libs' 'libpng' 'libxml2' 'freetype2' 'libxtst' 'libxinerama' 'libxv'
	 'libsm' 'libxxf86vm' 'alsa-lib')
makedepends=('libxt' 'v4l-utils')
source=("https://linuxtv.org/downloads/tvtime/tvtime-$pkgver.tar.gz"
	"FS#48350.patch::https://git.linuxtv.org/tvtime.git/patch/?id=73aaa27fd8a8e6481985211b819d1b7d0662c0cb")
sha256sums=('c9170c08045e54fc4e5dba2c1977ea8ca59739fbf8e6ac79d9125981a26d66d6'
            '4ef53221bd7e7c1e49cfde146376f46eeaf7eedf7e570b47471a91922e7ffa62')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  echo "StartupWMClass=tvtime" >>docs/tvtime.desktop
  patch -p1 <"$srcdir"/"FS#48350.patch"
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --mandir=/usr/share/man --localstatedir=/var --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -D -m644 docs/html/default.tvtime.xml "$pkgdir"/usr/share/tvtime/default.tvtime.xml
}
