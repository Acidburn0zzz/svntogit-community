# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>
# Contributor: Federico Quagliata (quaqo) <quaqo@despammed.com>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=twinkle
pkgver=1.4.2
pkgrel=14
pkgdesc="Softphone for voice over IP and IM communication using SIP"
arch=('x86_64' 'i686')
url="http://www.twinklephone.com/"
license=('GPL')
depends=('file' 'speex' 'boost-libs' 'libsndfile' 'qt3' 'libzrtpcpp')
makedepends=('pkg-config' 'boost')
source=("http://www.xs4all.nl/~mfnboer/$pkgname/download/$pkgname-$pkgver.tar.gz"
        "twinkle.desktop")
sha256sums=('807686c7ac1b54bb0ea27ff4be362db6926d1d3d6f1802f6d359a353157e89a5'
            '88578b314f434d88ef387ee8bf541dcdd4dfeffba161c1c66e43ac54103b3862')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  . /etc/profile.d/qt3.sh
  ./configure --prefix=/usr \
	--without-kde \
	--with-speex \
	--without-ilbc \
	--mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 "$pkgdir/usr/share/twinkle/twinkle48.png" \
    "$pkgdir/usr/share/pixmaps/twinkle.png"
  install -Dm644 twinkle.desktop \
    "$pkgdir/usr/share/applications/twinkle.desktop"
}

# vim:set ts=2 sw=2 et:
