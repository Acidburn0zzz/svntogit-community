# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Christopher Arndt <aur at chrisarndt dot de>

pkgname=python-mando
pkgver=0.6.4
pkgrel=1
arch=('any')
url="https://github.com/rubik/mando"
license=('MIT')
pkgdesc="Python library which wraps the argparse module to help write flexible CLI applications"
depends=('python' 'python-six')
checkdepends=('python-pytest')
makedepends=('python-setuptools')
source=(https://github.com/rubik/mando/archive/v${pkgver}.tar.gz)
sha512sums=('457fa0db4e8e5dbfac6146d254181213814ef1975c919b4573007face27749ec3a50eda850558ebdc0fdff94a5e9819fd825b10cb3023078b2b426c2825fdbf0')

build() {
  cd "mando-${pkgver}"
  python setup.py build
}

check() {
  cd "mando-${pkgver}/mando"
  pytest tests
}

package() {
  cd "mando-${pkgver}"
  python setup.py install --root="$pkgdir" --skip-build --optimize=1

  # license
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
