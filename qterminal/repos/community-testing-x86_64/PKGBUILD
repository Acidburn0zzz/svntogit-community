# Maintainer: Jerome Leclanche <jerome@leclan.ch>
# Co-Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

pkgname=qterminal
pkgver=0.16.0
pkgrel=2
pkgdesc="A lightweight Qt-based terminal emulator"
arch=("x86_64")
groups=("lxqt")
url="https://github.com/lxqt/$pkgname"
license=("GPL2")
depends=("hicolor-icon-theme" "qtermwidget" "qt5-x11extras")
makedepends=("cmake" "lxqt-build-tools")
source=(
  "https://github.com/lxqt/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
  "fix-preferences.diff"
)
sha256sums=('871d7b3ed8d6e815b6feccf6a603781c1794ac6bc20c6a52f067ee7fa192ed24'
            'SKIP'
            'a3f5a2e8361434a60087bd358e90a9b71eb15f2f3f764eda8900c8b6e2857f05')
validpgpkeys=(
  "169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
  "7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
  "19DFDF3A579BD509DBB572D8BE793007AD22DF7E"  # Pedram Pourang <tsujan2000@gmail.com>
)

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../fix-preferences.diff
}

build() {
  mkdir -p build
  cd build

  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
