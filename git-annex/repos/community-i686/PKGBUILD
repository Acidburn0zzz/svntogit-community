# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=git-annex
pkgver=6.20160114
pkgrel=1
pkgdesc="Manage files with git, without checking their contents into git"
url="http://git-annex.branchable.com/"
license=("GPL3")
arch=('i686' 'x86_64')
makedepends=("ghc=7.10.3" "haskell-aeson" "haskell-async" "haskell-aws" "haskell-bloomfilter"
             "haskell-case-insensitive" "haskell-concurrent-output" "haskell-conduit"
             "haskell-conduit-extra" "haskell-cryptonite" "haskell-data-default" "haskell-dav"
             "haskell-dbus" "haskell-dlist" "haskell-dns" "haskell-edit-distance"
             "haskell-esqueleto" "haskell-exceptions" "haskell-fdo-notify" "haskell-feed"
             "haskell-gnutls" "haskell-hinotify" "haskell-hslogger" "haskell-http-client"
             "haskell-http-conduit" "haskell-http-types" "haskell-ifelse" "haskell-json"
             "haskell-missingh" "haskell-monad-control" "haskell-monad-logger" "haskell-mtl"
             "haskell-network" "haskell-network-info" "haskell-network-multicast"
             "haskell-network-protocol-xmpp" "haskell-network-uri" "haskell-old-locale"
             "haskell-optparse-applicative" "haskell-persistent" "haskell-persistent-sqlite"
             "haskell-persistent-template" "haskell-quickcheck" "haskell-random"
             "haskell-regex-tdfa" "haskell-resourcet" "haskell-safesemaphore" "haskell-sandi"
             "haskell-stm" "haskell-text" "haskell-torrent" "haskell-unix-compat"
             "haskell-utf8-string" "haskell-uuid" "haskell-xml-types")
depends=('git' 'rsync' 'libxml2')
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('671d165f9ea583b9a86060b60741c6aa54dbfde0673a2b278d82d761b7500181')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    
    runhaskell Setup configure -O --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        -fcryptonite -fnetwork-uri -f-ekg -fconcurrentoutput -ftorrentparser \
        -ftahoe -fquvi -ffeed -ftdfa -f-testsuite -f-androidsplice \
        -f-android -fproduction -fdns -fpairing -f-webapp-secure -f-webapp \
        -fassistant -fwebdav -fs3 -f-benchmark
    runhaskell Setup build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    runhaskell Setup copy --destdir="${pkgdir}"

    install -Dm644 bash-completion.bash "${pkgdir}/usr/share/bash-completion/completions/git-annex"

    rm "$pkgdir/usr/share/doc/git-annex/COPYRIGHT"
    rmdir "$pkgdir/usr/share/doc/git-annex" "$pkgdir/usr/share/doc"
}
