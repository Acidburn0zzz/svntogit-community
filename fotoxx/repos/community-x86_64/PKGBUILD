# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=fotoxx  
pkgver=9.8.1
pkgrel=2
pkgdesc="A program for improving image files made with a digital camera"
url="http://kornelix.squarespace.com/fotoxx"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('gtk2' 'gimp-ufraw' 'perl-exiftool')
optdepends=('printoxx: for printing from within fotoxx' 'brasero: for burning')
source=("http://kornelix.squarespace.com/storage/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('772a29722947cd3034864878ff168809')
replaces=('fotox')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's#kornelix-##' Makefile || return 1
  sed -i 's+xdg-+#xdg-+' Makefile || return 1
  sed -i 's+/usr/local+/usr+g' desktop || return 1
  PREFIX=/usr make || return 1
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -d $pkgdir/usr/share/{$pkgname,applications,man/man1} || return 1
  make PREFIX=$pkgdir/usr menu || return 1
  make DESTDIR=$pkgdir PREFIX=/usr install || return 1
  install -Dm644 $pkgdir/usr/share/$pkgname/$pkgname.desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop || return 1
  install -Dm644 $pkgdir/usr/share/doc/$pkgname/$pkgname.man \
    $pkgdir/usr/share/man/man1/$pkgname.1 || return 1
  rm $pkgdir/usr/share/doc/$pkgname/$pkgname.man || return 1
}
