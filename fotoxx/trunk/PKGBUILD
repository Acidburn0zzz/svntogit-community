# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=('fotoxx' 'fotoxx-doc')
pkgbase=fotoxx
pkgver=11.04
pkgrel=1
pkgdesc="A program for improving image files made with a digital camera"
url="http://kornelix.squarespace.com/fotoxx"
arch=('i686' 'x86_64')
license=('GPL3')
depends=('gtk2' 'gimp-ufraw' 'perl-exiftool' 'xdg-utils')
optdepends=('mashup: for printing from within fotoxx' 'brasero: for burning')
source=("http://kornelix.squarespace.com/storage/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('535f78b5000ee117b2c91c1bee94145e')
replaces=('fotox')

build() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr \
    CFLAGS+="-c `pkg-config --cflags gtk+-2.0`" \
    LFLAGS+="`pkg-config --libs gtk+-2.0 gthread-2.0`" 
  sed -i 's#/local##' desktop
  sed -i 's+^# ++' desktop
}

package_fotoxx() {
  options=('!docs')
  cd $srcdir/$pkgname-$pkgver
  install -Dm644 desktop \
    $pkgdir/usr/share/applications/$pkgname.desktop 
  make DESTDIR=$pkgdir PREFIX=/usr install
  rm -rf $pkgdir/usr/share/{doc,man}
}

package_fotoxx-doc() {
  pkgdesc="fotoxx: documentation"
  depends=()
  optdepends=()
  options=('docs')
  arch=('any')
  cd $srcdir/${pkgname%-doc}-$pkgver
  make DESTDIR=$pkgdir PREFIX=/usr install
  install -Dm644 $pkgdir/usr/share/doc/${pkgname%-doc}/${pkgname%-doc}.man \
    $pkgdir/usr/share/man/man1/${pkgname%-doc}.1
  rm $pkgdir/usr/share/doc/${pkgname%-doc}/${pkgname%-doc}.man
  rm -rf $pkgdir/usr/bin
  rm -rf $pkgdir/usr/share/{applications,fotoxx}
}
