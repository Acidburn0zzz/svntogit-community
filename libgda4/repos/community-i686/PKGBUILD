# $Id$
# Maintainer: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libgda4
pkgver=4.2.9
pkgrel=1
pkgdesc="Data abstraction layer with mysql, pgsql, xml, sqlite providers"
arch=(i686 x86_64)
license=('GPL')
depends=('gtksourceview2' 'libunique' 'libxslt' 'db' 'ncurses' 'libsoup'
         'libmysqlclient' 'postgresql-libs' 'python2' 'libgnome-keyring'
         'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('intltool' 'gobject-introspection' )
options=('!libtool')
url="http://www.gnome-db.org"
install=libgda.install
source=(http://ftp.gnome.org/pub/GNOME/sources/libgda/${pkgver%.*}/libgda-${pkgver}.tar.xz)
sha256sums=('bbae913734b0bf0149b36d1b8617e343f08611c10219fa5ba24b84dfa782e814')

build() {
  cd "${srcdir}/libgda-${pkgver}"
  sed -i '1s/python$/&2/' libgda-report/RML/trml*/trml*.py
  ./configure --prefix=/usr --sysconfdir=/etc \
      --with-bdb=/usr --with-bdb-libdir-name=lib \
      --disable-static \
      --disable-gtk-doc 
  make
}

package() {
  cd "${srcdir}/libgda-${pkgver}"
  make DESTDIR="${pkgdir}" install
  rm -rf $pkgdir/usr/share/icons
  rm -rf $pkgdir/usr/share/gtk-doc
}
