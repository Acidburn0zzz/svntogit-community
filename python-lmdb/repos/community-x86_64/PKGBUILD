# Maintainer: Daniel Bermond <dbermond@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=py-lmdb
pkgname=python-lmdb
pkgver=1.1.1
pkgrel=1
pkgdesc="Universal Python binding for the LMDB Lightning Database"
arch=(x86_64)
url="https://github.com/jnwatson/py-lmdb/"
license=('custom: OpenLDAP')
depends=(python python-cffi lmdb)
makedepends=(python-setuptools)
checkdepends=(python-pytest)
source=(${url}/archive/${_pkg}_${pkgver}.tar.gz)
sha256sums=('ebe57cf3f8080beb72616f724a7b1b94e359fc8c237929330faa6bd790870c01')

build() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py build
}

check() {
  cd ${_pkg}-${_pkg}_${pkgver}
  local _pyver
  _pyver="$(python -c 'import sys; print("%s.%s" %sys.version_info[0:2])')"
  export LMDB_FORCE_SYSTEM=1
  export PYTHONPATH="$(pwd)/build/lib.linux-${CARCH}-${_pyver}"
  # https://github.com/jnwatson/py-lmdb/issues/278
  pytest --deselect tests/cursor_test.py::CursorTest2::testIterWithDeletes
}

package() {
  cd ${_pkg}-${_pkg}_${pkgver}
  LMDB_FORCE_SYSTEM=1 python setup.py install --root="$pkgdir" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
