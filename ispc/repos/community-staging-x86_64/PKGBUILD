# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ispc
pkgver=1.12.0
pkgrel=2
pkgdesc="A compiler for high-performance SIMD programming on the CPU"
arch=(x86_64)
url="https://ispc.github.io/"
license=(BSD)
depends=(ncurses zlib llvm-libs clang)
makedepends=(llvm python lib32-glibc cmake)
source=(https://github.com/ispc/ispc/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        ispc-no-old-llvm.patch # From https://github.com/ispc/pull/1563 & 1597 & 1602
        ispc-llvm10.patch # From https://github.com/ispc/ispc/pull/1635
        ispc-libclang-cpp.patch)
sha256sums=('9ebc29adcdf477659b45155d0f91e61120a12084e42113d0e9f4ce5cfdfbdcab'
            '94ea7da2d809c08f6d6f291b6e7a708d7442e1cb00d8f4c10c5db5985e186fb2'
            '6dafdaee197f2f7fc3e543b6a4b62d0207d63354defc5db98a79e4a4dc89c582'
            '828337a46aa330e609c4d6ae4d3364417c977dea1ec3a18d1e7ae02694f43fc5')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 -i ../ispc-no-old-llvm.patch # Necessary to apply the next one
  patch -p1 -i ../ispc-llvm10.patch # Actual fixes for LLVM 10
  patch -p1 -i ../ispc-libclang-cpp.patch # Build with unified libclang-cpp.so
}

build() {
  cmake -B build -S ${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DISPC_INCLUDE_EXAMPLES=OFF \
    -DISPC_NO_DUMPS=ON 
  make -C build
}

check() {
  make -C build check-all
}

package() {
  make -C build DESTDIR="${pkgdir}" install
  install -Dm644 ${pkgname}-${pkgver}/LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
