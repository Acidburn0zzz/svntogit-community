# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: yk <yk at archlinuxcn dot org>

pkgname=shadowsocks-qt5
pkgver=2.8.0
pkgrel=4
pkgdesc="A fast, reliable and cross-platform GUI fronted for Shadowsocks."
arch=("i686" "x86_64")
url="http://github.com/shadowsocks/shadowsocks-qt5"
license=("GPL")
depends=('qrencode' 'libqtshadowsocks' 'zbar')
makedepends=('git')
source=("$pkgname-$pkgver.tar.gz::http://github.com/shadowsocks/shadowsocks-qt5/archive/v$pkgver.tar.gz"
        "disable-appindicator.patch")
sha256sums=('dd983eb7de8a79346bd30a2b043baeaf24b1c54501ed6c7d4608dd54d2abb910'
            'd4dc3c22a46e57b800400c8a5eaf10dfaac0bc17440806f2b572cae3998a9080')

prepare() {
  cd shadowsocks-qt5-$pkgver

  # Disable appindicator (was needed for Qt < 5.4.2)
  patch -Np1 -i ../disable-appindicator.patch
}

build() {
  cd shadowsocks-qt5-$pkgver
  qmake INSTALL_PREFIX=/usr
  make
}

package() {
  cd shadowsocks-qt5-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
