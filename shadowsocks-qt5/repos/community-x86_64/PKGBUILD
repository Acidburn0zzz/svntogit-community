# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: yk <yk at archlinuxcn dot org>

pkgname=shadowsocks-qt5
pkgver=2.9.0
pkgrel=1
pkgdesc="A fast, reliable and cross-platform GUI fronted for Shadowsocks."
arch=("i686" "x86_64")
url="http://github.com/shadowsocks/shadowsocks-qt5"
license=("GPL")
depends=('qrencode' 'libqtshadowsocks' 'zbar')
makedepends=('git')
source=("$pkgname-$pkgver.tar.gz::http://github.com/shadowsocks/shadowsocks-qt5/archive/v$pkgver.tar.gz"
        "disable-appindicator.patch")
sha512sums=('2fb9662844fb9e6ff4806999059516f75477ac66802452d528979d501a4aafdb2df36789b8a97c7bc013d7021b49aec9eb3a99b0115ae812b2d2d9bdb3a4d295'
            '7869dec8cc0ebc637ac75e65fbeeaa8af513877d1c8a8e2b4f6390859e8aa15cc82bbb7e2e83b4b1b29d439893c0fb428f83c897ad16bcf2e51f047e3927136f')

prepare() {
  cd shadowsocks-qt5-$pkgver

  # Disable appindicator (was needed for Qt < 5.4.2)
  patch -Np1 -i ../disable-appindicator.patch
}

build() {
  cd shadowsocks-qt5-$pkgver
  qmake INSTALL_PREFIX=/usr
  make
}

package() {
  cd shadowsocks-qt5-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
