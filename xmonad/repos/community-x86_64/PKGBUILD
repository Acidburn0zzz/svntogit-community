# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.9.1
pkgrel=1
pkgdesc="A lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=6.12.1' 'gmp' 'libxinerama' 'haskell-x11=1.5.0.0' 'sh' 'haskell-mtl')
install='xmonad.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz xmonad.desktop xmonad.svg)

build() {
  cd $srcdir/$pkgname-$pkgver

  runhaskell Setup.lhs configure --ghc --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid || return 1

  runhaskell Setup.lhs build || return 1

  runhaskell Setup.lhs register --gen-script
  runhaskell Setup.lhs unregister --gen-script
  
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -d -m755 $pkgdir/usr/share/xmonad
  install -D LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad.desktop $pkgdir/etc/X11/sessions/xmonad.desktop
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
}
md5sums=('3d0dd5cf77768b10e1b7f3a47e32b09e'
         'f61961459f190b6d948f1f2782afa7a7'
         '72bfa5e62e4e44fe7fa59b6a7593d993')
