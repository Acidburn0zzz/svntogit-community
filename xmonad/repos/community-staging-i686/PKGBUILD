# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: shild <shildv@gmail.com>

pkgname=xmonad
pkgver=0.9.1
pkgrel=10
pkgdesc="A lightweight X11 tiled window manager written in Haskell"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.0.1' 'gmp' 'libxinerama' 'haskell-x11=1.5.0.0' 'sh' 'haskell-mtl=2.0.1.0' 'haskell-utf8-string=0.3.6')
install='xmonad.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        xmonad.desktop xmonad.svg
        ghc-6.12-warnings.patch)
md5sums=('3d0dd5cf77768b10e1b7f3a47e32b09e'
         'f61961459f190b6d948f1f2782afa7a7'
         '72bfa5e62e4e44fe7fa59b6a7593d993'
         '3ae1147228385a87d1fd9f636f9e603e')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Patch from darcs to suppress warnings (and build with GHC7)
  patch -p1 -i $srcdir/ghc-6.12-warnings.patch
  runhaskell Setup.lhs configure --ghc --prefix=/usr \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
#  runhaskell util/GenerateManpage.hs

  install -D -m644 man/xmonad.1 $pkgdir/usr/share/man/man1/xmonad.1

  install -D -m644 LICENSE $pkgdir/usr/share/licenses/xmonad/LICENSE

  install -D -m644 $srcdir/xmonad.desktop $pkgdir/usr/share/applications/xmonad.desktop
  mkdir -p $pkgdir/etc/X11/sessions
  ln -s /usr/share/applications/xmonad.desktop $pkgdir/etc/X11/sessions/xmonad.desktop
  install -D -m644 $srcdir/xmonad.svg $pkgdir/usr/share/pixmaps/xmonad.svg
}
