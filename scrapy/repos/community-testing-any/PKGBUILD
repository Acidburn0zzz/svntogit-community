# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: PyroDevil <p dot devil at gmail dot com>
# Contributor: Anibal Pacheco <apacheco.uy@gmail.com>

pkgname=scrapy
pkgver=1.5.1
pkgrel=1
pkgdesc="A fast high-level scraping and web crawling framework."
arch=('any')
license=('BSD')
url="http://scrapy.org"
depends=('python-twisted' 'libxml2' 'python-w3lib' 'python-lxml' 'python-six' 'python-queuelib'
         'python-setuptools' 'python-cssselect' 'python-pyopenssl' 'python-parsel'
         'python-pydispatcher' 'python-service-identity')
checkdepends=('python-pytest-runner' 'python-pillow' 'python-testfixtures' 'python-botocore'
              'python-jmespath' 'mitmproxy')
optdepends=('ipython: for enhanced support of the interactive scraping shell')
source=("$pkgname-$pkgver.tar.gz::https://github.com/scrapy/scrapy/archive/$pkgver.tar.gz"
        "https://github.com/scrapy/scrapy/commit/b364d27247b2d9b86c164569c7e0459fa3f8391b.patch"
        "https://github.com/scrapy/scrapy/commit/f4f39057cbbfa4daf66f82061e57101b88d88d05.patch")
sha512sums=('3811d10e901ec049bb7ea4d4c477e5115fc8a1090c7cb5888fddeee51a56f60679ccb7e85da5a4d9ce7320f8cc67eefe3eaa335e27d9c696e6a875a5058121bb'
            'd710e737d21ac52acb5de4b85ec757b13e492635c0c98249b3b5463b7a4471cabe434f9cd4a809db6e365019c1a71cc1bd555057a0545460c5bcbe147afe552a'
            '6300a2b790b4d377877d6883644627d7ff94dbd38f04cc1561e87bba2ba6ad474c41153dc0b8b55768ac38ae4cf72af942fcdb465e31fe90741471adb947d1d0')

prepare() {
  cd scrapy-$pkgver
  # fix flaky tests: https://github.com/scrapy/scrapy/pull/3210
  patch -p1 -i ../b364d27247b2d9b86c164569c7e0459fa3f8391b.patch
  # fix python37 issues: https://github.com/scrapy/scrapy/pull/3326
  patch -p1 -i ../f4f39057cbbfa4daf66f82061e57101b88d88d05.patch
}

build() {
  cd scrapy-$pkgver
  python setup.py build
}

check() {
  cd scrapy-$pkgver
  python setup.py pytest --addopts tests
}

package() {
  cd scrapy-$pkgver
  python setup.py install -O1 --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -D -m644 README.rst "$pkgdir"/usr/share/doc/$pkgname/README.rst
  install -D -m644 docs/intro/install.rst "$pkgdir"/usr/share/doc/$pkgname/INSTALL.rst
}
