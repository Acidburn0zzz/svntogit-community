# $Id$
# Maintainer: Evangelos Foutras <foutrelis@gmail.com>

pkgname=frostwire
pkgver=4.21.8
pkgrel=1
pkgdesc="P2P Gnutella/Bittorrent client"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
depends=('java-runtime' 'hicolor-icon-theme')
install=$pkgname.install
source=("http://downloads.sourceforge.net/project/$pkgname/FrostWire 4.21/$pkgver/$pkgname-$pkgver.noarch.tar.gz"
        $pkgname
        icons.tar.gz
        java-version-detection.patch)
md5sums=('f462828dd4be6cfab873a4713c4d9a67'
         'aec856d2e83205b7f17c1da7a3f78763'
         'dbea03ff4e35cc6c190976282682c2cf'
         'b9ffc80668dcc5e085be4476908cd3b9')

build() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -Np0 -i "$srcdir/java-version-detection.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}

# vim:set ts=2 sw=2 et:
