# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=frostwire
pkgver=5.3.5
pkgrel=1
pkgdesc="BitTorrent client with integrated search"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
depends=('java-runtime' 'hicolor-icon-theme' 'desktop-file-utils')
optdepends=('mplayer: audio playback'
            'vlc: video playback')
install=$pkgname.install
source=(http://dl.frostwire.com/frostwire/$pkgver/frostwire-$pkgver.noarch.tar.gz
        $pkgname
        icons.tar.gz
        java-version-detection.patch)
sha256sums=('650906a4f289e67f5b77127b4c48a29452fdeae4bebb351cde9df1771e964c5f'
            '815b2dc4cbaa4836df49254734fbf7c9d50f869fcde7c462b0fc81baa71e4620'
            '00e425501b33b7213a337314a5035d52459f743f623f9f37abbc60d6e56a2186'
            '50828981c20273c86ef7c05f801d0fcec28253a544f95e231db35402c738a62e')

build() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -Np0 -i "$srcdir/java-version-detection.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}

# vim:set ts=2 sw=2 et:
