# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>

pkgname=frostwire
pkgver=5.5.0
pkgrel=1
pkgdesc="BitTorrent client with integrated search"
arch=('any')
url="http://www.frostwire.com"
license=('GPL')
# java-runtime=6 due to: http://forum.frostwire.com/viewtopic.php?f=1&t=15896
depends=('java-runtime=6' 'mplayer' 'hicolor-icon-theme' 'desktop-file-utils'
         'gtk-update-icon-cache')
install=$pkgname.install
source=(http://downloads.sourceforge.net/project/$pkgname/FrostWire%205.x/$pkgver/$pkgname-$pkgver.noarch.tar.gz
        $pkgname
        icons.tar.gz
        java-version-detection.patch)
sha256sums=('3b9ae87cd3dcb1315778c1613389bb2eb4087e8cac2c5de762f7d162cd6c6bbc'
            '815b2dc4cbaa4836df49254734fbf7c9d50f869fcde7c462b0fc81baa71e4620'
            '00e425501b33b7213a337314a5035d52459f743f623f9f37abbc60d6e56a2186'
            '50828981c20273c86ef7c05f801d0fcec28253a544f95e231db35402c738a62e')

build() {
  cd "$srcdir/$pkgname-$pkgver.noarch"

  # FS#15544 (Java version detection fails when _JAVA_OPTIONS is set)
  patch -Np0 -i "$srcdir/java-version-detection.patch"

  install -d "$pkgdir/usr/share/$pkgname"
  cp -r * "$pkgdir/usr/share/$pkgname"
  cp -r "$srcdir/icons" "$pkgdir/usr/share"

  install -D "$srcdir/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 $pkgname.desktop "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$srcdir/icons/hicolor/16x16/apps/$pkgname.png" \
                 "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # Remove uneeded files
  rm -rf "$pkgdir"/usr/share/frostwire/{runFrostwireOSX.sh,unpack200.py,root/{.svn,magnet10/.svn}}
}

# vim:set ts=2 sw=2 et:
