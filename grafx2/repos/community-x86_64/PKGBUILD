# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Simon Parzer <simon.parzer@gmail.com>

pkgname=grafx2
pkgver=2.7
pkgrel=1
# TODO: place recoil (retro computer image library) in a separate package
_recoil=5.0.0
pkgdesc='Pixelart-oriented painting program'
arch=(x86_64)
url='https://gitlab.com/GrafX2/grafx2'
license=(GPL2)
depends=(fontconfig lua sdl2_image sdl2_ttf)
makedepends=(git)
options=(!emptydirs)
# tag: v2.7
source=("git+https://gitlab.com/GrafX2/grafX2.git#commit=c6460cf93999b2e6648ffd9cf9a7ef169b15104d"
        "https://sourceforge.net/projects/recoil/files/recoil/$_recoil/recoil-$_recoil.tar.gz")
sha256sums=('SKIP'
            'caa60b380c7e0a762d62459bc9ca31d16b22b0f85c972e6315e3744ae1f2ffb5')

prepare() {
  mv grafX2 $pkgname
  sed 's/-liconv//g' -i "$pkgname/src/Makefile"
  mkdir -p grafx2/3rdparty/archives
  cp -uv recoil-$_recoil.tar.gz grafx2/3rdparty/archives/recoil-$_recoil.tar.gz
}

build() {
  CFLAGS="$CFLAGS -w" make -C $pkgname/src PREFIX=/usr API=sdl2
}

package() {
  install -d "$pkgdir/usr/share/grafx2/scripts/samples_2.4/picture/"{thomson/lib,others-8bit/lib}
  CFLAGS="$CFLAGS -w" make -C $pkgname/src PREFIX=/usr DESTDIR="$pkgdir" API=sdl2 install
  mv "$pkgdir/usr/bin/grafx2-sdl2" "$pkgdir/usr/bin/grafx2"
  install -Dm644 "$pkgname/doc/README.txt" \
    "$pkgdir/usr/share/doc/$pkgname/README"
}

# getver: gitlab.com/GrafX2/grafX2/-/tags
# vim: ts=2 sw=2 et:
