# $Id$
# Maintainer:
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: William Rea <sillywilly@gmail.com>

pkgbase=python-reportlab
pkgname=(python2-reportlab python-reportlab)
_origname=reportlab
pkgver=3.5.1
pkgrel=2
pkgdesc="A proven industry-strength PDF generating solution"
arch=('x86_64')
url="http://www.reportlab.org/rl_toolkit.html"
depends=('freetype2')
conflicts=('python-reportlab<=2.5-2')
makedepends+=('python' 'python2' 'freetype2' 'python2-pillow' 'python-pillow' 'mercurial')
replaces=('python-reportlab<=2.5-2')
license=('CUSTOM')
source=(hg+https://bitbucket.org/rptlab/reportlab#tag=ReportLab_3_5_0)
md5sums=('SKIP')

build() {
    cd $srcdir
    cp -rf ${_origname} "${_origname}2"
}

package_python2-reportlab() {
    depends+=('python2' 'python2-pip' 'python2-pillow')

    cd "$srcdir/${_origname}2"
    python2 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}

package_python-reportlab() {
    depends+=('python3' 'python-pip' 'python-pillow')

    cd "$srcdir/${_origname}"
    python3 setup.py install --root=$pkgdir --optimize=1
    install -D -m644 LICENSE.txt $pkgdir/usr/share/licenses/$pkgname/license.txt
}

check() {
    cd ${srcdir}/${_origname}
    python2 setup.py tests
    python setup.py tests
}
