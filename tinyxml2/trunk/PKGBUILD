# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: dhead666 <myfoolishgames@gmail.com>

pkgname=tinyxml2
pkgver=7.0.0
pkgrel=1
pkgdesc='Simple XML parser made for easy integration'
arch=('x86_64')
url='http://www.grinninglizard.com/tinyxml2'
license=('zlib')
makedepends=('cmake' 'ninja')
source=($pkgname-$pkgver.tar.gz::https://github.com/leethomason/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('fa0d1c745d65d4d833e62cb183e23c2034dc7a35ec1a4977e808bdebb9b4fe60')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

  grep License -A20 readme.md > LICENSE
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  install -d build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -G Ninja
  ninja
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  DESTDIR="${pkgdir}" ninja -C build install
  cat LICENSE
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}

# getver: github.com/leethomason/tinyxml2
# vim: ts=2 sw=2 et:
