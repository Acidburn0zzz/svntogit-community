# Maintainer: David Runge <dave@sleepmap.de>
pkgname=freewheeling
pkgver=0.6.5
pkgrel=2
pkgdesc="A live looper"
arch=('x86_64')
url="https://github.com/free-wheeling/freewheeling"
license=('GPL2')
groups=('pro-audio')
depends=('fluidsynth' 'liblo' 'libxml2' 'nettle' 'sdl_gfx' 'sdl_ttf')
makedepends=('gendesk')
source=("$pkgname-$pkgver.tar.gz::https://github.com/free-wheeling/${pkgname}/archive/v${pkgver}.tar.gz"
        "$pkgname.jpg::https://raw.githubusercontent.com/wiki/free-wheeling/${pkgname}/${pkgname}-logo-1.jpg")
sha512sums=('c7ab563ac68b6466fa7da25b1110835ab3729058351bfabd0e99be68fbf2d08faa08adc37cf66b8a910d3a15deb64ef1994278e959319ded2dfbcc7c17425356'
            '9f8386bff7b34c8ca44d0ad5b87d4e74a0b901a64e50bc5d8d99dd1f1ad10eb5b6b29f7b6fc8f106c425b2de0285517af3158472654abb2c914d48645dab0913')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name Freewheeling \
          --exec fweelin \
          --categories "Audio;Sequencer"
}

build() {
  cd "$pkgname-$pkgver"
  # fix broken SDL/SDL.h include
#  CPPFLAGS="-I/usr/include/SDL" \
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  # XDG desktop integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "../${pkgname}.jpg" -t "${pkgdir}/usr/share/pixmaps/"
  #docs
  install -vDm 644 {AUTHORS,ChangeLog,NEWS,README.md,THANKS,TUNING} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  #examples
  install -vDm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}
