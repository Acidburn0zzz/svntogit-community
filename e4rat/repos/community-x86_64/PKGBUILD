# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=e4rat
pkgver=0.2.2
pkgrel=1
pkgdesc="Toolset to accelerate the boot process and application startups for systems with ext4 root partitions."
arch=('i686' 'x86_64')
url="http://e4rat.sourceforge.net/"
license=('GPL')
depends=('e2fsprogs' 'util-linux')
makedepends=('audit' 'boost' 'cmake')
options=(!strip)
backup=(etc/e4rat.conf)
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgver/e4rat_${pkgver}_src.tar.gz e4rat-static_core.patch)
md5sums=('dada998e857794dc7f76ffc887e04c19'
         '9432ac4a8658812828d90b2676e8519f')
sha256sums=('ec40740ac05fb0470edd682232fb6c7be0bb57420b70f6c39d1df234753f3ec9'
            '9381d3cee3c24de8b1df9a4c4d626a2d9d20bad695133e3dcecc9f0a8277880a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -p0 -i "$srcdir/e4rat-static_core.patch"

  unset LDFLAGS
  sed -i '/stdlib.h/a #include <unistd.h>' src/fiemap.cc
  cmake -DCMAKE_BUILD_TYPE=Release .
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir/var/lib/$pkgname"
}
