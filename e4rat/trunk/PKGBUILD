# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=e4rat
pkgver=0.2.1
pkgrel=3
pkgdesc="Toolset to accelerate the boot process and application startups.  Root partition must be ext4 ONLY!"
arch=('i686' 'x86_64')
url="http://e4rat.sourceforge.net/"
license=('GPL')
depends=('e2fsprogs' 'util-linux')
makedepends=('audit' 'boost' 'cmake' 'e2fsprogs' 'perl' 'util-linux')
options=(!strip)
install=("$pkgname.install")
source=(http://downloads.sourceforge.net/project/$pkgname/$pkgver/e4rat_"$pkgver"_src.tar.gz)

build() {
	  unset LDFLAGS
	  cd "$srcdir/$pkgname-$pkgver"
	  cmake -DCMAKE_BUILD_TYPE=Release .
	  make
}

package() {
	  cd "$srcdir/$pkgname-$pkgver"
	  make DESTDIR="$pkgdir" install
	  install -dm755 "$pkgdir"/var/lib/$pkgname
}

md5sums=('cbb1f3cdd2b8c96b4402028da07b11f2')
sha256sums=('c509f165eff317f80f5d13d7b370514c457df4542ff3b31bee4c26f97ce89769')
