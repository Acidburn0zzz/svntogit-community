# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>

pkgname=qoauth
pkgver=1.0.1
pkgrel=7
pkgdesc="Support with OAuth-powered network services"
url='http://github.com/ayoy/qoauth'
license=('GPL')
arch=('i686' 'x86_64')
depends=('qca-qt4')
makedepends=('cmake' 'automoc4')
source=($pkgname-$pkgver::"https://github.com/ayoy/qoauth/archive/v$pkgver.tar.gz")
md5sums=('255121fc0070a1fbed83703e39111ce4')

prepare() {
  cd ${pkgname}-${pkgver}

  # Install libraries in /usr/lib/
  sed -i 's|lib64|lib|' src/src.pro
}

build() {
  cd ${pkgname}-${pkgver}
  qmake-qt4
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make INSTALL_ROOT="${pkgdir}" install
}
