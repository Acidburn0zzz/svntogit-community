# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar
pkgver=0.113
pkgrel=1
pkgdesc="Sugar GTK shell"
arch=('any')
url="https://sugarlabs.org/"
license=('GPL')
depends=('gnome-keyring' 'gst-plugins-espeak' 'gtksourceview3' 'gvfs' 'libwnck3'
         'libxklavier' 'metacity' 'mobile-broadband-provider-info' 'openssh' 'python2-gwebsockets'
         'sugar-toolkit-gtk3' 'telepathy-gabble' 'telepathy-salut' 'upower' 'webkit2gtk'
         'xdg-user-dirs')
makedepends=('intltool')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz
        dont-overwrite-settings.patch)
sha256sums=('dd0f8d632db5f1123cca34106ebdd3a984145c33c0e3f530853e0819947bd2a0'
            '3ce2db5d003b3172302492a2c8ae1ba34aeafdfc6038c6751eb41c56909a062c')

prepare() {
  cd $pkgname-$pkgver

  # Use correct D-Bus config location
  sed -i '/^nmservicedir =/ s/sysconfdir/datadir/' data/Makefile.{am,in}

  # Don't overwrite default GNOME settings
  patch -Np1 -i ../dont-overwrite-settings.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/share/sugar/extensions/cpsection/updater/
}
