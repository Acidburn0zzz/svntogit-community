# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=nvchecker
pkgver=1.5
pkgrel=2
pkgdesc="New version checker for software releases"
arch=('any')
url="https://github.com/lilydjwg/nvchecker"
license=('MIT')
depends=('python' 'python-setuptools' 'python-structlog' 'python-tornado' 'python-pycurl')
checkdepends=('python-pytest' 'python-pytest-asyncio' 'python-pytest-httpbin' 'python-flaky' 'git' 'mercurial')
optdepends=(
  'pyalpm: allow using "sort_version_key = vercmp" in configuration files'
  'bzr: for VCS sources'
  'git: for VCS sources'
  'mercurial: for VCS sources'
  'subversion: for VCS sources'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/lilydjwg/nvchecker/archive/v$pkgver.tar.gz"
        $pkgname-update-tests.patch::https://github.com/lilydjwg/nvchecker/commit/7055ad555e033da7211852e3a2c45ef161deef6b.patch
        $pkgname-update-tests-2.patch::https://github.com/lilydjwg/nvchecker/commit/b6d26e68fb6cc098d70fc685ce92586e0f575f83.patch
        $pkgname-update-tests-3.patch::https://github.com/lilydjwg/nvchecker/commit/d9daa17a502a635fff93bb3968e91a11d6369432.patch)
sha512sums=('78902e43bbd9208b6668eab9a59036fe699918a87dc84ffbf174941597c6f23bf6952952d839a3a2941fad3cbcdc5a8c6336367d6d232491b67caa4631f85a58'
            '1c6a692ef7d86c3ad0ef2adcb671ec1028c976774d6f5a72f4a6f8a70f24fbefda3ca0d299951950b066563b9fa9f0b37421a573e9c867041eac4fc43fceeda5'
            '81cb482fcf53f906199de81ed2783a1713cbc78f14a74f4e7987dc77c53a0e4041bce05e6b0beb3b5dfa7da0cdb1379fe1c5f0eaa3f47a942536937a426e3b4b'
            '455a396c9480da1f2db56a3bbcb97238c990bf86ef8784b2d47c7ca12e639004e7685579438f8c000fec4ec8146d518817e4c4883f16854feee277efbb9f633a')

prepare() {
  cd nvchecker-$pkgver
  patch -Np1 -i ../$pkgname-update-tests.patch
  patch -Np1 -i ../$pkgname-update-tests-2.patch
  patch -Np1 -i ../$pkgname-update-tests-3.patch
}

build() {
  cd nvchecker-$pkgver
  python setup.py build
}

check() {
  cd nvchecker-$pkgver
  pytest
}

package() {
  cd nvchecker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}

# vim:set ts=2 sw=2 et:
