# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=gnome-twitch
pkgver=0.4.2
pkgrel=5
pkgdesc='Enjoy Twitch on your GNU/Linux desktop.'
arch=('x86_64')
url='https://github.com/vinszent/gnome-twitch'
license=('GPL3')
depends=('gtk3' 'libsoup' 'json-glib' 'webkit2gtk' 'libpeas')
makedepends=('meson' 'gobject-introspection' 'gstreamer' 'gst-libav' 'gst-plugins-base'
             'gst-plugins-good' 'gst-plugins-bad' 'clutter-gst' 'clutter-gtk' 'mpv')
optdepends=('gstreamer: gstreamer backends'
            'gst-libav: gstreamer backends'
            'gst-plugins-base: gstreamer backends'
            'gst-plugins-good: gstreamer backends'
            'gst-plugins-bad: gstreamer backends'
            'clutter-gst: gstreamer-clutter backend'
            'clutter-gtk: gstreamer-clutter backend'
            'mpv: mpv-opengl backend')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f13806a73b1a2ca5e84104b649f845f45410a01a1516cfe7bf2b28dfab12b99ebe4db8bee65c1658410512e2df57ec440308dc5a8d4047f805d230397ef7b92d')

build() {
  mkdir -p $pkgname-$pkgver/{build,gs-cairo,gs-clutter,gs-opengl,mpv-opengl}

  cd "$srcdir"/$pkgbase-$pkgver/build
  meson --prefix=/usr \
    --buildtype=plain \
    -Dbuild-player-backends=gstreamer-cairo,gstreamer-clutter,gstreamer-opengl,mpv-opengl \
    ..
  ninja
}

package_gnome-twitch() {
  cd $pkgbase-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
