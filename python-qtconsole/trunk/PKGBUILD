# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-qtconsole
pkgver=4.6.0
pkgrel=1
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.org/project/qtconsole/"
license=('BSD')
depends=('python-ipykernel' 'python-jupyter_core' 'python-jupyter_client'
         'python-traitlets' 'python-pygments' 'python-pyzmq'
         'qt5-svg' 'python-pyqt5' 'python-sip')
makedepends=('python-setuptools')
#checkdepends=('python-nose' 'python-pytest')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
md5sums=('dc92e6d4ad29cfd1726d75f26e9e8f5f')

build() {
  cd "$srcdir/qtconsole-$pkgver"
  python setup.py build
}

check() {
  # tests require an active X session
  true
  return
  cd "$srcdir/qtconsole-$pkgver"
  nosetests qtconsole
}

package() {
  cd "$srcdir/qtconsole-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd examples
  # FS#47046 FS#57556 FS#64628 fix .desktop icon
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.8/site-packages/qtconsole/resources/icon/JupyterConsole.svg|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"
}
