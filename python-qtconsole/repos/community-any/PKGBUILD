# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-qtconsole
pkgver=4.5.5
pkgrel=3
pkgdesc="Qt-based console for Jupyter with support for rich media output"
arch=('any')
url="https://pypi.org/project/qtconsole/"
license=('BSD')
depends=('python-ipykernel' 'python-jupyter_core' 'python-jupyter_client'
         'python-traitlets' 'python-pygments' 'python-pyzmq'
         'qt5-svg' 'python-pyqt5' 'python-sip')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-pytest')
source=("$pkgname-$pkgver.tgz::https://github.com/jupyter/qtconsole/archive/$pkgver.tar.gz")
md5sums=('b6e58c890712d9522f2bb2d28b5ace39')

build() {
  cd "$srcdir/qtconsole-$pkgver"
  python setup.py build
}

check() {
  # tests require an active X session
  return
  cd "$srcdir/qtconsole-$pkgver"
  nosetests qtconsole
}

package_python-qtconsole() {
  cd "$srcdir/qtconsole-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=0
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd examples
  # FS#47046 fix .desktop icon
  # FS#57556 fix .desktop icon again
  sed -i 's|^Icon=.*$|Icon=/usr/lib/python3.8/site-packages/qtconsole/resources/icon/JupyterConsole|' \
    jupyter-qtconsole.desktop
  install -Dm644 jupyter-qtconsole.desktop \
    "$pkgdir/usr/share/applications/jupyter-qtconsole.desktop"
}
