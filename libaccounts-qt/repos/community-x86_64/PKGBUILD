# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgbase=libaccounts-qt
_pkgbase=accounts-qt
pkgname=('libaccounts-qt' 'libaccounts-qt-doc')
pkgver=1.2
pkgrel=1
pkgdesc="Qt-based client library for accessing the online accounts database"
arch=('i686' 'x86_64')
url="http://code.google.com/p/accounts-sso/"
license=('LGPL')
depends=('qt' 'libaccounts-glib')
makedepends=('doxygen' 'graphviz' 'ttf-dejavu')
source=(http://accounts-sso.googlecode.com/files/$_pkgbase-$pkgver.tar.bz2)
sha1sums=('5824847558ffea57cafbeb7963595dd80366ad82')

build() {
  cd "$srcdir/$_pkgbase-$pkgver"

  # Do not install tests
  echo 'INSTALLS =' >>tests/accountstest.pro

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package_libaccounts-qt() {
  cd "$srcdir/$_pkgbase-$pkgver"

  make INSTALL_ROOT="$pkgdir" install

  # Split out documentation
  mv "$pkgdir/usr/share/doc" "$srcdir"
}

package_libaccounts-qt-doc() {
  pkgdesc="Developer documention of signon"
  depends=()
  options=('!emptydirs')

  mkdir -p "$pkgdir/usr/share"
  mv "$srcdir/doc" "$pkgdir/usr/share"
}
