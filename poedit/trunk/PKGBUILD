# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
pkgver=1.6
pkgrel=1
pkgdesc="Cross-platform gettext catalogs (.po files) editor"
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('custom')
depends=('wxgtk' 'gtkspell' 'db' 'hicolor-icon-theme' 'gettext')
makedepends=('pkgconfig' 'boost')
install=poedit.install
source=(http://www.poedit.net/dl/poedit-$pkgver.tar.gz)
md5sums=('41ce06c0d44962bf1898abff1599bf5e')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    export WX_CONFIG_PATH=/usr/bin/wx-config-2.9
    sh configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
