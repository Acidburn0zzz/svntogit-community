# $Id$
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=poedit
pkgver=1.5.2
pkgrel=1
pkgdesc='Cross-platform gettext catalogs (.po files) editor'
arch=('i686' 'x86_64')
url="http://www.poedit.net/"
license=('custom')
depends=('wxgtk>=2.8.11' 'gtkspell' 'db>=5.1' 'hicolor-icon-theme' 'gettext')
makedepends=('pkgconfig' 'boost')
install=poedit.install
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
md5sums=('d58ff8b5a821d68258699b2697ec9041')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR=${pkgdir} install
    install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
