# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Sven Klomp <mail@klomp.eu>

_pkgname=python-gnupg
pkgbase=python-gnupg
pkgname=('python-gnupg' 'python2-gnupg')
pkgdesc="A Python wrapper for GnuPG"
pkgver=0.4.0
pkgrel=2
url="https://pythonhosted.org/python-gnupg/"
license=("BSD")
arch=("any")
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/p/python-gnupg/${_pkgname}-${pkgver}.tar.gz"
		"https://pypi.io/packages/source/p/python-gnupg/${_pkgname}-${pkgver}.tar.gz.asc")
sha512sums=('f486ec97aee01cf46d055421211ff5704093a382155043b7ced6f809dc15d7669d5869038a1b26c5f1831076d85c929d781bc22e35ab293a14bc6e2f5f27fa32'
            'SKIP')
validpgpkeys=('CA749061914EAC138E66EADB9147B477339A9B86') # Vinay Sajip (CODE SIGNING KEY) <vinay_sajip@yahoo.co.uk>

prepare() {
    # Create a copy for the python2 package
    cp -r "${_pkgname}-${pkgver}" "python2-${_pkgname}-${pkgver}"
}

package_python-gnupg() {
    pkgdesc="A Python 3 wrapper for GnuPG"
    depends=('python' 'gnupg')

    cd "${srcdir}/${_pkgname}-${pkgver}"
    python setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-gnupg() {
    pkgdesc="A Python 2 wrapper for GnuPG"
    depends=('python2' 'gnupg')

    cd "${srcdir}/python2-${_pkgname}-${pkgver}"
    python2 setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
