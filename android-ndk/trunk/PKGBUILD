# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>

pkgbase=android-ndk
pkgname=('android-ndk32' 'android-ndk64')
pkgver=r10b
pkgrel=1
pkgdesc='Android C/C++ developer kit'
arch=('x86_64' 'i686')
url='http://developer.android.com/sdk/ndk/index.html'
license=('GPL' 'LGPL' 'custom')
depends=('android-sdk')
options=('!strip' 'staticlibs')
install="$pkgbase.install"

noextract=("${pkgbase}32-$CARCH.tbz2"
           "${pkgbase}64-$CARCH.tbz2")

if [[ $CARCH == x86_64 ]]; then
    source=("${pkgbase}32-$CARCH.tbz2::http://dl.google.com/android/ndk/${pkgbase}32-$pkgver-linux-x86_64.tar.bz2"
            "${pkgbase}64-$CARCH.tbz2::http://dl.google.com/android/ndk/${pkgbase}64-$pkgver-linux-x86_64.tar.bz2"
            "${pkgbase}.sh")
    sha256sums=('f309365a809f61ab012dd3560dff1ecdad8a3652b316938a6f9943cba42bda95'
                '32452087a846e73ef3007903e1368edc21847720566b34ae113e454ae259bd8b'
                '5bc58ccd7e7de03c9656ca8f13fb9bf9dff2eeee31a2670ce04a4b97be73dc95')
else
    source=("${pkgbase}32-$CARCH.tbz2::http://dl.google.com/android/ndk/${pkgbase}32-$pkgver-linux-x86.tar.bz2"
            "${pkgbase}64-$CARCH.tbz2::http://dl.google.com/android/ndk/${pkgbase}64-$pkgver-linux-x86.tar.bz2"
            "${pkgbase}.sh")
    sha256sums=('e4766586d0f65452317e662758eb7d070c55ad4092e1e82ab1717a1114a1c430'
                'f56782b70ceff53749b9e63f0384e4e0ad04a46a2d7cd64c3b27c7493d2f64dc'
                '5bc58ccd7e7de03c9656ca8f13fb9bf9dff2eeee31a2670ce04a4b97be73dc95')
fi

package_android-ndk32() {
  provides=('android-ndk')
  replaces=('android-ndk')

  install -Dm755 "$pkgbase.sh" "$pkgdir/etc/profile.d/$pkgbase.sh"
  install -d "$pkgdir/opt"
  tar kaxf "$pkgname-$CARCH.tbz2" -C "$pkgdir/opt"
  mv "$pkgdir/opt/$pkgbase-$pkgver" "$pkgdir/opt/$pkgname"
}

package_android-ndk64() {
  provides=('android-ndk')
  replaces=('android-ndk')

  install -Dm755 "$pkgbase.sh" "$pkgdir/etc/profile.d/$pkgbase.sh"
  install -d "$pkgdir/opt"
  tar kaxf "$pkgname-$CARCH.tbz2" -C "$pkgdir/opt"
  mv "$pkgdir/opt/$pkgbase-$pkgver" "$pkgdir/opt/$pkgname"
}

# vim:set ts=2 sw=2 et:
