# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Florian Pritz <bluewind@xinu.at>

pkgname=perl-term-ui
pkgver=0.48
pkgrel=1
pkgdesc='User interfaces via Term::ReadLine made easy'
arch=('any')
license=('PerlArtistic' 'GPL')
options=("!emptydirs")
depends=('perl-log-message' 'perl-log-message-simple')
url='https://metacpan.org/release/Term-UI'
source=("https://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/Term-UI-$pkgver.tar.gz")
sha512sums=('4432b398152d9aa3557655ea6bbd83dbbda3596a21a418e99ce6c5c426a1997b3300cc0b8bb65f2bb1eca06fb0db1dcfb03ea9ce9cccc93060abceecc9157d04')
_ddir="Term-UI-$pkgver"

build() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check() (
  cd "$srcdir/$_ddir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package() (
  cd "$srcdir/$_ddir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  #find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
