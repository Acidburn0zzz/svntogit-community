# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=hypercorn
pkgver=0.9.3
pkgrel=1
pkgdesc='An ASGI Server based on Hyper libraries and inspired by Gunicorn'
url=https://gitlab.com/pgjones/hypercorn
arch=(any)
license=(MIT)
depends=(
  python-h11
  python-h2
  python-toml
  python-typing_extensions
  python-wsproto
)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-hypothesis
  python-pytest
  python-pytest-asyncio
  python-pytest-sugar
  python-pytest-trio
  python-tox
  python-trio
)
optdepends=(
  'python-trio: trio support'
)
source=(git+https://gitlab.com/pgjones/hypercorn.git#tag=c0e45a5c1c61e7f3f698d770492a04b51e2b01bc)
sha256sums=(SKIP)

pkgver() {
  cd hypercorn

  git describe --tags
}

build() {
  cd hypercorn

  python setup.py build
}

check() {
  cd hypercorn

  tox -e py38
}

package() {
  cd hypercorn

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/hypercorn/
}

# vim: ts=2 sw=2 et:
