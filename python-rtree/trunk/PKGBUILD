# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.9.5
pkgrel=1
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('spatialindex' 'python-setuptools')
makedepends=('python-wheel')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Toblerity/rtree/archive/$pkgver.tar.gz")
sha512sums=('fdc1b2eb33610e157a51e914e70814c6d2d36c50c9b425bbbf4e953705df588e536347af358642d1fec7aab0a5432c840bc73f63aba8ae3f88b6200d30e40c0c')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
