# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-rtree
pkgver=0.9.2
pkgrel=1
pkgdesc="Rtree: spatial index for Python GIS"
url="https://toblerity.github.com/rtree/"
license=('LGPL')
arch=('any')
depends=('spatialindex' 'python-setuptools')
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Toblerity/rtree/archive/$pkgver.tar.gz")
sha512sums=('85ffeb1474102c9d00df8e9a51f4a19de992f23cff066cef2bef95da5dbed29ac652fa2cdd7ce6bf8e3a9f536594ad6ee40b4079f7cd330bfb1d7e772688a2be')

build() {
  cd rtree-$pkgver
  python setup.py build
}

check() {
  cd rtree-$pkgver
  python setup.py pytest
}

package() {
  cd rtree-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
