# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=xnoise
pkgver=0.2.18
pkgrel=1
pkgdesc="Media player with a slick GUI, great speed and lots of features."
arch=('i686' 'x86_64')
url="http://www.xnoise-media-player.com/"
license=('GPL2')
depends=('desktop-file-utils' 'gnome-icon-theme' 'gst-plugins-base-libs' 'gst-plugins-good' 'gtk3' 'libsoup' 'sqlite' 'libtaginfo' 'librsvg')
optdepends=('gst-plugins-base: Basic media codecs'
            'gst-plugins-bad: Extra media codecs'
            'gst-plugins-ugly: Extra media codecs'
	    'gst-libav: Extra media codecs'
	    'gvfs: Needed for fetching album art')
makedepends=('gettext' 'intltool')
options=('!libtool')
install=$pkgname.install
source=("https://bitbucket.org/shuerhaaken/xnoise/downloads/$pkgname-$pkgver.tar.gz")

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  
  # Remove obsolete files
  rm -rf "$pkgdir/usr/include" "$pkgdir/usr/share/vala"
}
sha512sums=('afe6d380f84c96d3612ed6a01a31b279b06d101252304a83be081eb25d4342e3487aa0f1955c3aa6b70297f8858d3c01be5f708a37c3741a1c2b6afd76127e9b')
