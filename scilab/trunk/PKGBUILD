# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Simon Lipp <sloonz+aur@gmail.com>
# Paulo Matias
pkgname=scilab
pkgver=5.2.1
pkgrel=1
pkgdesc="A scientific software package for numerical computations"
arch=('i686' 'x86_64')
url="http://www.scilab.org/"
provides=('scilab=$pkgver')
conflicts=('scilab4' 'scilab5')
license=('custom:CeCILL')
depends=('libxml2' 'pcre' 'java-saxon6<7' 'lapack' 'java-batik' \
  'java-flexdock' 'jogl' 'fop' 'jeuclid-core' 'fftw' 'libmatio' \
  'bash' 'tk' 'jrosetta' 'ocaml' 'javahelp2' 'jgraphx' \
  'jgoodies-looks' 'umfpack')
makedepends=('apache-ant' 'antlr')
source=("http://www.scilab.org/download/$pkgver/$pkgname-$pkgver-src.tar.gz" \
  'scilab.desktop')
md5sums=('47beab7be09516d89167e9a67a3be856' '460800803b34bfeb826b9b37d8c8fdd6')
options=('!libtool')

build() {
	cd $srcdir/scilab-$pkgver
	./configure --prefix=/usr \
	  --enable-build-localization --without-pvm \
	  --with-matio --with-umfpack --with-fftw \
	  --with-gfortran --disable-rpath --without-hdf5|| return 1
	make || return 1
}
package() {
	cd $srcdir/scilab-$pkgver
	make DESTDIR=$pkgdir install \
	  install-html || return 1
	install -d $pkgdir/usr/share/licenses/$pkgname &&
	install --mode=644 $srcdir/scilab-$pkgver/COPYING \
	  $pkgdir/usr/share/licenses/$pkgname || return 1
	#fix icon
	install -Dm644 $pkgdir/usr/share/scilab/icons/scilab.xpm \
	  $pkgdir/usr/share/pixmaps/scilab.xpm || return 1
	install -Dm644 $srcdir/$pkgname.desktop \
	  $pkgdir/usr/share/applications/$pkgname.desktop|| return 1
	sed -i 's#/jni##' $pkgdir/usr/share/scilab/etc/librarypath.xml
}
