# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-test-spec
pkgver=0.45
pkgrel=1
pkgdesc='Write tests in a declarative specification style'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-package-stash>=0.23' 'perl-test-deep>=0.103' perl-test-trap
         perl-tie-ixhash)
url=https://metacpan.org/release/Test-Spec
source=("http://search.cpan.org/CPAN/authors/id/P/PH/PHILIP/Test-Spec-$pkgver.tar.gz")
md5sums=(3f624aab9fd174ab5aaf128f6504014f)
sha512sums=(24dfe068f7651da2d3357fcabc256707d3b228eda5e7a5114f441ceaa4eb61fb91e389f3691796223dd82455d8d079aac4af9670bc7f7e25f22c935028775313)
_dir="$srcdir/Test-Spec-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
