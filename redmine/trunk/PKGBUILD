# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Federico Cuello <fedux@fedux.com.ar>

pkgname=redmine
pkgver=4.0.5
pkgrel=2
pkgdesc="A flexible project management web application written using Ruby on Rails framework."
arch=('any')
url="https://www.redmine.org"
license=('GPL2')
depends=('ruby2.6' 'libmagick6' 'libxml2' 'libxslt')
makedepends=('mariadb-libs' 'postgresql-libs')
optdepends=('apache: A supported application server'
            'nginx: A supported application server'
            'mysql: MySQL database support'
            'postgresql: PostgreSQL database support'
            'sqlite: SQLite database support')
source=("$url/releases/$pkgname-$pkgver.tar.gz"
	"Gemfile.local"
	"redmine.service")
sha512sums=('a6aa3e2418fd83ed3783fc6644c339c54ebb1952b8c2c0be4b718fb296fdff64fb4116faa2c88f3e5c3c25d7c5b3c21cd7822ff9f99f6f67270e0b8b64053678'
            '750ae2aa12ab6b94ab818f329b95e7aa04a148512472548a3d2f91df4af59709dd841d553402d4332e6fc49a8bb643b17e5bacb1c26a99702fde32cae053ec29'
            '0b897980a8920538674fa7585e1dbaef48a152226215d598c41b3fcf4ac6566b5866d6e3132c30c0adbdc0d8e0080e2541cb4a119aedc6bef24fd6c129abd4eb')

build() {
    cd "$srcdir"/redmine-$pkgver

    export PATH=/opt/ruby2.6/bin:$PATH
    export PKG_CONFIG_PATH="/usr/lib/imagemagick6/pkgconfig"

    cp "${srcdir}"/Gemfile.local Gemfile.local
    bundle-2.6 lock
    bundle-2.6 install --no-cache --deployment --without development test
}

package() {
    install -dm 755 "$pkgdir"/usr/share/webapps/
    cp -a --no-preserve=ownership "$srcdir"/redmine-$pkgver "$pkgdir"/usr/share/webapps/redmine

    install -Dm0644 "${srcdir}"/redmine.service "${pkgdir}"/usr/lib/systemd/system/redmine.service

    cd "$pkgdir"/usr/share/webapps/redmine
    rm -rf files log
    install -dm0755 -o http -g http "$pkgdir"/var/lib/redmine/files
    install -dm0755 -o http -g http "$pkgdir"/var/log/redmine
    mv tmp "$pkgdir"/var/lib/redmine/tmp
    chown -R http:http "$pkgdir"/var/lib/redmine/tmp
    ln -s /var/log/redmine log
    ln -s /var/lib/redmine/files files
    ln -s /var/lib/redmine/tmp tmp
}
