# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski
# Contributor: Laszlo Papp <djszapi2@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pigz
pkgver=2.3
pkgrel=1
pkgdesc='Parallel implementation of the gzip file compressor'
arch=('i686' 'x86_64')
url='http://www.zlib.net/pigz/'
license=('custom')
depends=('zlib')
source=(http://www.zlib.net/$pkgname/$pkgname-$pkgver.tar.gz
        pigz-2.3-make.patch)
md5sums=('042e3322534f2c3d761736350cac303f'
         'f6291ee36e36e098d7fbf0655a89b183')

build() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -Np1 -i "$srcdir"/pigz-2.3-make.patch
  make
}

package() {
  cd "$srcdir"/$pkgname-$pkgver

  install -Dm755 "$srcdir"/$pkgname-$pkgver/$pkgname \
    "$pkgdir"/usr/bin/$pkgname 

  pushd "$pkgdir"/usr/bin
    ln -s pigz unpigz 
  popd

  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.1 \
    "$pkgdir"/usr/share/man/man1/$pkgname.1
  install -Dm644 "$srcdir"/$pkgname-$pkgver/$pkgname.pdf \
    "$pkgdir"/usr/share/doc/$pkgname/$pkgname.pdf
  install -Dm644 "$srcdir"/$pkgname-$pkgver/README \
    "$pkgdir"/usr/share/licenses/$pkgname/README
}
