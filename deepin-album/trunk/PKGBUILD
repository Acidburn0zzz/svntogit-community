# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.6.9.26
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
         deepin-album-qt5.15.patch)
sha512sums=('7db5ab578f468715e9a7d28478c43c85e3987edce2a1e3286b394c8c28e240a7bce39aa67ac028e8a364beee20f54be458c7642a2a731c82b5d00f0b68104eb6'
            '52c48e5d2efe603cfa0d9ab9db9619370dfcf7fdeab8fb52d94c5481ad98fe3ca5bfadf240e3acc5542dbe5fa1be766ad7189316fc9db3632b76fbfd6ed9c640')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../deepin-album-qt5.15.patch # Fix build with Qt 5.15
  sed -i '1i #include <QPainterPath>' widgets/dlmenuarrow.h module/view/contents/imageinfowidget.cpp
}

build() {
  cd deepin-album-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-album-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
