# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.8.5
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'hicolor-icon-theme' 'udisks2-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz")
sha512sums=('dd644f374f5731f9bd241c08f738e3a5e0036dc444522a87b73674cbbf133f84f4dd8752a555aada54343a17649faaaa6f1624ec1ff4220ec12fd50774388260')

prepare() {
  cd deepin-album-$pkgver

  # Our build of freeimage disabled support for these formats
  sed -i '/FIF_FAXG3/d' libUnionImage/UnionImage/unionimage.cpp
}

build() {
  cd deepin-album-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd deepin-album-$pkgver
  DESTDIR="$pkgdir" ninja install
}
