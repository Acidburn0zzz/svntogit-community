# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-album
pkgver=5.6.9.17
pkgrel=1
pkgdesc='A fashion photo manager for viewing and organizing pictures'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-album"
license=('GPL3')
depends=('deepin-qt5integration' 'freeimage' 'gio-qt' 'udisks2-qt5')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-album/archive/$pkgver.tar.gz"
         deepin-album-qt5.15.patch)
sha512sums=('91598841fd8831653f88ab037a45a1eb3792965934863b2b6416af2ab7e66560cfb906b0246c9f60b9a51c39cd96bba8d185814f4a1c2b912ab3fae54b8f0492'
            '52c48e5d2efe603cfa0d9ab9db9619370dfcf7fdeab8fb52d94c5481ad98fe3ca5bfadf240e3acc5542dbe5fa1be766ad7189316fc9db3632b76fbfd6ed9c640')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../deepin-album-qt5.15.patch # Fix build with Qt 5.15
  sed -i '1i #include <QPainterPath>' widgets/dlmenuarrow.h widgets/dialogs/dfmdarrowlineexpand.cpp
}

build() {
  cd deepin-album-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-album-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
