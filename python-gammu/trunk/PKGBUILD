# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux>

pkgname=python-gammu
pkgver=3.2
pkgrel=1
pkgdesc="Python bindings for Gammu library"
arch=('x86_64')
url="https://wammu.eu/python-gammu/"
license=('GPL')
depends=('python' 'gammu')
makedepends=('python-setuptools')
checkdepends=('libdbi-drivers')
source=($pkgname-$pkgver.tar.gz::https://github.com/gammu/$pkgname/archive/refs/tags/$pkgver.tar.gz)
sha512sums=('37221c1c0aa2128273c510bd5daeca4d354cf2ab8160d3501d7589159284d3ec3ba17d62461ac257b80cb6fe65c1c20efe3aa1a586c20617209b339592671528')

build() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py build
}

check() {
  # tests can be flaky on slower hardware due to timing
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py test 
}

package() {
  cd "${srcdir}"/python-gammu-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
}
