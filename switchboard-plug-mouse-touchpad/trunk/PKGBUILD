# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-mouse-touchpad
pkgver=2.3.0
pkgrel=1
pkgdesc='Switchboard Mouse & Touchpad Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-mouse-touchpad
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-mouse-touchpad.git#tag=1616308891b6947e32394624191ce7492b5d6f10)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-mouse-touchpad

  git describe --tags
}

build() {
  arch-meson switchboard-plug-mouse-touchpad build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
