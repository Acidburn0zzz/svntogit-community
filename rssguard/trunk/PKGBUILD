# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: Alois Nespor <info@aloisnespor.info>
# Contributor: speps <speps dot aur dot archlinux dot org>

pkgbase=rssguard
pkgname=(rssguard{,-nowebengine})
pkgver=3.6.3
pkgrel=1
pkgdesc='Simple (yet powerful) Qt5 feed reader'
arch=(x86_64)
url='https://github.com/martinrotter/rssguard'
license=(GPL3)
depends=(qt5-webengine)
makedepends=(qt5-tools)
source=(https://github.com/martinrotter/$pkgbase/archive/$pkgver/$pkgbase-$pkgver.tar.gz
        0001-Fix-238-build.patch)
sha256sums=('12c2c36cd36b315e4ccfe75b67ae36c4bad770b9a4a1ab2fbb5a33a3b4ef0954'
            '90f70e9111c7d082ab20711187c2816eeec9cfa00c9d5d7749d792da78a1c470')

prepare() {
  mkdir build{,-nowebengine}
  cd $pkgname-$pkgver
  patch -Np1 -i ../0001-Fix-238-build.patch
}

build() {
  cd build
  qmake ../$pkgname-$pkgver -r CONFIG+=release PREFIX=/usr
  make

  cd ../build-nowebengine
  qmake ../$pkgname-$pkgver -r CONFIG+=release PREFIX=/usr USE_WEBENGINE=false
  make
}

package_rssguard() {
  depends=(qt5-webengine)

  cd build
  make INSTALL_ROOT="$pkgdir" install
}

package_rssguard-nowebengine() {
  pkgdesc+=' without WebEngine support'
  depends=(hicolor-icon-theme qt5-base)
  conflicts=($pkgbase)
  provides=($pkgbase=$pkgver)

  cd build-nowebengine
  make INSTALL_ROOT="$pkgdir" install

  # Ignore package by AppStream to avoid duplicated IDs
  rm -r "$pkgdir/usr/share/metainfo/"
  echo 'X-AppStream-Ignore=true' >> "$pkgdir/usr/share/applications/com.github.rssguard.desktop"
}
