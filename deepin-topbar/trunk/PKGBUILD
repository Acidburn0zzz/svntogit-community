# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: kirigaya <kirigaya@mkacg.com>

pkgname=deepin-topbar
pkgver=0.6.0
pkgrel=1
pkgdesc="This is topbar for Deepin"
arch=('x86_64')
url="https://github.com/kirigayakazushin/deepin-topbar"
license=('GPL')
depends=('deepin-network-utils' 'deepin-qt5integration' 'deepin-qt-dbus-factory' 'libdbusmenu-qt5')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kirigayakazushin/deepin-topbar/archive/$pkgver.tar.gz")
sha512sums=('fd9155c148235a76072e9a96d6e2b38122a5257319381a7f1224f5e2901ba9ca290c11dda09570ca942080126d49796b87c28371502e9d66032a4389724560df')

build() {
  cd $pkgname-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
