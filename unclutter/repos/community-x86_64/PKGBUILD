# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Anthony Martin <ality@pbrane.org>

_pkgname=unclutter-xfixes
pkgname=unclutter
epoch=1
pkgver=1.4
pkgrel=1
pkgdesc='A small program for hiding the mouse cursor'
url="https://github.com/Airblader/${_pkgname}"
arch=('x86_64')
license=('MIT')
depends=('libxi' 'libx11' 'libxfixes' 'libev')
makedepends=('asciidoc')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/v${pkgver}.tar.gz")
sha256sums=('f28cd690cfa184fddd3961dda49d14e114dccd5e70d9bac8ca5ebcd5124985b3')

build() {
    cd "${_pkgname}-${pkgver}"
    make all
}

package() {
    cd "${_pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install

    # TODO remove in the next version: https://github.com/Airblader/unclutter-xfixes/pull/37
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}

# vim:set ts=4 sw=4 et:
