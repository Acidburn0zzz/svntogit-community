# $Id$
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.11
pkgrel=7
pkgdesc='Basic, good-looking task manager for WMs'
arch=('i686' 'x86_64')
url='http://code.google.com/p/tint2/'
license=('GPL2')
conflicts=('ttm-svn' 'tint')
replaces=('tint')
depends=('gtk2' 'imlib2')
optdepends=('pygtk: for tint2wizard')
makedepends=('pygtk' 'cmake')
provides=('tint')
source=("http://tint2.googlecode.com/files/tint2-$pkgver.tar.bz2"
        'add-power-now-support.patch'
        'clock.patch'
        'freespace.patch'
        'graph.patch'
        'launcher_apps_dir-v2.patch'
        'src-task-align.patch'
        'zombie-fix.patch')

md5sums=('6fc5731e7425125fa84a2add5cef4bff'
         '6cfcad028f1bd2f69812167f5395f890'
         'bc0bab2979dacff551a97bdf2c2fdedc'
         'deb1ff7fafdde77f76c890b1adca83d4'
         '2866ebde44be8d48efd1e59ded5c5534'
         '13218765dd684ae825967d3ffb4f4a75'
         'f0d7f51ec8dbf2e7b6bcca942f0fd6c0'
         'cdb83cd911e005a3529e5d1cd952a956')

prepare() {
  cd "$pkgname-$pkgver"

  # making all the patches apply is a work in progress
  for f in "$srcdir"/*.patch; do
    echo
    echo "--- ${f##*/} ---"
    echo
    if [[ ${f##*/} == add-power-now-support.patch ]]; then
      patch -Np2 -i $f && echo "${f##*/} OK!"
    else
      patch -Np1 -i $f && echo "${f##*/} OK!"
    fi
  done

  # python2 fix
  sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' src/tint2conf/tintwizard.py
  sed -i 's_python _python2 _' src/tint2conf/main.c
}

build() {
  cd "$pkgname-$pkgver"

  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_TINT2CONF=0
  make 
}

package() {
  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
