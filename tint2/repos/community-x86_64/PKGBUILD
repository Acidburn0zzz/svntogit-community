# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Blue Peppers <bluepeppers@archlinux.us>
# Contributor: Stefan Husmann< stefan-husmann@t-online.de>

pkgname=tint2
pkgver=0.12.4
pkgrel=2
pkgdesc='Basic, good-looking task manager for WMs'
arch=('x86_64' 'i686')
url='https://gitlab.com/o9000/tint2'
license=('GPL2')
conflicts=('ttm-svn')
depends=('gtk2' 'imlib2' 'startup-notification')
makedepends=('cmake' 'startup-notification' 'git' 'ninja')
source=("$pkgname-$pkgver.tgz::https://gitlab.com/o9000/tint2/repository/archive.tar.gz?ref=$pkgver")
sha256sums=('68065a85eec6f4186c52adb48d9fdab2b1008df92e34380038739c146bb59f06')

prepare() {
  mv "$pkgname-$pkgver-"* "$pkgname"
}

build() {
  cd "$pkgname"
  mkdir -p build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr -DENABLE_TINT2CONF=1 -G Ninja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C "$pkgname/build" install
}

# vim:set ts=2 sw=2 et:
