# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Lubomir 'Kuci' Kucera <kuci24-at-gmail-dot-com>

pkgname=gitlab-runner
pkgver=14.0.0
_commit=3b6f852e
pkgrel=1
pkgdesc="The official GitLab CI runner written in Go"
arch=('x86_64')
url='https://gitlab.com/gitlab-org/gitlab-runner'
license=('GPL3')
depends=('ca-certificates' 'curl' 'git' 'glibc' 'tar')
makedepends=('git' 'go' 'git' 'mercurial' 'gox')
install=gitlab-runner.install
replaces=('gitlab-ci-multi-runner')
backup=('etc/gitlab-runner/config.toml')
noextract=("prebuilt-alpine-arm.tar.xz"
           "prebuilt-alpine-arm64.tar.xz"
           "prebuilt-alpine-s390x.tar.xz"
           "prebuilt-alpine-x86_64-pwsh.tar.xz"
           "prebuilt-alpine-x86_64.tar.xz"
           "prebuilt-ubuntu-arm.tar.xz"
           "prebuilt-ubuntu-arm64.tar.xz"
           "prebuilt-ubuntu-s390x.tar.xz"
           "prebuilt-ubuntu-x86_64-pwsh.tar.xz"
           "prebuilt-ubuntu-x86_64.tar.xz")
source=("git+https://gitlab.com/gitlab-org/gitlab-runner.git#tag=${_commit}"
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-arm64.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-s390x.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64-pwsh.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-alpine-x86_64.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-arm64.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-s390x.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64-pwsh.tar.xz
        https://gitlab-runner-downloads.s3.amazonaws.com/v${pkgver}/helper-images/prebuilt-ubuntu-x86_64.tar.xz
        "gitlab-runner.service"
        "gitlab-runner.sysusers"
        "gitlab-runner.tmpfiles"
        "config.toml")
sha512sums=('SKIP'
            'c36e4f24815003826ebdb4f61a1d5f02f73a5de9c9e5b342d7a6537345e5142af2ed38f3fa1c5beefa7655d032d16434096dd10e3dd04132cc6358b7f3f123de'
            'd52d41f6ce07ed645f5fd2994b17b5530fc225d83574e2d9b5a4ee5f89de6e655a427cf8402885a84b3c5686339070a8a95e36390899e06d7925924da9c3ca80'
            'c2b6802ad9ab36f2b664d963745fe94d45be96a03cdadfddb865ea602dced399ada83a0d87c612d919817620b5324f083f9af63cbfab88d7ffe2935aecab9524'
            '277c8a03d081f5f9393e7ab2c8cd60d691548062548dfee8d7d51a246d8980cde03d05baa3d2b745b09e3bbad1b3d3d1f17471eddc4686316edd4d18c63f0745'
            '97d72468211e9c29b529b43c042f804e50854781e11aa3b91dce48dd3e70fdc81fcf74fa696bf9bc56a90f5ba8c84c9235ca1eab510dc8ef52eafd768ca5a32e'
            '5d6ec3b611eb53a1c5fac5a4a0df22ce3fd4bb386e4bbfcafa395c60cb4b836e043d76e9e455a2bf1d42467c3446326929bc7276f5e1f604ffc1aaa433c4c044'
            'ec4a77ce11d830831301ad6080cf8b067de79baaaabf82709620fdb09ce817c4dfe4002c5cf93933771edf927fe96fdbbd80e3ea0c3655a927acbf6b5d0329c5'
            'b57fac88cf9f0adf620b3fae2e8973671190aa13828b368f0d6360a4c52735d361288d43ce9fcd675b88621fae07a1f1da9ad40bd5ed7278a8b9c3be6f200406'
            '18e9b1bcb06452e9fb9716a348cbb0cc8c650a35cc9634e1cbd92f840aec7559d516f034dd97f35225f97084d3aa1ad2c08bef84f092b832388bd91999671df7'
            '50abf600fb45073edcf9f2bcd06cbbdcdebb572d7a7ab58a0bd5634958b3a967139c0a6835907679a1c658adadb4d756925bd01e6e7fc7a6e4e995f72e6bd6f5'
            'c0af374b9986895aedcfaee6c67cfad68f0f7289f87e4611358adaff59a2f349f55764fe28b2b1f61f8bfeb61126d4f90d433c626fdf9b826a2de6217f86574f'
            '8aa7f08702e99053c696fcc2aaba83beb9e9cd6f31973d82862db9350ac46df3a095377625d31fe909677525290d2de922d7a97930ed235774cb8f0da8944d40'
            '6751d9fa0b27172d1b419c4138f5ac15cbc7c9147653a7258cf1470216142c637210bb60608c7ed0974e0e4057e5ddeae32225df1bb36e7dd1f20fec71e33cc3'
            '9718b94bd0ddb09095ffb8c1e60ca1e9649dabb1747e7fc95e58e404b2f9effdeb4cfd759f5b904443dc53a4e18c02003c38f85584713deb49f6a6d1007503de')

prepare() {
  cd gitlab-runner

  local version=$(make version | grep "Current version:" | sed -n "s/.*: \(.*\)/\1/p")
  local revision=$(make version | grep "Current revision:" | sed -n "s/.*: \(.*\)/\1/p")
  local branch=$(make version | grep "Current branch:" | sed -n "s/.*: \(.*\)/\1/p")

  sed -i "s/var VERSION.*/var VERSION = \"$version\"/" common/version.go
  sed -i "s/var REVISION.*/var REVISION = \"$revision\"/" common/version.go
  sed -i "s/var BRANCH.*/var BRANCH = \"$branch\"/" common/version.go
}

build() {
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=readonly -modcacherw"

  cd gitlab-runner
  go build -o gitlab-runner .
}

package() {
  cd gitlab-runner

  install -Dm644 "${srcdir}/config.toml" "${pkgdir}/etc/gitlab-runner/config.toml"
  install -Dm644 "${srcdir}/gitlab-runner.service" "${pkgdir}/usr/lib/systemd/system/gitlab-runner.service"
  install -Dm644 "${srcdir}/gitlab-runner.sysusers" "${pkgdir}/usr/lib/sysusers.d/gitlab-runner.conf"
  install -Dm644 "${srcdir}/gitlab-runner.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/gitlab-runner.conf"
  install -Dm755 gitlab-runner "${pkgdir}/usr/bin/gitlab-runner"

  # Move prebuilt Docker images to hard-coded canonical location
  for image in prebuilt-{alpine,ubuntu}-{arm,arm64,s390x,x86_64-pwsh,x86_64}.tar.xz; do
    install -Dm644 "${srcdir}/${image}" "${pkgdir}/usr/lib/gitlab-runner/helper-images/${image}"
  done
}
