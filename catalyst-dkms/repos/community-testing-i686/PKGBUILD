# $Id$
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vi0L0, wonder, Eduardo "kensai" Romero
# Contributor: aidanlinz, Rip-Rip, OvsInc, Sebastian Siebert

pkgname=catalyst-dkms
pkgver=13.1
pkgrel=2
pkgdesc="AMD proprietary gpu kernel driver"
arch=('i686' 'x86_64')
url="http://www.amd.com"
license=('custom')
depends=('dkms')
optdepends=('linux-headers: build the module against Arch kernel'
            'linux-lts-headers: build the module against LTS Arch kernel')
replaces=('catalyst-lts-dkms') # useless
source=(http://www2.ati.com/drivers/linux/amd-driver-installer-catalyst-${pkgver}-linux-x86.x86_64.zip
        dkms.conf
	kernel-3.7-fixes.patch
	kernel-3.8-fixes.patch)
md5sums=('b5d9de9b000448dcc2a9eb74a889116d'
         '972049dc247a74a7162f2227078abcb0'
         '02d6442c31dd8413bc1d316308d1fa56'
         '52a79bddac813f19f72fcb32acebb1b9')
install=catalyst.install

package() {
  depends=(${depends[@]} "catalyst-utils=${pkgver}")

  cd ${srcdir}
  
  if [ "${CARCH}" = "x86_64" ]; then
    _archdir=x86_64
  else
    _archdir=x86
  fi

  sh ./amd-driver-installer*.run --extract fglrx-install

  cd fglrx-install
  patch -Np1 -i ../kernel-3.7-fixes.patch
  patch -Np1 -i ../kernel-3.8-fixes.patch

  install -dm755 "${pkgdir}/usr/lib/modprobe.d"
  install -dm755 "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}"
  cp -r common/lib/modules/fglrx/build_mod/* "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp "arch/${_archdir}"/lib/modules/fglrx/build_mod/libfglrx_ip.a "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  cp ${srcdir}/dkms.conf "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/"
  sed -i -e "s/@VERSION@/${pkgver}-${pkgrel}/" "${pkgdir}/usr/src/fglrx-${pkgver}-${pkgrel}/dkms.conf"
  
  echo "blacklist radeon" >> "${pkgdir}/usr/lib/modprobe.d/catalyst.conf"
  
  # license
  install -Dm644 "common/usr/share/doc/fglrx/LICENSE.TXT" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.TXT"
}
