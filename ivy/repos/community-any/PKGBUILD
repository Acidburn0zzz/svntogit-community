# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Vitaliy Berdinskikh <ur6lad@archlinux.org.ua>

pkgname=ivy
pkgver=2.4.0
pkgrel=3
pkgdesc='Dependency manager'
arch=('any')
url='https://ant.apache.org/ivy/'
license=('Apache')
depends=('java-runtime>=1.8')
makedepends=('ant' 'setconf')
source=("https://apache.uib.no/ant/ivy/2.4.0/apache-ivy-2.4.0-src.tar.gz"
        'ivy.sh')
sha256sums=('202f08ca41f4bdf1c081aa8b2e531565be6c73e9e5e0d68137f454f14eb16ef6'
            'fc719139726f670f0acf7b09016a4bb3c4ffdd7a0db88e11eb827467b475ad2d')

prepare() {
  _fn="apache-$pkgname-$pkgver/build.properties"
  setconf "$_fn" ivy.minimum.javaversion=1.8
  setconf "$_fn" ivy.install.version=1.10
}

build() {
  cd "apache-$pkgname-$pkgver"

  ant jar
}

package() {
  install -Dm755 ivy.sh "$pkgdir/usr/bin/ivy"
  install -Dm644 "apache-$pkgname-$pkgver/build/artifact/jars/ivy.jar" \
    "$pkgdir/usr/share/java/ivy/ivy.jar"
}

# vim: ts=2 sw=2 et:
