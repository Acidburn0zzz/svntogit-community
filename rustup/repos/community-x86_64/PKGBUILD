# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jonathon Fernyhough <jonathon_at_manjaro_dot_org>
# Contributor: Jon Gjengset <jon@tsp.io>
pkgname=rustup
pkgver=1.15.0
pkgrel=1
pkgdesc="The Rust toolchain installer"
arch=('x86_64')
url="https://github.com/rust-lang-nursery/rustup.rs"
license=('MIT' 'Apache')
depends=('curl')
makedepends=('cargo')
optdepends=('lldb: rust-lldb script'
            'gdb: rust-gdb script')
provides=('rust' 'cargo' 'rust-nightly' 'cargo-nightly' 'rustfmt')
conflicts=('rust' 'cargo' 'rustfmt')
install='post.install'
source=("rustup-${pkgver}.tar.gz::https://github.com/rust-lang-nursery/rustup.rs/archive/${pkgver}.tar.gz")
sha512sums=('7917f1271105604c64bc405a044c0a4a1c536dd44b37342118c31ea01fec852e1b93bbfebe3a5ed4235e2f8ec4bff7c8b586a0e6db7803085942d9aa0aa11e75')
_binlinks=('cargo' 'rustc' 'rustdoc' 'rust-gdb' 'rust-lldb' 'rls' 'rustfmt' 'cargo-fmt' 'cargo-clippy' 'clippy-driver')

build() {
    cd "$srcdir/$pkgname.rs-${pkgver}"
    cargo build --release --features no-self-update --bin rustup-init
}

package() {
    cd "$pkgname.rs-${pkgver}"
    install -Dm755 "target/release/rustup-init" "${pkgdir}/usr/bin/rustup"
    for link in "${_binlinks[@]}"; do
        ln -s /usr/bin/rustup "${pkgdir}/usr/bin/${link}"
    done

    # Generate completion files.
    mkdir -p "$pkgdir/usr/share/bash-completion/completions"
    $pkgdir/usr/bin/rustup completions bash > "$pkgdir/usr/share/bash-completion/completions/rustup"
    mkdir -p "$pkgdir/usr/share/fish/completions"
    $pkgdir/usr/bin/rustup completions fish > "$pkgdir/usr/share/fish/completions/rustup.fish"
    mkdir -p "$pkgdir/usr/share/zsh/site-functions"
    $pkgdir/usr/bin/rustup completions zsh > "$pkgdir/usr/share/zsh/site-functions/_rustup"

    install -Dm644 LICENSE-MIT "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE-MIT
    install -Dm644 LICENSE-APACHE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:filetype=sh:
