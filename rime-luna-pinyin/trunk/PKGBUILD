# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-luna-pinyin
pkgver=0.0.0.20200914
_commit=4257e2a6853bc9749fff29a15e8181d2ba7c28c6
pkgrel=1
pkgdesc="Luna pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-luna-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-luna-pinyin/archive/$_commit/rime-luna-pinyin-$_commit.tar.gz")
sha512sums=('48d531e09ee8e0316abc383e50d865cfdbc87fef2cd8a1fb9300fe5f3d4d404421b9956e30cbc1d8175e1bc7730941a5a1c8bfa04a5777bb128b197e4b1a84fe')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
