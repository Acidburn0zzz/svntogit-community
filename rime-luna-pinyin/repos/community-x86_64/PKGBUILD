# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rime-luna-pinyin
pkgver=0.0.0.20210221
_commit=a6dc4418c85abf0453b2c4d77b8611a05a1d5336
pkgrel=1
pkgdesc="Luna pinyin for rime"
arch=('x86_64')
url="https://github.com/rime/rime-luna-pinyin"
license=('LGPL')
# dependency for reverse lookup
depends=('rime-stroke')
makedepends=('librime' 'rime-prelude' 'rime-essay')
source=("https://github.com/rime/rime-luna-pinyin/archive/$_commit/rime-luna-pinyin-$_commit.tar.gz")
sha512sums=('cf5bf379ce3e7db3a54b4a2b55e12ee2929d54f67e925955fc8edfcf71dbfa48d5ccfce3e4a93b0ab1a186d35a603533d126eb21a1fc2e15afb9c6567b2f1c4b')

prepare() {
  cd $pkgname-$_commit
  # Link essentials
  for _f in $(pacman -Qql rime-prelude rime-essay | grep -v "/$"); do ln -sf $_f; done
}

build(){
  cd $pkgname-$_commit
  for _s in $(ls *.schema.yaml); do rime_deployer --compile $_s; done
}

package() {
  cd $pkgname-$_commit
  find . -type l -delete
  rm build/*.txt
  install -Dm644 *.yaml -t "$pkgdir"/usr/share/rime-data/
  install -Dm644 build/* -t "$pkgdir"/usr/share/rime-data/build/
}
