# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: farwayer <farwayer@gmail.com>

pkgname=python-colour
_pyname=colour
pkgver=0.1.5
pkgrel=3
pkgdesc="Colour representations manipulation library (RGB, HSL, web, ...)"
arch=('any')
url="https://github.com/vaab/colour"
license=('BSD')
depends=('python')
makedepends=('git' 'python-setuptools')
_commit=11f138eb7841d2045160b378a2eec0c2321144c0  # tags/0.1.5
source=("git+https://github.com/vaab/colour#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pyname
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd $_pyname
  python setup.py build
}

package() {
  cd $_pyname
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
