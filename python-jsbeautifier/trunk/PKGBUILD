# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=('python-jsbeautifier' 'python-cssbeautifier')
pkgbase=python-jsbeautifier
pkgver=1.13.3
pkgrel=1
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-setuptools' 'python-six' 'python-editorconfig')
source=("https://github.com/beautify-web/js-beautify/archive/v$pkgver/js-beautify-v$pkgver.tar.gz")
sha512sums=('d327d41030441317cc5610251bfe4dc552eee1da940a3a12bbeba46b02f7743ea39f972347e3dcb20ac875ab1f7939495b9442d0e86cff89f53a4f2a3bc9cd48')

prepare() {
  cd js-beautify-$pkgver
  cp -a python python-css
}

build() {
  cd js-beautify-$pkgver/python
  python setup-js.py build
  cd ../python-css
  python setup-css.py build
}

check() {
  cd js-beautify-$pkgver
  python python/js-beautify-test.py
}

package_python-jsbeautifier() {
  pkgdesc='JavaScript unobfuscator and beautifier'
  cd js-beautify-$pkgver/python
  export PYTHONHASHSEED=0
  python setup-js.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[0]}" ../LICENSE
}

package_python-cssbeautifier() {
  pkgdesc='CSS unobfuscator and beautifier'
  depends+=('python-jsbeautifier')
  cd js-beautify-$pkgver/python-css
  export PYTHONHASHSEED=0
  python setup-css.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 -t "$pkgdir"/usr/share/licenses/"${pkgname[1]}" ../LICENSE
}
