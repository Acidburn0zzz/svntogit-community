# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsbeautifier
pkgver=1.10.3
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
depends=('python-six' 'python-editorconfig')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('1187a82d4271fb9ff928def7c4808037864df7b1b67d196c1e09b782e9ce6f622ca854f9fbeb2c2d744ce2eb95f0d5f6fbcd8edb359ea3d38116e8037d6caff3')

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
}

check() {
  cd js-beautify-$pkgver/python
  python setup.py test
}

package() {
  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
