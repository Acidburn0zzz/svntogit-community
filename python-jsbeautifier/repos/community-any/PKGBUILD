# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jsbeautifier
pkgname=('python-jsbeautifier' 'python2-jsbeautifier')
pkgver=1.10.0
pkgrel=1
pkgdesc='JavaScript unobfuscator and beautifier'
arch=('any')
license=('MIT')
url='https://github.com/beautify-web/js-beautify'
makedepends=('python-setuptools' 'python2-setuptools' 'python-editorconfig' 'python2-editorconfig')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/beautify-web/js-beautify/archive/v$pkgver.tar.gz")
sha512sums=('0a5d9f2e5d4701926fb4b6ebab8f9ba22b2ea96945e7cf8ba317e8960611e07a08e9b9b7b5b1b45c43bff6bcb53fef7f90b38dff0a5b70f43c89bbce68d1c9f5')

build() {
  cd js-beautify-$pkgver/python
  python setup.py build
  python2 setup.py build
}

check() {
  cd js-beautify-$pkgver/python
  python setup.py test || warning "Broken tests"
  python2 setup.py test || warning "Broken tests"
}

package_python-jsbeautifier() {
  depends=('python-six' 'python-editorconfig')

  cd js-beautify-$pkgver/python
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jsbeautifier() {
  depends=('python2-six' 'python2-editorconfig')

  cd js-beautify-$pkgver/python
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/js-beautify{,2}
}

# vim:set ts=2 sw=2 et:
