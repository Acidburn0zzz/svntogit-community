# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jake
pkgver=10.4.2
pkgrel=1
pkgdesc="JavaScript build tool, similar to Make or Rake"
arch=('any')
url="https://jakejs.com"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('b083ea980a873451ffae5b645c11666c615a4993bc4ed79d6b9be2a0681fdfc42bb144fd45505a150e757427c85f625f291c9ed2a778d77778bd8498ee02783b')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
