# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=jake
pkgver=10.4.5
pkgrel=1
pkgdesc="JavaScript build tool, similar to Make or Rake"
arch=('any')
url="https://jakejs.com"
license=('Apache')
depends=('nodejs')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('0b79587bafad4e4f262eadda0fea79f641d38e74605bc53903ab4c0502ae83408c6cb67fb0b9c0c1c1fd4831c5647fc3f62adb79d6e4bea0866916e4189fd006')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
