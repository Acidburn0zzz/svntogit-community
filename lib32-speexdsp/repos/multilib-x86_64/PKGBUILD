# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-speexdsp
pkgver=1.2rc3
pkgrel=4
pkgdesc="DSP library derived from Speex (32-bit)"
arch=(x86_64)
url="https://www.speex.org/"
license=(BSD)
depends=(lib32-glibc speexdsp)
source=(https://downloads.us.xiph.org/releases/speex/speexdsp-$pkgver.tar.gz
        speexdsp-fixbuilds-774c87d.patch)
sha512sums=('29dfa8345df025eeb076561648a9b5c0485692be699b6da3c2a3734b4329187a1c2eb181252f4df12b21f1309ecdf59797437dfb123d160fd723491ab216e858'
            '0f0c8054591915a4ea06b28b13d50da41be10d594f39a0496e2e80e4514ed53db7cd0e1fdb205ce5e87f602b9c1e15409c33cf946ab25cdef8a3b147dbfcc0f3')

prepare() {
  cd speexdsp-$pkgver
  patch -Np1 -i ../speexdsp-fixbuilds-774c87d.patch
  autoreconf -vi
}

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd speexdsp-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --disable-static \
    --libdir=/usr/lib32
  make
}

check() {
  cd speexdsp-$pkgver
  make -k check
}

package() {
  cd speexdsp-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{include,share}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s speexdsp "$pkgdir/usr/share/licenses/$pkgname"
}
