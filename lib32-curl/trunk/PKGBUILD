# $Id$
# Maintainer: Daniel Wallace <danielwallace@aur.archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lib32-curl
_pkgname=curl
pkgver=7.52.1
pkgrel=1
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('lib32-libssh2' 'lib32-krb5' 'lib32-libpsl' 'lib32-zlib' "${_pkgname}")
source=("https://curl.haxx.se/download/$_pkgname-$pkgver.tar.gz"{,.asc}
        0001-vtls-s-SSLEAY-OPENSSL.patch)
sha512sums=('414c3daa2754172f6fb645b3669d15f621fd248488d88666312963275c19c70d2b0c3b3bcb8d8aa8db6d504a59cb846dd2003ec38080d06caba6dfa8eb2d52ed'
            'SKIP'
            'fb094c61e63a2de9e573c7bcf79d5af04dbaaf136eb0da84c4ddb81a233505c8f20d4761e7f518b8859e176206bd35b252476d00e45291e3e626e43e4dac4a6d')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd curl-${pkgver}
  patch -p1 < "${srcdir}/0001-vtls-s-SSLEAY-OPENSSL.patch" # FS#52287
}

build() {
  cd "$_pkgname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
      --libdir=/usr/lib32

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{share,bin}

  # license
  install -d "$pkgdir/usr/share/licenses"
  ln -s "$_pkgname" "$pkgdir/usr/share/licenses/$pkgname"

  # devel
  find "${pkgdir}/usr/include/curl" -type f -not -name curlbuild.h -delete
  mv "$pkgdir/usr/include/curl/curlbuild.h" "$pkgdir/usr/include/$_pkgname/curlbuild-32.h"
}
