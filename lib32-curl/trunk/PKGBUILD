# $Id$
# Maintainer: Florian Pritz <flo@xssn.at>

_pkgbasename=curl
pkgname=lib32-$_pkgbasename
pkgver=7.23.1
pkgrel=1
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('x86_64')
url="http://curl.haxx.se"
license=('MIT')
depends=('lib32-zlib' 'lib32-openssl' 'bash' 'ca-certificates' 'lib32-libssh2' $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool')
source=("http://curl.haxx.se/download/${_pkgbasename}-${pkgver}.tar.bz2")
md5sums=('0296d3196b4bf82c896a869b38dbc5f2')


build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/${_pkgbasename}-${pkgver}

  ./configure \
      --with-random=/dev/urandom \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --enable-ipv6 \
      --disable-ldaps \
      --disable-ldap \
      --disable-manual \
      --enable-versioned-symbols \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
      --with-random=/dev/urandom \
      --without-libidn \
      --libdir=/usr/lib32 \
      --enable-threaded-resolver
  make
}

package() {
  cd ${srcdir}/${_pkgbasename}-${pkgver}

  make DESTDIR=${pkgdir} install

  rm -rf "${pkgdir}"/usr/{share,bin}
  #we need curlbuild.h
  find ${pkgdir}/usr/include/curl -type f -not -name curlbuild.h -delete
  #now save it as curlbuild-32.h
  mv ${pkgdir}/usr/include/curl/curlbuild.h ${pkgdir}/usr/include/curl/curlbuild-32.h

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
