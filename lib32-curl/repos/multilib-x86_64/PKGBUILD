# $Id$
# Maintainer: Daniel Wallace <danielwallace@aur.archlinux.org>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=lib32-curl
_pkgname=curl
pkgver=7.54.1
pkgrel=2
pkgdesc="An URL retrieval utility and library (32-bit)"
arch=('x86_64')
url="https://curl.haxx.se"
license=('MIT')
depends=('lib32-libssh2' 'lib32-krb5' 'lib32-libpsl' 'lib32-zlib' "${_pkgname}")
source=("https://curl.haxx.se/download/$_pkgname-$pkgver.tar.gz"{,.asc}
        includes-remove-curl-curlbuild.h-and-curl-curlrules.patch)
sha512sums=('59c236f46e13faf9124cecab991b139e57376b1b5a5db13005f55ffece67191f0f7ecb954ae14e382a561d34f8b652b532755746d0cda79713b56857833951c5'
            'SKIP'
            'd18b92c4998ca1490c2946fae123e665e8cd25419a53d9ddcd1eb95d95db9eb16757989ec65e8590d60eec249c185450f1e0537acd508f140f1851896185cc39')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2'   # Daniel Stenberg
              '914C533DF9B2ADA2204F586D78E11C6B279D5C91')  # Daniel Stenberg (old key)

prepare() {
  cd "$_pkgname-$pkgver"

  # Fix FS#53862 , merged upstream
  patch -Np1 -i ../includes-remove-curl-curlbuild.h-and-curl-curlrules.patch

  autoreconf -fiv
}

build() {
  cd "$_pkgname-$pkgver"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
      --prefix=/usr \
      --mandir=/usr/share/man \
      --disable-dependency-tracking \
      --disable-ldap \
      --disable-ldaps \
      --enable-ipv6 \
      --enable-manual \
      --enable-versioned-symbols \
      --enable-threaded-resolver \
      --with-gssapi \
      --with-random=/dev/urandom \
      --with-ca-bundle=/etc/ssl/certs/ca-certificates.crt \
      --libdir=/usr/lib32

  make
}

package() {
  cd "$_pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  rm -rf "${pkgdir}"/usr/{share,bin,include}

  # license
  install -d "$pkgdir/usr/share/licenses"
  ln -s "$_pkgname" "$pkgdir/usr/share/licenses/$pkgname"
}
