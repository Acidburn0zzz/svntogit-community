# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=drumstick
pkgver=1.1.3
pkgrel=1
pkgdesc="MIDI libraries for Qt5/C++"
arch=('x86_64')
url="http://drumstick.sourceforge.net/"
license=('GPL2')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'libpulse' 'qt5-svg')
makedepends=('cmake' 'doxygen' 'docbook-xsl')
provides=('libdrumstick-rt-alsa-in.so' 'libdrumstick-rt-alsa-out.so'
'libdrumstick-rt-easysynth.so' 'libdrumstick-rt-net-in.so'
'libdrumstick-rt-net-out.so' 'libdrumstick-rt-oss-in.so'
'libdrumstick-rt-oss-out.so')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('8831578a6422cbf6570fd0ff36836f808d452cd7cc338de82cc852d804c5ef404d97dfd73760b13f16fe33007b35a5fc4591f2c4d719a4cf2b6a34fa6ca81a8b')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DLIB_SUFFIX=
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,ChangeLog,NEWS,README,TODO}
}
