# Maintainer: Jiachen Yang <farseerfc@archlinux.org>
# Contributor: Andreas Bilke <andreas@bilke>

pkgname=pdfpc
pkgver=4.2.1
_pkgver=$pkgver
pkgrel=3
pkgdesc='A presenter console with multi-monitor support for PDF files'
arch=('x86_64')
url='https://pdfpc.github.io/'
license=('GPL')
depends=('gtk3' 'poppler-glib' 'libgee' 'gstreamer' 'gst-plugins-base')
optdepends=('gst-plugins-good: more codecs for video playback support'
            'gst-libav: more codecs for video playback support')
makedepends=('cmake' 'vala')
source=("$pkgname-$_pkgver.tar.gz::https://github.com/pdfpc/pdfpc/archive/v$_pkgver.tar.gz"
        "issue321.patch::https://github.com/pdfpc/pdfpc/compare/v4.2.1...42b436a89971066603bc5e6161278173913ca785.patch"
        "issue390.patch::https://github.com/pdfpc/pdfpc/commit/e0dfb5120869715739c994cbbee8cdb6c44430a4.patch")

sha256sums=('f67eedf092a9bc275dde312f3166063a2e88569f030839efc211127245be6df8'
            'f6b16b3d0cf37352d9333651980328f77bbff2783cbb67bfa919c4bebab5870a'
            '206c37d86a650bed48da3a69024550e4b2854d20327f593981555c637b9cdc0e')

prepare() {
    cd "$srcdir/$pkgname-$_pkgver"
    patch -Np1 -i ../issue321.patch
    patch -Np1 -i ../issue390.patch
}

build() {
    cd "$srcdir/$pkgname-$_pkgver"
    cmake  -DCMAKE_INSTALL_PREFIX="/usr/" -DSYSCONFDIR="/etc" .
    make
}

package() {
    cd "$srcdir/$pkgname-$_pkgver"
    make DESTDIR="$pkgdir/" install
}

# vim:set ts=4 sw=4 et:
