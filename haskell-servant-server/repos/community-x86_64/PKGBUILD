# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant-server
pkgname=haskell-servant-server
pkgver=0.14.1
pkgrel=12
pkgdesc="A family of combinators for defining webservices APIs and serving them"
url="http://haskell-servant.readthedocs.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-aeson' 'haskell-base64-bytestring'
         'haskell-exceptions' 'haskell-http-api-data' 'haskell-http-media' 'haskell-http-types'
         'haskell-network-uri' 'haskell-monad-control' 'haskell-network' 'haskell-servant'
         'haskell-string-conversions' 'haskell-resourcet' 'haskell-tagged'
         'haskell-transformers-base' 'haskell-transformers-compat' 'haskell-wai'
         'haskell-wai-app-static' 'haskell-warp' 'haskell-word8')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hspec' 'haskell-hspec-wai'
             'haskell-should-not-typecheck' 'haskell-quickcheck' 'haskell-safe' 'haskell-wai-extra'
             'haskell-temporary')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('6a22b6c60ff79363df740f48fed89f2bed264b207051dbe83a5f347b67359519c98df7769eceb03363c9ca354e858ff51b7b3c2026d173e311d756a0fe68344a')

prepare() {
    cd $_hkgname-$pkgver
    sed -i -e 's/< *4.12/<5/' -e 's/< *0.6/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
