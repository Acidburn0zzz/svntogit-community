# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=servant-server
pkgname=haskell-servant-server
pkgver=0.16.2
pkgrel=59
pkgdesc="A family of combinators for defining webservices APIs and serving them"
url="https://haskell-servant.readthedocs.org/"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-compat' 'haskell-aeson' 'haskell-base64-bytestring'
         'haskell-exceptions' 'haskell-http-api-data' 'haskell-http-media' 'haskell-http-types'
         'haskell-network-uri' 'haskell-monad-control' 'haskell-network' 'haskell-servant'
         'haskell-string-conversions' 'haskell-resourcet' 'haskell-tagged'
         'haskell-transformers-base' 'haskell-wai' 'haskell-wai-app-static' 'haskell-warp'
         'haskell-word8')
makedepends=('ghc' 'haskell-cabal-doctest' 'haskell-doctest' 'haskell-hspec' 'haskell-hspec-wai'
             'haskell-should-not-typecheck' 'haskell-quickcheck' 'haskell-safe' 'haskell-wai-extra'
             'haskell-temporary' 'haskell-transformers-compat')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('e3bf75e6fb6422e30d7851be7b07f33509f37d9c9c28464c20b3a1fd9cea9b7dd7c328b54438efabf42beecdf0f08fee174be526742c8915ad035fef701ca871')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/< *0.11/<1/' $_hkgname.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
