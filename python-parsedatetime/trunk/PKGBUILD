# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgbase=python-parsedatetime
pkgname=('python-parsedatetime' 'python2-parsedatetime')
pkgver=3.0
pkgrel=1
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='http://github.com/bear/parsedatetime'
makedepends=('python-future' 'python2-future' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/bear/parsedatetime/archive/v$pkgver.tar.gz")
sha512sums=('9512b80b2ec100d9790ee26564bad5760a29ea0bfa4c1e807a39b496e17712761fc01664cd5be892e55157c9a909d96466f3e68bb2929f001ab70317482ef955')

prepare() {
  cp -a parsedatetime-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/parsedatetime-$pkgver
  python setup.py build

  cd "$srcdir"/parsedatetime-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsedatetime-$pkgver
  python setup.py pytest

  cd "$srcdir"/parsedatetime-$pkgver-py2
  python2 setup.py pytest
}

package_python-parsedatetime() {
  depends=('python-future')

  cd parsedatetime-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-parsedatetime() {
  depends=('python2-future')

  cd parsedatetime-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
