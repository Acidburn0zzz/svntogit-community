# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Daniel M. Capella <polycitizen@gmail.com>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Jeremy Sands <cto@jeremysands.com>

pkgbase=python-parsedatetime
pkgname=('python-parsedatetime' 'python2-parsedatetime')
pkgver=2.3
pkgrel=1
pkgdesc="Parse human-readable date/time strings."
arch=('any')
license=('Apache')
url='http://github.com/bear/parsedatetime'
makedepends=('python-pytest-runner' 'python2-pytest-runner' 'python-future' 'python2-future' 'git')
source=("git+https://github.com/bear/parsedatetime.git#tag=v$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a parsedatetime{,-py2}
}

build() {
  cd "$srcdir"/parsedatetime
  python setup.py build

  cd "$srcdir"/parsedatetime-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/parsedatetime
  python setup.py pytest

  cd "$srcdir"/parsedatetime-py2
  python2 setup.py pytest
}

package_python-parsedatetime() {
  depends=('python-future')

  cd parsedatetime
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-parsedatetime() {
  depends=('python2-future')

  cd parsedatetime-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
