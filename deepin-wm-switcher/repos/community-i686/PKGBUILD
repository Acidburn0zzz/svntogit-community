# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-wm-switcher
pkgver=1.1.3
pkgrel=1
pkgdesc='Window manager switcher for Deepin'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-wm-switcher"
license=('GPL3')
groups=('deepin')
depends=('deepin-daemon' 'deepin-wm' 'deepin-metacity' 'qt5-x11extras')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm-switcher/archive/$pkgver.tar.gz")
sha512sums=('be9a495949f9a55e71904538405c8faa2260b2ecc04f4fb5b0d03c7c058024cfff1b72e271a281eff6750e071858a04777c073d2fe1e9492ba0ca63d28c18618')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
