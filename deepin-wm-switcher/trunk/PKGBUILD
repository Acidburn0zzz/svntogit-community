# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-wm-switcher
pkgver=1.1.8
pkgrel=1
pkgdesc='Window manager switcher for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-wm-switcher"
license=('GPL3')
groups=('deepin')
depends=('deepin-daemon' 'deepin-wm' 'deepin-metacity' 'qt5-x11extras')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm-switcher/archive/$pkgver.tar.gz")
sha512sums=('22f449421a5c61fc9dc110a1701f70c2a836865bebc3ddef21902692b64dff751564e185beb9964e131cfbeacbdc29eea7758934f7c441b62b36daf5eaa06791')

prepare() {
  mkdir build
}

build() {
  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release ../$pkgname-$pkgver
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
