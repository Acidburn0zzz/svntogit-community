# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Hilton Medeiros <medeiros.hilton@gmail.com>
# Contributor: Dave Reisner <d@falconindy.com>

pkgname=libgit2
pkgver=0.18.0
pkgrel=1
pkgdesc="A linkable library for Git"
arch=('i686' 'x86_64')
url="http://libgit2.github.com/"
depends=(zlib openssl)
makedepends=(cmake python)
license=('GPL2')
source=("src-$pkgname-$pkgver.tar.gz::https://github.com/$pkgname/$pkgname/tarball/v$pkgver")
md5sums=('50409ddb0c34713677b33ef617e92c94')

build() {
  cd $pkgname-*
  export LANG=en_US.UTF-8
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd $pkgname-*
  make test
}

package() {
  cd $pkgname-*
  make DESTDIR="$pkgdir" install
}
