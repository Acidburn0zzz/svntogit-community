# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libiio
pkgver=0.19
pkgrel=2
pkgdesc="Interface to the Linux Industrial Input/Output (IIO) Subsystem"
arch=('x86_64')
url="https://github.com/analogdevicesinc/libiio"
license=('LGPL2.1')
depends=('glibc' 'libaio' 'libserialport' 'libusb-1.0.so' 'libxml2')
optdepends=('python: Python bindings')
makedepends=('cmake' 'python')
provides=('libiio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/analogdevicesinc/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c904f517f95919caeedc0d1ddf80003233df2296364771216ef853d3b92f273ae7cdf4e7f972fa88f1483b25e762f749efe5117fa3f2b026b3adc5c5ab403b0a')

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_SBINDIR=bin \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DSYSTEMD_UNIT_INSTALL_DIR=/usr/lib/systemd/system \
        -DWITH_SYSTEMD=ON \
        -DPYTHON_BINDINGS=ON \
        -Bbuild \
        .
  make -C build VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
}
