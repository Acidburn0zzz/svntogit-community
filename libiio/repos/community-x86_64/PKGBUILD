# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libiio
pkgver=0.20
pkgrel=1
pkgdesc="Interface to the Linux Industrial Input/Output (IIO) Subsystem"
arch=('x86_64')
url="https://github.com/analogdevicesinc/libiio"
license=('LGPL2.1')
depends=('glibc' 'libaio' 'libserialport' 'libusb-1.0.so' 'libxml2')
optdepends=('python: Python bindings')
makedepends=('cmake' 'python' 'python-setuptools')
provides=('libiio.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/analogdevicesinc/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('2b6ef9f67642db4b06ba33f5c5b7b3fa3043c8963185c138e9946e01aaa790ad1453f3f522af18c4526da2b087752ec686228705d1a312e6b99146ded38b943e')

prepare() {
  sed -i '/self._check_libiio_installed()/d' "$pkgname-$pkgver"/bindings/python/setup.py.cmakein
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_SBINDIR=bin \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DUDEV_RULES_INSTALL_DIR=/usr/lib/udev/rules.d \
        -DSYSTEMD_UNIT_INSTALL_DIR=/usr/lib/systemd/system \
        -DWITH_SYSTEMD=ON \
        -DPYTHON_BINDINGS=ON \
        -Bbuild \
        .
  make -C build VERBOSE=1
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" -C build install
}
