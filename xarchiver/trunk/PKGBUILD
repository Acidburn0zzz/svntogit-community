# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: Andrew Simmons <andrew.simmons@gmail.com>

pkgbase=xarchiver
pkgname=(xarchiver xarchiver-gtk2)
pkgver=0.5.4.8
pkgrel=1
pkgdesc="GTK+ frontend to various command line archivers"
arch=('i686' 'x86_64')
url="https://github.com/ib/xarchiver"
license=('GPL')
depends=('gtk2' 'gtk3')
makedepends=('docbook-xsl' 'intltool' 'xmlto')
optdepends=('zip: ZIP support'
            'unzip: ZIP support'
            'p7zip: 7z support'
            'lzop: LZOP support'
            'cpio: RPM support'
            'unrar: RAR support')
source=(https://github.com/ib/xarchiver/archive/$pkgbase-$pkgver.tar.gz)
sha256sums=('ff6922aee9ee92035779a56d89958d3d19ac5343adc8d1defd65d512b6f50dd2')

build() {
  # GTK+ 3 version
  [ -d gtk3 ] || cp -r $pkgbase-$pkgbase-$pkgver gtk3
  cd gtk3
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4
  make -j1

  cd "$srcdir"
  # GTK+ 2 version
  [ -d gtk2 ] || cp -r $pkgbase-$pkgbase-$pkgver gtk2
  cd gtk2
  ./configure --prefix=/usr --libexecdir=/usr/lib/xfce4 --enable-gtk2
  make -j1
}

package_xarchiver() {
  depends=('gtk3')

  cd gtk3
  make DESTDIR="$pkgdir" install
}

package_xarchiver-gtk2() {
  pkgdesc+=' (GTK+ 2 version)'
  depends=('gtk2')
  conflicts=('xarchiver')

  cd gtk2
  make DESTDIR="$pkgdir" install
}
