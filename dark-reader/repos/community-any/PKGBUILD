# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=('dark-reader' 'firefox-dark-reader' 'thunderbird-dark-reader')
pkgbase=dark-reader
pkgver=4.9.44
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
arch=('any')
url=https://darkreader.org/
license=('MIT')
makedepends=('npm' 'strip-nondeterminism')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-$pkgver.tar.gz")
b2sums=('bc473ffb6c17ff76364a2f5a60170193019955dacae3e997dd354baa68952820d03636d97c9d3c5f45090600c53f7eec4129e82bcc2a0532f1b23ea7aa70657d')

prepare() {
  cd darkreader-$pkgver
  npm ci
}

build() {
  cd darkreader-$pkgver
  npm run build
  strip-nondeterminism -t zip build/release/*.xpi
}

check() {
  cd darkreader-$pkgver
  npm test
}

package_dark-reader() {
  pkgdesc+=' (unpacked webextension)'
  cd darkreader-$pkgver/build/release/chrome
  install -d "$pkgdir"/usr/lib/$pkgbase
  cp -r -- * "$pkgdir"/usr/lib/$pkgbase
}

package_firefox-dark-reader() {
  groups=('firefox-addons')
  cd darkreader-$pkgver/build/release
  install -Dm644 darkreader-firefox.xpi \
    "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
}

package_thunderbird-dark-reader() {
  cd darkreader-$pkgver/build/release
  install -Dm644 darkreader-thunderbird.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
