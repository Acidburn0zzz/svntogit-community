# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pathod
pkgver=0.16
pkgrel=1
pkgdesc="A pathological web daemon"
url="http://pathod.net"
depends=('python2-netlib' 'python2-requests' 'python2-flask' 'python2-pyparsing')
checkdepends=('python2-mock' 'python2-pytest-runner')
arch=('any')
license=('MIT')
source=("https://pypi.python.org/packages/source/p/pathod/pathod-$pkgver.tar.gz")
sha256sums=('e7e1f7f377ee00d33437386fb31914d83548a5f813e877506b91017b3f90b6bf')

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

check() {
  cd $pkgname-$pkgver
  python2 setup.py ptr
}

package() {
  cd $pkgname-$pkgver  
  python2 setup.py install -O1 --skip-build --root="$pkgdir"

  install -Dm 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
