# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-aiobotocore
_pkgname=aiobotocore
pkgver=1.0.2
pkgrel=1
pkgdesc='asyncio support for botocore library using aiohttp'
arch=(any)
url='https://github.com/aio-libs/aiobotocore'
license=(Apache)
depends=(python python-aiohttp python-botocore python-wrapt python-aioitertools)
checkdepends=(python-flask python-moto python-pytest python-pytest-asyncio)
source=("https://github.com/aio-libs/aiobotocore/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        unittest-mock.diff)
sha256sums=('0f8c3fab20f18fce65eacf547a144c2d18953006dec89b9ff13b0f343a5e91dd'
            'ffb6b7e922632f4c2de5c93f61ab53e1730cfe151b0a2dde5dfce6260405866e')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../unittest-mock.diff

  # Disable dependency pinning
  # Upstream tracking issue: https://github.com/aio-libs/aiobotocore/issues/670
  sed --in-place=.orig -r "s#'(botocore.*),<.*',#'\1',#" setup.py
  diff -u setup.py{.orig,} || true
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  PYTHONPATH="$PWD/build/lib:$PWD" pytest -v -m moto tests --ignore=tests/test_patches.py
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
