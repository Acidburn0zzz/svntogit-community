# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.305
_portal_pkgver=0.9.5
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'meson' 'python')
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
source=("https://gitlab.gnome.org/gnumdk/lollypop/uploads/6d0bae054101608108339802db583fb9/lollypop-${pkgver}.tar.xz"
        "https://gitlab.gnome.org/gnumdk/lollypop-portal/uploads/53e3fbe2d27328fc2a7b9e4073e9500b/lollypop-portal-${_portal_pkgver}.tar.xz")
sha256sums=('8b8c5a3c5d8ee0a5874b6c93fa5ce0346b8e67b9382ee06d4381fab5563f3c90'
            '0997150312aaf576dfe625f1e7cafcfe82ad4927d7e0b3e463a9da70ae0d7133')

build() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    pushd $d
    meson \
      --buildtype='release' \
      --prefix='/usr' \
      --libexecdir='lib/lollypop' \
      build
    ninja -C build
    popd
  done
}

package() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    DESTDIR="${pkgdir}" ninja -C $d/build install
  done
}

# vim: ts=2 sw=2 et:
