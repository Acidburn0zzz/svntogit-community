# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.514
_portal_pkgver=0.9.7
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://gitlab.gnome.org/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-plugins-base-libs' 'gtk3' 'python-beautifulsoup4' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'meson' 'python')
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'gst-plugins-bad: "Bad" plugin libraries'
            'gst-plugins-base: "Base" plugin libraries'
            'gst-plugins-good: "Good" plugin libraries'
            'gst-plugins-ugly: "Ugly" plugin libraries'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pillow: Blur background in lyrics view'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support')
source=("https://gitlab.gnome.org/gnumdk/lollypop/uploads/fc731e0878cea850ebadb46afd69b3b8/lollypop-0.9.514.tar.xz"
        "https://gitlab.gnome.org/gnumdk/lollypop-portal/uploads/42bd030f9c8391db93a335c6a23783bf/lollypop-portal-0.9.7.tar.xz")
sha256sums=('b9597979372a54445e8317e417331557302d212775a9d528593c568dc8c91803'
            'fef0d7b5a4065884582fef6e6e40ae781fe182063a07cfcc5d98a69602f4d57b')

build() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    pushd $d
    arch-meson build \
      --libexecdir='lib/lollypop'
    ninja -C build
    popd
  done
}

package() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    DESTDIR="${pkgdir}" ninja -C $d/build install
  done
}

# vim: ts=2 sw=2 et:
