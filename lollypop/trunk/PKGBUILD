# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.403
_portal_pkgver=0.9.7
pkgrel=2
pkgdesc='Music player for GNOME'
arch=('any')
url='https://gitlab.gnome.org/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'meson' 'python')
optdepends=('easytag: Modify tags'
            'flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
source=("https://gitlab.gnome.org/gnumdk/lollypop/uploads/a020c27371e3ee09de4951e7ed7297f2/lollypop-0.9.403.tar.xz"
        "https://gitlab.gnome.org/gnumdk/lollypop-portal/uploads/42bd030f9c8391db93a335c6a23783bf/lollypop-portal-0.9.7.tar.xz")
sha256sums=('5d24a913c53981ff55e2e7f32d185a95fd7cd6ea6d2b8d4f4c039be732570e18'
            'fef0d7b5a4065884582fef6e6e40ae781fe182063a07cfcc5d98a69602f4d57b')

build() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    pushd $d
    arch-meson build \
      --libexecdir='lib/lollypop'
    ninja -C build
    popd
  done
}

package() {
  for d in lollypop-portal-${_portal_pkgver} lollypop-${pkgver}; do
    DESTDIR="${pkgdir}" ninja -C $d/build install
  done
}

# vim: ts=2 sw=2 et:
