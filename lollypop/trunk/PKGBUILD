# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: kirek <rbuldo@gmail.com>
# Contributor: CÃ©dric Bellegarde <gnumdk@gmail.com>

pkgname=lollypop
pkgver=0.9.224
pkgrel=1
pkgdesc='Music player for GNOME'
arch=('any')
url='https://github.com/gnumdk/lollypop'
license=('GPL')
depends=('desktop-file-utils' 'gst-python' 'gtk3' 'python-cairo'
         'python-dbus' 'python-gobject' 'totem-plparser')
makedepends=('git' 'gobject-introspection' 'intltool' 'itstool' 'python')
optdepends=('flatpak: Flatpak Portal'
            'gst-libav: FFmpeg plugin for GStreamer'
            'kid3-qt: Store covers in tags'
            'libsecret: Last.FM support'
            'python-pylast: Last.FM support'
            'python-wikipedia: Wikipedia support'
            'youtube-dl: YouTube playback')
_commit='8f3fec20c99498a48f33df00eb64d5af20f81a38'
source=("https://github.com/gnumdk/lollypop/releases/download/${pkgver}/lollypop-${pkgver}.tar.xz"
        "git+https://github.com/gnumdk/lollypop-portal.git#commit=${_commit}")
sha256sums=('11b598453c4b508a5bd52aecc9f4c416bd0c2f2b7dc796a02da086eb462bac74'
            'SKIP')

build() {
  cd lollypop-portal

  ./autogen.sh \
    --prefix='/usr'
  make

  cd ../lollypop-${pkgver}

  ./configure \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd lollypop-portal

  make DESTDIR="${pkgdir}" install

  cd ../lollypop-${pkgver}

  make DESTDIR="${pkgdir}" install

  chmod +x "${pkgdir}"/usr/share/{lollypop/lollypop-sp,lollypop-portal/lollypop-portal}
}

# vim: ts=2 sw=2 et:
