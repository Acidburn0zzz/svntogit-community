# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=userpath
pkgname=python-$_pkgname
pkgver=1.5.0
pkgrel=1
pkgdesc='Cross-platform tool for adding locations to the user PATH, no elevated privileges required!'
arch=('any')
url='https://github.com/ofek/userpath'
license=('MIT' 'Apache')
depends=('python' 'python-click' 'python-distro')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('9d1cf71ef95f6eba7a14ae77dea5bc993868b5e3655cd993bd0bead1b69fd41fc84429fc31585021026c8a18377b74bc2a9a2d5654bbd57c05063cda21e43d2a')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
}

# vim:set ts=2 sw=2 et:
