# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Larry Hajali <larryhaja [at] gmail [dot] com>
 
pkgname=megaglest
pkgver=3.5.0
pkgrel=2
pkgdesc="Fork of Glest, a 3D real-time strategy game in a fantastic world."
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/megaglest/"
license=('custom:CCBYSA3.0')
depends=('curl' 'megaglest-data' 'xerces-c' 'sdl' 'libvorbis' 'openal' 'mesa' 'lua' 'icu')
makedepends=('ftjam' 'p7zip' 'wxgtk' 'cmake')
source=("http://downloads.sourceforge.net/project/${pkgname}/current_release/${pkgname}-source-${pkgver}.tar.bz2"
        "megaglest.sh")
md5sums=('03cb7e67be3e72ac0147b23c6b91eb2a'
         'e3d6abbe79263d2fa5edb0bcbcf8755a')

build() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  [[ -d build ]] && rm -r build
  mkdir build && cd build
  cmake .. \
   -DWANT_SVN_STAMP=OFF \
 	 -DCMAKE_INSTALL_PREFIX=/usr \
   -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version
  make
}

package() {
  cd ${srcdir}/${pkgname}-source-${pkgver}/

  # Megaglest doesn't have a nice installer. Let's package it ourselves. 
  # Damnit, upstream.
  install -d -m 0755 "${pkgdir}"/usr/{bin,share/${pkgname}}
  install -m 0755 "${srcdir}"/$pkgname.sh "${pkgdir}"/usr/bin/$pkgname
  install -m 0755 mk/linux/megaglest.bin "${pkgdir}"/usr/share/${pkgname}/$pkgname
  install -m 0755 mk/linux/megaglest_configurator "${pkgdir}"/usr/bin/megaglest_configurator
  install -m 0755 mk/linux/megaglest_editor "${pkgdir}"/usr/bin/megaglest_editor
  install -m 0644 glest.ini glestkeys.ini servers.ini "${pkgdir}"/usr/share/$pkgname

  sed -i s/Exec=.*/Exec=${pkgname}/g ${pkgname}.desktop
  sed -i s/Icon=.*/Icon=${pkgname}/g ${pkgname}.desktop
  install -D -m 0644 "${pkgname}.desktop" "${pkgdir}"/usr/share/applications/${pkgname}.desktop
  install -D -m 0644 "${pkgname}.png" "${pkgdir}"/usr/share/pixmaps/${pkgname}.png
}

# vim: sw=2:ts=2 et:
