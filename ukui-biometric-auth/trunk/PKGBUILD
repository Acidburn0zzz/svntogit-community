# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-biometric-auth
pkgver=1.2.2
pkgrel=1
pkgdesc="Biometric authentication for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-biometric-auth"
groups=('ukui')
depends=('opencv' 'polkit-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-biometric-auth/archive/v$pkgver.tar.gz")
sha512sums=('452113359e6d1b1f93bddef353379c0864aa577c1d40231fc22998730df564c2e1c7e30ae9d7d0cb8f052a8bebfe84278f13b3f81ee20c023633d680c292b3df')

prepare() {
  mkdir -p build
  sed -i 's|qt5/QtWidgets/||' $pkgname-$pkgver/polkit-agent/src/PolkitListener.cpp
  sed -i 's|/lib/security|/usr/lib/security|' $pkgname-$pkgver/pam-biometric/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -GNinja -DCMAKE_INSTALL_PREFIX=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
