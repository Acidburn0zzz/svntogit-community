# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-biometric-auth
pkgver=1.2.0
pkgrel=1
pkgdesc="Biometric authentication for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-biometric-auth"
groups=('ukui')
depends=('polkit-qt5')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-biometric-auth/archive/v$pkgver.tar.gz")
sha512sums=('bb32283b5f503d7014c0a730389a6d6fb35eb18555a7580b3e92b3ee2c9e21b22f793535f95bb29d139109bec081142cc9ae5e63a609fe2f8b48fa88ae27da03')

prepare() {
  mkdir -p build
  sed -i 's|qt5/QtWidgets/||' $pkgname-$pkgver/polkit-agent/src/PolkitListener.cpp
  sed -i 's|/lib/security|/usr/lib/security|' $pkgname-$pkgver/pam-biometric/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
