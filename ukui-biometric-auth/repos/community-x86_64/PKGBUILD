# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-biometric-auth
pkgver=1.0.4
pkgrel=1
pkgdesc="Biometric authentication for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-biometric-auth"
groups=('ukui')
depends=('polkit-qt5')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-biometric-auth/archive/v$pkgver.tar.gz")
sha512sums=('9a1fd56acefeb79e27250194e71e60733d2dc0e577389857f596bad49fce179a0ba37174a9e62b31dc78612e7fc18306349b8b9cf998a2ce4f0df3096ccb7b2e')

prepare() {
  mkdir -p build
  sed -i 's|qt5/QtWidgets/||' $pkgname-$pkgver/polkit-agent/src/PolkitListener.cpp
  sed -i 's|/lib/security|/usr/lib/security|' $pkgname-$pkgver/pam-biometric/CMakeLists.txt
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
