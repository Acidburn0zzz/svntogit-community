# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=1.9.11
pkgrel=1
pkgdesc="An enhanced gettext po file editor for the GNOME"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/gtranslator/"
license=('GPL')
depends=('gdl' 'gettext' 'gtksourceview2' 'gtkspell' 'hicolor-icon-theme' 'libgda' 'libglade' 'libgnomeui')
makedepends=('intltool>=0.40' 'gnome-doc-utils')
install=$pkgname.install
options=('!libtool')
changelog=$pkgname.changelog
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/1.9/$pkgname-$pkgver.tar.bz2)
md5sums=('c24727599a57f30be87c6267199f5c81')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-install
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/gconf/schemas
  gconf-merge-schema ${pkgdir}/usr/share/gconf/schemas/${pkgname}.schemas \
    --domain ${pkgname} ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -rf ${pkgdir}/etc
}
