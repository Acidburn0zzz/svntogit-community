# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=1.9.13
pkgrel=1
pkgdesc="An enhanced gettext po file editor for the GNOME"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gtranslator/"
license=('GPL')
depends=('gdl' 'gettext' 'gtksourceview2' 'gtkspell' 'libgda' 'libunique' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'dconf')
makedepends=('intltool' 'gnome-doc-utils' 'gnome-utils' 'gucharmap' 'json-glib')
optdepends=('gtkspell: spell check'
            'gucharmap: for charmap plugin'
            'json-glib: for open-tran plugin')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/1.9/$pkgname-$pkgver.tar.bz2)
sha256sums=('c0b371b81630cf2e4efb6968c20f7580c62c34d9161c852a5d008622cd91408e')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
