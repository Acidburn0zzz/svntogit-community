# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Christer Solskogen (solskogen@carebears.mine.nu)

pkgname=gtranslator
pkgver=2.91.6
pkgrel=3
pkgdesc="An enhanced gettext po file editor for the GNOME"
arch=('i686' 'x86_64')
url="http://projects.gnome.org/gtranslator/"
license=('GPL')
depends=('desktop-file-utils' 'gdl' 'gettext' 'gtksourceview3' 'gtkspell3' 'iso-codes' 'libgda' 'libpeas' 'gsettings-desktop-schemas' 'hicolor-icon-theme' 'dconf')
makedepends=('intltool' 'itstool' 'gnome-doc-utils' 'gucharmap' 'json-glib' 'gnome-common' 'yelp-tools')
optdepends=('gucharmap: for charmap plugin'
            'json-glib: for open-tran plugin')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/2.91/$pkgname-$pkgver.tar.xz
        gtkspell3-port.patch)
sha256sums=('a1fd4b17b1f32cc7223a31d34e34f95b20acc7b5a23768351a34ff0b7a4bcdf6'
            '29afb55c692869cbd47d0181ddd93acdd53f5b236ebabd9a96a983da66d4cb9d')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Port to the new gtkspell3
  patch -Np1 -i "$srcdir/gtkspell3-port.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
