# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=profile-cleaner
pkgver=2.43
pkgrel=1
pkgdesc="Simple script to vacuum and reindex sqlite databases used by browsers"
arch=(any)
url="https://github.com/graysky2/profile-cleaner"
license=(MIT)
depends=(bash bc findutils parallel sqlite)
optdepends=('zsh-completions: for completion when using zsh')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('379429633b554df8e4e61f7c7c01c1358a561ee159609b15b12b8e11b6bde49b24b5a3fb485ee1b1e8bab4a4b3d32a17b3b216fdf6ebaf60ad7b89eceeb38b0e')
b2sums=('022f5a070a18bfcf64728da68e1239a3d45f37aba7149d4160ded1ef2c4707db20c59fec0db342f7cba37fa2da9f06011f10f1ee96065e37863fca86688f7722')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
