# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=noise-repellent
pkgver=0.1.5
pkgrel=1
pkgdesc="An lv2 plug-in for broadband noise reduction"
arch=('x86_64')
url="https://github.com/lucianodato/noise-repellent"
license=('LGPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('fftw' 'glibc')
makedepends=('lv2' 'meson')
# upstream changes tag system sometimes:
# https://github.com/lucianodato/noise-repellent/issues/69
source=("$pkgname-$pkgver.tar.gz::https://github.com/lucianodato/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('d6fa6bffd072fd1c80baa39d42fb9c2890910c642be5ba433a140cb3da484c9d5a4c65624b0332391f25e1e8fc7caa218c23863503d5b3af2c540705ddc8194a')

build() {
  cd "$pkgname-$pkgver"
  # install_folder is broken:
  # https://github.com/lucianodato/noise-repellent/issues/68
  meson --prefix=/usr/lib/lv2 \
        build
  ninja -C build
}

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
