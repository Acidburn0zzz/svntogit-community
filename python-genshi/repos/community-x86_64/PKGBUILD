# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor : Todd Maynard <arch@toddmaynard.com>

pkgname=python2-genshi
pkgver=0.7
pkgrel=4
pkgdesc="Python toolkit for stream-based generation of output for the web."
url="https://genshi.edgewall.org/"
license=("BSD")
arch=('x86_64')
depends=('python2')
makedepends=('python2-setuptools')
conflicts=('python-genshi<=0.6-3')
replaces=('python-genshi<=0.6-3')
source=("https://ftp.edgewall.org/pub/genshi/Genshi-$pkgver.tar.gz")
md5sums=('54e64dd69da3ec961f86e686e0848a82')

build() {
  cd "$srcdir"/Genshi-${pkgver}
  python2 setup.py build
}

package() {
  cd "$srcdir"/Genshi-${pkgver}
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
