# Maintainer : Eli Schwartz <eschwartz@archlinux.org>

pkgname=python-genshi
pkgver=0.7.3
pkgrel=2
pkgdesc="Python toolkit for stream-based generation of output for the web."
arch=('x86_64')
url="https://genshi.edgewall.org/"
license=("BSD")
depends=('python')
makedepends=('python-setuptools')
source=("https://github.com/edgewall/genshi/archive/${pkgver}.tar.gz")
sha256sums=('6b6992618069d9535ec5ee42087ab04846e6df9bba8283157843ceb516ca85b5')

build() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/genshi-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 COPYING "${pkgdir}"/usr/share/licenses/${pkgname}/COPYING
}
