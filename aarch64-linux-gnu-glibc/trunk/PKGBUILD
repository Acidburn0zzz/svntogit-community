# Maintainer: Anatol Pomozov

_target=aarch64-linux-gnu
pkgname=$_target-glibc
pkgver=2.24
pkgrel=1
_commit=fdfc9260
pkgdesc="GNU C Library ARM64 target"
arch=(any)
url='http://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
depends=( $_target-gcc $_target-linux-api-headers)
makedepends=(git)
options=(!buildflags !strip staticlibs)
source=(git://sourceware.org/git/glibc.git#commit=$_commit)
sha1sums=('SKIP')

prepare() {
  mkdir -p glibc-build
}

build() {
  cd glibc-build

  echo "slibdir=/lib" >> configparms
  echo "rtlddir=/lib" >> configparms
  echo "sbindir=/bin" >> configparms
  echo "rootsbindir=/bin" >> configparms

  # remove hardening options for building libraries
  export CFLAGS="-U_FORTIFY_SOURCE -mlittle-endian -O2"
  export CPPFLAGS="-U_FORTIFY_SOURCE -O2"
  unset LD_LIBRARY_PATH

  export BUILD_CC=gcc
  export CC=${_target}-gcc
  export CXX=${_target}-g++
  export AR=${_target}-ar
  export RANLIB=${_target}-ranlib

  ../glibc/configure \
      --prefix=/usr \
      --target=$_target \
      --host=$_target \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --with-headers=/usr/$_target/include \
      --with-bugurl=https://bugs.archlinux.org/ \
      --enable-add-ons \
      --enable-obsolete-rpc \
      --enable-kernel=2.6.32 \
      --enable-bind-now \
      --disable-profile \
      --enable-stackguard-randomization \
      --enable-lock-elision \
      --enable-multi-arch \
      --disable-werror

  echo "build-programs=no" >> configparms
  make

  # re-enable hardening for programs
  sed -i "/build-programs=/s#no#yes#" configparms

  echo "CC += -fstack-protector-strong -D_FORTIFY_SOURCE=2" >> configparms
  echo "CXX += -fstack-protector-strong -D_FORTIFY_SOURCE=2" >> configparms
}

package() {
  cd glibc-build

  make install_root=$pkgdir/usr/$_target install

  rm -rf $pkgdir/usr/$_target/{bin,sbin,etc,share,var}
}
