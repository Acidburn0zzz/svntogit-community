# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-term-table
pkgver=0.003
pkgrel=1
pkgdesc="Format a header and rows into a table"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Term-Table"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Term-Table-$pkgver.tar.gz")
sha512sums=('89ac5c781d3ebdda218f9e4aaec8a006679d9263f02428c99e291ecfb7efcea505921bedbc6539c010a1a1797c9fbe22ad907e914da881a44d89c967df6362a5')

build() {
  cd Term-Table-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Term-Table-$pkgver
  make test
}

package() {
  cd Term-Table-$pkgver
  make DESTDIR="$pkgdir" install
}
