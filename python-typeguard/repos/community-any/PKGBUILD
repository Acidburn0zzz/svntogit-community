# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.9.0
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('python-pytest-cov' 'python-typing_extensions')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agronholm/typeguard/archive/$pkgver.tar.gz")
sha512sums=('7e88fc6ff1d7a12194b89bc39326a26ea6cb3738a3f6f65ad7988037fc95bdb409017c14e4af2f63fb9770ab1421918d8ecb427acb3c19fa64ea3e83d547bb1f')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  python -m pytest
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
