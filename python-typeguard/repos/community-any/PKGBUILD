# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-typeguard
pkgver=2.11.0
pkgrel=1
pkgdesc="Run-time type checker for Python"
url="https://github.com/agronholm/typeguard"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools-scm')
checkdepends=('mypy' 'python-pytest-cov' 'python-typing_extensions')
source=("https://github.com/agronholm/typeguard/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c0fdda9f080945187b3e4ec10cf9ceafae349411625cfd70511c9648abda59831c5ab4111226f6196ec5c7b500abaf7c495815330a574bb70d345bb32f90eceb')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd typeguard-$pkgver
  python setup.py build
}

check() {
  cd typeguard-$pkgver
  # https://github.com/agronholm/typeguard/issues/176
  PYTHONPATH="$PWD/build/lib" MYPYPATH="$PWD/build/lib" pytest --deselect tests/mypy/test_type_annotations.py::test_positive
}

package() {
  cd typeguard-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
