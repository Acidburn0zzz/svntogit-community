# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hspec-core
pkgname=haskell-hspec-core
pkgver=2.9.0
pkgrel=7
pkgdesc="A Testing Framework for Haskell"
url="http://hspec.github.io/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-ansi-terminal' 'haskell-call-stack' 'haskell-clock'
         'haskell-hspec-expectations' 'haskell-hunit' 'haskell-quickcheck' 'haskell-quickcheck-io'
         'haskell-random' 'haskell-setenv' 'haskell-tf-random')
makedepends=('ghc')
checkdepends=('haskell-base-orphans' 'haskell-hspec-meta' 'haskell-silently' 'haskell-temporary')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('298140ce6303610a2affd04d8d6639d418b6135433a8f12ef0226346dddb3545a542cd6c15369444ff4d1c4f156972f90af8eda77718aaf0e28964dcb496ef49')

build() {
    cd $_hkgname-$pkgver

    if (( CHECKFUNC )); then
        _opts=('--enable-tests')
    else
        _opts=('--disable-tests')
    fi
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname "${_opts[@]}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    # https://github.com/hspec/hspec/issues/432
    runhaskell Setup test || :
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
