# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

_name=progressbar2
pkgname=python-progressbar
pkgdesc="A progress bar for Python"
pkgver=3.43.1
pkgrel=1
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=('any')
depends=('python-six' 'python-utils')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-pytest' 'python-freezegun')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('57da1f804e4e3929751b9d27b3f6dc03df65dbf6d567a83d26c6e32dcba22592af1d1153fa3da9f764d11941db2372f22c04d45f963b420e58084d18a15db8c7'
            'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # don't care about custom pytest config
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  # disabling tests not honoring PYTHONPATH:
  # https://github.com/WoLpH/python-progressbar/issues/201
  py.test -k 'not test_non_timed \
              and not test_context_wrapper \
              and not test_rapid_updates \
              and not test_generator_example \
              and not test_list_example'
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {CHANGES,CONTRIBUTING,README}.rst \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
