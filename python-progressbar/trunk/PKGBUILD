# Maintainer: Maxim Andersson <thesilentboatman@gmail.com>
# Co-Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161

_pkgname=python-progressbar
pkgbase=python-progressbar
pkgdesc="A progress bar for Python 2 and Python 3"
pkgname=('python-progressbar' 'python2-progressbar')
pkgver=3.39.3
pkgrel=1
url="https://github.com/WoLpH/python-progressbar"
license=('BSD')
arch=("any")
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools' 'python-utils' 'python2-utils')
source=("${_pkgname}-${pkgver}.tar.xz::https://github.com/WoLpH/python-progressbar/releases/download/v${pkgver}/python-progressbar-v${pkgver}.tar.xz"
        "${_pkgname}-${pkgver}.tar.xz.asc::https://github.com/WoLpH/python-progressbar/releases/download/v${pkgver}/python-progressbar-v${pkgver}.tar.xz.asc")
sha512sums=('17930a6099d3b88094788d2142a7f308a217e9a0be359f15ec4cfba7b7d7c1d8f4c0bef79f5268b0de2f15970c15eb03dfeec265ef2e06cd868f60282d253310'
            'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
    cp -a "${srcdir}/${_pkgname}-v${pkgver}"{,-py2}
}

build() {
    cd "${srcdir}/${_pkgname}-v${pkgver}"
    python setup.py build

    cd "${srcdir}/${_pkgname}-v${pkgver}-py2"
    python2 setup.py build
}

package_python-progressbar() {
    depends=('python' 'python-utils')

    cd "${srcdir}/${_pkgname}-v${pkgver}"
    python setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-progressbar() {
    depends=('python2' 'python2-utils')

    cd "${srcdir}/${_pkgname}-v${pkgver}-py2"
    python2 setup.py install --skip-build --root="${pkgdir}" --optimize=1
    install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
