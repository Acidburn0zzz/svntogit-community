# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-progressbar
pkgname=('python-progressbar' 'python2-progressbar')
pkgver=2.3
pkgrel=2
arch=('any')
url='http://code.google.com/p/python-progressbar/'
license=('LGPL')
makedepends=('python' 'python-distribute' 'python2' 'python2-distribute')
source=("http://python-progressbar.googlecode.com/files/progressbar-$pkgver.tar.gz")
sha1sums=('aebb94b452990468c77090fb43b335cf0a724353')

build() {
  'cp' -a progressbar-$pkgver progressbar2-$pkgver
  # change python interpreter
  find progressbar2-$pkgver -name '*.py' -exec \
     sed -i '1s,^#!/usr/bin/python$,#!/usr/bin/python2,' {} \; -print
}

package_python-progressbar() {
  pkgdesc='Text progressbar library for python'
  depends=('python')

  cd progressbar-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-progressbar() {
  pkgdesc='Text progressbar library for python2'
  depends=('python2')

  cd progressbar2-$pkgver
  python2 setup.py install --root="$pkgdir"
}

# vim:set ts=2 sw=2 ft=sh et:
