# $Id$
# Maintainer: SÃ©bastien Luttringer

pkgbase=python-progressbar
pkgname=('python-progressbar' 'python2-progressbar')
pkgver=2.3
pkgrel=6
arch=('any')
url='http://code.google.com/p/python-progressbar/'
license=('LGPL')
makedepends=('python' 'python-setuptools' 'python2' 'python2-setuptools')
source=("http://python-progressbar.googlecode.com/files/progressbar-$pkgver.tar.gz"
        '01-python33.patch')
sha1sums=('aebb94b452990468c77090fb43b335cf0a724353'
          '5dc63611e364e90441cbc90fe2530b9fab67e780')

prepare() {
  cd progressbar-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local filename
  for filename in "${source[@]}"; do
    if [[ "$filename" =~ \.patch$ ]]; then
      msg2 "Applying patch $filename"
      patch -p1 -N -i "$srcdir/$filename"
    fi
  done

}

build() {
  cd progressbar-$pkgver
  python setup.py build --build-lib=build/python
  python2 setup.py build --build-lib=build/python2

  msg2 'Fix python2 sheebang'
  find build/python2 -type f -exec \
    sed -i '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \;
}

package_python-progressbar() {
  pkgdesc='Text progressbar library for python'
  depends=('python')

  cd progressbar-$pkgver
  python setup.py build --build-lib=build/python \
                  install --root="$pkgdir" --optimize=1
}

package_python2-progressbar() {
  pkgdesc='Text progressbar library for python2'
  depends=('python2')

  cd progressbar-$pkgver
  python2 setup.py build --build-lib=build/python2 \
                   install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
