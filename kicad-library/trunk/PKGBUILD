# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=6.0.0
pkgrel=3
pkgdesc="Assorted libraries for KiCad"
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('cmake')
source=("https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/$pkgver/kicad-symbols-$pkgver.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-footprints-$pkgver.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/$pkgver/kicad-packages3D-$pkgver.tar.bz2"
        "https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-templates-$pkgver.tar.bz2")
sha512sums=('5891606d47004cc2a530e1d327ae6bd7a89c571bb60dbc4e2f8c6659277de361b6ea337939414028c834392a2f69668a3d3c99962765cc76048b8e4dd153cca3'
            'd8eac8fcc380ef67ccbd3b8f2529d7023aecef674299d83aabccd41beef1934507edf3a9bdbf4b440ae550470d7f16382aba4090fe5f1d5397a8e0a0f4364ec9'
            '48f6698d8b48483e484638f780cf5a9522cda5381c3858b15b0862994dfacc271426ecbaaa5168a2fc8f6be84ac6e9b160e080ac74183d5be282de36088416af'
            'b880a728bf0b2ef5512fdf1d5b76b5fe60ce6c3791142d4ff81f118b131abe1a45ab7af095e3a133e5637ad0cf67b85e74b2cb5f3f8661025c1511267d8046d0')
b2sums=('e3113eda85f1d3054013bbc7c952f5ec3cf49c40708cb527c6bc944b1fa90aff901ae9c1bfb5c422c4df96c579ff71b4f51927763a4afec7a6bf68bbf43e9bec'
        'dd76d572b15f420de9587bea6556637ad1ad5c3e12a589950124e44fa2e0f71f6e500cf02d2cd0187e2ae92cd786407bc3fb093dd367dea9759d26c8b9d31200'
        '5287b9ae322f6eac5035c0dbe725a26f2d44cde259c95d8d5db950839de067075716ff343db32088861873b77f06116c2dc077461fc35681fcf4c86e5b9ba3d1'
        '9f72475b0b15c682e13fda9bc6bdf90c851fdfc6b2111c88507fce4cc6badbf1510e919d2d484c6397eaa12dec04a23a98d9b26c33d78b6e92a82b376b25f24c')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package-$pkgver" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
