# Maintainer: Kyle Keen <keenerd@gmail.com>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgbase=kicad-library
pkgname=('kicad-library' 'kicad-library-3d')
pkgver=6.0.0
pkgrel=1
pkgdesc="Kicad component and footprint libraries"
arch=('any')
url="https://kicad.github.io/"
license=('GPL')
makedepends=('cmake')
options=('!strip')
source=("https://gitlab.com/kicad/libraries/kicad-symbols/-/archive/$pkgver/kicad-symbols-$pkgver.tar.gz"
        "https://gitlab.com/kicad/libraries/kicad-footprints/-/archive/$pkgver/kicad-footprints-$pkgver.tar.gz"
        "https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/$pkgver/kicad-packages3D-$pkgver.tar.gz")
sha512sums=('9e223ba15e8c8d00d5f5889f4fc9f7677a290253121889dd162788af3bbf1400b39c6d41bb1167b53ea028c32016f8f465251a308f546ece69149a9e5b32e66c'
            '919883ee3a422e03ec7e89c6859d85431965e2ba6a6ee0449c1c7349d190abc65e6e9ac85e083e0fa8ed5d5657fa0a00f4094cf1c56046af470a2b4300bac19b'
            '84f8b319c34a31d669c089a1c26f1367e8369ad051d846417b7d646efd6a298aba3ebbabbc49f7bac849804cc6b7465281655593ecd6242bb25f4e77ab0df5f4')
b2sums=('ba96aac11c60ba590c23d3df56e0c3cb7fbea828833e0cd7874976f0ed93bf7e0dd21a3a367eff0e5086126c663eec075b45cb82f463c36289b780ecf1836e20'
        'f54b1e47e4eb575c2d7bbb7d009954ca12f1aad5c06e4b39c9fa55ba1450ed865d3528dec5d98fac558a183266a77fb58209fb1028a2e2f5f6bd4e1d14b5caf7'
        '17f63f6946350e2169f63d83872bf03044f88b37bca641e24d4b985f3be6cee8773142bc00802777c715c15f78e5b557891d266af9e968f827877c592e49a3b9')

build() {
  for package in symbols footprints packages3D; do
    cmake \
      -B "build-$package" \
      -S "kicad-$package-$pkgver" \
      -DCMAKE_INSTALL_PREFIX=/usr

    cmake --build "build-$package"
  done
}

package_kicad-library() {
  pkgdesc="Kicad component and footprint libraries"

  DESTDIR="$pkgdir" cmake --install build-symbols
  DESTDIR="$pkgdir" cmake --install build-footprints
}

package_kicad-library-3d() {
  pkgdesc="Kicad 3D render model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}
