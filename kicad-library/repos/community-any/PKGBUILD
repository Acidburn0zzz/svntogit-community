# Maintainer: Filipe La√≠ns <lains@archlinux.org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Rachel Mant <aur@dragonmux.network>

pkgbase=kicad-library
pkgname=($pkgbase{,-3d})
pkgver=6.0.1
pkgrel=1
pkgdesc="Assorted libraries for KiCad"
arch=('any')
url='http://kicad.org/'
license=('GPL')
makedepends=('git' 'cmake')
source=(
  'git+https://gitlab.com/kicad/libraries/kicad-packages3D.git#commit=530926da52f5226de6fc3167cf2e0671fb7f0cec'
  'git+https://gitlab.com/kicad/libraries/kicad-symbols.git#commit=e3b198d827817b79fb8a6df07f0cfc1cb9140edf'
  'git+https://gitlab.com/kicad/libraries/kicad-templates.git#commit=a27d83f0a20f0be0c1ab04b139a0c518da51a5d4'
  'git+https://gitlab.com/kicad/libraries/kicad-footprints.git#commit=ee227da2af60189b94cfae05496af17d47137f9e'
)
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

build() {
  for package in symbols footprints packages3D templates; do
     cmake \
       -B "build-$package" \
       -S "kicad-$package" \
       -DCMAKE_INSTALL_PREFIX=/usr

     cmake --build "build-$package"
   done
}

package_kicad-library() {
  pkgdesc="KiCad symbol, footprint and template libraries"

  for package in symbols footprints templates; do
    DESTDIR="$pkgdir" cmake --install "build-$package"
  done
}

package_kicad-library-3d() {
  pkgdesc="KiCad 3D model libraries"

  DESTDIR="$pkgdir" cmake --install build-packages3D
}

# vim:set ts=2 sw=2 et:
