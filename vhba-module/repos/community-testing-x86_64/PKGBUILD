# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Charles Lindsay <charles@chaoslizard.org>

pkgname=vhba-module
pkgver=20120422
_extramodules=extramodules-3.4-ARCH
pkgrel=2
pkgdesc="Kernel module that emulates SCSI devices"
arch=('i686' 'x86_64')
url="http://cdemu.sourceforge.net/"
license=('GPL')
depends=('linux>=3.4' 'linux<3.5')
makedepends=('linux-headers>=3.4')
install=$pkgname.install
source=("http://downloads.sourceforge.net/cdemu/$pkgname-$pkgver.tar.bz2"
        '60-vhba.rules')
md5sums=('d97372da1d270d1605742b2995fb6678'
         'b5e82d0160e7a181219b67c1794d5c27')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  _kernver="$(cat /lib/modules/$_extramodules/version)"

  make -j1 KDIR=/usr/src/linux-$_kernver
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -D vhba.ko "$pkgdir/lib/modules/$_extramodules/vhba.ko"

  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" \
    "$startdir/vhba-module.install"

  install -Dm644 "$srcdir/60-vhba.rules" \
	  "$pkgdir/usr/lib/udev/rules.d/60-vhba.rules"
}

# vim:set ts=2 sw=2 et:
