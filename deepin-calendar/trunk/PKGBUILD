# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.7.0.3
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('71e6d3973c6ad9f154b13993c269c16d74c04e626d13e61937cc703e7795afc9628fd37fbc3350babf733c39447dc85d59fd55816a828ecd56d6827542f71c8d')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/QMenuBar/a #include <QMouseEvent>' src/yearwindow.cpp
  sed -i '/<QQueue>/a #include <QMouseEvent>' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/draginfographicsview.cpp
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/schcedulesearchview.cpp src/daymonthview.cpp src/weekheadview.cpp src/customframe.cpp
}

build() {
  cd dde-calendar-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-calendar-$pkgver
  DESTDIR="$pkgdir" ninja install
}
