# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.6.2
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('c486b66a57f0f19abc8876f7eebf74b3fce1247b7f8c3b56161c72d81674fa73b758f64566f95d4bc9647c7feb76e0da2a4ddd1728b768ea575ddcf018091902')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/QMenuBar/a #include <QMouseEvent>/' src/yearwindow.cpp
  sed -i '/<QQueue>/a #include <QMouseEvent>/' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>/' src/schcedulesearchview.cpp
}

build() {
  cd dde-calendar-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-calendar-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
