# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.7.0.4
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('5102c1298dc7395b741729041fed26cf6012c5235c67b5bbac2c90afbbd2a806d0f290a44afa3c59a4da9cf9f088281bd1f348a64208ac53e64312dcc27cff03')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/<QQueue>/a #include <QMouseEvent>' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/draginfographicsview.cpp

  # Not included in https://github.com/linuxdeepin/dde-calendar/pull/30 yet
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/schcedulesearchview.cpp src/daymonthview.cpp src/weekheadview.cpp src/customframe.cpp
  sed -i '/include <QMessageBox>/a #include <QWheelEvent>' src/yearwindow.cpp
}

build() {
  cd dde-calendar-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-calendar-$pkgver
  DESTDIR="$pkgdir" ninja install
}
