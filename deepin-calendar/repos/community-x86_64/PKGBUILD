# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.7.0.10
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration' 'hicolor-icon-theme')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz"
        "dde-calendar-icon.patch")
sha512sums=('4b2893526f9703aa7cb7ba503d02ca5fbe4d757857be24816005f47b0b1b5b9a35d30d00c725c5ea31f5f3b9e9e060721fa64dc43af17bfe2783c453187ff0ee'
            '40ffae29fcbd386512dac9b91c39219b420b349f1f5facc254467b9e5407091886c8d0ed0979d566ba79539c093464cbc7719da7268c9745bc30273f9d7db7e2')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/<QQueue>/a #include <QMouseEvent>' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/draginfographicsview.cpp
  sed -i '/include <QPainter>/a #include <QMouseEvent>' schedule-plugin/src/widget/itemwidget.h schedule-plugin/src/widget/modifyscheduleitem.h

  # Not included in https://github.com/linuxdeepin/dde-calendar/pull/30 yet
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/schcedulesearchview.cpp src/daymonthview.cpp src/weekheadview.cpp \
                                                         src/customframe.cpp src/yearview.cpp schedule-plugin/src/widget/modifyscheduleitem.h \
                                                         schedule-plugin/src/widget/itemwidget.h
  sed -i '/include <QMessageBox>/a #include <QWheelEvent>' src/yearwindow.cpp

  # Install the application icon
  # https://github.com/linuxdeepin/dde-calendar/pull/31
  patch -Np1 -i ../dde-calendar-icon.patch
}

build() {
  cd dde-calendar-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd dde-calendar-$pkgver
  DESTDIR="$pkgdir" ninja install
}
