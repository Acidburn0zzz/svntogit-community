# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-calendar
pkgver=5.6.8
pkgrel=1
pkgdesc='Calendar for Deepin Desktop Environment'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-calendar"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-calendar/archive/$pkgver.tar.gz")
sha512sums=('af8a2d705207ec12a06f2a4f69e2d030cf11df414a7a48b1adb8b7bec8af313d5e4ac9fd691d34a6993232fc9359538ac3fb34aa7fda75a81c38ae7023a90bb8')

prepare() {
  cd dde-calendar-$pkgver
  sed -i '/QMenuBar/a #include <QMouseEvent>' src/yearwindow.cpp
  sed -i '/<QQueue>/a #include <QMouseEvent>' src/daymonthview.cpp
  sed -i '/<QStylePainter>/a #include <QMouseEvent>' src/schcedulesearchview.cpp
  sed -i '/include <QJsonObject>/a #include <QMouseEvent>' src/draginfographicsview.cpp
  sed -i '/include <QPainter>/a #include <QPainterPath>' src/schcedulesearchview.cpp src/daymonthview.cpp src/weekheadview.cpp src/customframe.cpp
}

build() {
  cd dde-calendar-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-calendar-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
