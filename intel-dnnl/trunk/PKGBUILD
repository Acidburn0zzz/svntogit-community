# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Baris Demirdelen <barisdemirdelen at gmail dot com>
# Contributor: Andrew Anderson <aanderso@tcd.ie>
# Contributor: Jonathon Fernyhough <jonathon_at manjaro_dotorg>
pkgname=intel-dnnl
_pkgname=mkl-dnn
pkgver=1.2
pkgrel=1
pkgdesc="Intel Deep Neural Network Library (formerly mkl-dnn)"
arch=(x86_64)
provides=(mkl-dnn)
url=https://github.com/intel/mkl-dnn
license=('APACHE')
makedepends=('cmake' 'doxygen')
source=("$pkgname-$pkgver.tar.gz::https://github.com/intel/mkl-dnn/archive/v${pkgver}.tar.gz")
sha256sums=('30979a09753e8e35d942446c3778c9f0eba543acf2fb0282af8b9c89355d0ddf')

prepare() {
  cd "$_pkgname-$pkgver"
  mkdir -p build
}

build() {
  cd "$_pkgname-$pkgver/build"
  cmake .. -DCMAKE_INSTALL_PREFIX="/usr"
  make
  make doc
}

check() {
  cd "$_pkgname-$pkgver/build"
  ctest
}

package() {
  cd "$_pkgname-$pkgver/build"
  DESTDIR="$pkgdir" make install

  cd ..
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
