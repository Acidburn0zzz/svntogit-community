# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.19.1
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('06a208085b4eb00b9a1615ec639b06c2201095edd3d8cbc1d3d31c0f0f47245e2725a9d3e44e942ad9d69bfa0df42166fcd195df5b5282e5e6da9556350395a9')
b2sums=('939eb0b6ecfd7043cafca8566ec22b93988359602df11aeff8c0c48252326168b5fc83dbf0a863e674f14fe86e11060ba396a24cdfadbe28eb91a22ef2cc8abe')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
