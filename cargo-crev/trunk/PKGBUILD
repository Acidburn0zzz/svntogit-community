# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.16.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('752b11f9c6ed852957dcecaa8828a54097c9baf6cfc7ecc0b1d04add82a7693ff08308add153ec5c8db3393642c8bae9fb59cf677a42b67e9d4ab56f39d9794f')
b2sums=('5ba45323add4e16df48be5efea72a38ca2d394f72dac833e65592456136e34d9f3052f5bc057756939ec08b48a1be85552f7531fe4f9b738cfb6dba2ae015286')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
