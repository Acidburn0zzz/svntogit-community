# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.15.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('647b600a85179101dddcfbe3c0b3ed94af8b41fb8bacebf10a1be045c8f4b80a52249fb9c943f00f45b3089376b994bbd7adfd018ce541b42412eddcbf134811')
b2sums=('1da28c566c7577fb4e059836eca760a342fafad37dc828121b25919e73bc9994ce31c2fb8dc650f6b6530a3b35bb7fa31650075af8af88a6a9626b92ac9058f0')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
