# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.19.0
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('277e9fc0bbbcc813b70cc6777be5cb0382921d3c19c8ddb463633e9fb14b29d68cfc7c66b1a035e0c76e865569ffc7e2d109ec04ff3769fc85b88bc4755e512c')
b2sums=('eb82b5dca40f6cc433c0a20944661e0bac5d1f812d0dc36ac0935d83119ed9a6d328b2769bcb7fbc78509ec00fa4cdeae47f9b80ce522ae49cfdec1ba9ac6e14')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
