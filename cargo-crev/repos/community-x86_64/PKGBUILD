# Maintainer: kpcyrd <git@rxv.cc>

pkgname=cargo-crev
pkgver=0.19.2
pkgrel=1
pkgdesc="Scalable, social, Code REView and recommendation system that we desperately need"
url="https://github.com/dpc/crev"
depends=('openssl' 'curl')
makedepends=('cargo' 'clang')
arch=('x86_64')
license=('MPL' 'Apache' 'MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/dpc/crev/archive/v${pkgver}.tar.gz")
sha512sums=('7f4c1cacb8a4e04a6ea8d16554391adc3afe5bada0f98053376b88206c779770b522363731f5237629b45c317a56d156dce49553ec0d0f588e1ccb156cdeeef7')
b2sums=('4d8903b61a4f1b7844080f6c064406e84fbf82168582dbf3042d1e2fbfdd95779ac5f22593b1c31bf803c7b389f8332edea97f8429afd6c3d01df58cebe8a821')

build() {
  cd "${pkgname}-${pkgver}/cargo-crev"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"

  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
