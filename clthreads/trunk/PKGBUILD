# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Christoph Zeiler <rabyte*gmail>
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Philipp Ãœberbacher <murks at lavabit dot com>

pkgname=clthreads
pkgver=2.4.2
pkgrel=1
pkgdesc="C++ wrapper library around the POSIX threads API"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('LGPL')
depends=('gcc-libs')
source=("${url}downloads/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('a91de7a287c2cb9b2ce17e4c158db79bbaeccb278510d11e6c05fcbe9295c26b4d0231fa97800b265decd1d0886b7425f7b5d4cbcb18758be540356408e72131')

prepare() {
  cd "${pkgname}-${pkgver}/source"
  # remove optimization
  # remove local call to ldconfig
  sed -e '/native/d' \
      -e '/ldconfig/d' \
      -i Makefile
}

build() {
  cd "${pkgname}-${pkgver}/source"
  make
}

package(){
  cd "${pkgname}-${pkgver}/source"
  # install header, shared object and symlink manually, as Makefile is broken
  install -vDm 644 "${pkgname}.h" -t "${pkgdir}/usr/include/"
  install -vDm 755 "lib${pkgname}.so.${pkgver}" -t "${pkgdir}/usr/lib/"
  ln -sv "lib${pkgname}.so.${pkgver}" "${pkgdir}/usr/lib/lib${pkgname}.so"
  install -vDm 644 ../AUTHORS -t "${pkgdir}/usr/share/doc/${pkgname}"
}
