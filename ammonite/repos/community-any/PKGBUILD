# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=2.1.2
_scalaversion=2.13.2
pkgrel=2
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('mill')
source=("https://github.com/lihaoyi/Ammonite/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('3dd2aa1382d021c6e2fa67c4a9fa782c01af1d7229d1e8b327af6c26c701eefb41f0f6705401cd3de7836150e72fffbd2e64d123864de39cb01a681b4b542433')

prepare() {
  cd Ammonite-$pkgver
  sed -i "s/'git/'true/;s/^    \\.toInt$//" build.sc
}

build() {
  cd Ammonite-$pkgver
  TRAVIS_TAG=$pkgver mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite-$pkgver
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly/dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
