# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thayne <astrothayne at gmail dot com>
# Contributor: Yiqiao Wang <yiqiao.wang96 at gmail dot com>
# Contributor: wenLiangcan <boxeed at gmail dot com>

pkgname=ammonite
pkgver=2.1.3
_scalaversion=2.13.2
pkgrel=1
pkgdesc="A cleanroom re-implementation of the Scala REPL from first principles. "
arch=('any')
url="https://github.com/lihaoyi/Ammonite"
license=('MIT')
depends=('bash' 'java-environment')
makedepends=('mill')
source=("https://github.com/lihaoyi/Ammonite/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('fb71ef83d718f94bbadcb5c005693539bf10f16771d6ca128dcc9286b348d78b420bb50b36a67bc67942fadbafc3da9a94229a191314a34952cd2ed6eec21888')

prepare() {
  cd Ammonite-$pkgver
  sed -i "s/'git/'true/;s/^    \\.toInt$//" build.sc
}

build() {
  cd Ammonite-$pkgver
  TRAVIS_TAG=$pkgver mill -i amm[$_scalaversion].assembly
}

package() {
  cd Ammonite-$pkgver
  install -dm755 "$pkgdir"/usr/bin
  (echo "#!/bin/sh"; cat out/amm/$_scalaversion/assembly/dest/out.jar) > "$pkgdir"/usr/bin/amm
  chmod +x "$pkgdir"/usr/bin/amm
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
