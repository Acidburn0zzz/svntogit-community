# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: kozec <kozec at kozec dot com>

pkgname=syncthing-gtk
pkgver=0.9.3.1
pkgrel=1
epoch=1
pkgdesc='GTK3 based GUI and notification area icon for Syncthing'
arch=('any')
url='https://github.com/syncthing/syncthing-gtk'
license=('GPL2')
depends=('syncthing>=0.13' 'gtk3' 'libnotify' 'python2-pyinotify'
         'python2-bcrypt' 'python2-cairo' 'python2-dateutil' 'python2-gobject')
makedepends=('python2-setuptools')
optdepends=('python2-caja: file manager integration for Caja'
            'python2-nautilus: file manager integration for Nautilus'
            'nemo-python: file manager integration for Nemo')
source=($pkgname-$pkgver.tar.gz::https://github.com/syncthing/$pkgname/archive/v$pkgver.tar.gz
        kde-statusicon.patch)
sha256sums=('0adcdd4be83f2e65aeeb5250d6d4b69c12216dbd740c8051b6a434a9154bd9b4'
            '109d8c970045e60251fc64865f05322b23a0995ee6725be02905941cb3a1ae0d')

prepare() {
  cd $pkgname-$pkgver

  # Enable Gtk.StatusIcon in KDE
  patch -Np1 -i ../kde-statusicon.patch
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="${pkgdir}" --optimize=1

  # Move filemanager extensions into their right place (FS#57123)
  mkdir -p "${pkgdir}"/usr/share/{caja,nautilus,nemo}-python/extensions/
  mv "${pkgdir}"/usr/share/syncthing-gtk/syncthing-plugin-caja.py "${pkgdir}"/usr/share/caja-python/extensions/
  mv "${pkgdir}"/usr/share/syncthing-gtk/syncthing-plugin-nautilus.py "${pkgdir}"/usr/share/nautilus-python/extensions/
  mv "${pkgdir}"/usr/share/syncthing-gtk/syncthing-plugin-nemo.py "${pkgdir}"/usr/share/nemo-python/extensions/
}
