# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-tblib
pkgname=('python-tblib' 'python2-tblib')
_pyname=tblib
pkgver=1.2.0
pkgrel=1
pkgdesc="Python traceback fiddling library"
url="https://github.com/ionelmc/python-tblib"
arch=('any')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
source=(${pkgbase}-${pkgver}.tar.gz::https://pypi.python.org/packages/source/t/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('39120b506e8e06435a2d04478c8c9a9049d52710110dfc5501931b6547df024ca4087733706982ac01a195b5669458bc0d008abe42ca38b9eb3b171808b67ef8')

check() {
  cd ${_pyname}-${pkgver}
  python setup.py test
  python2 setup.py test
}

package_python-tblib() {
  depends=('python-six')

  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-tblib() {
  depends=('python2-six')

  cd ${_pyname}-${pkgver}
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 README.rst "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
