# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=eva
pkgver=0.2.7
pkgrel=2
pkgdesc='simple calculator REPL, similar to bc(1)'
arch=(x86_64)
url="https://github.com/NerdyPepper/$pkgname"
license=(GPL3)
depends=(gcc-libs)
makedepends=(cargo)
options=(debug)
_archive="$pkgname-$pkgver"
source=("$url/archive/v$pkgver/$_archive.tar.gz")
sha256sums=('72b2e47e987102d67c9dcbb60e26c4ff0b20e6f844d0d2b9d91c3f073374aee0')

prepare() {
	cd "$_archive"
	cargo update # upstream release lockfile isn't synced
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_archive"
	cargo build --frozen --release
}

check() {
	cd "$_archive"
	# Upstream tests broken on current Rust
	# cargo test --frozen
}

package() {
	cd "$_archive"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$pkgname"
}
