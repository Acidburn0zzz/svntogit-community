# $Id: PKGBUILD,v 1.4 2009/07/14 09:52:46 allan Exp $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributer: Jason Chu <jason@archlinux.org>

pkgname=python3
pkgver=3.1
pkgrel=2
_pybasever=3.1
pkgdesc="Next generation of the python high-level scripting language"
arch=('i686' 'x86_64')
license=('custom')
url="http://www.python.org"
depends=('expat' 'bzip2' 'gdbm' 'openssl>=0.9.8i' 'zlib')
makedepends=('tk>=8.5.5' 'sqlite3')
optdepends=('tk: for pynche and modulator')
source=(http://www.python.org/ftp/python/${pkgver}/Python-${pkgver}.tar.bz2
        python-internal-expat.patch
        python-gdbm.patch
	modulator-launcher
	pynche-launcher)
md5sums=('f64437a24d39f1917aa1878cc70621f6'
         '993800dbcc0c4f199c1ceee998a06e71'
         '16af6360d8524de8f86f72c22a7165ec'
         'b4a531570415280dc406c8d5c1fa8d83'
         '9f731b9033db285132a3524fb36409e7')

build() {
  cd ${srcdir}/Python-${pkgver}

  patch -Np0 -i ${srcdir}/python-internal-expat.patch
  patch -Np1 -i ${srcdir}/python-gdbm.patch

  export OPT="${CFLAGS}"
  ./configure --prefix=/usr --enable-shared --with-threads --with-computed-gotos \
              --enable-unicode --with-wide-unicode

  make || return 1
  make DESTDIR=${pkgdir} install

  ln -sf python${_pybasever} ${pkgdir}/usr/bin/python3
  ln -sf python${_pybasever}-config ${pkgdir}/usr/bin/python3-config

  # Fix conflicts with python
  rm ${pkgdir}/usr/bin/2to3

  # Install the tools
  #modulator
  sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/modulator-launcher
  install -m755 ${srcdir}/modulator-launcher ${pkgdir}/usr/bin/modulator3
  cp -r Tools/modulator ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #pynche - broken in python3.1
  #sed -i "s#%%PYBASEVER%%#${_pybasever}#" ${srcdir}/pynche-launcher  
  #install -m755 ${srcdir}/pynche-launcher ${pkgdir}/usr/bin/pynche3
  #rm -f Tools/pynche/*.pyw
  #cp -r Tools/pynche ${pkgdir}/usr/lib/python${_pybasever}/site-packages/

  #gettext
  install -m755 Tools/i18n/pygettext.py ${pkgdir}/usr/bin/pygettext3.py
  install -m755 Tools/i18n/msgfmt.py ${pkgdir}/usr/bin/msgfmt3.py

  mkdir -p ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts
  install Tools/scripts/README ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/
  install Tools/scripts/*py ${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/

  # license
  install -Dm644 LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
