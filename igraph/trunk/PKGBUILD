# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.8.1
pkgrel=2
pkgdesc="A library for creating and manipulating (un)directed graphs."
arch=(x86_64)
url="https://igraph.org/c/"
license=(GPL2)
depends=(glpk arpack libxml2)
source=("https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('266e1bf9e81305b368fbaa2218a8416c51ae85ea164e3657c574dc3898ca7b71')

build() {
  cd $pkgname-$pkgver
  ./configure \
   --prefix=/usr \
   --with-external-blas \
   --with-external-lapack \
   --with-external-arpack \
   --with-external-glpk
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
