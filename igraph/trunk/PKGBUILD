# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fazlul Shahriar <fshahriar@gmail.com>
# Contributor: Denis Zawada <deno@bajtogrod.pl>

pkgname=igraph
pkgver=0.9.1
pkgrel=1
pkgdesc="A library for creating and manipulating (un)directed graphs."
arch=(x86_64)
url="https://igraph.org/c/"
license=(GPL2)
depends=(glpk arpack libxml2 suitesparse)
makedepends=(cmake)
source=("https://github.com/igraph/igraph/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('1902810650e8f9d98feefa3eca735db5a879416d00a08f68aad2ca07964cee9f')

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None \
    -DBUILD_SHARED_LIBS=ON
  cmake --build build
}

check() {
  cmake --build build --target check
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
