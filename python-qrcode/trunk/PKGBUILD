# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgname=python-qrcode
pkgver=7.3
pkgrel=3
pkgdesc='Python library to generate QR codes'
arch=('any')
url='https://github.com/lincolnloop/python-qrcode'
license=('BSD')
depends=('python' 'python-setuptools')
optdepends=('python-pillow: generate images')
checkdepends=('python-pillow' 'python-pytest')
source=($url/archive/v$pkgver/$pkgname-v$pkgver.tar.gz)
sha512sums=('1d24dd7d3c9e0cd909a780b2639d2db54e0db6bc98100b6446cb290aa78ed1b9ed6d14c4cdd8a8ddcbe7a0ae6be8724c5b26118d25985450a974d78a520b3b0e')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  pytest
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
