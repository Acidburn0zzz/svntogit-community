# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.12.0
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=('x86_64')
url="https://amsynth.github.io/"
license=('GPL2')
groups=('dssi-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'fontconfig' 'gcc-libs' 'gdk-pixbuf2' 'glibc')
makedepends=('atk' 'autoconf-archive' 'dssi' 'freetype2' 'glib2' 'gtk2'
'harfbuzz' 'intltool' 'jack' 'ladspa' 'liblo' 'lv2' 'pandoc')
checkdepends=('lv2lint')
optdepends=('alsa-lib: for standalone application'
            'dssi-host: for DSSI plugin'
            'jack: for standalone application'
            'liblo: for standalone application and DSSI plugin'
            'new-session-manager: for session management with the standalone application'
            'lv2-host: for LV2 plugin'
            'vst-host: for VST plugin')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/amsynth/amsynth/archive/release-${pkgver}.tar.gz"
        "${pkgname}-1.12.0-fix_ttl.patch::https://github.com/amsynth/amsynth/pull/179/commits/ecbb2dbe37f67cfe5ec90a4a806c3f4d24b74adb.patch")
sha512sums=('800ae249805a470118df5d6f4bb8e790df41004c7cb09f5ef5732346bad91ed3d864357d1245cddb06960a51325ed618b4b33bcac0eda98ea45a8124cc793041'
            '13e7f4e67aed1ddaafc06a81a3e24a8b9b2af614c346f4d2f6b80a01340597b6e86f676557958fdb896348efc90cee4290dd1766a53203dad4d9d2c2d5fd097f')
b2sums=('c89fe6c3683b050f76d72524816bc8b490870e87592eff888766d8dfd801b94ed854433d1021b39f0981933f4b0a995749a88ff0e5a2be2cb4c6b754f24ae93c'
        'f1a60b2f6ed614e44ed4712767914529ef3984a3532bbc24a88dac7147de3a23f2563c16887df641b0be0d738006553067cc77d864cb16b00696930ecdffe028')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  patch -Np1 -i ../"${pkgname}-1.12.0-fix_ttl.patch"
  autoreconf -vfi
  intltoolize -f
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-dssi \
              --with-lv2 \
              --with-vst \
              --with-pandoc
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  cp .libs/${pkgname}_lv2*.so data/${pkgname}.lv2/
  lv2lint -Mpack \
          -I "data/${pkgname}.lv2" \
          "http://code.google.com/p/amsynth/amsynth"
}

package() {
  depends+=('libatk-1.0.so' 'libfreetype.so' 'libgdk-x11-2.0.so'
  'libgio-2.0.so' 'libglib-2.0.so' 'libgobject-2.0.so' 'libgtk-x11-2.0.so'
  'libharfbuzz.so' 'libpangocairo-1.0.so' 'libpangoft2-1.0.so'
  'libpango-1.0.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
