# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Alessio Biancalana <dottorblaster@gmail.com>

pkgname=amsynth
pkgver=1.12.3
pkgrel=1
pkgdesc="Analogue Modeling SYNTHesizer"
arch=(x86_64)
url="https://amsynth.github.io/"
license=(GPL2)
groups=(dssi-plugins lv2-plugins pro-audio vst-plugins)
depends=(cairo gcc-libs glibc)
makedepends=(atk autoconf-archive dssi fontconfig freetype2 gdk-pixbuf2 glib2
gtk2 harfbuzz intltool jack ladspa liblo lv2 pandoc)
checkdepends=(lv2lint)
optdepends=('alsa-lib: for standalone application'
            'dssi-host: for DSSI plugin'
            'jack: for standalone application'
            'liblo: for standalone application and DSSI plugin'
            'new-session-manager: for session management with the standalone application'
            'lv2-host: for LV2 plugin'
            'vst-host: for VST plugin')
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/amsynth/amsynth/archive/release-${pkgver}.tar.gz"
  "${pkgname}-1.12.3-lv2_invalid_word.patch::https://github.com/amsynth/amsynth/pull/200/commits/aacf6c21d833ce35f41efac2ee8ca272b5eb948e.patch"
)
sha512sums=('8bb7386b84b9eae4e0704a2077e82de6849d525c391bef6cbd3813d61d2c108b9a42310e47b8329a4077868d536b6c2e2f89977d2980aa1e338a19c92523bb6a'
            'c988d330d5275ebf8b6ebe153288fc42b07bf6832ada7eace9798e1aa4805581b1159921956da724bde1bd4abe2a6572dd88e801c35e32960cfb009fe0d6df69')
b2sums=('b7bf237acfde07df445681bfdfa60b4ef9ed32ee7b7a82e8d56dbb2a2f56887168408713978c68187d51ab3179d1247218b6688740f9b3ebf8c0c85437e80846'
        '85fc6830a97ce1217dcce77a2d59506399117e41fc4fbd3239e3ba53d8b31537320961631b65ff34277135738bbad654b5c9632bae81d4a6ff5ac8f6531fc94c')

prepare() {
  mv -v "${pkgname}-release-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # fix an issue with invalid word in one of the lv2 turtle files:
  # https://github.com/amsynth/amsynth/issues/199
  patch -Np1 -i ../"${pkgname}-1.12.3-lv2_invalid_word.patch"
  autoreconf -fiv
  intltoolize -f
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --with-alsa \
              --with-jack \
              --with-dssi \
              --with-lv2 \
              --with-vst \
              --with-pandoc
  make
}

check() {
  cd "${pkgname}-${pkgver}"
  cp .libs/${pkgname}_lv2*.so data/${pkgname}.lv2/
  lv2lint -Mpack -I "data/${pkgname}.lv2" "http://code.google.com/p/amsynth/amsynth"
}

package() {
  depends+=(libatk-1.0.so libfontconfig.so libfreetype.so libgdk_pixbuf-2.0.so
  libgdk-x11-2.0.so libgio-2.0.so libglib-2.0.so libgobject-2.0.so
  libgtk-x11-2.0.so libharfbuzz.so libpangocairo-1.0.so libpangoft2-1.0.so
  libpango-1.0.so)

  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,NEWS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
