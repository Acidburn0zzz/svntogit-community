# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-gtk2
pkgver=1.8.16
pkgrel=1
pkgdesc="A configurable set of widget styles for KDE and Gtk"
arch=('i686' 'x86_64')
url="http://www.kde-look.org/content/show.php?content=40492"
license=('GPL')
depends=('gtk2')
makedepends=('cmake')
groups=('qtcurve')
source=("http://craigd.wikispaces.com/file/view/QtCurve-Gtk2-${pkgver}.tar.bz2")
md5sums=('8923caaa0704f6a30252a5eadb015d76')

build() {
  cd "${srcdir}"

  mkdir -p build
  cd build/
  cmake ../QtCurve-Gtk2-${pkgver} \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
