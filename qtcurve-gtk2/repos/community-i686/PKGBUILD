# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-gtk2
pkgver=1.8.15
pkgrel=2
pkgdesc="A configurable set of widget styles for KDE and Gtk"
arch=('i686' 'x86_64')
url="http://www.kde-look.org/content/show.php?content=40492"
license=('GPL')
depends=('gtk2')
makedepends=('cmake')
groups=('qtcurve')
options=('!libtool')
source=("http://craigd.wikispaces.com/file/view/QtCurve-Gtk2-${pkgver}.tar.bz2"
         cairo_flush_fix.patch)
md5sums=('00054b1923f995fa55e0573730b9f3a6'
         '3aec00bd00af49ed42a47855ec37f670')

build() {
  cd "${srcdir}"

  patch -Np1 -i ${srcdir}/cairo_flush_fix.patch -d QtCurve-Gtk2-${pkgver}

  mkdir -p build
  cd build/
  cmake ../QtCurve-Gtk2-${pkgver} \
      -DCMAKE_BUILD_TYPE=Release \
      -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}"/build
  make DESTDIR="${pkgdir}" install
}
