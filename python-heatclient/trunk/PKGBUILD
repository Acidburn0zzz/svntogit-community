# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-heatclient
pkgver=2.5.0
pkgrel=1
pkgdesc="OpenStack Orchestration API Client Library"
arch=('any')
url="https://docs.openstack.org/python-heatclient/latest/"
license=('Apache')
depends=('python-babel' 'python-pbr' 'python-cliff' 'python-iso8601' 'python-osc-lib'
         'python-prettytable' 'python-oslo-i18n' 'python-oslo-serialization' 'python-oslo-utils'
         'python-keystoneauth1' 'python-swiftclient' 'python-yaml' 'python-requests' 'python-six')
checkdepends=('python-stestr' 'python-testscenarios' 'python-requests-mock')
source=("https://github.com/openstack/python-heatclient/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('c60ad8cdca9349e5d135ac3a39365588dcb7a64a77684a85c4497cc8db103ff1b9e5077ed9c67688db89c7c4d336912898b826fb9272c1ea94fb2b07b6e0e2ab')

export PBR_VERSION=$pkgver

build() {
  cd python-heatclient-$pkgver
  python setup.py build
}

check() {
  cd python-heatclient-$pkgver
  stestr run
}

package() {
  cd python-heatclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
