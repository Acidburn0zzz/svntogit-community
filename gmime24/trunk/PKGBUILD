# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Ben <ben@benmazer.net>

pkgname=gmime24
pkgver=2.4.32
pkgrel=1
pkgdesc="Core mime parsing library"
arch=(i686 x86_64)
license=('GPL')
url="http://spruce.sourceforge.net/gmime/"
depends=('glib2' 'zlib')
makedepends=('pkgconfig')
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/gmime/2.4/gmime-${pkgver}.tar.xz)
md5sums=('46a52361d43769d20dbd87aa225bc3f9')

build() {
  # get rid of that .wapi errors in fakeroot
  export MONO_SHARED_DIR="${srcdir}/weird"
  mkdir -p "${MONO_SHARED_DIR}"

  cd ${srcdir}/gmime-${pkgver}
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr \
	--disable-mono \
	--disable-static
  make
}

package() {
  cd ${srcdir}/gmime-${pkgver}
  make DESTDIR=${pkgdir} install
  # These are gmime alternatives for the same shareutils tools
  rm -rf ${pkgdir}/usr/bin/uuencode ${pkgdir}/usr/bin/uudecode ${pkgdir}/usr/share
}
