# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.1
pkgrel=1
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='http://www.fresse.org/dateutils/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha1sums=('2e27914cfd15bc1c9cd7c13fdf10f705477d9d97'
          'SKIP')
sha512sums=('c13461de6dad483b0014692a2319a7bf9ed1677415b8ee168131b397086f75725eb8b4e833d8b4a22f4aa726d6aca74be2597c2e07ef75a036cfea9bf67ccf1b'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-old-links=no
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
