# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>

pkgname=dateutils
pkgver=0.4.5
pkgrel=2
pkgdesc='nifty command line date and time utilities; fast date calculations and conversion in the shell'
url='https://www.fresse.org/dateutils/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
source=("$pkgname-$pkgver.tar.xz::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "$pkgname-$pkgver.tar.asc::https://github.com/hroptatyr/$pkgname/releases/download/v$pkgver/$pkgname-$pkgver.tar.asc")
validpgpkeys=('6CB0D61E23A4275CC2CFA16194C9A1AC82C9390E')
sha1sums=('32997bb9fc2f6e20bb0a44ee13f01d6e1e73ecb8'
          'SKIP')
sha512sums=('91d7b83dc70a0f46920c6bb5b1f456d6b3b5a8f9511218ac74888006501adef5c938f95e424ca8537e2a69bf632b2a08ef4a2536d89367089afb3da55cde70c7'
            'SKIP')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    ./configure --prefix=/usr --with-old-links=no
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    make
}

check() {
    cd "$srcdir/$pkgname-$pkgver"
    make check
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
    install -Dm0644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
