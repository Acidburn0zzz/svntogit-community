# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=shake
pkgname=haskell-shake
pkgver=0.18.3
pkgrel=21
pkgdesc="Build system library, like Make, but more accurate dependencies."
url="https://shakebuild.com"
license=("BSD")
arch=('x86_64')
depends=('ghc-libs' 'haskell-extra' 'haskell-filepattern' 'haskell-hashable' 'haskell-heaps'
         'haskell-js-dgtable' 'haskell-js-flot' 'haskell-js-jquery' 'haskell-primitive'
         'haskell-random' 'haskell-unordered-containers' 'haskell-utf8-string')
makedepends=('ghc' 'haskell-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d7d3ac1c7db8cec76df7e09d115f54be09cd66f95740c27ad807e3fa35b9ea37d20874c279b22d62b173b52205050a7bcaa805a265dab0fe73776e9a0b9a36db')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/ghc --make/ghc -dynamic --make/' src/Test/Command.hs
    sed -e 's/runhaskell -package=Cabal Setup.hs configure/runhaskell -package=Cabal Setup.hs configure --enable-executable-dynamic --disable-library-vanilla/' \
        -e 's/ghc -fno-code/ghc -dynamic -fno-code/' \
        -i src/Test/Docs.hs
    sed -i 's/cmd "ghc" flags/cmd "ghc" "-dynamic" flags/' src/Test/Self.hs src/Test/SelfMake.hs
    sed -i 's/ghc --make/ghc -dynamic --make/' docs/manual/build.sh
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --datasubdir="$pkgname"  --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-portable -f-cloud
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
