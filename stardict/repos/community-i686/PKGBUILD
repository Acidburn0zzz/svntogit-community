# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>
# Contributor: Neil Lin <neil@vip.url.com.tw>

pkgname=stardict
pkgver=3.0.3
pkgrel=2
pkgdesc='International dictionary software'
arch=('i686' 'x86_64')
url='http://www.stardict.org/'
license=('GPL')
depends=('enchant' 'gtk2' 'libsigc++' 'libsm')
makedepends=('intltool' 'popt' 'gnome-doc-utils' 'gnome-common' 'libmysqlclient')
options=('!libtool' '!emptydirs')
source=("http://stardict-3.googlecode.com/files/stardict-${pkgver}.tar.bz2"
        'crash-empty-dict.patch'
        'gcc46.patch'
        'glib2.patch'
        'zlib.patch')
sha1sums=('5043c0918963cd4d40e6d6289353f295766ec74f'
          '97af8fdb73c8f04674234b629c6867254e9bd043'
          '6605a9cdfc4abc2a0ab4462d1228233d34806f0a'
          '467880bca5e89aa25cb4a57a9e1caba9a8841822'
          '024ac5eb75a11c96569e31021a4bd65463b3cf5b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../crash-empty-dict.patch
	patch -p1 -i ../gcc46.patch
	patch -p1 -i ../glib2.patch
	patch -p1 -i ../zlib.patch
	./autogen.sh # gnome-autogen.sh requires gnome-common

	./configure \
		PKG_CONFIG=/usr/bin/pkg-config \
		--prefix=/usr \
		--sysconfdir=/etc \
		--mandir=/usr/share/man \
		--disable-gnome-support \
		--disable-schemas-install \
		--disable-espeak \
		--disable-gucharmap \
		--disable-festival \
		--disable-updateinfo \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
