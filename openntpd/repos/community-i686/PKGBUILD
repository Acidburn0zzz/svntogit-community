# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=openntpd
pkgver=5.9p1
pkgrel=2
pkgdesc='Free, easy to use implementation of the Network Time Protocol'
url='http://www.openntpd.org/'
arch=('i686' 'x86_64')
license=('BSD')
depends=('openssl')
conflicts=('ntp')
backup=('etc/ntpd.conf')
install=${pkgname}.install
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenNTPD/${pkgname}-${pkgver}.tar.gz{,.asc}
        openntpd.tmpfiles
        openntpd.service)
sha512sums=('227a4d42f43f4abfaa97fb85cf121d3b9a6646259faeda785dbeb3e4a27285a7f95daf96e72135871a31f772895f3b66c10bd628c87e453507ce69102f5e1213'
            'SKIP'
            '15ecb1a8673944d73b9675e812b134531cd99ae7e5a62c1415bfd0cc58ab9d86bd4a6342a5f13e3d7d05af6ae7d9fe2cc81c88df1c20dc94e3f5e37b51e05c04'
            '21a66f72be71a6dc294ed4d841fb76769d3cd6ae8049fb95117591534bb87afd3bc80a425d5e9ee41b1f107ba440a8c9cf70c5c7d771f4c5c8a0c722813e3dd9')
validpgpkeys=('A1EB079B8D3EB92B4EBD3139663AF51BD5E4D8D5') # Brent Cook <bcook@openbsd.org>

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --with-privsep-user=ntp \
    --with-privsep-path=/run/openntpd \
    --localstatedir=/var/lib/ntp \
    --with-adjtimex
  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
  sed -i 's/\*/0.0.0.0/' "${pkgdir}/etc/ntpd.conf"

  install -Dm 644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
  install -Dm 644 "${srcdir}/openntpd.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/openntpd.conf"
  install -Dm 644 "${srcdir}/openntpd.service" "${pkgdir}/usr/lib/systemd/system/openntpd.service"

  install -dm 755 "${pkgdir}/usr/lib/systemd/ntp-units.d"
  echo ${pkgname}.service > "${pkgdir}/usr/lib/systemd/ntp-units.d/${pkgname}.list"
}

# vim: ts=2 sw=2 et:
