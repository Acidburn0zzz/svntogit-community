# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: Mark Rosenstand <mark@borkware.net>
# Contributor: Giorgio Lando <patroclo7@gmail.com> (adjtimex patch)
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=openntpd
pkgver=3.9p1
pkgrel=18
pkgdesc="Free, easy to use implementation of the Network Time Protocol."
url="http://www.openntpd.org/"
arch=('x86_64' 'i686')
license=('BSD')
depends=('openssl')
backup=('etc/ntpd.conf' 'etc/conf.d/openntpd')
install=$pkgname.install
source=(ftp://ftp.openbsd.org/pub/OpenBSD/OpenNTPD/$pkgname-$pkgver.tar.gz
	'openntpd' 'openntpd.conf' 'linux-adjtimex.patch' 'openntpd.tmpfiles' 'openntpd.service')
sha256sums=('83dd7c1e8ec8b4567afe49af539271b5a73562fb7a3ca51df73eccba89ec8c49'
            'b486b7034b8d3d790b6bc1793dd8a44c68b82c8dc833b617c60674b0a7c9d98f'
            '77a0d1f3439d779c2ce259becf3fa583c21a16682a4f5327094da2e9837f6f96'
            '3abcec94c072b5913ef7156a22049d796ff04e19105befe1ea81a66522928fd2'
            'fe12841110c3c080519e248988c4b6334f54bd9646b015753c7e15de2a9600c5'
            '7ad4d3ce4af3a6ff47ba2e5428092139666ad821101dafe1599b209d53f6943f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i ../linux-adjtimex.patch
  autoreconf -fi
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-privsep-user=ntp \
    --with-privsep-path=/run/openntpd/ \
    --with-adjtimex
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/$pkgname" "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 "$srcdir/$pkgname.conf" "$pkgdir/etc/conf.d/$pkgname"
  install -Dm644 "$srcdir/$pkgname-$pkgver/LICENCE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENCE"
  sed -i 's/\*/0.0.0.0/' $pkgdir/etc/ntpd.conf

  install -Dm644 "$srcdir/openntpd.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/openntpd.conf"
  install -Dm644 "$srcdir/openntpd.service" "$pkgdir/usr/lib/systemd/system/openntpd.service"

  # avoid conflict with filesystem>=2012.06
  rm -r "$pkgdir/run"

  install -dm755 "$pkgdir/usr/lib/ntp-units.d"
  echo "$pkgname.service" > "$pkgdir/usr/lib/ntp-units.d/$pkgname.list"
}

# vim:set ts=2 sw=2 et:
