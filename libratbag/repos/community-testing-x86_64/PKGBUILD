# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libratbag
pkgver=0.9.906
pkgrel=1
pkgdesc='A DBus daemon to configure gaming mice'
arch=('x86_64')
url='https://github.com/libratbag/libratbag'
license=('MIT')
depends=('glib2' 'libevdev' 'libudev.so' 'python' 'python-evdev')
makedepends=('meson' 'swig' 'git' 'python-sphinx' 'python-sphinx_rtd_theme')
checkdepends=('check' 'valgrind' 'python-gobject' 'python-lxml')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('f241c931a566b1b008c5cffee59f782ef448425e28c47186067e681e6af6ba0bd2f53d3cedac038628464fa72c6dade4d940750d903b69c081911d9d9ed5ee18')
provides=('ratbagd' 'liblur')
conflicts=('ratbagd' 'liblur')

prepare() {
  sed -i 's|sphinx-build3|sphinx-build|' $pkgname-$pkgver/doc/meson.build
}

build() {
  mkdir -p $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson .. \
  	-Dsystemd-unit-dir=/usr/lib/systemd/system \
  	-Ddocumentation=true

  ninja
}

check() {
  cd $pkgname-$pkgver/build

  meson test --no-rebuild
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  # Install documentation
  install -dm 755 "$pkgdir"/usr/share/doc/$pkgname
  cp -r -a --no-preserve=ownership doc/html "$pkgdir"/usr/share/doc/$pkgname

  # Install license
  install -Dm 644 ../COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

