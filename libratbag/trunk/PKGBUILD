# $Id$
# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libratbag
pkgver=0.9.902
pkgrel=2
pkgdesc='A DBus daemon to configure gaming mice'
arch=('x86_64')
url='https://github.com/libratbag/libratbag'
license=('MIT')
depends=('glib2' 'libevdev' 'libudev.so' 'python' 'python-evdev')
makedepends=('meson' 'swig' 'git')
checkdepends=('check' 'valgrind' 'python-gobject' 'python-lxml')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('7c4f3c7c2864aadc6b08bd184bf924376b7ec0ac6bc5725ec0693284c39a94ee3440640f57e402c25068112866508743da6eddcdcc4d99dc5c706fda9927e03a')
provides=('ratbagd' 'liblur')
conflicts=('ratbagd<=0.4')

build() {
  mkdir -p "$srcdir"/$pkgname-$pkgver/build
  cd "$srcdir"/$pkgname-$pkgver/build

  meson --prefix=/usr ..

  ninja
}

check() {
  cd "$srcdir"/$pkgname-$pkgver/build

  meson test -v
}

package() {
  cd "$srcdir"/$pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install

  install -Dm 644 ../COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
