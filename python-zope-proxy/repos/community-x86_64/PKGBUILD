# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-proxy
pkgname=('python-zope-proxy' 'python2-zope-proxy')
pkgver=4.2.1
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('i686' 'x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface')
checkdepends=('python-zope-security' 'python2-zope-security')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('d1351901cc56964f3cda8d299d93e5644685e13fd8313a8dd6ea162cfcc73ef54b3a09e102e6cfc5fa2136c034526ec6e9807b0d9b1112346d67386f38f7b28e')

prepare() {
  cp -a zope.proxy-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build

  cd "$srcdir"/zope.proxy-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py test || :

  cd "$srcdir"/zope.proxy-$pkgver-py2
  python2 setup.py test
}

package_python-zope-proxy() {
  depends=('python-setuptools' 'python-zope-interface')

  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-proxy() {
  depends=('python2-setuptools' 'python2-zope-interface')

  cd zope.proxy-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
