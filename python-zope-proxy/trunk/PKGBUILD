# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-proxy
pkgver=4.3.4
pkgrel=1
pkgdesc="Generic Transparent Proxies"
arch=('x86_64')
url="https://github.com/zopefoundation/zope.proxy"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface')
checkdepends=('python-zope-security' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.proxy/archive/$pkgver.tar.gz")
sha512sums=('6517ee8b56948acbc608d7e9d7af06351a941c35294b8a70e8bed7a0f5e7811832d61ea6c6e506a36752aebbb12a51eb2b7539c9129fae041cce5efe8decb87a')

build() {
  cd "$srcdir"/zope.proxy-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.proxy-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.8:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd zope.proxy-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
