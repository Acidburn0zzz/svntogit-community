# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>

pkgname=vim-systemd
pkgver=20170306
pkgrel=1
pkgdesc='ViM syntax highlighting for systemd unit files'
arch=('any')
url="https://fedorapeople.org/cgit/wwoods/public_git/vim-scripts.git/"
license=('unknown')
depends=('vim')
group=('vim-plugins')
source=("syntax-systemd.vim::https://fedorapeople.org/cgit/wwoods/public_git/vim-scripts.git/plain/syntax/systemd.vim"
        "ftdetect-systemd.vim::https://fedorapeople.org/cgit/wwoods/public_git/vim-scripts.git/plain/ftdetect/systemd.vim"
        "syntax-udev.vim::https://fedorapeople.org/cgit/wwoods/public_git/vim-scripts.git/plain/syntax/udev.vim"
        "ftdetect-udev.vim::https://fedorapeople.org/cgit/wwoods/public_git/vim-scripts.git/plain/ftdetect/udev.vim"
        '0001-Add-more-syntax-keywords-for-service-and-socket-file.patch')
sha256sums=('45e97770953d3e64be3d0e1fd2b5c75f106cbfcddd989d10eabd913afaa76e80'
            'eb9837c5ca35cc4cc34a136805b03b0d5dc7e153ed1dac0cb3efda1479b0c19f'
            '3fea3621f37fc0db15555b0e34b1e3be9fdaf83724c9143722498ee483407bf9'
            'b96c381b8355a102946751d2e9e9e95025e70dfe634a44e416ca4ce291eb20b8'
            'a04f02a99bb28cbd0aeb99e349fc39aa0e16628aabfd58cee54a76c7f0b801ad')

pkgver() {
  date +%Y%m%d
}

prepare() {
  # Patch submitted upstream but not applied to Git tree
  patch -Np0 --follow-symlinks -i "$srcdir/0001-Add-more-syntax-keywords-for-service-and-socket-file.patch"
}

package() {
  install -Dm644 syntax-systemd.vim "$pkgdir/usr/share/vim/vimfiles/syntax/systemd.vim"
  install -Dm644 ftdetect-systemd.vim "$pkgdir/usr/share/vim/vimfiles/ftdetect/systemd.vim"
  install -Dm644 syntax-udev.vim "$pkgdir/usr/share/vim/vimfiles/syntax/udev.vim"
  install -Dm644 ftdetect-udev.vim "$pkgdir/usr/share/vim/vimfiles/ftdetect/udev.vim"
}

# vim:set ts=2 sw=2 et:
