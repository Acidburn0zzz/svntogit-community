# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=puzzles
pkgver=10282
pkgrel=1
pkgdesc="Simon Tatham's Portable Puzzle Collection"
arch=('i686' 'x86_64')
url="http://www.chiark.greenend.org.uk/~sgtatham/puzzles/"
license=('MIT')
makedepends=('pkgconfig' 'subversion' 'xorg-server-xvfb' 'imagemagick')
depends=('gtk2' 'desktop-file-utils')
install=puzzles.install
source=("svn://svn.tartarus.org/sgt/puzzles#revision=$pkgver")
md5sums=("SKIP")

prepare() {
    cd ${srcdir}/${pkgname}

    ./mkfiles.pl
    sed -i 's|\$(gamesdir)|\$(bindir)|' Makefile.gtk
    sed -i 's_\$icondir\/\$id-48d24.png_$id_' desktop.pl
}

build() {
    cd ${srcdir}/${pkgname}

    make -f Makefile.gtk prefix=/usr

    # Generate icons.

    cd ${srcdir}/${pkgname}/icons

    xvfb-run make pngicons
}

package() {
    cd ${srcdir}/${pkgname}/icons

    install -d ${pkgdir}/usr/share/icons/hicolor/48x48/
    for oldname in *-48d24.png; do
        newname=$(sed 's/\(\w*\)-48d24.png/\1.png/' <<< ${oldname})
        install -Dm644 ${oldname} ${pkgdir}/usr/share/icons/hicolor/48x48/${newname}
    done

    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/share/applications
    ./desktop.pl ${pkgdir}/usr/share/applications \
                 /usr/bin \
                 /usr/share/icons/

    cd ${srcdir}/${pkgname}

    install -d ${pkgdir}/usr/{bin,share/licenses/${pkgname}}
    install -Dm644 LICENCE ${pkgdir}/usr/share/licenses/${pkgname}/

    make -f Makefile.gtk prefix=/usr DESTDIR=${pkgdir} install

    #fix FS#14600
    for oldname in net cube blackbox; do
        newname=puzzles-${oldname}
        mv ${pkgdir}/usr/bin/${oldname} ${pkgdir}/usr/bin/${newname}
        sed -i "s_Exec=/usr/bin/${oldname}_Exec=/usr/bin/${newname}_" \
            ${pkgdir}/usr/share/applications/${oldname}.desktop
    done
}
