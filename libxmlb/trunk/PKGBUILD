# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.1.6
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL')
depends=('glib2' 'libstemmer')
makedepends=('meson' 'gobject-introspection' 'gtk-doc')
checkdepends=('shared-mime-info')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('4903cae469fd292f1e7af72fd15ba0f351b924cc5fd6c18600ec1032bbe0e5ad' 'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
    cd ${pkgname}-${pkgver}
    arch-meson ../build
    ninja -v -C ../build
}

check() {
    cd build
    meson test
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
}
