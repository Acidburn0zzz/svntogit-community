# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libxmlb
pkgver=0.1.3
pkgrel=1
pkgdesc="A library to help create and query binary XML blobs"
arch=('x86_64')
url="https://github.com/hughsie/libxmlb"
license=('LGPL')
depends=('glib2')
makedepends=('meson' 'gobject-introspection' 'gtk-doc')
checkdepends=('shared-mime-info')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('b609a95d078ab956231a43fd082382b386ed2f90e3fe5e8b785c4278a1b4787e' 'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
    cd ${pkgname}-${pkgver}
    arch-meson ../build
    ninja -v -C ../build
}

check() {
    cd build
    meson test
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    # Remove the tests
    #rm -r ${pkgdir}/usr/share/installed-tests/
}
