# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.4
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' "alsa-lib=${pkgver}")
provides=('libasound.so' 'libatopology.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/alsa-project/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('cacff66b888ebd9646a2d1794613b38c2fdacc290f4c71e0f53b25d53372af977d3a01bed323f58fd87e79c4556e8166d242fe7632e2a65dda4fd26b466a4a5c')
b2sums=('2e86009d638e600dafe2722d68c190726f31b268f11966f7f3d65efb56244e0bf7d410c75d33f2310d0069d1af0083e108d149a6022528efe55b9bb3b9b544ab')

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -vfi
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --prefix=/usr \
              --libdir=/usr/lib32 \
              --without-debug \
              --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
