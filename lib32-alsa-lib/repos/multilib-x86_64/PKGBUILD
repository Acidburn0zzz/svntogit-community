# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

_name=alsa-lib
pkgname=lib32-alsa-lib
pkgver=1.2.3
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
license=('LGPL2.1')
depends=('lib32-glibc' 'alsa-lib')
provides=('libasound.so' 'libatopology.so')
source=("https://www.alsa-project.org/files/pub/lib/${_name}-$pkgver.tar.bz2")
sha512sums=('567c094914833af511bd7b175b0865ae4e4483ae68a7d4f90a34b12d7f077782835eec24257a7928631c104c78eff588f7b92068aade37e3c4806d79353758a8')
b2sums=('522c5c2a4bd3bc13d96f64781270647271e131042e0a6ed0f2104d9cb9f93341af1c14d000d5acb9c4c327e10202b0e9f280e59f355819f8861ae023a5a0f0a8')

prepare() {
  cd "${_name}-$pkgver"
  autoreconf -vfi
}

build() {
  cd "${_name}-$pkgver"

  export CC='gcc -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

check() {
  cd "${_name}-$pkgver"
  export LD_LIBRARY_PATH="${PWD}/src/.libs/:${LD_LIBRARY_PATH}"
  make -k check
}

package() {
  cd "${_name}-$pkgver"
  make DESTDIR="$pkgdir" install
  rm -r "$pkgdir"/usr/{bin,include,share}
}
