# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>

pkgname=lib32-alsa-lib
pkgver=1.1.9
pkgrel=1
pkgdesc="An alternative implementation of Linux sound support (32 bit)"
arch=('x86_64')
url="https://www.alsa-project.org"
depends=('lib32-glibc' 'alsa-lib')
makedepends=('gcc-multilib')
license=('LGPL')
source=(ftp://ftp.alsa-project.org/pub/lib/alsa-lib-$pkgver.tar.bz2)
sha256sums=('488373aef5396682f3a411a6d064ae0ad196b9c96269d0bb912fbdeec94b994b')

build() {
  cd alsa-lib-$pkgver

  export CC='gcc -m32'
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  ./configure --without-debug --prefix=/usr --libdir=/usr/lib32 --disable-python
  make
}

package() {
  cd alsa-lib-$pkgver
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/{bin,include,share}
}
