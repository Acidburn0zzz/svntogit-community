# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-turtleart
_pkgname=TurtleBlocks
pkgver=218
pkgrel=1
pkgdesc="Logo-inspired graphical turtle for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Turtle_Art"
license=('MIT')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/TurtleArt/$_pkgname-$pkgver.tar.bz2)
sha256sums=('b6bbdb86ce955ee981d128e84f853b37fefab641a5c69fb12c0984a00232b36c')

prepare() {
  cd $_pkgname-$pkgver
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
