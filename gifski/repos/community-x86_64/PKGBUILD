# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor: Philipp Wolfer <ph.wolfer@gmail.com>

pkgname=gifski
pkgver=0.10.1
pkgrel=1
pkgdesc='GIF encoder based on libimagequant (pngquant). Squeezes maximum possible quality from the awful GIF format.'
arch=('x86_64')
url='https://gif.ski'
license=('AGPL3')
makedepends=('rust' 'clang' 'openmp')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ImageOptim/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('fa84f06620b498c90d1a4a2b83792590271f5e2ca264ca5c032b9b0eead752f73948b1b60968f52731d8242bd0b9f083b85b206ad3d458c10c39619e4670520a')

build() {
  cd $pkgname-$pkgver

  cargo build --release --features=openmp
}

package() {
  cd $pkgname-$pkgver

  install -Dm 755 target/release/gifski "$pkgdir"/usr/bin/gifski
}

