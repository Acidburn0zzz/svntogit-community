# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=toolbox
pkgver=0.0.15
pkgrel=1
pkgdesc='Unprivileged development environment'
arch=(any)
url='https://github.com/containers/toolbox'
license=(APACHE)
depends=(podman bash)
makedepends=(shellcheck go-md2man bash-completion ninja git meson)
_commit=0ee5b592a2a82cbff0171405d782783e17f4f579
source=("git+$url#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd toolbox
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  arch-meson toolbox build -D profile_dir=/etc/profile.d
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}
