# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: carstene1ns <arch carsten-teibes de> - http://git.io/ctPKG
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Vlad Kolotvin <vlad.kolotvin@gmail.com>

pkgname=emscripten
# To figure out the llvm commit that upstream used for this emscripten release, go to
# https://github.com/emscripten-core/emsdk/blob/master/emscripten-releases-tags.txt
# and note the SHA provided there.
# Then, get commit SHAs from here:
# https://chromium.googlesource.com/emscripten-releases/+/e7e39da9c81faecd9ecf44065cee864d76e4e34d/DEPS
# Replace that pre-filled SHA with the one from the release. Then search for the "llvm_project_revision"
# variable. It has the llvm commit to use here.
_llvm_commit=55fa315b0352b63454206600d6803fafacb42d5e
pkgver=1.39.20
pkgrel=1
pkgdesc="LLVM-based project that compiles C and C++ into highly-optimizable JavaScript for the web"
arch=('x86_64')
url="http://emscripten.org"
license=('custom')
depends=('nodejs' 'python' 'binaryen' 'which')
makedepends=('cmake' 'libxml2' 'git' 'ninja')
optdepends=('java-environment: for using clojure'
            'ruby: for using websockify addon'
            'cmake: for emcc --show-ports')
install=emscripten.install
source=("git+https://github.com/kripken/emscripten#tag=$pkgver"
        git+https://github.com/llvm/llvm-project.git#commit=$_llvm_commit
        "emscripten.sh"
        emscripten-config
        https://patch-diff.githubusercontent.com/raw/emscripten-core/emscripten/pull/11713.patch)
sha512sums=('SKIP'
            'SKIP'
            'a87cf5d4a5ac10a4f84ff02ea577d54b560929dc64457b874cd1cbd88311cf6c4dcfbf3242150f6e556f4ba6efd370a99b9f9065faf494f25d91fc012cd5aa58'
            '4a93ea4291701a509f30541ad534f325b3e4f014a496e030bc19791095640931f18706264fa83a113225c6ee1a2ecd97bdefdc1dd15caaa5799105f7a96e3b7d'
            'b2e0c162335c40ee84968ab8c5bae5083a96f18dcc6c4d4f50e4a7f312d075ad965f9afb0a9d2ca290068ed1ef5fe2599c681162690c645ca1f36f3af592a05f')

prepare() {
  cd emscripten

  patch -Np1 --no-backup-if-mismatch -i "$srcdir"/11713.patch
}

build() {
  cd llvm-project/llvm

  # Inspired from https://github.com/WebAssembly/waterfall/blob/db2ea5eeb11b74cce9b9459be0cc88807744b1b5/src/build.py#L868
  cmake . \
    -Bbuild \
    -GNinja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_SKIP_RPATH=YES \
    -DLLVM_TARGETS_TO_BUILD="X86;WebAssembly" \
    -DLLVM_BUILD_RUNTIME=OFF \
    -DLLVM_TOOL_LTO_BUILD=ON \
    -DLLVM_INSTALL_TOOLCHAIN_ONLY=ON \
    -DLLVM_INCLUDE_EXAMPLES=OFF \
    -DLLVM_INCLUDE_TESTS=OFF \
    -DLLVM_ENABLE_PROJECTS="lld;clang" \
    -DCLANG_INCLUDE_TESTS=OFF
  ninja -C build
}

package() {
  # Install LLVM stuff according to https://github.com/emscripten-core/emscripten/blob/master/docs/packaging.md
  install -d "$pkgdir"/usr/lib
  cp -r "$srcdir"/llvm-project/llvm/build/bin "$pkgdir"/usr/lib/emscripten-llvm

  # Install emscripten
  cd emscripten
  make DESTDIR="$pkgdir"/usr/lib/emscripten install
  install -Dm644 "$srcdir"/emscripten-config "$pkgdir"/usr/lib/emscripten/.emscripten

  install -d "$pkgdir"/usr/share/doc
  ln -s /usr/lib/emscripten/site/source/docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm755 "$srcdir"/emscripten.sh "$pkgdir"/etc/profile.d/emscripten.sh
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
