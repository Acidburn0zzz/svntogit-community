# $Id$
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qbittorrent
pkgver=2.4.10
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt' 'libtorrent-rasterbar>=0.15.4' 'xdg-utils')
makedepends=('boost' 'geoip')
optdepends=('python2: needed for search'
            'geoip: improves peer country resolution')
install=qbittorrent.install
source=(http://downloads.sourceforge.net/sourceforge/qbittorrent/${pkgname}-${pkgver}.tar.gz
        python2.patch)
md5sums=('4954606fb147ed0a23611c151390e615'
         '06807f5d8a1fdca891cb07f239565639')

build() {
  cd ${pkgname}-${pkgver}

  sed -i -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
       -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
       $(find ./ -name '*.py')

  patch -Np1 -i ${srcdir}/python2.patch

  ./configure \
  --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
