# $Id$
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>

pkgname=qbittorrent
pkgver=2.9.2
pkgrel=4
pkgdesc="A bittorrent client written in C++ / Qt4 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt' 'libtorrent-rasterbar' 'xdg-utils')
makedepends=('boost' 'geoip')
optdepends=('python2: needed for search'
            'geoip: improves peer country resolution')
install='qbittorrent.install'
source=("http://downloads.sourceforge.net/sourceforge/qbittorrent/qbittorrent-${pkgver}.tar.gz")
md5sums=('0c2188e0065be57e50c06f5934d5f7a2')

build() {
  cd qbittorrent-${pkgver}

  sed -i \
    -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
    -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find ./ -name '*.py')

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd qbittorrent-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
