# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qupzilla
pkgver=2.2.3
pkgrel=2
pkgdesc="Cross-platform QtWebEngine browser"
arch=(x86_64)
url="https://www.qupzilla.com/"
license=(GPL3)
depends=(qt5-webengine qt5-x11extras qt5-svg)
makedepends=(qt5-tools kwallet libgnome-keyring)
optdepends=('kwallet: kf5 kwallet integration' 'libgnome-keyring: gnome keyring integration')
source=(https://github.com/QupZilla/qupzilla/releases/download/v$pkgver/QupZilla-$pkgver.tar.xz
        qupzilla-tab-loading.patch::"https://github.com/QupZilla/qupzilla/commit/b5ab2747.patch")
sha256sums=('76f4aa19ef3877b68a6b38a6fb460caae4d2d13361412e986366b4907ce16c72'
            'feeb2c3249dbce225d424bb91b8ed015e0465a2a41541a9e0954b5b498f2438e')

prepare() {
  cd QupZilla-$pkgver
  patch -p1 -i ../qupzilla-tab-loading.patch # Fix tab loading animation with Qt 5.10
}

build() {
  cd QupZilla-$pkgver
  export USE_WEBGL=true \
    QUPZILLA_PREFIX=/usr/ \
    KDE_INTEGRATION=true \
    GNOME_INTEGRATION=true
  qmake
  make
}

package() {
  cd QupZilla-$pkgver
  make INSTALL_ROOT="$pkgdir/" install

  # zsh completion
  install -Dm644 linux/completion/_$pkgbase \
    "$pkgdir/usr/share/zsh/site-functions/_$pkgbase"
}
