# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>

pkgbase=bullet
pkgname=('bullet' 'bullet-docs')
pkgver=2.80
pkgrel=1
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and animation"
arch=('i686' 'x86_64')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=('freeglut')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu')
source=(http://bullet.googlecode.com/files/${pkgbase}-${pkgver}-rev2531.tgz
        ${pkgbase}.pc)
md5sums=('d1eec0332110264a21d0fc0f2fb127c3'
         'd1da06deba3b08b884d2212f6838905c')

build() {
  cd ${pkgbase}-${pkgver}-rev2531

  # get a clean build dir
  [[ -d build ]] && rm -rf build
  mkdir build && cd build 

  # generate CMake Makefile
  cmake .. \
       -DCMAKE_INSTALL_PREFIX=/usr \
       -DBUILD_SHARED_LIBS=1 \
       -DBUILD_DEMOS=0 \
       -DBUILD_EXTRAS=1 \
       -DINSTALL_LIBS=1 \
       -DINSTALL_EXTRA_LIBS=1 \
       -DCMAKE_BUILD_TYPE=Release # set =Debug for debugging version

  # compile
  make

  # generate docs
  cd ..
  sed -i 's/GENERATE_HTMLHELP.*//g' Doxyfile
  doxygen
}

package_bullet() {
  optdepends=('bullet-docs: documentation')

  cd ${pkgbase}-${pkgver}-rev2531/build

  # install it
  make DESTDIR=${pkgdir} install

  # fix #29012
  install -Dm644 ../Extras/HACD/hacdVector.inl ${pkgdir}/usr/include/bullet/HACD/hacdVector.inl
  install -Dm644 ../Extras/HACD/hacdCircularList.inl ${pkgdir}/usr/include/bullet/HACD/hacdCircularList.inl

  # manually handle pkgconfig file
  mkdir -p "${pkgdir}"/usr/lib/pkgconfig
  sed "s|VERSION|${pkgver}|g" ${srcdir}/bullet.pc > ${pkgdir}/usr/lib/pkgconfig/bullet.pc

  # install license
  install -Dm644 ../BulletLicense.txt ${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE
}

package_bullet-docs() {
  pkgdesc="Documentation for bullet"
  depends=()

  cd ${pkgbase}-${pkgver}-rev2531

  # install docs
  install -Dm644 Bullet_User_Manual.pdf ${pkgdir}/usr/share/doc/bullet/Bullet_User_Manual.pdf
  cp -r html ${pkgdir}/usr/share/doc/bullet/html  
}
# vim: sw=2 ts=2 et:
