# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-tldextract
pkgver=3.0.0
pkgrel=1
pkgdesc="Accurately separate the TLD from the registered domain and subdomains of a URL, using the Public Suffix List"
arch=('any')
url="https://github.com/john-kurkowski/tldextract"
license=('BSD')
depends=('python-idna' 'python-requests' 'python-requests-file' 'python-filelock')
makedepends=('python-setuptools-scm' 'python-pip')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-pytest-pylint' 'python-responses')
source=("https://pypi.io/packages/source/t/tldextract/tldextract-$pkgver.tar.gz")
sha512sums=('8a5663880b7470e7bfa4440c56da4e22992f8326ead207dea21ac2c02f2361b7e1785b4cb6bae25ccbcdc154f491dda58fa4dc2075e4fa15f69ce0e54ff2419a')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd tldextract-$pkgver
  python setup.py build
}

check() {
  cd tldextract-$pkgver
  python -m pytest --ignore build
}

package() {
  cd tldextract-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # use the snapshot version, because generating a new one requires Internet access and root permission
  ln -s .tld_set_snapshot "$pkgdir"/usr/lib/python3.8/site-packages/tldextract/.tld_set
}

# vim:set sw=2 et:
