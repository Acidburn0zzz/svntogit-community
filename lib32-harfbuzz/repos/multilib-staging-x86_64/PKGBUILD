# Maintainer: Florian Pritz <bluewind@xinu.at>

_pkgbasename=harfbuzz
pkgname=lib32-$_pkgbasename
pkgver=1.3.1
pkgrel=2
pkgdesc="OpenType text shaping engine. (32-bit)"
arch=('x86_64')
url="http://www.freedesktop.org/wiki/Software/HarfBuzz"
license=('MIT')
depends=('lib32-icu' 'lib32-glib2' 'lib32-freetype2' $_pkgbasename)
makedepends=('gcc-multilib' 'ragel')
source=(http://www.freedesktop.org/software/harfbuzz/release/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('a242206dd119d5e6cc1b2253c116abbae03f9d930cb60b515fb0d248decf89a1')

prepare() {
  cd ${_pkgbasename}-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/${_pkgbasename}-${pkgver}"
 
  CFLAGS+=' -DNDEBUG'
  CXXFLAGS+=' -DNDEBUG'
  ./configure --prefix=/usr --libdir=/usr/lib32 --disable-silent-rules --with-graphite2=no --without-cairo
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin}
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
