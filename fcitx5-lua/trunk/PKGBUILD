# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fcitx5-lua
pkgver=0.0.0.20200728
_commit=17c57a79a550a1f94b12a3279e6014ce0f8f1b4e
pkgrel=1
pkgdesc="Lua support for Fcitx 5"
arch=('x86_64')
url="https://github.com/fcitx/fcitx5-lua"
license=('GPL')
depends=('fcitx5' 'lua')
makedepends=('extra-cmake-modules' 'ninja')
source=("https://github.com/fcitx/fcitx5-lua/archive/$_commit/fcitx5-lua-$_commit.tar.gz"
        unbreak-lua54.patch)
sha512sums=('62b694d7406028031eb11bef6350c5c78d46a2a3778a1dbb72608500618e42e64643566c8b06ae7bcf928ccc0db8ff13f284750eef5070cc9343cef717c46bbc'
            '74b1fa15c36fd7a58c259fba85876a451d4ca0b154585162235b9ae16a015368da0b9c97d88c591a6cb03cd20357a5b450a6073cc68c8cf8730ed12298401dfa')

prepare() {
  cd $pkgname-$_commit
  patch -Rp1 -i ../unbreak-lua54.patch
}

build(){
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
