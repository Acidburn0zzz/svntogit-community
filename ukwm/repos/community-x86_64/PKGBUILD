# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukwm
pkgver=1.1.14
pkgrel=1
pkgdesc="A lightweight GTK+ window manager"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukwm"
groups=('ukui')
depends=('gnome-desktop' 'gobject-introspection-runtime' 'libinput' 'libxkbcommon-x11' 'ukui-settings-daemon' 'upower')
makedepends=('gobject-introspection' 'mate-common')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukwm/archive/v$pkgver.tar.gz"
        0001-Fix-building-with-libglvnd-1.2.0.patch)
sha512sums=('ce3416d9b2d0616a51665c040db9c71654cbc8c0479aaed3f481d5da8baa97705e0d5de95ea0c904adec35efd0616c38219eee979f869aa9507c872570085fa0'
            '26f1cb91152c8279162eb43d27964815ebf87c14ecaa486a14b53bd85c4c58f90f1b4fad4c9d72a7ac3feb283d95f19be1fb2a371eee2f93a04b1871aae4343d')

prepare() {
  cd $pkgname-$pkgver
  patch -d cogl -p1 -i ../../0001-Fix-building-with-libglvnd-1.2.0.patch
  NOCONFIGURE=1 ./autogen.sh

  sed -i 's/GETTEXT_MACRO_VERSION = 0.19/GETTEXT_MACRO_VERSION = 0.20/' po/Makefile.in.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc --enable-introspection --enable-compile-warnings=yes
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
