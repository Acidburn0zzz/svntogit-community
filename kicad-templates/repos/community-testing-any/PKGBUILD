# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=kicad-templates
pkgver=6.0.0
pkgrel=1
pkgdesc="KiCad project templates & worksheets"
arch=('any')
url="https://www.kicad.org"
license=('custom:CC-BY-SA-4.0')
makedepends=('cmake')
source=("https://gitlab.com/kicad/libraries/kicad-templates/-/archive/$pkgver/kicad-templates-$pkgver.tar.bz2")
sha512sums=('b880a728bf0b2ef5512fdf1d5b76b5fe60ce6c3791142d4ff81f118b131abe1a45ab7af095e3a133e5637ad0cf67b85e74b2cb5f3f8661025c1511267d8046d0')
b2sums=('9f72475b0b15c682e13fda9bc6bdf90c851fdfc6b2111c88507fce4cc6badbf1510e919d2d484c6397eaa12dec04a23a98d9b26c33d78b6e92a82b376b25f24c')

build() {
  cmake \
    -B build \
    -S "$pkgname-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname-$pkgver/LICENSE.md"
}

