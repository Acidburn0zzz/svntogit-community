# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=python-rawkit
_pyname=rawkit
pkgver=0.6.0
pkgrel=3
pkgdesc='CTypes based LibRaw bindings'
arch=('any')
url='https://github.com/photoshell/rawkit'
license=('MIT')
depends=('libraw' 'python')
makedepends=('python-setuptools')
source=("$_pyname-$pkgver.tar.gz::https://github.com/photoshell/$_pyname/archive/v$pkgver.tar.gz")
sha256sums=('c07971ddbca9568cef93753ebb06a72b5b622f9b827a5d8bf96f2a137cc4219e')

build() {
  cd $_pyname-$pkgver
  python setup.py build
}

package(){
  cd $_pyname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
