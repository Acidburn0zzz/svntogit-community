# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=homebank
pkgver=4.5
pkgrel=1
pkgdesc="Free, easy, personal accounting for everyone"
arch=('i686' 'x86_64')
url="http://homebank.free.fr/"
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'hicolor-icon-theme' 'libofx' 'librsvg' 'perlxml' 'shared-mime-info')
makedepends=('intltool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://homebank.free.fr/public/$pkgname-$pkgver.tar.gz)
sha256sums=('3c6dcb19fd975037857f84fd6a186a5b0b84f590f37ead0d37868fa484c6e547')

build() {
  cd "${srcdir}/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
