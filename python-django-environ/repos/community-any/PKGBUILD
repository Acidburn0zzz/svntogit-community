# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-environ
pkgname=python-django-environ
pkgver=0.7.0
pkgrel=1
pkgdesc="Utilize 12factor inspired environment variables to configure your Django application"
arch=('any')
url="https://github.com/joke2k/django-environ"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b2e3fe9bfbcd7b37a5fa72e1a850488cbeb606a86cb05699c2a6422e9b1ba9918548d2dcd2f89c1eb90b6f8d27e0a835d5e02f86d07cd71646f7f8d98c4c28a0')
b2sums=('f701e0d2d43899e6e4e0c0360d5a15727918d873eca890b8849536fe7e9f5b7f67538c75a3083758440bf9d2e4bc4e7bbec6e0e71498a9d31608c9354324a9df')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  python setup.py test
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGELOG,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
