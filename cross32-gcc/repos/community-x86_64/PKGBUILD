# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Mikhail Vorozhtsov <mikhail.vorozhtsov@gmail.com>

pkgname=cross32-gcc
pkgver=4.5.0
pkgrel=6
_snapshot=4.5-20100610
_dirver=$pkgver
test -n "$_snapshot" && _dirver=$_snapshot
pkgdesc="The GNU Compiler Collection (i686 version)"
arch=('x86_64')
_target=i686-unknown-linux-gnu
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'custom')
depends=('cross32-binutils' 'lib32-glibc>=2.12-5'
         "lib32-gcc-libs=${pkgver}" 'libmpc>=0.8.2-2' 'cloog-ppl>=0.15.8'
         'zlib' 'libelf')
options=('!libtool')
source=(#http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2
        http://www.mirrorservice.org/sites/sourceware.org/pub/gcc/snapshots/${_snapshot}/gcc-${_snapshot}.tar.bz2
        gcc-hash-style-both.patch
        r160561.patch)
md5sums=('225cb21d0b432e4b34292b0213a4b200'
         '6fd395bacbd7b6e47c7b74854b478363'
         '79cb26e66eb2502171ef69438fa8666d')

build() {
  cd "$srcdir/gcc-$_dirver"

  # Do not install libiberty
  sed -i -e 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
  # Do not run fixincludes
  sed -i -e 's@\./fixinc\.sh@-c true@' gcc/Makefile.in

  patch -Np0 -i "$srcdir/gcc-hash-style-both.patch"
  # http://gcc.gnu.org/bugzilla/show_bug.cgi?id=44258
  patch -Np1 -i "$srcdir/r160561.patch"

  echo $pkgver > gcc/BASE-VER

  rm -rf build
  mkdir build
  cd build      

  ../configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --libexecdir=/usr/lib \
    --target=$_target \
    --with-sysroot=/opt/lib32 \
    --enable-languages=c,c++,fortran,objc,obj-c++ \
    --enable-shared \
    --enable-threads=posix \
    --enable-__cxa_atexit \
    --enable-clocale=gnu \
    --enable-gnu-unique-object \
    --enable-lto \
    --enable-plugin \
    --disable-multilib \
    --disable-libstdcxx-pch \
    --with-cloog \
    --with-ppl \
    --with-system-zlib
  make
}

package() {
  cd "$srcdir/gcc-$_dirver/build"

  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/$_target/lib/*
  rm -rf "$pkgdir"/usr/{include,share}

  # Install Runtime Library Exception
  install -Dm644 ../COPYING.RUNTIME \
    "$pkgdir/usr/share/licenses/$pkgname/RUNTIME.LIBRARY.EXCEPTION"
}

