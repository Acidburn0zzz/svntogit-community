# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Michal Soltys <soltys@ziu.info>

pkgname=ebtables
pkgver=2.0.10_4
pkgrel=1
pkgdesc='Ethernet bridge filtering utilities'
arch=('i686' 'x86_64')
backup=('etc/conf.d/ebtables')
url='http://ebtables.sourceforge.net/'
license=('GPL2')
# ebtables segfault with --as-needed
options=('!buildflags')
source=(
  "http://downloads.sourceforge.net/${pkgname}/${pkgname}-v${pkgver/_/-}.tar.gz"
  'ebtables.rc'
  'ebtables.conf'
  )
md5sums=('506742a3d44b9925955425a659c1a8d0'
         '368825c83a2b1180d2223e61b9f3bd07'
         '86fc3622e6fc0a7a7920c90ff576cc38')

build() {
  cd ${pkgname}-v${pkgver/_/-}
  make CFLAGS='-Wunused -Wall -Werror -Wno-error=unused-but-set-variable'
}

package() {
  cd "${pkgname}-v${pkgver/_/-}"
  make install \
    DESTDIR="${pkgdir}" \
    LIBDIR=/usr/lib \
    MANDIR=/usr/share/man \
    BINDIR=/usr/sbin \
    INITDIR=/etc/rc.d \
    SYSCONFIGDIR=/etc/ebtables

  # rm package ebtables rc.d scripts
  rm "${pkgdir}/etc/rc.d/ebtables"
  rm "${pkgdir}/etc/ebtables/ebtables-config"

  # install custom ebtables rc.d scripts
  install -D -m 0755 "${srcdir}/$pkgname.rc" "${pkgdir}/etc/rc.d/$pkgname"
  install -D -m 0644 "${srcdir}/$pkgname.conf" "${pkgdir}/etc/conf.d/$pkgname"
}

# vim:set ts=2 sw=2 ft=sh et:
