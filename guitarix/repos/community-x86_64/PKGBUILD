# Maintainer: David Runge <dvzrv@archlinux.org

pkgname=guitarix
pkgver=0.42.0
pkgrel=1
pkgdesc="A simple mono guitar amplifier and FX for JACK using Faust"
arch=('x86_64')
url="https://guitarix.org"
license=('GPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio')
depends=('atkmm' 'bluez-libs' 'cairo' 'cairomm' 'gcc-libs' 'glibc' 'glibmm'
'gtkmm3' 'libsigc++' 'libx11' 'pangomm' 'ttf-roboto')
makedepends=('avahi' 'boost' 'curl' 'gdk-pixbuf2' 'eigen' 'faust' 'fftw'
'glib2' 'gperf' 'gtk3' 'intltool' 'jack' 'ladspa' 'liblo' 'liblrdf' 'lilv'
'lv2' 'pango' 'sassc' 'waf' 'zita-convolver' 'zita-resampler')
optdepends=('new-session-manager: for session management')
provides=('guitarix2' 'libgxw.so' 'libgxwmm.so' 'ladspa-host' 'lv2-host')
conflicts=('guitarix2')
replaces=('guitarix2')
source=("${pkgname}-${pkgver}.tar.xz::https://download.sourceforge.net/project/guitarix/guitarix/${pkgname}2-${pkgver}.tar.xz")
sha512sums=('917896b99b9a4ac9fcb9788a4ee578eb89a233c85ed3d16a091ab9934072850e95089186ab30eefdee3e1f2db50583dcb3430c092008023c5e9c284545214d66')
b2sums=('0a0cb94bf0fc4eeb453d4430804a29b4f3c1b822a651c79f47bff112e27086415f300676b5cdad075c152d23eb9e4292d38cd72d81483d2f0f7d13b79e6ac85a')

build() {
  cd "${pkgname}-${pkgver}"
  waf configure --prefix=/usr \
                --enable-nls \
                --ladspa \
                --new-ladspa \
                --shared-lib \
                --lib-dev \
                --ldflags="${LDFLAGS}"
  waf build -vv
}

package() {
  depends+=('libavahi-common.so' 'libavahi-gobject.so' 'libboost_iostreams.so'
  'libcurl.so' 'libfftw3f.so' 'libgdk-3.so' 'libgdk_pixbuf-2.0.so'
  'libgio-2.0.so' 'libglib-2.0.so' 'libgobject-2.0.so' 'libjack.so'
  'liblilv-0.so' 'liblo.so' 'liblrdf.so' 'libpangocairo-1.0.so'
  'libpango-1.0.so' 'libsndfile.so' 'libzita-convolver.so'
  'libzita-resampler.so')
  cd "${pkgname}-${pkgver}"
  waf install --destdir="${pkgdir}"
  # docs
  install -vDm 644 {changelog,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
