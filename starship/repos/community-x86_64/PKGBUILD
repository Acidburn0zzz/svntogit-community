# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Kevin Song <kevin.s05@gmail.com>

pkgname=starship
pkgdesc='The cross-shell prompt for astronauts'
pkgver=0.51.0
pkgrel=1
arch=(x86_64)
url=https://starship.rs/
license=(ISC)
depends=(
  gcc-libs
  glibc
  libgit2.so
  openssl
)
optdepends=(
  'noto-fonts-emoji: emoji support for terminals'
  'powerline-fonts: powerline symbols for terminals'
)
makedepends=(
  git
  rust
)
checkdepends=(python)
_tag=88c3844db35655d76c6271112b3674cda481222d
source=(git+https://github.com/starship/starship.git#tag=${_tag})
b2sums=(SKIP)

prepare() {
  cargo fetch \
    --locked \
    --manifest-path starship/Cargo.toml
}

pkgver() {
  cd starship
  git describe --tags | sed 's/^v//'
}

build() {
  cargo build \
    --release \
    --frozen \
    --manifest-path starship/Cargo.toml
}

check() {
  cargo test \
    --release \
    --frozen \
    --manifest-path starship/Cargo.toml
}

package() {
  cargo install \
    --frozen \
    --offline \
    --no-track \
    --path starship \
    --root "${pkgdir}"/usr
    install -Dm 644 starship/LICENSE -t "${pkgdir}"/usr/share/licenses/starship/
}

# vim: ts=2 sw=2 et:
