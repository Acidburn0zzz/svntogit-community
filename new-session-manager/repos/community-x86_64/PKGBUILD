# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.3.2
pkgrel=1
pkgdesc="Audio session manager"
arch=('x86_64')
url="https://github.com/linuxaudio/new-session-manager"
license=('GPL3')
depends=('gcc-libs' 'glibc')
makedepends=('fltk' 'jack' 'liblo' 'meson')
optdepends=('fltk: for nsm-legacy-gui')
provides=('non-session-manager')
conflicts=('non-session-manager')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('4dcd904ea0b40da23c563311d4aedb1f8d96ac90bd98477f52801222904f255ec2da331be8012b4d2f3921ef13e4b21b48921bb390bf2de5a138879be96e424b')
b2sums=('bfaee6eea5f636a01f55cc0e83e25dbb87c19ee453eb0b3f00d78459026c8be1550d696d48445634f66c4d01f52caa2f6cf6667c9d763b99100b62ebcabaf0e2')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

package() {
  depends+=('libjack.so' 'liblo.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
