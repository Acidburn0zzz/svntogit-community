# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.3.1
pkgrel=1
pkgdesc="Audio session manager"
arch=('x86_64')
url="https://github.com/linuxaudio/new-session-manager"
license=('GPL3')
depends=('fltk' 'gcc-libs' 'glibc')
makedepends=('jack' 'liblo' 'meson')
provides=('non-session-manager')
conflicts=('non-session-manager')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('0876256e8e7a3540a108e702c9b5a755548d0bb76de2fa35a3bf71fd66a4c572d4daa88aa5c20c14e7e8d27da504e5e0eb80d1d1945f4a2e31ee6651a9413734')
b2sums=('75f9a550edb86d8bf2cb7f426a16994eff9e79c8089acfc5af32262d9adb11fd28d2378c91d626c82395f315b6ad61e49f6d9573a03ac9b255afe8430b9ac7d0')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix='/usr' \
        --buildtype plain \
        --wrap-mode nodownload \
        -D b_lto=true \
        -D b_pie=true \
        build
  ninja -C build
}

package() {
  depends+=('libjack.so' 'liblo.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
