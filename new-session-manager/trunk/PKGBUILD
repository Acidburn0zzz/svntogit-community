# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=new-session-manager
pkgver=1.3
pkgrel=1
pkgdesc="Audio session manager"
arch=('x86_64')
url="https://github.com/linuxaudio/new-session-manager"
license=('GPL3')
depends=('fltk' 'gcc-libs' 'glibc')
makedepends=('jack' 'liblo' 'meson')
provides=('non-session-manager')
conflicts=('non-session-manager')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/linuxaudio/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('aa65767c9598e70956ccdf4d0a3ab4caef88e4e7490afd743dd4342bda5093f151d14be4bbd0fa079138232be00844fc92fb0c099e525812dfb0a061659644e3')
b2sums=('acecb5035f20361ac0d9810bc0c9a874c4d86663df2b9c1d182f6d42524393e2e51d6ce3ab25df9d0c0ec5dae60c6aec72bb7992f8a1bb7d4485011325077248')

build() {
  cd "$pkgname-$pkgver"
  meson --prefix='/usr' build
  ninja -C build
}

package() {
  depends+=('libjack.so' 'liblo.so')
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  install -vDm 644 {CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
