# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=pysolfc
pkgver=2.6.4
pkgrel=3
pkgdesc="PySol (Fan Club edition) is an exciting collection of more than 1000 solitaire games"
arch=('any')
url="http://pysolfc.sourceforge.net/"
license=('GPL3')
depends=('tk' 'python-pillow' 'python-random2' 'python-six' 'pysolfc-cardsets')
makedepends=('patch')
source=(https://downloads.sourceforge.net/$pkgname/PySolFC-$pkgver.tar.xz
        time-attribute-clock-fix.diff)
sha256sums=('4dab3579531530136fb629c045d4b8389f410962e6cf77494f18c61c9de6ea78'
            '28f593d28d71e01a5f07c51aacedd98b7222c03ce96888ce685e4292cddb4676')

prepare() {
  cd PySolFC-$pkgver

  patch -Np1 -i "${srcdir}"/time-attribute-clock-fix.diff
  sed -i 's/pysol.py/pysol/' data/pysol.desktop
}


package() {
  cd PySolFC-$pkgver

  python setup.py install --root="${pkgdir}"
  mv "${pkgdir}"/usr/bin/pysol.py "${pkgdir}"/usr/bin/pysol
}
