# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test-simple
pkgver=1.302184
pkgrel=1
pkgdesc="Basic utilities for writing tests"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test-Simple"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test-Simple-$pkgver.tar.gz")
sha512sums=('03022c6711ba106f5d95e0ce1417a1752cb3836d9e67defbfa7047e03dc197083f9de8a1ccc100742c45b10881c1cebf88feb2c5791ce6ca780666b4f2085445')

build() {
  cd Test-Simple-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test-Simple-$pkgver
  make test
}

package() {
  cd Test-Simple-$pkgver
  make DESTDIR="$pkgdir" install
}
