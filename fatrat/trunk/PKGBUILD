# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=fatrat
pkgver=1.2.0_beta2
pkgrel=6
pkgdesc="QT4 based download manager with support for HTTP, FTP, SFTP, BitTorrent, rapidshare and more"
arch=('i686' 'x86_64')
url="http://fatrat.dolezel.info/"
license=('GPL')
depends=('gloox' 'libtorrent-rasterbar' 'pion' 'qtwebkit')
optdepends=('geoip: GeoIP support')
makedepends=('boost' 'cmake' 'java-environment')
source=(http://www.dolezel.info/download/data/fatrat/fatrat-$pkgver.tar.xz \
        QTBUG-22829-workaround.diff)
md5sums=('ad823d2a81f0583316ece815b928c71b'
         '9c017f227437b8a72e92219ee6d58578')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # workaround the moc bug (QTBUG-22829) with new boost
  patch -Np1 < "$srcdir/QTBUG-22829-workaround.diff" || true

  cmake \
    -DWITH_EVERYTHING=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
