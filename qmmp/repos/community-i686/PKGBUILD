#$Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=qmmp
pkgver=0.4.2
pkgrel=1
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt>=4.3' 'taglib' 'xdg-utils')
makedepends=('cmake'  'flac' 'jack' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libsamplerate' 'libmodplug' 'libsndfile' 'wavpack')
optdepends=('flac: native FLAC support'
            'jack: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
source=(http://qmmp.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
md5sums=('4ae6d4db68852bf985d3f0f94fc210a9')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install
}

