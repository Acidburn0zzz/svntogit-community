#$Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=qmmp
pkgver=0.3.2
pkgrel=1
pkgdesc="Qt4 based audio-player"
arch=('i686' 'x86_64')
url="http://qmmp.ylsoftware.com/"
license=('GPL')
depends=('alsa-lib' 'curl' 'hicolor-icon-theme' 'libmad' 'libvorbis' 'libogg'  'qt>=4.3' 'taglib' 'xdg-utils')
makedepends=('cmake'  'flac' 'jack-audio-connection-kit' 'libmpcdec' 'pulseaudio' 'ffmpeg' 'libsamplerate' 'libmodplug' 'libsndfile' 'wavpack')
optdepends=('flac: native FLAC support' 
            'jack-audio-connection-kit: JACK sound output'
            'libmpcdec: Musepack support'
            'pulseaudio: PulseAudio output'
            'libmodplug: MOD playing library'
            'libsndfile: sampled sound support'
            'ffmpeg' 'libsamplerate' 'wavpack')
install=$pkgname.install
source=(http://qmmp.ylsoftware.com/files/$pkgname-$pkgver.tar.bz2)
md5sums=('29fdb5e543d93634054844e17dcaba82')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  cmake . -DCMAKE_INSTALL_PREFIX=/usr || return 1
  make || return 1
  make DESTDIR=${pkgdir} install
}
