# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=fluajho
pkgver=1.6
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('fluidsynth' 'glib2' 'jack' 'libsndfile' 'libutil-linux' 'ncurses'
'nuitka')
optdepends=('non-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('dce7a8f88b79d837c07246ea1dd58160af2ac07efd3c0906816c766ef60d69a57a937d17b5fd7b94120c733049d50a7d9cfca5e65770e9f36c812eb8d14e9b38'
            'SKIP')
b2sums=('9248aaa588b222501611a5f6311b177eb9dcca2a5e0df5e54e9bfeca0270602bf95938797896b32f8c46907d25a9cf541d7cbcf9bbeb0f3b6c953ab7be5456d6'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfluidsynth.so' 'libglib-2.0.so' 'libjack.so' 'libncursesw.so'
  'libsndfile.so' 'libuuid.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
