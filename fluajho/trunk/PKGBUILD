# Maintainer: David Runge <dave@sleepmap.de>

pkgname=fluajho
pkgver=1.4
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho.git/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'glib2' 'hicolor-icon-theme' 'libfluidsynth.so' 'libjack.so'
'libsndfile' 'libusb-1.0.so' 'libuuid.so' 'python' 'python-pyqt5' 'ttf-dejavu')
makedepends=('nuitka')
optdepends=('non-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('40c31c5659f48357591155991bf32151b0992c7cb498a352f022fb288231e949a8c42b29606f3dd55349feaf97ad6b6162a0aa34cec6c9ec4d0fa474b7036b4e'
            'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
