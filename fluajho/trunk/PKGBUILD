# Maintainer: David Runge <dave@sleepmap.de>

pkgname=fluajho
pkgver=1.2
pkgrel=1
pkgdesc="A simple sf2 soundfont host/player"
arch=('x86_64')
url="https://www.laborejo.org/fluajho.git/"
license=('GPL3')
groups=('pro-audio')
depends=('fluidsynth' 'hicolor-icon-theme' 'non-session-manager' 'python-pyqt5'
'ttf-dejavu')
makedepends=('nuitka')
# optdepends=('non-session-manager: Session management')
provides=('soundfont-synthesizer')
source=("https://www.laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('3ae5d127ff9069a3e6fa8beb2838b1723e8d2319a13242f39c451298e5821141cc24db795229db5c2b0bd3b3ab3a409791f41acbfb805ba456b417981e72ace7'
            'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  # making sure, that the resulting binary doesn't have executable stack
  # this is a known problem with current nuitka:
  # https://www.freelists.org/post/nuitka-dev/Nuitka-compiles-with-executable-stack-which-is-a-security-risk
  export LDFLAGS+=",-z,noexecstack"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
