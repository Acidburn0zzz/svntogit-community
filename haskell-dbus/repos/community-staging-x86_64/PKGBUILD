# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=dbus
pkgname=haskell-dbus
pkgver=0.10.14
pkgrel=2
pkgdesc="A client library for the D-Bus IPC system"
url="https://john-millikin.com/software/haskell-dbus/"
license=("GPL3")
arch=('x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-libxml-sax' 'haskell-network' 'haskell-parsec'
         'haskell-random' 'haskell-text' 'haskell-vector' 'haskell-xml-types')
makedepends=('ghc' 'haskell-extra' 'haskell-quickcheck' 'haskell-resourcet' 'haskell-tasty'
             'haskell-tasty-hunit' 'haskell-tasty-quickcheck')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('8212d38f680379de7f530aad5df935d56fb29de4dfe59acb1b7561497b25db3b60b767ab2a50a8c00fd9252d4ba6e92f6103735db450cdd3244335a45e730d67')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    # https://github.com/rblaze/haskell-dbus/issues/9
    cd $_hkgname-$pkgver
    runhaskell Setup test || warning "Tests failed"
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
