# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-datetime
pkgver=2.1.7
pkgrel=1
pkgdesc='Switchboard Date & Time Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-datetime
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libgee
  libgranite.so
  libswitchboard-2.0.so
)
makedepends=(
  git
  meson
  vala
)
source=(git+https://github.com/elementary/switchboard-plug-datetime.git#tag=352dc4e7b13550e8d54719cec56db7f137d06a17)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-datetime

  git describe --tags
}

build() {
  arch-meson switchboard-plug-datetime build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
