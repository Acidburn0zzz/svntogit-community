# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>

pkgname=dockmanager
pkgver=0.1.0
pkgrel=0.20110211
pkgdesc="Dock-independent helper scripts for compatible docks"
url="https://launchpad.net/dockmanager"
arch=(i686 x86_64)
license=(GPL)
depends=('libdesktop-agnostic>=0.3.90-2' gconf dbus-glib python2)
makedepends=(bzr 'vala>=0.10' pkgconfig intltool autoconf gnome-common)
conflicts=(dockmanager-bzr)
provides=("dockmanager-bzr")
options=('!libtool' '!emptydirs')
install=dockmanager.install
source=(vala.patch)
md5sums=('f6db8c3a432b41b620f6fcc8bfccfd81')

__bzrtrunk=lp:dockmanager
__bzrmod=dockmanager

build() {
  cd "$srcdir"

  msg2 "Connecting to Launchpad...."

  if [ -d $__bzrmod ] ; then
    ( cd $__bzrmod && bzr up ) || warning "Bzr up failed!"
  else
    bzr checkout $__bzrtrunk $__bzrmod
  fi

  msg2 "BZR checkout done or server timeout"

  rm -rf $pkgname-build
  cp -r $__bzrmod $pkgname-build
  cd $pkgname-build

  msg2 "Starting make..."

  patch -Np0 -i "$srcdir/vala.patch"

  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib \
    --sysconfdir=/etc

  make
}

package() {
  cd "$srcdir/$pkgname-build"

  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  # Fix for python2
  find $pkgdir -type f -exec sed -i '1s|#!/usr/bin/env python$|&2|' {} +

  mkdir -p "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/dockmanager.schemas" "$pkgdir"/etc/gconf/schemas/*.schemas
  rm -rf "$pkgdir/etc/gconf"
}
