# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Maintainer: Laurent Carlier <lordheavym@gmail.com>
# Contributor: Vamp898 <vamp898@web.de>
# Contributor: Jaroslaw Swierczynski <swiergot@aur.archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Todd Maynard <arch@toddmaynard.com>

pkgname=kmymoney
pkgver=4.6.3
pkgrel=2
pkgdesc="Personal finance manager for KDE which operates similarly to MS-Money or Quicken"
arch=('i686' 'x86_64')
url="http://kmymoney2.sourceforge.net/"
license=('GPL')
depends=('kdebase-workspace' 'shared-mime-info' 'aqbanking' 'libofx' 'libalkimia' 'boost-libs')
makedepends=('cmake' 'automoc4' 'docbook-xml' 'docbook-xsl' 'boost')
replaces=('kmymoney2')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!makeflags')
source=(http://downloads.sourceforge.net/project/kmymoney2/KMyMoney-KDE4/$pkgver/$pkgname-$pkgver.tar.bz2
        git-fixes.diff)
sha256sums=('94e109992b7e9ebb066b608d56c168216b8a944d62538c95176bc36ea4db542e'
            'e618cc5531f00c14171c5cae837d8b5fb58bccaac363a5b09eed863355c242d3')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -Np1 -i ../git-fixes.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build
  cd build

  cmake ../ \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=RELEASE -DCMAKE_SKIP_RPATH=YES
  make VERBOSE=1
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"

  make DESTDIR="${pkgdir}" install
}
