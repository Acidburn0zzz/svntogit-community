# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=yapf
pkgname=("yapf" "python2-yapf")
pkgver=0.16.3
pkgrel=1
pkgdesc="Python style guide checker"
arch=('any')
url="http://github.com/google/yapf"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/google/yapf/archive/yapf-v$pkgver.tar.gz")
sha512sums=('a8ebfbc41e2839fd304137608b8c7136576c0859d78f3fbd20fd5b42a599c11915157deeb3c0286fb45d38441a82c5bad3d17359990c637f134e85075fdbdecb')

prepare() {
  cp -a yapf-yapf-v$pkgver{,-py2}
}

build() {
  cd "$srcdir"/yapf-yapf-v$pkgver
  python setup.py build

  cd "$srcdir"/yapf-yapf-v$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/yapf-yapf-v$pkgver
  nosetests3

  cd "$srcdir"/yapf-yapf-v$pkgver-py2
  nosetests2
}

package_yapf() {
  depends=('python-setuptools')

  cd yapf-yapf-v$pkgver
  python setup.py install -O1 --root="$pkgdir"
}

package_python2-yapf() {
  depends=('python2-setuptools')

  cd yapf-yapf-v$pkgver-py2
  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir"/usr/bin/yapf{,2}
}

# vim:set ts=2 sw=2 et:
