# $Id$
# Maintainer: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: graysky
# Contributor: Tomasz Å»ok <tomasz dot zok at gmail dot com>

pkgname=pymol
pkgver=2.1.0
_majorver=2
pkgrel=1
pkgdesc="Molecular visualization system on an Open Source foundation"
arch=('x86_64')
url="http://pymol.org/"
license=('custom')
depends=('freetype2' 'glew' 'glut' 'python-numpy' 'tcsh')
makedepends=('desktop-file-utils' 'gendesk' 'msgpack-c')
optdepends=('python-pmw: pmw based UI'
            'python-pyqt4: Qt4 based UI'
            'python-pyqt5: Qt5 based UI')
source=("https://downloads.sourceforge.net/project/pymol/pymol/${_majorver}/pymol-v${pkgver}.tar.bz2"
        ${pkgname}.png::"https://c.fsdn.com/allura/p/pymol/icon"
        "apbstools_tcltk8.6.patch")
sha512sums=('67175c0668a13826febcb73f7c62a43cfa03a7f86bb80113d52fb337e7d29008684c03163fc06338c4d68edd9246ffcf63aec1ee3110ae91c8a71ef24ab7b605'
            '4ceb8123b21becac7f2f1e7c3521b8fbaa22038d76b4749d07d286eed1646025dccd1962e58c0923357261006deb589aaf1fe4f6f53fd2327475d8db3adca5c4'
            '90fce6d3c4364cd775b44bdd9013a08f26392339bec40ecd749220b3d11391381f5b159734bdb2e2ac675fbc016a6a1b9c4cec2fa8c9c0226609a5f09b610b9c')
changelog=ChangeLog

prepare() {
  # create desktop file
  gendesk -f -n --pkgname "${pkgname}" --pkgdesc "${pkgdesc}" \
          --name="PyMOL Molecular Graphics System" \
          --categories="Science;Chemistry"

  # suppress non-zero exit code that breaks makepkg
  sed -i '/sys.exit/ s,2,0,' "${srcdir}/pymol/setup.py"
  # fix FS#39526
  cd "${srcdir}/${pkgname}"
  patch -p0 -i "${srcdir}/apbstools_tcltk8.6.patch"
}

build() {
  cd "${srcdir}/${pkgname}"
  python setup.py build
}

package() {
  cd "${srcdir}/${pkgname}"
  python setup.py install --prefix=/usr --root="${pkgdir}"
  install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -Dm644 "${srcdir}/pymol.desktop" "${pkgdir}/usr/share/applications/pymol.desktop"
  install -Dm644 "${srcdir}/pymol.png" "${pkgdir}/usr/share/pixmaps/pymol.png"
}

# vim:set ts=2 sw=2 et:

