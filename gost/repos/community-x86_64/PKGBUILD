# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.7.0
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('0a822550f500bee30518a34efcdd8b00eb325de44fce10f777aa7081bf24290ac0da48d06d5867ada2dfb44cae7316e59051806e3343f832a51fef1a718d0f35'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

prepare() {
  mkdir .gopath
  ln -sf "$PWD/gost-$pkgver/vendor" .gopath/src
  ln -s "$PWD/gost-$pkgver" .gopath/src/github.com/ginuerzh/gost
  export GOPATH="$srcdir/.gopath"
}

build() {
  cd gost-$pkgver/cmd/gost
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
