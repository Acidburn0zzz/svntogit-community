# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.10.1
pkgrel=2
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('0f142f7e4751a8a08f6a1330e78ce7a20e7d7965690a71e1ab04a502fe152a6eb3cfae6dd407da7629b8cc36aa435ad4ca5fdc0de1e9046ee3449c7ea5ed496b'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

prepare() {
  mkdir -p .gopath/src/github.com/ginuerzh
  ln -s "$PWD/gost-$pkgver" .gopath/src/github.com/ginuerzh/gost
}

build() {
  export GOPATH="$srcdir/.gopath"
  cd gost-$pkgver/cmd/gost
  go build -ldflags "-extldflags ${LDFLAGS}" # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
