# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.2
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('i686' 'x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('gcc-libs')
makedepends=('git' 'mercurial' 'gcc-go')
source=("git+https://github.com/ginuerzh/gost.git#tag=v$pkgver"
        LICENSE)
md5sums=('SKIP'
         '24a6686f7498d7af0b51690f712dd618')

prepare() {
  cd gost

  mkdir -p .gopath/src/github.com/ginuerzh
  ln -sf "$PWD" .gopath/src/github.com/ginuerzh/gost
  export GOPATH="$PWD/.gopath"

  go get github.com/ginuerzh/gosocks5 github.com/gorilla/websocket github.com/shadowsocks/shadowsocks-go/shadowsocks github.com/golang/glog golang.org/x/net/http2
}

build() {
  cd gost/cmd/gost
  go build -o gost -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
