# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.6.1
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ginuerzh/gost/archive/v$pkgver.tar.gz"
        LICENSE)
sha512sums=('0e01cb418934add739fd3b288adcee0526b87e07a35c41ef7342a64cd47574f9c312dae25b66f945d13a4300a2ac0b556dc541a2c8a0f21cbc3211ae90fa8b4c'
            '8815c27a49d747e90fbcbf03c6011a76b95bc72a6c963ee8df9490e6aba9a3f4f9895e6a7759988aef123160ef8a6d2de85e8fce01c5980f8744e212792ad07c')

prepare() {
  mkdir .gopath
  ln -sf "$PWD/gost-$pkgver/vendor" .gopath/src
  ln -s "$PWD/gost-$pkgver" .gopath/src/github.com/ginuerzh/gost
  export GOPATH="$srcdir/.gopath"
}

build() {
  cd gost-$pkgver/cmd/gost
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost-$pkgver
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
