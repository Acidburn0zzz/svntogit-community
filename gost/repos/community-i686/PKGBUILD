# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=gost
pkgver=2.3
pkgrel=1
pkgdesc="GO Simple Tunnel"
arch=('i686' 'x86_64')
url="https://github.com/ginuerzh/gost"
license=('MIT')
depends=('glibc')
makedepends=('git' 'mercurial' 'go')
source=("git+https://github.com/ginuerzh/gost.git#tag=v$pkgver"
        LICENSE)
md5sums=('SKIP'
         '24a6686f7498d7af0b51690f712dd618')

prepare() {
  mkdir .gopath
  ln -sf "$PWD/gost/cmd/gost/vendor" .gopath/src
  export GOPATH="$srcdir/.gopath"
}

build() {
  cd gost/cmd/gost
  go build # -gccgoflags "$CFLAGS $LDFLAGS"
}

package() {
  cd gost
  install -Dm755 cmd/gost/gost "$pkgdir/usr/bin/gost"
  install -Dm644 ../LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
