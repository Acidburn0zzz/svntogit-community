# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=0.11.0
pkgrel=2
pkgdesc="LXQt system configuration."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt" "libkscreen" "libxcursor" "qt5-svg")
makedepends=("cmake" "qt5-tools" "git")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"e254994caf0535bf6f9da659dc4410b63b5b5cfabea0b961bfb8c00e3c393d38"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
