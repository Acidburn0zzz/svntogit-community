# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-config
pkgver=0.10.0
pkgrel=6
pkgdesc="LXQt system configuration."
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt" "libkscreen" "libxcursor" "qt5-svg")
makedepends=("cmake" "qt5-tools")
source=(
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://downloads.lxqt.org/lxqt/$pkgver/$pkgname-$pkgver.tar.xz.asc"
	"build-without-lxqt-config-appearance.patch"
)
sha256sums=(
	"4567be4653f2f1f9ff156126250a08d7b480f627fa1410484d49f65ba5dab9b4"
	"SKIP"
	"a68acc45b1a917c3cee9ab6a9d02df902d8d32fa928474e1ca9d6c1773fea8f9"
)
validpgpkeys=("169704C6FB490C6892C7F23C37E0AF1FDA48F373")


prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -p1 < "$srcdir/build-without-lxqt-config-appearance.patch"
}


build() {
	mkdir -p build
	cd build
	# Build with LC_ALL=en_US.UTF-8 to work around lxqt#957 (FS#48754)
	# Remove in 0.11
	LC_ALL=en_US.UTF-8 cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=lib
	LC_ALL=en_US.UTF-8 make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
