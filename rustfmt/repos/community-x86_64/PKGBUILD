# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Vlad M. <vlad@archlinux.net>

pkgname=rustfmt
pkgver=0.8.1
pkgrel=1
pkgdesc="A tool for formatting Rust code according to style guidelines"
url="https://github.com/rust-lang-nursery/rustfmt"
makedepends=('cargo')
arch=('i686' 'x86_64')
license=('Apache' 'MIT')
source=("https://github.com/rust-lang-nursery/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('4985fc39ae5ac227f1166ce002a17f69d0f1596320867f5756d9dc33befb9ae0749e705cefdb035f38fb068b8b59e41924e785ee2ed844e0bc5501cd150f4dd8')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE-MIT LICENSE-APACHE
  cd target/release
  install -Dm755 -t "$pkgdir/usr/bin/" rustfmt cargo-fmt
}
