# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributo: Christoph Gysin <christoph.gysin@gmail.com>

pkgname=pasystray
pkgver=0.7.0
pkgrel=1
pkgdesc='PulseAudio system tray (a replacement for padevchooser)'
arch=('x86_64')
url="https://github.com/christophgysin/pasystray"
license=('LGPL')
depends=('libpulse' 'libnotify' 'avahi' 'libx11' 'gtk3')
optdepends=('pulseaudio: To control a local pulseaudio instance'
            'pavucontrol: Launch PulseAudio mixer from tray icon'
            'paprefs: Launch PulseAudio preferences from tray icon')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('a617be4654a82ff363ec8ad3f65b107638e941bcef39d7c179bc076087d1bbfee9d7ea83b8a886df94774b1696b8052aca1690695f738599619ee3b838683feb')

build() {
  cd $pkgname-$pkgver

  autoreconf -vif
  ./configure \
        --prefix=/usr \
        --sysconfdir=/etc

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

