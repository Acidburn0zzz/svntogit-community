# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfig
pkgver=0.63.00
pkgrel=1
pkgdesc="Professional vector animation program (CLI renderer only)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('libxml++' 'libsigc++2.0' 'etl' 'imagemagick' 'ffmpeg' 'fontconfig' 'libpng' 'libtiff' 'libdv' 'libmng')
optdepends=('openexr' 'libsigc++')
conflicts=('synfig-core')
replaces=('synfig-core')
options=(!libtool)
source=(http://downloads.sourceforge.net/sourceforge/$pkgname/$pkgname-$pkgver.tar.gz
	build-fix.patch)
md5sums=('e6af1aa9426cf629127d23edbd772f6d'
         '0e983a00807c6c76c380f4f9918e6724')

build() {
  cd $srcdir/$pkgname-$pkgver
  [ $NOEXTRACT -eq 1 ] || patch -p1 <$srcdir/build-fix.patch
  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
