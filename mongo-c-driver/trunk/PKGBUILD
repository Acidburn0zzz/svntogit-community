# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=libmongoc
pkgver=1.10.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libbson' 'libsasl' 'snappy' 'zlib')
makedepends=('cmake')
checkdepends=('python-pifpaf' 'mongodb')
source=("mongo-c-driver-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz")
sha512sums=('d0d5686dbb7a5843a00f99f979ea64c0e9b95018a8630b6d147552b3227d8ebe6b4db52f5dd4c3c79ddb5da392784da7d890cf2d652567c0922b16e894a178c2')

prepare() {
  mkdir -p build
  cd mongo-c-driver-$pkgver
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DENABLE_BSON=SYSTEM
  make
}

check() {
  cd build
  pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
