# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mongo-c-driver
pkgver=1.13.0
pkgrel=1
pkgdesc='A client library written in C for MongoDB'
arch=('x86_64')
url='http://www.mongodb.org/display/DOCS/C+Language+Center'
license=('Apache')
depends=('libsasl' 'icu' 'snappy' 'zlib')
makedepends=('cmake')
checkdepends=('pifpaf' 'mongodb')
provides=('libbson' 'libmongoc')
conflicts=('libbson' 'libmongoc')
replaces=('libbson' 'libmongoc')
source=("mongo-c-driver-$pkgver.tar.gz::https://github.com/mongodb/mongo-c-driver/archive/$pkgver.tar.gz"
        https://github.com/mongodb/mongo-c-driver/commit/b50c9e4004833e3c0034b68b4082d6e590510949.patch)
sha512sums=('d2f5b04b3d2dbdeba4547ec1fe8a0da7bad5214de92fff480ef0ff7d97ea45d5e6347c11c249867d4905b1dd81b76c7cfbb9094a58df586dae881955ee246907'
            'b376251b8f6958390b42766553bd1c9457c6917272df90c812cb9386879f034882751969af96af6159ffc4495d55c652477858f2569a49873be6da9c6cb38179')

prepare() {
  mkdir -p build
  cd mongo-c-driver-$pkgver
  patch -p1 -i ../b50c9e4004833e3c0034b68b4082d6e590510949.patch
}

build() {
  cd build
  cmake ../mongo-c-driver-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DENABLE_STATIC=OFF -DENABLE_TESTS=OFF
    # Test profiles are not found even with ENABLE_TESTS=ON
  make
}

check() {
  cd build
  # No test profile found
  # pifpaf run mongodb --port 27017 make test || warning "Tests failed"
}

package() {
  cd build
  make DESTDIR="$pkgdir/" install
}
