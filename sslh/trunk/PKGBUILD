# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.11
pkgrel=1
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('i686' 'x86_64')
url='http://www.rutschle.net/tech/sslh.shtml'
license=('GPL2')
backup=('etc/conf.d/sslh')
source=("http://www.rutschle.net/tech/$pkgname-$pkgver.tar.gz"
        'sslh.rc'
        'sslh.conf'
        'sslh.service')
md5sums=('4ea599f0fa31ae235646e5a200b8f8fb'
         'f64da291e97f1a99c33cb6ebf939b95b'
         '808199410bd6ab67421bf3544f805d1a'
         'be7af567bb35f592afaf4cbb070ab171')

build() {
  cd $pkgname-$pkgver
  # fix archlinux path in manpage
  sed -i -e "s/init.d/rc.d/" sslh.pod
  sed -i -e "s+/etc/default+/etc/conf.d+" sslh.pod
  make
}

package() {
  # install initscripts
  install -Dm 755 sslh.rc "$pkgdir/etc/rc.d/sslh"
  install -Dm 644 sslh.conf "$pkgdir/etc/conf.d/sslh"
  # install systemd service
  install -Dm 644 sslh.service "$pkgdir/usr/lib/systemd/system/sslh.service"
  # manually install to have both ssl-fork and ssl-select
  cd $pkgname-$pkgver
  install -Dm 0755 sslh-fork "$pkgdir/usr/bin/sslh-fork"
  install -Dm 0755 sslh-select "$pkgdir/usr/bin/sslh-select"
  ln -s sslh-fork "$pkgdir/usr/bin/sslh"
  install -Dm 0644 sslh.8.gz "$pkgdir/usr/share/man/man8/sslh.8.gz"
}

# vim:set ts=2 sw=2 ft=sh et:
