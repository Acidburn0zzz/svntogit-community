# $Id$
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>
# Contributor: Le_suisse <lesuisse.dev+aur at gmail dot com>
# Contributor: Jason Rodriguez <jason-aur@catloaf.net>

pkgname=sslh
pkgver=1.10
pkgrel=3
pkgdesc='SSL/SSH/OpenVPN/XMPP/tinc port multiplexer'
arch=('i686' 'x86_64')
url='http://www.rutschle.net/tech/sslh.shtml'
license=('GPL2')
backup=('etc/conf.d/sslh')
source=("http://www.rutschle.net/tech/${pkgname}-${pkgver}.tar.gz"
        'rc.sslh'
        'conf.sslh')
md5sums=('bc34e9a4770d634633e70589c72708cc'
         '1ad105ad572284fa137d06ae8d1f16fc'
         'e9b05de9d626803e35f46bdd1e968421')

build() {
  cd $pkgname-$pkgver
  # fix archlinux path in manpage
  sed -i -e "s/init.d/rc.d/" sslh.pod
  sed -i -e "s+/etc/default+/etc/conf.d+" sslh.pod
  make
}

package() {
  install -D -m 755 rc.sslh "${pkgdir}/etc/rc.d/sslh"
  install -D -m 644 conf.sslh "${pkgdir}/etc/conf.d/sslh"
  cd $pkgname-$pkgver
  make PREFIX="$pkgdir/usr" install
}

# vim:set ts=2 sw=2 ft=sh et:
