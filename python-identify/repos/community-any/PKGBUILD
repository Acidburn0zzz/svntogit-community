# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.4.1
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/pre-commit/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-ukkonen' 'python-pytest')
optdepends=('python-ukkonen: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/pre-commit/${_name}/archive/refs/tags/v${pkgver}.tar.gz")
sha512sums=('69253bd364ed96ae64d83ee25da0d9aae97731e602f193008471dd99f47db2b87aa33dda1f87fb9edf939992e0452ca45f07f1d7d5e628352b50d4c2e68b7d0e')
b2sums=('e7c04480159062ec25063d956dda6d904031bbcc30fdab6011ad24a8c3816490d0dfe08cec19fdfe660509a246da820ccfe41d4dcd909daa9c59b622bf506148')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
