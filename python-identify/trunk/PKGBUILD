# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.2.4
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('25e255fd62d0e75aafa7037085362b7bf2ba8ae142bfb11e0e20452d503d8ec3f90ec84c8ef8966fb021fe1054c226862b3ce99d2288ec9aaa758a8971d1c87a')
b2sums=('9473a157ce2aed13ed45d6e8d83b4510c40ae977406b9d7b211caa70c27d1850cf49da8848495841281b1f192b576d269a5372215effd7d89c9211e51d51151d')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
