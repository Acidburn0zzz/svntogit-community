# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.2.13
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('37764325268c164028b7719f91571c006c9c6db3f4bba5a93d20f3d9ea30712cfe7e5710b175266a3d74f3f85485f16ebf7d21486d4329050b55e5835e8b4197')
b2sums=('b1835ca6b576a15be16de124554cfaf7b6dc55a1dfee70021613d35251135c295c3acf141eb645823add4c260bec9fdcde4799f0b72fbd57bc7a2e8659e416bd')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
