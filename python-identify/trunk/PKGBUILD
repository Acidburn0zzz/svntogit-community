# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=1.6.2
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance' 'python-pytest')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('c721fc77c2875b23ffc821a36a90611c64e4a81a22e9f9d372600b64d02cd78d1bf140c85c113dda6d75ec4ef7cfa79a82c936ed1bb0bcc43f24ab036f11d872')
b2sums=('7fe0c360add95839c683266008cd467fced7ef6f06fd459ee2c4bf994749cef194aea9958e25319782598ac0b145d8dcd972da060fb148d0a555ca7dd8d7008a')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
