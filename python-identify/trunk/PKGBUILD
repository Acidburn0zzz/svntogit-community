# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>

_name=identify
pkgname=python-identify
pkgver=2.2.2
pkgrel=1
pkgdesc="File identification library for Python"
arch=('any')
url="https://github.com/chriskuehl/identify"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-editdistance-s' 'python-pytest')
optdepends=('python-editdistance-s: for returning license identifiers')
source=("${_name}-${pkgver}.tar.gz::https://github.com/chriskuehl/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('5e8ca27ddd4c125b357298d61d465b8a6ede670a8771573da1b74070febf120d83f180a1e89f43d77dcc9bf09b8cbf3438cde28de8c4560e03e3ecfa8df1df4b')
b2sums=('68b6abbd6b1799edb008d547021a9b2b7ae252d3dd0216b4b0ed625a2feb20fc83c19cb48668797d37df40c50017dcfaefeec9c12d09da22f17bf38c6f84aef8')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
