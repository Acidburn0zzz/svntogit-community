# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: KokaKiwi <admin@kokaelkiwi.eu>
# Contributor: James Bulmer <nekinie@gmail.com>

pkgname=nanomsg
pkgver=0.9
_tag=$pkgver-beta
pkgrel=1
pkgdesc='Simple high-performance implementation of several "scalability protocols"'
url='http://nanomsg.org/'
license=(MIT)
arch=(i686 x86_64)
depends=(glibc)
makedepends=(cmake)
source=(nanomsg-$pkgver.tar.gz::https://github.com/nanomsg/nanomsg/archive/$_tag.tar.gz)
sha1sums=('0ff033d8431ec4b7fece67db8859f2a8785162d4')


build() {
  cd nanomsg-$_tag
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib .
  make
}

check() {
  cd nanomsg-$_tag
  ctest
}

package() {
  cd nanomsg-$_tag
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
