# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Vlad M. <vlad@archlinux.net>
# Contributor: issue <issue at archlinux dot info>

pkgname=rust-racer
_pkgname=racer
pkgver=2.1.25
pkgrel=1
pkgdesc="Code completion for Rust"
url="https://github.com/racer-rust/racer"
makedepends=('rustup' 'cmake')
arch=('x86_64')
license=('MIT')
source=("$pkgname-$pkgver.tar.gz::https://crates.io/api/v1/crates/$_pkgname/$pkgver/download")
sha256sums=('0727b9d7baaf9e42851145545d7b980b5c1752bd16a4c77c925c5e573d0069d9')

build() {
  cd "$_pkgname-$pkgver"
  rustup default nightly
  cargo build --release
}

package() {
  cd "$_pkgname-$pkgver"
  install -Dm755 target/release/racer "$pkgdir/usr/bin/racer"
  install -Dm644 LICENSE-MIT "$pkgdir/usr/share/licenses/$pkgname/LICENSE-MIT"
}
