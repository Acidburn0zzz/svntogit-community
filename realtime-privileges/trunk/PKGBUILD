# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=realtime-privileges
pkgver=1
pkgrel=1
pkgdesc="Realtime privileges for users"
arch=('any')
url="https://wiki.archlinux.org/index.php/Realtime_process_management"
license=('GPL')
groups=('pro-audio' 'realtime')
install="${pkgname}.install"
changelog=ChangeLog
source=("99-${pkgname}.conf"
        "sysusers-${pkgname}.conf"
        "40-${pkgname}.rules"
        'ChangeLog')
sha512sums=('4d812d89561519fe79613d03b531408122a3ae729eba631187848c3480d6235b483102e8c647e6eb2f579293f462b0dae7f4f04d56e085063e24d69f3ed96b4a'
            '2f4d048a3e3c6de75bb7432092e1943143507110ff33aa9f7f2ca4ed3f028536c8f910735f5218f0fa6cc89e8eae70699bcce18c0dadc07e518fc3cf33611518'
            '10e047ec557540c3847ed8c1e494d59d52dc2d102a58d8cf210cbad6042740782172bd7e674c19f1f6048f5210576b075a494f9b4bba9cc5fd3e78f61fa35cc4'
            '05a958946d691861e6803c1cfe4147159c09a43d6a9eca7169ad4c23009c4bea2890243307e21f3798746e65d877ba9b37f4f3e444a8560ef60b7f052296b27f')

package() {
  # realtime group installed through sysusers.d
  install -vDm 644 "sysusers-${pkgname}.conf" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
  # limits for realtime group
  install -vDm 644 "99-${pkgname}.conf" -t "${pkgdir}/etc/security/limits.d/"
  # udev rules, giving access to devices to realtime group
  install -vDm 644 "40-${pkgname}.rules" -t "${pkgdir}/usr/lib/udev/rules.d/"
}
