# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>

_pkgbasename=gnome-settings-daemon
pkgname=$_pkgbasename-pulse
pkgver=2.32.1
pkgrel=1
pkgdesc="The GNOME Settings daemon with PulseAudio support"
arch=('i686' 'x86_64')
license=('GPL')
depends=('libgnomekbd>=2.32.0' 'gnome-desktop>=2.32.0' 'libnotify>=0.4.5' 'pulseaudio' 'libcanberra-pulse' 'hicolor-icon-theme' 'libgnome-data')
makedepends=('intltool' 'pkg-config')
conflicts=("$_pkgbasename")
provides=("$_pkgbasename=$pkgver-$pkgrel")
options=(!emptydirs !libtool)
install=gnome-settings-daemon.install
url="http://www.gnome.org"
groups=(pulseaudio-gnome)
source=(http://ftp.gnome.org/pub/gnome/sources/${_pkgbasename}/2.32/${_pkgbasename}-${pkgver}.tar.bz2)
sha256sums=('0074b3fec3ad6e3ab91a05dc20906b06101ea8bca0cd2caf394a5cc141b05e86')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --libexecdir=/usr/bin --disable-static
  make
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make DESTDIR="${pkgdir}" GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 install

  install -d -m755 "${pkgdir}/usr/share/gconf/schemas"
  gconf-merge-schema "${pkgdir}/usr/share/gconf/schemas/${_pkgbasename}.schemas" --domain gnome-settings-daemon ${pkgdir}/etc/gconf/schemas/*.schemas
  rm -f ${pkgdir}/etc/gconf/schemas/*.schemas
}
