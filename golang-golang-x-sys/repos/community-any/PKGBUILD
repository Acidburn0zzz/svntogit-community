# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-sys
pkgver=0.0.20190401
_commit=e4093980e83e70784fc393a93d5620e2654097f0
pkgrel=1
pkgdesc='Go packages for low-level interaction with the operating system'
arch=('any')
url="https://github.com/golang/sys"
license=('BSD')
depends=('go-pie')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/sys/archive/$_commit.tar.gz")
sha512sums=('c2148ebce46e2830266cd077c064077463c689548d626f18775ee5ba2d1c34428ae58c91862e27d4df377920af14e6faeb2324b5b885e2341d33380a6ac77c10')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$srcdir"/build/src/golang.org/x/sys
  cd "$srcdir"/build/src/golang.org/x/sys
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/sys

  install -Dm644 sys-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
