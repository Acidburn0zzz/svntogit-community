# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-sys
pkgver=0.0.20190515
_commit=61b9204099cb1bebc803c9ffb9b2d3acd9d457d9
pkgrel=1
pkgdesc='Go packages for low-level interaction with the operating system'
arch=('any')
url="https://github.com/golang/sys"
license=('BSD')
depends=('go-pie')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/sys/archive/$_commit.tar.gz")
sha512sums=('1a8d68ce52f29e5cdf28ff36986db47989f3d131ccadc63dade98edc01607fc6ff9255394ac430dcb136ef4466d4a42f3656776de12ce5baa617f8e582df6553')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$srcdir"/build/src/golang.org/x/sys
  cd "$srcdir"/build/src/golang.org/x/sys
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/sys-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/sys

  install -Dm644 sys-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
