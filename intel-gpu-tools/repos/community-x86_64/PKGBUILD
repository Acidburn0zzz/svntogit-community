# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Mikael Eriksson <mikael_eriksson@miffe.org>
# Originally by: Denis Smirnov <detanator@gmail.com>

_pkgname=igt-gpu-tools
pkgname=intel-gpu-tools
pkgver=1.23
pkgrel=1
pkgdesc="Tools for development and testing of the Intel DRM driver"
arch=(x86_64)
license=(MIT)
url='https://gitlab.freedesktop.org/drm/igt-gpu-tools'
depends=(libdrm libpciaccess cairo python xorg-xrandr procps-ng kmod libxv libunwind peg systemd)
makedepends=(swig gtk-doc xorg-util-macros dri2proto meson)
source=(https://xorg.freedesktop.org/releases/individual/app/${_pkgname}-$pkgver.tar.xz{,.sig})
sha512sums=('8697265a978359e28983f483bf95819844597fc2080923d15d0587f6d648081af27e8ea318873e20103bbf8446b9f1005c6bc90a6276d3169837abd181f96839'
            'SKIP')
validpgpkeys=('669AC671793B22350E46960F045DBC54CFE49C2E')

prepare() {
  mkdir -p build
}

build() {
  cd build
  meson ../$_pkgname-$pkgver \
    --prefix=/usr \
    --libexecdir=/usr/lib

  ninja
}

check() {
  cd build
  ninja test
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install

  cd ../$_pkgname-$pkgver
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/COPYING"
}
