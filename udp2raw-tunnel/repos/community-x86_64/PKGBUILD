# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Peter Cai <peter at typeblog dot net>

pkgname=udp2raw-tunnel
pkgver=20181113.0
pkgrel=3
pkgdesc='An Encrypted, Anti-Replay, Multiplexed UdP Tunnel, tunnels udp traffic through fake-tcp or icmp by using raw socket'
url='https://github.com/wangyu-/udp2raw-tunnel'
arch=('x86_64' 'i686')
depends=('iptables')
license=('MIT')
install=udp2raw-tunnel.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/wangyu-/udp2raw-tunnel/archive/$pkgver.tar.gz"
        "udp2raw_script.sh"
        "udp2raw@.service")
sha512sums=('92b1f3bcfdf3eb9c20db94d1f0e47cde1784691e5058920095830c98ca9d6ac22e178b820b0c6ebc4d6562362864e94e3d08e3f77d910634c4813f240b554a1a'
            'bd58325188f7a4d143d97ea6dfb0b8f2f86f6b82b8cb7496478fb3a9c425d42044db91bd7f554ed95443ce11bad288d48aad1f76404e3efec68ecbff8c56820f'
            'f1e2831c02163646c26601896a617e07704dc1b6324fc73f4ea576959bd33d70425472d42333ad8b7f210bd8761a83bf08db40987b016067f01590703982ae42')

build() {
  cd $pkgname-$pkgver
  make fast
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 udp2raw "$pkgdir"/usr/bin/udp2raw
  install -Dm644 example.conf "$pkgdir"/etc/udp2raw/example.conf
  install -Dm755 "$srcdir"/udp2raw_script.sh "$pkgdir"/usr/lib/udp2raw/udp2raw_script.sh
  install -Dm644 "$srcdir"/udp2raw@.service "$pkgdir"/usr/lib/systemd/system/udp2raw@.service
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
