# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Peter Cai <peter at typeblog dot net>

pkgname=udp2raw-tunnel
pkgver=20180225.1
pkgrel=1
pkgdesc='An Encrypted, Anti-Replay, Multiplexed UdP Tunnel, tunnels udp traffic through fake-tcp or icmp by using raw socket'
url='https://github.com/wangyu-/udp2raw-tunnel'
arch=('x86_64' 'i686')
depends=('iptables')
license=('MIT')
install=udp2raw-tunnel.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/wangyu-/udp2raw-tunnel/archive/$pkgver.tar.gz"
        "udp2raw_script.sh"
        "udp2raw@.service")
sha512sums=('e9b711b50c963f59c7a820ea70caf5456ce079dea337407533453bcfe067d91f284abd2af6fb11c049fddb8cac6ed133242776452844cd1c5a830dfe27666680'
            'bd58325188f7a4d143d97ea6dfb0b8f2f86f6b82b8cb7496478fb3a9c425d42044db91bd7f554ed95443ce11bad288d48aad1f76404e3efec68ecbff8c56820f'
            '8ae8ae36d20e40447e85b38fd437e7aee381ec35c78638f97e7418eb616e1aacbbd3eace1a5615314ffec05f290c1ebd4c2e30073ca6373d84f5ff2514f214da')

build() {
  cd $pkgname-$pkgver
  make fast
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 udp2raw "$pkgdir"/usr/bin/udp2raw
  install -Dm644 example.conf "$pkgdir"/etc/udp2raw/example.conf
  install -Dm755 "$srcdir"/udp2raw_script.sh "$pkgdir"/usr/lib/udp2raw/udp2raw_script.sh
  install -Dm644 "$srcdir"/udp2raw@.service "$pkgdir"/usr/lib/systemd/system/udp2raw@.service
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
