# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-zope-schema
pkgname=('python-zope-schema' 'python2-zope-schema')
pkgver=4.9.1
pkgrel=1
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
makedepends=('python-setuptools' 'python2-setuptools' 'python-zope-interface'
             'python2-zope-interface' 'python-zope-event' 'python2-zope-event')
checkdepends=('python-zope-testrunner' 'python2-zope-testrunner' 'python-zope-testing'
              'python2-zope-testing' 'python-zope-i18nmessageid' 'python2-zope-i18nmessageid')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/zopefoundation/zope.schema/archive/$pkgver.tar.gz")
sha512sums=('9234eda33e65cf84604c0939d69ba02cb24dc6052999b2cb6e6147894ffcee9e9c5bfbf4e614583d74998b6727ee6398208b02b1945787fd0ecd78c7ab8ddef0')

prepare() {
  cp -a zope.schema-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py build

  cd "$srcdir"/zope.schema-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/zope.schema-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src

  cd "$srcdir"/zope.schema-$pkgver-py2
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python2 -m zope.testrunner --test-path=src
}

package_python-zope-schema() {
  depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')

  cd "$srcdir"/zope.schema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-zope-schema() {
  depends=('python2-setuptools' 'python2-zope-interface' 'python2-zope-event')

  cd "$srcdir"/zope.schema-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
