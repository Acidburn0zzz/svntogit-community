# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zope-schema
pkgver=6.1.1
pkgrel=1
pkgdesc="zope.interface extension for defining data schemas"
arch=('any')
url="https://github.com/zopefoundation/zope.schema"
license=('ZPL')
depends=('python-setuptools' 'python-zope-interface' 'python-zope-event')
checkdepends=('python-zope-testrunner' 'python-zope-testing' 'python-zope-i18nmessageid')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/zope.schema/archive/$pkgver.tar.gz")
sha512sums=('d45b0f996385d471d2528d0ce0b13a08fe46135d0c2a8b434e32b0b9205ac288ad1da2612238a57f4708163b0f4a4bf31fc6f3dc6dd886df50311514651c9a27')

build() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/zope.schema-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python -m zope.testrunner --test-path=src
}

package() {
  cd "$srcdir"/zope.schema-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
