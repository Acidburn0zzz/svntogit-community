# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=cordova
pkgver=9.0.0
pkgrel=1
pkgdesc='Cordova command line interface tool'
arch=('any')
url='https://cordova.apache.org'
license=('Apache')
depends=('npm' 'semver')
install=cordova.install
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('cd610fa3db868fd28d704854d8ba68debe0760adb5b4bf9ab78f7a3762cb9ee096b9656776fe9059e77cf84625ff4f2badc36c86b12d7f35e3f54c7abd09b63d')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"

  # Delete npm installed pyc files
  find "$pkgdir" -name "*.pyc" -delete

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}
