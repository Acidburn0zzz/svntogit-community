# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-os-client-config
pkgver=1.33.0
pkgrel=2
arch=('any')
pkgdesc='OpenStack Client Configuation Library'
url='https://github.com/openstack/os-client-config'
license=('Apache')
depends=('python-openstacksdk')
makedepends=('python-setuptools')
checkdepends=('python-stestr' 'python-oslotest' 'python-jsonschema' 'python-mock'
              'python-glanceclient')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/os-client-config/archive/$pkgver.tar.gz")
sha512sums=('0ca58456e90b532917f6e4a52f27ba8c434f48874e3f096ff7b34b7152ae49b914e2f47d20b876b8c2dc4e8357cd23aa149193d8aa438934742fc2686a89fbde')

export PBR_VERSION=$pkgver

prepare() {
  # FS#54781
  # https://bugs.launchpad.net/os-client-config/+bug/1707019
  # sed -i "/^packages =/i package_data = os_client_config = *.json */*.json" os-client-config-$pkgver/setup.cfg

  sed -i 's/assertItemsEqual/assertCountEqual/' os-client-config-$pkgver/os_client_config/tests/test_config.py
}

build() {
  cd os-client-config-$pkgver
  python setup.py build
}

check() {
  cd os-client-config-$pkgver
  stestr run
}

package() {
  cd os-client-config-$pkgver
  python setup.py install --root "$pkgdir" --optimize=1
}
