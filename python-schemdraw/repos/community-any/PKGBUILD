# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Sequencer <liujiuyang1994@gmail.com>

pkgname=python-schemdraw
pkgver=0.7.1
pkgrel=1
pkgdesc="Electrical circuit schematic drawing."
url="https://bitbucket.org/cdelker/schemdraw"
license=('BSD')
arch=('any')
depends=('python-numpy' 'python-matplotlib')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://bitbucket.org/cdelker/schemdraw/get/$pkgver.tar.gz")
sha512sums=('5d7d57bd98c99a0cca4a77d998f610a5de02bbe0f6a625e7e6d2e3ae26bf2a3fb84bb69b92b5ea1d748285045c47769e7df4c338ddfd43087c7c9026bbb973a9')

prepare() {
  mv cdelker-schemdraw-* schemdraw-$pkgver
}

build() {
  cd schemdraw-$pkgver
  python setup.py build
}

package() {
  cd schemdraw-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
