# $Id: PKGBUILD 143902 2015-10-13 11:22:31Z spupykin $
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Igor Scabini <furester @ gmail.com>

pkgname=cython2-devel
pkgver=0.24a1
pkgrel=1
pkgdesc="C-Extensions for Python - Development version"
arch=(i686 x86_64)
url="http://www.cython.org"
license=('APACHE')
depends=('python2-setuptools')
provides=('cython2')
conflicts=('cython2')
source=("http://cython.org/release/Cython-$pkgver.tar.gz"
        https://patch-diff.githubusercontent.com/raw/cython/cython/pull/483.patch)
md5sums=('72fcfb4415ebd843aa9edaf3cd89c3f8'
         '90d18245397a3fe983418f4d736a7167')

prepare() {
  cd Cython-$pkgver
  patch -p1 -i ../483.patch
}

package() {
  cd $srcdir/Cython-$pkgver
  python2 setup.py install --root=$pkgdir

  mv $pkgdir/usr/bin/cygdb $pkgdir/usr/bin/cygdb2
  mv $pkgdir/usr/bin/cython $pkgdir/usr/bin/cython2
  mv $pkgdir/usr/bin/cythonize $pkgdir/usr/bin/cythonize2
}
