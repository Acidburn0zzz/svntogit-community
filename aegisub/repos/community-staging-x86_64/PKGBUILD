# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>
# Contributor: kozec <kozec@kozec.com>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>

pkgname=aegisub
pkgver=3.0.4
pkgrel=2
pkgdesc="A general-purpose subtitle editor with ASS/SSA support"
arch=('i686' 'x86_64')
url="http://www.aegisub.org"
license=('GPL' 'BSD')
depends=('desktop-file-utils' 'ffmpegsource' 'fftw' 'hicolor-icon-theme' 'hunspell' 'lua51' 'wxgtk2.9')
makedepends=('mesa')
install=aegisub.install
source=("http://ftp.aegisub.org/pub/releases/${pkgname}-${pkgver}.tar.xz"
        'wxgtk2.9.5.patch')
sha256sums=('7d5d8b94da02278b3327f24dd546c0c897c4b369bd2da9e094dc60371422019a'
            'b3dc9f094d23e6f38a7a03dc3692056bd9e09095dab8385d7f2fd7c0e73e19d2')

prepare() {
  cd ${pkgname}/${pkgname}

  patch -Np2 -i ../../wxgtk2.9.5.patch
}

build() {
  cd ${pkgname}/${pkgname}

  export ACLOCAL=aclocal
  export AUTOMAKE=automake
  ./autogen.sh --prefix=/usr --without-{portaudio,openal,oss} --with-wxdir=/usr/include/wx-2.9 --with-wx-config=/usr/bin/wx-config-2.9
  make
}

package() {
  cd ${pkgname}/${pkgname}

  make DESTDIR="${pkgdir}" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/aegisub
  install -m 644 LICENCE "${pkgdir}"/usr/share/licenses/aegisub/LICENSE
}

# vim: ts=2 sw=2 et:
