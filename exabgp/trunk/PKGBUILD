# $Id$ 
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>

pkgname=exabgp
pkgver=1.3.4
pkgrel=1
pkgdesc='BPG route injector'
arch=('any')
license=('custom')
url='http://code.google.com/p/exabgp/'
depends=('python2' 'start-stop-daemon')
backup=('etc/exabgp.conf')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tgz"
        'exabgp.rc'
        'exabgp.conf'
        'exabgp.1')
md5sums=('fe7a34de48966b34e126d73d6454ef31'
         '139cf5cccb29dd6addb64cbc1730c18e'
         '50301686d63177a5b251f15e532b55e7')

package() {
  # install initscripts
  install -D -m 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  
  # install empty config file
  install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"

  # install man page (debian made)
  install -D -m 644 exabgp.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --install-data=/data

  # remove useless data
  rm -r "$pkgdir/data"

  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # rename binary
  mv "$pkgdir/usr/bin/bgpd" "$pkgdir/usr/bin/exabgp"
}

# vim:set ts=2 sw=2 ft=sh et:
