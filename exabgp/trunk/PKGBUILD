# $Id$ 
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=exabgp
pkgver=2.0.8
pkgrel=1
pkgdesc='BPG route injector'
arch=('any')
license=('custom')
url='http://code.google.com/p/exabgp/'
depends=('python2' 'start-stop-daemon')
backup=('etc/exabgp.conf' 'etc/conf.d/exabgp')
source=("http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tgz"
        'exabgp.rc'
        'exabgp.conf')
sha1sums=('99110c9dd609bc742f39b2e5cb98bb28da89ebcc'
          'e07bc077ebb7b380cee3b808013f8a0132f33d1d'
          'ad11f8b2d5a2a8448b3f09d7f2d15201528054a3')

build() {
  # arch python2
  find $pkgname-$pkgver -type f -exec \
    sed -i '1s,^#!/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' {} \; -print
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --install-data=/

  # install license
  install -D -m 644 COPYRIGHT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # move data in a more private directory
  mv "$pkgdir/etc/$pkgname" "$pkgdir/usr/share/$pkgname"

  # install initscripts
  install -D -m 755 ../$pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -D -m 644 ../$pkgname.conf "$pkgdir/etc/conf.d/$pkgname"

  # install empty config file
  install -D -m 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
