# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.16.0.0
pkgrel=1
pkgdesc="A 2D CAD package based upon Qt"
arch=('i686' 'x86_64')
url="http://www.qcad.org"
license=('GPL3')
depends=('qtwebkit')
makedepends=('glu' 'gdb')
options=('libtool')
source=($pkgname-$pkgver.tar.gz::https://github.com/qcad/qcad/archive/v${pkgver}.tar.gz
        QCad.desktop)
sha256sums=('4563a2909415fe19e66a9304316be3ba31bc632cfc3d9c971795a7af84720a4d'
            '1e194dcf59122098be0219fa220a0a3f33cf8919f0a778b6cd888da009efc6d5')

prepare() {
  cd "$srcdir"/qcad-$pkgver
}

build() {
  cd "$srcdir"/qcad-$pkgver
  qmake-qt4 -r
  make all -j1
}

package() {
  cd "$srcdir"/qcad-$pkgver

  # remove project files
  find . \( -name '*.pri' -or -name '.pro' -or -name '*.ts' \) -delete
  find . \( -name 'Makefile' -name '.gitignore' \) -delete

  install -dm755 "$pkgdir"/usr/share/{qcad,pixmaps,applications}
  cp -r examples fonts libraries linetypes patterns plugins scripts ts "$pkgdir"/usr/share/qcad
  cp debug/* "$pkgdir"/usr/share/qcad

  install -m755 readme.txt "$pkgdir"/usr/share/qcad/readme.txt

  # qtwebkit
  ln -sf /usr/lib/qt4/plugins/designer/libqwebview.so "$pkgdir"/usr/share/qcad/plugins/designer/libqwebview.so
  # qt
  for sofiles in /usr/lib/qt4/plugins/imageformats/*.so
  do
    ln -sf ${sofiles} "$pkgdir"/usr/share/qcad/plugins/imageformats/${sofiles##/*/}
  done
  for sofiles in /usr/lib/qt4/plugins/sqldrivers/*.so
  do
    ln -sf ${sofiles} "$pkgdir"/usr/share/qcad/plugins/sqldrivers/${sofiles##/*/}
  done

  install -Dm644 scripts/qcad_icon.png "$pkgdir"/usr/share/pixmaps/qcad_icon.png
  install -Dm644 "$srcdir"/QCad.desktop "$pkgdir"/usr/share/applications/QCad.desktop

  install -dm0755 "$pkgdir"/usr/bin
  echo -e '#!/bin/sh\nLD_LIBRARY_PATH=${LD_LIBRARY_PATH:+$LD_LIBRARY_PATH:}"/usr/share/qcad" exec /usr/share/qcad/qcad-bin "$@"' >"$pkgdir"/usr/bin/qcad

  chmod 0755 "$pkgdir"/usr/bin/qcad
}
