# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Giovanni Scafora <linuxmania@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=qcad
pkgver=3.0.14.0
pkgrel=2
pkgdesc="A 2D CAD package based upon Qt"
arch=('i686' 'x86_64')
url="http://www.ribbonsoft.com/qcad.html"
license=('GPL3')
depends=('qtwebkit')
makedepends=('glu')
options=(libtool)
source=($pkgname-$pkgver.zip::https://github.com/qcad/qcad/archive/v${pkgver}.zip
        QCad.desktop)
md5sums=('2c460838235a95a4d969e508a75b8206'
         '8c4288986b78b14a813b005e81b6ba53')

prepare() {
  cd ${srcdir}/qcad-$pkgver

  sed -i '1,1i#include <unistd.h>' src/core/{RLocalPeer,RS}.cpp
  sed -i '1,1i#include <sys/sysinfo.h>' src/core/{RScriptHandler,RS}.cpp
}

build() {
  cd ${srcdir}/qcad-$pkgver

  qmake-qt4
  make PREFIX=/usr CXX="g++ -fpermissive"
}

package() {
  cd ${srcdir}/qcad-$pkgver

  # remove project files
  find . \( -name '*.pri' -or -name '.pro' -or -name '*.ts' \) -delete
  find . \( -name 'Makefile' -name '.gitignore' \) -delete

  install -dm755 ${pkgdir}/usr/share/{qcad,pixmaps,applications}
  cp -r examples fonts libraries patterns plugins scripts ts ${pkgdir}/usr/share/qcad
  cp release/* ${pkgdir}/usr/share/qcad

  install -m755 qcad ${pkgdir}/usr/share/qcad/qcad
  install -m755 readme.txt ${pkgdir}/usr/share/qcad/readme.txt

  # qtwebkit
  ln -s /usr/lib/qt4/plugins/designer/libqwebview.so ${pkgdir}/usr/share/qcad/plugins/designer/libqwebview.so
  # qt
  for sofiles in /usr/lib/qt4/plugins/imageformats/*.so
  do
    ln -s ${sofiles} ${pkgdir}/usr/share/qcad/plugins/imageformats/${sofiles##/*/}
  done
  for sofiles in /usr/lib/qt4/plugins/sqldrivers/*.so
  do
    ln -s ${sofiles} ${pkgdir}/usr/share/qcad/plugins/sqldrivers/${sofiles##/*/}
  done

  install -Dm644 scripts/qcad_icon.png ${pkgdir}/usr/share/pixmaps/qcad_icon.png
  install -Dm644 $srcdir/QCad.desktop ${pkgdir}/usr/share/applications/QCad.desktop

  install -dm0755 $pkgdir/usr/bin
  echo -e '#!/bin/sh\ncd /usr/share/qcad\nexec ./qcad' >$pkgdir/usr/bin/qcad
  chmod 0755 $pkgdir/usr/bin/qcad
}
