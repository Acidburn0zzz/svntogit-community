# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
# Contributor: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=python2-matplotlib
pkgver=2.2.3
pkgrel=5
pkgdesc="A python plotting library, making publication quality plots"
arch=('x86_64')
url="https://matplotlib.org"
license=('custom')
depends=('python2-numpy' 'python2-dateutil' 'python2-pyparsing' 'python2-pytz' 'libpng' 'freetype2'
         'python2-cycler' 'python2-six' 'python2-backports.functools_lru_cache' 'python2-subprocess32' 'python2-kiwisolver')
optdepends=('tk: Tk{Agg,Cairo} backends'
            'python2-pyqt5: Qt5{Agg,Cairo} backends'
            'pygtk: GTK{,Agg,Cairo} backends'
            'python2-gobject: for GTK3{Agg,Cairo} backend'
            'python2-wxpython3: WX{,Agg,Cairo} backend'
            'python2-cairo: {GTK,GTK3,Qt5,Tk,WX}Cairo backends'
            'python2-cairocffi: alternative for Cairo backends'
            'python2-tornado: WebAgg backend'
            'ffmpeg: for saving movies'
            'imagemagick: for saving animated gifs'
            'python2-pillow: for reading/saving jpeg/bmp/tiff files'
            'ghostscript: usetex dependencies'
            'texlive-bin: usetex dependencies')
makedepends=('python2-setuptools'
             'tk' 'python2-pyqt5' 'pygtk' 'python2-gobject'
             'python2-wxpython3' 'python2-cairocffi' 'python2-tornado'
             'python2-pillow' 'ghostscript' 'texlive-bin')
checkdepends=('python2-pytest-xdist' 'python2-pytest-runner' 'xorg-server-xvfb'
              'texlive-core' 'texlive-latexextra' 'inkscape' 'mencoder'
              'ffmpeg' 'imagemagick' 'ttf-freefont' 'python2-pandas' 'python2-mock')
install=$pkgname.install
source=($pkgname-$pkgver.tar.gz::"https://github.com/matplotlib/matplotlib/archive/v$pkgver.tar.gz"
        $pkgname-numpy-asscalar.patch::"https://github.com/matplotlib/matplotlib/commit/62297d167c2b7ac8305ba6a60ec0191c6e7a09af.patch"
        $pkgname-13181.diff
        setup.cfg)
sha512sums=('d118f5d56e2f578031aba22933c0b3a4423a31a04f50f08cc1aa660186546d09692a9cf401bb5f24cb296f94fbfd8707460728d501ac2bd4a624dfa89e92949b'
            'bb307885c17de71edf33dd09322aca99c2fade6dfbdf3482a7c11f2522fc40e2d41d8f51e77d2531de746906aabf3a68fa1bd8d76d48ea979c8010937713c0a9'
            '88d71e85f707c9becc1d77812770889759b4dd96ec5c5fce20361c0c1a3fe1bd6e04ba804fcd7ff0472249ec8099ec63716184975b3d609aed84bfdd0a8e05ad'
            'f08c0b2e94599fdf2b736b8a655d862209934441d90f20ed872cfc128b8d2a6525763ab7cf64f2b3630e22a90798f4c52aa93b18087e9b48708d9bae34aada0b')

prepare() {
  cd matplotlib-$pkgver

  patch -p1 -i ../$pkgname-numpy-asscalar.patch
  patch -p1 -i ../$pkgname-13181.diff

  for file in $(find . -name '*.py' -print); do
    sed -i -e "s|^#!.*/usr/bin/python|#!/usr/bin/python2|" \
           -e "s|^#!.*/usr/bin/env *python|#!/usr/bin/env python2|" $file
  done

  cd ..

  cp -a matplotlib-$pkgver{,-test}

  # Configure tests (FS#48175)
  cp setup.cfg matplotlib-$pkgver-test/
}

build() {
  cd matplotlib-$pkgver
  python2 setup.py build
}

check() {
  cd matplotlib-$pkgver-test
  xvfb-run -a -s "+extension GLX +extension RANDR +render -screen 0 1280x1024x24" \
    python2 setup.py pytest --addopts="-n auto" || warning "Tests failed" # Different font rendering details
}

package() {
  cd matplotlib-$pkgver
  python2 setup.py install --root "$pkgdir" --prefix=/usr --optimize=1 --skip-build
  # FS#41820 - unset default backend
  sed -i 's/backend      : qt5agg/#backend      : agg/' $pkgdir/usr/lib/python2.7/site-packages/matplotlib/mpl-data/matplotlibrc
  install -Dm644 doc/users/license.rst -t "$pkgdir"/usr/share/licenses/${pkgname}/
}
