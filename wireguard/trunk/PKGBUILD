# Maintainer: Christian Hesse <mail@eworm.de>
# Contributor: Jason A. Donenfeld <Jason@zx2c4.com>

pkgbase=wireguard
pkgname=(wireguard-dkms wireguard-tools)
pkgver=0.0.20191212
pkgrel=2
pkgdesc='next generation secure network tunnel'
arch=('x86_64')
url='http://www.wireguard.com/'
license=('GPL')
makedepends=('git' 'libmnl')
validpgpkeys=('AB9942E6D4A4CFC3412620A749FC7012A5DE03AE') # Jason A. Donenfeld <Jason@zx2c4.com>
source=("https://git.zx2c4.com/WireGuard/snapshot/WireGuard-${pkgver}.tar"{.xz,.asc}
        kernel-5.4.5.patch)
sha256sums=('b0d718380f7a8822b2f12d75e462fa4eafa3a77871002981f367cd4fe2a1b071'
            'SKIP'
            '603e53fb70ef0b40601924f699c3629c65c879e03c57b3e628e5fea47ac710ba')

prepare() {
	cd WireGuard-${pkgver}/

	patch -Np1 -i ../kernel-5.4.5.patch

	find contrib/examples/ -name '.gitignore' -delete
}

build() {
	cd WireGuard-${pkgver}/

	make -C src/tools/
}

package_wireguard-dkms() {
	depends=('dkms')
	provides=('WIREGUARD-MODULE')

	cd WireGuard-${pkgver}/

	make -C src/ \
		DESTDIR="${pkgdir}/" \
		DKMSDIR="/usr/src/wireguard-${pkgver}/" \
		dkms-install
}

package_wireguard-tools() {
	depends=('libmnl')
	optdepends=('openresolv: for DNS functionality'
	            'wireguard-dkms: wireguard module, built by dkms'
	            'wireguard-arch: wireguard module for linux'
	            'wireguard-lts: wireguard module for linux-lts')

	cd WireGuard-${pkgver}/

	make -C src/tools/ \
		DESTDIR="${pkgdir}/" \
		WITH_BASHCOMPLETION=yes \
                WITH_WGQUICK=yes \
                WITH_SYSTEMDUNITS=yes \
                install

	install -d -m0755 "${pkgdir}"/usr/share/${pkgbase}/
	cp -r contrib/examples/ "${pkgdir}"/usr/share/${pkgbase}/
}

