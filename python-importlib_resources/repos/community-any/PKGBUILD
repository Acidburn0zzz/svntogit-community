# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=5.1.3
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python')
makedepends=('python-setuptools-scm' 'python-toml')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('cfd778e6a07799577625a0102b79d3fa69c710215168b6aaeffea68db41ff84ff5c15681fcd4d68be043a618554189720520fc0d1266df3e522035924903d39a')
b2sums=('7edf1d75ae977c674223059504e5d7912cb83503d7a70b0b9251c5af130acf8c1466b0314c583167c1210a3fc3005d298be5fd1e6b1d7e6d128ad46142decfab')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
