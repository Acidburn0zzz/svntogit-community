# Maintainer: David Runge <dvzrv@archlinux.org>

_name=importlib_resources
pkgname=python-importlib_resources
pkgver=3.2.0
pkgrel=1
pkgdesc="Design and implementation for a planned importlib.resources"
arch=('any')
url="https://github.com/python/importlib_resources"
license=('Apache')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-tests')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('4cd1f3e54c44c0ba13c420668baa4a4e1cd964a978927b0c96d8c70ad6a11fc7d33cfd0a52367f719e1b3ff2945d32f030a672c1c7cfb0bb8152636767518470')
b2sums=('e6e300ffc8b60f573b2d9df7dfaeadcb2dfa64c6c0b6e55a0320ae14a8966893443984d8907a090e1a5d834c1f595375f1cca658ebaa1ad4e4f889cc7f324843')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
