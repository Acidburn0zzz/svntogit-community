# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer:
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: Gregor Ibic <gregor.ibic@intelicom.si>

pkgname=dia
pkgver=0.97.1
pkgrel=3
pkgdesc="A GTK+ based diagram creation program"
arch=('i686' 'x86_64')
license=('GPL')
url="http://live.gnome.org/Dia"
install=dia.install
depends=('libxslt' 'desktop-file-utils' 'libart-lgpl' 'gtk2')
makedepends=('intltool' 'python2' 'docbook-xsl')
optdepends=('python2')
options=('!libtool')
source=("ftp://ftp.gnome.org/pub/gnome/sources/${pkgname}/0.97/${pkgname}-${pkgver}.tar.bz2"
	"dia-overflow-fix.patch")
md5sums=('57e44bb9f387559a0506b52a134deaf0'
         '8fd9a2ad35b5a6fd8c758d7c73dbfe66')
md5sums=('57e44bb9f387559a0506b52a134deaf0'
         '8fd9a2ad35b5a6fd8c758d7c73dbfe66')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  export PYTHON=/usr/bin/python2
  sed -i 's#python2\.1#python2 python2.1#' configure
  patch -p1 <$srcdir/dia-overflow-fix.patch

  ./configure --prefix=/usr \
    --enable-db2html \
    --with-cairo \
    --with-python \
    --disable-gnome
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
