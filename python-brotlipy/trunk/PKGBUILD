# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-brotlipy
pkgname=('python-brotlipy' 'python2-brotlipy')
pkgver=0.6.0
pkgrel=2
pkgdesc='Python binding to the Brotli library'
arch=('i686' 'x86_64')
license=('MIT')
url='https://github.com/python-hyper/brotlipy'
makedepends=('python-setuptools' 'python2-setuptools' 'python-cffi' 'python2-cffi' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-hypothesis'
              'python2-hypothesis')
source=("git+https://github.com/python-hyper/brotlipy.git#tag=v$pkgver"
        "git+https://github.com/google/brotli.git")
md5sums=('SKIP'
         'SKIP')

prepare() {
  cd brotlipy
  git submodule init
  git config submodule."libbrotli".url "$srcdir"/brotli
  git submodule update

  rm -r libbrotli/python

  cp -a "$srcdir"/brotlipy{,-py2}
}

build() {
  cd "$srcdir"/brotlipy
  python setup.py build

  cd "$srcdir"/brotlipy-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/brotlipy
  python setup.py ptr

  cd "$srcdir"/brotlipy-py2
  python2 setup.py ptr
}

package_python-brotlipy() {
  depends=('python-cffi')

  cd brotlipy
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-brotlipy() {
  depends=('python2-cffi')

  cd brotlipy-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
