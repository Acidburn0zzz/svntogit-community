# $Id$

_pkgbasename=libsndfile
pkgname=lib32-$_pkgbasename
pkgver=1.0.23
pkgrel=1
pkgdesc="A C library for reading and writing files containing sampled sound (32-bit)"
arch=('x86_64')
url="http://www.mega-nerd.com/libsndfile"
license=('LGPL')
depends=('lib32-flac>=1.2.1' 'lib32-libvorbis' $_pkgbasename)
makedepends=('lib32-alsa-lib' 'gcc-multilib')
options=('!libtool')
source=(http://www.mega-nerd.com/libsndfile/files/${_pkgbasename}-${pkgver}.tar.gz)
md5sums=('d0e22b5ff2ef945615db33960376d733')
sha1sums=('dee36861b503fe7754f0131364523f45720a0914')

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  export CFLAGS="$CFLAGS -D_FILE_OFFSET_BITS=64"

  ./configure --prefix=/usr --disable-sqlite --libdir=/usr/lib32
  make -C src
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"
  make -C src DESTDIR="${pkgdir}" install
  make DESTDIR="$pkgdir" install-pkgconfigDATA

  rm -rf "$pkgdir/usr/include"
}
