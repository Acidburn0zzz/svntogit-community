# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=nginx-mod-passenger
pkgver=5.1.7
pkgrel=1

_modname="${pkgname#nginx-mod-}"
_nginxver=1.12.1

pkgdesc="Fast and robust web server and application server for Ruby, Python and Node.js"
arch=('i686' 'x86_64')
depends=("nginx=$_nginxver" 'ruby')
url="https://www.phusionpassenger.com"
license=('MIT')

source=(
	http://nginx.org/download/nginx-$_nginxver.tar.gz
	https://github.com/phusion/passenger/archive/release-$pkgver/$_modname-$pkgver.tar.gz
)

sha256sums=('8793bf426485a30f91021b6b945a9fd8a84d87d17b566562c3797aba8fac76fb'
            'e7cd8625870d80a38eaf6d0c376c9fe404ac22f1e4b31a5a0ef63a55a96a7da7')

build() {
	cd "$srcdir"/nginx-$_nginxver
	./configure --with-compat --add-dynamic-module=../$_modname-release-$pkgver/src/nginx_module
	make modules
}

package() {
	install -Dm644 "$srcdir"/$_modname-release-$pkgver/LICENSE \
	               "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

	cd "$srcdir"/nginx-$_nginxver/objs
	for mod in ngx_*.so; do
		install -Dm755 $mod "$pkgdir"/usr/lib/nginx/modules/$mod
	done
}
