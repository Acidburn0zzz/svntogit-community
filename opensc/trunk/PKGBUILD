# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: kevku <kevku@msn.com>

pkgname=opensc
pkgver=0.12.2
pkgrel=2
pkgdesc='Access smart cards that support cryptographic operations'
arch=('x86_64' 'i686')
url='http://www.opensc-project.org/opensc/'
license=("LGPL")
backup=('etc/opensc.conf')
depends=('openssl' 'pcsclite' 'libltdl')
options=('!libtool')
source=("http://www.opensc-project.org/files/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('5116adea5f2f9f22fb9896965789144b')
 
build() {
  cd $pkgname-$pkgver
  export LIBS=-lltdl
  ./configure --prefix=/usr --sysconfdir=/etc --enable-pcsc --disable-static
  make
}
 
package(){
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install install-man
  install -D -m644 etc/opensc.conf "$pkgdir/etc/opensc.conf"
} 

# vim:set ts=2 sw=2 ft=sh et:
