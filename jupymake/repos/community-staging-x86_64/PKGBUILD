# Maintainer: Antonio Rojas <arojas@archlinux.org>

_name=JuPyMake
pkgbase=jupymake
pkgname=(python-jupymake python2-jupymake)
pkgver=0.9
pkgrel=3
pkgdesc="A Python interface to Polymake"
arch=(x86_64)
url="https://github.com/sebasguts/JuPyMake"
license=(GPL2)
makedepends=(python-setuptools python2-setuptools polymake)
source=("https://pypi.python.org/packages/source/${_name:0:1}/${_name}/${_name}-${pkgver}.tar.gz")
sha256sums=('4c756cd6a6056a20cd71f7b5e463c79dad0257241bbd32a40b750882fafdb545')

prepare() {
  cp -r $_name-$pkgver{,-py2}
}

build() {
  cd $_name-$pkgver
  python setup.py build

  cd ../$_name-$pkgver-py2
  python2 setup.py build
}

package_python-jupymake() {
  depends=(python polymake)

  cd $_name-$pkgver
  python setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
}

package_python2-jupymake() {
  depends=(python2 polymake)

  cd $_name-$pkgver-py2    
  python2 setup.py install --skip-build --prefix=/usr --root="$pkgdir" --optimize=1
}
