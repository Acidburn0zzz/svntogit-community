# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.4.4
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python')
optdepends=('python-six: for logilab.common.testlib')
makedepends=('python-setuptools')
checkdepends=('python-six' 'python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz"
        py38.patch)
sha512sums=('a6b52e135748687958649f2cb50449fa1f5af4cea0c398f09dff10821f402ec1be51850843e2393614802014c2f94c14e6b593cb570f4487756b406152943367'
            'bcd1ad3a7a80edfe12e6b595d915476a6506713c5d9cc03a95305a9bacfdfa135b1f95e84a252555a4844ee5bbf6b8e4c0ca0cfc3743836e24e618d64b1578d2')

prepare() {
  patch -d logilab-common-$pkgver -p1 -i ../py38.patch
  cp -a logilab-common-$pkgver{,-py2}

  sed -i 's/from cgi import escape/from html import escape/' logilab-common-$pkgver/logilab/common/ureports/html_writer.py
}

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
