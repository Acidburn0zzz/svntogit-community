# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.7.2
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python-setuptools' 'python-mypy_extensions' 'python-typing_extensions')
checkdepends=('python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha512sums=('aa129645a81cd95bd112000a312dce965c714bddd393a8174cb012a549a61e5433864fdcbc2ee9325c5025dcb4948f0861b611e86ae09a032afe435089d6ea04')

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
