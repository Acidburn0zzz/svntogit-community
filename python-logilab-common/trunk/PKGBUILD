# Maintainer: Angel Velasquez <angvp@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=python-logilab-common
pkgver=1.5.2
pkgrel=1
pkgdesc="Useful miscellaneous modules used by Logilab projects"
arch=('any')
url="https://www.logilab.org/project/logilab-common"
license=('LGPL')
depends=('python')
optdepends=('python-six: for logilab.common.testlib')
makedepends=('python-setuptools')
checkdepends=('python-six' 'python-pytz')
source=("https://pypi.io/packages/source/l/logilab-common/logilab-common-$pkgver.tar.gz")
sha512sums=('311caede51f8486fb0ead3fe9ed53d01211e284f706d10a78e8dbe1611faa209ebef649a8be95eabf2873b57b10703e9d66bf5efc5cd4096c63885d6a58b5506')

prepare() {
  cp -a logilab-common-$pkgver{,-py2}

  sed -i 's/from cgi import escape/from html import escape/' logilab-common-$pkgver/logilab/common/ureports/html_writer.py
}

build() {
  cd logilab-common-$pkgver
  python setup.py build
}

check() {
  cd logilab-common-$pkgver
  PYTHONPATH="$PWD/build/lib:$PYTHONPATH" python bin/logilab-pytest
}

package() {
  cd logilab-common-$pkgver
  python setup.py install --optimize=1 --skip-build --prefix=/usr --root="$pkgdir"
}
