# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.7.2
pkgrel=1
pkgdesc='Almost exact clone of ncmpc with some new features'
url='http://ncmpcpp.rybczak.net/'
arch=('i686' 'x86_64')
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses' 'fftw' 'boost-libs')
makedepends=('boost')
source=(${pkgname}-${pkgver}.tar.bz2::http://rybczak.net/ncmpcpp/stable/${pkgname}-${pkgver}.tar.bz2)
sha512sums=('6ecc02694ecc6ff10e3a6d89d0b227385b1ab36c30afce57a8a8b74725063154736c78141d3fdaafb3a008e80855bc206dbf7e4aefc8486e01f300354967f1f8')

build() {
  cd ${pkgname}-${pkgver}
  BOOST_LIB_SUFFIX='' ./configure --prefix=/usr \
    --enable-clock \
    --enable-outputs \
    --enable-unicode \
    --enable-visualizer \
    --with-curl \
    --with-fftw \
    --with-taglib
  make
  make -C extras
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 extras/artist_to_albumartist -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
