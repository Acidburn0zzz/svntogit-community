# $Id$
# Maintainer: Mateusz Herych <heniekk@gmail.com>
# Contributor: Army <uli[dot]armbruster[at]gmail[dot]com>

pkgname=ncmpcpp
pkgver=0.5.7
pkgrel=1
pkgdesc="An almost exact clone of ncmpc with some new features."
arch=('i686' 'x86_64')
url="http://unkart.ovh.org/ncmpcpp/"
license=('GPL')
depends=('curl' 'libmpdclient' 'taglib' 'ncurses')
install=ncmpcpp.install
source=(http://unkart.ovh.org/ncmpcpp/${pkgname}-${pkgver}.tar.bz2)
md5sums=('6632c76f2f0836c5aa5a1a2fbb1c921c')

build() {
  cd "$srcdir/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
 	 --enable-unicode \
	 --enable-clock \
  	 --with-taglib \
  	 --with-curl
  make
}

package() {
  cd "$srcdir/${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}
