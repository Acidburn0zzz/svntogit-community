#Maintainer: Xyne <ac xunilhcra enyx, backwards>
pkgname=reflector
pkgver=2020.12.20.1
pkgrel=1
pkgdesc='A Python 3 module and script to retrieve and filter the latest Pacman mirror list.'
arch=(any)
license=(GPL)
url="https://xyne.archlinux.ca/projects/reflector"
depends=(python3)
optdepends=('rsync: rate rsync mirrors')
backup=(etc/xdg/reflector/reflector.conf)
source=(
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.20.1.tar.xz
  https://xyne.archlinux.ca/projects/reflector/src/reflector-2020.12.20.1.tar.xz.sig
)
sha512sums=(
  9b5825a45d5a079c3f7217edf881801bc58dcaba718df1877a169d0ed0653038325f88d3b6a0ec28ba389534c37bb4a1e76257a5ab9f1364e0f7bdcb75673b39
  a90c630778ca87af49c7d47a9cee61abdc3fb3b175f36457b03c7b975826e3e4e9643452f2fafb02583f5d6d7333d4c5252990e32643a30f6f96ace513961d56
)
md5sums=(
  a9866b97ed7e9fcecb52741ffb59ce94
  a020d3c7bd20e191b5b7d67a2a73bd35
)
validpgpkeys=('EC3CBE7F607D11E663149E811D1F0DC78F173680')

package ()
{
  cd "$srcdir/$pkgname-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 "man/${pkgname}.1.gz" "${pkgdir}/usr/share/man/man1/${pkgname}.1.gz"
  install -Dm644 'reflector.service' "$pkgdir/usr/lib/systemd/system/reflector.service"
  install -Dm644 'reflector.timer' "$pkgdir/usr/lib/systemd/system/reflector.timer"
  install -Dm644 'reflector.conf' "$pkgdir/etc/xdg/reflector/reflector.conf"
}


# vim: set ts=2 sw=2 et:
