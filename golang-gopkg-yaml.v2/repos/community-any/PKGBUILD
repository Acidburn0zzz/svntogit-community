# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-gopkg-yaml.v2
pkgver=2.2.8
pkgrel=2
pkgdesc='YAML support for the Go language'
arch=('any')
url="https://github.com/go-yaml/yaml"
license=('Apache')
depends=('go-pie')
checkdepends=('golang-gopkg-check.v1')
source=("$pkgname-$pkgver.tar.gz::https://github.com/go-yaml/yaml/archive/v$pkgver.tar.gz")
sha512sums=('ba9f916a09998af14371060432b414bf4f92dac8b623fa72667e425784b54d8dd3f088aa4bdb0d4499b79de904d0d2b04f1ebce1320c94b4af349e17cd170699')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$srcdir"/build/src/gopkg.in/yaml.v2
  cd "$srcdir"/build/src/gopkg.in/yaml.v2
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/gopkg.in
  cp -a "$srcdir"/yaml-$pkgver "$pkgdir"/usr/share/gocode/src/gopkg.in/yaml.v2
}
