# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgbase=simplebayes
pkgbase=python-simplebayes
pkgname=(python2-simplebayes python-simplebayes)
pkgver=1.5.7
pkgrel=1
pkgdesc="Memory-based, optional-persistence naïve bayesian text classifier Python library"
arch=(any)
url="https://github.com/hickeroar/simplebayes"
license=(MIT)
depends=(python python2)
makedepends=(git python-setuptools python2-setuptools)
_commit=b8da72c50d20b6f8c0df2c2f39620715b08ddd32  # tags/1.5.7
source=("git+https://github.com/hickeroar/simplebayes#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $_pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cp -a $_pkgbase{,-py2}
}

build() {
  # Building Python2
  cd $_pkgbase-py2
  python2 setup.py build

  # Building Python3
  cd ../$_pkgbase
  python3 setup.py build
}

package_python2-simplebayes() {
  depends=(python2)

  cd $_pkgbase-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python-simplebayes() {
  depends=(python)

  cd $_pkgbase
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
