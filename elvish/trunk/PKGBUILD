# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Haochen Tong <i at hexchain dot org>
# Contributor: Qi Xiao <xiaqqaix at gmail dot com>

pkgname=elvish
pkgver=0.15.0
pkgrel=3
pkgdesc="A friendly and expressive Unix shell"
arch=('x86_64')
url="https://github.com/elves/elvish"
license=('BSD')
makedepends=('git' 'go')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
b2sums=('51ccf4bf51e1d750ee10b95b67c819f3d81b5bf494c0257bd7cc20d16a7de0a4e6ff61623d1fe358c3ce2d9cb360af4732a899c66a831bae66f967bb061b2c8a')

prepare() {
  cd "$pkgname-$pkgver"
  mkdir build
  go mod vendor
}

build() {
  cd "$pkgname-$pkgver"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS} \
    -X github.com/elves/elvish/pkg/buildinfo.Reproducible=true \
    -X github.com/elves/elvish/pkg/buildinfo.Version=$pkgver" \
    -o build \
    ./cmd/...
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -race ./...
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/elvish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" *.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
