# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Haochen Tong <i at hexchain dot org>
# Contributor: Qi Xiao <xiaqqaix at gmail dot com>

pkgname=elvish
pkgver=0.16.2
pkgrel=1
pkgdesc="A friendly and expressive Unix shell"
arch=('x86_64')
url="https://elv.sh"
license=('BSD')
makedepends=('git' 'go')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/elves/elvish/archive/v$pkgver.tar.gz")
b2sums=('d76942353dc46c8ee8a04c585c745b2c4ba39857bfca8738d24e186ae88e83a57633190d55d138cca2459e265e2125ba588496908da9e2fcd20b0ff9069d194f')

prepare() {
  cd "$pkgname-$pkgver"

  # create directory for build output
  mkdir build

  # download dependencies
  go mod vendor
}

build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=vendor \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS} \
    -X github.com/elves/elvish/pkg/buildinfo.Reproducible=true \
    -X github.com/elves/elvish/pkg/buildinfo.Version=$pkgver" \
    -o build \
    ./cmd/...
}

check() {
  cd "$pkgname-$pkgver"
  go test -v -race ./...
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" build/elvish

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
