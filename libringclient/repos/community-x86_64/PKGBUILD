# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libringclient
pkgver=20190103
pkgrel=1
epoch=2
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (client communication library)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=('ring')
depends=('ring-daemon' 'qt5-base')
makedepends=('git' 'cmake' 'qt5-tools')
_commit=febeded0d0dc66b6b06b2f3d0b67238c1c3c6f4e
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-lrc#commit=${_commit}")
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ring-lrc \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
