# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=libringclient
pkgver=20190315
pkgrel=1
epoch=2
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (client communication library)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=('ring')
depends=('ring-daemon' 'qt5-base')
makedepends=('git' 'cmake' 'qt5-tools')
_commit=e97ed99658dc8e4a044cfb3d1cdce6bb607e32ed
source=("git+https://git.ring.cx/savoirfairelinux/ring-lrc.git#commit=${_commit}")
md5sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ring-lrc \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
