# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>

pkgbase=python-networkx
pkgname=(python-networkx python2-networkx)
pkgver=2.2
pkgrel=1
pkgdesc='Python package for the creation, manipulation, and study of the structure, dynamics, and functions of complex networks.'
arch=('any')
license=('BSD')
url="https://networkx.github.io"
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator')
checkdepends=('python-nose' 'python2-nose')
source=("https://github.com/networkx/networkx/archive/networkx-$pkgver.tar.gz")
sha512sums=('247ca4d66d33cec86412df846cc1fdbe1cbb0eeee83fb2a9ccdd702eb1453dd7167b397ed77d6b3dfdef6346485160ce75c12be58372238aaf98d334b4c8aeb4')

build() {
  cd networkx-networkx-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd networkx-networkx-$pkgver
  python setup.py nosetests
  python2 setup.py nosetests
}

package_python-networkx() {
  depends=('python-decorator')
  optdepends=('python-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python-scipy: Provides flexible drawing of graphs.'
              'python-pyparsing: Required for pydot, GML file reading.'
              'python-yaml: Required for YAML format reading and writing.')

  cd networkx-networkx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}

package_python2-networkx() {
  depends=('python2-decorator')
  optdepends=('python2-numpy: Provides sparse matrix representation of graphs and many numerical scientific tools.'
              'python2-scipy: Provides flexible drawing of graphs.'
              'python2-pydot: Provides graph drawing and graph layout algorithms.'
              'python2-pyparsing: Required for pydot, GML file reading.'
              'python2-yaml: Required for YAML format reading and writing.')

  cd networkx-networkx-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
  mv "$pkgdir"/usr/share/doc/networkx-$pkgver "$pkgdir"/usr/share/doc/$pkgname
}
