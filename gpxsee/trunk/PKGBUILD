# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=8.9
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL3)
depends=(qt6-imageformats)
makedepends=(qt6-tools)
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('abafa1543de4c1aab2dcc804afa4e7c93402db6084aa8f24011a2c4ccfcc075d')

build() {
  cd $_pkgname-$pkgver
  lrelease6 gpxsee.pro
  qmake6 gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  # Install appdata
  install -Dm644 pkg/appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
