# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Raffaele Zamorano
# Contributor: Jose Riha <jose1711 gmail com>
# Contributor: Nikolay Korotkiy <sikmir@gmail.com>

pkgname=gpxsee
_pkgname=GPXSee
pkgver=7.15
pkgrel=1
pkgdesc='GPS log file viewer and analyzer'
arch=(x86_64)
url='https://www.gpxsee.org/'
license=(GPL3)
depends=(desktop-file-utils qt5-imageformats)
makedepends=(qt5-tools)
source=(https://downloads.sourceforge.net/$pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('49e4f1b8d394af9dd4cb13d1c6e3937a7544506596e01ae2239acb47694d8958')

build() {
  cd $_pkgname-$pkgver
  lrelease-qt5 gpxsee.pro
  qmake gpxsee.pro PREFIX=/usr
  make
}

package() {
  cd $_pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  # Install appdata
  install -Dm644 pkg/appdata.xml "$pkgdir"/usr/share/metainfo/$pkgname.appdata.xml
}
