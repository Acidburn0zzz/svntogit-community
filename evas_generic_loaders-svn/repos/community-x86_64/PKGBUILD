# $Id$
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=evas_generic_loaders-svn
pkgver=75109
pkgrel=1
pkgdesc="Additional generic loaders for Evas"
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('librsvg' 'gstreamer0.10' 'poppler' 'libraw' 'libspectre' 'eina-svn')
makedepends=('subversion')
conflicts=('evas_generic_loaders')
provides=('evas_generic_loaders')
options=('!libtool' '!emptydirs')
md5sums=()
         
_svntrunk="http://svn.enlightenment.org/svn/e/trunk/evas_generic_loaders"
_svnmod="evas_generic_loaders"

build() {
   cd "$srcdir"

msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_svnmod-build"
  make DESTDIR="$pkgdir" install

# install license files
  install -Dm644 "$srcdir/$_svnmod-build/COPYING" \
        "$pkgdir/usr/share/licenses/$pkgname/COPYING"

  rm -r "$srcdir/$_svnmod-build"

}
