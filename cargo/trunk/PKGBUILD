# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Christopher Reimer <mail+aur@c-reimer.de>

pkgname=cargo
pkgver=0.11.0
pkgrel=2
pkgdesc='Rust package manager'
url='http://crates.io/'
arch=('x86_64' 'i686')
license=('APACHE' 'MIT' 'custom')
depends=('curl' 'rust')
makedepends=('git' 'python' 'cmake')
options=('!emptydirs')
source=("git+https://github.com/rust-lang/cargo.git#tag=$pkgver")
md5sums=('SKIP')

prepare() {
  for cmd in init update; do (cd "$pkgname"; git submodule "$cmd"); done
  sed 's^share/doc^share/licenses^g' -i "$pkgname/Makefile.in"
  sed 's^bash_completion.d/^bash-completion/completions/^g' -i "$pkgname/Makefile.in"
}

build() {
  cd "$pkgname"

  ./configure --prefix=/usr --enable-optimize
  make
}

package() {
  cd "$pkgname"

  make DESTDIR="$pkgdir" install

  # Remove files that contains references to $srcdir or $pkgdir,
  # or that conflicts with the rust package.
  find "$pkgdir" \
    -name install.log \
    -o -name manifest-cargo \
    -o -name uninstall.sh \
    -delete
}

# vim:set ts=2 sw=2 et:
