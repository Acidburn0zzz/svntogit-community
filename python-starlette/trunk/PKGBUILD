# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=starlette
pkgname=python-$_pkgname
pkgver=0.13.4
pkgrel=1
pkgdesc='The little ASGI framework that shines'
arch=('any')
url='https://github.com/encode/starlette'
license=('BSD')
depends=('python')
optdepends=('python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
            'python-yaml' 'python-requests' 'python-ujson')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner'
              'python-aiofiles' 'python-graphene' 'python-itsdangerous' 'python-jinja'
              'python-yaml' 'python-requests' 'python-ujson')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('ca986f60f1735b73844648c92aec6f4b3695923c806fcab9321912a38c8e53c6c9cd1373ee4912d7bbfe0052666507ad625ae1d16dbf80459534c066aaec828b')

prepare() {
  cd $_pkgname-$pkgver

  # python-graphene is broken (FS#66833)
  rm tests/test_graphql.py

  # needs python-multipart
  rm tests/test_formparsers.py

  # needs python-databases
  rm tests/test_database.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  # test_request_form_urlencoded needs python-multipart
  python setup.py pytest --addopts -k-test_request_form_urlencoded
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

