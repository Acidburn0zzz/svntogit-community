# $Id$
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jon Nordby <jononor@gmail.com>

pkgname=augeas
pkgver=1.8.0
pkgrel=1
pkgdesc="A configuration editing tool that parses config files and transforms them into a tree"
arch=('i686' 'x86_64')
url="http://augeas.net"
license=('LGPL')
depends=('libxml2' 'gcc-libs')
validpgpkeys=('AED6E2A185EEB379F17476D2E012D07AD0E3CC30')
source=(http://download.augeas.net/augeas-$pkgver.tar.gz{,.sig})
sha256sums=('515ce904138d99ff51d45ba7ed0d809bdee6c42d3bc538c8c820e010392d4cc5'
            'SKIP')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's|Requires:.*|Requires: libxml-2.0|' augeas.pc.in
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}
