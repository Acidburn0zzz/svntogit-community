# Maintainer: Jiachen YANG <farseerfc@archlinux.org>
# Contributor: xyzzy <628208@gmail.com>

pkgname=flameshot
pkgver=0.8.0
pkgrel=1
pkgdesc="Powerful yet simple to use screenshot software"
arch=('x86_64')
url="https://github.com/lupoDharkael/flameshot"
license=('GPL')
depends=('qt5-svg' 'hicolor-icon-theme')
makedepends=('make' 'qt5-tools' 'cmake')
source=("https://github.com/lupoDharkael/flameshot/archive/v${pkgver}.tar.gz")
sha256sums=('41d04c1ccb5c492768d6b66252b554ff1e5fec11fccb7bb0bdb8ae878dd01909')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build
  cd build
  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
  
  # provided by zsh-completions so exclude from packaging
  rm ${pkgdir}/usr/share/zsh/site-functions/_flameshot
}
