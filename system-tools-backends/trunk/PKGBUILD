# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugo@archlinux.org>

pkgname=system-tools-backends
pkgver=2.10.1
pkgrel=1
pkgdesc='Backends for Gnome System Tools.'
arch=('i686' 'x86_64')
url='http://system-tools-backends.freedesktop.org/'
license=('GPL')
depends=('perl>=5.12.1' 'perl-net-dbus' 'dbus>=1.4.0' 'polkit>=0.98' 'dbus-glib')
makedepends=('perlxml' 'pkgconfig' 'intltool')
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.10/${pkgname}-${pkgver}.tar.bz2)
install=system-tools-backends.install
sha256sums=('0c1cfdf5f0550c0ccacbbc4f00ed059ab9ea483d48138132db44b3a20575d7b4')

build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr \
    --with-dbus-sys=/etc/dbus-1/system.d \
    --localstatedir=/var \
    --mandir=/usr/share \
    --disable-static

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install

  #clean up man
  rm -rf $pkgdir/usr/share/system-tools-backends-2.0/modules/share/
}
