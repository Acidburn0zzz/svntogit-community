# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Anatol Pomozov
# Contributor: Tim Hütz <tim@huetz.biz>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Sebastien Binet <binet@cern.ch>

_pkg=mpi4py
pkgbase=python-${_pkg}
pkgname=(python-${_pkg} python2-${_pkg})
pkgver=3.0.2
pkgrel=3
pkgdesc="Python bindings for the Message Passing Interface (MPI) standard"
arch=(x86_64)
url="https://bitbucket.org/mpi4py/mpi4py"
license=(BSD)
makedepends=(python-setuptools python2-setuptools openmpi cython)
checkdepends=(inetutils)
source=("https://files.pythonhosted.org/packages/source/m/${_pkg}/${_pkg}-${pkgver}.tar.gz"
        'python-3.8.patch')
sha256sums=('f8d629d1e3e3b7b89cb99d0e3bc5505e76cc42089829807950d5c56606ed48e0'
            '44f7f5816a91f3cc0d61553b1316094873f22a9193d87b3cd397b7cb1c974e59')

prepare() {
    cp -a ${_pkg}-${pkgver}{,-py2}

    # https://bitbucket.org/mpi4py/mpi4py/commits/62a7b879051f
    patch -Np1 -d ${_pkg}-${pkgver} <python-3.8.patch

    # Force recreation of Cython-generated files for Python 3.8 compatibility
    rm $(grep -rl '/\* Generated by Cython' ${_pkg}-${pkgver})
}

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py build
}

check() {
    # This is required starting with OpenMPI 3.0 when trying to run more
    # processes than the number of available cores
    export OMPI_MCA_rmaps_base_oversubscribe=yes

    cd ${_pkg}-${pkgver}
    python setup.py test

    cd ../${_pkg}-${pkgver}-py2
    python2 setup.py test
}

package_python-mpi4py() {
    depends=(python openmpi)

    cd ${_pkg}-${pkgver}
    python setup.py install --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_python2-mpi4py() {
    depends=(python2 openmpi)

    cd ${_pkg}-${pkgver}-py2
    python2 setup.py install --root="$pkgdir" --skip-build --optimize=1
    install -Dm644 LICENSE.rst -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
