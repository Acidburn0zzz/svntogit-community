# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.1.4
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'python-cerberus' 'python-click' 'python-click-completion'
'python-click-help-colors' 'python-colorama' 'python-cookiecutter'
'python-jinja' 'python-packaging' 'python-paramiko' 'python-pexpect'
'python-pluggy' 'python-rich' 'python-selinux' 'python-sh' 'python-tabulate'
'python-tree-format' 'python-yaml' 'yamllint')
makedepends=('python-setuptools-scm' 'python-simplejson')
# TODO: python-pytest-plus
checkdepends=('ansible-lint' 'python-ansi2html' 'python-mock' 'python-pytest'
'python-pytest-cov' 'python-pytest-helpers-namespace' 'python-pytest-html'
'python-pytest-mock' 'python-pytest-verbose-parametrize'
'python-pytest-verbose-parametrize' 'python-pytest-xdist' 'python-testinfra')
optdepends=('molecule-docker: for the docker driver'
            'molecule-podman: for the podman driver'
            'molecule-vagrant: for the vagrant driver'
            'python-pywinrm: for Windows support'
            'python-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b01d72bf0441df98484f813da5ef982efc15842c66782fc8d2709db7997820965c394213e0a1089c97055d4c9242cea8c46f3ea1d3e3acfc0b538c6e85027248')
b2sums=('9f89c6a3ce8d58dd78bb33b21e48a0c46d8512a5b952bd8ec7a2cb01bd638b108dc4c70438735d613c04462c8f94fd5afd4668da1393cec4fbdf5c284c651b39')

prepare() {
  cd "$pkgname-$pkgver"
  # python-pre-commit is absolutely not a runtime dependency
  # remove additional pytest options
  # unpin python-sh
  sed -e '/pre-commit/d' \
      -e '/addopts/d' \
      -e 's/, < 1.14//g' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  # installing into test location, so pytest can pick up the executable
  mkdir -vp test_dir
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="test_dir"
  local python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  export PYTHONPATH="${PWD}/test_dir/usr/lib/python${python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"
  # do not run functional tests
  pytest -v \
         --ignore='lib/molecule/test/functional/conftest.py' \
         --ignore='lib/molecule/test/functional/test_command.py'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
