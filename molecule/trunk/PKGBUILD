# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.6.0
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=(any)
url="https://github.com/ansible-community/molecule"
license=(MIT)
depends=(python-ansible-compat python-cerberus python-click
python-click-help-colors python-cookiecutter python-enrich python-jinja
python-packaging python-paramiko python-pluggy python-rich python-yaml)
makedepends=(python-build python-setuptools-scm python-setuptools-scm-git-archive python-install)
checkdepends=(ansible python-ansi2html python-filelock python-pexpect
python-pytest python-pytest-html python-pytest-mock python-pytest-testinfra
python-pytest-xdist yamllint)
optdepends=(
  'ansible: for local ansible support'
  'molecule-docker: for the docker driver'
  'molecule-podman: for the podman driver'
  'molecule-vagrant: for the vagrant driver'
  'python-pywinrm: for Windows support'
  'python-pytest-testinfra: for the testinfra verifier'
)
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6e60387f43c6a8a4e50951d6b71c5e161be0bc42d79f03483ea306598cf8cf6d4872034316dea1021a40fea505e4b0c6e656867ad62bf0ce8a0c802e18eb3085')
b2sums=('a46f238c301b2b707e40c1e4e36f203d8da187a1184ac105245fe6a253a484efbffed400a685fa21b74ae147ac884d12f71b108fda8fe6a061bb91230a40799e')

prepare() {
  cd "$pkgname-$pkgver"
  # allegedly cerberus is not compatible with py39, but we don't mind:
  # https://github.com/pyeve/cerberus/issues/568
  sed -e '/cerberus/ s/,.*//' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python -m build --wheel --skip-dependency-check --no-isolation
}

check() {
  cd "$pkgname-$pkgver"
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir="${PWD}/test_dir"

  # install into test location
  mkdir -vp "${_test_dir}"
  python -m install --optimize=1 --destdir="$_test_dir" dist/*.whl
  chmod +x "$_test_dir"/usr/bin/*

  export PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}"
  export PATH="${_test_dir}/usr/bin:${PATH}"
  pytest -v "${_test_dir}/${_site_packages}/molecule/test/unit/" -c /dev/null
}

package() {
  cd "$pkgname-$pkgver"
  python -m install --optimize=1 --destdir="$pkgdir" dist/*.whl
  # https://github.com/FFY00/python-install/pull/6
  chmod +x "$pkgdir"/usr/bin/*
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
