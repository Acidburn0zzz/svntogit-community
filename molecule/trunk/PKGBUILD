# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=molecule
pkgver=3.3.1
pkgrel=1
pkgdesc="Aids in the development and testing of Ansible roles"
arch=('any')
url="https://github.com/ansible-community/molecule"
license=('MIT')
depends=('ansible' 'ansible-lint' 'python-cerberus' 'python-click'
'python-click-completion' 'python-click-help-colors' 'python-cookiecutter'
'python-enrich' 'python-jinja' 'python-packaging' 'python-paramiko'
'python-pluggy' 'python-rich' 'python-selinux' 'python-setuptools'
'python-subprocess-tee' 'python-yaml')
makedepends=('python-setuptools-scm' 'python-simplejson' 'python-toml')
checkdepends=('python-ansi2html' 'python-mock' 'python-pexpect'
'python-pytest' 'python-pytest-helpers-namespace' 'python-pytest-html'
'python-pytest-mock' 'python-pytest-testinfra'
'python-pytest-verbose-parametrize' 'python-pytest-xdist' 'yamllint')
optdepends=('molecule-docker: for the docker driver'
            'molecule-podman: for the podman driver'
            'molecule-vagrant: for the vagrant driver'
            'python-pywinrm: for Windows support'
            'python-pytest-testinfra: for the testinfra verifier')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('893f2594a3936ea1769aa9f3f6087790bd5a190a1bd272a8e6a73f1ebdd4fba81f43b8ef3fdd578bbf6be2d12a16abaf55cb9be21cd322b3e87e6150e878e0fa')
b2sums=('70702b2bce1711a1be2877a47d8dfbb4b26f6ae5ec1e1a0b68ff17e0b8d14b26c8ac9304a32ad1ee2df1eb001bcb7f70f535f49fc8a68af668938b050852d729')

prepare() {
  cd "$pkgname-$pkgver"
  # remove additional pytest options
  # remove fitler to raise warnings as errors: https://github.com/ansible-community/molecule/issues/3004
  sed -e '/addopts/d' \
      -e '/testpaths/d' \
      -e '/error/d' \
      -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local _python_version="$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')"
  local _test_dir='test_dir'

  # install into test location
  mkdir -vp "${_test_dir}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${_test_dir}"

  export PYTHONPATH="${PWD}/${_test_dir}/usr/lib/python${_python_version}/site-packages/:${PYTHONPATH}"
  export PATH="${PWD}/test_dir/usr/bin:${PATH}"

  pytest -v "${_test_dir}/usr/lib/python${_python_version}/site-packages/molecule/test/unit/"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
