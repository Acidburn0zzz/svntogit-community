# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Emanuele Rampichini <emanuele.rampichini@gmail.com>

pkgname=smb4k
pkgver=2.0.1
pkgrel=1
pkgdesc="A KDE program that browses samba shares"
arch=(i686 x86_64)
url="http://smb4k.sourceforge.net/"
license=(GPL)
depends=(kparts hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools python plasma-framework)
source=("http://downloads.sourceforge.net/project/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('0418ca9ae9a62cde6cfdc4ffad7525868048827366166578936e988ce7a6009e')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../$pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=RELEASE \
        -DKDE_INSTALL_LIBDIR=lib \
        -DBUILD_TESTING=OFF
  make
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
