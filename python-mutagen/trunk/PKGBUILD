# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

_name=mutagen
pkgname=python-mutagen
pkgver=1.44.0
pkgrel=1
arch=('any')
pkgdesc="An audio metadata tag reader and writer (python library)"
url="https://github.com/quodlibet/mutagen"
license=('GPL2')
depends=('python-setuptools')
checkdepends=('python-pytest' 'python-hypothesis' 'python-pyflakes'
'python-pycodestyle')
source=("https://github.com/quodlibet/${_name}/releases/download/release-$pkgver/${_name}-$pkgver.tar.gz"{,.sig})
sha512sums=('88c8c181b1333989451533951727b7c43e4a012defd3fd1f7ddd970d083f938783d61f98970e74c677ee9725dac92c47ee37af4bd7a474e85c7a56793c6063a8'
            'SKIP')
validpgpkeys=('0EBF782C5D53F7E5FB02A66746BD761F7A49B0EC') # Christoph Reiter <reiter.christoph@gmail.com>

prepare() {
  mv -v "${_name}-$pkgver" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  pytest -v
}

package_python-mutagen() {
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --prefix=/usr \
                          --root="${pkgdir}"
  install -vDm 644 {NEWS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
