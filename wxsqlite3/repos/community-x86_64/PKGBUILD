# $Id$
# Maintainer : speps <speps at aur dot archlinux dot org>
# Maintainer : Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: napa3um <napa3um@gmail.com>
# Contributor: Scott Furry <scott.wl.furry@gmail.com>

pkgname=wxsqlite3
pkgver=3.5.1
pkgrel=1
pkgdesc="wxWidgets wrapper for SQLite3"
arch=('i686' 'x86_64')
url="http://utelle.github.io/wxsqlite3"
license=('custom:wxWindows')
depends=('wxgtk')
source=($pkgname-$pkgver.tar.gz::https://github.com/utelle/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('132ec5881001e48bc36e371e6e5061ba17b77e68478837cdaea8bf27622f60ca')

build() {
  cd $pkgname-$pkgver

  autoreconf
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="${pkgdir}" install

# license
  install -Dm644 LICENCE.txt "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
# FS#49035 fix
  install -Dm644 $pkgname.pc "${pkgdir}"/usr/lib/pkgconfig/$pkgname.pc
}
