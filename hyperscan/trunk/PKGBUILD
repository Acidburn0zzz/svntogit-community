# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Alireza Ayinmehr <alireza.darksun@gmail.com>
# Contributor: Maxwell Pray a.k.a. Synthead <synthead@gmail.com>
# Contributor: JSkier <jskier at gmail dot com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=hyperscan
pkgver=5.2.0
pkgrel=1
pkgdesc='High-performance regular expression matching library'
arch=(x86_64)
url=https://www.hyperscan.io
license=(BSD)
makedepends=(
  boost
  cmake
  git
  python
  ragel
)
source=(git+https://github.com/intel/hyperscan.git#tag=v${pkgver})
sha256sums=(SKIP)

prepare() {
  if [[ -d build ]]; then
    rm -rf build
  fi
  mkdir build
}

build() {
  cd build

  cmake ../hyperscan \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_STATIC_AND_SHARED=ON
  make
}

package() {
  make DESTDIR="$pkgdir" -C build install
  install -Dm 644 hyperscan/COPYING -t "${pkgdir}"/usr/share/licenses/hyperscan/
}

# vim: ts=2 sw=2 et:
