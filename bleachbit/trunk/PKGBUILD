# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: yugrotavele <yugrotavele at archlinux dot us>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Contributor: Arnaud Durand <biginozNOSPAM_at_free.dot.fr>
# Contributor: Nuno Araujo <nuno.araujo at russo79.com>

pkgname=bleachbit
pkgver=2.2
pkgrel=2
pkgdesc='Deletes unneeded files to free disk space and maintain privacy'
arch=(any)
url='https://www.bleachbit.org/'
license=(GPL3)
depends=(pygtk)
source=(https://download.bleachbit.org/$pkgname-$pkgver.tar.bz2)
sha256sums=('0318cd1bc83655971c9ffd6bf27f4866bbe57381e92cfbcf8a2a6833075b49fa')

prepare() {
  cd $pkgname-$pkgver
  sed -e 's|python|python2|g' -i Makefile -i bleachbit/CLI.py
}

package() {
  cd $pkgname-$pkgver

  make prefix=/usr DESTDIR="$pkgdir" install
}
