# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Wesley Moore <wes@wezm.net>
pkgname=sd
pkgver=0.6.5
pkgrel=1
pkgdesc='Intuitive find & replace'
arch=('x86_64')
url="https://github.com/chmln/sd"
license=('MIT')
makedepends=('rust' 'cargo' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/chmln/sd/archive/${pkgver}.tar.gz")
sha256sums=('ed38e5103080373b00443f72683ac2785b18e354ab6ef4797e27af028be9baf2')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked
}

package() {
  install -Dm755 "$pkgname-$pkgver/target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "$pkgname-$pkgver/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
