# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Marcelo Avalos Tejeda <marcelo.avalos@gmail.com>

pkgname=extrema
pkgver=4.4.4
pkgrel=1
pkgdesc='Extrema is a powerful visualization and data analysis tool.'
arch=('i686' 'x86_64')
url='http://exsitewebware.com/extrema/index.html'
license=('GPL')
depends=('wxgtk' 'zlib' 'libxml2' 'libpng')
source=("http://downloads.sourceforge.net/project/extrema/extrema/extrema-linux%20v${pkgver}/extrema-${pkgver}.tar.gz"
        'extrema.desktop')
md5sums=('5072e2620acad5bc7ece0f429e76e734'
         '0f4d310d230e28436dff20e77aed71bb')
   
build() {
  cd ${pkgname}-${pkgver}

  ./configure \
    --prefix=/usr

  make
}

package() {
  cd ${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install 

  tar xf extrema_icons.tar.bz2
  install -D -m644 32x32/apps/extrema.png \
    ${pkgdir}/usr/share/pixmaps/extrema.png

  install -D -m644 ${srcdir}/extrema.desktop \
    ${pkgdir}/usr/share/applications/extrema.desktop
}
