# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sortedcontainers
pkgver=2.2.0
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grantjenks/python-sortedcontainers/archive/v$pkgver.tar.gz")
sha512sums=('0eb56aa8af964c3701e9afbafa2f928b2bbe1905280a136a474789c0eae645c0605e0244c454f4e54757a5d7d73fb74f78dd0c7d95526b7d2faefeaa88910e13')

prepare() {
  # Don't use tox
  sed -i '/tests_require/d' python-sortedcontainers-$pkgver/setup.py
}

build() {
  cd python-sortedcontainers-$pkgver
  python setup.py build
}

check() {
  cd python-sortedcontainers-$pkgver
  python setup.py pytest
}

package() {
  cd python-sortedcontainers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
