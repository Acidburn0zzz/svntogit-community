# $Id: PKGBUILD 141139 2015-09-20 03:14:59Z fyan $
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-sortedcontainers
pkgname=('python-sortedcontainers' 'python2-sortedcontainers')
pkgver=1.5.10
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/grantjenks/sorted_containers/archive/v$pkgver.tar.gz")
sha512sums=('22ec92d9d7dc44159f47be9b028cc468637cae68d4702fff87e8531a155d56bb031b32c77efcc6f839ead19d27471cb80660fe990b3d424ed1fe58178ee803b8')

prepare() {
  cp -a sorted_containers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sorted_containers-$pkgver
  python setup.py build

  cd "$srcdir"/sorted_containers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/sorted_containers-$pkgver
  nosetests3

  cd "$srcdir"/sorted_containers-$pkgver-py2
  nosetests2
}

package_python-sortedcontainers() {
  depends=('python')

  cd sorted_containers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-sortedcontainers() {
  depends=('python2')

  cd sorted_containers-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
