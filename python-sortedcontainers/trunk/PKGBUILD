# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-sortedcontainers
pkgver=2.2.1
pkgrel=1
pkgdesc="Python Sorted Container Types: SortedList, SortedDict, and SortedSet"
arch=('any')
license=('Apache')
url="http://www.grantjenks.com/docs/sortedcontainers/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grantjenks/python-sortedcontainers/archive/v$pkgver.tar.gz")
sha512sums=('d4a45c4db9ec86c83c71506525a452fd5716818326635f69fb6e830d86803c5d39821678cb67e142ddd1ae9d093f9bb583c29b6bdc59a18a5ec0213eea4a1e29')

prepare() {
  # Don't use tox
  sed -i '/tests_require/d' python-sortedcontainers-$pkgver/setup.py
}

build() {
  cd python-sortedcontainers-$pkgver
  python setup.py build
}

check() {
  cd python-sortedcontainers-$pkgver
  python setup.py pytest
}

package() {
  cd python-sortedcontainers-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
