# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=frobby
pkgver=0.9.0
pkgrel=1
pkgdesc="A software system and project for computations with monomial ideals"
arch=(i686 x86_64)
url="http://www.broune.com/frobby/"
license=(GPL)
depends=(gmp)
makedepends=()
source=(http://www.broune.com/frobby/frobby_v$pkgver.tar.gz frobby-gcc4.7.patch frobby-gmp.patch)
md5sums=('2204f340dad8fb7be156f9f7c403c4a0'
         '07591e56be81809cc235177613078dcf'
         'c5e95fbbb7942aa00258f7070e98723c')

prepare() {
  cd ${pkgname}_v$pkgver
# Fix build with GCC 4.7 and recent GMP (Gentoo)
  patch -p1 -i ../frobby-gcc4.7.patch
  patch -p1 -i ../frobby-gmp.patch
}

build() {
  cd ${pkgname}_v$pkgver

  make
  MODE=shared make library
}

package() {
  cd ${pkgname}_v$pkgver

  install -d "$pkgdir"/usr/{bin,lib,include}
  install -m755 bin/frobby "$pkgdir"/usr/bin
  install -m644 bin/libfrobby.so "$pkgdir"/usr/lib
  install -m644 src/frobby.h "$pkgdir"/usr/include
}
