# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=1.2.4
pkgrel=2
arch=(x86_64)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools intltool)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kdebug-375765.patch::"https://cgit.kde.org/kio-gdrive.git/patch/?id=1b710a46")
sha256sums=('ba903a5bf3b538d42a09bf4d6192e9f06f7e1f80bf5cb4b5c7163b251142c0fc'
            'SKIP'
            '9b2307676a2037467aa601b2d51b56184948d151a0c3e9c1c1542b3546344c29')
validpgpkeys=('F07D85CAA18ACF46A346FD017C7FC6EA8633B4EA') # Elvis Angelaccio <elvis.angelaccio@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kdebug-375765.patch # Fix error when downloading large files
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
   -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
