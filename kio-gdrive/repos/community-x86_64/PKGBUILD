# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kio-gdrive
pkgver=1.2.6
pkgrel=2
arch=(x86_64)
pkgdesc="KIO Slave to access Google Drive"
url="https://community.kde.org/KIO_GDrive"
license=(GPL)
depends=(libkgapi kaccounts-providers)
makedepends=(extra-cmake-modules kdesignerplugin kdoctools intltool)
source=(https://download.kde.org/stable/$pkgname/$pkgver/src/$pkgname-$pkgver.tar.xz{,.sig}
        kio-gdrive-libkgapi-19.08.patch::"https://cgit.kde.org/kio-gdrive.git/patch/?id=b11f84ba")
sha256sums=('7a9169e1d464641eb0ae4013ca1732b46bdfd50a13b3e87810c19ba794527f7b'
            'SKIP'
            '2c3cc73e2382390cd39fd7ef122592b4cf7f206e33406fb28126c1bdf29e53d9')
validpgpkeys=('F07D85CAA18ACF46A346FD017C7FC6EA8633B4EA') # Elvis Angelaccio <elvis.angelaccio@kde.org>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../kio-gdrive-libkgapi-19.08.patch # Fix build with libkgapi 19.08
}

build() {
  cd build
  cmake ../$pkgname-$pkgver
  make
}

package(){
  cd build
  make DESTDIR="$pkgdir" install
}
