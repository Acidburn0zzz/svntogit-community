# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Roberto Alsina <ralsina@kde.org>

pkgname=nsd
pkgver=3.2.7
pkgrel=3
pkgdesc='Authoritative only, high performance and simple DNS server'
arch=('i686' 'x86_64')
url='http://www.nlnetlabs.nl/nsd/'
license=('BSD')
depends=('openssl')
makedepends=('flex')
source=("http://www.nlnetlabs.nl/downloads/nsd/nsd-$pkgver.tar.gz")
sha1sums=('1fc4af1831f64b76e1dc5c82b353770f9186de15')

install=nsd.install

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/ --exec-prefix=/usr --datarootdir=/usr/share
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -d "$pkgdir"/{etc/rc.d,usr/share/{doc,licenses}/"$pkgname"}
	rm doc/differences.pdf
	install -m644 doc/* "$pkgdir"/usr/share/doc/"$pkgname"
	ln -s ../../doc/"$pkgname"/LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
	ln -s ../../usr/sbin/nsdc "$pkgdir"/etc/rc.d/nsd
}
