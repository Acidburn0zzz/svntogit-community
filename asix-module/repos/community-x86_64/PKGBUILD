# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=asix-module
pkgver=4.2.0
pkgrel=1
pkgdesc="A kernel module for the ASIX USB 2.0 low power AX88772B/AX88772A/AX88760/AX88772/AX88178 ethernet controllers"
url="http://www.asix.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' 'linux>=3.1' 'linux<3.2')
makedepends=('linux-headers')
source=(http://www.asix.com.tw/FrootAttach/driver/AX88772B_772A_760_772_178_LINUX_Driver_v${pkgver}_Source.zip)
install=$pkgname.install

_extramodules=extramodules-3.1-ARCH
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/AX88772B_772A_760_772_178_LINUX_Driver_v4.2.0_Source"
	make SYSSRC=/usr/src/linux-$_kernver
	gzip -f9 asix.ko
}

package() {
	cd "$srcdir/AX88772B_772A_760_772_178_LINUX_Driver_v4.2.0_Source"
	install -D -m644 asix.ko.gz "$pkgdir/lib/modules/$_extramodules/asix.ko.gz"
}

md5sums=('b3261d17e21b026d2c50dbc54227eb9c')
sha256sums=('cac6b217672399439b571c77232f4beb22a5dffb54afeaf714c2b7559d41493d')
