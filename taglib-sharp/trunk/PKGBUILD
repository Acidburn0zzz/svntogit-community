# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Daniel Isenmann <daniel@archlinux.org>
# Contributor: Carlos Ruiz <cailovirtual@gmail.com>

pkgname=taglib-sharp
pkgver=2.1.0.0
pkgrel=3
pkgdesc="Library for reading and writing metadata in media files for Mono"
arch=('any')
url="https://github.com/mono/taglib-sharp"
license=('LGPL2.1')
depends=('mono')
source=(http://download.banshee-project.org/taglib-sharp/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('713eb7d3011092a4842d0955776271e5ee7b122d7e3c7c24cffb7f96132e8179')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's/gmcs/mcs/' configure{.ac,}
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --disable-docs
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
