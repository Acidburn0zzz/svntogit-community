# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-openstackclient
pkgver=3.19.0
pkgrel=1
pkgdesc="OpenStack Command-line Client"
arch=('any')
url="https://docs.openstack.org/python-openstackclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-babel' 'python-cliff' 'python-keystoneclient'
         'python-keystoneauth1' 'python-osc-lib' 'python-oslo-utils' 'python-oslo-i18n'
         'python-openstacksdk' 'python-glanceclient' 'python-novaclient' 'python-cinderclient')
checkdepends=('python-oslotest' 'python-requests-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-openstackclient/archive/$pkgver.tar.gz")
sha512sums=('e3e0f58627e9d292ddefe4ec9091ff6daf4b0b89ef78c629257c9dcbc6386309d77894aabcc055b3dc60cf5459fa14cb407f4c2f81c839ab3510f3e899c30ab0')

export PBR_VERSION=$pkgver

build() {
  cd python-openstackclient-$pkgver
  python setup.py build
}

check() {
  cd python-openstackclient-$pkgver
  stestr run || warning "Tests failed: https://storyboard.openstack.org/#!/story/2003143"
}

package() {
  cd python-openstackclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
