# Maintainer: Konstantin Gizdov <arch at kge dot pw>

_pyname=xxhash
pkgname=python-${_pyname}
pkgver=1.4.3
pkgrel=1
pkgdesc='Python binding for xxHash'
arch=('x86_64')
url="https://pypi.python.org/pypi/${_pyname}/"
depends=('python' 'xxhash')
makedepends=('python-setuptools')
license=('BSD')
source=("https://github.com/ifduyue/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('e044e3db0f532b80fcb822987255e960de7b232593bdd1dead508d3c448abb12bfeb3d6798364ea7f716151a73e2c3928267d4ef96860be9d34775181f3da339')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # link to xxHash
    export XXHASH_LINK_SO=1
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py test
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    python setup.py install --root="${pkgdir}" --optimize=1
    install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
