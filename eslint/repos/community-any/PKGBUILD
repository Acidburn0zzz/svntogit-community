# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=eslint
pkgver=5.15.3
pkgrel=1
pkgdesc='An AST-based pattern checker for JavaScript'
arch=('any')
url='http://eslint.org'
license=('MIT')
depends=('nodejs' 'acorn')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=("$pkgname-$pkgver.tgz")
sha512sums=('bcc1a2d0f8c21d2a2467113434ba76567786c39b22a3b4928833602149f6b42d1791625134e2281c88370a588b55f5e726c887c46018ae4c343e2780bbcf8a61')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  install -dm755 "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -s ../../../lib/node_modules/eslint/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn; do
    rm -r $dep;
  done
}
