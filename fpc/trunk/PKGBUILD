# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Valeriy Lyasotskiy <onestep@ukr.net>
# Contributor: Jan Willemson <janwil@hot.ee>
# Contributor: Hugo Ideler <hugoideler@dse.nl>
# Original PKGBUILD: Andre Naumann <anaumann@SPARCed.org>
# See http://bbs.archlinux.org/viewtopic.php?t=9318&highlight=fpc

pkgname=fpc
pkgver=2.4.4
pkgrel=2
pkgdesc="The Free Pascal Compiler is a Turbo Pascal 7.0 and Delphi compatible 32bit Pascal Compiler. It comes with fully TP 7.0 compatible run-time library."
arch=('i686' 'x86_64')
url="http://www.freepascal.org/"
license=('GPL' 'LGPL' 'custom')
backup=("etc/fpc.cfg")
depends=(ncurses)
makedepends=(fpc)
options=(zipman)
source=(ftp://ftp.freepascal.org/pub/fpc/dist/$pkgver/source/fpcbuild-$pkgver.tar.gz
	Makefile-mk382.diff)
md5sums=('d069dfd3412bd0d26dcd1b81ac998305'
         'a2242d261b055d08fa4d70e88ca1d1a7')

build() {
  cd ${srcdir}/fpcbuild-$pkgver
  pushd fpcsrc/compiler
  patch -uN Makefile.fpc $srcdir/Makefile-mk382.diff
  fpcmake -Tall
  popd
  make NOGDB=1 build
  make NOGDB=1 build OS_TARGET=win32 CPU_TARGET=i386
}

package() {
  cd ${srcdir}/fpcbuild-$pkgver

  export HOME=$srcdir

  make -j1 NOGDB=1 PREFIX=${pkgdir}/usr install

  make NOGDB=1 PREFIX=${pkgdir}/usr crossinstall OS_TARGET=win32 CPU_TARGET=i386
  ln -s /usr/lib/fpc/$pkgver/ppcross386 $pkgdir/usr/bin/ppcross386

  # install package license
  install -m 755 -d ${pkgdir}/usr/share/licenses/${pkgname}
  install -m 644 fpcsrc/rtl/COPYING.FPC ${pkgdir}/usr/share/licenses/${pkgname}/

  # create symlink for compiler
  [ "$CARCH" = "i686" ] && ln -s /usr/lib/fpc/${pkgver}/ppc386 ${pkgdir}/usr/bin/
  [ "$CARCH" = "x86_64" ] && ln -s /usr/lib/fpc/${pkgver}/ppcx64 ${pkgdir}/usr/bin/

  # install sample config file
  mkdir -p ${pkgdir}/etc
  ${pkgdir}/usr/lib/fpc/${pkgver}/samplecfg /usr/lib/fpc/${pkgver} ${pkgdir}/etc

  mv $pkgdir/usr/man $pkgdir/usr/share/
}
