# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=python-zeroconf
pkgver=0.28.6
pkgrel=3
pkgdesc="A pure python implementation of multicast DNS service discovery"
arch=('any')
url="https://github.com/jstasiak/python-zeroconf"
license=('LGPL')
depends=('python' 'python-netifaces' 'python-six' 'python-ifaddr')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=($pkgname-$pkgver.tar.gz::https://github.com/jstasiak/${pkgname}/archive/${pkgver}.tar.gz)
sha256sums=('9971344cb69132a68fac928522ca423c97db0f02310ee15b8551a6a63f8898a2')

build() {
  cd "${pkgbase}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${pkgbase}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

check() {
  cd "${pkgbase}-${pkgver}"
  nosetests --exclude test_integration_with_listener_ipv6 zeroconf/test.py
}

# vim:set ts=2 sw=2 et:
