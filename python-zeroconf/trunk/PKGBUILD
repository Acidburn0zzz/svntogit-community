# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>

pkgname=python-zeroconf
pkgver=0.21.3
pkgrel=1
pkgdesc="A pure python implementation of multicast DNS service discovery"
arch=('any')
url="https://github.com/jstasiak/python-zeroconf"
license=('LGPL')
depends=('python' 'python-netifaces' 'python-six' 'python-ifaddr')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/jstasiak/${pkgname}/archive/${pkgver}.tar.gz")
sha256sums=('8c3aaed98f950b56c6e1aa440b56e0d48af49565cb4d7830a82cb3b01088aad8')

build() {
  cd "${pkgbase}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${pkgbase}-${pkgver}"

  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
}

check() {
  cd "${pkgbase}-${pkgver}"
  py.test .
}

# vim:set ts=2 sw=2 et:
