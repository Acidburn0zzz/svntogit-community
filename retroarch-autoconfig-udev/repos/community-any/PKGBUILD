# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=retroarch-autoconfig-udev
pkgver=1147
pkgrel=1
pkgdesc='udev joypad autoconfig for RetroArch'
arch=('any')
url='http://www.libretro.com/'
license=('GPL')
groups=('libretro')
depends=('retroarch')
makedepends=('git')
_commit='67b67105c8b3f2c444bdb27a9586379091b3bfa3'
source=("git+https://github.com/libretro/retroarch-joypad-autoconfig.git#commit=${_commit}")
sha256sums=('SKIP')

pkgver() {
  cd retroarch-joypad-autoconfig

  git rev-list --count HEAD
}

package() {
  cd retroarch-joypad-autoconfig

  install -dm 755 "${pkgdir}"/usr/share/libretro/autoconfig
  cp -dr --no-preserve='ownership' udev "${pkgdir}"/usr/share/libretro/autoconfig/
}

# vim: ts=2 sw=2 et:
