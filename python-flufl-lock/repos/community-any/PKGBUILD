# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=5.0.5
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://gitlab.com/warsaw/flufl.lock"
license=('Apache')
depends=('python-atpublic' 'python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
replaces=('python-flufl.lock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('11d0dde6addd34fd3812b346d851692addee49e7a65620acae87da791051f89638036a652645cb7fe34edd786afe3112bf5efb3fe4aa36760e2054622b0c1c8e')
b2sums=('978581adb55ce468ed2dd0f84978234a54293ee7cda4517dcdda2a2fc3507c965c419424f6a6bdc8cccbc15b14ebb739ff02e0c00d427f4f92afc31f913d0201')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't use coverage with pytest
  sed -s '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
