# Maintainer: David Runge <dvzrv@archlinux.org>

_name=flufl.lock
pkgname=python-flufl-lock
pkgver=4.0
pkgrel=1
pkgdesc="NFS-safe file locking with timeouts for POSIX systems for Python"
arch=('any')
url="https://flufllock.readthedocs.io/en/latest/"
license=('Apache')
depends=('python-atpublic' 'python-psutil')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-sybil')
replaces=('python-flufl.lock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('78f984ca691ca720a78e01928352b89ba7a4d4c7a4830150887d450c055658b9ce1bf010e45a26aa9b729ec06a6f22a95e7d348996412e14ea1261f1226ea71c')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we don't use coverage with pytest
  sed -s '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
