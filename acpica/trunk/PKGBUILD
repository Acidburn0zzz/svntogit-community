# Maintainer : Bruno Pagani <archange@archlinux.org>
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=acpica
pkgver=20180531
pkgrel=2
pkgdesc="ACPI tools, including Intel ACPI Source Language compiler"
arch=('x86_64')
url="https://acpica.org"
license=('GPL2')
depends=('glibc')
makedepends=('flex' 'bison')
provides=('iasl')
conflicts=('iasl')
replaces=('iasl')
options=('!makeflags')
source=("https://acpica.org/sites/acpica/files/acpica-unix-${pkgver}.tar.gz"
        'fix-gcc8-compilation.patch')
sha256sums=('8f6cdcaa4039c2b3db141117ec8223f0e1297684b8ab47839e211bddad027665'
            '8f1dac990ca7a05855ac34d90ac76e77434481f7c6e4679de1337e3621a88a53')

prepare() {
  cd acpica-unix-${pkgver}
  patch -p1 -i ../fix-gcc8-compilation.patch
}

build() {
  cd acpica-unix-${pkgver}
  make
}

package() {
  cd acpica-unix-${pkgver}
  make DESTDIR=${pkgdir} install
}
