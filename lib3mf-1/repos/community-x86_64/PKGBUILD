# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=lib3mf-1
pkgver=1.8.1
pkgrel=3
pkgdesc='Implementation of the 3D Manufacturing Format file standard (legacy version)'
arch=('x86_64')
url='https://3mf.io/'
license=('BSD')
depends=('libzip' 'zlib') 
makedepends=('cmake')
checkdepends=('gtest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/3MFConsortium/lib3mf/archive/v$pkgver.tar.gz"
        'lib3mf-1_use-system-gtest.patch::https://github.com/3MFConsortium/lib3mf/commit/cf679cc3736d4b22ce5711b5e81d28b7b6f4f4b0.patch'
        'lib3mf-1_link-libzip-and-zlib.patch::https://github.com/3MFConsortium/lib3mf/commit/07dec180622d6a3b94b915ae3716a926b2cd473b.patch')
sha512sums=('f7fd75bcb6472de1595a018e5add516d0d78ab0aee92462b686b77f8d2bef05270e7b737cb7e1d31fc5850815056e753874c2f9ec456a455e8461c4010fe914a'
            '84da1e659ed52582aca3c6f9ce2b234aad000e96bb657ff2aa7d118a613d5bca9b3f05f117f5db0a91440a9d51494730a637ed844ca4adb4887f357c15394663'
            '198fb50372222499e537ab350e5f5bad5563870bd8ee4eb5d3f34fb0543c0b119dc13355b6363e172c258c3b943450dd90195a62f398b60929f6eaf50e140945')

prepare() {
	mkdir build
	cd "${pkgname%-1}-$pkgver"
	patch --strip=1 --input="$srcdir/lib3mf-1_use-system-gtest.patch"
	patch --strip=1 --input="$srcdir/lib3mf-1_link-libzip-and-zlib.patch"
}

build() {
	cd build
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_INCLUDEDIR="include/$pkgname" \
	      -DUSE_INCLUDED_LIBZIP=OFF -DUSE_INCLUDED_ZLIB=OFF -DUSE_INCLUDED_GTEST=OFF \
	      $( ((CHECKFUNC)) || echo -DLIB3MF_TESTS=OFF) \
	      "$srcdir/${pkgname%-1}-$pkgver"
	make
}

check() {
	cd build
	ctest
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
	install -Dm644 "$srcdir/${pkgname%-1}-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname"
}
