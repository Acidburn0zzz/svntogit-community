# $Id$
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>
# Maintainer: Brian Bidulock <bidulock@openss7.org>
# Contributor: Ryan Corder <ryanc@greengrey.org>
# Contributor: Samuel Tardieu <sam@rfc1149.net>

pkgname=ckermit
pkgver=9.0.302
pkgrel=5
pkgdesc="Portable scriptable network and serial communication software"
arch=('i686' 'x86_64')
license=('custom')
depends=('ncurses')
url="http://www.columbia.edu/kermit/ck90.html"
#source=('ftp://kermit.columbia.edu/kermit/archives/cku302.tar.gz' 'lockdir.patch')
source=('ftp://ftp.kermitproject.org/kermit/archives/cku302.tar.gz'
	'lockdir.patch')
md5sums=('eac4dbf18b45775e4cdee5a7c74762b0'
         '5968e55dd472b2efb0e31cb3a6fb1655')

build() {
  cd $srcdir
  chmod -R og-rwx ./
  patch -p1 < lockdir.patch

  make linux

  echo "#!/usr/bin/ckermit" > _tmp.ini
  cat ckermit.ini >> _tmp.ini
}

package() {
  cd $srcdir

  mkdir -m 0755 -p $pkgdir/usr/bin
  mkdir -m 0755 -p $pkgdir/usr/share/man/man1
  mkdir -m 0755 -p $pkgdir/usr/share/licenses/$pkgname

  install -m 755 wermit $pkgdir/usr/bin/ckermit
  ln -s /usr/bin/ckermit $pkgdir/usr/bin/ckermit-sshsub
  install -m 644 ckuker.nr $pkgdir/usr//share/man/man1/ckermit.1
  install -m 644 COPYING.TXT $pkgdir/usr/share/licenses/$pkgname/License.txt

  install -m 755 _tmp.ini $pkgdir/usr/bin/ckermit.ini
}
