# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Alessandro Pezzoni <donbex at nerdshack dot com>

pkgbase=pycdio
pkgname=(python-pycdio python2-pycdio)
pkgver=2.1.0
pkgrel=2
pkgdesc="Python OO interface to libcdio (CD Input and Control library)"
url="https://pypi.python.org/pypi/pycdio"
arch=(x86_64)
license=(GPL)
makedepends=(libcdio python-setuptools python2-setuptools swig)
source=("https://pypi.io/packages/source/p/$pkgbase/$pkgbase-$pkgver.tar.gz")
sha512sums=('dc18fbd212f7040b8b06efaf263b17568e9c2d089cff7dfc4dda50a7225059e44c9c86e2d81ade92cf09c823f7189b5723fb601cb12f0884dc04e9f5418c5927')

prepare() {
  cd $pkgbase-$pkgver

  # patches here

  cd ..
  cp -a $pkgbase-$pkgver python2
  mv $pkgbase-$pkgver python3
}

build() {
  cd python3
  python3 setup.py build

  cd ../python2
  python2 setup.py build
}

package_python-pycdio() {
  depends=(python3 libcdio)

  cd python3
  python3 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python2-pycdio() {
  depends=(python2 libcdio)

  cd python2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set sw=2 sts=2 et:
