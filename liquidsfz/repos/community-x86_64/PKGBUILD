# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=liquidsfz
pkgver=0.2.1
pkgrel=1
pkgdesc="SFZ Sampler"
arch=('x86_64')
url="https://github.com/swesterfeld/liquidsfz"
license=('LGPL2.1')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc')
makedepends=('jack' 'libsndfile' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone application')
provides=('libliquidsfz.so' 'soundfont-synthesizer')
source=("$pkgname-$pkgver.tar.gz::https://github.com/swesterfeld/${pkgname}/archive/${pkgver}.tar.gz"
        )
sha512sums=('e3d61e7add6cf76c1840e192df30051210f0f7c773866ef5fd80d399832abf903e71b4ae1b6e8c11f1d7c1837cfae246524368ad7f499bf00214d0887da5d951')
b2sums=('ed493f7b779c3afc25c2c9c5c563b9bcf30eef2c5552413b6855e8cb3f1394f014fc6e3eceb4664151cadf79bac8150c4ec96b7ded7c6e49df0e128c83d74225')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --enable-shared \
              --disable-static \
              --disable-static-cxx
  make
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
  LD_LIBRARY_PATH="lib/.libs/:$LD_LIBRARY_PATH" lv2lint -Mpack -I "${PWD}/lv2" "http://spectmorph.org/plugins/liquidsfz"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {NEWS,TODO,{OPCODES,README}.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
