# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pzl <alsoelp _at_ gmail>

pkgname=perl-devel-checklib
pkgver=1.12
pkgrel=1
pkgdesc="Module to check if other perl modules are installed"
arch=('any')
url='http://search.cpan.org/dist/Devel-CheckLib/'
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-io-captureoutput')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MA/MATTN/Devel-CheckLib-${pkgver}.tar.gz")
sha512sums=('3a8eeb0ce500b17dad42a639776cb28aadbbf7e539da42ea95acabc1c23874fdba5af064f457ba81c801ca2c24289c25909d7a05907672d0b77373e2b157a67d')

build() {
  cd Devel-CheckLib-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Devel-CheckLib-$pkgver
  make test
}

package() {
  cd Devel-CheckLib-$pkgver
  make install DESTDIR="$pkgdir"
}
