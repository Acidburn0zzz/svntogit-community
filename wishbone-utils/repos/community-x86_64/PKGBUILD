# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=wishbone-utils
pkgver=0.6.7
pkgrel=1
pkgdesc='Utilities for working with a Wishbone bridge'
arch=(x86_64)
url="https://github.com/litex-hub/wishbone-utils"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::https://github.com/litex-hub/wishbone-utils/archive/v$pkgver.tar.gz)
sha512sums=('16c3a73fdfed4b828563624252842fc7de615ad70da34cf1b77eed4cb0570511991c74c23d4fe6bd2cabb481ebd0a552aedea65e00726f348959419b92790b6c')

build() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 wishbone-tool/target/release/wishbone-tool -t "${pkgdir}/usr/bin/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
