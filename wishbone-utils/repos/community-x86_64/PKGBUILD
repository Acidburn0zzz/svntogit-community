# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Maintainer: Daurnimator<daurnimator@archlinux.org>

pkgname=wishbone-utils
pkgver=0.7.3
pkgrel=1
pkgdesc='Utilities for working with a Wishbone bridge'
arch=(x86_64)
url="https://github.com/litex-hub/wishbone-utils"
license=(BSD)
depends=(glibc gcc-libs)
makedepends=(rust)
source=($pkgname-$pkgver.tar.gz::https://github.com/litex-hub/wishbone-utils/archive/v$pkgver.tar.gz)
sha512sums=('621ddbcf0b1e0c5243e97ac923755fc52012397545fb5df0a440ad64cef0fbc2b05d931f15f5ba8021e4fd70e79d21c0ae1fa77f205a8c6ba167d505d6da3fe4')

build() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo build --release --locked
}

check() {
  cd $pkgname-$pkgver/wishbone-tool
  cargo test --release --locked
}

package() {
  cd $pkgname-$pkgver
  install -Dm 755 wishbone-tool/target/release/wishbone-tool -t "${pkgdir}/usr/bin/"
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
