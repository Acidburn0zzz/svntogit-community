# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=rebuilderd
pkgver=0.7.0
pkgrel=1
pkgdesc='Independent verification system of binary packages'
url='https://github.com/kpcyrd/rebuilderd'
arch=('x86_64')
license=('GPL3')
depends=('libsodium' 'sqlite' 'archlinux-repro')
makedepends=('cargo' 'scdoc')
backup=('etc/rebuilderd.conf'
        'etc/rebuilderd-sync.conf'
        'etc/rebuilderd-worker.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('8f61cf365c67e36650ff76e01eac201440557344d9fd610b966e73501e5d8a303bf781fcd71fa797125ee943d1be8d1ffd35062a53b27cfd3df9586b9dc0f2ee'
            'SKIP')
b2sums=('f72c4f715f7521957d98e37faef534f4bb8d67a8c2fa971947d4e8761e1b9ea0035858a289fa1c31b260be6f7e2c4e88eb10672623ce8c501eb6d1dfafaf115d'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
  make docs
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 -t "${pkgdir}/usr/bin" \
    target/release/rebuilderd \
    target/release/rebuilderd-worker \
    target/release/rebuildctl

  # install rebuilder scripts
  install -Dm 755 -t "${pkgdir}/usr/libexec/rebuilderd" \
    worker/rebuilder-archlinux.sh \
    worker/rebuilder-debian.sh

  # install config files
  install -Dm 644 -t "${pkgdir}/etc" \
    contrib/confs/rebuilderd-sync.conf
  install -Dm 640 -g 212 -t "${pkgdir}/etc" \
    contrib/confs/rebuilderd-worker.conf \
    contrib/confs/rebuilderd.conf

  # install completions
  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/rebuildctl" completions bash > "${pkgdir}/usr/share/bash-completion/completions/rebuildctl"
  "${pkgdir}/usr/bin/rebuildctl" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_rebuildctl"
  "${pkgdir}/usr/bin/rebuildctl" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/rebuildctl.fish"

  # install systemd configs
  install -Dm 644 -t "${pkgdir}/usr/lib/systemd/system" \
    contrib/systemd/rebuilderd-sync@.service \
    contrib/systemd/rebuilderd-sync@.timer \
    contrib/systemd/rebuilderd-worker@.service \
    contrib/systemd/rebuilderd.service

  install -Dm 644 contrib/systemd/rebuilderd.sysusers "${pkgdir}/usr/lib/sysusers.d/rebuilderd.conf"
  install -Dm 644 contrib/systemd/rebuilderd.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/rebuilderd.conf"

  # install docs
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 -t "${pkgdir}/usr/share/man/man1" \
    contrib/docs/rebuilderd.1 \
    contrib/docs/rebuildctl.1 \
    contrib/docs/rebuilderd-worker.1
  install -Dm 644 -t "${pkgdir}/usr/share/man/man5" \
    contrib/docs/rebuilderd.conf.5 \
    contrib/docs/rebuilderd-sync.conf.5 \
    contrib/docs/rebuilderd-worker.conf.5
}

# vim: ts=2 sw=2 et:
