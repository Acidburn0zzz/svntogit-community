# $Id$
# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tuntox
pkgver=0.0.5.6d74a7a
_commit=6d74a7a2da9690ca56b3543a240d53b6b3915bf3
pkgrel=3
pkgdesc='Tunnel TCP connections over the Tox protocol'
arch=('i686' 'x86_64')
url='https://github.com/gjedeer/tuntox'
license=('GPL3')
depends=('toxcore')
makedepends=('git' 'cscope')
source=("git+https://github.com/gjedeer/tuntox.git#commit=$_commit"  #tag=$pkgver
       )
sha512sums=('SKIP')

prepare() {
  cd $pkgname
  sed -i "s|DEPS=libsodium toxcore|DEPS=libsodium libtoxcore|" Makefile
}

build() {
  cd $pkgname

  make gitversion.h tuntox_nostatic  # use dynamic linking
  mv tuntox_nostatic tuntox
}

package() {
  cd $pkgname
  install -Dm755 tuntox "$pkgdir/usr/bin/tuntox"
  install -Dm644 scripts/tuntox.service "$pkgdir/usr/lib/systemd/system/tuntox.service"
}
