# $Id$
# Maintainer: Jiachen Yang <farseerfc@gmail.com>
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=tuntox
pkgver=0.0.7
_commit=ae9993aba7a7c90045a1747fbf33518039b9bb08
pkgrel=1
pkgdesc='Tunnel TCP connections over the Tox protocol'
arch=('i686' 'x86_64')
url='https://github.com/gjedeer/tuntox'
license=('GPL3')
depends=('toxcore' 'libevent')
makedepends=('cscope')
source=( # "git+https://github.com/gjedeer/tuntox.git#commit=$_commit"  #tag=$pkgver
        "https://github.com/gjedeer/${pkgname}/archive/${pkgver}.tar.gz"
        "https://github.com/gjedeer/${pkgname}/releases/download/${pkgver}/${pkgver}.tar.gz.asc"
       )

sha512sums=('0b346fc5019b4dae6170771e3a544345cc048b2225737f6af608d1707fa153d762fdf8c22e53dd357fd5f410ad1e1d7ae920dc404ec3e38fa99744a44b03ff91'
            'SKIP')
validpgpkeys=('11C1B15A5D5DD662E469928AEBDA6B974ED3D2B7')  # GDR!_ <gdr@gdr.name>

prepare() {
  cd $pkgname-$pkgver
  #sed -i "s|DEPS=libsodium toxcore|DEPS=libsodium libtoxcore|" Makefile
}

build() {
  cd $pkgname-$pkgver

  # Makefile requires these, make it happy
  # pending issue: https://github.com/gjedeer/tuntox/issues/35
  mkdir -p .git
  touch .git/HEAD .git/index
  echo "#define GITVERSION \"${_commit}\"" > gitversion.h
  make tuntox_nostatic  # use dynamic linking
  mv tuntox_nostatic tuntox
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 tuntox "$pkgdir/usr/bin/tuntox"
  install -Dm755 scripts/tokssh "$pkgdir/usr/bin/tokssh"
  install -Dm644 scripts/tuntox.service "$pkgdir/usr/lib/systemd/system/tuntox.service"
}
