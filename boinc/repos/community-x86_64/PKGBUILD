# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=boinc
pkgver=6.10.13
pkgrel=1
pkgdesc="Berkeley Open Infrastructure for Network Computing"
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
depends=('curl' 'wxgtk')
makedepends=('libxslt' 'pkgconfig' 'perl-xml-sax' 'subversion')
options=(!libtool)
install=$pkgname.install
source=($pkgname.rc $pkgname.bash)

md5sums=('2fa7bcfaf4b03b118afe9e26b50f3a38'
         '05ed267db973ef7cbaf1118bb20bf9ce')

_svnroot="http://boinc.berkeley.edu/svn/tags"
_svnmod="boinc_core_release_${pkgver//./_}"

build() {
  cd ${srcdir}

  msg "Connecting to boinc.berkeley.edu SVN server..."
  svn co ${_svnroot}/${_svnmod} || (error "SVN checkout failed" && return 1)

  msg "SVN checkout done or server timeout"
  msg "Starting make..."
  if [ -d ${srcdir}/${_svnmod}-build ]; then
    rm -rf ${srcdir}/${_svnmod}-build
  fi
  cp -r ${srcdir}/$_svnmod ${srcdir}/$_svnmod-build
  cd ${srcdir}/$_svnmod-build

#configure
  ./_autosetup
  ./configure --prefix=/usr --disable-server --with-wxdir=/usr/lib \
              --enable-unicode --with-x --with-wx-config=$(which wx-config) \
              --enable-optimize --with-ssl
#  sed -i 's|LDFLAGS = |LDFLAGS = -L../lib |g' */Makefile || return 1

  make || return 1
  make DESTDIR=${pkgdir} install || return 1

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/etc/bash_completion.d/$pkgname

#install .desktop File
#  install -D -m644 ${srcdir}/${pkgname}.desktop ${pkgdir}/usr/share/applications/${pkgname}.desktop

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d || return 1
}
