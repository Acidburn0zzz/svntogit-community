# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Michal Krenek <mikos@sg1.cz>

pkgname=('boinc' 'boinc-nox')
pkgbase=boinc
pkgver=6.12.34
pkgrel=3
arch=('i686' 'x86_64')
url="http://boinc.berkeley.edu/"
license=('LGPL')
makedepends=('curl' 'libxslt' 'perl-xml-sax' 'subversion' 'wxgtk' 'libnotify')
options=('!libtool')
install=$pkgname.install
changelog=$pkgname.changelog
source=(boinc.rc boinc.bash boinc.desktop libnotify.patch subdirs.patch)
sha256sums=('92fe2468a1b2a85755c52dcb8045bb5443a4ff6a70d56ad586f44dab65730a6e'
            '63c7d8560a603f09ff46caf3797228c0b1552c69ee14d0a987f6e02e7142b025'
            '9cfe8ab04206131552af5030d4e1f8ddbfb0c1f8daeabe81384741373a7e9610'
            '7b15f58d701731d74bea9b44c79dc31096acd53cbc770c4cfc582fb7032ad657'
            'a424a53df0f3154bd4ae305493ef512646e882944349c5e1e98d305f614c685d')

_svnroot="http://boinc.berkeley.edu/svn/tags"
_svnmod="boinc_core_release_${pkgver//./_}"

build() {
  cd ${srcdir}

  msg "Connecting to boinc.berkeley.edu SVN server..."
  svn co ${_svnroot}/${_svnmod} || (error "SVN checkout failed" && return 1)
  msg "SVN checkout done or server timeout"

  msg "Starting make..."
  if [ -d ${srcdir}/${_svnmod}-build ]; then
    rm -rf ${srcdir}/${_svnmod}-build
  fi
  cp -r ${srcdir}/$_svnmod ${srcdir}/$_svnmod-build
  cd ${srcdir}/$_svnmod-build

  patch -p1 -i ../subdirs.patch
  patch -p1 -i ../libnotify.patch

  ./_autosetup

  # fix FS#28402, use flags; anything prettier is welcome
  sed \
	-e "/^CFLAGS =/c CFLAGS = $(pkg-config --cflags gtk+-2.0) ${CFLAGS}" \
	-e "/^LDFLAGS =/c LDFLAGS = $(pkg-config --libs gtk+-2.0) ${LDFLAGS}" \
	-e "/^CXXFLAGS =/c CXXFLAGS = $(pkg-config --cflags gtk+-2.0) ${CXXFLAGS}" \
	-i {.,*,*/*,*/*/*,*/*/*/*}/Makefile.in 

  ./configure --prefix=/usr --disable-server --enable-unicode --with-ssl \
              --with-wxdir=/usr/lib --with-wx-config=$(which wx-config) \
              --disable-static --enable-client --enable-manager
  make
}

package_boinc() {
  pkgdesc="Berkeley Open Infrastructure for Network Computing for desktop with X"
  depends=('curl' 'wxgtk' 'libnotify')
  conflicts=('boinc-nox')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

#install rc-script
  install -D -m755 ${srcdir}/$pkgname.rc ${pkgdir}/etc/rc.d/$pkgname

#install bash-completion
  install -D -m644 ${srcdir}/$pkgname.bash ${pkgdir}/etc/bash_completion.d/$pkgname

#install .desktop File
  install -D -m644 ${srcdir}/${pkgname}.desktop \
    ${pkgdir}/usr/share/applications/${pkgname}.desktop

#install icons
  install -D -m644 ${srcdir}/${_svnmod}-build/clientgui/res/boincmgr.48x48.png \
    ${pkgdir}/usr/share/pixmaps/$pkgname.png

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d
}

package_boinc-nox() {
  pkgdesc="Berkeley Open Infrastructure for Network Computing for server without X"
  depends=('curl')
  provides=('boinc')
  conflicts=('boinc')

  cd ${srcdir}/$_svnmod-build

  make DESTDIR=${pkgdir} install

#install rc-script
  install -D -m755 ${srcdir}/boinc.rc ${pkgdir}/etc/rc.d/boinc

#install bash-completion
  install -D -m644 ${srcdir}/boinc.bash ${pkgdir}/etc/bash_completion.d/boinc

#killing /etc/init.d directory
  rm -rf ${pkgdir}/etc/init.d

#removing graphical tools
  rm ${pkgdir}/usr/bin/boincmgr
  rm -fr ${pkgdir}/usr/share/boinc
}
