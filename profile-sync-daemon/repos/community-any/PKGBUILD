# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: graysky <graysky AT archlinux DOT us>

pkgname=profile-sync-daemon
pkgver=6.43
pkgrel=1
pkgdesc="Symlinks and syncs browser profile dirs to RAM"
arch=('any')
url="https://github.com/graysky2/profile-sync-daemon"
license=('MIT')
depends=('bash' 'findutils' 'inetutils' 'procps-ng' 'rsync' 'systemd')
optdepends=('zsh-completions: for completion when using zsh')
install="${pkgname}.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/graysky2/$pkgname/archive/v$pkgver.tar.gz")
sha512sums=('bfde1b07a0f087cb248d21b4460e85d1a18f2979ce3caab02b9ac96d7668aca276fe9ed1147b2b494d15269363a88f313e9250faf587b5813813f9606634049e')
b2sums=('76d468954ec53653ab41f757f9c7dc98716934ec11c28967df178ca9566574fe25892991c4beab5989d12ba5aed7d1392a4703b5990a735b9fed951bd67fe586')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
