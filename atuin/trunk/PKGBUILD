# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Ellie Huxtable <e@elm.sh>

pkgname=atuin
pkgver=0.9.0
pkgrel=1
pkgdesc="Magical shell history"
arch=('x86_64')
url="https://github.com/ellie/atuin"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz"
        "$pkgname-lockfile.patch::$url/commit/681c5f6bbd48dc4fcf1c2215bb0b7fa21b09187a.patch")
sha256sums=('82ecf05fb39b25339ef9ea22e26f28e643f5cf08b41bfc767eb17f2b2ced5ea1'
            '707aabe64b7f02c3c7c3223fbfb8dfd3d34a45d95e8c6aa5aa7d6c53210b34e7')
options=('!lto')

prepare() {
  cd "$pkgname-$pkgver"
  patch -Np1 -i "../$pkgname-lockfile.patch"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
  mkdir completions/
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --all-features
  for sh in 'bash' 'fish' 'zsh'; do
    "target/release/$pkgname" gen-completions -s "$sh" -o completions/
  done
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 "completions/$pkgname.bash" "${pkgdir}/usr/share/bash-completion/completions/$pkgname"
  install -Dm 644 "completions/$pkgname.fish" -t "${pkgdir}/usr/share/fish/vendor_completions.d"
  install -Dm 644 "completions/_$pkgname" -t "${pkgdir}/usr/share/zsh/site-functions"
}

# vim: ts=2 sw=2 et:
