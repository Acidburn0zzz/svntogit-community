# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Fredrick Brennan <copypaste@kittens.ph>
# Contributor: Stephan Eisvogel <eisvogel at embinet dot de>

pkgname=python-pikepdf
pkgver=1.11.2
pkgrel=1
pkgdesc='Read and write PDFs with Python, powered by qpdf'
arch=(x86_64)
url='https://github.com/pikepdf/pikepdf'
license=(MPL2)
makedepends=(pybind11 python-setuptools)
depends=(python-lxml qpdf)
source=($pkgname-$pkgver.tar.gz::"https://github.com/pikepdf/pikepdf/archive/v$pkgver.tar.gz")
sha256sums=('3eff3c78985bcb47b2bc43276ac1d9f83c6e981966b91b9b202bd9174ef06ecb')

build() {
  cd pikepdf-$pkgver
  python setup.py build
}

package() {
  cd pikepdf-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1 --skip-build
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
}
