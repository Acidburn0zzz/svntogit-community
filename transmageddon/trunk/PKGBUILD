# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=transmageddon
pkgver=0.21
pkgrel=1
pkgdesc="Simple python application for transcoding video into formats supported by GStreamer"
arch=('any')
url="http://linuxrising.org/"
license=('LGPL')
depends=('gstreamer0.10-python' 'pygtk' 'gstreamer0.10-base-plugins')
makedepends=('intltool')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'            
            'gstreamer0.10-ffmpeg: Extra media codecs')
source=(http://linuxrising.org/files/$pkgname-$pkgver.tar.xz
        revert-prs-directory.patch)
md5sums=('1149a5b670ab30007274c27850550de9'
         '7419d6fd391dfc10969ca0da60c51bef')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python transmageddon.py/python2 transmageddon.py/' bin/transmageddon.in

  # Revert the custom location of prs directory, because it's not supported yet by gst-python
  patch -Np1 -i "$srcdir/revert-prs-directory.patch"

  autoreconf -fi
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
