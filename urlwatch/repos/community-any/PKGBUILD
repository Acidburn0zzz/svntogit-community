# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.17
pkgrel=3
pkgdesc="Tool for monitoring webpages for updates"
arch=('any')
url="https://thp.io/2008/urlwatch/"
license=('BSD')
depends=('python-appdirs' 'python-keyring' 'python-lxml' 'python-cssselect' 'python-minidb' 'python-requests' 'python-yaml')
optdepends=('lynx: convert HTML to plain text'
            'python-beautifulsoup4: prettify HTML'
            'python-html2text: convert HTML to plain text')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/thp/urlwatch/archive/${pkgver}.tar.gz"
        "escape.patch")
sha256sums=('83101382c2cbc6e84752f12cdbdefed67b3018085fa5ffb68923d6b1fe97854d'
            '421653801e8c397c5147db30a67fcf44aaff8f471af33db83bf16847c2f90ecc')

prepare() {
    cd "${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/escape.patch"
}

build() {
    cd "${pkgname}-${pkgver}"
    python3 setup.py build
}

package() {
    cd "${pkgname}-${pkgver}"
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
