# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=2.19
pkgrel=1
pkgdesc='Tool for monitoring webpages for updates'
arch=('any')
url=https://thp.io/2008/urlwatch/
license=('BSD')
depends=('python-appdirs' 'python-cssselect' 'python-keyring' 'python-lxml'
         'python-minidb' 'python-requests' 'python-setuptools' 'python-yaml')
optdepends=('lynx: for one of the html2text filter methods'
            'python-beautifulsoup4: for the beautify filter and one of the html2text filter methods'
            'python-cssbeautifier: for the beautify filter'
            'python-html2text: for one of the html2text filter methods'
            'python-jsbeautifier: for the beautify filter')
#checkdepends=('python-nose' 'python-pycodestyle')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('3279abed54868c09a4fcf113e06ba9d70c77e48c968136cfc1d9b602e54c4927')

prepare() {
  cd $pkgname-$pkgver
  sed -i "s/'pycodestyle', //" setup.py
}

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

# Code style errors (also on trunk)
#check() {
#  cd $pkgname-$pkgver
#  nosetests
#}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname COPYING
}
