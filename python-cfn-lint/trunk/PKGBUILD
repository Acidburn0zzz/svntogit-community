# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.20.3
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-pyaml python-six python-requests python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-mock)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver.tar.gz"
        fix-tests.patch)
sha256sums=('b8fed4cce8b629e30f10d2d7ec079f5ddeb5ec66275ec3ac4864d90dbffa709d'
            'e530839ba21e4aa6d19fb08563aa91b16d5f445e0ceb7f97021a130ff8948465')

prepare() {
  cd cfn-python-lint-$pkgver
  patch -Np1 -i ../fix-tests.patch
  sed -i 's|,<=2.21.0||' setup.py
}

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  PYTHONPATH=build/lib pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
