# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
pkgver=0.29.6
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-python-lint'
license=('custom:MIT No Attribution')
depends=(python python-yaml python-six python-aws-sam-translator
         python-jsonpatch python-jsonschema python-setuptools python-networkx)
checkdepends=(python-pytest python-mock python-pydot)
optdepends=(
  'python-pydot: for building graphs from templates'
)
source=("https://github.com/aws-cloudformation/cfn-python-lint/archive/v$pkgver/cfn-python-lint-$pkgver.tar.gz")
sha256sums=('f5e99eed83d76ba1ce0065df60dce12a7b56d0d46e16f650e6fcce9d7a75ca2c')

build() {
  cd cfn-python-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-python-lint-$pkgver

  # Tests in test/integration need the cfn-lint binary
  python setup.py install_scripts --install-dir="$srcdir"/tmp_install

  export PYTHONPATH="$PWD/src"
  export PATH="$PATH:$srcdir/tmp_install"
  pytest -v test
}

package() {
  cd cfn-python-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
