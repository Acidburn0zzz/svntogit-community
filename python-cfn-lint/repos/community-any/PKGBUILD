# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-cfn-lint
# https://github.com/aws-cloudformation/cfn-lint/blob/main/CHANGELOG.md
pkgver=0.57.0
pkgrel=1
pkgdesc='CloudFormation Linter'
arch=(any)
url='https://github.com/aws-cloudformation/cfn-lint'
license=('custom:MIT No Attribution')
# python-six may be unnecessary in the next version https://github.com/aws-cloudformation/cfn-lint/pull/2204
depends=(python python-yaml python-six python-aws-sam-translator
         python-jsonpatch python-jsonschema python-networkx
         python-junit-xml python-jschema-to-python python-sarif-om)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pydot)
optdepends=(
  'python-pydot: for building graphs from templates'
)
source=("https://github.com/aws-cloudformation/cfn-lint/archive/v$pkgver/cfn-lint-$pkgver.tar.gz")
sha256sums=('12fa2816c5e04f1243b81133321aeab7ca8e16f00eba0a5daae24d4b9351156c')

build() {
  cd cfn-lint-$pkgver
  python setup.py build
}

check() {
  cd cfn-lint-$pkgver

  # Tests in test/integration need the cfn-lint binary
  python setup.py install_scripts --install-dir="$srcdir"/tmp_install

  export PYTHONPATH="$PWD/src"
  export PATH="$PATH:$srcdir/tmp_install"
  # test_update_docs requires cfn-lint as a git repo
  pytest test -k 'not test_update_docs'
}

package() {
  cd cfn-lint-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
