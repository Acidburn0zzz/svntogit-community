# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libvirt-dbus
pkgver=1.3.0
pkgrel=1
pkgdesc="Wrapper around libvirt API to provide a high-level object-oriented API better suited for dbus-based applications"
arch=('x86_64')
url="https://libvirt.org/dbus.html"
license=('LGPL')
depends=('libvirt-glib' 'dbus' 'libssh')
validpgpkeys=('4252D86A52041137C291CADFC85C5E957062A701')
source=("https://libvirt.org/sources/dbus/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha512sums=('44e31beedc5aeb5bcd032d3a9766b0380b2320f1ae78cbcfb316d7a9025fb33f1ff24fdc65c023f38c2408062e7d40e62392cbc756d1cf8f8644a086bc871544'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  autoreconf -vi
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir build && cd build
  ../configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --disable-static \
    --with-runstatedir=/run
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}/build"
  make DESTDIR="${pkgdir}" install
}
