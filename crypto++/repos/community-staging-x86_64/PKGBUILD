# Maintainer: Baptiste Jonglez <archlinux at bitsofnetworks.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kritoke <kritoke@gamebox.net>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>

pkgname=crypto++
pkgver=6.0.0
_srcver=${pkgver//./}
pkgrel=1
pkgdesc="A free C++ class library of cryptographic schemes"
arch=('x86_64')
url="https://www.cryptopp.com/"
license=('custom')
depends=('gcc-libs')
makedepends=('unzip')
# Fix https://bugs.archlinux.org/task/56689
install="crypto++.install"
source=("https://www.cryptopp.com/cryptopp${_srcver}.zip"
        "fix-simon-speck-simd-build.patch"
        'libcrypto++.pc')
# Checksums from https://www.cryptopp.com/release600.html
sha1sums=('290dfc1948b860b29da40f8c5559f6559ce787db'
          'a4be3000116568260a8fe3169b948deca5576fdf'
          '7a6c6fc7aed96ff7be554eee60450a1914b0cd95')
sha256sums=('64ac2db96b3f1b7a23675e2be95d16c96055edffa2d5e2de6245fdb6baa92dda'
            '80d43fe472f2ccffa56097eda5e56820d0944c1d585a68d944049a1d00d395c3'
            '14228b0d1a6f07572090ab17b60bf9829308d174e763eb31966f723b0cb51134')
sha512sums=('7a60c28be1fc9418adeabe8c66d868b46e65fb3d77d308b9431b16905cde7faa670180619150e849ae44d70f44aa8501a0a0c4354ec84f194fd8138d14aca9bf'
            '80378bb6b19427fa316892126a3b533a98831405215df6f8c0b251e319b05c625a5d437f7217641ca8e41e5be153a796204686c4844338befdfc42e43a8ece6e'
            'bba4ff9c01e353f0336e01836fd1418a08f43ab9215f678cc80cc94954d689e94e1425c7ed95f9b12d210bfc73574c98d0579b1356232c50f32f342395408ac2')

prepare() {
  patch -p1 < "$srcdir"/fix-simon-speck-simd-build.patch
}

build() {
  make -f GNUmakefile-cross dynamic cryptest.exe \
    CXXFLAGS+=" -DNDEBUG -fPIC" HAS_SOLIB_VERSION=1 \
    ARIA_FLAG="-mssse3" \
    SIMON_FLAG="-mssse3 -msse4.1" \
    SPECK_FLAG="-mssse3 -msse4.1" \
    SSSE3_FLAG="-mssse3" \
    BLAKE2_FLAG="-msse4.2" \
    CRC_FLAG="-msse4.2" \
    GCM_FLAG="-mssse3 -mpclmul" \
    AES_FLAG="-msse4.1 -maes" \
    SHA_FLAG="-msse4.2 -msha"
}

check() {
  make test
}

package() {
  make -f GNUmakefile-cross install DESTDIR="$pkgdir" PREFIX="/usr" HAS_SOLIB_VERSION=1
  # Install pkgconfig file
  install -d "${pkgdir}/usr/lib/pkgconfig"
  install -m644 "${srcdir}/libcrypto++.pc" "${pkgdir}/usr/lib/pkgconfig/libcrypto++.pc"
  # Remove crypttest.exe: it's only needed for check(), and does not even work when
  # installed in /usr/bin (it needs additional files)
  rm "${pkgdir}/usr/bin/cryptest.exe"
  rmdir "${pkgdir}/usr/bin/"
  # Install license
  install -D -m644 License.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
