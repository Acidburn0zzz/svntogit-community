# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=xchm
pkgver=1.29
pkgrel=1
pkgdesc="Cross-platform GUI front-end to chmlib"
arch=('x86_64')
url="https://github.com/rzvncj/xCHM"
license=('GPL')
depends=('chmlib' 'wxgtk3')
source=("https://github.com/rzvncj/xCHM/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('cea900cf4252c7f2f6736e48a4f639b0bad00fdce249aa55ebb6a44dcf976b0a')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 art/xchm-16.png "$pkgdir/usr/share/icons/hicolor/16x16/apps/xchm.png"
  install -Dm644 art/xchm-32.png "$pkgdir/usr/share/icons/hicolor/32x32/apps/xchm.png"
  install -Dm644 art/xchm-48.png "$pkgdir/usr/share/icons/hicolor/48x48/apps/xchm.png"
  install -Dm644 art/xchm-128.png "$pkgdir/usr/share/icons/hicolor/128x128/apps/xchm.png"
  rm -r "$pkgdir"/usr/share/pixmaps
}
