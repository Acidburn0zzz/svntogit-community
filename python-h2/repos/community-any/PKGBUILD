# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-h2
pkgver=4.0.0
pkgrel=3
pkgdesc="HTTP/2 State-Machine based protocol implementation"
arch=('any')
license=('MIT')
url="https://hyper.rtfd.org"
depends=('python-hpack' 'python-hyperframe')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-hypothesis')
source=("https://github.com/python-hyper/h2/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        '0646279dab694a89562846c810202ce2c0b49be3.patch::https://github.com/python-hyper/h2/commit/0646279dab694a89562846c810202ce2c0b49be3.patch')
sha512sums=('ae145421549ecd29845b100e3895f1088598142824d82cf0b59293b0f647939e4acd0ff809187cabd67a3a524a0337cc925a3f17624c86767bd87a2d18a59b2b'
            'c92a5577338b6ef355a203a2a74ba3d6b997458a179fa7d12eaf4a0f89fe430a2c90a586791afd0c1563d94352586d59aacfb34926c67e5ec7d2236b2c0fcff2')

prepare() {
  cd h2-$pkgver
  patch -p1 <"$srcdir"/0646279dab694a89562846c810202ce2c0b49be3.patch
}

build() {
  cd h2-$pkgver
  python setup.py build
}

check() {
  cd h2-$pkgver
  python setup.py pytest
}

package() {
  cd h2-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
