# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-daemon
pkgver=2.93.5.20151221
_commit=26cd2d175b50b68a6a72ebc4ea6375a50abf3ef3
#_golibver=0.4.4
_golibrev=c276a48ce147db8cba1951da6164f8979d021cb4
#_deepinapiver=2.92.4
_deepinapirev=9d927e1a74a684edea6d7d80bca096952355c75b
#_dbusfactoryver=2.92.3
_dbusfactoryrev=bef8c791e07beb144312b0eee83b00d1c7f68618
#_girgeneratorver=0.9.1
_girgeneratorrev=6e53a3b74f2076b2f472920cd2bc1ae4752e0608
_desktopbaserev=77f030f19ce5b04da5634d1a7275feb4487b7fb3
pkgrel=1
pkgdesc='Daemon handling the DDE session settings'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-daemon"
license=('GPL3')
depends=('deepin-desktop-schemas' 'gvfs' 'libcanberra-pulse' 'metacity' 'poppler-glib'
         'rfkill' 'acpid' 'bluez' 'deepin-notifications' 'iso-codes' 'lsb-release'
         'mobile-broadband-provider-info' 'polkit-gnome' 'udisks2' 'upower' 'gcc-libs>=5.3.0-3'
         'libxkbfile' 'accountsservice')
makedepends=('deepin-dbus-generator' 'deepin-gir-generator' 'sqlite' 'git' 'mercurial' 'gcc-go')
optdepends=('networkmanager: Network Management daemon'
            'deepin-grub2-themes: deepin theme for grub menu')
conflicts=('dde-daemon')
replaces=('dde-daemon')
groups=('deepin')
install="${pkgname}.install"
source=(#"git+https://github.com/linuxdeepin/dde-daemon.git#tag=$pkgver"
        "git+https://github.com/linuxdeepin/dde-daemon.git#commit=$_commit"
        #"git+https://github.com/linuxdeepin/go-lib.git#tag=$_golibver"
        "git+https://github.com/linuxdeepin/go-lib.git#commit=$_golibrev"
        #"git+https://github.com/linuxdeepin/dde-api.git#tag=$_deepinapiver"
        "git+https://github.com/linuxdeepin/dde-api.git#commit=$_deepinapirev"
        #"git+https://github.com/linuxdeepin/dbus-factory.git#tag=$_dbusfactoryver"
        "git+https://github.com/linuxdeepin/dbus-factory.git#commit=$_dbusfactoryrev"
        "git+https://github.com/linuxdeepin/go-gir-generator.git#commit=$_girgeneratorrev"
        "git+https://github.com/linuxdeepin/deepin-desktop-base.git#commit=$_desktopbaserev"
        'deepin-daemon.sysusers'
        'polkit-gnome-authentication-agent-1-deepin.desktop')
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '4482f2c82c3652040021dd43515f131184a0417e341dc37db487117012245e25'
            '014f4c1111530e7556c3a83ab4409ae7dd47a87550004128ed8b7d02c58357e7')

prepare() {
  sed -i "s|\"\${GOLDFLAGS}\"|\"\${GOLDFLAGS} $CFLAGS $LDFLAGS\"|" dde-daemon/Makefile
  sed -i 's| generator$||' go-gir-generator/makefile

  export GOPATH="$srcdir/build"
  mkdir -p build/src/pkg.deepin.io/{lib,dde/api}
  cp -a go-lib/* build/src/pkg.deepin.io/lib
  cp -a dde-api/* build/src/pkg.deepin.io/dde/api/
  
  go get github.com/BurntSushi/xgb github.com/BurntSushi/xgbutil github.com/howeyc/fsnotify \
         github.com/mattn/go-sqlite3 gopkg.in/alecthomas/kingpin.v2 github.com/disintegration/imaging \
         code.google.com/p/graphics-go/graphics code.google.com/p/jamslam-freetype-go/freetype \
         code.google.com/p/jamslam-freetype-go/freetype/truetype
}

build() {
  cd "$srcdir/dbus-factory"
  make install-golang

  cd "$srcdir/go-gir-generator"
  make GENERATOR=gir-generator build
  make copyfile
  cp -r out/src/gir "$GOPATH/src"/

  cd "$srcdir/dde-daemon"
  make USE_GCCGO=1

  cd "$srcdir/deepin-desktop-base"
  make
}

package() {
  cd dde-daemon
  make USE_GCCGO=1 DESTDIR="$pkgdir" install

  install -Dm644 ../polkit-gnome-authentication-agent-1-deepin.desktop "$pkgdir/etc/xdg/autostart/polkit-gnome-authentication-agent-1-deepin.desktop"
  install -Dm644 ../deepin-daemon.sysusers "$pkgdir/usr/lib/sysusers.d/deepin-daemon.conf"
  install -Dm644 "$srcdir"/deepin-desktop-base/files/desktop-version "$pkgdir"/etc/deepin-version
  #install -Dm644 "$srcdir"/deepin-desktop-base/files/desktop-version "$pkgdir"/usr/lib/deepin/deepin-version
}
