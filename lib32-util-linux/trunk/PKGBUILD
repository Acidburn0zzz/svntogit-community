# $Id$
# Maintainer: judd <jvinet@zeroflux.org>

_pkgbasename=util-linux
pkgname=lib32-$_pkgbasename
pkgver=2.19
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux (32-bit)"
url="http://userweb.kernel.org/~kzak/util-linux-ng/"
arch=('x86_64')
depends=('lib32-glibc' $_pkgbasename)
makedepends=('gcc-multilib')
provides=('lib32-util-linux-ng')
conflicts=('lib32-util-linux-ng')
replaces=('lib32-util-linux-ng')
license=('GPL2')
options=('!libtool' '!emptydirs')
source=(ftp://ftp.kernel.org/pub/linux/utils/${_pkgbasename}/v2.19/${_pkgbasename}-${pkgver}.tar.bz2)
md5sums=('590ca71aad0b254e2631d84401f28255')

shopt -s extglob

build() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  # hardware clock
  sed -e 's%etc/adjtime%var/lib/hwclock/adjtime%' -i hwclock/hwclock.c
  mkdir -p "${pkgdir}/var/lib/hwclock"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./autogen.sh
  ./configure --without-ncurses --libdir=/usr/lib32

  cd shlibs
  for lib in *; do
    make -C $lib
  done
}

package() {
  cd "${srcdir}/${_pkgbasename}-${pkgver}"

  make DESTDIR="$pkgdir" install

  # remove unnecessary files
  cd "$pkgdir"
  rm -rf !(usr)
  cd usr
  rm -rf !(lib32)
}
