# $Id$
# Maintainer:  Ionut Biru <ibiru@archlinux.org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: Michael Barker

pkgname=sensors-applet
pkgver=2.2.7
pkgrel=3
pkgdesc="An applet for the GNOME Panel to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings."
arch=('i686' 'x86_64')
url="http://sensors-applet.sourceforge.net/"
license=('GPL')
depends=('gnome-panel-bonobo' 'lm_sensors' 'hicolor-icon-theme')
makedepends=('intltool' 'gnome-doc-utils>=0.18.1' 'libxnvctrl' 'libgnomeui')
optdepends=('hddtemp: for HDD support'
	    'libxnvctrl: for Nvidia GPU support')
install=sensors-applet.install
source=(http://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz
        sensors-applet-2.2.7-libnotify-0.7.patch)
options=('!libtool')
md5sums=('f342fab98be4f1b9a25af0689af20130'
         '96a4393d9e75e5eb88a6e906a5a7e464')

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i $srcdir/sensors-applet-2.2.7-libnotify-0.7.patch
  ./configure --prefix=/usr \
    --libexecdir=/usr/lib/sensors-applet \
    --localstatedir=/var \
    --disable-scrollkeeper \
    --with-nvidia \
    --enable-libnotify
  make
  make DESTDIR=$pkgdir install
}
