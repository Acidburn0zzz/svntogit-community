# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=libsemigroups
pkgver=1.0.3
pkgrel=1
pkgdesc="C++ library for semigroups and monoids"
url="https://libsemigroups.github.io/libsemigroups/"
arch=(x86_64)
license=(GPL3)
depends=(gcc-libs)
makedepends=(fmt)
source=(https://github.com/libsemigroups/libsemigroups/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz
        system-fmt.patch)
sha256sums=('6adc2e023ad1349f0a942355b88241198d23790e62216bfeb62c356d803119da'
            '86c0ae8613df2959924d86c16874ee8d72288b10f87225c3909132b1d2a83fdf')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../system-fmt.patch
  rm -r extern/fmt-*
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
