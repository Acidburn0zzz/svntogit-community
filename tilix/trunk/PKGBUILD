# $Id$
# Maintainer: Dan Printzell <arch@vild.io>
# Contributor: dsboger <https://github.com/dsboger>
# Contributor: Carl George < arch at cgtx dot us >

pkgname="tilix"
pkgver=1.7.7
pkgrel=2
pkgdesc="A tiling terminal emulator for Linux using GTK+ 3"
arch=('x86_64')
url="https://github.com/gnunn1/tilix"
license=('MPL')
depends=('libx11' 'gtkd' 'vte3' 'dconf' 'gsettings-desktop-schemas')
makedepends=('ldc' 'po4a')
optdepends=('python-nautilus: for "Open Tilix Here" support in nautilus'
            'libsecret: for the password manager')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        "gtkd-3.8.0.patch::$url/commit/76ac251da7f6e639a1a49d1c4d4ad48e135ab79f.patch")
sha256sums=('9c8ba7734c61fee7efcd3c75548c996216383092e4d33472aeaa457ddfdb3828'
            '9be77af2bd98a2353c23e86405a26a6cc253ca4affb66bb1bf341503bcd2d9b5')

prepare() {
    cd "$pkgname-$pkgver"

    patch -p1 -i "$srcdir/gtkd-3.8.0.patch"

    ./autogen.sh
}

build() {
    cd "$pkgname-$pkgver"

    ./configure \
    --prefix=/usr \
    PO4A_TRANS=/usr/bin/vendor_perl/po4a-translate \
    DC='ldmd' \
    DCFLAGS='-O -inline -release -version=StdLoggerDisableTrace'
    make
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install
}
