# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=8.6.1
pkgrel=2
pkgdesc='Text document format for short documents, articles, books and UNIX man pages.'
arch=('any')
url='http://www.methods.co.nz/asciidoc/'
license=('GPL')
depends=('python2' 'libxslt' 'docbook-xsl')
optdepends=('lilypond: music-filter'
            'imagemagick: music-filter (used in conjunction with lilypond)'
            'source-highlight: source-highlight-filter'
            'fop: pdf generation'
            'lynx: text generation'
            'w3m: text generation (alternative to lynx)')
source=("http://downloads.sourceforge.net/asciidoc/asciidoc-${pkgver}.tar.gz")
md5sums=('3d16b16a953c0c2d00b6a6d089882d0e')

build() {
  cd ${pkgname}-${pkgver}

  # python2 fix
  for file in asciidocapi.py a2x.py asciidoc.py filters/music/music2png.py \
      filters/code/code-filter.py filters/graphviz/graphviz2png.py; do
    sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
}

package() {
  cd ${pkgname}-${pkgver}

  make install DESTDIR=${pkgdir}

  install -Dm644 asciidocapi.py \
    ${pkgdir}/usr/lib/python2.7/site-packages/asciidocapi.py

  install -Dm644 vim/syntax/asciidoc.vim \
    ${pkgdir}/usr/share/vim/vimfiles/syntax/asciidoc.vim

  install -Dm644 vim/ftdetect/asciidoc_filetype.vim \
    ${pkgdir}/usr/share/vim/vimfiles/ftdetect/asciidoc.vim
}
