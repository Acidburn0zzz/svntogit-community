# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=waybar
pkgver=0.9.0
pkgrel=2
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors'
arch=('x86_64')
url="https://github.com/Alexays/Waybar/"
license=('MIT')
depends=(
    'gtkmm3'
    'jsoncpp'
    'libjsoncpp.so'
    'libspdlog.so'
    'libdbusmenu-gtk3'
    'libpulse'
    'libmpdclient'
)
# Sway is needed for building support for workspace and window title modules
makedepends=(
    'meson'
    'scdoc'  # For generating manpages
    'sway'
)
optdepends=(
    'otf-font-awesome: Icons in the default configuration'
)
source=(
    "$pkgname-$pkgver.tar.gz::https://github.com/Alexays/Waybar/archive/$pkgver.tar.gz"
)
sha256sums=('65e8397d5a8880cbb9172138e361b0d91f649bc99327d36945e38d1e5dbb157d')

build() {
    cd "Waybar-$pkgver"
    meson --prefix=/usr build
    ninja -C build
}

package() {
    cd "Waybar-$pkgver"
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname/"
}
