# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=6.3.1
pkgrel=1
pkgdesc='A powerful IMAP/Maildir synchronization tool'
arch=('any')
url='https://github.com/nicolas33/offlineimap'
license=('GPL')
depends=('python2')
noextract=($pkgname.1.gz)
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/nicolas33/$pkgname/tarball/v$pkgver \
        $pkgname.1.gz)
sha256sums=('489f84d953bc5ceab34269d330ee4a6e0b27b87fe24dc4eb588dec897927d529'
            '3bfbf289b9d2bd50c8c6567f4eb0ac5d0ab6337192093eecc5f59428f727c524')

package() {
  cd ${srcdir}/nicolas33-${pkgname}-98f5181

  python2 setup.py build install --root=${pkgdir}

  install -D -m644 $pkgname.conf.minimal ${pkgdir}/usr/share/$pkgname/$pkgname.conf.minimal
  install -D -m644 $pkgname.conf ${pkgdir}/usr/share/$pkgname/$pkgname.conf

#man page doesn't convert: http://lists.complete.org/offlineimap@complete.org/2008/05/msg00018.html.gz
#extracted from debian package: http://ftp.debian.org/debian/pool/main/o/offlineimap/offlineimap_6.2.0_all.deb
  install -D -m644 ${srcdir}/$pkgname.1.gz ${pkgdir}/usr/share/man/man1/$pkgname.1.gz
}
