# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: mucknert <mucknert@gmx.net>

pkgname=offlineimap
pkgver=7.3.0
pkgrel=1
pkgdesc='Synchronizes emails between two repositories'
arch=('any')
url='http://offlineimap.org/'
license=('GPL')
depends=('python2-six' 'python2-uritemplate' 'python2-rfc6555')
makedepends=('asciidoc')
optdepends=('python2-pysocks: for SOCKS5 proxy functionality')
changelog=$pkgname.changelog
source=($pkgname-$pkgver.tar.gz::https://github.com/OfflineIMAP/offlineimap/archive/v${pkgver}.tar.gz)
sha256sums=('d8378e82e392c70f5c20cb08705687da30cd427f2bca539939311512777e6659')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  find . -type f -exec sed '1s,^#! \?/usr/bin/\(env \|\)python$,#!/usr/bin/python2,' -i {} \;
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  python2 setup.py build

  cd "${srcdir}"/${pkgname}-${pkgver}/docs
  make man
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 docs/offlineimap.1 "${pkgdir}"/usr/share/man/man1/offlineimap.1
  install -Dm644 docs/offlineimapui.7 "${pkgdir}"/usr/share/man/man7/offlineimapui.7
  install -Dm644 offlineimap.conf "${pkgdir}"/usr/share/offlineimap/offlineimap.conf
  install -Dm644 offlineimap.conf.minimal "${pkgdir}"/usr/share/offlineimap/offlineimap.conf.minimal

#systemd files
  install -Dm644 contrib/systemd/$pkgname.service "${pkgdir}"/usr/lib/systemd/user/$pkgname.service
  install -Dm644 contrib/systemd/$pkgname@.service "${pkgdir}"/usr/lib/systemd/user/$pkgname@.service
  install -Dm644 contrib/systemd/$pkgname-oneshot.service "${pkgdir}"/usr/lib/systemd/user/$pkgname-oneshot.service
  install -Dm644 contrib/systemd/$pkgname-oneshot.timer "${pkgdir}"/usr/lib/systemd/user/$pkgname-oneshot.timer
  install -Dm644 contrib/systemd/$pkgname-oneshot@.service "${pkgdir}"/usr/lib/systemd/user/$pkgname-oneshot@.service
  install -Dm644 contrib/systemd/$pkgname-oneshot@.timer "${pkgdir}"/usr/lib/systemd/user/$pkgname-oneshot@.timer
}
