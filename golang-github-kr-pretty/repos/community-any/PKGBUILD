# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-kr-pretty
pkgver=0.1.0
pkgrel=1
pkgdesc='Pretty printing for Go values'
arch=('any')
url="https://github.com/kr/pretty"
license=('MIT')
depends=('go-pie' 'golang-github-kr-text')
source=("$pkgname-$pkgver.tar.gz::https://github.com/kr/pretty/archive/v$pkgver.tar.gz")
sha512sums=('7a367c317043d40cfbf7fac631a286c0bbb8b45a716fe7388d6271431e354b3913505398b8ccf7ffbb01abf71f04216738936a4282c3c9645e26c51b7692e820')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/kr
  cp -a "$srcdir"/pretty-$pkgver "$srcdir"/build/src/github.com/kr/pretty
  go test -v ./pretty-$pkgver
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/kr
  cp -a "$srcdir"/pretty-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/kr/pretty

  install -Dm644 pretty-$pkgver/License "$pkgdir"/usr/share/licenses/$pkgname/License
}
