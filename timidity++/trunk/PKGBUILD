# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=timidity++
pkgver=2.15.0
pkgrel=1
pkgdesc="A MIDI to WAVE converter and player"
arch=('x86_64')
url="http://timidity.sourceforge.net"
license=('GPL')
depends=('libao' 'jack')
makedepends=('xaw3d' 'gtk2' 'tk' 'libxaw')
optdepends=('gtk2: for using the GTK+ interface'
            'tk: for using the Tk interface'
            'xaw3d: for using the Xaw interface')
backup=('etc/timidity++/timidity.cfg')
source=(http://downloads.sourceforge.net/timidity/TiMidity++-${pkgver}.tar.xz
        timidity.cfg timidity.service timidity-jack.patch)
sha512sums=('36c3c79d941903109d5d6d529963d12e8192a857e80402ad5ab0d1e223451a7cf5dc8fdd046b22ae7da96f413d68db8587a8fce348635838eba2f30f7019192f'
            '054cd39bef913314e9901171566398af6280236531ac13ece683eaeb642c52b3280116adaa6328054c94bd79b0ab88c253281e386012a2c6d67f4b85c38e4859'
            'b77e460c9803300db67d571d5298edbf4c29a469593c41640057ff8d173ed6edf8b9c2d6cd9a35f6cf1cc32d5b92be38513105f927991fd451725802abb1108b'
            '51c075da04aeedca4edecb5a7f1926ccb80637b550f8fafca6d68be466cd4353a77bbb832878941919ec354c279a6014403e995b9152a47a3817c99890a4afeb')

prepare() {
  cd TiMidity++-${pkgver}
  patch -p1 -i "${srcdir}/timidity-jack.patch"
}

build() {
  cd TiMidity++-${pkgver}
  ./configure --prefix=/usr --mandir=/usr/share/man --with-default-path=/etc/timidity++/ \
    --enable-server --enable-alsaseq --enable-spectrogram --enable-network --enable-xft \
    --enable-audio=alsa,oss,ao,vorbis,flac,jack \
    --enable-dynamic=ncurses,tcltk,vt100,xaw,gtk
  make
}

package() {
  cd TiMidity++-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -D -m644 ../timidity.cfg "${pkgdir}/etc/timidity++/timidity.cfg"
  install -D -m644 ../timidity.service "${pkgdir}/usr/lib/systemd/system/timidity.service"
}
