# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=libopencm3
_commit=38b45c87861e5461283609f3aa2df31b141faae6
pkgver=r2548.38b45c87
pkgrel=2
pkgdesc='Open Source firmware library for various ARM Cortex microcontrollers'
arch=('any')
url='http://www.libopencm3.org'
license=('GPL')
depends=('arm-none-eabi-newlib')
makedepends=('git' 'arm-none-eabi-gcc' 'python-yaml')
options=(!strip staticlibs !buildflags)
source=("git://github.com/libopencm3/libopencm3.git#commit=$_commit"
        '0001-Revert-Drop-install-target-and-documentation.patch')
sha512sums=('SKIP'
            '80780290b3de985cee7c950317aa6a2f7d2d920f6520a8b0d2af0df5623be01ccd0d883ac3dc52ae711c4049208906643ec9b391b2e0294fe4d3650fca343c77')

pkgver() {
  cd $pkgname

  printf 'r%s.%s' $(git rev-list --count HEAD) $(git rev-parse --short HEAD)
}

prepare() {
  cd $pkgname

  patch -p1 < ../0001-Revert-Drop-install-target-and-documentation.patch
}

build() {
  cd $pkgname

  make
}

package() {
  cd $pkgname

  make DESTDIR="$pkgdir" PREFIX=/usr/arm-none-eabi install
}

