# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
_pkgname=packer
pkgname=packer-io
pkgver=1.0.0
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('i686' 'x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL2')
makedepends=('git' 'go')
depends=('glibc')
go_checkpoint_commit='a8d0786e7fa88adb6b3bcaa341a99af7f9740671'
go_cleanhttp_commit='3573b8b52aa7b37b9358d966a898feb387f62437'
go_uuid_commit='64130c7a86d732268a38cb04cfbaf0cc987fda98'
osext_commit='ae77be60afb1dcacde03767a8c37337fad28ac14'
panicwrap_commit='fce601fe55579125e1b3cb0b992287e7290f7b83'
cli_commit='b481eac70eea3ad671b7c360a013f89bb759b252'
prefixedio_commit='6e6954073784f7ee67b28f2d22749d6479151ed7'
go_radix_commit='4239b77079c7b5d1243b7b4736304ce8ddb6f0f2'
speakeasy_commit='4aabc24848ce5fd31929f7d1e4ea74d3709c14cd'
go_isatty_commit='fc9e8d8ef48496124e79ae0df75490096eccf6fe'
source=("git+https://github.com/hashicorp/packer#tag=v${pkgver}"
        "git+https://github.com/hashicorp/go-checkpoint#commit=${go_checkpoint_commit}"
        "git+https://github.com/hashicorp/go-cleanhttp#commit=${go_cleanhttp_commit}"
        "git+https://github.com/hashicorp/go-uuid#commit=${go_uuid_commit}"
        "git+https://github.com/kardianos/osext#commit=${osext_commit}"
        "git+https://github.com/mitchellh/panicwrap#commit=${panicwrap_commit}"
        "git+https://github.com/mitchellh/cli#commit=${cli_commit}"
        "git+https://github.com/mitchellh/prefixedio#commit=${prefixedio_commit}"
        "git+https://github.com/armon/go-radix#commit=${go_radix_commit}"
        "git+https://github.com/bgentry/speakeasy#commit=${speakeasy_commit}"
        "git+https://github.com/mattn/go-isatty#commit=${go_isatty_commit}")
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')


prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/mitchellh"
  mkdir -p "src/github.com/hashicorp"
  mkdir -p "src/github.com/kardianos"
  mkdir -p "src/github.com/armon"
  mkdir -p "src/github.com/bgentry"
  mkdir -p "src/github.com/mattn"
  mv ${_pkgname} src/github.com/mitchellh
  mv go-checkpoint src/github.com/hashicorp
  mv go-cleanhttp src/github.com/hashicorp
  mv go-uuid src/github.com/hashicorp
  mv osext src/github.com/kardianos
  mv panicwrap src/github.com/mitchellh
  mv cli src/github.com/mitchellh
  mv prefixedio src/github.com/mitchellh
  mv go-radix src/github.com/armon
  mv speakeasy src/github.com/bgentry
  mv go-isatty src/github.com/mattn

  if [ `uname -m` == 'x86_64' ]; then
    ARCH='amd64'
  elif [ `uname -m` == 'i686' ]; then
    ARCH='i686'
  else
    echo "[ERROR] not supported architecture"
    exit 1
  fi
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/mitchellh/${_pkgname}/Makefile"

}
build() {
  cd "src/github.com/mitchellh/${_pkgname}"
  make dev
}

check() {
  cd "src/github.com/mitchellh/${_pkgname}"
  make test
}

package() {
  cd "${srcdir}/bin/"
  install -Dm755 packer "${pkgdir}/usr/bin/packer-io"
}

# vim:set et sw=2 ts=2 tw=79:
