# Maintainer: Christian Rebischke <Chris.Rebischke[at]archlinux[dot]org>
_pkgname=packer
pkgname=packer-io
pkgver=1.2.3
pkgrel=1
pkgdesc="tool for creating identical machine images for multiple platforms from a single source configuration"
arch=('x86_64')
url="https://github.com/hashicorp/packer"
license=('MPL')
makedepends=('git' 'go-pie' 'go-tools')
depends=('glibc')
source=("${pkgname}-${pkgver}::https://github.com/hashicorp/packer/archive/v${pkgver}.tar.gz")
sha512sums=('36ee75669bf61e14ec733be3946ee6e1f8e265a585e21bdfa98fa6073c6e5bf5a02c7b0b8669632d379e4094d4b7b0fbb06e220865af9c3a89ed258b3166fa58')

prepare() {
  export GOPATH="${srcdir}"
  export PATH="$PATH:$GOPATH/bin"
  mkdir -p "src/github.com/hashicorp"
  mv "${_pkgname}-${pkgver}" "src/github.com/hashicorp/${_pkgname}"
  sed -e '/VersionPrerelease = ""/ {N;N;N;d;}' -i "${srcdir}/src/github.com/hashicorp/${_pkgname}/Makefile"
}
build() {
  cd "src/github.com/hashicorp/${_pkgname}"
  go build -o packer-binary
}

package() {
  cd "src/github.com/hashicorp/${_pkgname}"
  install -Dm755 packer-binary "${pkgdir}/usr/bin/packer-io"
}

# vim:set et sw=2 ts=2 tw=79:
