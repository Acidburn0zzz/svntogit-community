# $Id$
# Maintainer: Aaron Griffin <aaron@archlinux.org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=('lib32-udev')
pkgver=180
pkgrel=1
arch=('x86_64')
pkgdesc="The userspace dev tools (udev) (32-bit)"
url="http://www.kernel.org/pub/linux/utils/kernel/hotplug/udev.html"
license=('GPL')
options=(!makeflags !libtool)
depends=('lib32-glibc' 'lib32-glib2' 'lib32-acl' 'lib32-libusb-compat' 'lib32-kmod' 'lib32-util-linux' 'udev')
makedepends=('gcc-multilib' 'gperf')
source=(ftp://ftp.kernel.org/pub/linux/utils/kernel/hotplug/udev-$pkgver.tar.xz)
md5sums=('1cf124ab5a124ea35d4748ea414926ec')

build() {
  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $srcdir/udev-$pkgver

  ./configure --libdir=/usr/lib32 --disable-introspection
  make
}
  
package() {
  
  cd $srcdir/udev-$pkgver

  make DESTDIR=${pkgdir} install
  rm -rf ${pkgdir}/usr/{etc,include,lib,libexec,sbin,share}
  rm -rf ${pkgdir}/usr/lib32/girepository*
}
