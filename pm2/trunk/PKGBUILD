# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pm2
pkgver=2.9.0
pkgrel=1
pkgdesc='Production process manager for Node.JS applications with a built-in load balancer'
arch=('any')
url='http://pm2.keymetrics.io'
license=('AGPL3')
depends=('nodejs' 'semver')
makedepends=('npm' 'git')
install=pm2.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz
        pm2.service)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('014429869e7bb1e151721800e7d21f12579be286cb7a134236a3123e7f02dbfae601ad771615fdf270d2a31d0939e88837eac5ddbd3ccfa9bbaeebe80e4eaf6f'
            '0d80c16d4694511cc19970a3eb4a2e10f706a58c5cdc554809e12a12093a5b29ff6d30027cbe8646f837a42e40f7d9b7fe2853f96a76a38f47ddc14833f141ef')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  install -Dm644 pm2.service "$pkgdir"/usr/lib/systemd/system/pm2.service
  rm -r "$pkgdir"/usr/etc

  # Why 777? :/
  chmod -R u=rwX,go=rX "$pkgdir"

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}
