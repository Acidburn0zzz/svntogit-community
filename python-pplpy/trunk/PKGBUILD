# Maintainer: Antonio Rojas <arojas@archlinux.org>

_pipname=pplpy
pkgbase=python-pplpy
pkgname=(python2-pplpy python-pplpy pplpy-doc)
pkgver=0.8.4
pkgrel=2
pkgdesc="Python wrapper to the C++ Parma Polyhedra Library (PPL)"
arch=(x86_64)
url="https://gitlab.com/videlec/pplpy/"
license=(LGPL2.1)
makedepends=(cython2 cython python-cysignals python2-cysignals python-gmpy2 python2-gmpy2 ppl python-sphinx)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz")
sha256sums=('b962914d5814edab320d00c11ca396b70c4ce1e67db7a6d59c4b0a3772a16836')

prepare() {
  cp -r $_pipname-$pkgver{,-py2}  
}

build() {
  cd $_pipname-$pkgver
  python setup.py build

  cd ../$_pipname-$pkgver-py2
  python2 setup.py build
}

package_python2-pplpy() {
  depends=(python2-cysignals python2-gmpy2 ppl)

  cd $_pipname-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

package_python-pplpy() {
  depends=(python-cysignals python-gmpy2 ppl)
  
  cd $_pipname-$pkgver 
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  make -C docs html
}

package_pplpy-doc() {
  pkgdesc="HTML documentation for pplpy"
  
  mkdir -p "$pkgdir"/usr/share/doc/pplpy
  cp -r $_pipname-$pkgver/docs/build/html/* "$pkgdir"/usr/share/doc/pplpy
}
