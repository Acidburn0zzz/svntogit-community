# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.4.0
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=('any')
url="https://github.com/aio-libs/aiosmtpd"
license=('Apache')
depends=('python-atpublic' 'python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('cefbc819a51cbdacc8f24d0f5ff4f2a92ad6e4c5809a9b97635226767f1dd20c63565522ab66fd6c7ede5efdafa89c42e3c3bae4e94ea5c57b49531b02e1bad6')
b2sums=('5ae007be6953de8fd804bef25e290153ead6730bf44dbf71448946c1380a972a59b0fcf6f669035dd23acc582d697dc746efe0b03612e6c3ee51b31d58aee309')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v --ignore=aiosmtpd/qa/
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
