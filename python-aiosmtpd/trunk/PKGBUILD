# Maintainer: David Runge <dvzrv@archlinux.org>

_name=aiosmtpd
pkgname=python-aiosmtpd
pkgver=1.3.1
pkgrel=1
pkgdesc="An asyncio based SMTP server"
arch=('any')
url="https://github.com/aio-libs/aiosmtpd"
license=('Apache')
depends=('python-atpublic' 'python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('fafccd8597d2a05356b05a46abd3bb42fe5f401e259fb51078d264e3e96a28a63f8b1d2c4ae2453c00e9cf7fb8dac110b4089844efac7fa7fb7202ffc5ca7ccf')
b2sums=('0e0d27e7c907f3494adcd853c3e60351971e4a0db02903cbfe1131156e230b3118ffa4155783a57fd533de7aeddc57d914a15eab877cad3e945c7730db516815')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v --ignore=aiosmtpd/qa/
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
