# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgname=(python-texttable python2-texttable)
pkgver=0.9.0
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python' 'python2')
checkdepends=('python-pytest' 'python2-pytest')
source=("https://pypi.io/packages/source/t/texttable/texttable-$pkgver.tar.gz")
sha512sums=('cdbaa6f54f1b70be48f1ee685ed43ffc7e6388a50c06ba544748d6d5ecbf3dbbf92803850fa9ec3d8f5675020ebd3cc8ca0b2e4dc72078cbdcddad4a07cd7ef0')

prepare() {
  cp -a texttable-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/texttable-$pkgver
  python setup.py build

  cd "$srcdir"/texttable-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/texttable-$pkgver
  py.test tests.py

  cd "$srcdir"/texttable-$pkgver-py2
  py.test2 tests.py
}

package_python-texttable() {
  depends=('python')

  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-texttable() {
  depends=('python2')

  cd texttable-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
