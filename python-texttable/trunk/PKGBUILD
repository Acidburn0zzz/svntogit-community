# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgname=(python-texttable python2-texttable)
pkgver=0.8.8
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python' 'python2')
source=("https://pypi.io/packages/source/t/texttable/texttable-$pkgver.tar.gz")
sha512sums=('4db089bee787103c00a54dfffcad15a8c299abeb48623c3b179f2a872bcae159a4e7068b2e4ca0efec95e6782b806a5898ad6e57f4290be697d0c8b913446b32')

prepare() {
  cp -a texttable-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/texttable-$pkgver
  python setup.py build

  cd "$srcdir"/texttable-$pkgver-py2
  python2 setup.py build
}

package_python-texttable() {
  depends=('python')

  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-texttable() {
  depends=('python2')

  cd texttable-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
