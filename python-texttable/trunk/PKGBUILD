# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Yen Chi Hsuan <yan12125 at gmail.com>
# Contributor: rich_o <rich_o@lavabit.com>
# Contributor: Jason St. John <jstjohn .. purdue . edu>
# Contributor: Daniel YC Lin <dlin.tw at gmail>

pkgbase=python-texttable
pkgname=(python-texttable python2-texttable)
pkgver=1.5.0
pkgrel=1
pkgdesc="Generate a formatted text table using ASCII characters"
arch=('any')
url="https://pypi.python.org/pypi/texttable/"
license=('LGPL')
makedepends=('python-cjkwrap' 'python2-cjkwrap' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest' 'python2-pytest')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/foutaise/texttable/archive/v$pkgver.tar.gz")
sha512sums=('f4727947a302c8e1bf09ba08d73f79f083304b42f86f2f181a98f44602961e0b280aedd4e38660630671eac17e6eb92403b89c1ae6ea3cdd3cf861c122b904a3')

build() {
  cd texttable-$pkgver
  python setup.py build
  python2 setup.py build
}

check() {
  cd texttable-$pkgver
  pytest tests.py
  pytest2 tests.py
}

package_python-texttable() {
  depends=('python')
  optdepends=('python-cjkwrap: for better CJK support')

  cd texttable-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-texttable() {
  depends=('python2')
  optdepends=('python2-cjkwrap: for better CJK support')

  cd texttable-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
