# Maintainer: David Runge <dave@sleepmap.de>
pkgname=luppp
pkgver=1.1.1
pkgrel=1
pkgdesc="A music creation tool, intended for live use"
arch=('x86_64')
url="http://openavproductions.com/luppp/"
license=('GPL2')
groups=('pro-audio')
depends=('jack' 'liblo' 'libsamplerate' 'libsndfile' 'ntk')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openAVproductions/openAV-${pkgname}/archive/release-${pkgver}.tar.gz")
md5sums=('430a7028610bcfff5bc0922619a7e042')

prepare() {
  mv -v "openAV-Luppp-release-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  mkdir -v bld
}

build() {
  cd "$pkgname-$pkgver/bld"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        ..
  make
}

package() {
  cd "$pkgname-$pkgver/bld"
  make DESTDIR="$pkgdir/" install
  #docs
  install -vDm 644 ../{CHANGELOG,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  # XDG integration
  install -vDm 644 "../resources/metadata/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "../resources/metadata/${pkgname}.appdata.xml" \
    -t "${pkgdir}/usr/share/appdata/"
  install -vDm 644 "../resources/icons/${pkgname}.png" \
    -t "${pkgdir}/usr/share/pixmaps/"
  # controllers
  install -vDm 644 "../resources/controllers/"*.ctlr \
    -t "${pkgdir}/usr/share/${pkgname}/"
}
