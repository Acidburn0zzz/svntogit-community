# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=gammaray
pkgver=2.11.1
pkgrel=3
pkgdesc="A tool for examining the internals of a Qt application and to some extent also manipulate it"
arch=(x86_64)
url="https://www.kdab.com/gammaray/"
license=(GPL)
depends=(syntax-highlighting qt5-tools qt5-svg qt5-3d)
makedepends=(cmake kcoreaddons qt5-script qt5-wayland qt5-webengine qt5-scxml qt5-location qt5-connectivity doxygen glslang)
optdepends=('qt5-wayland: Wayland compositor inspector plugin'
            'qt5-webengine: web inspector plugin'
            'qt5-scxml: state machine viewer plugin'
            'qt5-connectivity: bluetooth plugin'
            'qt5-script: script engine debugger plugin'
            'kcoreaddons: KJob tracker plugin')
source=(https://github.com/KDAB/GammaRay/releases/download/v$pkgver/gammaray-$pkgver.tar.gz{,.asc}
        gammaray-qt5.15a.patch::"https://github.com/KDAB/GammaRay/commit/ec9fe408.patch"
        gammaray-qt5.15b.patch::"https://github.com/KDAB/GammaRay/commit/7cd17bca.patch")
sha256sums=('87a1d72ad1ad6d1a0156c54a85b0976ab38c6a64136458ca7c4ee491566d25d0'
            'SKIP'
            'e98094b3ca0594e069fc08cdbfa520dd53208ccac454255da5e84d6bd39685f2'
            '4fa605325a0e98ff25fea4cd1b46796d1fd1d668a04dbfc28569140a2c8f104d')
validpgpkeys=(E86C000370B1B9E2A9191AD53DBFB6882C9358FB) # KDAB Products <info@kdab.com>

prepare() {
# Fix plugin install dir
  sed -e 's|plugins/gammaray|lib/qt/plugins/gammaray|' -i $pkgname-$pkgver/CMakeLists.txt

  patch -d $pkgname-$pkgver -p1 -i ../gammaray-qt5.15a.patch
  patch -d $pkgname-$pkgver -p1 -i ../gammaray-qt5.15b.patch # Fix build with Qt 5.15
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DGAMMARAY_INSTALL_QT_LAYOUT=ON \
    -DECM_MKSPECS_INSTALL_DIR=/usr/lib/qt/mkspecs/modules \
    -DPLUGIN_INSTALL_DIR=/usr/lib/qt/plugins/gammaray
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
