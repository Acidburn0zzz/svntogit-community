# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=libopusenc
pkgver=0.2
pkgrel=1
pkgdesc="High-level API for encoding .opus files"
arch=('x86_64')
url="https://opus-codec.org/"
license=('BSD')
depends=('glibc' 'opus')
options=('staticlibs')
source=("https://archive.mozilla.org/pub/opus/libopusenc-$pkgver.tar.gz")
#source=("https://ftp.osuosl.org/pub/xiph/releases/opus/libopusenc-$pkgver.tar.gz")
sha256sums=('c79e95eeee43a0b965e9b2c59a243763a8f8b0a7e71441df2aa9084f6171c73a')

build() {
  cd "$pkgname-$pkgver"

  ./configure --prefix="/usr"
  make
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "COPYING" "$pkgdir/usr/share/licenses/libopusenc/COPYING"
  rm -rf "$pkgdir/usr/share/doc"
}
