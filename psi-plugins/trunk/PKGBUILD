# $Id: PKGBUILD 266875 2017-11-15 14:29:11Z foutrelis $
# Maintainer: Sergej Pupykin <arch+pub@sergej.pp.ru>

pkgname=psi-plugins
pkgver=1.3
pkgrel=1
pkgdesc="Additional plugins for Psi"
arch=('x86_64')
url="http://psi-plus.com"
license=('GPL2')
depends=('qt5-base' 'qt5-webkit' 'qt5-multimedia' 'psi' 'qt5-webengine')
conflicts=('psi-plus-plugins-git' 'psi-plus-plugins-qt5-git' 'psi-plus-plugins')
replaces=('psi-plus-plugins')
makedepends=('libotr' 'tidyhtml')
optdepends=('libotr: for OTR plugin'
            'tidyhtml: for OTR plugin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/psi-im/plugins/archive/$pkgver.tar.gz"
        'psiplugin.pri')
sha256sums=('aef310c98c5a43399505a71a6b61f6ad64c97e0529d398462ce669249b4ffb9b'
            'd7753327ab4896f6f7ce2d42b9b7a4f7c62fdf225d34833a629543cb5764969e')

prepare() {
    cp -v psiplugin.pri plugins-$pkgver/
}

build() {
    cd plugins-$pkgver
    # Kill broken plugins
    rm -rf generic/juickplugin
    for plugin in generic/*; do
        if [ -d "$plugin" ]; then
            (cd "$plugin" && qmake-qt5 PREFIX="/usr" QMAKE_STRIP= && make)
        fi
    done
}

package() {
    cd plugins-$pkgver
    for plugin in generic/* unix/*; do
        if [ -d "$plugin" ]; then
            (cd "$plugin" && make INSTALL_ROOT="$pkgdir" install)
        fi
    done
}
