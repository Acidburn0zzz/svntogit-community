# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geopy
pkgname=python-geopy
pkgver=1.22.0
pkgrel=1
pkgdesc="Geocoding library for Python"
arch=('any')
url="https://github.com/geopy/geopy"
license=('MIT')
depends=('python-geographiclib')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest' 'python-pytz'
'python-six')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('18773b2bfc95c4bf26e27a0a1a09de4b57cdae39e67e4f73ff5ccf92c32844f3b517d3a8fa2a027bd1c4cee189182ad3becb0f4fc8773e1375ea2e22d1f726ba'
            'SKIP')
validpgpkeys=('BE3D633AB6792715ECF34D742D3B9C1712FF84F7') # Kostya Esmukov <kostya@esmukov.ru>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
