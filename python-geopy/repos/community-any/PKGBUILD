# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geopy
pkgname=python-geopy
pkgver=1.21.0
pkgrel=1
pkgdesc="Geocoding library for Python"
arch=('any')
url="https://github.com/geopy/geopy"
license=('MIT')
depends=('python-geographiclib')
makedepends=('git' 'python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest' 'python-pytz' 'python-six')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('8060d565d1e86cc3ebea6c5a1f923565f4384faedad478a11dd61ac6c00210a642c2b496802b908bd1bcc12da14f62e0bfc6ce403062205f2d7f39061555f7d5'
            'SKIP')
validpgpkeys=('BE3D633AB6792715ECF34D742D3B9C1712FF84F7') # Kostya Esmukov <kostya@esmukov.ru>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # some endpoint tests fail:
  # https://github.com/geopy/geopy/issues/386
  pytest -v -k 'not test_country_codes_list \
               and not test_country_codes_str \
               and not test_params \
               and not test_reverse \
               and not test_reverse_kind_param \
               and not test_unicode_name'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
