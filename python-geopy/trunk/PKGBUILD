# Maintainer: David Runge <dvzrv@archlinux.org>

_name=geopy
pkgname=python-geopy
pkgver=1.20.0
pkgrel=2
pkgdesc="Geocoding library for Python"
arch=('any')
url="https://github.com/geopy/geopy"
license=('MIT')
depends=('python-geographiclib')
makedepends=('git' 'python-setuptools')
checkdepends=('python-coverage' 'python-mock' 'python-pytest' 'python-pytz' 'python-six')
# sdist on pypi doesn't include tests:
# https://github.com/geopy/geopy/issues/385
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
source=("${pkgname}-${pkgver}::git+https://github.com/${_name}/${_name}#tag=${pkgver}?signed")
sha512sums=('SKIP')
validpgpkeys=('BE3D633AB6792715ECF34D742D3B9C1712FF84F7') # Kostya Esmukov <kostya@esmukov.ru>

# prepare() {
#   mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
# }

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # some endpoint tests fail:
  # https://github.com/geopy/geopy/issues/386
  pytest -v -k 'not test_country_codes_list \
               and not test_country_codes_str \
               and not test_params \
               and not test_reverse \
               and not test_reverse_kind_param \
               and not test_unicode_name'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
