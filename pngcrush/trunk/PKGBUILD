# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>
# Contributor: HuntXu <huntxu@live.cn>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=pngcrush
pkgver=1.7.91
pkgrel=1
pkgdesc="A tool for optimizing the compression of PNG files"
url="http://pmt.sourceforge.net/pngcrush"
arch=('i686' 'x86_64')
license=('custom')
depends=('libpng')
source=(http://downloads.sourceforge.net/pmt/${pkgname}-${pkgver}-nolib.tar.xz
        LICENSE)
sha512sums=('38fc0fdb931f2a6e7ffaee3db500cc0100490309b853edc13e5dc85e645441df98ac1dce3d8bfb548dd143e496089ce7b3b84859d030903b110e9cb49ec66efd'
            'f18613b9da9b4f31a317411e726def5122246087ff57887fba6b46f0a7a7fae7109388ecb2ce049bbe992cc6ef0a3fbbe0f5c5bc816705efd7583836cbd4e69d')

prepare() {
  cd ${pkgname}-${pkgver}-nolib
  sed -r 's| -g||g' -i Makefile
  sed -r 's|^(CFLAGS )=|\1+=|' -i Makefile
  sed -r 's|^(LDFLAGS )=|\1+=|' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}-nolib
  LDFLAGS="${LDFLAGS} -Wl,-z,now -pie" \
    CFLAGS="${CFLAGS} -fPIE" \
    make
}

package() {
  cd ${pkgname}-${pkgver}-nolib
  make DESTDIR="${pkgdir}" install
  install -Dm 644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
