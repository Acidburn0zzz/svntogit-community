# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.40.0
pkgrel=1
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
sha512sums=('83e8382a524699bb8bca03b1262a4943eba9acd0e4855deb3556f03bbc53c643b4697dc7ffb09e9d9d05326d14f4101b793fea7f39bcb68ba10b532873a08daa')
b2sums=('fe469a3bec4ea439bdfc064a770d50e589327e6e02a4611142103e0f49073ab827973afac11856cf281b229d9e820e7df3acb97d2c951191d008a0aa2c427635')

# NOTE: as of 0.40.0, upstream uses upx to reduce binary size, but this strips
# RELRO + PIE, so we do not enable this

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --workspace --features=extra
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --workspace --features=extra
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # remove binaries not present in upstream releases
  rm -f "$pkgdir/usr/bin/table"
  rm -f "$pkgdir/usr/bin/"nu_plugin_{core,extra}_*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
