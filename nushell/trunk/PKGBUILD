# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor:  KokaKiwi <kokakiwi+aur@kokakiwi.net>
# Contributor: Felix Golatofski <contact@xdfr.de>
# Contributor: Bumsik Kim <k.bumsik@gmail.com>

pkgname=nushell
pkgver=0.38.0
pkgrel=1
pkgdesc="A new type of shell"
arch=('x86_64')
url="https://www.nushell.sh"
license=('MIT')
depends=('openssl' 'libxcb' 'libx11' 'curl' 'bzip2')
makedepends=('rust')
install="$pkgname.install"
source=("$pkgname-$pkgver.tar.gz::https://github.com/nushell/nushell/archive/$pkgver.tar.gz")
b2sums=('e8f3cb4a3f84677d949c187fa4304c32f2f2f1f57c7546a42946c6e01abdf6b1147efeb3243a3b934c7ea6ac30b0042dadff9794f811009865a29f65fd3ebef6')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen --workspace --features=extra
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen --workspace --features=extra
}

package() {
  cd "$pkgname-$pkgver"

  # binaries
  find target/release \
    -maxdepth 1 \
    -executable \
    -type f \
    -exec install -vDm755 -t "$pkgdir/usr/bin" "{}" +

  # remove binaries not present in upstream releases
  rm -f "$pkgdir/usr/bin/table"
  rm -f "$pkgdir/usr/bin/"nu_plugin_{core,extra}_*

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" ./*.md
  cp -vr docs/* "$pkgdir/usr/share/doc/$pkgname"

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
