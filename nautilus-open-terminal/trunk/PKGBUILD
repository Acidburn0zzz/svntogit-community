# $Id: PKGBUILD,v 1.15 2009/05/18 16:33:19 wonder Exp $
# Maintainer: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=nautilus-open-terminal
pkgver=0.9
pkgrel=1
pkgdesc="A nautilus plugin for opening terminals in arbitrary local paths"
arch=('i686' 'x86_64')
url="http://manny.cluecoder.org/packages/nautilus-open-terminal"
license=('GPL')
depends=('gconf' 'nautilus' 'gnome-vfs')
makedepends=('pkgconfig' 'gettext' 'intltool')
install=$pkgname.install
source=(ftp://ftp.gnome.org/pub/GNOME/sources/$pkgname/$pkgver/${pkgname}-${pkgver}.tar.gz
	fix-gnome-vfs.patch)
options=('!libtool' 'force')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  #fix gnome-vfs header include
  patch -Np1 -i "$srcdir/fix-gnome-vfs.patch" || return 1

  autoreconf -v || return 1
  libtoolize -v || return 1
  ./configure --prefix=/usr

  #fix non-standard directory
  sed -i 's|${prefix}/etc|/etc|' Makefile

  make || return 1
  make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR="$pkgdir" install

  install -dm755 "$pkgdir/usr/share/gconf/schemas"
  gconf-merge-schema "$pkgdir/usr/share/gconf/schemas/$pkgname.schemas" \
                     --domain $pkgname \
                     "$pkgdir"/etc/gconf/schemas/*.schemas || return 1
  rm -f "$pkgdir"/etc/gconf/schemas/*.schemas
}
md5sums=('576e6d727ce758d1ac1748960d6897d1'
         '9b90557a5a8e300b146fa27db2b1b219')
