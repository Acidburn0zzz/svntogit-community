# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-editor
pkgver=5.9.0.20
pkgrel=1
pkgdesc='Simple editor for Deepin'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-editor"
license=('GPL3')
depends=('deepin-qt5integration' 'syntax-highlighting' 'kcodecs' 'polkit-qt5')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-editor/archive/$pkgver.tar.gz")
sha512sums=('51ea7fbfdbba417d2603de099326f5b7c87474cba202733f3bb53d51e6e543ac5c3b6c7382a029c6ebd2bf0610d6ef90e8018d7874fc9ec38981f437e4901e34')

build(){
  cd deepin-editor-$pkgver
  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
  ninja
}

check() {
  cd deepin-editor-$pkgver
  ninja test || :
}

package() {
  cd deepin-editor-$pkgver
  DESTDIR="$pkgdir" ninja install
}
