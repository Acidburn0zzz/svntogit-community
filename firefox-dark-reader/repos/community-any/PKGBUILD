# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Cookie Engineer <cookiengineer@protonmail.ch>

pkgname=firefox-dark-reader
pkgver=4.9.27
pkgrel=1
pkgdesc='Inverts brightness of web pages and aims to reduce eyestrain while browsing the web'
url=https://darkreader.org/
arch=('any')
license=('MIT')
groups=('firefox-addons')
makedepends=('npm')
source=("https://github.com/darkreader/darkreader/archive/v$pkgver/darkreader-v$pkgver.tar.gz")
b2sums=('2e5e886eb02ec1dccdd6036dcc0d82b0fb777043d516809667658f392b6ec41326ca9dd167fa685a1d114f053cb8ecc256437ebf740b13d0de10c71b64990f70')

build() {
  cd darkreader-$pkgver
  npm install
  npm run build
}

check() {
  cd darkreader-$pkgver
  npm test
}

package() {
  cd darkreader-$pkgver
  install -Dm644 build-firefox.xpi "$pkgdir"/usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi
  install -d "$pkgdir"/usr/lib/thunderbird/extensions
  ln -s /usr/lib/firefox/browser/extensions/addon@darkreader.org.xpi \
    "$pkgdir"/usr/lib/thunderbird/extensions/addon@darkreader.org.xpi
}

# vim:set ts=2 sw=2 et:
