# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lucky-commit
_pkgname=lucky_commit
pkgver=2.0.4
pkgrel=1
pkgdesc="Customize your git commit hashes"
arch=('x86_64')
url="https://github.com/not-an-aardvark/lucky-commit"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('74279160df8290ab35990e84e77f7c0fe0dccfe37aaacf66aa2c81a27c72d2a1f704ccfd1b5c5f7be7edeb9cba4486e5e14b7c926342b4df22b3fc3c5848ca0b')

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --locked --no-default-features
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --release --locked --no-default-features
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$_pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
