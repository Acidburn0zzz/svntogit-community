# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.9.18
pkgrel=1
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=('x86_64')
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('alsa-lib' 'cmake' 'jack' 'liblo' 'libsndfile' 'lv2' 'qt5-tools')
optdepends=('alsa-lib: for standalone application'
            'jack: for standalone application'
            'liblo: for standalone application'
            'new-session-manager: for session management using the standalone application')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('208cf7474e486abf04fdf58dc6afbbbdfc1624a116ef8d20faa913c22d84f81704738c6509db830fee4219471b528594e7936d93b8043d55caa7ef91c8bed6e9')
b2sums=('e30b22529d9b67a1805f17a58c80af399b21f1dc1b52b4c33cf2cb6d37cdcf3ea36edb98cfc3ea9eea85b95703e29f55749b12dbb0dd95b796671c27542940b3')

build() {
  cd "${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX='/usr' \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

package() {
  depends+=('libsndfile.so')
  cd "${pkgname}-${pkgver}"
  make VERBOSE=1 DESTDIR="$pkgdir/" install -C build
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
