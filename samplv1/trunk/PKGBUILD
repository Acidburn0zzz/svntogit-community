# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=samplv1
pkgver=0.8.5
pkgrel=2
pkgdesc="Polyphonic sampler synthesizer with stereo fx"
arch=(x86_64)
url="https://samplv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins')
depends=('jack' 'liblo' 'lv2' 'qt5-base' 'desktop-file-utils'
         'hicolor-icon-theme' 'shared-mime-info')
makedepends=('qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ef6a8d1727c06289cbe247d8e0a4566486f0098c4942ef95f35a473688e32c0298cb0aa89d1eb437d003d162c4c9afa297ea0acd68180c1d932e588d55515619')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}/" install
  install -Dm644 AUTHORS "${pkgdir}/usr/share/doc/${pkgname}/AUTHORS"
  install -Dm644 ChangeLog "${pkgdir}/usr/share/doc/${pkgname}/ChangeLog"
  install -Dm644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm644 TODO "${pkgdir}/usr/share/doc/${pkgname}/TODO"
}

# vim:set ts=2 sw=2 et:
