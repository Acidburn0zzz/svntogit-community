# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-pafy
pkgname=(python-pafy python2-pafy)
pkgver=0.5.3
pkgrel=1

pkgdesc="Python API for YouTube"
url="http://np1.github.io/pafy"
arch=('any')
license=('GPL3')

makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('ffmpeg: fix issues with audio file downloads'
            'youtube-dl: alternative backend')

source=("python-pafy-$pkgver.tar.gz::https://github.com/np1/pafy/archive/v$pkgver.tar.gz")

sha1sums=('5801a80b03c3853ec7794be5e26429bac055db47')

package_python-pafy() {
	depends=('python')

	cd pafy-$pkgver
	PYTHONIOENCODING='UTF-8' python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pafy() {
	depends=('python2')

	cd pafy-$pkgver

	PYTHONIOENCODING='UTF-8' python2 setup.py install --root="$pkgdir" --optimize=1
	rm "$pkgdir"/usr/bin/ytdl
}
