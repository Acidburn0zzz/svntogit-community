# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>

pkgbase=python-pafy
pkgname=(python-pafy python2-pafy)
pkgver=0.5.1
pkgrel=2

pkgdesc="Python API for YouTube"
url="http://np1.github.io/pafy"
arch=('any')
license=('GPL3')

makedepends=('python-setuptools' 'python2-setuptools')
optdepends=('ffmpeg: fix issues with audio file downloads'
            'youtube-dl: alternative backend')

source=("python-pafy-$pkgver.tar.gz::https://github.com/np1/pafy/archive/v$pkgver.tar.gz")

sha1sums=('dee00283ac054941f909981ff8b524965d60bbb3')

package_python-pafy() {
	depends=('python')

	cd pafy-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pafy() {
	depends=('python2')

	cd pafy-$pkgver
	python2 setup.py install --root="$pkgdir" --optimize=1
}
