# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=lens
pkgname=haskell-lens
pkgver=4.15.2
pkgrel=2
pkgdesc="Lenses, Folds and Traversals"
url="http://github.com/ekmett/lens/"
license=("custom:BSD3")
arch=('i686' 'x86_64')
depends=("ghc=8.0.1" "haskell-base-orphans" "haskell-bifunctors" "haskell-comonad"
         "haskell-contravariant" "haskell-distributive" "haskell-exceptions"
         "haskell-free" "haskell-hashable" "haskell-kan-extensions" "haskell-mtl"
         "haskell-parallel" "haskell-profunctors" "haskell-reflection"
         "haskell-semigroupoids" "haskell-semigroups" "haskell-tagged" "haskell-text"
         "haskell-transformers-compat" "haskell-unordered-containers" "haskell-vector"
         "haskell-void")
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('2b958df01c83873e03fa4ba9803449bb564c215480fd16aa64d81a0bdf656b0621c8c15fd4decfee1b694b9494bfc37cf7c686aa63958357d3cdfa62870ec1b4')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid \
            -f-j -ftrustworthy -f-safe -ftest-templates -ftest-properties -ftest-hunit -ftest-doctests -ftest-hlint -f-dump-splices -f-old-inline-pragmas -finlining -f-benchmark-uniplate
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
