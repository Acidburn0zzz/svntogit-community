# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffmpegsource
pkgver=743
pkgrel=2
pkgdesc="A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmpegsource/"
license=('MIT')
depends=('ffmpeg')
makedepends=('svn')
options=('!libtool')
source=('autoconf.patch' 'enable-libavresample.patch')
sha256sums=('b09a7e9a08a16bdaf19d43c7ad8d3ec455f6fecec2f4f5ada417345343adda93'
            '05f03515cc2405cdf8a8ba835f5adc2057f40054a4a1d9e493f0ad512c5de70d')

_svntrunk=http://ffmpegsource.googlecode.com/svn/trunk/
_svnmod=ffmpegsource

build() {
  cd "${srcdir}"

# Checkout
  msg "Connecting to SVN server...."

  if [[ -d ${_svnmod}/.svn ]]; then
    (cd ${_svnmod} && svn up -r ${pkgver})
  else
    svn co ${_svntrunk} --config-dir ./ -r ${pkgver} ${_svnmod}
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting build..."

  rm -rf "${srcdir}"/${_svnmod}-build
# svn export "${srcdir}"/${_svnmod} "${srcdir}"/${_svnmod}-build
  cp -R "${srcdir}"/${_svnmod} "${srcdir}"/${_svnmod}-build
  cd "${srcdir}"/${_svnmod}-build

# Patch
  patch -Np1 -i "${srcdir}"/autoconf.patch
  patch -Np1 -i "${srcdir}"/enable-libavresample.patch

# Build
  ./autogen.sh --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "${srcdir}"/${_svnmod}-build

# Install
  make DESTDIR="${pkgdir}" install

# License
  install -dm 755 "${pkgdir}"/usr/share/licenses/ffmpegsource
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/ffmpegsource/LICENSE
}

# vim: ts=2 sw=2 et:
