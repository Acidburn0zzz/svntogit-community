# $Id$
# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=ffmpegsource
pkgver=755
pkgrel=1
pkgdesc="A libav/ffmpeg based source library and Avisynth plugin for easy frame accurate access"
arch=('i686' 'x86_64')
url="http://code.google.com/p/ffmpegsource/"
license=('MIT')
depends=('ffmpeg')
makedepends=('svn')
options=('!libtool')
source=("${pkgname}::svn+http://ffmpegsource.googlecode.com/svn/trunk/"
        'autoconf.patch')
sha256sums=('SKIP'
            '2cfa7d2e0219a5c8df9a50fe2460a1a1e6b69df10e6d9eee1d723766e62535e7')

pkgver() {
  cd "${SRCDEST}"/${pkgname}

  svnversion | tr -d [A-z]
}

prepare() {
  cd "${srcdir}"/${pkgname}

  patch -Np1 -i ../autoconf.patch
}

build() {
  cd "${srcdir}"/${pkgname}

  ./autogen.sh --prefix=/usr --enable-shared --disable-static
  make
}

package() {
  cd "${srcdir}"/${pkgname}

  make DESTDIR="${pkgdir}" install

  install -dm 755 "${pkgdir}"/usr/share/licenses/ffmpegsource
  install -m 644 COPYING "${pkgdir}"/usr/share/licenses/ffmpegsource/LICENSE
}

# vim: ts=2 sw=2 et:
