# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=flit
pkgbase=python-$_pkgname
pkgname=($pkgbase{,-core})
pkgver=2.3.0
pkgrel=5
pkgdesc='Simplified packaging of Python modules'
arch=('any')
url='https://github.com/takluyver/flit'
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-dephell'
             'python-requests' 'python-docutils' 'python-pytoml')
             #'python-build' 'python-install' 'python-pyproject2setuppy'
checkdepends=('python-pytest-runner' 'python-pytest-cov' 'python-testpath' 'python-responses')
source=("$pkgbase-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('d60ad9ac7d5a0af7a5f8ed095c3f828a714b7f1aa27609e4baa89cd4d9a51da37749bccb9b794c0c3c37cee3cc073b9a251d49b2c18f5b1b2902c7a0838b527c')

prepare() {
  cd $_pkgname-$pkgver

  rm tests/test_installfrom.py

  dephell deps convert --from pyproject.toml --to setup.py

  cd flit_core

  dephell deps convert --from pyproject.toml --to setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build

  cd flit_core

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package_python-flit() {
  depends+=('python-flit-core' 'python-requests' 'python-docutils' 'python-pytoml')
  provides=($_pkgname)
  conflicts=($_pkgname)

  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-flit-core() {
  pkgdesc+=' (core backend)'

  cd $_pkgname-$pkgver/flit_core

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 ../LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

