# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>

pkgname=rebuild-detector
pkgver=4.4.0
pkgrel=1
pkgdesc='Detects which packages need to be rebuilt'
arch=('any')
url="https://github.com/maximbaz/${pkgname}"
license=('ISC')
depends=('parallel' 'pacutils' 'pacman-contrib')
makedepends=('marked-man')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/maximbaz/${pkgname}/archive/refs/tags/${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig::https://github.com/maximbaz/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.sig")
sha256sums=('cb7f649ed856d6e690a9fb9f09899d5b2221b31e663c99a04ca9f2ef82b60f9c'
            'SKIP')
validpgpkeys=('56C3E775E72B0C8B1C0C1BD0B5DB77409B11B601')

prepare() {
    cd "${pkgname}-${pkgver}"
    marked-man -i README.md -o "${pkgname}.7"
    gzip "${pkgname}.7"
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin" checkrebuild
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" LICENSE
    install -Dm644 -t "${pkgdir}/usr/share/libalpm/hooks" "${pkgname}.hook"
    install -Dm644 -t "${pkgdir}/usr/share/man/man7" "${pkgname}.7.gz"
    install -dm755 "${pkgdir}/usr/share/man/man1"
    ln -Tsf "/usr/share/man/man7/${pkgname}.7.gz" "${pkgdir}/usr/share/man/man1/checkrebuild.1.gz"
}

# vim:set ts=4 sw=4 et:
