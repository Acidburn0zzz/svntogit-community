# $Id$
# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=yubico-pam
_shortname=pam_yubico
pkgver=2.25
pkgrel=2
pkgdesc='Yubico YubiKey PAM module'
arch=('x86_64')
url='https://github.com/Yubico/yubico-pam'
license=('BSD')
depends=('pam' 'yubico-c-client' 'yubikey-personalization' 'json-c')
checkdepends=('perl-net-ldap-server')
provides=("${_shortname}")
conflicts=("${_shortname}")
validpgpkeys=('0A3B0262BCA1705307D5FF06BCA00FD4B2168C0A') # Klas Lindfors <klas@yubico.com>
source=("https://developers.yubico.com/${pkgname}/Releases/${_shortname}-${pkgver}.tar.gz"{,.sig}
        '0001-util-make-sure-to-close-the-authfile-before-returnin.patch')
sha256sums=('624a5754d82665f6ba5f2e012f57cb41ea9bf61781ed2272dba49dffb4704e36'
            'SKIP'
            '1b044b779cb08d3a02fea7a62203b494fec621f74a47f27a085b06c22cbe8c88')

prepare() {
	cd "${_shortname}-${pkgver}"

	patch -Np1 < "${srcdir}"/0001-util-make-sure-to-close-the-authfile-before-returnin.patch
}

build() {
	cd "${_shortname}-${pkgver}"

	./configure \
		--prefix=/usr
	make
}

check() {
	cd "${_shortname}-${pkgver}"

	make check
}

package() {
	cd "${_shortname}-${pkgver}"

	install -D -m0644 COPYING "${pkgdir}/usr/share/licenses/yubico-pam/COPYING"
	install -D -m0644 README "${pkgdir}/usr/share/doc/yubico-pam/README"
	make DESTDIR="${pkgdir}/" install
}

