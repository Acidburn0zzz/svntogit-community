# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

_name=elfutils
pkgname=lib32-libelf
pkgver=0.183
pkgrel=1
pkgdesc="Handle ELF object files and DWARF debugging information (libraries) (32-bit)"
arch=('x86_64')
url="https://sourceware.org/elfutils/"
license=('LGPL3' 'GPL3')
depends=('lib32-curl' 'lib32-bzip2' 'lib32-gcc-libs' 'lib32-xz' 'lib32-zlib'
'lib32-zstd' "libelf=${pkgver}")
provides=('libasm.so' 'libdebuginfod.so' 'libdw.so' 'libelf.so')
source=("https://sourceware.org/elfutils/ftp/${pkgver}/elfutils-${pkgver}.tar.bz2"{,.sig})
options=('staticlibs')
sha512sums=('bdafdd738a33cebe4f87849e5e6f1133881e77ef71b27faa0f0234ff80e9674f506957898326c2a55fd8438cbd189f7930597d0b4b9ca6c77921cc0cbd83b6b7'
            'SKIP')
b2sums=('8dd92e7c2df13d51bb51085f6cf762ea1ec7b803dcebf1202a4441ba152ed0e3332b1267276de4650d5ed5c5bed656f72f4c4e9bbf20700be45312a202351574'
        'SKIP')
validpgpkeys=(
  '47CC0331081B8BC6D0FD4DA08370665B57816A6A' # Mark J. Wielaard <mark@klomp.org>
  'EC3CFE88F6CA0788774F5C1D1AA44BE649DE760A' # Mark Wielaard <mjw@gnu.org>
)

build() {
  cd "${_name}-${pkgver}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  CFLAGS+=" -g"  # required for test-suite success

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --libdir=/usr/lib32 \
    --disable-debuginfod
  make
}

check() {
  cd "${_name}-${pkgver}"
  make check || echo "Known problem: The build environment might lack enough resources."
}

package() {
  cd "${_name}-${pkgver}"

  make DESTDIR=${pkgdir} install
  # remove anything that would conflict with elfutils
  rm -rf ${pkgdir}{/etc,/usr/{bin,include,share}}
}
