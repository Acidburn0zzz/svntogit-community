# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dmitry N. Shilov <stormblast@land.ru>

pkgname=luminancehdr
pkgver=2.0.2
pkgrel=1
pkgdesc='Open source graphical user interface application that aims to provide a workflow for HDR imaging'
arch=('i686' 'x86_64')
url='http://qtpfsgui.sourceforge.net/'
license=('GPL')
depends=('exiv2' 'fftw' 'gsl' 'hicolor-icon-theme' 'libraw' 'libtiff' 'openexr' 'qt')
replaces=('qtpfsgui')
install=$pkgname.install
changelog=$pkgname.changelog
options=('!emptydirs')
source=(http://downloads.sourceforge.net/sourceforge/qtpfsgui/luminance-hdr-${pkgver}.tar.gz)
sha256sums=('8a13aff0041fd7f7542af0b6b11e412c01f022d509458c4a701f8ca84bade993')

build() {
  cd ${srcdir}/LuminanceHDR-${pkgver}

  if [ ${CARCH} = i686 ]; then
    sed -i 's/ -msse2//' project.pro
  fi

  lrelease project.pro
  qmake PREFIX=/usr
  make
}

package() {
  cd ${srcdir}/LuminanceHDR-${pkgver}

  make INSTALL_ROOT=${pkgdir} install
}
