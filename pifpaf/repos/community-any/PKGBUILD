# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pifpaf
pkgver=2.5.0
pkgrel=2
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
depends=('python-daiquiri' 'python-click' 'python-pbr' 'python-jinja' 'python-six'
         'python-fixtures' 'python-psutil' 'python-xattr')
provides=("python-pifpaf=$pkgver")
conflicts=('python-pifpaf')
replaces=('python-pifpaf')
makedepends=('python-setuptools')
checkdepends=('python-testrepository' 'python-requests' 'python-testtools' 'python-oslotest'
              'memcached' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb' 'httpbin')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgname-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz"
        pifpaf-httpbin.patch::https://github.com/jd/pifpaf/pull/131.patch)
sha512sums=('fbbe891180cbbf592b90f76a9ab7604187f3c7c5d3fcd3a5848bf740646ae15bb08a704b5bd2ec58a3501ad5ed35c2d027eba026151d702283db67980a20a56d'
            '2b64f9c943d103980eb541b65dd46890ae39da8a80fd89e895963d10efebb1284a386a55d1c7018ec5f42ed3dda30f922ef271a5172189c44949d08b0d32c33c')

export PBR_VERSION=$pkgver

prepare() {
  cd pifpaf-$pkgver
  patch -p1 -i ../pifpaf-httpbin.patch
}

build() {
  cd pifpaf-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.8/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr
}

package() {
  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
