# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7
pkgrel=15
epoch=1
pkgdesc='Script based raytracer for creating 3D graphics'
arch=('x86_64' 'i686')
license=('AGPL3')
url='http://povray.org/'
depends=('libtiff' 'libpng' 'boost-libs' 'openexr')
makedepends=('boost' 'openexr')
backup=("etc/povray/$pkgver/povray.conf"
        "etc/povray/$pkgver/povray.ini")
source=("$pkgname-$pkgver.zip::https://github.com/POV-Ray/povray/archive/3.7-stable.zip"
        'povray3.7.0_rc3-user-conf.patch')
sha256sums=('48d28a88d5e82506fd52ec5b1d9fcde06a39c866d045c53c8c441d0207c753e3'
            '95b04ec4973e6e8a80fc83df2e3caff0b4d6699c4154144f3017d42cab7236e0')

prepare() {
  cd "$pkgname-$pkgver-stable/unix"

  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac
  ./prebuild.sh

  cd ..
  ./bootstrap
  patch -p1 < "$srcdir/povray3.7.0_rc3-user-conf.patch"
}

build() {
  cd "$pkgname-$pkgver-stable"

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    LIBS="-lboost_system -lboost_thread" \
    COMPILED_BY='Arch Linux'
  make \
    CXXFLAGS+="-std=c++03 -w -lboost_system -lboost_thread"
  gzip -f povray.1
}

package() {
  cd "$pkgname-$pkgver-stable"

  install -d "$pkgdir/usr/share/$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  cp -r icons include ini scenes scripts "$pkgdir/usr/share/$pkgname-$pkgver"
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/$pkgver/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/$pkgver/povray.ini"
  install -Dm644 povray.1.gz "$pkgdir/usr/share/man/man1/povray.1.gz"
}

# getver: povray.org/documentation
# vim:set ts=2 sw=2 et:
