# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: red_over_blue
# Contributor: neri

pkgname=povray
pkgver=3.7
pkgrel=3
epoch=1
pkgdesc='Script based raytracer for creating 3D graphics'
arch=('x86_64' 'i686')
license=('AGPL3')
url='http://povray.org/'
depends=('libtiff' 'libpng' 'boost-libs')
makedepends=('boost' 'git')
backup=("etc/povray/$pkgver/povray.conf"
        "etc/povray/$pkgver/povray.ini")
source=("https://github.com/POV-Ray/$pkgname/archive/$pkgver-stable.zip"
        'povray3.7.0_rc3-user-conf.patch'
        'git://github.com/UberPOV/UberPOV.git')
sha256sums=('1261d963327a4b04c09d56bbd7436d0062e6bf8363674e3fb4e240beefb5b5cb'
            '95b04ec4973e6e8a80fc83df2e3caff0b4d6699c4154144f3017d42cab7236e0'
            'SKIP')

prepare() {
  # Fix FS#37689, can be removed for next povray release
  cp -ruv UberPOV/* "$pkgname-$pkgver-stable"

  cd "$pkgname-$pkgver-stable/unix"

  # configure the script that configures configure
  sed 's/automake --w/automake --add-missing --w/g' -i prebuild.sh
  sed 's/dist-bzip2/dist-bzip2 subdir-objects/g' -i configure.ac

  # configure configure
  ./prebuild.sh
  cd ..
  ./bootstrap

  patch -p1 < "$srcdir/povray3.7.0_rc3-user-conf.patch" ||  true
}

build() {
  cd "$pkgname-$pkgver-stable"

  # just configure
  ./configure LIBS="-lboost_system -lboost_thread" COMPILED_BY='Arch Linux' \
    --prefix=/usr --sysconfdir=/etc

  # compile
  make CXXFLAGS+="-w -lboost_system -lboost_thread"

  # compress the man page
  gzip -f povray.1
}

package() {
  cd "$pkgname-$pkgver-stable"

  install -d "$pkgdir/usr/share/$pkgname-$pkgver"
  install -d "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  cp -r icons include ini scenes "$pkgdir/usr/share/$pkgname-$pkgver"
  cp -r scripts "$pkgdir/usr/share/$pkgname-$pkgver" || true
  cp -r doc "$pkgdir/usr/share/doc/$pkgname-$pkgver"
  install -Dm755 unix/povray "$pkgdir/usr/bin/povray"
  install -Dm644 povray.conf "$pkgdir/etc/povray/$pkgver/povray.conf"
  install -Dm644 povray.ini "$pkgdir/etc/povray/$pkgver/povray.ini"
  install -Dm644 povray.1.gz "$pkgdir/usr/share/man/man1/povray.1.gz"
}

# vim:set ts=2 sw=2 et:
