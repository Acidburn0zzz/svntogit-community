# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Romain Gautier <romain.gautier@nimamoh.com>

pkgname=kotlin
pkgver=1.0.5
pkgrel=1
pkgdesc='The Kotlin programming language'
arch=('any')
url='http://kotlinlang.org/'
license=('apache' 'custom')
makedepends=('setconf')
depends=('bash' 'java-environment>=6')
source=("https://github.com/JetBrains/kotlin/releases/download/v$pkgver/kotlin-compiler-$pkgver.zip")
sha256sums=('e45fcf6d55268c8c354d9d959b58a378bd5d344f1f3f9132b17da6d40433d9f6')

prepare() {
  cd "${pkgname}c/bin"

  setconf kotlin DIR /usr/bin
  setconf kotlinc KOTLIN_HOME "/usr/share/$pkgname"
  setconf kotlinc-js DIR /usr/bin
  setconf kotlinc-jvm DIR /usr/bin
}

package() {
  cd "${pkgname}c"

  msg2 'Packaging executables...'
  install -Dm755 bin/kotlinc "$pkgdir/usr/bin/kotlinc"
  install -Dm755 bin/kotlin "$pkgdir/usr/bin/kotlin"
  install -Dm755 bin/kotlinc-js "$pkgdir/usr/bin/kotlinc-js"
  install -Dm755 bin/kotlinc-jvm "$pkgdir/usr/bin/kotlinc-jvm"

  msg2 'Packaging jar files...'
  cd "$srcdir/${pkgname}c/lib"
  install -d "$pkgdir/usr/share/$pkgname/lib"
  for jar in *.jar; do
    install -Dm644 "$jar" "$pkgdir/usr/share/$pkgname/lib"
  done

  msg2 'Packaging licenses...'
  cd "$srcdir/${pkgname}c/license"
  install -d "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm644 NOTICE.txt "$pkgdir/usr/share/licenses/$pkgname"
  cd third_party
  for txt in *.txt; do
    install -Dm644 "$txt" "$pkgdir/usr/share/licenses/$pkgname"
  done
}

# vim:set ts=2 sw=2 et:
