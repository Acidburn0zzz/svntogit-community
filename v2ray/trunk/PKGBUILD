# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=3.48
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.zip::https://github.com/v2ray/v2ray-core/releases/download/v$pkgver/src_all.zip"
        "v2ray-ext-$pkgver.tar.gz::https://github.com/v2ray/ext/archive/v$pkgver.tar.gz"
        'v2ray.service')
sha512sums=('f9ccc67c18a3087050b8090b6014b0d6dd9fd34ff701bb98b7e081fd7cae2a62611bd3d43e440d2e235b9ce1e5156baa326bcc11e6d8906068f6d6dd76065131'
            'b31d9ac3273d23be4942763c2df63e622426f35f08c2227f82d29b2460bf65d79c1d6c8c0a2db0184ef121a3510c4e98e623db432e86e1fe3728adb1579e4aec'
            'bebb87f43b3920be8efde9aece9c4305323cd7d3a65a3b2441f0dbd0098c02a18ca89a5b08833eee70482cffae2c8e7b7237bb5a4d1076e3c30d273898e2bedb')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p "$GOPATH"/src

  mv *.com *.io *.org "$GOPATH"/src/
}

build() {
  go build -o v2ray v2ray.com/core/main
  go build -o v2ctl v2ray.com/ext/tools/control/main
}

check() {
  cd "$GOPATH"/src/v2ray.com/core
  go test -p 1 -tags json -v v2ray.com/core/...
}

package() {
  cd "$GOPATH"/src/v2ray.com/core
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service ${pkgdir}/usr/lib/systemd/system/v2ray.service
  sed -i '/ExecStart/c\ExecStart=/usr/bin/env v2ray.location.asset=/etc/v2ray /usr/bin/v2ray -config /etc/v2ray/config.json' "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/geoip.dat release/config/geosite.dat release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 "$srcdir"/v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 "$srcdir"/v2ctl -t "$pkgdir"/usr/bin/

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
