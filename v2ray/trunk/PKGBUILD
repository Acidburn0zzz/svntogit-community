# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.0
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
makedepends=('go' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.zip::https://github.com/v2ray/v2ray-core/releases/download/v$pkgver/src_all.zip"
        "v2ray-ext-$pkgver.tar.gz::https://github.com/v2ray/ext/archive/v$pkgver.tar.gz"
        'v2ray.service')
sha512sums=('af16a2869b3c82b52fc80d5edec7a6653ddab15a86cf3597fe09eb931bc1d37fd245a1e1b5fac61abfda11a4d81648beb2fa94f5338900197f299ada7b806589'
            '521ae508ad221125f02f9b1116ffe11d3451e513474796e08ad2678a65af157241694c656423919b1d13cf1ba3f7d3b274ed9a2b04eaa82a51e45a00efd848e7'
            'bebb87f43b3920be8efde9aece9c4305323cd7d3a65a3b2441f0dbd0098c02a18ca89a5b08833eee70482cffae2c8e7b7237bb5a4d1076e3c30d273898e2bedb')

prepare() {
  export GOPATH="$srcdir/build"
  mkdir -p "$GOPATH"/src

  mv *.com *.io *.org "$GOPATH"/src/
}

build() {
  go build -o v2ray v2ray.com/core/main
  go build -o v2ctl v2ray.com/ext/tools/control/main
}

check() {
  cd "$GOPATH"/src/v2ray.com/core
  go test -p 1 -tags json -v v2ray.com/core/...
}

package() {
  cd "$GOPATH"/src/v2ray.com/core
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service ${pkgdir}/usr/lib/systemd/system/v2ray.service
  sed -i '/ExecStart/c\ExecStart=/usr/bin/env v2ray.location.asset=/etc/v2ray /usr/bin/v2ray -config /etc/v2ray/config.json' "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/geoip.dat release/config/geosite.dat release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 "$srcdir"/v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 "$srcdir"/v2ctl -t "$pkgdir"/usr/bin/

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
