# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: pandada8 <pandada8@gmail.com>

pkgname=v2ray
pkgver=4.21.3
pkgrel=1
pkgdesc="A platform for building proxies to bypass network restrictions"
arch=('x86_64')
url="https://github.com/v2ray/v2ray-core"
license=('MIT')
depends=('glibc' 'v2ray-domain-list-community' 'v2ray-geoip')
makedepends=('go-pie' 'golang-golang-x-crypto' 'golang-golang-x-net' 'git')
backup=(etc/v2ray/config.json)
source=("$pkgname-$pkgver.tar.gz::https://github.com/v2ray/v2ray-core/archive/v$pkgver.tar.gz"
        'v2ray.service')
sha512sums=('a5daf8bd50849f09da584c2c278ca3abdacad0a14cf2426938ae2d609b53c974713019da4dd3187dfada95ed72dbe47dd7a0eb3b73ac5eff028099393b19ea80'
            '37efc20ef71147f4400eba2bf57766462b5803b5952768bd32d5224f35e37c9c5639aaa19e0f3b63b52063dafd0381f40e2bfbb60dace08b055f0b3245cfa2b7')

prepare() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/v2ray.com

  # mv *.com *.io *.org *.net "$srcdir"/build/src/
  mv v2ray-core-$pkgver "$srcdir"/build/src/v2ray.com/core

  # Future makedepends
  go get github.com/golang/protobuf/proto go.starlark.net/starlark go.starlark.net/syntax \
         google.golang.org/grpc github.com/gorilla/websocket github.com/refraction-networking/utls

  # Future checkdepends
  go get github.com/golang/mock/gomock github.com/google/go-cmp/cmp \
         golang.org/x/sync/errgroup github.com/miekg/dns h12.io/socks
}

build() {
  go build -trimpath \
    -ldflags "-extldflags ${LDFLAGS}" \
    -o v2ray v2ray.com/core/main

  go build -trimpath \
    -ldflags "-extldflags ${LDFLAGS}" \
    -o v2ctl v2ray.com/core/infra/control/main
}

check() {
  cd "$srcdir"/build/src/v2ray.com/core
  go test -p 1 -tags json -v -timeout 30m v2ray.com/core/...
}

package() {
  cd "$srcdir"/build/src/v2ray.com/core
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/v2ray/LICENSE
  install -Dm644 release/config/systemd/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray.service
  sed -i -e '/ExecStart/i Environment=V2RAY_LOCATION_ASSET=/etc/v2ray' \
         -e 's|/usr/bin/v2ray/v2ray|/usr/bin/v2ray|' \
         "$pkgdir"/usr/lib/systemd/system/v2ray.service
  install -Dm644 release/config/*.json -t "$pkgdir"/etc/v2ray/
  install -Dm755 "$srcdir"/v2ray -t "$pkgdir"/usr/bin/
  install -Dm755 "$srcdir"/v2ctl -t "$pkgdir"/usr/bin/

  install -Dm644 "$srcdir"/v2ray.service "$pkgdir"/usr/lib/systemd/system/v2ray@.service
}
