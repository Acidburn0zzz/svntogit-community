# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Felix Fung <fylixeoi@gmail.com>

pkgname=timescaledb
pkgver=2.2.1
pkgrel=1
pkgdesc="An open-source time-series database optimized for fast ingest and complex queries"
arch=('x86_64')
url="https://www.timescale.com/"
license=('Apache')
depends=('postgresql' 'postgresql-libs')
makedepends=('gcc' 'cmake')
install=timescaledb.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/timescale/timescaledb/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('5dae0ed0b71309598bfc9605e838f2d8b4179ccf7098396315153c4044c7bdaaa411fd11ee70ca7807afde4b609faab281dcb1aa2f8d65330ca95e13fbe24582')

build() {
    cd "${pkgname}-${pkgver}"
    ./bootstrap -DWARNINGS_AS_ERRORS=OFF -DREGRESS_CHECKS=OFF
    cd build && make
}

package() {
    cd "${pkgname}-${pkgver}/build"
    make DESTDIR="$pkgdir/" install
}
