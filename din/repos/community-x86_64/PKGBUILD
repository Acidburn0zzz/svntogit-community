# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=50.1
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-48-devendor_rtaudio_rtmidi.patch"
)
sha512sums=('c860c70bb9f3c3db66e05dd9f9048daecab1b0c9d0588fde901ce26723e291b56d23086f792180fb5fc688165e4ed7efdc302d0d3d02efa913be5f2d4a90f5eb'
            'bea57324b517605b793b15329b7e5b9ffc855b906b611800b38d8482829e467e462c28eb7ebf789ece2216e7ed677f47e29643f3bd09f67c6bf6510fc57d2cca')
b2sums=('3d73f31f91a57188b63f1f5fc9a68923c4fb6d9a14cbfd602510d0006ff4079ab60865e49118a02e46381aebd4cc4ee9415172bac724f8491655326147b6466b'
        '3b8965fa7e6e71772b4228fbdaaab4ebb4e0024d0a4ba5ec0e416fb4276bc5b0d358f16cfa93083bf5648ddcfb7401e301f2220c6b1549f71dc48003cc1af832')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-48-devendor_rtaudio_rtmidi.patch"
  rm -fv src/{RtAudio,RtMidi}.*
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -D__UNIX_JACK__ $(pkg-config --cflags rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
