# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=33
pkgrel=2
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl' 'sdl' 'tcl')
makedepends=('boost' 'glu')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz")
sha512sums=('df94b16fcfe1d95f824e4c0937935db1efaff6705912f4f629ce4b55ada7c9118d56dd3dce48fd9a73c6bbe2b0918c72113cffad42f3bd387bfd2accf5f1a4cb')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  CXXFLAGS="${CXXFLAGS} -O3 -D__UNIX_ALSA__ -D__UNIX_JACK__" \
  CFLAGS="${CFLAGS} -O3" \
  ./configure --prefix='/usr'
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # move desktop file and icon to correct directories
  # change of the default location has been requested upstream
  install -vdm 755 \
    "${pkgdir}/usr/share/"{applications,icons/hicolor/scalable/apps}
  mv -v "${pkgdir}/usr/share/${pkgname}/pixmaps/${pkgname}.desktop" \
    "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  mv -v "${pkgdir}/usr/share/${pkgname}/pixmaps/${pkgname}.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
