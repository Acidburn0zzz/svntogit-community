# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=47.1
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'jack' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-47-rtaudio_rtmidi.patch"
)
sha512sums=('883008fa54751cd626ee60a5c5a4785a82680c229adefc2c8a86bfa28bf6be6549500c1bfe4661028ae8a2015383a2d9e6fe1d8992476d49576a999eea26ba17'
            'f553dd0c55f08468d1c33e7231630358b6f1a257f380d2208167e8831cecef23b3b1e64aacddaaea87e233c7def55cf0605782fafe6011d07146403adb120357')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-47-rtaudio_rtmidi.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs jack rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('libjack.so' 'librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
