# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=47
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'sdl' 'tcl')
makedepends=('boost' 'glu' 'jack' 'rtaudio' 'rtmidi')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-47-rtaudio_rtmidi.patch"
)
sha512sums=('900aadf8a214585b5dd7b56b02da579410d305893a53e433e8dd000a3892027c19dfa867c775a4eb0e85e879b0a578cf46a3183083c468de53d47d44a43614b8'
            'f553dd0c55f08468d1c33e7231630358b6f1a257f380d2208167e8831cecef23b3b1e64aacddaaea87e233c7def55cf0605782fafe6011d07146403adb120357')

prepare() {
  cd "${pkgname}-${pkgver}"
  # use system rtaudio/rtmidi, instead of vendored versions
  patch -Np1 -i "../${pkgname}-47-rtaudio_rtmidi.patch"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  export CXXFLAGS="${CXXFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export CFLAGS="${CFLAGS} -O3 $(pkg-config --cflags jack rtaudio rtmidi)"
  export LIBS="${LIBS} $(pkg-config --libs jack rtaudio rtmidi)"
  ./configure --prefix='/usr'
  make
}

package() {
  depends+=('libjack.so' 'librtaudio.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
