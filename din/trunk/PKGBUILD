# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: muzhed <chustokes@126.com>

pkgname=din
pkgver=35
pkgrel=1
pkgdesc="A sound synthesizer and musical instrument."
arch=('x86_64')
url="https://dinisnoise.org/"
license=('GPL2')
groups=('pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl' 'sdl' 'tcl')
makedepends=('boost' 'glu')
source=("https://archive.org/download/dinisnoise_source_code/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a3f16996eab7362624e6bb6156843173c6d5e5662843a4502920aedff4c81efa34723d07b8982973cc99ff5e0c945efeb1b66d7e52e14f92cf26d817b8a5a309')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  CXXFLAGS="${CXXFLAGS} -O3 -D__UNIX_ALSA__ -D__UNIX_JACK__" \
  CFLAGS="${CFLAGS} -O3" \
  ./configure --prefix='/usr'
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {AUTHORS,BUGS,CHANGELOG,NEWS,README,TODO}
}
