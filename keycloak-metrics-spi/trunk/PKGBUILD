# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.0.0
pkgrel=1
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('608b33f8934c8f31b5e47ab8919debea286d66616313ce02527d15e3006c665f44dc09f90fb1ec34fb39818fe2542039c8b2c8e963ffc9814a5ae42e7be48e16')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}
  install -dm 751 ${pkgdir}/opt/keycloak/standalone
  install -dm 755 ${pkgdir}/opt/keycloak/standalone/deployments
  install -Dm644 build/libs/keycloak-metrics-spi-*-SNAPSHOT.jar ${pkgdir}/opt/keycloak/standalone/deployments/
}
