# Maintainer: Jelle van der Waa <jelle@archlinux.org>

pkgname=keycloak-metrics-spi
pkgver=2.2.0
_jarversion=2.2.1
pkgrel=1
pkgdesc='A Service Provider that adds a Prometheus metrics endpoint to Keycloak'
url="https://github.com/aerogear/keycloak-metrics-spi"
arch=('any')
license=('Apache')
depends=('java-runtime>=8')
makedepends=('java-environment=8' 'gradle' 'keycloak')
source=($pkgname-$pkgver.tar.gz::https://github.com/aerogear/keycloak-metrics-spi/archive/${pkgver}.tar.gz)
sha512sums=('4e26a33e4d4788ea538f9759100b98dfefdee113fd099023d1253575e217a01340715fd9854537b4f2a2ab6059b48726c961084f49bf3b453261630e6701bc82')

build() {
  cd ${pkgname}-${pkgver}
  gradle jar
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/opt/keycloak/standalone/deployments/"
  install -Dm644 "build/libs/keycloak-metrics-spi-${_jarversion}-SNAPSHOT.jar" "${pkgdir}/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar"
  ln -s "/usr/share/java/${pkgname}/keycloak-metrics-spi-${pkgver}.jar" "${pkgdir}/opt/keycloak/standalone/deployments/keycloak-metrics-spi-${pkgver}.jar"
}
