# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=alembic
pkgver=1.7.10
pkgrel=1
pkgdesc="A open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD')
depends=('openexr' 'boost-libs' 'hdf5')
makedepends=('cmake' 'boost')
source=(https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('e98ffaedb98dbc5c53fe9703d3063bb118d32c83c47e3af04c8fc96237034b02fe0fc2c628ca82bdd0e0ef17d9375f4f48e0022ce33380b9ad91970539611ced')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=OFF
    # Temporarily disable HDF5 until upstream fixes the linking or until we make a patch.

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
