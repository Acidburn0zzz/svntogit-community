# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=alembic
pkgver=1.7.7
pkgrel=1
pkgdesc="A open framework for storing and sharing scene data"
arch=('x86_64')
url="http://www.alembic.io/"
license=('BSD3')
depends=('openexr' 'boost-libs' 'hdf5' 'zlib')
makedepends=('cmake' 'boost')
source=(https://github.com/alembic/alembic/archive/${pkgver}.tar.gz)
sha512sums=('0ebcf6b9304e84bc60f1c146d0b5e5c5b1de43974ec0725293c444b48b22640945f5883eb9afd46c1ac9f0c260d6f22ff29b4866d6525c416339877be984b149')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  mkdir build && cd build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DUSE_HDF5=OFF
    # Temporarily disable HDF5 until upstream fixes the linking or until we make a patch.

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  cd build
  make DESTDIR="${pkgdir}/" install
}

# vim:set ts=2 sw=2 et:
