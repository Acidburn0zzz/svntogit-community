# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor : Vassilis Palassopoulos <palasso [at] gmail [dot] com>

pkgname=rkward
pkgver=0.7.1.b
_pkgver=0.7.1b
pkgrel=1
pkgdesc="An easy to use and easily extensible IDE/GUI for R"
url="https://rkward.kde.org/"
arch=(x86_64)
license=(GPL)
depends=(kdewebkit ktexteditor qt5-script r)
makedepends=(extra-cmake-modules kdoctools)
source=("https://download.kde.org/stable/rkward/$_pkgver/src/$pkgname-$_pkgver.tar.gz")
sha256sums=('0cca23b106f569549a005110eeecb2557805049e6659c3a2ebd5eb18e392ac76')

build() {
  cmake -B build -S $pkgname-$_pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  rm "$pkgdir"/usr/share/org.kde.syntax-highlighting/syntax/r.xml # provided by syntax-highlighting
}
