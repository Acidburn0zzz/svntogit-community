# $Id$
# Maintainer: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy.carrier@aur.archlinux.org>
# Contributor: Erol V. Aktay <e.aktay@gmail.com>

pkgname=python-webpy
pkgver=0.35
pkgrel=1
pkgdesc="A simple but powerful web framework for python"
arch=('any')
url="http://webpy.org/"
license=('unknown')
depends=('python2')
optdepends=('python-flup: deploy web applications via FastCGI (recommended)'
            'mod_wsgi: deploy web applications via an Apache module'
            'python2-psycopg2: use PostgreSQL as the backing database'
            'mysql-python: use MySQL as the backing database'
            'python-mako: alternative templating engine')
source=("$pkgname-$pkgver.tar.gz::https://github.com/webpy/webpy/tarball/webpy-$pkgver")

build() {
  # Shamelessly borrowed from the yajl PKGBUILD:
  local _dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$srcdir/$_dirname"
  python2 setup.py build 
}

package() {
  local _dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$srcdir/$_dirname"
  python2 setup.py install --root="$pkgdir" --optimize=1
  find "$pkgdir" -name '*.py' -print0 |xargs -0 \
    sed -i -e 's,^#!/usr/bin/env python$,#!/usr/bin/env python2,' \
    -e 's,^#!/usr/bin/python$,#!/usr/bin/python2,'
}
md5sums=('f24202afdc78e552ade46bcce9a9a704')
