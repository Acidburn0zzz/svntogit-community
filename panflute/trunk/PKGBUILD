# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Owain Sutton <owainsutton @ gmail dot com>

pkgname=panflute
pkgver=0.7.0
pkgrel=1
pkgdesc="Music player interface and panel applet - successor to music-applet"
arch=('i686' 'x86_64')
url="https://launchpad.net/panflute"
license=('GPL')
depends=('dbus-python' 'gnome-python' 'python-notify' 'pygtk' 'python-numpy' 'python-gnomeapplet')
makedepends=('perlxml' 'gnome-doc-utils>=0.18' 'intltool' 'gnome-python-desktop' 'python-mpd')
optdepends=('python-mpd: for mpd support')
install=panflute.install
source=(http://launchpad.net/${pkgname}/0.7/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('3499e5e7beb9ee737898a7f1ccff9cb2')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
  	--sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/panflute \
	--disable-schemas-install

  make
  make DESTDIR="${pkgdir}" install

  sed -i "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find ${pkgdir}/usr/lib/python2.7/site-packages/panflute/ -name '*.py') \
    ${pkgdir}/usr/lib/panflute/panflute-applet \
    ${pkgdir}/usr/bin/panflute-{daemon,launch-player} \
    ${pkgdir}/usr/bin/panflute-debugger \
    ${pkgdir}/usr/bin/panflute-tests

  install -m755 -d "${pkgdir}"/usr/share/gconf/schemas
  gconf-merge-schema "${pkgdir}"/usr/share/gconf/schemas/${pkgname}.schemas --domain ${pkgname} \
 	"${pkgdir}"/etc/gconf/schemas/*.schemas

  rm -rf "${pkgdir}"/etc/gconf
}
