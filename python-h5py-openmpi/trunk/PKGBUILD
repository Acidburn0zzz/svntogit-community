# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Joey Dumont <joey.dumont@gmail.com>

_pkg=h5py
_mpi=openmpi
pkgname=python-${_pkg}-${_mpi}
pkgver=3.0.0
pkgrel=1
pkgdesc="General-purpose Python bindings for the HDF5 library (${_mpi} version)"
arch=(x86_64)
url="https://www.h5py.org/"
license=(BSD)
depends=(hdf5-${_mpi} liblzf python-numpy python-mpi4py)
makedepends=(cython python-pkgconfig)
checkdepends=(inetutils python-pytest python-pytest-mpi python-pytables)
conflicts=(python-h5py)
provides=(python-h5py)
source=(https://files.pythonhosted.org/packages/source/h/${_pkg}/${_pkg}-${pkgver}.tar.gz
        h5py-system-lzf.patch::https://github.com/h5py/h5py/commit/da96e2b8b9f73fc67c128c9b65787b3527be119c.patch
        h5py-py3.8-cached_property.patch::https://github.com/h5py/h5py/commit/a87984f0bc2b2596948880d2ec38196017e275fc.patch)
sha256sums=('7d3803be1b530c68c2955faba726dc0f591079b68941a0c0269b5384a42ab519'
            '665c15ffc714788749f4e0d731775cb927593fed496097e4abf2174612d9a747'
            '4d5cdf7af4ea5d79ec0ac39f178ef55fe6f571efb76a008e553c4b3030730372')
validpgpkeys=(AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7 # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>
              96B7334D7610EE3E68AFFE589E027116943D6A8B) # Thomas A Caswell <tcaswell@bnl.gov> (new key)
# See https://github.com/h5py/h5py/issues/1299 about lack of GPG sigs for recent releases

prepare() {
  cd ${_pkg}-${pkgver}
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" setup_build.py
  # Build against system lzf
  patch -Np1 -i ../h5py-system-lzf.patch
  # cached_property is included in python 3.8
  patch -Np1 -i ../h5py-py3.8-cached_property.patch
}

build() {
  export CC=mpicc
  cd ${_pkg}-${pkgver}
  HDF5_MPI="ON" H5PY_SYSTEM_LZF=1 python setup.py build
}

check() {
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')
  PYTHONPATH="$PWD/${_pkg}-${pkgver}/build/lib.linux-$CARCH-${python_version}" python -m pytest --pyargs h5py -rxXs --with-mpi

}

package() {
  cd ${_pkg}-${pkgver}
  HDF5_MPI="ON" python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 licenses/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
