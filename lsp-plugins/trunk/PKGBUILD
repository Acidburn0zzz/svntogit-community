# Maintainer: David Runge <dave@sleepmap.de>
pkgname=lsp-plugins
pkgver=1.1.7
pkgrel=1
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=('x86_64')
url="https://lsp-plug.in"
license=('LGPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'glu' 'jack' 'libglvnd')
makedepends=('ladspa' 'lv2' 'php')
source=("https://github.com/sadko4u/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('e98000c98464abc1db75e044bc1b7adb535b94fbe1025a52baf0a2c92bcd21039c9b330e790f0607b4747356aff3f7d4ae20e666a9135108e875057c5459c8ae')

prepare() {
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # removing /usr/local/lib from RPATH
  sed -e '/LD_PATH/ s|:/usr/local/lib||g' \
      -i scripts/make/configure.mk
}

build() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  make
  make OBJDIR=${_test_path} clean
  make OBJDIR=${_test_path} test
}

check() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  ${_test_path}/lsp-plugins-test utest \
    --nofork \
    --debug \
    --verbose || warning "Known flaky test: https://github.com/sadko4u/lsp-plugins/issues/19"
}

package() {
  cd "$pkgname-$pkgver"
  make BIN_PATH=/usr/bin \
       LIB_PATH=/usr/lib \
       DOC_PATH=/usr/share/doc \
       DESTDIR="$pkgdir/" \
       install
}
