# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lsp-plugins
pkgver=1.1.22
pkgrel=1
pkgdesc="Collection of free plugins compatible with LADSPA, LV2 and LinuxVST"
arch=('x86_64')
url="https://lsp-plug.in"
license=('LGPL3')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('cairo' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libglvnd' 'libx11')
makedepends=('jack' 'ladspa' 'libsndfile' 'lv2' 'php')
optdepends=('jack: for standalone applications')
checkdepends=('lv2lint')
source=("https://github.com/sadko4u/${pkgname}/archive/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6004136b55a990033d22e60af6c418a5ee708aaf0f4263fc3af8097876f7eea6e125a44392468ed8e30a92c0fa44eb65f40d80b25a5f243d3fd4f1c0f3af70d6')

prepare() {
  mv -v "$pkgname-$pkgname-$pkgver" "$pkgname-$pkgver"
}

build() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  make
  make OBJDIR="${_test_path}" clean
  make OBJDIR="${_test_path}" test
}

check() {
  local _test_path="$(pwd)/.build-test"
  cd "$pkgname-$pkgver"
  "${_test_path}/lsp-plugins-test" utest \
    --nofork \
    --debug \
    --verbose || echo "Known flaky test: https://github.com/sadko4u/lsp-plugins/issues/19"
}

package() {
  depends+=('libsndfile.so')
  cd "$pkgname-$pkgver"
  make PREFIX='/usr' \
       DESTDIR="$pkgdir/" \
       install
}
