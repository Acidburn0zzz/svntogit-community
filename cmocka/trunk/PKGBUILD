# $Id$
# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>

pkgname=cmocka
url='http://cmocka.org/'
pkgver=1.0.1
pkgrel=1
pkgdesc='an elegant unit testing framework for C with support for mock objects'
arch=(i686 x86_64)
license=(Apache)
depends=(glibc)
makedepends=(cmake)
source=(https://cmocka.org/files/1.0/cmocka-$pkgver.tar.{xz,asc})
validpgpkeys=('8DFF53E18F2ABC8D8F3C92237EE0FC4DCC014E3D') # Andreas Schneider <asn@cryptomilk.org>
sha1sums=('1f9baabbac3d8b777e83d5c84b64954d019dea34'
          'SKIP')

build() {
  rm -rf build; mkdir build
  cd build
  cmake ../$pkgname-$pkgver -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DUNIT_TESTING=ON
  make
}

check() {
  cd build
  make test
}

package() {
  cd build
  make install DESTDIR="$pkgdir"
}
