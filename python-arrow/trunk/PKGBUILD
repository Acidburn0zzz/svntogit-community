# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgbase=python-arrow
pkgname=(
  python-arrow
  python2-arrow
)
pkgver=0.14.2
pkgrel=1
pkgdesc='Better dates and times for Python'
arch=(any)
url=https://arrow.readthedocs.io
license=(Apache)
makedepends=(
  git
  python-dateutil
  python-setuptools
  python2-dateutil
  python2-setuptools
  python2-backports.functools_lru_cache
)
checkdepends=(
  python-chai
  python-nose
  python-pytz
  python-simplejson
  python2-chai
  python2-nose
  python2-pytz
  python2-simplejson
)
source=(git+https://github.com/crsmithdev/arrow.git#tag=${pkgver})
sha256sums=('SKIP')

prepare() {
  cp -r {,python-}arrow
  mv {,python2-}arrow
}

build() {
  for py in python{,2}; do
    pushd ${py}-arrow
    ${py} setup.py build
    popd
  done
}

check() {
  cd python-arrow
  nosetests
  nosetests2
}

package_python-arrow() {
  depends=(
    python
    python-dateutil
  )

  cd python-arrow

  python setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

package_python2-arrow() {
  depends=(
    python2
    python2-backports.functools_lru_cache
    python2-dateutil
  )

  cd python2-arrow

  python2 setup.py install --skip-build --root="${pkgdir}" --optimize='1' --skip-build
}

# vim: ts=2 sw=2 et:
