# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Tatsuyuki Ishi <ishitatsuyuki@gmail.com>

pkgname=python-arrow
pkgver=0.15.5
pkgrel=1
pkgdesc='Better dates and times for Python'
arch=(any)
url=https://arrow.readthedocs.io
license=(APACHE)
depends=(python-dateutil)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-chai
  python-dateparser
  python-mock
  python-nose
  python-pytz
  python-simplejson
)
source=(git+https://github.com/crsmithdev/arrow.git#tag=c856ab9dad4901551f2bf7012f6a9dc2779091db)
sha256sums=(SKIP)

pkgver() {
  cd arrow

  git describe --tags
}

build() {
  cd arrow

  python setup.py build
}

check() {
  cd arrow

  nosetests
}

package() {
  cd arrow

  python setup.py install --skip-build --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
