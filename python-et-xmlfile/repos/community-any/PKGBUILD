# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>

pkgbase=python-et-xmlfile
pkgname=('python2-et-xmlfile' 'python-et-xmlfile')
pkgver=1.0.1
pkgrel=4
pkgdesc="A low memory library for creating large XML files."
arch=('any')
url="https://et-xmlfile.readthedocs.io/"
license=('MIT')
makedepends=('python2-setuptools' 'python-setuptools' 'python-lxml' 'python2-lxml')
source=("https://pypi.python.org/packages/source/e/et_xmlfile/et_xmlfile-${pkgver}.tar.gz"
        "LICENCE")
sha1sums=('a3e502098bcb7fff22857882d80813c1b6db7608'
          '8b67150a9c68fa385ebe71453141020860a72ff4')

prepare() {
  cd "$srcdir"
  cp -a et_xmlfile-${pkgver} et_xmlfile-py2-${pkgver}
  cd et_xmlfile-py2-${pkgver}

  sed -e "s|#![ ]*/usr/bin/python$|#!/usr/bin/python2|" \
      -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
      -e "s|#![ ]*/bin/env python$|#!/usr/bin/env python2|" \
      -i $(find . -name '*.py')
}

build() {
  # "Building Python2"
  cd "$srcdir"/et_xmlfile-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/et_xmlfile-${pkgver}
  python setup.py build
}

package_python2-et-xmlfile() {
  depends=('python2' 'python2-lxml')

  cd "$srcdir"/et_xmlfile-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

package_python-et-xmlfile() {
  depends=('python' 'python-lxml')

  cd "$srcdir"/et_xmlfile-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 "$srcdir"/LICENCE "$pkgdir"/usr/share/licenses/$pkgname/LICENCE
}

