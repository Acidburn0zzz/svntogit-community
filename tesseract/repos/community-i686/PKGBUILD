# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andreas Hauser <andy-aur@splashground.de>

_langs=(afr ara aze bel ben bul cat ces chi_sim chi_tra chr dan deu ell
eng enm epo epo_alt equ est eus fin fra frk frm glg grc heb hin hrv hun
ind isl ita ita_old jpn kan kor lav lit mal mkd mlt msa nld nor pol por
ron rus slk slv spa spa_old sqi srp swa swe tam tel tgl tha tur ukr vie)

pkgname=tesseract
pkgver=3.05.00
pkgrel=1
pkgdesc="An OCR program"
arch=(i686 x86_64)
#url="http://code.google.com/p/tesseract-ocr"
url="https://github.com/tesseract-ocr/tesseract"
license=("APACHE")
depends=(libpng libtiff libjpeg zlib giflib gcc-libs leptonica)
makedepends=(icu cairo pango)
install=tesseract.install
optdepends=('icu' 'cairo' 'pango'
	    $(for l in ${_langs[@]}; do echo tesseract-data-${l}; done))
source=($pkgname-$pkgver.tar.gz::https://github.com/tesseract-ocr/tesseract/archive/$pkgver.tar.gz
	https://github.com/tesseract-ocr/tessdata/raw/master/osd.traineddata)
sha256sums=('3fe83e06d0f73b39f6e92ed9fc7ccba3ef734877b76aa5ddaaa778fac095d996'
            '9cf5d576fcc47564f11265841e5ca839001e7e6f38ff7f7aacf46d15a96b00ff')

build() {
  cd "$srcdir"/$pkgname-${pkgver}
  [ -x configure ] || ./autogen.sh
  [ -f Makefile ] || ./configure --prefix=/usr
  make
  make training
}

package() {
  cd "$srcdir"/$pkgname-${pkgver}
  make DESTDIR="$pkgdir" install
  make DESTDIR="$pkgdir" training-install
  mkdir -p "$pkgdir"/usr/share/tessdata
  install -Dm0644 "$srcdir"/osd.traineddata "$pkgdir"/usr/share/tessdata/osd.traineddata
}
