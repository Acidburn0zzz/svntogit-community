# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-artwork
pkgver=0.114
pkgrel=1
pkgdesc="Sugar icons and themes"
arch=('x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
makedepends=('gtk2' 'gtk3' 'icon-naming-utils' 'python' 'xorg-xcursorgen')
optdepends=('gtk2: GTK+ 2 theme'
            'gtk3: GTK+ 3 theme')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('0d9c63bf861d6facf8d64046ab76191a6a6b4b54daad778b40e3be1e45aa2db0')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
