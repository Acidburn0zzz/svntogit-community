# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: eric <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>

pkgname=stow
pkgver=2.2.2
pkgrel=5
pkgdesc='Manage installation of multiple softwares in the same directory tree'
url='https://www.gnu.org/software/stow/'
arch=('any')
license=('GPL2')
depends=('perl')
makedepends=('texi2html')
checkdepends=('perl-test-output' 'perl-io-stringy')
validpgpkeys=('942B9075ACCA04E9037C73FED31B5563DAC1D4FA')
source=("https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"{,.sig})
md5sums=('8712d22b7abe26b1bef0e22818981248'
         'SKIP')

build() {
  cd $pkgname-$pkgver
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --infodir=/usr/share/info \
    --with-pmdir=/usr/share/perl5/vendor_perl
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # remove html doc
  rm -r "$pkgdir/usr/share/doc/$pkgname"/{manual*,version.texi}
}

# vim:set ts=2 sw=2 et:
