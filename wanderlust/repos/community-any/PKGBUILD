# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Norberto Lopes <shelika@mail.telepac.pt>

pkgname=wanderlust
pkgver=20180409
_commit=afecc04e806c8338e553bdbdb716328fa9f2227a
pkgrel=1
pkgdesc="Mail/News reader supporting IMAP4rev1 for emacs."
arch=(any)
url="https://github.com/wanderlust/wanderlust/"
license=('GPL')
depends=('emacs' 'emacs-apel>=10.7' 'flim' 'semi')
makedepends=('git')
#optdepends=('bbdb: contact management utility')
install=wanderlust.install
source=("wanderlust::git://github.com/wanderlust/wanderlust.git#commit=${_commit}")
md5sums=('SKIP')

package() {
  cd "$srcdir"/wanderlust
  PIXMAPDIR=/usr/share/emacs/`emacs -batch -eval "(princ (format \"%d.%d\" emacs-major-version emacs-minor-version))"`/etc/wl/icons/
  install -dm0755 "$pkgdir"/usr/share/info
  make all info
  make install install-info \
	LISPDIR="$pkgdir"/usr/share/emacs/site-lisp \
	PIXMAPDIR="$pkgdir"$PIXMAPDIR \
	INFODIR="$pkgdir"/usr/share/info
  install -m644 utils/ssl.el "$pkgdir"/usr/share/emacs/site-lisp/ssl.el
}
