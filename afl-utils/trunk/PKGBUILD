# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=afl-utils
pkgver=1.31a
pkgrel=1
pkgdesc="Utilities for automated crash sample processing/analysis, easy afl-fuzz job management and corpus optimization"
url="https://github.com/rc0r/afl-utils"
arch=('any')
license=('Apache')
depends=('afl' 'coreutils' 'python' 'rsync')
optdepends=(
  'sqlite: database support'
  'screen: interactive/screen mode support'
  'gdb: gdb script execution support'
)
makedepends=('python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rc0r/${pkgname}/archive/v${pkgver}.tar.gz)
sha512sums=('3d4095ea85179ed8cb27ed720db09d28b4cbe461ffa55605e488efab4f991bb5939967b7968d6d93e19b50b3af45d5be6be714f70e7ca8da31ab067c37da3308')

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
  install -Dm 644 docs/CHANGELOG.md "${pkgdir}/usr/share/doc/${pkgname}/CHANGELOG.md"
  install -Dm 644 config/* -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
