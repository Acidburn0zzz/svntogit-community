# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-structlog
pkgname=(python-structlog python2-structlog)
pkgver=18.1.0
pkgrel=2
pkgdesc="Structured Logging for Python"
url="http://www.structlog.org"
license=('Apache')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-six' 'python2-six')
checkdepends=('python-pytest' 'python2-pytest' 'python-freezegun' 'python2-freezegun'
              'python-pretend' 'python2-pretend' 'python-simplejson' 'python2-simplejson'
              'python-twisted' 'python2-twisted' 'python-rapidjson' 'python-greenlet'
              'python2-greenlet')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/hynek/structlog/archive/$pkgver.tar.gz")
sha512sums=('01ea560f16979312acd168c08994007e1116de470cf933a1524033884058114999f815cf3061b2d080c95453c9b628ed7747ea5d283bb2fcda7180edd977c09b')

prepare() {
  cp -a structlog-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/structlog-$pkgver
  python setup.py build

  cd "$srcdir"/structlog-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/structlog-$pkgver
  PYTHONPATH="$PWD"/build/lib pytest

  cd "$srcdir"/structlog-$pkgver-py2
  PYTHONPATH="$PWD"/build/lib pytest2
}

package_python-structlog() {
  depends=('python-six')

  cd structlog-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-structlog() {
  depends=('python2-six')

  cd structlog-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
