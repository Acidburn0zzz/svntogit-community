# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: xRemaLx <anton.komolov@gmail.com>

pkgname=perl-module-scandeps
_pkgname=Module-ScanDeps
pkgver=1.26
pkgrel=1
pkgdesc="Scan file prerequisites"
arch=('any')
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
depends=('perl')
makedepends=('perl-test-requires')
url="http://search.cpan.org/dist/Module-ScanDeps"
source=("http://search.cpan.org/CPAN/authors/id/R/RS/RSCHUPP/Module-ScanDeps-$pkgver.tar.gz")
sha512sums=('b4b54813f55b82c20073ede9be90ab3680a636c3c8743ae03ccd0d7a26097f3d05b3e55d8b0d7d414e4156eaa686dc6eec0541ebe6c89a5c896dcd98845eb10f')

build() {
  cd Module-ScanDeps-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Module-ScanDeps-$pkgver
  make test
}

package() {
  cd Module-ScanDeps-$pkgver
  make DESTDIR="$pkgdir" install
}
