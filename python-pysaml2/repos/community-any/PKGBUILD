# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>

pkgname=python-pysaml2
pkgver=6.4.1
pkgrel=1

pkgdesc='Python implementation of SAML Version 2'
url='https://pypi.python.org/pypi/pysaml2'
arch=('any')
license=('Apache')

depends=('python'
         'python-future'
         'python-requests'
         'python-zope-interface'
         'python-pycryptodomex'
         'python-pytz'
         'python-pyopenssl'
         'python-dateutil'
         'python-six'
         'python-defusedxml'
         'xmlsec')
makedepends=('python-setuptools')

source=("https://pypi.org/packages/source/p/pysaml2/pysaml2-$pkgver.tar.gz")

md5sums=('9a17a52d1df96cb9e41f35f68e7bc0bb')

build() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py build
}

check() {
	cd "$srcdir"/pysaml2-$pkgver
	python setup.py test
}

package() {
	cd pysaml2-$pkgver
	python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim: set ts=4 sw=4 tw=0 ft=PKGBUILD :
