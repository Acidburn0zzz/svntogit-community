# Maintainer: David Runge <dvzrv@archlinux.org>

_name=hsluv
pkgname=python-hsluv
pkgver=5.0.1
pkgrel=1
pkgdesc="A Python implementation of HSLuv (revision 4)"
arch=('any')
url="https://github.com/hsluv/hsluv-python"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('9f51273aee3979430d75422c52a5deec527677e525f5870ee31e2ba5f86e8bc6dc3a891e9e4cd07d217e4b22cb82915d196fdc6ca488a56459a0c70399bb7f73')
b2sums=('e613210e5066fa6b5c776e455c9378de5f6ca9a73a53ff9c3620b0cbe30a7c40347a3046c233dd215e93c530431fbb1aa693a54ae9515ac614381a4504ef7a41')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
