# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Felix Kaiser <felix.kaiser@fxkr.net>

pkgbase=python-pytest
pkgname=('python-pytest' 'python2-pytest')
pkgver=3.2.2
pkgrel=1
pkgdesc="Simple powerful testing with Python"
arch=('any')
license=('MIT')
url="http://pytest.org/"
makedepends=('python-setuptools' 'python2-setuptools' 'python-py' 'python2-py' 'python-pluggy'
             'python2-pluggy')
checkdepends=('lsof' 'python-nose' 'python2-nose' 'python-mock' 'python2-mock' 'python-tox'
              'python2-tox' 'python-yaml' 'python2-yaml' 'python-pytest-xdist'
              'python2-pytest-xdist' 'python-twisted' 'python2-twisted' 'python-requests'
              'python2-requests' 'python-hypothesis' 'python2-hypothesis')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/pytest-dev/pytest/archive/$pkgver.tar.gz")
sha512sums=('e61c059b2df3cb1f8002a956195abd08a8293396a874cb95700eb049b9631e171d5b1e44ae95e734dc2150e781ba9cb94d37ce16512524fa7a816b40b1578b8d')

prepare() {
  rm -r pytest-$pkgver/_pytest/vendored_packages
  sed -i "s/'_pytest.vendored_packages'//" pytest-$pkgver/setup.py
  sed -i 's/assert numentries == 0/assert numentries == 26/' pytest-$pkgver/testing/python/collect.py

  cp -a pytest-$pkgver{,-py2}

  export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py build

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytest-$pkgver
  python setup.py test

  cd "$srcdir"/pytest-$pkgver-py2
  python2 setup.py test
}

package_python-pytest() {
  depends=('python-py' 'python-setuptools' 'python-pluggy')

  cd pytest-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-pytest() {
  depends=('python2-py' 'python2-setuptools' 'python2-pluggy')

  cd pytest-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/py.test{,2}
  mv "$pkgdir"/usr/bin/pytest{,2}
}
