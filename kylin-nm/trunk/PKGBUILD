# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=kylin-nm
pkgver=1.2.4
pkgrel=1
pkgdesc="Kylin network monitor used in ubuntu-kylin operation system"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/kylin-nm"
groups=('ukui')
depends=('gsettings-qt' 'kwindowsystem' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/kylin-nm/archive/v$pkgver.tar.gz")
sha512sums=('95b1794efc367a2dd499311e48fede14d56cc59ed76183b7846fd136ca617c03c1c3428b2569fde5eaffcadf36818efa704e0e7b4344318f088004cebea68625')

prepare() {
  cd kylin-nm-$pkgver
  sed -i '/#include <QPainter>/a #include <QPainterPath>' wireless-security/kylinheadfile.h src/confform.h src/utils.h
  sed -i '/#include <QTimer>/a #include <QPainterPath>' src/notifysend.h
  sed -i '/dpkg-buildflags/d' kylin-nm.pro
}

build() {
  cd kylin-nm-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd kylin-nm-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
