# $Id: PKGBUILD,v 1.19 2009/07/15 11:22:14 ibiru Exp $
# Maintainer: Andrea Scarpino <bash.lnx@gmail.com>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=tagpy
pkgver=0.94.5
pkgrel=9
pkgdesc="Python bindings for TagLib"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/tagpy"
license=('MIT')
depends=('taglib' 'boost')
makedepends=('setuptools')
source=(http://pypi.python.org/packages/source/t/$pkgname/$pkgname-$pkgver.tar.gz)
md5sums=('84d7862786ad7bab91d0d45ded15a875')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  sed -i 's|boost_python-gcc42-mt|boost_python-mt|' setup.py

  ./configure --taglib-lib-dir=/usr/lib/ \
  	--taglib-inc-dir=/usr/include/taglib/ \
	--boost-inc-dir=/usr/include/boost/ \
	--boost-lib-dir=/usr/lib/ \
	--boost-python-libname=boost_python-mt
  python setup.py build || return 1
  python setup.py install --root="$pkgdir"

  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
