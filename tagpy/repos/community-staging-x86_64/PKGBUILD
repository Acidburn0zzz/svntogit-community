# $Id$
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Callan Barrett <wizzomafizzo@gmail.com>
# Contributor: Scott Horowitz <stonecrest@gmail.com>

pkgname=tagpy
pkgver=0.94.8
pkgrel=2
pkgdesc="Python bindings for TagLib"
arch=('i686' 'x86_64')
url="http://pypi.python.org/pypi/tagpy"
license=('MIT')
depends=('python2' 'taglib' 'boost-libs')
makedepends=('python2-distribute' 'boost')
source=("http://pypi.python.org/packages/source/t/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('6baff63318cf90b9bc5a2497a0597802')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  sed -i -e "s|#![ ]*/usr/bin/env python$|#!/usr/bin/env python2|" \
    $(find . -name '*.py') 
 
  ./configure.py --taglib-lib-dir=/usr/lib/ \
  	--taglib-inc-dir=/usr/include/taglib/ \
	--boost-inc-dir=/usr/include/boost/ \
	--boost-lib-dir=/usr/lib/ \
	--boost-python-libname=boost_python-mt
  python2 setup.py build
}

package(){
  cd ${srcdir}/${pkgname}-${pkgver}
  python2 setup.py install --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/$pkgname/LICENSE"
}
