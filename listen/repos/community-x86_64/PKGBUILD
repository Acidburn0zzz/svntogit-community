# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Evangelos Foutras <foutrelis@gmail.com>
# Contributor: Hugo Doria <hugodoria@gmail.com>

pkgname=listen
pkgver=0.6.5
pkgrel=7
pkgdesc="Music player and management for GNOME"
arch=('i686' 'x86_64')
url="http://www.listen-project.org/"
license=('GPL2')
depends=('python2-egg' 'python2-libgnome'   'mutagen'
         'gnome-icon-theme' 'gstreamer0.10-python' 'notification-daemon' 'pyxdg'
         'pywebkitgtk' 'pyinotify' 'gstreamer0.10-good-plugins' 'dbus-python')
makedepends=('gettext' 'pkgconfig' 'intltool' 'docbook2x' 'docbook-xml')
optdepends=('python-musicbrainz2: musicbrainz & cd support'
            'libgpod: iPod support'
            'python-daap: DAAP support'
            'tunepimp: tagging support')
source=("http://download.listen-project.org/0.6/${pkgname}-${pkgver}.tar.gz")
md5sums=('76cd9ad2fc9c600dbdf4808c15aeccf1')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  chmod +x mmkeys/setup.py

  # Fix command name
  sed -i 's/docbook2x-man/docbook2man/' Makefile

  # Point Python scripts to the python2 binary
  sed -i -e 's/python -/python2 -/' \
         -e 's/PYTHON  = python$/\02/' \
    Makefile
  sed -i 's/env python$/env python2/' mmkeys/setup.py

  make CHECK_DEPENDS=0 PREFIX=/usr
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} PREFIX=/usr install
}

# vim:set ts=2 sw=2 et:
