# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=iasl
pkgver=20110211
pkgrel=1
pkgdesc="Intel ACPI Source Language compiler"
arch=('i686' 'x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
install=iasl.install
source=(http://acpica.org/download/acpica-unix-${pkgver}.tar.gz
	remove-duplicated-objectfile.patch
	LICENSE)
md5sums=('ee688e36b84e468a9d8b5b9ec356a11b'
         '43acc96af956ce2f98673dff7fadf999'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd "${srcdir}/acpica-unix-${pkgver}"

  # fix linking, merged upstream
  # see http://www.acpica.org/bugzilla/show_bug.cgi?id=907
  patch -Np1 -i ../remove-duplicated-objectfile.patch

  cd compiler
  make

  cd ../tools/acpisrc
  make


  #cd ../acpiexec
  #make
  #install -m755 acpiexec "${pkgdir}/usr/bin"

  cd ../acpixtract
  make
}

package() {
  cd "${srcdir}/acpica-unix-${pkgver}"

  install -D -m755 compiler/iasl "${pkgdir}/usr/bin/iasl"
  install -m755 tools/acpisrc/acpisrc "${pkgdir}/usr/bin"
  install -m755 tools/acpixtract/acpixtract "${pkgdir}/usr/bin"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
