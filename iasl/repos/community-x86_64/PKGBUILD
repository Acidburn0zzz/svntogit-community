# $Id$
# Maintainer : Laurent Carlier <lordheavym@gmail.com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alessio 'mOlOk' Bolognino <themolok@gmail.com>

pkgname=iasl
pkgver=20110922
pkgrel=2
pkgdesc="Intel ACPI Source Language compiler"
arch=('i686' 'x86_64')
url="http://acpica.org"
license=('custom')
depends=('glibc')
makedepends=('flex' 'bison')
options=('!makeflags')
install=iasl.install
source=(http://acpica.org/download/acpica-unix-${pkgver}.tar.gz
	LICENSE)
md5sums=('b95af67bb277657ae01844db01f2f9c7'
         '8615526144f69ea4e870d9bc37df9b29')

build() {
  cd "${srcdir}/acpica-unix-${pkgver}"

  # fix building with gcc-4.6
  sed -i -e 's/-Werror//g' compiler/Makefile tools/acpisrc/Makefile

  cd compiler
  make

  cd ../tools/acpisrc
  make


  #cd ../acpiexec
  #make
  #install -m755 acpiexec "${pkgdir}/usr/bin"

  cd ../acpixtract
  make
}

package() {
  cd "${srcdir}/acpica-unix-${pkgver}"

  install -D -m755 compiler/iasl "${pkgdir}/usr/bin/iasl"
  install -m755 tools/acpisrc/acpisrc "${pkgdir}/usr/bin"
  install -m755 tools/acpixtract/acpixtract "${pkgdir}/usr/bin"
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
