# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# ardour, liblrdf and raptor are heavily dependent on each other. Updating of
# one package mostly will require an update of all the other packages. I vote
# for fixed dependencies speaking in terms of versions
pkgname=ardourvst
_realname=ardour
pkgver=2.8.12
pkgrel=1
pkgdesc="Ardour with win32 VST support"
arch=('i686')
url="http://ardour.org/"
license=('GPL')
depends=('liblrdf>=0.4.0-8' 'liblo' 'aubio' 'libusb-compat'
         'libgnomecanvasmm' 'soundtouch' 'slv2>=0.6.6-6' 'wine')
         # -rubberband -libgnomecanvas(+libgnomecanvasmm)
         # +soundtouch (these +/- changes are for SYSLIBS)
         # liblrdf versioned for https://bugs.archlinux.org/task/25005
         # slv2 versioned for https://bugs.archlinux.org/task/25060
makedepends=('scons' 'boost' 'pkg-config' 'cwiid')
optdepends=('cwiid: Wiimote support')
conflicts=('ardour')
provides=('ardour')
source=("ftp://ftp.archlinux.org/other/ardour/$_realname-$pkgver.tar.bz2"
        "$pkgname.desktop"
        gcc46.patch
        raptor2.patch)
sha256sums=('f51514910f7734823a13b35823bbdc9d709252bad65c65b34c17efe1d3975be4'
            '3ba74b58348abfe6bc59aff7dc778404b26aaca809c0e1f705d4b2c0fef027e4'
            '074b43c7d609c88f093ca124016fd0b12e34a385387447c0125a49bb36c81c19'
            '24e08a1f696e341ac8b741af3fb5cd9a820b9864ba5fac39b749385935799af3')

build() {
  cd "$srcdir/$_realname-$pkgver"

  # gcc 4.6 compatibility
  # see http://tracker.ardour.org/view.php?id=3771
  patch -Np1 -i "$srcdir/gcc46.patch"

  # raptor2 compatibility
  # see https://bugs.archlinux.org/task/25060
  # http://tracker.ardour.org/view.php?id=4179
  patch -Np0 -i "$srcdir/raptor2.patch"

  sed -i '/-O3/d' SConstruct

  # use syslibs until internal sigc++ issue w/ latest gcc (?) resolves
  # TODO: report upstream
  scons "$MAKEFLAGS" ARCH="$CFLAGS" \
                     PREFIX="/usr" \
                     FREEDESKTOP=0 \
                     FREESOUND=1 \
                     WIIMOTE=1 \
                     VST=1 \
                     SYSLIBS=1 \
                     DESTDIR="$pkgdir"
}

package() {
  cd "$srcdir/$_realname-$pkgver"

  scons PREFIX="/usr" \
        FREEDESKTOP=0 \
        FREESOUND=1 \
        WIIMOTE=1 \
        VST=1 \
        SYSLIBS=1 \
        DESTDIR="$pkgdir" install

  # install some freedesktop.org compatibility
  install -Dm644 "$srcdir/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}

# vim:set ts=2 sw=2 et:
