# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Christoph Drexler <chrdr at gmx dot at>

pkgname=leptonica
pkgver=1.74.2
pkgrel=1
pkgdesc="Software that is broadly useful for image processing and image analysis applications"
arch=('i686' 'x86_64')
url="http://www.leptonica.com/"
license=('custom')
depends=('giflib' 'libjpeg' 'libpng' 'libtiff' 'zlib' 'libwebp')
source=("$pkgname-$pkgver.tar.gz::https://github.com/DanBloomberg/leptonica/archive/$pkgver.tar.gz")
sha256sums=('8b2e9bacd23597d0ee21625a7d8443177b1cb85daf196d86b67ad75044e29c27')

build() {
  cd "$srcdir"/leptonica-${pkgver}
  ./autobuild
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir"/leptonica-${pkgver}
  make DESTDIR="$pkgdir" install
  install -D leptonica-license.txt "$pkgdir"/usr/share/licenses/leptonica/leptonica-license.txt
}
