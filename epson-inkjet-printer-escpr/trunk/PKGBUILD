# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Contributor:  Hyacinthe Cartiaux <hyacinthe.cartiaux@free.fr>
# Contributor: James An <james@jamesan.ca>
# Contributor: Stefano Bergamini <bergs at live dot it>
# Contributor: Heiko Baums <heiko@baums-on-web.de>
# Contributor: Quentin Foussette <quentinf7@gmail.com>
# Contributor: MetaNova

pkgname=epson-inkjet-printer-escpr
pkgver=1.6.32
pkgrel=1
pkgdesc='Epson Inkjet Printer Driver (ESC/P-R) for Linux'
arch=('x86_64')
url='http://download.ebz.epson.net/dsc/search/01/search/?OSC=LX'
license=('GPL2')
depends=('cups' 'ghostscript')
source=("https://download3.ebz.epson.net/dsc/f/03/00/08/49/68/d26ae8059b62e89a2c3602b5b7caa5d57fda35c5/epson-inkjet-printer-escpr-$pkgver-1lsb3.2.tar.gz"
        'bug_x86_64.patch')
sha512sums=('e6b128d1e5f9353f13c74bdf812699a253597c86e1ff791bafa1c7c73b4c789be95148647ca75cbeda04f2ccd7ecd87f3016808c404e07871c27f5218b4586f2'
            'ff4f736ec884c4d2354a83c89ee535732cb414ef0c6443bdc8ec869f889f486b242d76e143ad59d61b182231697dfe3bdf68122cb8aae873a9776ce109a07dab')

prepare() {
  cd $pkgname-$pkgver

  patch -p1 < ../bug_x86_64.patch
}

build() {
  cd $pkgname-$pkgver

  ./configure \
  	--prefix=/usr \
  	--with-cupsfilterdir=/usr/lib/cups/filter \
  	--with-cupsppddir=/usr/share/ppd

  make
}

package() {
  cd $pkgname-$pkgver

  make DESTDIR="$pkgdir" install
}

