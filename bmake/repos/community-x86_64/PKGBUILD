# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Alex Szczuczko <alex at szc dot ca>
# Contributor: Devin Cofer <ranguvar{AT]archlinux[DOT}us>
# Contributor: Imanol Celaya <ilcra1989@gmail.com>

pkgname=bmake
pkgver=20131001
pkgrel=1
pkgdesc="Portable version of the NetBSD 'make' build tool"
arch=('i686' 'x86_64')
url="http://www.crufty.net/help/sjg/bmake.html"
license=('BSD')
depends=('bmake-mk-files')
source=("http://www.crufty.net/ftp/pub/sjg/bmake-$pkgver.tar.gz")
sha256sums=('0a91ac60b82270c4b4f21a43e66eb51c32b4df55e01ea592e42eb323909a559b')

build() {
    rm -rf bmake-build
    mkdir bmake-build
    cd bmake-build
    ../bmake/configure --prefix=/usr --with-default-sys-path=/usr/share/mk
    sh make-bootstrap.sh
}

package() {
    install -Dm755 bmake-build/bmake "$pkgdir"/usr/bin/bmake
    install -Dm644 bmake/bmake.1 "$pkgdir"/usr/share/man/man1/bmake.1
}
