# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=plasma5-applets-window-buttons
pkgname=(libappletdecoration plasma5-applets-window-buttons)
pkgver=0.8.1
pkgrel=3
pkgdesc="Plasma 5 applet in order to show window buttons in your panels"
arch=(x86_64)
url="https://github.com/psifidotos/applet-window-buttons"
license=(GPL)
makedepends=(extra-cmake-modules kdecoration plasma-framework)
source=($pkgname-$pkgver.tar.gz::"https://github.com/psifidotos/applet-window-buttons/archive/$pkgver.tar.gz"
        window-buttons-kdecoration-5.21.patch::"https://github.com/psifidotos/applet-window-buttons/commit/dc5ed862.patch")
sha256sums=('9f7afca910072d3e6b23e9aad1bd0905965b2022e0498b43173d7a712cca8986'
            '56ad08f1fcb612bf3aade5047cc346d4560e422946ffeeed962a48c749aa2e73')

prepare() {
  patch -d applet-window-buttons-$pkgver -p1 < window-buttons-kdecoration-5.21.patch # Fix build with kdecoration 5.21
}

build() {
  cmake -B build -S applet-window-buttons-$pkgver
  cmake --build build
}

package_libappletdecoration() {
  pkgdesc="Library for applet window decorations"
  depends=(kdecoration plasma-framework)
  DESTDIR="$pkgdir" cmake --install build/libappletdecoration
}

package_plasma5-applets-window-buttons() {
  depends=(libappletdecoration plasma-workspace)
  DESTDIR="$pkgdir" cmake --install build
  rm -r "$pkgdir"/usr/lib # Provided by libappletdecoration
}

