# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=arch-audit-gtk
pkgver=0.1.2
pkgrel=1
pkgdesc='Arch Linux Security Update Notifications'
url='https://github.com/kpcyrd/arch-audit-gtk'
arch=('x86_64')
license=('GPL3')
depends=('arch-audit' 'libappindicator-gtk3')
optdepends=(
  'gnome-shell-extension-appindicator: support for gnome3'
)
makedepends=('cargo' 'clang' 'llvm')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('b5725e26e8284e4df189b33bc58fd46d2f16678be0db53ddd124d3f6471e44f2f142645975058cc9f18eea8a53d0bf71bc67c71d2ec04d323324aa14c1c191a0'
            'SKIP')
b2sums=('03a5cf220e2084be02e8fb57736f07eb4be1b6769438c3639d89fc97390895ae9d1566d4f2e6ce72a97ee0831a79f09fd15463d4e8e28dfcca30204542b3ffab'
        'SKIP')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 icons/*.svg -t "${pkgdir}/usr/share/arch-audit-gtk/icons"
  install -Dm 644 contrib/arch-audit-gtk.tmpfiles "${pkgdir}/usr/lib/tmpfiles.d/arch-audit-gtk.conf"
  install -Dm 644 contrib/arch-audit-gtk.hook "${pkgdir}/usr/share/libalpm/hooks/arch-audit-gtk.hook"
  install -Dm 644 contrib/arch-audit-gtk.desktop -t "${pkgdir}/etc/xdg/autostart"
}

# vim: ts=2 sw=2 et:
