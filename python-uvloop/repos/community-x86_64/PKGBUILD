# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=uvloop
pkgname=python-$_pkgname
pkgver=0.15.3
pkgrel=1
pkgdesc='Ultra fast asyncio event loop'
arch=('x86_64')
url='https://github.com/MagicStack/uvloop'
license=('Apache' 'MIT')
depends=('python' 'libuv')
makedepends=('python-setuptools' 'cython')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('77dd99d03851f736324b2436547c5ad4e195ae48f7395f057d6bfce65cdc250cec84aa54f604d779c8df1b2aea9604262199e77d5247f394a9539a05b96072e1')

build() {
  cd $_pkgname-$pkgver

  python setup.py build build_ext --use-system-libuv
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE-APACHE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-APACHE
  install -Dm 644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
