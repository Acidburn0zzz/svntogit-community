# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Denis Martinez <deuns.martinez@gmail.com>

pkgname=wt
pkgver=3.2.2
_pkgver=3.2.2
pkgrel=2
pkgdesc="a C++ library and application server for developing and deploying web applications"
arch=('i686' 'x86_64')
url="http://www.webtoolkit.eu/"
license=('GPL')
depends=('boost-libs>=1.36')
makedepends=('boost>=1.36' 'cmake>=2.8'
	     'postgresql-libs' 'zlib' 'fcgi' 'sqlite' 'libharu' 'graphicsmagick' 'pango' 'mysql++' 'qt')
optdepends=('openssl: for SSL support in built-in webserver'
	    'zlib: for compression in HTTP protocol'
	    'fcgi: for FastCGI support'
	    'postgresql-libs: for PostgreSQL Dbo support'
	    'sqlite: for Sqlite Dbo support'
	    'libharu: for PDF generation (WPdfImage)'
	    'graphicsmagick: for raster image support (WRasterImage)'
	    'pango: for advanced font rendering (WRasterImage)'
	    'mysql++: for the hangman example'
	    'qt: for the Wt/Qt interopability example (wtwithqt)')
backup=('etc/wt/wt_config.xml')
source=(http://downloads.sourceforge.net/witty/$pkgname-${_pkgver}.tar.gz)
md5sums=('86e1eac9baa7a061fd3dd9b53dd77baa')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  mkdir -p build
  cd build
  cmake \
      -DCONNECTOR_HTTP=ON \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_EXE_LINKER_FLAGS="-lboost_random" \
      -DWEBUSER=http \
      -DWEBGROUP=http \
      -DUSE_SYSTEM_SQLITE3=ON \
      -DINSTALL_EXAMPLES=ON \
      ..
  make
}

package() {
  cd ${srcdir}/${pkgname}-${_pkgver}/build
  make DESTDIR=${pkgdir} install

  mkdir -p ${pkgdir}/usr/share/cmake-2.8/Modules
  mv ${pkgdir}/usr/cmake/FindWt.cmake ${pkgdir}/usr/share/cmake-2.8/Modules/
  rm -r ${pkgdir}/usr/cmake
  rm -rf $pkgdir/var/run
}
