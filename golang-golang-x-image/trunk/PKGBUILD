# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-image
pkgver=0.0.20190216
_commit=31aff87c08e9a5e5d524279a564f96968336f886
pkgrel=1
pkgdesc='Go supplementary image libraries'
arch=('any')
url="https://github.com/golang/image"
license=('BSD')
depends=('go-pie' 'golang-golang-x-text')
source=("$pkgname-$_commit.tar.gz::https://github.com/golang/image/archive/$_commit.tar.gz")
sha512sums=('759eb385b576b30eb37564e287093dcf9438b1fd6b0f703024aef17b83a1dfd3bede119732d8df75df296598b51d9d2d69f00b76e4bf9b0ba90256c9d2734d22')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$srcdir"/build/src/golang.org/x/image
  cd "$srcdir"/build/src/golang.org/x/image
  go test -v $(go list ./... | grep -v vendor)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/image-$_commit "$pkgdir"/usr/share/gocode/src/golang.org/x/image

  install -Dm644 image-$_commit/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
