# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-keystoneclient
pkgver=3.22.0
pkgrel=3
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
         'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-pycodestyle' 'python-lxml'
              'python-testresources')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-keystoneclient/archive/$pkgver.tar.gz")
sha512sums=('1fa668df36abc7073c938bca9b44b0b6856b3c48d66f750fd8edaf742f63240ed01bb55dd6233f2c379a539f71c9929683b75b844f66fa13554e4d4cb5376e04')

export PBR_VERSION=$pkgver

build() {
  cd python-keystoneclient-$pkgver
  python setup.py build
}

check() {
  cd python-keystoneclient-$pkgver
  stestr run
}

package() {
  cd python-keystoneclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
