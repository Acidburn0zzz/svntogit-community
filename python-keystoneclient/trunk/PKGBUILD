# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-keystoneclient
pkgver=3.20.0
pkgrel=1
pkgdesc="Client Library for OpenStack Identity"
arch=('any')
url="https://docs.openstack.org/python-keystoneclient/latest/"
license=('Apache')
depends=('python-six' 'python-pbr' 'python-iso8601' 'python-prettytable' 'python-debtcollector'
         'python-keystoneauth1' 'python-oslo-utils' 'python-oslo-i18n'
         'python-oslo-serialization' 'python-oslo-config' 'python-requests' 'python-stevedore')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-requests-mock' 'python-pycodestyle' 'python-lxml'
              'python-testresources')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/python-keystoneclient/archive/$pkgver.tar.gz")
sha512sums=('f53ef1f3b82cb6b789303a83a72176bcfe96eaa124c87e857cae2be3e00d89fcc4593339ed6c1d771c45ec7f1c77fee8724a9e538ea1f7e8b7de13659c7a7ab0')

export PBR_VERSION=$pkgver

build() {
  cd python-keystoneclient-$pkgver
  python setup.py build
}

check() {
  cd python-keystoneclient-$pkgver
  stestr run
}

package() {
  cd python-keystoneclient-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
