# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=param
pkgname=python-${_pkg}
pkgver=1.9.3
pkgrel=3
pkgdesc="Make your Python code clearer and more reliable by declaring Parameters"
arch=(any)
url="https://param.holoviz.org/"
license=(BSD)
makedepends=(python-setuptools)
checkdepends=(python-nose python-numpy python-pandas ipython)
# No tests in Pypi tarballs
#source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
source=(https://github.com/holoviz/param/archive/v${pkgver}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('c489599c2510849381404fcf34c18449dd15fa4a545ea1fd436f528ee5e878d2')

build() {
    cd ${_pkg}-${pkgver}
    python setup.py build
}

check() {
    cd ${_pkg}-${pkgver}
    PARAM_TEST_NUMPY=1 PARAM_TEST_PANDAS=1 PARAM_TEST_PANDAS=1 nosetests
}

package() {
    cd ${_pkg}-${pkgver}
    python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
    install -Dm644 LICENSE.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
