# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-gcp-devrel-py-tools
pkgname=('python-gcp-devrel-py-tools' 'python2-gcp-devrel-py-tools')
pkgver=0.0.13
_commit=73a070ac5e8b3751ae1310dbbf1c9b78483e684e
pkgrel=1
pkgdesc='Tools for Cloud Platform Python libraries and samples'
arch=('any')
license=('Apache')
url='https://github.com/GoogleCloudPlatform/python-repo-tools'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-retrying' 'python2-retrying' 'python-packaging' 'python2-packaging'
             'python-pylint' 'python2-pylint')
source=("$pkgbase-$_commit.tar.gz::https://github.com/GoogleCloudPlatform/python-repo-tools/archive/$_commit.tar.gz")
sha512sums=('0a4d2ed48915e20d36408a7de789b4863271d97c60922e6226f04d0c0e8507799e0b9a57a216688e90a64215e0ad9d5589c67c81873b40860053cd788db4d8a1')

prepare() {
  cp -a python-repo-tools-$_commit{,-py2}
}

build() {
  cd "$srcdir"/python-repo-tools-$_commit
  python setup.py build

  cd "$srcdir"/python-repo-tools-$_commit-py2
  python2 setup.py build
}

package_python-gcp-devrel-py-tools() {
  depends=('python-requests' 'python-retrying' 'python-setuptools' 'python-packaging'
           'python-pylint')

  cd python-repo-tools-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-gcp-devrel-py-tools() {
  depends=('python2-requests' 'python2-retrying' 'python2-setuptools' 'python2-packaging'
           'python2-pylint')

  cd python-repo-tools-$_commit-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/gcp-devrel-py-tools{,2}
}

# vim:set ts=2 sw=2 et:
