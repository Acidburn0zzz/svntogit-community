# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-gcp-devrel-py-tools
pkgname=('python-gcp-devrel-py-tools' 'python2-gcp-devrel-py-tools')
pkgver=0.0.10
_commit=7fb7518f5e1dbd90ebe393815dc56fa776129db8
pkgrel=1
pkgdesc='Tools for Cloud Platform Python libraries and samples'
arch=('any')
license=('Apache')
url='https://github.com/GoogleCloudPlatform/python-repo-tools'
makedepends=('python-setuptools' 'python2-setuptools' 'python-requests' 'python2-requests'
             'python-retrying' 'python2-retrying' 'python-packaging' 'python2-retrying')
source=("$pkgbase-$_commit.tar.gz::https://github.com/GoogleCloudPlatform/python-repo-tools/archive/$_commit.tar.gz")
sha512sums=('ee63ea289052f7fbccd1fc23b59dc8105d4669fe0345f5d63dc732947327704a454938658b0acb7a0799188f62978cba6d4417ba6498cdc4a5324b69cf755e9b')

prepare() {
  cp -a python-repo-tools-$_commit{,-py2}
}

build() {
  cd "$srcdir"/python-repo-tools-$_commit
  python setup.py build

  cd "$srcdir"/python-repo-tools-$_commit-py2
  python2 setup.py build
}

package_python-gcp-devrel-py-tools() {
  depends=('python-requests' 'python-retrying' 'python-setuptools' 'python-packaging')

  cd python-repo-tools-$_commit
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-gcp-devrel-py-tools() {
  depends=('python2-requests' 'python2-retrying' 'python2-setuptools' 'python2-packaging')

  cd python-repo-tools-$_commit-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/gcp-devrel-py-tools{,2}
}

# vim:set ts=2 sw=2 et:
