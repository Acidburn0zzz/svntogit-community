# Maintainer: François Charette <firmicus ατ gmx δοτ net>

pkgname=perl-list-moreutils
_cpanname=List-MoreUtils
pkgver=0.32
pkgrel=1
pkgdesc="Provide the stuff missing in List::Util"
arch=('i686' 'x86_64')
url="http://search.cpan.org/dist/List-MoreUtils"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=('http://search.cpan.org/CPAN/authors/id/A/AD/ADAMK/List-MoreUtils-0.32.tar.gz')
md5sums=('5af56334f8261f4662bd5e2b3f5e4e62')

build() {
  cd $srcdir/${_cpanname}-$pkgver
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd $srcdir/${_cpanname}-$pkgver
  make test
}

package() {
  cd $srcdir/${_cpanname}-$pkgver
  make install DESTDIR=$pkgdir
  find $pkgdir -name '.packlist' -delete
  find $pkgdir -name '*.pod' -delete
}

