# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=padthv1
pkgver=0.9.1
pkgrel=2
pkgdesc="An old-school polyphonic additive synthesizer"
arch=('x86_64')
url="https://padthv1.sourceforge.io/"
license=('GPL2')
groups=('lv2-plugins' 'pro-audio' 'vee-one')
depends=('desktop-file-utils' 'fftw' 'hicolor-icon-theme' 'jack' 'liblo'
'qt5-base')
makedepends=('lv2' 'qt5-tools')
source=("https://download.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a88cea9ca9e3b3098918cc98a2244e1b116014a1874e002b2cb705c85e63ac221e6b220f97581768ff120806c5229ebab504c7ae7660eadac653b0c09bd0832b')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr \
              --libdir=/usr/lib
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir/" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm644 {AUTHORS,ChangeLog,README,TODO}
}
# vim:set ts=2 sw=2 et:
