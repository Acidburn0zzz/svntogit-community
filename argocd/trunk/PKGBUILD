# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=argocd
pkgver=2.0.0
pkgrel=1
pkgdesc="Declarative continuous deployment for Kubernetes command line utility"
arch=('x86_64')
url='https://argoproj.github.io/argo-cd/'
license=('Apache')
makedepends=('go')
depends=('glibc')
provides=('argocd-cli')
replaces=('argocd-cli')
source=("argo-cd-${pkgver}.tar.gz::https://github.com/argoproj/argo-cd/archive/v${pkgver}.tar.gz")
sha512sums=('cc5c02dc708215e1e68360c9fe9c771ed51d9fd7580d955a43d67f54123c73f0cfb4dda51220eb5e24212809b5e410e11bb589f09333febf910df76217102368')

build() {
  cd "argo-cd-$pkgver"

  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  go run github.com/gobuffalo/packr/packr build -o argocd \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-X github.com/argoproj/argo-cd/common.version=${pkgver} \
              -X github.com/argoproj/argo-cd/common.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
              -X github.com/argoproj/argo-cd/common.gitTag=${pkgver} \
              -linkmode=external \
              -extldflags \"${LDFLAGS}\"" \
    ./cmd

  ./argocd completion bash > argocd.bash
  ./argocd completion zsh > argocd.zsh
}

package() {
  cd "argo-cd-$pkgver"

  install -D argocd "$pkgdir/usr/bin/argocd"
  ln -s argocd "$pkgdir/usr/bin/argocd-util"
  ln -s argocd "$pkgdir/usr/bin/argocd-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-repo-server"
  ln -s argocd "$pkgdir/usr/bin/argocd-application-controller"
  ln -s argocd "$pkgdir/usr/bin/argocd-dex"

  install -Dm644 argocd.bash "$pkgdir/usr/share/bash-completion/completions/argocd"
  install -Dm644 argocd.zsh "$pkgdir/usr/share/zsh/site-functions/_argocd"
}
