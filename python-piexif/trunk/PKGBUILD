# Maintainer: David Runge <dave@sleepmap.de>
_name=piexif
pkgname=python-piexif
pkgver=1.1.2
pkgrel=1
pkgdesc="To simplify exif manipulations with python. Writing, reading, and more"
arch=('any')
url="https://github.com/hMatoba/Piexif"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pillow' 'python-sphinx')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.zip")
sha512sums=('045906fdbd49350866058bacafb61841030766bccb7d63ad3ee17ce9490858773c88e9fc86ca3e2985e752871e52b89778147d1b3d0abe5f1055d6cb02fc300d')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  py.test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 LICENSE.txt \
    "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
