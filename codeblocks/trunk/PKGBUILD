# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Robert Hollencamp <rhollencamp@gmail.com>
# Contributor: Daniel J Griffiths <griffithsdj@inbox.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=codeblocks
pkgver=10.05
pkgrel=2
pkgdesc='Open source and cross-platform C/C++ IDE'
arch=('i686' 'x86_64')
url="http://www.codeblocks.org"
license=('GPL3')
depends=('wxgtk' 'hicolor-icon-theme' 'bzip2' 'shared-mime-info' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('zip')
options=('!libtool')
source=(http://download.berlios.de/$pkgname/$pkgname-${pkgver}-src.tar.bz2)
md5sums=('ab077d562e98b0586f2f86c14cb773ba')
install=codeblocks.install

build() {
  cd "$srcdir/$pkgname-$pkgver-release"
 
  LDFLAGS+=' -lX11'
  ./bootstrap
  ./configure --prefix=/usr --with-contrib-plugins=all
  make 
}

package() {
  cd "$srcdir/$pkgname-$pkgver-release"
  make DESTDIR="$pkgdir" install
}

