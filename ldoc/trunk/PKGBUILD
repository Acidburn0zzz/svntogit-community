# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sébastien Luttringer

pkgname=ldoc
pkgver=1.4.5
pkgrel=1
pkgdesc='LuaDoc-compatible documentation generation system'
url='http://stevedonovan.github.com/ldoc/'
arch=('any')
license=('custom')
depends=('lua' 'lua-penlight')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/stevedonovan/ldoc/archive/${pkgver}.tar.gz)
sha512sums=('11a8177aab110157ee7b7aca8627b41059da748bdc795ba8233f82c7b9f1273d4607288f5ad45c53f492862c653f734cbe48abdb994c74897a46ccb3b3dd8281')

package() {
  cd LDoc-${pkgver}

  # install library
  install -dm 755 "${pkgdir}/usr/share/lua/5.3"
  cp -a ldoc "${pkgdir}/usr/share/lua/5.3"

  # install binaries
  install -Dm 755 ldoc.lua "${pkgdir}/usr/bin/ldoc.lua"
  ln -s ldoc.lua "${pkgdir}/usr/bin/ldoc"

  # copy docs
  install -Dm 644 readme.md doc/doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
