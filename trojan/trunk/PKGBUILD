# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: GreaterFire <GreaterFire at protonmail dot com>

pkgname=trojan
pkgver=1.11.0
pkgrel=1
pkgdesc="An unidentifiable mechanism that helps you bypass GFW"
arch=('x86_64')
url="https://github.com/trojan-gfw/trojan"
license=('GPL3')
depends=('boost-libs' 'openssl' 'mariadb-libs')
optdepends=('ca-certificates: server certificate verification'
            'mariadb: advanced user management')
makedepends=('cmake' 'boost' 'openssl' 'mariadb-libs')
checkdepends=('openssl' 'python' 'curl')
source=("$pkgname-$pkgver.tar.gz::https://github.com/trojan-gfw/$pkgname/archive/v$pkgver.tar.gz")
backup=('etc/trojan.json'
        'etc/trojan/config.json')
sha512sums=('3dab571287f609b724c216cbafe6eaa46c0833b2b1cf7f9772211abeb71bb09948336a9c5e4b4ab1831cd62c3fd000e7763be600ecb5576fef6f5a7f0d4766d7')

build() {
  cd $pkgname-$pkgver
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
    cd $pkgname-$pkgver
    ctest
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
