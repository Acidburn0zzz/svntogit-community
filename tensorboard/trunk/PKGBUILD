# $Id: PKGBUILD 244979 2017-07-13 15:39:06Z svenstaro $
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=tensorboard
pkgver=0.1.5
pkgrel=1
pkgdesc="TensorFlow's Visualization Toolkit"
url='https://github.com/tensorflow/tensorboard'
arch=('x86_64')
license=('APACHE')
depends=('python' 'python-werkzeug' 'python-numpy' 'python-bleach' 'python-wheel' 'python-markdown' 'python-html5lib' 'python-protobuf' 'python-tensorflow')
makedepends=('bazel' 'python-setuptools')
source=("https://github.com/tensorflow/tensorboard/archive/${pkgver}.tar.gz")
sha512sums=('1d2f0a7f83160fd3ccc5b8ae78caf8f29ca0749a935e261f402f9d71c6e10cab87183a55145d16f10afd0d1ed353d5d1fed13063a92e33605f879344ff6c1706')

build() {
  cd "$pkgname-$pkgver"

  bazel build tensorboard:tensorboard
  bazel build //tensorboard/pip_package:build_pip_package

  mkdir pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/external pypackage
  cp -R bazel-bin/tensorboard/pip_package/build_pip_package.runfiles/org_tensorflow_tensorboard/tensorboard pypackage
  cp tensorboard/pip_package/MANIFEST.in pypackage
  cp README.md pypackage
  cp tensorboard/pip_package/setup.py pypackage
  cd pypackage
  python setup.py build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  cd pypackage
  python setup.py install --root="$pkgdir" --optimize=1

  rm -rf "$pkgdir"/usr/lib/python3.6/site-packages/external/

  # This is ugly: We need to patch it to use current bleach and html5lib
  # because old ones are not even compatible with Python 3.6 and we don't have
  # those in Arch of course.
  sed -i 's/bleach==.*/bleach/' "$pkgdir"/usr/lib/python3.6/site-packages/tensorflow_tensorboard-${pkgver}-py3.6.egg-info/requires.txt
  sed -i 's/html5lib==.*/html5lib/' "$pkgdir"/usr/lib/python3.6/site-packages/tensorflow_tensorboard-${pkgver}-py3.6.egg-info/requires.txt
}

# vim:set ts=2 sw=2 et:
