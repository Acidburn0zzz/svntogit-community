# Maintainer: David Runge <dave@sleepmap.de>
pkgname=zam-plugins
pkgver=3.11
pkgrel=1
pkgdesc="Collection of LADSPA/LV2/VST/JACK audio plugins for high-quality processing"
arch=('x86_64')
url="https://github.com/zamaudio/zam-plugins"
license=('GPL2')
groups=('ladspa-plugins' 'lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('jack' 'libglvnd' 'liblo' 'zita-convolver')
makedepends=('git' 'ladspa' 'libx11' 'libgl' 'lv2')
source=("$pkgname-$pkgver::git+https://github.com/zamaudio/${pkgname}.git#tag=${pkgver}"
        "git+https://github.com/distrho/dpf.git")
sha512sums=('SKIP'
            'SKIP')

prepare() {
  cd "$pkgname-$pkgver"
  git submodule init
  git config submodule.dpf.url "${srcdir}/dpf"
  git submodule update
}

build() {
  cd "$pkgname-$pkgver"
  export HAVE_ZITA_CONVOLVER=true
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {README.md,changelog}
}
