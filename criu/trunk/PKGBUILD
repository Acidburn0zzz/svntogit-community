# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Pavel Borzenkov <pavel@voidptr.ru>
# Contributor: aksr <aksr at t-com dot me>

pkgname=criu
pkgver=3.16
pkgrel=1
pkgdesc="Utilities to checkpoint and restore processes in userspace"
url="http://criu.org"
license=('GPL2')
arch=('x86_64')
source=("$pkgname-$pkgver.tar.gz::https://github.com/checkpoint-restore/criu/archive/v$pkgver.tar.gz")
depends=('libbsd' 'libnet' 'libnl' 'protobuf-c' 'python-protobuf' 'gnutls' 'nftables')
makedepends=('xmlto' 'asciidoc')
options=("!buildflags")
sha512sums=('3f057332ff7e2199b93b678ae3aba4dfd5cbaba87db333b9b4080651d4b375d708a528ef2f58e8a2c6c4a5267fc24ae722f3636ee4bfb73d6fef22ef2bcabdd1')
b2sums=('91b481a936384b1d0d082f373a70affbad8ff4e073f4f53327baf73c68d5606f4be54ae62426146e7c240eb2d4a32146b53c580d3d5f39791367ee1abfec1cbb')

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" \
    PREFIX=/usr \
    SBINDIR=/usr/bin \
    LIBDIR=/usr/lib \
    LIBEXECDIR=/usr/lib \
    install

  # remove empty directory
  rmdir "$pkgdir/usr/include/compel/common/asm"
}
