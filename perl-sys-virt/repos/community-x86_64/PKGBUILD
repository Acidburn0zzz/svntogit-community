# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-sys-virt
pkgver=5.4.0
pkgrel=1
pkgdesc="Represent and manage a libvirt hypervisor connection"
arch=('x86_64')
license=('PerlArtistic')
url="https://metacpan.org/release/Sys-Virt"
depends=('libvirt' 'perl')
makedepends=('perl-cpan-changes' 'perl-module-build' 'perl-xml-xpath' 'perl-test-pod'
             'perl-test-pod-coverage')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DA/DANBERR/Sys-Virt-v$pkgver.tar.gz")
sha512sums=('315e7df8884ca24f622fbdcb64425e4573ddef7e7498ba5787656e47b0becaff98c87b2d96cd4bb3b1eb031b0e57b6f6e8a7667c47b70572c08a70e96640d0c6')

build() {
  cd Sys-Virt-v$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Sys-Virt-v$pkgver
  make test
}

package() {
  cd Sys-Virt-v$pkgver
  make DESTDIR="$pkgdir" install
}
