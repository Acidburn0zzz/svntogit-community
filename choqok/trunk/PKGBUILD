# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Peter Richard Lewis <plewis@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Emanuele Rossi <newdna1510@yahoo.it>
# Contributor: Bram Schoenmakers <me@bramschoenmakers.nl>

pkgname=choqok
pkgver=1.7.0
pkgrel=2
pkgdesc="A micro-blogging client by the KDE community"
url='https://choqok.kde.org/'
license=(GPL)
arch=(x86_64)
depends=(qt5-networkauth qca knotifyconfig kcmutils kemoticons kdewebkit purpose hicolor-icon-theme)
makedepends=(extra-cmake-modules telepathy-qt kdoctools)
optdepends=('telepathy-qt: IMstatus plugin')
source=("https://download.kde.org/stable/$pkgname/${pkgver%.*}/src/$pkgname-$pkgver.tar.xz"{,.sig}
         choqok-retweet.patch choqok-conversation.patch)
sha256sums=('26600d95f152c3e04881efb7fb3eb07b5c52fa6ffee838690f28b45defb4a47e'
            'SKIP'
            'da3d892c3bbc5736ec19dffb72286fb90623fb24c6dfee1fba6c105fdf235453'
            '7fc1f8c5848e8c63b37651280f0ec1edec3b3685f91886a3d4f0650d19e8eba8')
validpgpkeys=('2C7849767F14C23890B12791918F7DDFFD8D0B6E') # Andrea Scarpino <me@andreascarpino.it>

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../choqok-retweet.patch # Fix retweet display https://bugs.kde.org/show_bug.cgi?id=370260
  patch -p1 -i ../choqok-conversation.patch # Fix fetching conversations https://bugs.kde.org/show_bug.cgi?id=417193
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DBUILD_TESTING=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
