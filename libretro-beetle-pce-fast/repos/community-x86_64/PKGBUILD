# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-beetle-pce-fast
pkgver=976
pkgrel=1
pkgdesc='NEC PC Engine/CD core'
arch=(x86_64)
url=https://github.com/libretro/beetle-pce-fast-libretro
license=(GPL2)
groups=(libretro)
depends=(
  gcc-libs
  glibc
  libretro-core-info
)
makedepends=(git)
source=(libretro-beetle-pce-fast::git+https://github.com/libretro/beetle-pce-fast-libretro.git#commit=17ba58b4695970dad499de04a3b81777ff47c9d9)
sha256sums=(SKIP)

pkgver() {
  cd libretro-beetle-pce-fast

  git rev-list --count HEAD
}

build() {
  make -C libretro-beetle-pce-fast
}

package() {
  install -Dm 644 libretro-beetle-pce-fast/mednafen_pce_fast_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
