# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight')
pkgver=1.4.1
pkgrel=1
pkgdesc='Lua libraries focusing on input data handling'
url='https://github.com/stevedonovan/penlight'
arch=('any')
license=('custom')
depends=('lua' 'lua-filesystem')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/stevedonovan/penlight/archive/${pkgver}.tar.gz
        fix-tablex.patch)
sha512sums=('ed34c119a43bb9b8baf0cf570ace3d175bd0beeacb250a5d5ee3efe5782864b79a8f5b7e92bd1e676a369ec32def29a1c5b0a7d77b1c665a22c1e69a4904abd4'
            'e3384422aee4a20db7fd4cbd86ed7acafe61a397c51293d3285be94c950bbf06e746ed8d4db121a53ef52fe0d19c9a7b09f7d02acfcd722118b2ae19971d3c41')

prepare() {
  cd Penlight-${pkgver}
  patch -p1 < "${srcdir}/fix-tablex.patch"
}

check() {
  cd Penlight-${pkgver}
  export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;$(lua -e 'print(package.path)')"
  lua run.lua
}

package_lua-penlight() {
  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/5.3/pl"
  # copy docs
  install -Dm 644 CONTRIBUTING.md CHANGES.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  # copy license
  install -Dm 644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
