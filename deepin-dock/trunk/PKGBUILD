# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.5.5
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('dtkwidget' 'qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher'
         'deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('e70241681584ecaf52c3d8c9eba27baed3fa4b8d40b45f20dd68627076815d3dcb81f6df5f7cc1a0eeb0bc204dfca26e7c06d3b15300e95dc9d43523875f7e67')

build() {
  cd dde-dock-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
