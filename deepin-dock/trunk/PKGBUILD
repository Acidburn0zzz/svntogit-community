# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.6.2
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory')
makedepends=('cmake' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('5e88605d64676df5aaa4ea9d0159782647fa9656ab049e5af4e3810242b72593e1168b29de4909b32fa6a39bb1f2bc8c7c3691f33de91700d02c116a585fe756')

build() {
  cd dde-dock-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make DESTDIR="$pkgdir" install
}
