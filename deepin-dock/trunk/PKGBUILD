# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=5.3.0.12
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils' 'libdbusmenu-qt5')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/dde-dock/commit/1611be8c849d19fab44de5eecc663a7c38575e70.patch)
sha512sums=('61ca0953a8a7a0cc9d76cebd4430f35c00c3f1efc2ec8199ed7cfa36ccdc10922dfc78d26a0b24f00cc614447093f3b444c9486e68a17fd1868af36829858352'
            'f65a066dbbd27caec29e59dcb5f0328dd462cc5b86d4d899a10f04d9164c29729575230ab1be81f47ea4f4c9ee8bdeaa0a2b4d89fe82d30f8858509ebb2e289a')

prepare() {
  cd dde-dock-$pkgver
  patch -p1 -i ../1611be8c849d19fab44de5eecc663a7c38575e70.patch
}

build() {
  cd dde-dock-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=lib -DDOCK_TRAY_USE_NATIVE_POPUP=YES
  ninja
}

package() {
  cd dde-dock-$pkgver
  DESTDIR="$pkgdir" ninja install
}
