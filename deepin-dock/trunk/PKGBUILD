# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.5.8
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('397db49d725827d1bf0a7efabef9e7a8513775cae47cd413eefb70fff4fe44e4b3104aed2305e0735a632195f097ea648e63a2cb46dd9f18ece3cd884e708c14')

build() {
  cd dde-dock-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
