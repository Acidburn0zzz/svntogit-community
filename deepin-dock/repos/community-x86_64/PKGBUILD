# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.3.1
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('gtk2' 'deepin-tool-kit' 'qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher'
         'deepin-qt5integration' 'deepin-qt-dbus-factory')
makedepends=('qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('644f215859d9a8121676803cb51984297ca59ca0a1e0a54562acf1010c84826223a656eb71a240ff11f74233ceb5334c87b5c684a15be2b4ce84a85b78c77fa0')

build() {
  cd dde-dock-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd dde-dock-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
