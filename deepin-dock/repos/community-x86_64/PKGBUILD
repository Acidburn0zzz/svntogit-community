# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-dock
pkgver=4.7.3
pkgrel=1
pkgdesc='Deepin desktop-environment - dock module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-dock"
license=('GPL3')
depends=('qt5-svg' 'deepin-menu' 'deepin-daemon' 'deepin-launcher' 'deepin-qt5integration'
         'deepin-qt-dbus-factory' 'deepin-network-utils')
makedepends=('cmake' 'qt5-tools')
conflicts=('dde-dock')
replaces=('dde-dock')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/felixonmars/dde-dock/archive/$pkgver.tar.gz")
sha512sums=('038a11d5c75f344099f4fe9ad3b751181265eb34daa8dcad47416d3fb256e11507a28be0b53f2271c93f6c3b15431deddc170ab2bd5a3ad81da65da381123a49')

build() {
  cd dde-dock-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr -DARCHITECTURE=$CARCH
  make
}

package() {
  cd dde-dock-$pkgver
  make DESTDIR="$pkgdir" install
}
