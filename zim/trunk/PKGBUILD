# Maintainer: Florian Pritz <bluewind@xinu.at>
# Contributor: Kevin Piche <kevin@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=zim
pkgver=0.70
pkgrel=1
pkgdesc="A WYSIWYG text editor that aims at bringing the concept of a wiki to the desktop."
arch=(any)
license=('GPL' 'PerlArtistic')
url="https://zim-wiki.org/"
depends=('python' 'python-gobject' 'ttf-dejavu' 'gtk3' 'python-xdg')
checkdepends=('xorg-server-xvfb')
optdepends=('bzr: Version Control plugin'
            'git: Version Control plugin'
            'mercurial: Version Control plugin'
            'gnuplot: Insert Gnuplot plugin'
            'ditaa: Insert Ditaa plugin'
            'graphviz: Insert Diagram & Link Map plugins'
            'python2-gtkspell: Spell Checker plugin'
            'r: Insert GNU R Plot plugin'
            'scrot: Insert Screenshot plugin'
            'zeitgeist: Log events with Zeitgeist plugin'
            'lilypond: Insert Score plugin'
            'pygtksourceview2: Source View plugin'
            'texlive-bin: Insert Equation plugin')
source=(https://www.zim-wiki.org/downloads/${pkgname}-${pkgver}.tar.gz)
md5sums=('14bde80a5805c5e84cd4b109ece013f6')


check() {
	cd ${srcdir}/${pkgname}-${pkgver}

	xvfb-run -w0 ./test.py
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	python setup.py install --root=${pkgdir} --optimize=1
}
