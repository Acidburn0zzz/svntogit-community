# Maintainer: David Runge <dave@sleepmap.de>
_name=tabulate
pkgbase=python-tabulate
pkgname=('python-tabulate' 'python2-tabulate')
pkgver=0.8.3
pkgrel=2
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth' 'python2-nose' 'python2-numpy' 'python2-pandas' 'python2-wcwidth')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('7f0205a4d80ed0b426a45622a3b8ec0a9b77cf6d67c1207db7fbc20a12e83a15748e63d5f8e5e65fa348b6265fd0cac93be4d04f9c1c182476b93507e1131cbf')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
  python2 setup.py test
}

package_python-tabulate() {
  depends=('python-setuptools')
  optdepends=('python-numpy: NumPy array support'
              'python-pandas: pandas.DataFrame support'
              'python-wcwidth: wide-character support')
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_python2-tabulate() {
  depends=('python2-setuptools')
  optdepends=('python2-numpy: NumPy array support'
              'python2-pandas: pandas.DataFrame support'
              'python2-wcwidth: wide-character support')
  cd "$pkgbase-$pkgver"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # move executable to unique name
  mv -v "${pkgdir}/usr/bin/${_name}"{,2}
}
