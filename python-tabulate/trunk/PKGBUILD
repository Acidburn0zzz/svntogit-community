# Maintainer: David Runge <dave@sleepmap.de>
_name=tabulate
pkgname=python-tabulate
pkgver=0.8.5
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python-setuptools')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('137f576ac5adc29ad82f77da499804b110ddc7f12c5f85cdc79bdd5dfd37404d49f1c5667e6f8721e48f949b684c71b330b8e4fae4ef8a933af24d68fd0665c5')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
}

package_python-tabulate() {
  cd "${_name}-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
