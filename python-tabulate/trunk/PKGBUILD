# Maintainer: David Runge <dvzrv@archlinux.org>

_name=tabulate
pkgname=python-tabulate
pkgver=0.8.8
pkgrel=1
pkgdesc="Pretty-print tabular data in Python, a library and a command-line utility."
arch=('any')
url="https://bitbucket.org/astanin/python-tabulate"
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy' 'python-pandas' 'python-wcwidth')
optdepends=('python-numpy: NumPy array support'
            'python-pandas: pandas.DataFrame support'
            'python-wcwidth: wide-character support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('1d938138843b824833539eca4714abebc6a394737d24b9fe6adfbc952ef7f25afbf62c1e92a9020f5492516e04377b54a3a2735ab539a20a3e0d441981b86879')
b2sums=('13f34565e2c266b5cbfd672f05adc73809418a84f329853aa362593c885379596ce6607045dee23e71f24bfc7fc6834f64662f1f7ccc854b565a8e29fa3422a8')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PYTHONPATH}:${PWD}"
  python setup.py test
}

package_python-tabulate() {
  cd "${_name}-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}/"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
