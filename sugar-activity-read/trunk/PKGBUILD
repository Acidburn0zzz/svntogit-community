# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-read
_pkgname=Read
pkgver=122
pkgrel=1
pkgdesc="Sugar activity to read books"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Read"
license=('GPL')
groups=('sugar-fructose')
depends=('evince' 'sugar-toolkit-gtk3' 'webkit2gtk')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('5cc9f1c14a4c156f8799035b32418cdb81742bb4c9f65b36c926e06de1ecfb33')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' readdialog.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
