# Maintainer: Brett Cornwall <ainola@archlinux.org>
# Contributor: Alexander Courtis

pkgname=interception-dual-function-keys
pkgver=1.2.0
pkgrel=2
pkgdesc="Interception plugin for dual-function keys: Tap for one key, hold for another"
arch=('x86_64')
url="https://gitlab.com/interception/linux/plugins/dual-function-keys"
license=('MIT')
depends=('interception-tools')
source=("$pkgname-$pkgver.tar.gz::https://gitlab.com/interception/linux/plugins/dual-function-keys/-/archive/$pkgver/dual-function-keys-$pkgver.tar.gz"
"0001-make-Append-don-t-overwrite-user-defs.patch")
sha256sums=('fc64d6ddf80c1a153dd0e2180a581649f4a9d489e1718053a70b61faef98e0ca'
            '98f9dbc96b3f71ba4969146282676946648dfc085d7722794d9bb8b0a9740d30')

prepare() {
    cd "dual-function-keys-$pkgver"
    # https://gitlab.com/interception/linux/plugins/dual-function-keys/-/issues/20
    patch -p1 < ../0001-make-Append-don-t-overwrite-user-defs.patch
}

build() {
    cd "dual-function-keys-$pkgver"
    make VERSION="$pkgver"
}

package() {
    cd "dual-function-keys-$pkgver"
    make PREFIX=/usr DESTDIR="$pkgdir/" install
    install -Dm644 {README,doc/examples}.md -t "$pkgdir/usr/share/doc/$pkgname"
    install -Dm644 LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname"
}
