# $Id$
# Contributor: Tom K <tomk@runbox.com>
# Contributor: Thayer Williams <thayer@archlinux.org>
# Maintainer: schuay <jakob.gruber@gmail.com>

pkgname=epdfview
pkgver=0.1.7
pkgrel=6
pkgdesc="A free lightweight PDF document viewer."
url="http://www.emma-soft.com/projects/epdfview/"
arch=('i686' 'x86_64')
license=('GPL')
depends=('poppler-glib>=0.16.2' 'desktop-file-utils' 'hicolor-icon-theme')
makedepends=('pkgconfig')
install=epdfview.install
source=("http://www.emma-soft.com/projects/${pkgname}/chrome/site/releases/${pkgname}-${pkgver}.tar.bz2"
        "${pkgname}.desktop.patch"
        "fix-scrolling.patch"
        "changeset_r354.diff")
md5sums=('1919bb19c16ef0a97d48b0a8303d3c7b'
         'fbf22bbabdbb7544db615ac5775d57e2'
         '7a1574e0a8d0bcde316050629fb7bedd'
         'd5beedb1bfd71aef4effd2bf8da1131d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np0 -i "${srcdir}/${pkgname}.desktop.patch"
  patch -Np0 -i "${srcdir}/fix-scrolling.patch"
  patch -Np0 -i "${srcdir}/changeset_r354.diff" # patches poppler api change, can
                                                # be removed with a new epdfview release

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}
package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  for size in 24 32 48; do
      install -Dm644 data/icon_${pkgname}-${size}.png \
          "${pkgdir}/usr/share/icons/hicolor/${size}x${size}/apps/${pkgname}.png"
  done
}
