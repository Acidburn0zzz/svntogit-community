# $Id$
# Maintainer:
# Contributor: Sarah Hay <sarahhay@mb.sympatico.ca>
# Contributor: roberto <roberto@archlinux.org>

pkgname=transcode
_sripver=0.3-4
pkgver=1.1.7
pkgrel=25
pkgdesc="A video/DVD ripper and encoder for the terminal/console"
arch=(x86_64)
url="http://www.transcoding.org/"
license=(GPL)
depends=(gawk libmagick lzo libdvdread ffmpeg mjpegtools libmpeg2 libxaw libxv a52dec)
makedepends=(nasm libx264)
source=(https://bitbucket.org/france/transcode-tcforge/downloads/$pkgname-$pkgver.tar.bz2
        transcode-imagemagick7.patch
	transcode-1.1.7-preset-free.patch
	transcode-1.1.7-preset-force.patch
	transcode-ffmpeg3.patch)
sha256sums=('1e4e72d8e0dd62a80b8dd90699f5ca64c9b0cb37a5c9325c184166a9654f0a92'
            '4ede15540ea6932954ac332c12dde130bf48e7e4773d1e04d3c3f23038c6ac51'
            '2219fe618be2b2c3fbd6e3545cdbd8db8d1c268a0b37bfe867514f871d7d91ef'
            'fe95e83093761924ba0fa5b3214e169812f7740c6c3c434845b1b563d4a37241'
            '728ec3101039d98116275e36ea5cf0434a23a0bb82aca830ea9612fb9d16b062')

prepare() {
  cd $pkgname-$pkgver
#  patch -p1 -i "$srcdir"/transcode-1.1.7-preset-free.patch
#  patch -p1 -i "$srcdir"/transcode-1.1.7-preset-force.patch
  patch -p1 -i ../transcode-ffmpeg3.patch # LFS patch
  patch -p1 -i ../transcode-imagemagick7.patch # Gentoo patch
  sed -i 's|freetype/ftglyph.h|freetype2/freetype/ftglyph.h|' filter/subtitler/load_font.c
  autoreconf -vi
}


build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --disable-sse --disable-sse2 --disable-altivec --enable-mmx \
    --enable-lame --enable-ogg --enable-vorbis --enable-theora \
    --enable-libdv --enable-libxml2 --enable-v4l \
    --enable-imagemagick --enable-libjpeg --enable-lzo --enable-mjpegtools \
    --enable-sdl --enable-freetype2 --enable-a52 --enable-libpostproc \
    --enable-xvid --enable-x264 --enable-alsa --enable-libmpeg2 --enable-libmpeg2convert

  #https://bugzilla.gnome.org/show_bug.cgi?id=655517
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
