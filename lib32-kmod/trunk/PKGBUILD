# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=lib32-kmod
_pkgbasename=kmod
pkgver=5
pkgrel=1
pkgdesc="Linux kernel module handling"
arch=('x86_64')
url="http://git.profusion.mobi/cgit.cgi/kmod.git"
license=('GPL2')
depends=('lib32-glibc' 'lib32-zlib' "$_pkgbasename")
makedepends=('gcc-multilib')
options=('!libtool')
source=("http://packages.profusion.mobi/$_pkgbasename/$_pkgbasename-$pkgver.tar.xz")
md5sums=('b271c2ec54aba1c67bda63c8579d8c15')

build() {
  cd "$_pkgbasename-$pkgver"

  export CC="gcc -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --disable-tools \
    --with-rootprefix= \
    --with-zlib

  make
}

package() {
  make -C "$_pkgbasename-$pkgver" DESTDIR="$pkgdir" install

  # nuke manpages and headers
  rm -rf "$pkgdir"/usr/{share,include,bin}
}

# vim: ft=sh syn=sh et
