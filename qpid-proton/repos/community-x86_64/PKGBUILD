# $Id$
# Maintainer: Nicola Squartini <tensor5@gmail.com>

pkgname=(qpid-proton python2-qpid-proton)
pkgver=0.22.0
pkgrel=1
pkgdesc='High-performance, lightweight messaging library'
arch=('x86_64')
url='https://qpid.apache.org/proton'
license=('Apache')
makedepends=('cmake' 'doxygen' 'python' 'python2' 'swig')
source=("http://www.apache.org/dist/qpid/proton/${pkgver}/qpid-proton-${pkgver}.tar.gz")
sha512sums=('abcc0c18e7f657433e11ee6d7093d11048f2607dc3e531352c037df4dad841d7f8a465fc30a049110c9d208709b04d38545ab1825709ac25774fc9e1bfef43e7')

build() {
    cd ${pkgname}-${pkgver}

    mkdir build
    cd build
    cmake \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DLIB_SUFFIX='' \
        -DSYSINSTALL_BINDINGS=ON \
        ..
    make

    cd proton-c/bindings/python/dist
    python2 setup.py build
}

package_qpid-proton() {
    depends=('python' 'ruby')

    cd ${pkgname}-${pkgver}/build
    make DESTDIR="${pkgdir}" install
}

package_python2-qpid-proton() {
    depends=('python2')

    cd qpid-proton-${pkgver}/build/proton-c/bindings/python/dist
    python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1
}
