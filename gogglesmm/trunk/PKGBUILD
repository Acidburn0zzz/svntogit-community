# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sander Jansen <sander@knology.net>

pkgname=gogglesmm
pkgver=0.12.5
pkgrel=2
pkgdesc="Music Manager and Player"
arch=('i686' 'x86_64')
url="http://code.google.com/p/gogglesmm/"
license=('GPL3')
install=gogglesmm.install
depends=('fox>=1.6.0' 'xine-lib>=1.0' 'sqlite3>=3.4.0' 'taglib' 'dbus-core' 'curl' 'expat' 'libgcrypt')
makedepends=('pkgconfig' 'glproto' 'dri2proto')
replaces=('musicmanager')
conflicts=('musicmanager')
source=(http://gogglesmm.googlecode.com/files/gogglesmm-$pkgver.tar.bz2)
md5sums=('07e92bcf2daf39d6b380590816edd013')

build() {
  cd $srcdir/gogglesmm-$pkgver
  patch configure <<EOF
diff gogglesmm-0.12.5/configure gogglesmm-0.12.5.my/configure
206,207c206,207
< XINE_MINOR=\${XINE_MINOR:-1}
< XINE_LEVEL=\${XINE_LEVEL:-16}
---
> XINE_MINOR=\${XINE_MINOR:-2}
> XINE_LEVEL=\${XINE_LEVEL:-0}
EOF
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/gogglesmm-$pkgver
  mkdir -p $pkgdir/usr/bin
  make DESTDIR=$pkgdir install
}
