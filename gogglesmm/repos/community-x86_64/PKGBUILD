# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sander Jansen <sander@knology.net>

pkgname=gogglesmm
pkgver=1.0.18
pkgrel=1
pkgdesc="Music Manager and Player"
arch=('i686' 'x86_64')
url="http://gogglesmm.github.io/"
license=('GPL3')
depends=('fox-devel>=1.7.48' 'libmad' 'faad2' 'sqlite3'
	 'libpulse' 'opus' 'dbus' 'taglib>=1.9.0' 'glew'
	 'hicolor-icon-theme' 'desktop-file-utils')
makedepends=('glproto' 'dri2proto')
conflicts=('musicmanager')
replaces=('musicmanager')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gogglesmm/gogglesmm/archive/$pkgver.tar.gz")
md5sums=('ccf474969d11c963117bb0775bd5322a')

prepare() {
  cd gogglesmm-$pkgver
  sed -i 's|HAVE_FAAD_PLUGIN|HAVE_AAC_PLUGIN|' src/gap/plugins/ap_aac_plugin.cpp
}

build() {
  cd gogglesmm-$pkgver
  ./configure --prefix=/usr
  make
}

package() {
  cd gogglesmm-$pkgver
  make DESTDIR="$pkgdir" install
}
