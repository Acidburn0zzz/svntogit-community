# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.9.0
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git pcre libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz)
sha256sums=('90f230c87cc7b94ca845e6fe34f2523edcadb562d715daaf98603edfa2a94d65')

build() {
  cd shards-$pkgver
  crystal build --release -o bin/shards src/shards.cr
}

check() {
  cd shards-$pkgver
  ./bin/shards install
  # currently tests fail because they try to write to read-only directory
  #make test
}

package() {
  cd shards-$pkgver

  install -Dm755 bin/shards "$pkgdir/usr/bin/shards"
  install -Dm755 man/shards.1 "$pkgdir/usr/share/man/man1/shards.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
