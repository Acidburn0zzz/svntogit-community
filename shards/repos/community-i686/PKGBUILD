# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.6.1
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(i686 x86_64)
url='https://github.com/ysbaddaden/shards'
license=(Apache)
depends=(libyaml git libpcl libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/ysbaddaden/shards/archive/v$pkgver.tar.gz)
sha256sums=('8e7d179a499a2fca895b534c6204e2e34828e6a645e48f83f08fbefcd6a03951')

build() {
  cd shards-$pkgver
  crystal build --release -o bin/shards src/shards.cr
}

check() {
  cd shards-$pkgver
  ./bin/shards install
  make test
}

package() {
  cd shards-$pkgver

  install -Dm755 bin/shards "$pkgdir/usr/bin/shards"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
