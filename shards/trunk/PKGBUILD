# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Contributor: Jonne Ha√ü <me@jhass.eu>

pkgname=shards
pkgver=0.10.0
pkgrel=1
pkgdesc='The package manager for the Crystal language'
arch=(x86_64)
url='https://github.com/crystal-lang/shards'
license=(Apache)
depends=(libyaml git pcre libevent gc)
makedepends=(crystal)
source=(shards-$pkgver.tar.gz::https://github.com/crystal-lang/shards/archive/v$pkgver.tar.gz
        fix_build.patch) # a few upstream changes to fix the build
sha256sums=('3aea420df959552d1866d473c878ab1ed0b58489c4c9881ef40a170cfb775459'
            '61f1a81bd3d6e386de8746fffd7f9bc1ac6db6b5a82f18fd901fd5c5edf7a09c')

prepare() {
  cd shards-$pkgver
  patch -p1 < ../fix_build.patch
}

build() {
  cd shards-$pkgver
  CRYSTAL_OPTS=--release make -j1
}

check() {
  cd shards-$pkgver
  # tests are currently failing. TODO: fix it
  # make test -j1 # https://github.com/crystal-lang/shards/issues/347
}

package() {
  cd shards-$pkgver
  PREFIX=/usr DESTDIR="$pkgdir" make install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
