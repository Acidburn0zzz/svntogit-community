# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Ilya Elenskiy <elenskiy.ilya@gmail.com>
# Contributor: Julien Nicoulaud <julien.nicoulaud@gmail.com>

pkgname=python-rx
pkgver=3.1.0
pkgrel=1
pkgdesc='Reactive Extensions for Python'
arch=(any)
url=http://reactivex.io
license=(APACHE)
depends=(python)
makedepends=(
  git
  python-setuptools
)
checkdepends=(
  python-coverage
  python-nose
  python-pytest
  python-pytest-asyncio
)
source=(git+https://github.com/ReactiveX/RxPY.git#tag=4ed60bb5c04aa85de5210e5537a6adfe1b667d50)
sha256sums=(SKIP)

pkgver() {
  cd RxPY

  git describe --tags | sed 's/^v//'
}

build() {
  cd RxPY

  python setup.py build
}

check() {
  cd RxPY

  python setup.py test
}

package() {
  cd RxPY

  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
