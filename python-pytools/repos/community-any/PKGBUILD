# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2019.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs' 'python-numpy'
             'python2-numpy')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('7f3911ecb17b00d21ab10330a275e2d1f27705774a1b56acd8e99b13f777569d884d4687fe8475c197c8e7e924e01782ab13797c920ce189bc0bcad8afaa31c6')

prepare() {
  cp -a pytools-$pkgver{,-python2}
}

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py pytest
}

package_python-pytools(){
  depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')

  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python2-pytools() {
  depends=('python2-six' 'python2-decorator' 'python2-appdirs' 'python2-numpy')

  cd pytools-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
