# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2016.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs' 'python-numpy'
             'python2-numpy')
checkdepends=('python-pytest' 'python2-pytest')
source=("http://pypi.python.org/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('5492b8b78b6fd00f190d97f3be82c3e807a8e73661888f3b70fc26c5825544f3a76f94c76149b825d77557a74aee2d956231193a9f8056cd433e108556af61df')

prepare() {
   cp -a pytools-${pkgver}{,-python2}
}

build() {
   cd "$srcdir/pytools-$pkgver"
   python setup.py build

   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py build
}

check() {
   cd "$srcdir/pytools-$pkgver"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test

   cd "$srcdir/pytools-$pkgver-python2"
   PYTHONPATH="$PWD/build/lib:$PYTHONPATH" py.test2
}

package_python2-pytools() {
   depends=('python2-six' 'python2-decorator' 'python2-appdirs' 'python2-numpy')
   cd "$srcdir/pytools-$pkgver-python2"
   python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python-pytools(){
   depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')
   cd "$srcdir/pytools-$pkgver"
   python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

