# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2019.1.1
pkgrel=1
pkgdesc="A collection of tools for Python"
arch=('any')
url="https://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs' 'python-numpy'
             'python2-numpy')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('757ce5e291f49763afb17210659cadfa2610aa1c9b0912d46107ac50b0d28b7b607198fa4e806e66a7f061d761057a58b00f04a4190009ec7087cffed5f9d5f9')

prepare() {
  cp -a pytools-$pkgver{,-python2}
}

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py pytest

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py pytest
}

package_python-pytools(){
  depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')

  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python2-pytools() {
  depends=('python2-six' 'python2-decorator' 'python2-appdirs' 'python2-numpy')

  cd pytools-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
