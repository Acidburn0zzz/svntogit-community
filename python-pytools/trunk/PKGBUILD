# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: St√©phane Gaudreault <stephane@archlinux.org>

pkgbase=python-pytools
pkgname=('python2-pytools' 'python-pytools')
pkgver=2016.2.6
pkgrel=2
pkgdesc="A collection of tools for Python"
arch=('any')
url="http://mathema.tician.de/software/pytools"
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-decorator' 'python2-decorator'
             'python-six' 'python2-six' 'python-appdirs' 'python2-appdirs' 'python-numpy'
             'python2-numpy')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/p/pytools/pytools-$pkgver.tar.gz")
sha512sums=('87faf5221982815ffecf8bbe4a81eb211111aa8f893132cb5ed0d23e3464b651dcd5070589972c5dc7fac7c4dce768dfe4e064d8346e637af4bfff36aea17833')

prepare() {
  cp -a pytools-$pkgver{,-python2}
}

build() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py build

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py build
}

check() {
  cd "$srcdir"/pytools-$pkgver
  python setup.py ptr

  cd "$srcdir"/pytools-$pkgver-python2
  python2 setup.py ptr
}

package_python-pytools(){
  depends=('python-six' 'python-decorator' 'python-appdirs' 'python-numpy')

  cd pytools-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}

package_python2-pytools() {
  depends=('python2-six' 'python2-decorator' 'python2-appdirs' 'python2-numpy')

  cd pytools-$pkgver-python2
  python2 setup.py install --prefix=/usr --root="$pkgdir" --skip-build --optimize=1
}
