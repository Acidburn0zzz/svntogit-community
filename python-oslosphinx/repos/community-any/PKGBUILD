# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-oslosphinx
pkgname=(python-oslosphinx python2-oslosphinx)
pkgver=4.13.0
pkgrel=1
pkgdesc="OpenStack Sphinx Extensions and Theme"
arch=('any')
url="https://github.com/openstack/oslosphinx"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-requests' 'python2-requests' 'python-six' 'python2-six')
checkdepends=('python-sphinx' 'python2-sphinx')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslosphinx/archive/$pkgver.tar.gz")
sha512sums=('4c8e236a14d973454f238ead711a52b6b1786fb8462ffe7d54bdca7927ed720d05992942c2d21abce1975eb49d6d1a47b8d0dab5ac4dfe185b003b2c3aaed6c3')

prepare() {
  cp -a oslosphinx-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslosphinx-$pkgver
  python setup.py build

  cd "$srcdir"/oslosphinx-$pkgver-py2
  python2 setup.py build
}

package_python-oslosphinx() {
  depends=('python-six' 'python-requests')

  cd "$srcdir"/oslosphinx-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-oslosphinx() {
  depends=('python2-six' 'python2-requests')

  cd "$srcdir"/oslosphinx-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
