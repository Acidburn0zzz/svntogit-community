# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.1.2
pkgrel=2
pkgdesc='CSS parser and selection engine'
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/projects/libcss/"
license=('MIT')
depends=('libparserutils>=0.1.1' 'libwapcaplet>=0.1.1')
source=("http://www.netsurf-browser.org/projects/releases/libcss-$pkgver-src.tar.gz")
sha256sums=('92c112a059bc5ae490392439401005b47ca5c270f891f6317b64aac738ae1f5e')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  
  export CFLAGS="-Wno-error=unused-but-set-variable"
  make PREFIX=/usr COMPONENT_TYPE="lib-shared"
  make PREFIX=/usr COMPONENT_TYPE="lib-static"
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-shared"
  make install PREFIX=/usr DESTDIR="$pkgdir" COMPONENT_TYPE="lib-static"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
