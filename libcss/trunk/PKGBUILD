# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=libcss
pkgver=0.9.0
pkgrel=1
pkgdesc='CSS parser and selection engine'
arch=(x86_64)
url='https://www.netsurf-browser.org/projects/libcss/'
license=(MIT)
makedepends=(netsurf-buildsystem)
depends=('libparserutils>=0.2.4' 'libwapcaplet>=0.4.2')
source=("https://download.netsurf-browser.org/libs/releases/libcss-$pkgver-src.tar.gz")
sha256sums=('b98fba7664bd5307cb39a6101098da76f635a97e157b87f45e9ce42ada9089ef')

prepare() {
  sed -i 's,_BSD_SOURCE,_DEFAULT_SOURCE,' "$pkgname-$pkgver/Makefile"
}

build() {
  export CFLAGS="$CFLAGS -w"
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    COMPONENT_TYPE=lib-shared
}

package() {
  make -C "$pkgname-$pkgver" \
    INCLUDEDIR=include \
    LIBDIR=lib \
    PREFIX=/usr \
    DESTDIR="$pkgdir" \
    install

  install -Dm644 "$pkgname-$pkgver/COPYING" \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim: ts=2 sw=2 et:
