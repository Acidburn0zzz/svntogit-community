# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: unikum <unikum dot pm at gmail.com>
# Contributor: speed145a <jonathan at tagchapter dot com>

pkgname=firewalld
pkgver=0.3.2
pkgrel=2
pkgdesc="A firewall daemon with D-BUS interface providing a dynamic firewall"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python2-slip')
makedepends=('intltool')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install=${pkgname}.install
source=("http://fedorahosted.org/released/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'firewalld-paths.patch')
sha256sums=('a9dce34dca673dedbdfc9ed2dd011340e473699a2d5a935e7563a85a56cb443d'
            '77238eb2dee6c8c3be84631b25095dff60b6abaa060abe5c026c5c8851a68610')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/firewalld-paths.patch
  sed -i 's|^#!.*python$|#!/usr/bin/python2|
          s|^#!.*python -Es$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
  sed -i 's|python|python2|g' fix_python_shebang.sh
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  export  PYTHON=python2
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
