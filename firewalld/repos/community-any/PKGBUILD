# $Id$
# Maintainer: Maxime Gauduin <alucryd at gmail dot com>
# Contributor: unikum <unikum dot pm at gmail.com>
# Contributor: speed145a <jonathan at tagchapter dot com>

pkgname=firewalld
pkgver=0.3.4
pkgrel=1
pkgdesc="A firewall daemon with D-BUS interface providing a dynamic firewall"
arch=('any')
url="http://fedorahosted.org/firewalld"
license=('GPL2')
depends=('ebtables' 'gtk3' 'iptables' 'libnotify' 'networkmanager' 'python2-slip')
makedepends=('intltool' 'docbook-xsl')
backup=('etc/conf.d/firewalld'
        'etc/firewalld/firewalld.conf')
install=${pkgname}.install
source=("http://fedorahosted.org/released/${pkgname}/${pkgname}-${pkgver}.tar.bz2"
        'firewalld-paths.patch')
sha256sums=('c5d58c23534f841a50bd3d03077ab37f6fcd62dc94d77233764e732b3a2962f1'
            'e2f96a3fbd8e8b9168d49c9f39e76a69fff169e69f302d0c12e458ed698cabc6')

prepare() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  patch -Np1 -i "${srcdir}"/firewalld-paths.patch
  sed -i 's|^#!.*python$|#!/usr/bin/python2|
          s|^#!.*python -Es$|#!/usr/bin/python2|' $(grep -rl '^#!.*python')
  sed -i 's|python|python2|g' fix_python_shebang.sh
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  export  PYTHON=python2
  ./configure --prefix=/usr --sbindir=/usr/bin --sysconfdir=/etc --disable-schemas-compile
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
