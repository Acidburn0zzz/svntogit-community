# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-sekizai
pkgname=python-django-sekizai
pkgver=3.0.0
pkgrel=1
pkgdesc="Django Template Blocks with extra functionality"
arch=(any)
url="https://github.com/django-cms/django-sekizai"
license=(BSD)
depends=(python-django python-django-classy-tags)
makedepends=(python-setuptools)
# test files are missing in pypi sdist tarball
# https://github.com/django-cms/django-sekizai/issues/115
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${_name}-${pkgver}.tar.gz::https://github.com/divio/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('1e307a5dc04b2f5492bb954146ab0a421b2342497ded5d3b5ce34d83afc17a82f306ad80c3c5e028b4f3f730d2b63f400c6a519d257b19f1e304ef092c531891')
b2sums=('c8461a935177b6655e42d6ef043f728dbe025e625d1b70e6045b19972f0217acf1fa0605a33c3ddfa5a0644cdfbbeb6f676aeb456ea0907ac14489e1c19bdd7b')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  # project uses deprecated way of running tests: https://github.com/django-cms/django-sekizai/issues/116
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
