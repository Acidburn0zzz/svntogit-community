# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=7.0.0
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
depends=('python-setuptools' 'python-dateutil' 'python-text-unidecode')
makedepends=('python-pip' 'python-wheel' 'python-pytest-runner')
checkdepends=('python-validators' 'python-ukpostcodeparser' 'python-random2'
              'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('e3f747115b431151f3c5bd88000dbbf2343b053f043636a4e65d87a901507ce19d1a07d1d6e7341383c4ac02d43a05aec8edada014f183088dc1004adfcb7a4b')

prepare() {
  sed -i -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python setup.py egg_info
  python -m pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
