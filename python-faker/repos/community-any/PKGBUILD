# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgname=python-faker
pkgver=3.0.1
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='https://faker.readthedocs.io/en/master/'
license=('MIT')
provides=("python-fake-factory=$pkgver")
conflicts=('python-fake-factory')
replaces=('python-fake-factory')
depends=('python-setuptools' 'python-dateutil' 'python-text-unidecode')
makedepends=('python-pip' 'python-wheel')
checkdepends=('python-validators' 'python-ukpostcodeparser' 'python-pytest' 'python-random2'
              'python-freezegun')
source=("$pkgname-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('58a6b32dab1a1de3840b49d92b179d15a759c4d8d547aabb1bdb3e2f0fb96e4b92870db0fe30c02dac1523296a92724203819b8d490f97be2ad7a835f5a5b964')

prepare() {
  sed -i -e 's/<3.9/<6/' -e 's/==/>=/' faker-$pkgver/setup.py
}

build() {
  cd faker-$pkgver
  python setup.py build
}

check() {
  cd faker-$pkgver
  python -m pytest
}

package() {
  cd faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}
