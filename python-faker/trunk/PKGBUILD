# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: dnuux <dnuuxx@gmail.com>

pkgbase=python-faker
pkgname=(python-faker python2-faker)
pkgver=0.7.18
pkgrel=1
pkgdesc='Faker generates fake data for you.'
arch=('any')
url='http://www.joke2k.net/faker/'
license=('MIT')
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil')
checkdepends=('python-ukpostcodeparser' 'python2-ukpostcodeparser'
              'python-mock' 'python2-mock' 'python-email-validator' 'python2-email-validator'
              'python2-ipaddress')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/joke2k/faker/archive/v$pkgver.tar.gz")
sha512sums=('2a2e84d01820ff4f55b43f212f51e60eae67f310b770514ee42f8fa5b68d850e4f73f35d9e1f8c8f4375e83c49a4f1b8299e2e32b537bd4648b590b2e22e9f40')

prepare() {
  cp -a faker-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/faker-$pkgver
  python setup.py build

  cd "$srcdir"/faker-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/faker-$pkgver
  python setup.py test

  cd "$srcdir"/faker-$pkgver-py2
  python2 setup.py test
}

package_python-faker() {
  depends=('python-setuptools' 'python-dateutil')
  provides=("python-fake-factory=$pkgver")
  conflicts=('python-fake-factory')
  replaces=('python-fake-factory')

  cd "$srcdir"/faker-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

package_python2-faker() {
  depends=('python2-setuptools' 'python2-dateutil' 'python2-ipaddress')
  provides=("python2-fake-factory=$pkgver")
  conflicts=('python2-fake-factory')
  replaces=('python2-fake-factory')

  cd "$srcdir"/faker-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  mv "$pkgdir"/usr/bin/faker{,2}
  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.txt
}

# vim:set ts=2 sw=2 et:
