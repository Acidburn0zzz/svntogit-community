# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=cosign
pkgver=1.3.0
pkgrel=1
pkgdesc="Container Signing with support for ephemeral keys and Sigstore signing"
arch=('x86_64')
url="https://github.com/sigstore/cosign"
license=('Apache')
depends=('pcsclite')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('d9ef11e22e7cb874ba8d0b4af9eabefbcc42341acd1d5c2645905fb420801fe8ff6e5f580548a9b562a5f5f5db71a9c8cb50e9a013d903b80fa2d969d866e3ff')
b2sums=('4f9f8b5d67df72a1e19db82fa57b3a65e1c32b9c11913470d7e7b5d2c4b3af00e05493b59ff3da17ff56f6f14528b67dc0550c66e138c1c05f0df3226128bf95')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/cosign "${pkgdir}/usr/bin/${pkgname}"
}
