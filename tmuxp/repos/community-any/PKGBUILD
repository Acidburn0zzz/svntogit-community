# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.5.8
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6e026742d8fa8f002de9a2c7144019a2a52965055886993c41fdef23f20b8d596a3ef005125bc9d7b95a68ca10cdc91f644050132beb4ab751d4641c7d4ed104')
b2sums=('cd4131054174f06485afedfd0b9957f42237df2d9abbe1052c7167b33a5c87aec1c0831032f0e36b9f8fe22dfbcfece9979a440fb57649a7d4962cacba8da1fe')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES,README.rst}
}

# vim:set ts=2 sw=2 et:}
