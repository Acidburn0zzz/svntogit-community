# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=tmuxp
pkgver=1.6.1
pkgrel=1
pkgdesc="Tmux session manager built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b6f3ef4039cb5c06b8379dbefd2bfbd5f87df76865da63748a777d202cfa19178c555da29f35b290647f87b725aedb9c05c41bc34d0bcccd7f7fa63666d1528b')
b2sums=('0953bbf65d25785589ae5794752d4b8831b5a99f43b886f01459a3b4f9242f0a0091e21a7791096b96a7dff4b37fdc52b8f4e5c4594fa7fdaa780310d5a76d07')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  # test_workspacebuilder.py::test_pane_order does not return:
  # https://github.com/tmux-python/tmuxp/issues/377
  pytest -v -k "not test_pane_order"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -vDm 644 {CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 examples/*.{json,yaml} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
}

# vim:set ts=2 sw=2 et:}
