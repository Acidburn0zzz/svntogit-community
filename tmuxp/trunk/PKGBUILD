# Maintainer: David Runge <dave@sleepmap.de>
pkgname=tmuxp
pkgver=1.5.2
pkgrel=1
pkgdesc="tmux session manager. built on libtmux"
arch=('any')
url="https://tmuxp.git-pull.com/en/latest/"
license=('MIT')
depends=('python-click' 'python-colorama' 'python-kaptan' 'python-setuptools' 'python-libtmux')
checkdepends=('python-pytest' 'python-pytest-rerunfailures')
# pypi sources don't include tests: https://github.com/tmux-python/tmuxp/issues/431
# pypi sources don't include tests/scripts: https://github.com/tmux-python/tmuxp/issues/474
# source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz")
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('fac531d47c1cedd1836184d2c898801a60172d4975f2fca1538eb04a875e653c254212afa07da54da8482df4adbc48b608a77c1da31509bc3dc141956366b42c')

prepare() {
  cd "${pkgname}-${pkgver}"
  # remove all hard version pinning:
  # https://github.com/tmux-python/tmuxp/issues/432
  sed -e 's/==/>=/g' -i requirements/*.txt
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
# test_workspacebuilder.py does not return:
# https://github.com/tmux-python/tmuxp/issues/377
  py.test -k "not test_workspacebuilder"
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {CHANGES,README.rst}
}

# vim:set ts=2 sw=2 et:}
