# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.7
pkgrel=2
pkgdesc='Deepin Window Manager'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'granite'
         'deepin-mutter' 'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz"
        switcher-fix.patch)
sha512sums=('f2664d7fdd23e1824e6575ec5ae7051cd70eec7785ea4d16bdad1e8c9c84e6363180d5b4266666c92ad1073637fe4859398eec58e4738215eb8f39cc057806c1'
            'ad798edea74e9e985a4f286fb0a43dfaf00cd7d642af6214b706613abbd1e920517dccec2e728ed44d2ab43d7c4fb325f05fce20053462c5e553ab98bb7ec070')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../switcher-fix.patch

  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="${pkgdir}" install
}
