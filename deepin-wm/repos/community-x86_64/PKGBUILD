# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.37
pkgrel=2
pkgdesc='Deepin Window Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'deepin-menu' 'granite' 'deepin-mutter'
         'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
options=(debug)
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz"
        https://github.com/linuxdeepin/deepin-wm/pull/49.patch)
sha512sums=('92988a19bb260a4373002ae72ad87cae06e18c48ec799b058e23ac62d06c94819cd7e84a046ad8940737eb82a808f13e62717bc6ce713a8e09839ee0a9e39cd1'
            '0a10ed1a3b081cb88f10741a5db2ee9d439e91ceafa8b0b335f03eb4d02b8e993eab54a5556d49b859d3465610ad30aeefd79c2ba1dece85366915fce23e6acd')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../49.patch
  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' $pkgname-$pkgver/src/Background/BackgroundSource.vala
}

build() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"

  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
