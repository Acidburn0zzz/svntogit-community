# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-wm
pkgver=1.9.35
pkgrel=1
pkgdesc='Deepin Window Manager'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-wm"
license=('GPL3')
depends=('bamf' 'clutter-gtk' 'deepin-desktop-schemas' 'deepin-menu' 'granite' 'deepin-mutter'
         'gnome-desktop' 'libgee')
makedepends=('gnome-common' 'intltool' 'vala')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-wm/archive/$pkgver.tar.gz")
sha512sums=('524134cd2e37114cf584b810b286932afd098747131b0273658340c2c2f6ccd91474afecdece1180e48120dd54a9f5aebbfad129d30ce76a85253c4fa6a23c26')

prepare() {
  export CFLAGS="$CFLAGS -DWNCK_I_KNOW_THIS_IS_UNSTABLE"

  sed -i 's#/usr/share/backgrounds/default_background.jpg#/usr/share/backgrounds/deepin/desktop.jpg#' $pkgname-$pkgver/src/Background/BackgroundSource.vala
}

build() {
  cd $pkgname-$pkgver
  ./autogen.sh \
    --prefix='/usr' \
    --disable-schemas-compile
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
