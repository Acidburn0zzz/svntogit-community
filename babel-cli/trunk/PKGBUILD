# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Mario Finelli <mario dot finelli at yahoo dot com>
# Contributor: Andy Weidenbaum <archbaum at gmail dot com>
# Contributor: Peter Wu <lekensteyn at gmail dot com>

pkgname=babel-cli
pkgver=6.24.0
pkgrel=1
pkgdesc="A compiler for writing next generation JavaScript"
arch=('any')
url="https://babeljs.io/"
license=('MIT')
depends=('nodejs')
makedepends=('npm')
provides=('nodejs-babel-cli')
conflicts=('nodejs-babel-cli' 'openbabel')
replaces=('nodejs-babel-cli')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz"
        LICENSE)
noextract=($pkgname-$pkgver.tgz)
sha256sums=('55803277dbe11695eb1304af43f67a55c437395d6efe6b80e5aaf75e5d3f1da5'
            '8a5a22eb7991f5a022b91273b2395c2342f56fb43deddd29c248efb40c8ad62c')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
