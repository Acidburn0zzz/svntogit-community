# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>

pkgname=lxpanel
pkgver=0.5.12
pkgrel=1
pkgdesc='Lightweight X11 desktop panel for LXDE'
arch=('i686' 'x86_64')
license=('GPL2')
url='http://lxde.org/'
groups=('lxde')
depends=('gtk2' 'alsa-lib' 'menu-cache' 'lxmenu-data' 'libwnck')
makedepends=('docbook-xml' 'docbook-xsl' 'wireless_tools')
optdepends=('wireless_tools: netstat plugin')
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz
        lxpanel-0.5.12-automake-1.13-support.patch)
sha256sums=('c29997c522f138e4beb02fc549c84136fc840a836c1f69b400d90d2d4e91de1b'
            'a35c786e3cd2977c5cf018e126c7f309b7fc4c15f9b821895e1f6fe25125f68f')

build() {
  cd $srcdir/$pkgname-$pkgver
 
  patch -Np0 -i $srcdir/lxpanel-0.5.12-automake-1.13-support.patch 
  ./autogen.sh

  CFLAGS+=' -lgmodule-2.0' ./configure \
    --sysconfdir=/etc \
    --prefix=/usr \
    --enable-man 
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
