# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=3.0.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="http://docs.python-zeep.org"
depends=('python-appdirs' 'python-cached-property' 'python-defusedxml' 'python-isodate'
         'python-lxml' 'python-requests' 'python-requests-toolbelt' 'python-six' 'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-tornado'
              'python-aioresponses' 'python-xmlsec')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('02b8b16a6375181697b376a7951477f0e62adc520961124f90ff61300d3ac71ccdcb298a9a0692f95f11819c12e6eb4b481bf51f69e131f59fd52e33170adcac')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest || warning "https://github.com/mvantellingen/python-zeep/issues/724"
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
