# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-zeep
pkgver=3.3.0
pkgrel=1
pkgdesc="A fast and modern Python SOAP client"
arch=('any')
license=('MIT')
url="http://docs.python-zeep.org"
depends=('python-appdirs' 'python-attrs' 'python-cached-property' 'python-defusedxml'
         'python-isodate' 'python-lxml' 'python-requests' 'python-requests-toolbelt' 'python-six'
         'python-pytz')
checkdepends=('python-pytest-runner' 'python-freezegun' 'python-mock' 'python-pretend'
              'python-pytest-cov' 'python-requests-mock' 'python-pytest-tornado'
              'python-aioresponses' 'python-xmlsec')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mvantellingen/python-zeep/archive/$pkgver.tar.gz")
sha512sums=('3e328115e6d4912f04d867140c73ffade6fb4849cb05f057b9226814c03df2042288954096cb6308ede43f5a8141cb9de2e03a018f903b0a3df150f74911f3f7')

prepare() {
  sed -i -e 's/==/>=/' -e '/flake8/d' -e '/isort/d' python-zeep-$pkgver/setup.py

  sed -i 's/get_marker/get_closest_marker/' python-zeep-$pkgver/tests/conftest.py
}

build() {
  cd python-zeep-$pkgver
  python setup.py build
} 

check() {
  cd python-zeep-$pkgver
  python setup.py pytest || warning "https://github.com/mvantellingen/python-zeep/issues/724"
}

package() {
  cd python-zeep-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
