# Maintainer: Chih-Hsuan Yen <yan12125@gmail.com>

pkgname=lxqt-archiver
pkgver=0.1.1
pkgrel=1
pkgdesc="A simple & lightweight desktop-agnostic Qt file archiver"
arch=("x86_64")
url="https://lxqt.org"
license=("GPL2")
depends=('libfm-qt' 'qt5-x11extras' 'glib2' 'json-glib' 'libfm-qt.so')
makedepends=('cmake' 'qt5-tools' 'lxqt-build-tools')
source=("https://github.com/lxqt/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('00a967427428951096eb2f4a9b5fc492dde6e7c8405cffc7dc4460f77d7686bd')

build() {
  # Bring in -D_FORTIFY_SOURCE=2
  export CFLAGS+=" $CPPFLAGS"
  export CXXLAGS+=" $CPPFLAGS"

  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBEXECDIR=lib

  make -C build
}

package() {
  cd build

  make DESTDIR="$pkgdir" install
}
