# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Micha Alt <micha.tucker at gmail dot com>

pkgname=gulp
pkgver=4.0.1
pkgrel=1
pkgdesc="The streaming build system"
arch=('any')
url="http://gulpjs.com/"
license=('MIT')
depends=('nodejs' 'semver')
makedepends=('npm')
source=("https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz")
noextract=($pkgname-$pkgver.tgz)
sha512sums=('c8356d56e9f1ac076c93bac857f6fb95548189f3cdd44a9eeb04e3b04486ac570bf8c115cda69e35392952e193529b65a12394fbca099bf94125b80f57eb4c03')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  mkdir -p "$pkgdir/usr/share/licenses/$pkgname"
  ln -s "../../../lib/node_modules/$pkgname/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/"

  # Fix permissions
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
  done
}
