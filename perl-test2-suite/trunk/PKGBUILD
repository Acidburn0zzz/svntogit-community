# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000063
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('2e5ef77fbcaffefe02c92be09dd2cb6af4144076f012fc5352449817f3913adbd972c83832db4aee8d523e45c8f5350a1a6f910c50b07e14342a4a2d5d4288f1')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
