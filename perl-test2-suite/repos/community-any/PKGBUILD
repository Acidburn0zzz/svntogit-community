# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000122
pkgrel=2
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
depends=('perl-module-pluggable' 'perl-importer' 'perl-scope-guard' 'perl-sub-info'
         'perl-term-table' 'perl-test-simple>=1.302158')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('6332b4300ad37d16378a37a931f470b33c4c82603e4e0b3a0c42c707c75a735908001daecc47ed73361635aabd65c0909f37a74d731bc958f4345ea75207dab2')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
