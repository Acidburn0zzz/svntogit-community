# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000072
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
# Specify version bounds to pick up perl-test-simple in [community], since perl includes an old one.
depends=('perl-importer' 'perl-sub-info' 'perl-term-table' 'perl-test-simple>=1.300000')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('34f6f781c29fcb79146ee2cd979addfe34edb21db99f38d417a1efacaac8400731fc4d986c8b904383d251d95967c2d922dba28429a862b40cb33c346e55b353')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
