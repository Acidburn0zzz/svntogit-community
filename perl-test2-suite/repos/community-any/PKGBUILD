# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-test2-suite
pkgver=0.000120
pkgrel=1
pkgdesc="Distribution with a rich set of tools built upon the Test2 framework."
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Test2-Suite"
depends=('perl-module-pluggable' 'perl-importer' 'perl-scope-guard' 'perl-sub-info'
         'perl-term-table' 'perl-test-simple>=1.302158')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/E/EX/EXODIST/Test2-Suite-$pkgver.tar.gz")
sha512sums=('bdaa2ee8eb6e4444b40598f42a87b29b7b049b8f8cd1629d3bd0c4a56de0579b610898312e0a6cb039f4107d4e8ca00510b31485e96a1e01db87a28102ea2c92')

build() {
  cd Test2-Suite-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Test2-Suite-$pkgver
  make test
}

package() {
  cd Test2-Suite-$pkgver
  make DESTDIR="$pkgdir" install
}
