# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=0.0.0.20200806
_commit=87aee5e3a94b989c66279b9e49f1a8d1f935b906
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('x86_64')
url="https://github.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules' 'ninja' 'uthash' 'xorgproto')
source=("https://github.com/fcitx/xcb-imdkit/archive/$_commit/xcb-imdkit-$_commit.tar.gz")
sha512sums=('68a3ccf6aa64926fa28528a978f0fe8cb4d842fed10d248c1e210ad6a602ed62886d85512f2c90f7dadb9231008226328c5205da2e2826b72c328b9e1fc99535')

build() {
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$_commit
  ninja test
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
