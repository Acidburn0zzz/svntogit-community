# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=0.0.0.20201028
_commit=6a5280cf92f8715e483d1511a6d0cd50efea6633
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('x86_64')
url="https://github.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules' 'ninja' 'uthash' 'xorgproto')
source=("https://github.com/fcitx/xcb-imdkit/archive/$_commit/xcb-imdkit-$_commit.tar.gz")
sha512sums=('f2cead24a0286669f5c1228ba17949c04d43c4c3db14df1a5147eb03e8eff775319d46cc24b880ede3133b4cc97771aa500fd9e6f2786208f2c586a7c073e819')

build() {
  cd $pkgname-$_commit

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  ninja
}

check() {
  cd $pkgname-$_commit
  ninja test
}

package() {
  cd $pkgname-$_commit
  DESTDIR="$pkgdir" ninja install
}
