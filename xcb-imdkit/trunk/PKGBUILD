# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=0.0.0.20171205
_commit=b82eddb3432b25f0db4aa7db4f68c6744ad895c1
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('i686' 'x86_64')
url="https://gitlab.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules')
source=("https://gitlab.com/fcitx/xcb-imdkit/-/archive/$_commit/xcb-imdkit-$_commit.tar.bz2")
sha512sums=('904eb76fc203c126100f3ecc08f7aa12d58901687f05219b3e8a17f59630d12af225163f7bcc653e17a9c704755bc53fe3073802a302891814407ef9c7ce2d25')

build(){
  cd $pkgname-$_commit
 
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
