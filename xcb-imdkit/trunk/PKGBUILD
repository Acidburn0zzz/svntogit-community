# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=xcb-imdkit
pkgver=0.0.0.20200402
_commit=d30ec16ef36478070acbe0a5b1ae290ff9d8b73e
pkgrel=1
pkgdesc="Input method development support for xcb"
arch=('x86_64')
url="https://github.com/fcitx/xcb-imdkit"
license=('GPL')
depends=('xcb-util' 'xcb-util-keysyms')
makedepends=('extra-cmake-modules' 'xorgproto')
source=("https://github.com/fcitx/xcb-imdkit/archive/$_commit/xcb-imdkit-$_commit.tar.gz")
sha512sums=('05354314d4c4859dded0a5e7d203745190741245885998556f9b434a5b6243e56afa50c1ec2decd854e6f9424f280abfffdf82ef92005c8c1f7a71e7722d30e4')

build() {
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib .
  make
}

check() {
  cd $pkgname-$_commit
  make test
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}
