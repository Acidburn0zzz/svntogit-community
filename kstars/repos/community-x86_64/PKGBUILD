# Maintainer: Felix Yan <felixonamrs@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=kstars
pkgver=3.5.7
pkgrel=2
epoch=1
pkgdesc='Desktop Planetarium'
url='https://kde.org/applications/education/kstars/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(stellarsolver libraw kplotting knewstuff knotifyconfig qt5-datavis3d libindi qt5-quickcontrols qt5-websockets qtkeychain
         breeze-icons hicolor-icon-theme)
makedepends=(extra-cmake-modules kdoctools eigen)
optdepends=('xplanet: XPlanet support')
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/education/kstars/-/commit/6978d77d.patch
        stellarsolver2.patch)
sha256sums=('aa8f122ee9b8e81334433191eab276c361112b9dcb9bcf8dfa0851e87a100d06'
            'SKIP'
            '24b01cebcf0092b987cce583de73cf860aea32e96d68433984db2c18af683899'
            'f0970f1ce51009b81d986b30b3450584ca39ab1c76854762a0ec224952e56551')
validpgpkeys=(259D9FCEE9175351965633696D9CE2AEE028C4F3) # Jasem Mutlaq <mutlaqja@ikarustech.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 6978d77d.patch
  patch -d $pkgname-$pkgver -p1 < stellarsolver2.patch # Support stellarsolver 2.0
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DBUILD_TESTING=OFF \
    -DCMAKE_C_FLAGS="$CFLAGS -ffat-lto-objects" \
    -DCMAKE_CXX_FLAGS="$CXXFLAGS -ffat-lto-objects"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
