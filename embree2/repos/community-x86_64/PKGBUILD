# $Id: PKGBUILD 313188 2018-03-28 22:30:42Z archange $
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=embree
pkgname=${_pkg}2
pkgver=2.17.5
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'openexr')
source=("${_pkg}-${pkgver}.tar.gz::https://github.com/embree/embree/archive/v${pkgver}.tar.gz")
sha256sums=('1a6fb2d1b32a10eb165c8bce6c61fc9a7671ca49fcd6a9cde512fbcfff49c1db')

build() {
    cd ${_pkg}-${pkgver}
    cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DEMBREE_TUTORIALS=OFF \
      -DEMBREE_MAX_ISA="AVX512SKX"
    # Embree detects actual ISA at runtime
    make
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install
}

