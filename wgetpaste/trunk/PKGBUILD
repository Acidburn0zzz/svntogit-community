# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>

pkgname=wgetpaste
pkgver=2.18
pkgrel=2
pkgdesc="A script that automates pasting to a number of pastebin services"
url="http://wgetpaste.zlin.dk/"
arch=('any')
license=('custom')
depends=('wget')
optdepends=('xclip: interface to Xclipboard')
source=("http://wgetpaste.zlin.dk/$pkgname-$pkgver.tar.bz2"
        "http://wgetpaste.zlin.dk/$pkgname.example")
md5sums=('0085d679445af9e6f695d036abee106d'
         '77392c788659cc648a59df2d239ebe19')

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  install -D -m755 ${pkgname} ${pkgdir}/usr/bin/${pkgname}
  install -D -m644 _${pkgname} $pkgdir/usr/share/zsh/site-functions/_${pkgname}
  install -D -m644 ${srcdir}/${pkgname}.example ${pkgdir}/usr/share/${pkgname}/${pkgname}.example

# license
  sed -n '2,5p' wgetpaste > LICENSE
  install -D -m644 LICENSE ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}
