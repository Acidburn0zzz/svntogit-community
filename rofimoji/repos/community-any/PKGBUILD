# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

pkgname=rofimoji
pkgver=4.3.0
pkgrel=5
pkgdesc='Character picker for rofi'
arch=('any')
url=https://github.com/fdw/rofimoji
license=('MIT')
depends=('emoji-font' 'python-configargparse' 'python-xdg' 'rofi')
makedepends=('python-setuptools')
optdepends=('wl-copy: for the Wayland clipboarder'
            'wtype: for the Wayland typer'
            'xclip: for one of the X.Org clipboarders'
            'xsel: for one of the X.Org clipboarders'
            'xdotool: for the X.Org typer')
install=$pkgname.install
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('b432dab44de5046dc26bd95d583102bec9373da6891a2d44131e6fc391c9bf980530d33f25fce66917496cb772c66ba840de87c61443cf12198b52cd32f6b694')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
