# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipykernel
pkgver=6.7.0
pkgrel=1
pkgdesc="The ipython kernel for Jupyter"
arch=('any')
url="https://pypi.org/project/ipykernel/"
license=('BSD')
depends=('python-traitlets' 'python-tornado' 'ipython' 'python-jupyter_client' 'python-debugpy')
makedepends=('python-setuptools' 'ipython' 'python-jupyter_client')
checkdepends=('python-pytest' 'python-mock' 'python-flaky')
source=("ipykernel-$pkgver.tgz::https://github.com/ipython/ipykernel/archive/v$pkgver.tar.gz")
md5sums=('6c0665b1808224597ec8b576eb5f1bb0')

# dep cycle with python-jupyter_client

build() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py build
}

check() {
  cd "$srcdir/ipykernel-$pkgver"
  # python3 -m pytest
  # tests fail both locally and in a chroot, for different reasons
}

package() {
  cd "$srcdir/ipykernel-$pkgver"
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
