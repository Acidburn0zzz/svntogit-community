# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jeremy "Ichimonji10" Audet <ichimonji10 at gmail dot com>
# Contributor: pumpkin <pumpkin at mailoo dot org>
# Contributor: Vsevolod Balashov <vsevolod at balashov dot name>

pkgname=gunicorn
pkgver=20.0.1
pkgrel=1
pkgdesc='WSGI HTTP Server for UNIX'
arch=('any')
url='https://gunicorn.org/'
license=('MIT')
depends=('python-setuptools')
optdepends=('python-eventlet: For asynchronous request handling with eventlet.'
            'python-gevent: For asynchronous request handling with gevent.')
checkdepends=('python-aiohttp' 'python-coverage' 'python-pytest' 'python-pytest-cov'
              'python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/benoitc/$pkgname/archive/$pkgver.tar.gz")
sha512sums=('f38803bc69d14ef7a1a2ca94c5f4664088133f585ecf0b715ffaeee2c0c92f94965070343845db08843542b210a873eb6789a6702772916fa84d3e7638170c1a')

prepare() {
  sed -e 's/==/>=/' -e 's/,<.*$//' -i gunicorn-$pkgver/requirements_test.txt
}

build() {
  cd gunicorn-$pkgver
  python setup.py build
}

check() {
  cd gunicorn-$pkgver
  python setup.py pytest
}

package() {
  cd gunicorn-$pkgver
  python setup.py install --root="$pkgdir" --prefix=/usr --optimize=1
  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
