# $Id$
# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Alexandre Bique <bique.alexandre@gmail.com>
# Contributor: Louis R. Marascio <lrm@fitnr.com>
# Contributor: Cody Maloney <cmaloney@theoreticalchaos.com>

pkgname=gtest
pkgver=1.7.0
pkgrel=5
pkgdesc="Google Test - C++ testing utility based on the xUnit framework (like JUnit)"
arch=('i686' 'x86_64')
url="http://code.google.com/p/googletest/"
license=('custom:BSD3')
depends=('gcc-libs' 'sh')
makedepends=('python2' 'cmake')
source=(http://googletest.googlecode.com/files/$pkgname-$pkgver.zip
        pkgconfig.patch
        gtest.pc.in)
sha512sums=('8859369f2dd32cbc2ac01aba029aa3ff20a321f40658b9643aff442d34c33468221866b801b28c66a28af47dbcd362d26941fc98db92b6efb7e41ea5b7be1a07'
            '7e9ad4cd470eaab7ed5fb1cce1d5721d40fc03a62b0c895b8f9657679ffa43f91b99800bb1e02a6c5c2d0a72e94bb8ea008aa6e74fdb1460b1326a84da5e9f91'
            '4760a6e039eaeb6c3b917cfac173e4738a5d4675b89b84ad91b0260b7f4386ce3561529c0b7d1f8c3792beb3c8535470372b196585ec4468739a17fa314637e0')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  cp "${srcdir}/gtest.pc.in" .
  sed -r "s|(Version:) .+|\1 ${pkgver}|" -i gtest.pc.in
  patch -p1 < "${srcdir}/pkgconfig.patch"
  mkdir build
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # this is odd but needed only to generate gtest-config.
  ./configure --prefix=/usr

  cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_SKIP_RPATH=ON ..
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 755 scripts/gtest-config -t "$pkgdir"/usr/bin
  install -Dm 644 m4/gtest.m4 -t "$pkgdir"/usr/share/aclocal
  install -Dm 644 build/libgtest{,_main}.so -t "$pkgdir"/usr/lib
  install -Dm 644 include/gtest/*.h -t "$pkgdir"/usr/include/gtest
  install -Dm 644 include/gtest/internal/*.h -t "$pkgdir"/usr/include/gtest/internal
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 fused-src/gtest/* -t "$pkgdir"/usr/src/gtest/src
  install -Dm 644 cmake/* -t "$pkgdir"/usr/src/gtest/cmake

  # undo pkg-config patch so subprojects including this don't try to generate it
  patch -Rp1 < "${srcdir}/pkgconfig.patch"
  install -Dm 644 CMakeLists.txt -t "$pkgdir"/usr/src/gtest
}

# vim:set ts=2 sw=2 et:
