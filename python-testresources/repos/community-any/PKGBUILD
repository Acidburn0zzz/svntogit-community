# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-testresources
pkgname=('python-testresources' 'python2-testresources')
pkgver=1.0.0
pkgrel=1
pkgdesc="Testresources, a pyunit extension for managing expensive test resources"
arch=('any')
license=('Apache' 'BSD')
url="https://launchpad.net/testresources"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-testtools' 'python2-testtools')
source=("https://pypi.python.org/packages/source/t/testresources/testresources-$pkgver.tar.gz")
sha512sums=('7c9f9cbcc62066ab4a75d68780d88925484eeee23e2e0ef0395b63ec2a92968a6969d1bd6f483945e32ff5566e686b426f2f37daf4f803d2ef779032ea97cc6b')

prepare() {
  cp -a testresources-$pkgver{,-py2}
}

build() {
  cd "$srcdir/testresources-$pkgver"
  python setup.py build

  cd "$srcdir/testresources-$pkgver-py2"
  python2 setup.py build
}

check() {
  cd "$srcdir/testresources-$pkgver"
  python -m testtools.run discover

  cd "$srcdir/testresources-$pkgver-py2"
  python2 -m testtools.run discover
}

package_python-testresources() {
  depends=('python')

  cd testresources-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

package_python2-testresources() {
  depends=('python2')

  cd testresources-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
