# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: flixie <69one@gmx.net>
# Contributor: Imanol Celaya <ornitorrincos@archlinux-es.org>
pkgname=luxrender
pkgver=1.6
_pkgver=b3f85cf7742f
pkgrel=17
pkgdesc="Rendering system for physically correct, unbiased image synthesis"
arch=('x86_64')
url="http://www.luxrender.net/"
license=('GPL')
depends=('boost-libs' 'embree2' 'freeimage' 'openexr' 'openimageio' 'libpng' 'opencl-icd-loader' 'libgl' 'fftw')
optdepends=('luxblend25: Blender exporter' 'qt4: Qt GUI'
            'python: pylux Python interface')
makedepends=('cmake' 'boost' 'mesa' 'qt4' "luxrays=$pkgver" 'python' 'opencl-headers')
source=($pkgname-$pkgver.tar.bz2::https://bitbucket.org/luxrender/lux/get/v${pkgver/./}.tar.bz2 \
        force_python3.diff luxrender-gcc7.patch fix_deprecated_native.diff no-lib64.diff py3.7.diff)
sha256sums=('29144f657087644086928a66421df5d25fc4dccd05970200932f5693650ddecf'
            '916a45aec1326e6990c1ff9f141080c17a33ed793c44d1af9a2dacb9b21c19d2'
            'e51e33ac85c247696a0cab7751b5e91a23df5dac45af05b9ee68299e4a37dc6d'
            '0a35caff0cb72fb57c13ba932403849b9280f56506676d0144eceaf13e470209'
            '6172c8dc1616dd5e396f1c9dc538f74a62959a818690839ea700db19745c13dc'
            'e1b86f3d678f765ff22001db388c6e12cdd342d1771dc5247d061590164d8a9f')

prepare() {
  cd "$srcdir"/luxrender-lux-$_pkgver

  patch -Np1 < ../force_python3.diff
  # fix build with GCC 7 (Fedora)
  patch -Np1 < ../luxrender-gcc7.patch
  # fix build with boost 1.66 (the deprecated native() method was removed)
  patch -Np1 < ../fix_deprecated_native.diff
  # fix library path on x86_64
  patch -Np1 < ../no-lib64.diff
  # fix for python 3.7
  patch -Np1 < ../py3.7.diff
}

build() {
  cd "$srcdir"/luxrender-lux-$_pkgver


  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DLUXRAYS_DISABLE_OPENCL=OFF \
    -DPYTHON_CUSTOM=ON \
    -DPYTHON_LIBRARIES=/usr/lib/libpython3.7m.so \
    -DPYTHON_INCLUDE_PATH=/usr/include/python3.7m/ \
    -DCMAKE_EXE_LINKER_FLAGS=-lpthread
  make
}

package() {
  cd "$srcdir"/luxrender-lux-$_pkgver
  make DESTDIR="$pkgdir" install

  # install pylux
  install -D -m644 pylux.so "$pkgdir"/usr/lib/python3.7/site-packages/pylux.so
}

# vim:set ts=2 sw=2 et:
