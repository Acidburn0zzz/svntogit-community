# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Morten Linderud <foxboron@archlinux.org>

pkgname=netavark
_commit=1c7c51a53641fb363f3e07582d6646cbc844938a  # refs/tags/v1.0.0^{}
pkgver=1.0.0
pkgrel=1
pkgdesc="Container network stack"
arch=(x86_64)
url="https://github.com/containers/netavark"
license=(Apache)
depends=(gcc-libs glibc)
makedepends=(cargo git libgit2 mandown)
provides=(container-network-stack)
source=("git+https://github.com/containers/netavark.git#commit=$_commit")
sha256sums=('SKIP')
# NOTE: pinning commit until upstream clarifies commitment to chain of trust:
# https://github.com/containers/netavark/issues/231
# validpgpkeys=('74FE091D25519980B2D84447160386BECB6F0643')  # Brent Baude <bbaude@redhat.com>

prepare() {
  cd $pkgname
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  export CARGO_TARGET_DIR=target

  cd $pkgname
  cargo build --frozen --release --all-features
  make -C docs
}

check() {
  export RUSTUP_TOOLCHAIN=stable

  cd $pkgname
  cargo test --frozen --all-features
}

package() {
  cd $pkgname
  install -vDm 755 target/release/$pkgname -t "$pkgdir/usr/lib/podman/"
  make DESTDIR="$pkgdir" PREFIX=/usr install -C docs
  install -vDm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname/"
}
