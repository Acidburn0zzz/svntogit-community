# $Id$
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk@gmail.com>
# Contributor: Pantelis Panayiotou <p.panayiotou@gmail.com>

pkgname=catfish
pkgver=1.4.5
pkgrel=1
pkgdesc='Versatile file searching tool'
arch=('any')
url='https://launchpad.net/catfish-search'
license=('GPL')
install="$pkgname.install"
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-pexpect' 'python-ptyprocess' 'python-xdg' 'xdg-utils')
makedepends=('intltool' 'python-distutils-extra')
optdepends=('mlocate: filename search'
            'zeitgeist: integration with zeitgeist')
source=("https://launchpad.net/catfish-search/${pkgver%.*}/$pkgver/+download/$pkgname-$pkgver.tar.gz")


build() {
  cd "$pkgname-$pkgver"

  python setup.py build
}

package () {
  cd "$pkgname-$pkgver"

  python setup.py install --root="$pkgdir" --optimize=1

  install -d "$pkgdir/usr/share/pixmaps"
  ln -s "/usr/share/icons/hicolor/scalable/apps/$pkgname.svg" \
    "$pkgdir/usr/share/pixmaps/$pkgname.svg"
}

# getver: launchpad.net/catfish-search/+download
# vim: ts=2 sw=2 et:
md5sums=('5b55cee4816e8d01dbc7119a6021ff5f')
