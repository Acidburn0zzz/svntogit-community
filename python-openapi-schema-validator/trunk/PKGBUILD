# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-openapi-schema-validator
pkgver=0.1.4
pkgrel=1
pkgdesc="OpenAPI schema validation for Python"
url="https://github.com/p1c2u/openapi-schema-validator"
license=('BSD')
arch=('any')
depends=('python-isodate' 'python-jsonschema' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-cov' 'python-pytest-flake8' 'python-rfc3339-validator'
              'python-strict-rfc3339')
source=("https://github.com/p1c2u/openapi-schema-validator/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('24e0a2db946437b70648c39edb02bb92429d617e681cbca01b028a556c9379206be6e57537dac2f85fcfc243cd94a2ffb3d8b648179515ccc7f9cdd990689317')

prepare() {
  cd openapi-schema-validator-$pkgver
  sed -i 's/import mock/from unittest import mock/' tests/integration/test_validators.py
}

build() {
  cd openapi-schema-validator-$pkgver
  python setup.py build
}

check() {
  cd openapi-schema-validator-$pkgver
  python -m pytest
}

package() {
  cd openapi-schema-validator-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
