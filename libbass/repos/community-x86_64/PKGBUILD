# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Alexander Sulfrian <asulfrian@zedat.fu-berlin.de>

pkgname=libbass
pkgver=2.4.15
pkgrel=4
pkgdesc='BASS audio library'
arch=(x86_64)
url='https://www.un4seen.com/'
license=(custom)
depends=(alsa-lib)
makedepends=(unzip)
source=("$pkgname-$pkgver.zip::http://dl.un4seen.com/files/bass24-linux.zip")
sha256sums=('05200f84de1ba507e717f607640ccc3b5cef5860c002b61130b17941418d01fc')

prepare() {
  unzip -q "$pkgname-$pkgver.zip" -d "$srcdir/$pkgname-$pkgver"
  grep Lic "$pkgname-$pkgver/bass.txt" -A62 > LICENSE
}

package() {
  cd "$pkgname-$pkgver"
  install -D -m644 x64/libbass.so "$pkgdir/usr/lib/libbass.so"
  install -D -m644 bass.h "$pkgdir/usr/include/bass.h"
  install -D -m644 bass.chm "$pkgdir/usr/share/doc/libbass/bass.chm"
  install -D -m644 "$srcdir/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# getver: -u=2 un4seen.com/bass.html
# vim: ts=2 sw=2 et:
