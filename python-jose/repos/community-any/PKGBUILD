# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-jose
pkgname=('python-jose' 'python2-jose')
pkgver=1.3.2
pkgrel=1
pkgdesc="A JOSE implementation in Python"
arch=('any')
license=('MIT')
url="http://github.com/mpdavis/python-jose"
makedepends=('python-setuptools' 'python2-setuptools' 'python-crypto' 'python2-crypto' 'python-six'
             'python2-six' 'python-ecdsa' 'python2-ecdsa' 'python-future' 'python2-future' 'git')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("git+https://github.com/mpdavis/python-jose.git#tag=$pkgver")
sha512sums=('SKIP')

prepare() {
  cp -a python-jose{,-py2}
}

build() {
  cd "$srcdir"/python-jose
  python setup.py build

  cd "$srcdir"/python-jose-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-jose
  python setup.py pytest

  cd "$srcdir"/python-jose-py2
  python2 setup.py pytest
}

package_python-jose() {
  depends=('python-crypto' 'python-six' 'python-ecdsa' 'python-future')

  cd python-jose
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-jose() {
  depends=('python2-crypto' 'python2-six' 'python2-ecdsa' 'python2-future')

  cd python-jose-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
