# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-media
pkgver=2.0.0
pkgrel=2
pkgdesc="UKUI media utilities"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-media"
groups=('ukui')
depends=('gsettings-qt' 'libmatemixer' 'libqtxdg' 'mate-desktop' 'qt5-svg')
makedepends=('autoconf-archive' 'intltool' 'mate-common' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-media/archive/v$pkgver.tar.gz"
        fix-installation.patch)
sha512sums=('d741e96e2961135913d65108839722d81d414ddd1094c41707bd149b7bc26d4d9d96208bdface885acb5abf252a4fe6e9412c9316c24ae3a3a5eab8e0bca78c8'
            'f8feb25ae30a7ab04663641da94bbd611ecf3914d34e8abb3a459d14719b80bd7cf721eaf0119f742dbee14cca9504be81cb3158f7f2ef59055ac1b0379f1682')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../fix-installation.patch
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make

  cd ukui-volume-control-applet-qt
  qmake PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd ukui-volume-control-applet-qt
  make INSTALL_ROOT="$pkgdir" install
}
