# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-media
pkgver=2.0.2
pkgrel=1
pkgdesc="UKUI media utilities"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-media"
groups=('ukui')
depends=('gsettings-qt' 'libmatemixer' 'libqtxdg' 'mate-desktop' 'qt5-svg')
makedepends=('autoconf-archive' 'intltool' 'mate-common' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-media/archive/v$pkgver.tar.gz")
sha512sums=('cf2399efc2d0672c97523960a0c3b4ce65e13799b4d5b9e0532179ffbb923cb47ab946225251410a58f50e6c6aadfefbaf59dd72f251739b8679fbe0fd036828')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make

  cd ukui-volume-control-applet-qt
  qmake PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd ukui-volume-control-applet-qt
  make INSTALL_ROOT="$pkgdir" install
}
