# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-media
pkgver=2.0.1
pkgrel=1
pkgdesc="UKUI media utilities"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-media"
groups=('ukui')
depends=('gsettings-qt' 'libmatemixer' 'libqtxdg' 'mate-desktop' 'qt5-svg')
makedepends=('autoconf-archive' 'intltool' 'mate-common' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-media/archive/v$pkgver.tar.gz")
sha512sums=('4452fb519d8f4966932b9c89dad2ed885a5fe3de65a6102848e4d25eaff41116e93b3e768134d6a2c92232f20651dd18d6f33c5fb31a46bd78200e04302943e2')

prepare() {
  cd $pkgname-$pkgver
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname --sysconfdir=/etc
  make

  cd ukui-volume-control-applet-qt
  qmake PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  cd ukui-volume-control-applet-qt
  make INSTALL_ROOT="$pkgdir" install
}
