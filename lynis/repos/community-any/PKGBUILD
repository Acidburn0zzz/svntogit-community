# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=2.3.1
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='https://cisofy.com/lynis/'
arch=('any')
license=('GPL3')
backup=('etc/lynis/default.prf')
depends=('sh')
optdepends=(
  'net-tools: networking tests'
  'bash-completion: completion for bash'
)
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/CISOfy/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('239a8a464801b20706ac361c23d89f71bfbecffc884a5a9c28a29249031a5bf02320a28a57eef3ae02a001ed0e394def01765fb03919a4193fcbe9c8b349f9f8')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
}

package() {
  cd ${pkgname}-${pkgver}

  # binary
  install -Dm 755 "${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

  # profile
  install -Dm 644 default.prf "${pkgdir}/etc/${pkgname}/default.prf"

  # plugins, include, db
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -a db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  install -m 644 -t "${pkgdir}/usr/share/doc/${pkgname}" README INSTALL CHANGELOG.md FAQ
  install -Dm 644 "${pkgname}.8" "${pkgdir}/usr/share/man/man8/${pkgname}.8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"

  # systemd
  install -d "${pkgdir}/usr/lib/systemd/system/"
  install -m 644 extras/systemd/{lynis.service,lynis.timer} "${pkgdir}/usr/lib/systemd/system/"
}

# vim: ts=2 sw=2 et:
