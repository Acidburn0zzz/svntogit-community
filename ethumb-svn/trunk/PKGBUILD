# $Id$
# Maintainer: Ronald van Haren <ronald.archlinux.org>

pkgname=ethumb-svn
pkgver=75994
pkgrel=1
pkgdesc='Thumbnailing library'
arch=('i686' 'x86_64')
groups=('e17-libs-svn' 'e17-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('libexif' 'e_dbus-svn' 'emotion-svn')
makedepends=('subversion')
conflicts=('ethumb')
provides=('ethumb')
options=(!libtool)
source=()
md5sums=()

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/ethumb"
_svnmod="ethumb"

build() {
   cd $srcdir

msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh --prefix=/usr --libexecdir=/usr/lib
  make
}

package(){
  cd $_svnmod-build
  make DESTDIR=$pkgdir install
  
  # install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
  
  rm -r $srcdir/$_svnmod-build

}
