# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-write
_pkgname=Write
pkgver=100
pkgrel=1
pkgdesc="Sugar activity to edit texts"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Write"
license=('GPL')
groups=('sugar-fructose')
depends=('abiword' 'sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2)
sha256sums=('00235f57d7f35ab2bca1357402f0ebce8b5ff0a4b814bda2389cb6f21aa558dc')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' gridcreate.py setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr" --skip-install-desktop-file
}
