# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Ryan Coyner <rcoyner@gmail.com>

pkgbase=mod_wsgi
pkgname=($pkgbase 'mod_wsgi2')
pkgver=4.5.24
pkgrel=4
pkgdesc='Python WSGI adapter module for Apache'
arch=('x86_64')
url='http://www.modwsgi.org/'
license=('APACHE')
makedepends=('apache' 'git' 'python' 'python2')
install='mod_wsgi.install'
source=("git+https://github.com/GrahamDumpleton/mod_wsgi#tag=$pkgver"
        python-3.8.patch)
md5sums=('SKIP'
         '73ac9f44d14c04e4bd14eeb506313ec4')

prepare() {
  cp -r "$pkgbase" py2
  patch -Np1 -d "$pkgbase" <python-3.8.patch
}

build() {
  # Configure and build mod_wsgi for py2
  cd py2
  ./configure \
    --prefix=/usr \
    --with-apxs=/usr/bin/apxs \
    --with-python=/usr/bin/python2

  make

  # Configure and build mod_wsgi for py3
  cd "$srcdir/$pkgbase"
  ./configure \
    --prefix=/usr \
    --with-apxs=/usr/bin/apxs \
    --with-python=/usr/bin/python

  make
}

package_mod_wsgi() {
  pkgdesc='Python WSGI adapter module for Apache'
  depends=('apache' 'python')
  conflicts=('mod_wsgi2')

  make -C "$pkgbase" DESTDIR="$pkgdir" install
}

package_mod_wsgi2() {
  pkgdesc='Python2 WSGI adapter module for Apache'
  depends=('apache' 'python2')
  conflicts=('mod_wsgi')

  make -C py2 DESTDIR="$pkgdir" install
}

# getver: github.com/GrahamDumpleton/mod_wsgi
# vim: ts=2 sw=2 et:
