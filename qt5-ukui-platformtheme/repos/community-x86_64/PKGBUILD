# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-ukui-platformtheme
pkgver=1.0.1
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/qt5-ukui-platformtheme"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'kwindowsystem')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/qt5-ukui-platformtheme/archive/v$pkgver.tar.gz"
        $pkgname-test-no-deployment.patch::https://github.com/ukui/qt5-ukui-platformtheme/pull/18.patch)
sha512sums=('600831b780585fddb6dffec5e131fcd41ccbea3d6c0e9e187d6c2ce040ff059f22c68773f168c1c53b9dfa9f8568fefcb5e0f9ebe36cc60c436d7423409e23a9'
            '35181d43b50c403f2981be9c5eb9191ad59b6357d06795d16030b150105d06fcaba8acfa9eb4b41ca8af87b52b01e5169048cb397a9583466809df69535fdabd')

prepare() {
  cd qt5-ukui-platformtheme-$pkgver
  patch -p1 -i ../$pkgname-test-no-deployment.patch
}

build() {
  cd qt5-ukui-platformtheme-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5-ukui-platformtheme-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
