# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=qt5-ukui-platformtheme
pkgver=1.0.2
pkgrel=2
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/qt5-ukui-platformtheme"
groups=('ukui')
depends=('dconf' 'gsettings-qt' 'kwindowsystem')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/qt5-ukui-platformtheme/archive/v$pkgver.tar.gz"
         qt5-ukui-platformtheme-qt5.15.patch)
sha512sums=('c057e65a47d5fa30d00fb9d49be2d334a83e35dbe861caef33993d268d4673afe4d8302627bd69dc0fe42ab518b61006f9feff96aed03a40245b9af041b4b929'
            '6071c470d9079b7eb8600753882381a4c9cb1bd345a0d14e0f35b984af0973c57728c50861258a3dc2ee1183e6c72d7235bd225685a9628fcc5322ea4671ab2d')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../qt5-ukui-platformtheme-qt5.15.patch # Fix build with Qt 5.15
}

build() {
  cd qt5-ukui-platformtheme-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd qt5-ukui-platformtheme-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
