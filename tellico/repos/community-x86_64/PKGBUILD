# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=tellico
pkgver=3.2.3
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('exempi' 'hicolor-icon-theme' 'kio' 'libksane' 'libkcddb'
         'libcdio' 'kfilemetadata' 'knewstuff' 'kxmlgui' 'kitemmodels'
         'python2' 'solid' 'khtml' 'yaz' 'taglib' 'poppler-qt5' 'qt5-base')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools')
changelog=$pkgname.changelog
source=(http://tellico-project.org/files/$pkgname-$pkgver.tar.xz)
sha256sums=('f4456eb31f71151b9786fa171531265b421b2de8140e9adbf6bb584862c42952')

build() {
  mkdir build && cd build
  cmake ../$pkgname-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install

  # fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
