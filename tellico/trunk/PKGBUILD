# $Id$
# Maintainer: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>
# Contributor: mdv

pkgname=tellico
pkgver=2.3.2
pkgrel=1
pkgdesc="A collection manager for KDE"
arch=('i686' 'x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('kdebase-workspace' 'yaz' 'exempi' 'kdegraphics-libs' 'taglib'
         'kdemultimedia-kioslave' 'poppler-qt' 'qjson')
makedepends=('automoc4' 'cmake' 'docbook-xsl')
install=$pkgname.install
source=("http://tellico-project.org/files/${pkgname}-${pkgver}.tar.bz2")
md5sums=('3425c12055ae7ea512cd7242de73575e')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  # fix python 2.7 path
  find "$pkgdir" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}

# vim:set ts=2 sw=2 et:
