# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <dsa@aur.archlinux.org>

pkgname=tellico
pkgver=3.1.4
pkgrel=3
pkgdesc="A collection manager for KDE"
arch=('x86_64')
url="http://tellico-project.org/"
license=('GPL')
depends=('exempi' 'hicolor-icon-theme' 'kio' 'libksane' 'libkcddb' 'libcddb' 
         'libcdio' 'kfilemetadata' 'knewstuff' 'kxmlgui' 'kitemmodels'
         'python2' 'solid' 'khtml' 'yaz' 'taglib' 'poppler-qt5' 'qt5-base')
makedepends=('cmake' 'extra-cmake-modules' 'kdoctools' 'libkcddb')
changelog=$pkgname.changelog
source=(http://tellico-project.org/files/$pkgname-$pkgver.tar.xz
        compilefix.diff)
sha256sums=('8f611e3be8be095de38fe27d76f3ddeae09962c9aec00298031cc3e71eae95f9'
            '7f0a13e2047b61b104dfc38e456e6d51808d3f5080e0aff97e75d6a161327069')

prepare() {
  cd $pkgname-$pkgver

  # Fix "error: template with C linkage"
  patch -Np1 -i ../compilefix.diff
}

build() {
  cmake -H$pkgname-$pkgver -Bbuild \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DWITH_Nepomuk=OFF
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --build build --target install

  # fix python 2.7 path
  find "${pkgdir}" -iname "*.py" | xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
