# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Dominik Picheta <morfeusz8@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>

pkgname=nimrod
pkgver=0.9.4
pkgrel=1
pkgdesc='Imperative, multi-paradigm, compiled programming language'
arch=('x86_64' 'i686')
url='http://nimrod-code.org/'
license=('MIT')
makedepends=('git')
options=('!emptydirs')
source=("nimrod::git://github.com/Araq/Nimrod.git"
        "csources::git://github.com/nimrod-code/csources")
md5sums=('SKIP' 'SKIP')

prepare() {
  cp -r "$srcdir/csources" "$pkgname/csources"
}

build() {
  cd "$pkgname/csources"
  sh build.sh
  cd "$srcdir/$pkgname"
  ./bin/nimrod c koch
  PATH="$PATH":./bin/nimrod ./koch boot -d:release
}

package() {
  install -Dm755 "$pkgname/bin/$pkgname" "$pkgdir/usr/bin/$pkgname"
  cp -r "$pkgname" "$pkgdir/usr/lib/"
  rm -r "$pkgdir/usr/lib/$pkgname/"{csources,bin}
  install -Dm644 "$pkgname/copying.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
