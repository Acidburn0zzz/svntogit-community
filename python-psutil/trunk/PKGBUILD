# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=0.4.1
pkgrel=1
arch=('any')
url='http://code.google.com/p/psutil/'
license=('custom: BSD')
makedepends=('python' 'python-distribute' 'python2' 'python2-distribute')
source=("https://psutil.googlecode.com/files/psutil-$pkgver.tar.gz") 
sha1sums=('2705862e00280a7fa76cfb687dbd61623d5e236e')

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('python')

  cd psutil-$pkgver
  python setup.py install --root="$pkgdir"
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('python2')

  cd psutil-$pkgver
  python2 setup.py install --root="$pkgdir"
  find "$pkgdir" -name '*.py' -exec \
     sed -i '1s,^#!/usr/bin/env python$,#!/usr/bin/env python2,' {} \; -print
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 ft=sh et:
