# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgbase=python-psutil
pkgname=('python-psutil' 'python2-psutil')
pkgver=0.4.1
pkgrel=2
arch=('i686' 'x86_64')
url='http://code.google.com/p/psutil/'
license=('custom: BSD')
makedepends=('python' 'python-distribute' 'python2' 'python2-distribute')
source=("https://psutil.googlecode.com/files/psutil-$pkgver.tar.gz") 
sha1sums=('2705862e00280a7fa76cfb687dbd61623d5e236e')

build() {
  'cp' -a psutil-$pkgver psutil2-$pkgver
  # build python
  cd psutil-$pkgver
  python setup.py build
  
  cd ../psutil2-$pkgver
  # change python interpreter
  find -name '*.py' -exec \
     sed -i '1s,^#!/usr/bin/env python$,#!/usr/bin/python2,' {} \; -print
  python2 setup.py build
}

package_python-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python'
  depends=('glibc' 'python')

  cd psutil-$pkgver
  python setup.py install --root="$pkgdir"
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

package_python2-psutil() {
  pkgdesc='A cross-platform process and system utilities module for Python 2'
  depends=('glibc' 'python2')

  cd psutil2-$pkgver
  python2 setup.py install --root="$pkgdir"
  install -D -m 644 LICENSE "$pkgdir/"usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 ft=sh et:
