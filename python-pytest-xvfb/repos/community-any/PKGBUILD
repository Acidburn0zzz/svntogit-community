# Maintainer: Bruno Pagani <archange@archlinux.org>

_pkg=pytest-xvfb
pkgname=python-${_pkg}
pkgver=1.2.0
pkgrel=1
pkgdesc="pytest plugin to run Xvfb for tests"
arch=(any)
url="https://github.com/The-Compiler/pytest-xvfb"
license=(MIT)
depends=(python-pytest python-pyvirtualdisplay xorg-server-xvfb)
makedepends=(python-setuptools)
checkdepends=(xorg-xauth)
source=(https://files.pythonhosted.org/packages/source/${_pkg::1}/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('a7544ca8d0c7c40db4b40d7a417a7b071c68d6ef6bdf9700872d7a167302f979')

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  PYTHONPATH="${PWD}/build/lib/" pytest
}

package() {
  cd ${_pkg}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=1
  install -Dm644 LICENSE -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
