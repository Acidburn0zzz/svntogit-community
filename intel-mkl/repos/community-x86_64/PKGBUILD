# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Redistribution information:
# https://software.intel.com/en-us/mkl
# https://software.intel.com/en-us/articles/intel-math-kernel-library-license-faq
pkgbase=intel-mkl
pkgname=(intel-mkl intel-mkl-static)
pkgver=2020.1.217
_pkgver=${pkgver/%./-/}
_weirdnum=16533
pkgrel=3
pkgdesc="Intel Math Kernel Library"
arch=(x86_64)
url="https://software.intel.com/en-us/mkl"
license=("custom:ISSL")
source=("http://registrationcenter-download.intel.com/akdlm/irc_nas/tec/$_weirdnum/l_mkl_$pkgver.tgz"
        intel-mkl.sh
        intel-mkl.conf)
options=(!strip staticlibs)
sha512sums=('ad3993ba2005168eb273942109ceeffb392f86c083643db9a2e25c7a168ab77d3e2c985c9ebf7841ad8e27ba0e2c0cedace20860ba450d5bfcccd5be23dc62dd'
            '1268685ff3ccd47733084dedcd4ea0d393f4ed3f6d00cd92125765ae5c8e14e7fe49b82692b3f667bfea931ad3c284a1c1c37235b474db09b5350c0046566c3c'
            'c7181ee34a8225eefcd66f937e58b29184181c1caa1f24ce264b6fb4c0415fcde743aff3c1a2b165c2af1920dda6c50f5e470a94fe1e11ea61b4ceb9388c3ce4')

prepare() {
  cd l_mkl_$pkgver/rpm
  for rpm in *.rpm; do
    bsdtar -xf $rpm
  done
}

package_intel-mkl() {
  cd l_mkl_$pkgver/rpm

  # We'll ship the terrible mklvars.sh file because some users surely expect it.
  # TODO: This is basically just hard-coded paths anyway. Just ship a static file.
  # It's a terrible pkg-config replacement by Intel.
  # install -Dm755 opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/mklvars.sh "${pkgdir}"/usr/bin/mklvars.sh
  # sed -i "s|<INSTALLDIR>|/opt/intel/mkl|g" "${pkgdir}"/usr/bin/mklvars.sh

  mkdir -p "${pkgdir}/usr/bin"
  install -Dm755 opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/bin/mkl_link_tool "${pkgdir}"/opt/intel/mkl/bin/mkl_link_tool
  ln -s /opt/intel/mkl/tools/mkl_link_tool "${pkgdir}"/usr/bin/mkl_link_tool

  mkdir -p "${pkgdir}"/opt/intel/mkl/lib/intel64
  ln -s intel64 "${pkgdir}"/opt/intel/mkl/lib/intel64_lin

  # Sadly Intel seems to expect a certain structure for MKL
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-high-level-directory-structure
  # We'll try to follow that as per upstream suggestion.
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.so "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/compiler/lib/intel64_lin/*.so "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp -r opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/include "${pkgdir}"/opt/intel/mkl/include
  install -Dm644 opt/intel/compilers_and_libraries_${pkgver}/licensing/mkl/en/license.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE

  # Install profile and ld.so.config files
  # See https://software.intel.com/en-us/mkl-linux-developer-guide-scripts-to-set-environment-variables
  install -Dm755 "${srcdir}/intel-mkl.sh" "${pkgdir}/etc/profile.d/intel-mkl.sh"
  install -Dm644 "${srcdir}/intel-mkl.conf" "${pkgdir}/etc/ld.so.conf.d/intel-mkl.conf"
}

package_intel-mkl-static() {
  pkgdesc="Intel Math Kernel Library (static libraries)"
  depends=('intel-mkl')

  cd l_mkl_$pkgver/rpm
  mkdir -p "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/mkl/lib/intel64_lin/*.a "${pkgdir}"/opt/intel/mkl/lib/intel64
  cp opt/intel/compilers_and_libraries_${pkgver}/linux/compiler/lib/intel64_lin/*.a "${pkgdir}"/opt/intel/mkl/lib/intel64
}
# vim:set ts=2 sw=2 et:
