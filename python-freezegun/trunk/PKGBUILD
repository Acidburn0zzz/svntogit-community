# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-freezegun
pkgname=('python-freezegun' 'python2-freezegun')
pkgver=0.3.14
pkgrel=1
pkgdesc="Let your Python tests travel through time"
arch=('any')
license=('Apache')
url="https://github.com/spulec/freezegun"
makedepends=('python-setuptools' 'python2-setuptools' 'python-dateutil' 'python2-dateutil')
checkdepends=('python-pytest' 'python2-pytest' 'python-mock' 'python2-mock'
              'python-coverage' 'python2-coverage' 'python-dateutil' 'python2-dateutil')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/spulec/freezegun/archive/$pkgver.tar.gz")
sha512sums=('7f01439b36ac580c5cd4e3953f7285fb9cf00e99e99cd515d66581477c5c7c67fe2f3c42701d50f64562500e2b31d3d085f05d2b53ab5b757df07944e3411dd4')

prepare() {
  cp -a freezegun-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/freezegun-$pkgver
  python setup.py build

  cd "$srcdir"/freezegun-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/freezegun-$pkgver
  # https://github.com/spulec/freezegun/issues/250
  pytest || warning "Tests failed"

  cd "$srcdir"/freezegun-$pkgver-py2
  pytest2
}

package_python-freezegun() {
  depends=('python-dateutil')

  cd freezegun-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-freezegun() {
  depends=('python2-dateutil')

  cd freezegun-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
