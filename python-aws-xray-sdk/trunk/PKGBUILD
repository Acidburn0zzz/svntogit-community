# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-aws-xray-sdk
_pkgname=aws-xray-sdk-python
pkgver=2.4.3
pkgrel=2
pkgdesc='AWS X-Ray SDK for Python'
arch=(any)
url='https://github.com/aws/aws-xray-sdk-python'
license=(Apache)
makedepends=(python-setuptools python-pypandoc)
depends=(python python-botocore python-future python-jsonpickle python-wrapt)
checkdepends=(python-flask-sqlalchemy python-pynamodb python-pytest python-aiohttp
              python-pytest-aiohttp python-testing.postgresql python-psycopg2
              python-pg8000 python-requests python-aiobotocore)
# Not using PyPI source tarball as it does not include tests
source=("https://github.com/aws/aws-xray-sdk-python/archive/$pkgver/$pkgname-$pkgver.tar.gz"
        aiobotocore-0.12.patch)
sha256sums=('adb528c7c092293613484ca28c5466c79889ff6035493681daf9fbde9ba95b3a'
            '20d99cfbf6675f2b2d8147b5e4bc218de5666b0c0629d3fea8fb9a39b66c4d8b')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../aiobotocore-0.12.patch
}

build() {
  cd $_pkgname-$pkgver
  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  export AWS_SECRET_ACCESS_KEY=fake_key
  export AWS_ACCESS_KEY_ID=fake_id

  # the test suite is not compatible with Django 2.x yet
  # https://github.com/aws/aws-xray-sdk-python/issues/85
  pytest -v --ignore tests/ext/django
}

package() {
  cd $_pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
