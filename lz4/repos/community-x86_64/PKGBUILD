# $Id$
# Maintainer: SÃ©bastien Luttringer

# Contacted by mail, author use svn commit as release version.
# He only push tested release code into svn and support using revision as version

pkgname=lz4
pkgver=102
pkgrel=1
pkgdesc='Very fast lossless compression algorithm'
arch=('i686' 'x86_64')
url='https://code.google.com/p/lz4/'
license=('BSD')
makedepends=('svn')
depends=('glibc')
source=("$pkgname::svn+http://lz4.googlecode.com/svn/trunk/#revision=$pkgver")
md5sums=('SKIP')

_binaries=(lz4c lz4cs fuzzer fullbench)

build() {
  cd $pkgname
  make "${_binaries[@]}"
}

package() {
  cd $pkgname
  install -d "$pkgdir/usr/bin"
  install -Dm755 "${_binaries[@]}" "$pkgdir/usr/bin"
  install -Dm644 cmake/pack/release_COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
