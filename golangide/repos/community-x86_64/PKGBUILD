#$Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: spambanane <happy.house@gmx.de>
# Contributor: Matteo <matteo.dek@gmail.com>

pkgname=golangide
pkgver=18.1
pkgrel=1
pkgdesc='IDE for editing and building projects written in the Go programming language (now LiteIDE X)'
license=('LGPL')
arch=('x86_64' 'i686')
url='https://github.com/visualfc/liteide'
depends=('go' 'libpng12' 'glib2' 'qt5-base' 'qt5-webkit')
makedepends=('gendesk')
options=('!strip')
source=("$pkgname.tgz::https://github.com/visualfc/liteide/archive/x$pkgver.zip"
        'golangide.png'
        'golangide.sh')
sha256sums=('4ca6438099314789f4cd8fd3acc8016d8e32594634a6d0f130f5666dcb0a3d6c'
            '47c52b22326034bd3d6a7b11b05a53c8b3838c08e145171cf5cad2ca00260697'
            '93ef16d59054ef9b37f8781e90f7d0f83d726779d2029660dfd21e84d808bf04')
_name=('Golang IDE')

prepare() {
  cd "$srcdir"

  gendesk -n

  cd "$srcdir/liteide-x$pkgver/build"
  head -n-3 build_linux.sh > build_linux_fixed.sh
  mv build_linux_fixed.sh build_linux.sh
  head -n-4 webkit_linux.sh > webkit_linux_fixed.sh
  mv webkit_linux_fixed.sh webkit_linux.sh
  chmod +x build_linux.sh webkit_linux.sh
}

build() {
  cd "$srcdir/liteide-x$pkgver/build"

  msg2 'Compiling...'
  QTDIR=/usr ./build_linux.sh
  QTDIR=/usr ./webkit_linux.sh

  # Fixing insecure RPATH, need to test if this is still needed
  msg2 'Fixing...'
  cd ../liteidex
  find . -name "*.so" -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
  find . -name liteide -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
}

package() {
  msg2 'Creating directories...'
  mkdir -p "$pkgdir/usr/lib/liteide"
  mkdir -p "$pkgdir/usr/share/liteide"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  cd "$srcdir/liteide-x$pkgver/build/liteide"

  msg2 'Packaging executables...'
  for binary in goastview goapi goexec godocview liteide; do
    install -Dm755 "bin/$binary" "$pkgdir/usr/bin/$binary"
  done
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  cd "$srcdir/liteide-x$pkgver/liteidex"

  msg2 'Packaging resources...'
  cp -r deploy/* "$pkgdir/usr/share/liteide"
  cp -r os_deploy/* "$pkgdir/usr/share/liteide"

  msg2 'Packaging libraries and plugins...'
  cp -r liteide/lib/liteide/* "$pkgdir/usr/lib/liteide"
  cp -r liteide/bin/libliteapp.* "$pkgdir/usr/lib/"
  chmod -x "$pkgdir/usr/lib/liteide/plugins/"*

  msg2 'Packaging license and license exception...'
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION"

  cd ../..

  msg2 'Packaging menu entry and icon...'
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 'Cleaning up...'
  rm -rf "$pkgdir/usr/share/$pkgname/doc"
}

# vim:set ts=2 sw=2 et:
