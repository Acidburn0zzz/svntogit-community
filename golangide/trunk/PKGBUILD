#$Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: spambanane <happy.house@gmx.de>
# Contributor: Matteo <matteo.dek@gmail.com>

pkgname=golangide
pkgver=15
pkgrel=1
pkgdesc='Simple IDE for editing and building projects written in Go'
license=('LGPL')
arch=('x86_64' 'i686')
url='https://github.com/visualfc/liteide'
depends=('go' 'libpng12' 'glib2' 'qt')
makedepends=('gendesk')
options=('!strip')
_name=('Golang IDE')
_genericname=('Integrated development environment')
source=("$pkgname.tgz::https://nodeload.github.com/visualfc/liteide/tar.gz/master"
        'golangide.png'
        'golangide.sh')
sha256sums=('23158d09d5d3c439a07d12532dc8cdbc963c65dde1739f2e19b611a7d2f8944e'
            '47c52b22326034bd3d6a7b11b05a53c8b3838c08e145171cf5cad2ca00260697'
            'd20185eae18605943e24ad2f472f110cf28ad32e3f3458647c69a233df869bc7')

build() {
  cd "$srcdir"

  msg2 'Generating .desktop file'
  gendesk -n

  msg2 'Compiling...'
  cd "liteide-master/build"
  chmod +x build_linux.sh webkit_linux.sh
  QTDIR=/usr ./build_linux.sh
  QTDIR=/usr ./webkit_linux.sh

  # Fixing insecure RPATH, need to test if this is still needed
  msg2 'Fixing...'
  cd "../liteidex"
  find . -name "*.so" -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
  find . -name liteide -type f -exec sed -i 's|/home/win|/usr/lib/|g' {} \;
}

package() {
  cd "$srcdir/liteide-master/liteidex"

  msg2 'Creating directories...'
  mkdir -p "$pkgdir/usr/lib/liteide"
  mkdir -p "$pkgdir/usr/share/liteide"
  mkdir -p "$pkgdir/usr/share/doc/$pkgname"

  msg2 'Packaging executables...'
  for binary in goastview goapi goexec godocview liteidex; do
    install -Dm755 "bin/$binary" "$pkgdir/usr/bin/$binary"
  done
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  msg2 'Packaging resources...'
  cp -r deploy/* "$pkgdir/usr/share/liteide"
  cp -r os_deploy/* "$pkgdir/usr/share/liteide"

  msg2 'Packaging libraries and plugins...'
  cp -r liteide/lib/liteide/* "$pkgdir/usr/lib/liteide"
  cp -r liteide/bin/libliteapp.* "$pkgdir/usr/lib/"
  chmod -x "$pkgdir/usr/lib/liteide/plugins/"*

  msg2 'Packaging license and license exception...'
  install -Dm644 LICENSE.LGPL \
    "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 LGPL_EXCEPTION.TXT \
    "$pkgdir/usr/share/licenses/$pkgname/LGPL_EXCEPTION"

  cd ../..

  msg2 'Packaging menu entry and icon...'
  install -Dm644 "$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 "$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  msg2 'Cleaning up...'
  rm -rf "$pkgdir/usr/share/$pkgname/doc"
}

# vim:set ts=2 sw=2 et:
