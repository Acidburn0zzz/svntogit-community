# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: derfenix <derfenix@gmail.com>

pkgname=neochat
pkgver=1.1.0
pkgrel=1
pkgdesc='A client for matrix, the decentralized communication protocol'
license=(GPL3)
arch=(x86_64)
url='https://apps.kde.org/neochat'
depends=(kirigami2 kdbusaddons ki18n knotifications libquotient kquickimageeditor kitemmodels qt5-svg qtkeychain-qt5 cmark hicolor-icon-theme)
makedepends=(extra-cmake-modules)
source=("https://download.kde.org/stable/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"{,.sig}
         neochat-fix-build.patch::"https://invent.kde.org/network/neochat/-/commit/b67a35bf.patch"
         neochat-fix-build2.patch::"https://invent.kde.org/network/neochat/-/commit/37a68159.patch")
sha256sums=('9dc386b4630e630970fb8e6e47ddb034f9e6758996b27b0615a8c2725839b4c2'
            'SKIP'
            'cda7b30e337a8463f596fecb34adc2b72e66dc12b06e1e062b14461bc3c4a19d'
            'a1f6b0df8db9964e5fb62072604d0aa0534b18516dd408dabf8944aa2788ada3')
validpgpkeys=(B3ED0BAE371BEDA00717921D18F4795C1CE7042B) # Carl Schwan <carl@carlschwan.eu>

prepare() {
  patch -d $pkgname-$pkgver -p1 < neochat-fix-build.patch
  patch -d $pkgname-$pkgver -p1 < neochat-fix-build2.patch
}

build() {
  cmake -B build -S $pkgname-$pkgver
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
