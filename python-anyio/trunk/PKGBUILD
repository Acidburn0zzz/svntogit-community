# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-anyio
pkgver=3.3.0
pkgrel=1
pkgdesc='High level compatibility layer for multiple asynchronous event loop implementations'
arch=(any)
url='https://github.com/agronholm/anyio'
license=(MIT)
depends=(python python-idna python-sniffio)
makedepends=(python-setuptools python-setuptools-scm)
checkdepends=(python-pytest python-trustme python-hypothesis python-uvloop
              python-curio python-trio python-pytest-mock)
optdepends=(
  'python-curio: curio backend'
  'python-trio: trio backend'
)
source=(https://github.com/agronholm/anyio/archive/$pkgver/anyio-$pkgver.tar.gz
        https://github.com/agronholm/anyio/commit/5ed3270c8d12cffc4cd3349d9ff32bc32451ae65.patch
        bpo-45097.diff)
sha256sums=('5872afba5b2a03461e8eb64538d8050d1f64fe54192ad67d8f34f6287ea7c729'
            '30e94b5a37c124d3620c77abd835384b224eee3349bea0ddd19f3a268e71c9ca'
            'ed0ad72f604697d247a2d74f5b4740fcda0fe478040fa6ff75120cd97d5985c8')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cd anyio-$pkgver
  # pytest-mock 3.3 comes with an API change [1], and anyio uses the new API,
  # while python-pytest-mock in Arch is out-dated. Patch the sources to use
  # the old API.
  # [1] https://github.com/pytest-dev/pytest-mock/issues/202
  sed -i 's#MockerFixture#MockFixture#' tests/test_eventloop.py

  # https://github.com/agronholm/anyio/issues/347
  patch -Np1 -i ../5ed3270c8d12cffc4cd3349d9ff32bc32451ae65.patch

  # Work around https://bugs.python.org/issue45097
  patch -Np1 -i ../bpo-45097.diff
}

build() {
  cd anyio-$pkgver
  python setup.py build
}

check() {
  cd anyio-$pkgver
  # Install to a temporary root as the test suite requires the entry point for
  # its pytest plugin
  pyver=$(python -c "import sys; print('{}.{}'.format(*sys.version_info[:2]))")
  python setup.py install --root="$PWD/tmp_install" --skip-build
  PYTHONPATH="$PWD/tmp_install/usr/lib/python$pyver/site-packages" pytest
}

package() {
  cd anyio-$pkgver
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
}
