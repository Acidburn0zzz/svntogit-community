# $Id$
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=exaile
pkgver=0.3.2.1
pkgrel=1
pkgdesc="A full-featured media player for GTK+"
arch=('i686' 'x86_64')
url="http://www.exaile.org"
license=('GPL')
depends=('python2' \
	 'gstreamer0.10-python' \
	 'gstreamer0.10-good-plugins' \
	 'mutagen' \
	 'dbus-python' \
	 'pygtk>=2.10' \
	 'librsvg')
makedepends=('make' 'help2man')
optdepends=('pycddb: CD metadata retrieval' \
	    'hal: device autodetection' \
	    'gstreamer0.10-bad-plugins: support for more formats' \
	    'gstreamer0.10-ugly-plugins: support for more formats' \
	    'gstreamer0.10-ffmpeg: support for more formats' \
	    'python-sexy: UI enhancements')
source=(https://www.launchpad.net/exaile/${pkgver%.*}/${pkgver}/+download/exaile-${pkgver}.tar.gz)
md5sums=('7ecfa9e52a9f2882717b3483518b604b')
sha1sums=('86e1451ab9e6f96077c9dfc59eea3d4dccb00cf0')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 fix
    for file in $(find . -name Makefile -print) tools/generate-launcher; do
        sed -i 's_python_python2_' $file
    done
    for file in plugins/*/*.py exaile.py; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    done

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make PREFIX=/usr DESTDIR="${pkgdir}" install

	# fix for clicking files with spaces in names from nautilus
	sed -i "s#%u#%f#" "${pkgdir}/usr/share/applications/exaile.desktop"
}
