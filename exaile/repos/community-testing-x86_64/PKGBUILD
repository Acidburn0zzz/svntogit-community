# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Benjamin Wild <benwild@gmx.de>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=exaile
pkgver=0.3.2.0
pkgrel=3
pkgdesc="A full-featured media player for GTK+"
arch=('i686' 'x86_64')
url="http://www.exaile.org"
license=('GPL')
depends=('python2' \
	 'gstreamer0.10-python' \
	 'gstreamer0.10-good-plugins' \
	 'mutagen' \
	 'dbus-python' \
	 'pygtk>=2.10' \
	 'librsvg')
makedepends=('make' 'help2man')
optdepends=('pycddb: CD metadata retrieval' \
	    'hal: device autodetection' \
	    'gstreamer0.10-bad-plugins: support for more formats' \
	    'gstreamer0.10-ugly-plugins: support for more formats' \
	    'gstreamer0.10-ffmpeg: support for more formats' \
	    'python-sexy: UI enhancements')
source=(https://www.launchpad.net/exaile/0.3.2/0.3.2.0/+download/exaile-${pkgver}.tar.gz)
md5sums=('fdd1f75118032a5aa0932cb449b2c533')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

    # python2 fix
    for file in $(find . -name Makefile -print) tools/generate-launcher; do
        sed -i 's_python_python2_' $file
    done
    for file in plugins/*/*.py exaile.py; do
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
    done

    export PYTHON=python2
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make PREFIX=/usr DESTDIR=${pkgdir} install

	# fix for clicking files with spaces in names from nautilus
	sed -i "s#%u#%f#" ${pkgdir}/usr/share/applications/exaile.desktop
}
