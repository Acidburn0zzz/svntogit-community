# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=libtraceevent
pkgver=1.3.0
pkgrel=1
epoch=1
pkgdesc="Linux kernel trace event library"
arch=('x86_64')
url="https://git.kernel.org/pub/scm/libs/libtrace/libtraceevent.git/about/"
license=('GPL2' 'LGPL2.1')
depends=('glibc')
makedepends=('asciidoc' 'xmlto')
provides=('libtraceevent.so')
source=("$pkgname-$pkgver.tar.gz::https://git.kernel.org/pub/scm/libs/libtrace/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('af0104cbc07599c565e0b1d20bcd6da00ba4c60c4b82575cd9e00a24b93986a815be7246ccb4ca656989e6d256a80ecce83ed275226604e7418c624966092848')
b2sums=('d86aeab3c0b1919d40800d0e4ebc2745f8d4d2401eab4e9a9efa63abc469c0f4c8ae8526d7d6c04d78b770ba88846ddb507b1321de14d5eed149087520b1d234')

build() {
  cd "$pkgname-$pkgver"
  make
  make -C Documentation
}

package() {
  cd "$pkgname-$pkgver"
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install
  make libdir_relative=lib prefix=/usr DESTDIR="$pkgdir/" install -C Documentation
}
