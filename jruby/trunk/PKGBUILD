# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=1.7.5
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="http://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
depends=(java-runtime-headless bash)
options=(!strip)
source=(http://jruby.org.s3.amazonaws.com/downloads/$pkgver/$pkgname-bin-${pkgver}.tar.gz)
sha256sums=('9ebd082cf9f29697c76e503e00b79d30e3e9b87071afb8823c3b8b033f5f5723')

build() {
  cd jruby-$pkgver/tool/nailgun
  ./configure
  make
}


package() {
  cd "$pkgdir"

  mkdir opt
  tar -C opt -xf "$srcdir/$pkgname-bin-$pkgver.tar.gz"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -delete
  rm -r opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-FreeBSD}

  install "$srcdir/$pkgname-$pkgver/tool/nailgun/ng" opt/jruby/tool/nailgun/ng

  mkdir -p usr/bin
  for _file in jirb{,_swing} jruby jgem; do
    ln -s ../../opt/jruby/bin/$_file usr/bin/$_file
  done

  mkdir -p usr/share/licenses/$pkgname
  ln -s ../../../../opt/jruby/COPYING usr/share/licenses/$pkgname/COPYING
}
