# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=1.7.4
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="http://www.jruby.org/"
license=(EPL/1.0 GPL2 LGPL2.1 custom)
depends=(java-runtime)
options=(!strip)
source=(http://jruby.org.s3.amazonaws.com/downloads/$pkgver/$pkgname-bin-${pkgver}.tar.gz)
sha256sums=('ef6dead787780f18a43758003689fd9ba40e64abc04cd41a6ff1eaf1557dfa69')

# For build from source (-src-) package
#makedepends=(java-environment apache-ant)
#build() {
#  cd jruby
#  ant dist-bin
#  tar -C .. -xf dist/$pkgname-bin-$pkgver.tar.gz
#}

package() {
  cd "$pkgdir"

  mkdir opt
  cp -r "$srcdir/jruby-$pkgver" opt/jruby

  mkdir -p usr/bin
  for _file in jirb{,_swing} jruby{,c} jgem; do
    ln -s ../../opt/jruby/bin/$_file usr/bin/$_file
  done

  mkdir -p usr/share/licenses/$pkgname
  ln -s ../../../../opt/jruby/COPYING \
    usr/share/licenses/$pkgname/COPYING
}
