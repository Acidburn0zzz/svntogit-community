# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.2.4.1
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="https://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
_tarname=jruby-dist-$pkgver-bin.tar.gz
noextract=($_tarname)
source=(https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/$_tarname)
md5sums=('d373bbc2bd6caabfcc9914c4067e9d24')
sha1sums=('5c51d146362ef7282e83e3487bfc80c0743d36d3')
sha256sums=('c89821120d74f17f90c9bc346cc7bd1278df623fc1fe60ea3b5c0a8a01360d5b')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$_tarname"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -print -delete
  rm -rv opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}

  chmod -cR g-w opt/jruby

  mkdir -p usr/bin
  ln -srt usr/bin opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}

  mkdir -p usr/share/licenses/$pkgname
  ln -srt usr/share/licenses/$pkgname opt/jruby/COPYING
}
