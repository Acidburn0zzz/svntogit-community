# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: FaziBear <fazibear@gmail.com>

pkgname=jruby
pkgver=9.2.7.0
pkgrel=1
pkgdesc="100% pure-Java implementation of the Ruby programming language"
arch=(any)
url="https://www.jruby.org/"
license=(EPL GPL2 LGPL2.1 custom)
options=(!strip)
_tarname=jruby-dist-$pkgver-bin.tar.gz
noextract=($_tarname)
source=(https://repo1.maven.org/maven2/org/jruby/jruby-dist/$pkgver/$_tarname)
md5sums=('c0111b2cafa920df76284044e1aeedc7')
sha1sums=('dc35f9bb991f526f058bf6b9591c460f98cffe9e')
sha256sums=('da7c1a5ce90015c0bafd4bca0352294e08fe1c9ec049ac51e82fe57ed50e1348')

package() {
  depends=(java-runtime-headless bash)

  cd "$pkgdir"

  mkdir opt
  bsdtar -C opt -xf "$srcdir/$_tarname"
  mv opt/jruby-$pkgver opt/jruby

  find opt/jruby -regextype posix-extended -regex '.*\.(bat|dll|exe)' -print -delete
  rm -rv opt/jruby/lib/jni/{Darwin,*-SunOS,*-Windows,*-AIX,*-*BSD}

  chmod -cR g-w opt/jruby

  mkdir -p usr/bin
  ln -srt usr/bin opt/jruby/bin/j{irb,irb_swing,gem,ruby,rubyc}

  mkdir -p usr/share/licenses/$pkgname
  ln -srt usr/share/licenses/$pkgname opt/jruby/COPYING
}
