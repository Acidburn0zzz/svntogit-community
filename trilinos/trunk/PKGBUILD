# Contributor: Jingbei Li <i@jingbei.li>
# Contributor: Simon Pintarelli <simon.pintarelli@gmail.com>
# Contributor: Feng Wang <wanng.fenng@gmail.com>
pkgname=trilinos
pkgver=12.12.1
pkgrel=2
pkgdesc="algorithms for the solution of large-scale scientific problems"
arch=('x86_64')
url="http://trilinos.org"
license=('LGPL3')
depends=('lapack' 'openmpi' 'python2' 'boost' 'netcdf' 'libmatio' 'libx11')
source=("https://github.com/trilinos/Trilinos/archive/trilinos-release-${pkgver//./-}.tar.gz")
md5sums=('ecd4606fa332212433c98bf950a69cc7')
makedepends=('python2-numpy' 'swig' 'gcc-fortran' 'perl' 'blas' 'cmake' 'gtest' 'doxygen')

prepare() {
	mv "$srcdir/Trilinos-trilinos-release-${pkgver//./-}" "$srcdir/Trilinos"

    # https://github.com/trilinos/Trilinos/issues/862#issuecomment-365852261
	find "$srcdir/Trilinos" -name "*.py" -exec \
		sed -i '1s#\(/usr/bin/env \|/usr/bin/\)python[2-3]*#\1python2#' {} \;
}

build() {
	cd "$srcdir/Trilinos"
	mkdir -p build
	cd build

	cmake .. -DTrilinos_ENABLE_ALL_OPTIONAL_PACKAGES:BOOL=ON \
             -DTrilinos_ENABLE_ALL_PACKAGES:BOOL=ON \
             -DTrilinos_ENABLE_Gtest:BOOL=OFF \
             -DTrilinos_ENABLE_TESTS=OFF \
             -DTPL_ENABLE_gtest:BOOL=ON \
             -DTPL_ENABLE_MPI:BOOL=ON \
             -DPYTHON_EXECUTABLE:PATH=/usr/bin/python2 \
             -DCMAKE_INSTALL_PREFIX:PATH=/usr \
             -DCMAKE_BUILD_TYPE=Release \
             -DBUILD_SHARED_LIBS:BOOL=ON
	make
}

package() {
	cd "$srcdir/Trilinos/build"
	make DESTDIR="$pkgdir" install
}
