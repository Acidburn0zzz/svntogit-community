# $Id$
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Roman Kyrylych <Roman.Kyrylych@gmail.com>
# Contributor: Mark Rosenstand <mark@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Chris Brannon <chris@the-brannons.com>

pkgname=webfs
pkgver=1.21
pkgrel=8
pkgdesc="Simple and instant http server for mostly static content."
arch=(i686 x86_64)
url="http://linux.bytesex.org/misc/webfs.html"
license=("GPL")
depends=('mime-types' 'openssl')
backup=(etc/conf.d/webfsd)
source=(http://dl.bytesex.org/releases/webfs/webfs-${pkgver}.tar.gz
        webfsd.rc webfsd.conf webfs.patch
        webfsd.launcher webfsd.service)
md5sums=('6dc125fe160479404147e7bbfc781dbc'
         '3ae9fcdcf79b193aa88e386055e9ebd7'
         'b2c1ab041a82acd8391b06dc38d986be'
         '7294edcec2589df04bb775270d56536e'
         '256533a2fa5a293a55da1f32fab2cb28'
         '7ee2bcd77e4113f5bfe7540844cbabd3')

build() {
	cd "$srcdir/$pkgname-$pkgver"

	patch -i "$srcdir/webfs.patch"
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make prefix=/usr DESTDIR="$pkgdir" install
	install -Dm755 "$srcdir/webfsd.rc"       "$pkgdir/etc/rc.d/webfsd"
	install -Dm644 "$srcdir/webfsd.conf"     "$pkgdir/etc/conf.d/webfsd"
	install -Dm755 "$srcdir/webfsd.launcher" "$pkgdir/usr/lib/systemd/scripts/webfsd"
	install -Dm755 "$srcdir/webfsd.service"  "$pkgdir/usr/lib/systemd/system/webfsd.service"
}
