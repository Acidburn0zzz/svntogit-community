# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=lfs
pkgver=1.4.0
pkgrel=1
pkgdesc="Get information on your mounted disks"
arch=('x86_64')
url="https://github.com/Canop/lfs"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('ff517980ffa1cd7b4b2843b6c78272faf0cc9033ee47762e8f565bec647109d4270e071592fdb280ad833b7c8d9201a96388bf782aeddb8174434d8235dbb8cc')

prepare() {
  cd "$pkgname-$pkgver"
  cargo fetch --locked
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

package() {
  cd "$pkgname-$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
