# $Id$
# Maintainer:
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=kradio
pkgver=4.0.6
pkgrel=1
arch=('i686' 'x86_64')
license=('GPL2')
pkgdesc="A comfortable KDE internet and AM/FM radio application"
url="http://kradio.sourceforge.net/"
depends=('kdebase-runtime' 'lirc-utils' 'libmms' 'ffmpeg')
makedepends=('automoc4' 'cmake' 'boost')
install=$pkgname.install
source=("http://downloads.sourceforge.net/$pkgname/kradio4-$pkgver.tar.bz2"{,.asc})
md5sums=('ee9214156465e0416bb369f78b4a5eae'
         'f43706e63d24788f2278bada7b10cc6f')

build() {
  cd "$srcdir"
  mkdir build
  cd build
  cmake ../${pkgname}4-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir" install
}
