# $Id$
# Maintainer: Thomas Dziedzic <gostrc@gmail.com>
# Contributor: simo <simo@archlinux.org>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
 
pkgname=happy
pkgver=1.18.9
pkgrel=4
pkgdesc="The Parser Generator for Haskell"
url="http://www.haskell.org/happy/"
arch=('i686' 'x86_64')
license=("custom:BSD3")
depends=('gmp')
makedepends=('ghc=7.4.1' 'haskell-mtl=2.0.1.0-4')
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz)
md5sums=('aeb565fe572f979f15a24db0df2de37d')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  runhaskell Setup.lhs configure -O --prefix=/usr
  runhaskell Setup.lhs build
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  runhaskell Setup.lhs copy --destdir="${pkgdir}"
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
