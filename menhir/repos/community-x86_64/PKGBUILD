# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=menhir
pkgver=20190626
pkgrel=1
pkgdesc="LR(1) parser generator for OCaml"
arch=(x86_64)
url="http://cristal.inria.fr/~fpottier/menhir/"
license=(GPL2 LGPL3)
depends=(ocaml)
makedepends=(ocamlbuild ocaml-findlib)
source=("https://gitlab.inria.fr/fpottier/${pkgname}/-/archive/${pkgver}/${pkgname}-${pkgver}.tar.bz2")
sha256sums=('27639743c1a7ba7cdeed3e8045bc28bf162475d2a99255aaf73da56c3e5fd28d')

build() {
  cd ${pkgname}-${pkgver}
  make PREFIX="/usr" all
}

package() {
  cd ${pkgname}-${pkgver}
  export OCAMLFIND_DESTDIR="${pkgdir}$(ocamlfind printconf destdir)"
  install -d "$OCAMLFIND_DESTDIR"
  make PREFIX="${pkgdir}"/usr install
}
