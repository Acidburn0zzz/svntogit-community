# $Id$
# Maintainer: Angel 'angvp' Velasquez <angvp@archlinux.org> 
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Rubin Simons <rubin@xs4all.nl>

pkgname=eclipse-gef
pkgver=3.6.1
_reldate=201009132020
pkgrel=1
pkgdesc="GEF framework for the Eclipse platform"
arch=('any')
url="http://www.eclipse.org/gef/"
license=('EPL')
depends=('eclipse')
source=(http://download.eclipse.org/tools/gef/downloads/drops/${pkgver}/R${_reldate}/GEF-ALL-${pkgver}.zip)
md5sums=('95cd7523fc8629f1ca07383a152f98e6')

build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}/eclipse

  # Features
  find features -type f | while read _feature ; do
    if [[ ${_feature} =~ (.*\.jar$) ]] ; then
      install -dm755 ${_dest}/${_feature%*.jar}
      cd ${_dest}/${_feature/.jar}
      jar xf ${srcdir}/${_feature}
    else
      install -Dm644 ${_feature} ${_dest}/${_feature}
    fi
  done

  # Plugins
  find plugins -type f | while read _plugin ; do
    install -Dm644 ${_plugin} ${_dest}/${_plugin}
  done
}

