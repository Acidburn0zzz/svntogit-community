# $Id$

# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: jiribb <jiribb@gmail.com> and <mathieui> <mathieui@mathieui.net>
# Contributor: mathieui <mathieui@mathieui.net>

pkgname=ldns
pkgver=1.6.11
pkgrel=1
pkgdesc='Fast DNS library with DNSSEC support, including tools such as Drill'
arch=('i686' 'x86_64')
url='http://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
depends=('openssl')
makedepends=('libpcap')
optdepends=('libpcap: ldns-dpa tool')
options=('!libtool')
source=("http://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha1sums=('2c4537eee39a1af63e8dde4f35498ce78c968c1f')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	_conf='--prefix=/usr --sysconfdir=/etc --localstatedir=/var --enable-static=no --disable-rpath'
	_tmp="$(mktemp -d)"

	./configure ${_conf}
	make
	make DESTDIR="${_tmp}" install

	cd drill
	./configure ${_conf} --with-ldns="${_tmp}"
	make

	cd ../examples
	./configure ${_conf} --with-ldns="${_tmp}"
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	cd drill
	make DESTDIR="${pkgdir}" install

	cd ../examples
	make DESTDIR="${pkgdir}" install
}
