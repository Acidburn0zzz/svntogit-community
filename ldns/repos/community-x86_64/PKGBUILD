# $Id$
# Maintainer: Gaetan Bisson <bisson@archlinux.org>
# Contributor: mathieui <mathieui@mathieui.net>
# Contributor: jiribb <jiribb@gmail.com>

pkgname=ldns
pkgver=1.6.12
pkgrel=1
pkgdesc='Fast DNS library supporting recent RFCs, including Drill and other tools'
url='http://www.nlnetlabs.nl/projects/ldns/'
license=('custom:BSD')
arch=('i686' 'x86_64')
options=('!libtool')
depends=('openssl')
makedepends=('libpcap')
optdepends=('libpcap: ldns-dpa tool')
source=("http://www.nlnetlabs.nl/downloads/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'Makefile.patch')
sha1sums=('1d61df0f666908551d5a62768f77d63e727810aa'
          '01bce260e9639d9cd26109a689dddf2498e5026a')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	patch -p1 -i ../Makefile.patch

	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-static=no \
		--disable-rpath \
		--with-drill \
		--with-examples \

	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"

	make DESTDIR="${pkgdir}" install

	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
