# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=mkinitcpio-systemd-tool
pkgver=30
pkgrel=2
pkgdesc="Provisioning tool for systemd in initramfs (systemd-tool)"
arch=('any')
url="https://github.com/random-archer/mkinitcpio-systemd-tool"
license=('Apache')
depends=('mkinitcpio' 'systemd')
optdepends=('cryptsetup: for initrd-cryptsetup.service'
            'dropbear: for initrd-dropbear.service'
            'busybox: for initrd-tinysshd.service'
            'mc: for initrd-debug-progs.service'
            'nftables: for initrd-nftables.service'
            'tinyssh: for initrd-tinysshd.service'
            'tinyssh-convert: for initrd-tinysshd.service')
conflicts=('mkinitcpio-dropbear' 'mkinitcpio-tinyssh')
backup=("etc/${pkgname}/config/crypttab"
        "etc/${pkgname}/config/fstab"
        "etc/${pkgname}/config/initrd-nftables.conf"
        "etc/${pkgname}/config/initrd-util-usb-hcd.conf"
        "etc/${pkgname}/network/initrd-network.network" )
source=("$pkgname-$pkgver.tar.gz::https://github.com/random-archer/${pkgname}/archive/v${pkgver}.tar.gz")
install="${pkgname}.install"
sha512sums=('094996038a326327874d5080a9ee1ac66c9beb415e0b555a74ce673d1a9f51b088035d1423a2a93a65bdb1e8e62bd732265c82a83117a962cc42096b9a44bcec')

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" PREFIX='/usr' install
}
