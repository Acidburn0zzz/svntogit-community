# $Id: PKGBUILD 91063 2010-09-21 19:21:24Z ibiru $
# Maintainer: Ionut Biru <ibiru@archlinux.org>
_pkgbasename=gdk-pixbuf2
pkgname=lib32-$_pkgbasename
pkgver=2.24.0
pkgrel=1
pkgdesc="An image loading library for gtk2 (32-bit)"
arch=('i686' 'x86_64')
url="http://www.gtk.org/"
license=('GPL2')
depends=('lib32-glib2>=2.25.15'
          lib32-lib{'png>=1.4.3','tiff>=3.9.4','jpeg>=8.0.2',x11}
          $_pkgbasename)
makedepends=(gcc-multilib)
options=('!libtool' '!docs')
install=gdk-pixbuf2.install
source=(http://download.gnome.org/sources/gdk-pixbuf/2.24/gdk-pixbuf-${pkgver}.tar.bz2)
md5sums=('d8ece3a4ade4a91c768328620e473ab8')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  ./configure --prefix=/usr  --libdir=/usr/lib32 \
    --without-libjasper \
    --with-included-loaders=png
  make
}

package() {
  cd "${srcdir}/gdk-pixbuf-${pkgver}"

  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/etc
  rm -rf "${pkgdir}"/usr/{include,share}

  cd "${pkgdir}"/usr/bin
  mv gdk-pixbuf-query-loaders gdk-pixbuf-query-loaders-32
  rm gdk-pixbuf-csource
}

# vim:set ts=2 sw=2 et:
