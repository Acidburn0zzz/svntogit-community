# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.7.1.r152.g178bb58c
pkgrel=1
_commit='178bb58c16069506908fa1332e4e6738ad159f42'
pkgdesc="Access the Mediathek of many German TV stations"
arch=('any')
url="https://github.com/mediathekview/MediathekView"
license=('GPL3')
depends=('bash' 'hicolor-icon-theme' 'java-runtime')
makedepends=('git' 'maven')
conflicts=('mediathek')
provides=('mediathek')
replaces=('mediathek')
optdepends=('libnotify: to use desktop notifications'
            'mplayer: for recording streams'
            'vlc: for stream playback')
source=("git+https://github.com/${pkgname}/${pkgname}#commit=${_commit}"
        "de.${pkgname}.${_name}.desktop"
        "${pkgname}.sh")
sha512sums=('SKIP'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '3b4b96bd7f8dd26b9fbc71b109c57257eb57969bfc67dabb87b4d541da37b196f54534eb38266c0b74407c5493aa35db3dd1374b84ab562eb02a02d1660e2ec8')
b2sums=('SKIP'
        '6dbcdea2918009621fc132b4ff1056ef79f06e27c3299b69ccd7e3cb2b093e3a2a5f76acd6b1ab62689edd867ac1650f61bf829f2a1c575835d31e117d9b9ae5'
        'db19620fdde16e95df7b866745d95484571db421c791d2f8e28e5b96c59347dc13cc24fab69f07a6d32d5567bdefd8dda7a7057018be7a98a8186d3b156aa298')

pkgver() {
  cd "$pkgname"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  ./mvnw clean install -Plinux,install4j-linux
}

package() {
  local _size
  cd "$pkgname"
  # jar
  install -vDm 644 "target/${_name}.jar" \
    -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
