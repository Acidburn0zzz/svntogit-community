# Maintainer: David Runge <dvzrv@archlinux.org>

_name=MediathekView
pkgname=mediathekview
pkgver=13.5.1
pkgrel=2
pkgdesc="Access the Mediathek of many German TV stations"
arch=('any')
url="https://github.com/mediathekview/MediathekView"
license=('GPL3')
depends=('bash' 'hicolor-icon-theme' 'java-runtime>=11')
makedepends=('maven')
optdepends=('mplayer: for recording streams'
            'vlc: for stream playback')
source=("$pkgname-$pkgver.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "de.${pkgname}.${_name}.desktop"
        "${pkgname}.sh")
sha512sums=('8e46259eb0ecc4f378d01fa378cf3f52d9e419194e9d6b37668bb07a9c3f6590dbd60a08ff4feff1d475c1266eb852279db37e11c76c32d39aa5ca02c7ae6daa'
            '24a94a078180aca7c50ed7763ef4806c116c27f901f644ef570ee413ffc3ac795b5ebd24d696a9b2ec426e7c9b6eaf8a8b22addb5ac7c9fe9700f7c04305f64b'
            '3769eb34164d967a729aeaf733e8a20a6ea123b9ba63fdead628c6b245cc4d578847fc2c800f4606d933d62a509adce5bd489353cd2c70c20f890089acf0c8d5')

prepare() {
  mv -v "${_name}-$pkgver" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  ./mvnw clean install -Plinux,install4j-linux
}

package() {
  local _size
  cd "$pkgname-$pkgver"
  # jar
  install -vDm 644 "target/${_name}.jar" \
    -t "${pkgdir}/usr/share/java/${pkgname}"
  # script
  install -vDm 755 "../${pkgname}.sh" \
    "${pkgdir}/usr/bin/${pkgname}"
  # XDG desktop file
  install -vDm 644 "../de.mediathekview.${_name}.desktop" \
    -t "${pkgdir}/usr/share/applications"
  # icons
  for _size in 16 32 48 128; do
    install -vDm 644 "target/${_name}@x${_size}.png" \
      "${pkgdir}/usr/share/icons/hicolor/${_size}x${_size}/apps/${pkgname}.png"
  done
  install -vDm 644 res/${_name}.svg \
    -t "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  # docs
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
