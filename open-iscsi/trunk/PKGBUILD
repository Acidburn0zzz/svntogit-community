# Maintainer: Stefan Kirrmann <stefan.kirrmann at gmail dot com>
pkgname=open-iscsi
pkgver=2.0.873
_pkgver=2.0-873
pkgrel=4
pkgdesc="High performance, transport independent iSCSI implementation"
arch=('i686' 'x86_64')
url="http://www.open-iscsi.org"
license=('GPL')
depends=('bash')
install=$pkgname.install
backup=('etc/iscsi/iscsid.conf' 'etc/iscsi/initiatorname.iscsi' 'etc/conf.d/open-iscsi')
source=("http://www.open-iscsi.org/bits/${pkgname}-${_pkgver}.tar.gz" \
        "open-iscsi" "open-iscsi.conf.d")
options=('docs')
changelog=Changelog
sha256sums=('7dd9f2f97da417560349a8da44ea4fcfe98bfd5ef284240a2cc4ff8e88ac7cd9'
            '649536c4abc9c4d9f994cc1c0033449d46c120b73a836854a228f7e103711314'
            '289ba27b72a3bd9d251e08b3d61dd3ae2f6ad545e5ffe2edaa2b79638996654c')

build() {
  cd ${srcdir}/${pkgname}-${_pkgver}

  # include iscsistart in the package
  sed -i -e '/^PROGRAMS = /s/$/ usr\/iscsistart/' Makefile

  # fix sbindir, so it installs to /usr/bin/ instead of /sbin
  sed -i -e 's/\/bin/\/usr\/bin/' Makefile
  sed -i -e 's/sbindir/bindir/' Makefile
  sed -i -e 's/\/sbin\//\/usr\/bin\//' Makefile

  # build breaks if the openslp package is installed
  sed -i -e 's/\(\.\/configure\)/ \1 --without-slp/g' Makefile 

  make user
}

package(){
  cd ${srcdir}/${pkgname}-${_pkgver}
  make DESTDIR=${pkgdir} install_user
  
  # install config files
  install -D -m755 ${srcdir}/open-iscsi ${pkgdir}/etc/rc.d/open-iscsi
  install -D -m644 ${srcdir}/open-iscsi.conf.d ${pkgdir}/etc/conf.d/open-iscsi
  install -D -m644 ${srcdir}/${pkgname}-${_pkgver}/etc/iscsid.conf ${pkgdir}/etc/iscsi

  touch ${pkgdir}/etc/iscsi/initiatorname.iscsi
  
  # copy docs
  mkdir -p ${pkgdir}/usr/share/doc/${pkgname}
  install -m644 README ${pkgdir}/usr/share/doc/${pkgname}/
}
