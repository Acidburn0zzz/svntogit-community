# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-jsonpickle
pkgver=1.2
pkgrel=3
arch=('any')
pkgdesc="Python library for serializing any arbitrary object graph into JSON"
url="https://jsonpickle.github.io/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose-show-skipped' 'python-numpy' 'python-feedparser' 'python-coverage'
              'python-simplejson' 'python-pymongo' 'python-ujson')
license=('BSD')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jsonpickle/jsonpickle/archive/v$pkgver.tar.gz"
         jsonpickle-python-3.8.patch)
sha512sums=('32bcb3df5d2833a52e52d27f6594124119c33f2ad7c111074da4670711f8651c208f4bb1ec826752044b3b2d19053860679d124d9a81f9e303d1377f1e3900c0'
            'a07bda01508bbbe36f31c9f3be46507bff0d3f81cfb0df5eb6702690db1ba5d853aab1065b7a79e89bc9c8abaad01dd9fc6f71e59194f819addf99b0c28f0c1b')

prepare() {
  cd jsonpickle-$pkgver
# Fix tests with python 3.8 - file descriptors no longer have __getstate__
  patch -p1 -i ../jsonpickle-python-3.8.patch
}

build() {
  cd "$srcdir"/jsonpickle-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/jsonpickle-$pkgver
  nosetests3 --show-skipped
}

package() {
  cd jsonpickle-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
