# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ipvsadm
pkgver=1.26
pkgrel=7
pkgdesc='The IP Virtual Server administration utility'
arch=('i686' 'x86_64')
url='http://www.linuxvirtualserver.org/software/ipvs.html'
license=('GPL2')
depends=('libnl1' 'popt' 'bash')
backup=("etc/$pkgname.conf")
options=('!makeflags')
install=$pkgname.install
source=("http://www.linuxvirtualserver.org/software/kernel-2.6/$pkgname-$pkgver.tar.gz"
        "$pkgname.systemd"
        "$pkgname.service"
        '01-fix-not-showing-all-server.patch')
md5sums=('eac3ba3f62cd4dea2da353aeddd353a8'
         'b45ef25d384a7a68b51ff80d0fe81dd2'
         'dc87684064f85b64c1e79c1eac336303'
         'e9fb77d2fbe2d3f41d60257316338543')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i ../01-fix-not-showing-all-server.patch
  make
}

package() {
  pushd $pkgname-$pkgver
  make \
    BUILD_ROOT="$pkgdir" \
    MANDIR=usr/share/man \
    SBIN="$pkgdir/usr/sbin" \
    INIT="$pkgdir/etc/rc.d" \
    install
  popd
  # systemd
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # remove legacy init system
  rm -rf "$pkgdir/etc/rc.d"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 et:
