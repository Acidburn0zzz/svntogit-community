# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=ipvsadm
pkgver=1.26
pkgrel=5
pkgdesc='The IP Virtual Server administration utility'
arch=('i686' 'x86_64')
url='http://www.linuxvirtualserver.org/software/ipvs.html'
license=('GPL2')
depends=('libnl1' 'popt')
backup=("etc/$pkgname.conf" "etc/conf.d/$pkgname")
options=('!makeflags')
install=$pkgname.install
source=("http://www.linuxvirtualserver.org/software/kernel-2.6/$pkgname-$pkgver.tar.gz"
        "$pkgname.conf"
        "$pkgname.rc"
        "$pkgname.systemd"
        "$pkgname.service"
        '01-fix-not-showing-all-server.patch')
md5sums=('eac3ba3f62cd4dea2da353aeddd353a8'
         'b6cb7724aa6c61c3a9cdd40e8eaa973e'
         'f2f4a7c6d57c5301c5f12f9b323fdea8'
         'df28308873b437eeb55657f6aa438ecd'
         'dc87684064f85b64c1e79c1eac336303'
         'e9fb77d2fbe2d3f41d60257316338543')

build() {
  cd $pkgname-$pkgver
  patch -p1 -i ../01-fix-not-showing-all-server.patch
  make #INCLUDE="-I/usr/src/linux-$(uname -r)/include -I.. -I."
}

package() {
  pushd $pkgname-$pkgver
  make \
    BUILD_ROOT="$pkgdir" \
    MANDIR=usr/share/man \
    SBIN="$pkgdir/usr/sbin" \
    INIT="$pkgdir/etc/rc.d" \
    install
  popd
  # install initscripts
  install -Dm 755 $pkgname.rc "$pkgdir/etc/rc.d/$pkgname"
  install -Dm 644 $pkgname.conf "$pkgdir/etc/conf.d/$pkgname"
  # install systemd service
  install -Dm 755 $pkgname.systemd \
    "$pkgdir/usr/lib/systemd/scripts/$pkgname"
  install -Dm 644 $pkgname.service \
    "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  # default config file
  install -Dm 644 /dev/null "$pkgdir/etc/$pkgname.conf"
}

# vim:set ts=2 sw=2 ft=sh et:
