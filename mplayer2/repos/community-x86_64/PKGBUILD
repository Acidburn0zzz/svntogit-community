# $Id$
# Maintainer:  Bartłomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Martin Panter <vadmium+aur@gmail.com>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>

pkgname=mplayer2
pkgver=20120729
pkgrel=2
pkgdesc='Advanced general-purpose media player. A fork of the original MPlayer project'
arch=('i686' 'x86_64')
license=('GPL')
url="http://www.mplayer2.org/"
install=$pkgname.install
depends=('a52dec' 'aalib' 'cdparanoia' 'desktop-file-utils' 'enca' 'faad2' 'ffmpeg' 'fontconfig'
         'freetype2' 'jack' 'ladspa' 'lame' 'libass' 'libbluray' 'libcaca' 'libcdio' 'libdca' 'libdvdcss'
         'libdvdnav' 'libdvdread' 'libgl' 'libjpeg' 'libmad' 'libpulse' 'libquvi' 'libtheora' 'libvdpau'
         'libxinerama' 'libxss' 'libxv' 'libxxf86dga' 'libxxf86vm' 'lirc-utils' 'mpg123' 'ncurses' 'sdl' 'ttf-dejavu')
makedepends=('mesa' 'unzip' 'yasm' 'python' 'python-docutils')
backup=('etc/mplayer/codecs.conf' 'etc/mplayer/input.conf')
provides=('mplayer')
conflicts=('mplayer')
options=(!emptydirs)
source=(ftp://ftp.archlinux.org/other/community/$pkgname/$pkgname-$pkgver.tar.xz
		mplayer2-20120729-fix-vf_lavc-compilation.patch
		mplayer2-20120729-libquvi-support.patch)
md5sums=('53a266106f4c0dd687af3f807727812c'
         'b1e9188001d31b066488aa35401b62bc'
         'd5a23b19d1f84399526bd0dd9bae0e90')

build() {
  cd $srcdir/$pkgname-$pkgver

  patch -Np1 -i $srcdir/mplayer2-20120729-libquvi-support.patch
  patch -Np1 -i $srcdir/mplayer2-20120729-fix-vf_lavc-compilation.patch

  sed 's/gmplayer/mplayer/g' -i etc/mplayer.desktop
  find -type f -exec sed -e 's/python3/python/' -i {} \;

  ./configure --prefix=/usr --confdir=/etc/mplayer \
              --enable-translation --language=all \
              --enable-runtime-cpudetection \
              --enable-joystick \
              --disable-speex \
              --disable-openal \
              --disable-libdv \
              --disable-musepack
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install

  install -Dm644 etc/{codecs.conf,input.conf,example.conf} $pkgdir/etc/mplayer/
  install -dm755 $pkgdir/usr/share/mplayer/
  ln -s /usr/share/fonts/TTF/DejaVuSans.ttf $pkgdir/usr/share/mplayer/subfont.ttf

  install -dm755 $pkgdir/usr/share/applications/
  install -m 644 etc/mplayer.desktop $pkgdir/usr/share/applications/
}
