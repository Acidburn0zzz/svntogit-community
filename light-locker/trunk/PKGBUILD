# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=light-locker
pkgver=1.9.0
pkgrel=1
pkgdesc='A simple session locker for LightDM'
arch=(x86_64)
url=https://github.com/the-cavalry/light-locker
license=(GPL2)
depends=(
  dbus-glib
  gtk3
  lightdm
  libsystemd
  libxss
  upower
)
makedepends=(
  git
  gnome-common
  intltool
  meson
  xorgproto
)
source=(git+https://github.com/the-cavalry/light-locker.git#tag=7a96e9f87dc95c794cd45fd0378a62f45308ea87)
sha256sums=(SKIP)

pkgver() {
  cd light-locker

  git describe --tags | sed 's/^v//'
}

build () {
  arch-meson light-locker build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
