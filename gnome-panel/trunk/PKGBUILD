# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.18.2
pkgrel=2
pkgdesc="Panel of GNOME Flashback"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomePanel"
license=('GPL')
depends=('evolution-data-server' 'gnome-desktop' 'gnome-menus' 'libwnck3')
#makedepends=('intltool' 'itstool' 'gobject-introspection')
makedepends=('intltool' 'itstool' 'gobject-introspection' 'gtk-doc' 'autoconf-archive')
optdepends=('alacarte: Main menu editor'
            'gnome-applets: Extra applets for the panel')
install=$pkgname.install
#source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
source=(https://git.gnome.org/browse/gnome-panel/snapshot/gnome-panel-9814d539d3a858e4de831a9451cda66f0f947209.tar.xz)
#sha256sums=('e2d8a51a2b9b3a4aa948647992f6fee23c3e69ec5639f2a824383e8a61fbac29')
sha256sums=('6a3d2128a7963b1beeff284b1d5376bc3d227390b6cec15b271003809ee33598')

prepare() {
  cd $pkgname-*
  NOCONFIGURE=1 ./autogen.sh
}

build() {
#  cd $pkgname-$pkgver
  cd $pkgname-*
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile

  #https://bugzilla.gnome.org/show_bug.cgi?id=656231
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

  make
}

package() {
#  cd $pkgname-$pkgver
  cd $pkgname-*
  make DESTDIR="$pkgdir" install
}
