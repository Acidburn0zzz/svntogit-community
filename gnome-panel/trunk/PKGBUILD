# Maintainer:
# Contributor: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=gnome-panel
pkgname=('gnome-panel' 'gnome-flashback-session')
pkgver=3.8.0
pkgrel=2
arch=('i686' 'x86_64')
license=('GPL')
pkgdesc="Legacy GNOME panel"
url="https://live.gnome.org/GnomePanel"
depends=('dconf' 'evolution-data-server' 'gconf' 'gnome-desktop' 'gnome-menus' 'librsvg'
         'libwnck3' 'telepathy-glib')
makedepends=('gobject-introspection' 'intltool' 'itstool' 'networkmanager')
install=$pkgbase.install
options=('!libtool')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgbase/${pkgver:0:3}/$pkgbase-$pkgver.tar.xz
        libgweather-3.10.patch
        session.patch)
sha256sums=('d1cda458a38cb4f7d2a50c61b1a72f4bf4c39cab65ae0e6cfdc4e0c122252e7a'
            'd9e21449fc6c5f235673fe47e5b817ac7597490518a557908b14ca875263d90e'
            'cd4c21cf394ea5a702c660bb0d31cae31260bec1cee4e7ca8ee6c8aec2707ebb')

prepare() {
  cd "$pkgbase-$pkgver"

  # Fix build with libgweather 3.10
  patch -Np1 -i "$srcdir/libgweather-3.10.patch"

  # Add more required components to the session file
  patch -Np1 -i "$srcdir/session.patch"
}

build() {
  cd "$pkgbase-$pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgbase \
              --disable-static --disable-schemas-compile \
              PYTHON=/usr/bin/python2 
  make
}

package_gnome-panel() {
  cd "$pkgbase-$pkgver"
  make DESTDIR="$pkgdir" install

  # Split out the session files
  rm -r "$pkgdir"/usr/{lib/gnome-panel/gnome-*,share/{gnome-session,xsessions}}
}

package_gnome-flashback-session() {
  pkgdesc="Shell for Gnome 3 which was initially called Gnome Fallback"
  url="https://live.gnome.org/GnomeFlashback"
  depends=('gnome-screensaver' 'gnome-panel' 'gnome-session' 'gnome-settings-daemon'
           'gnome-settings-daemon-compat' 'gnome-themes-standard' 'metacity'
           'notification-daemon' 'polkit-gnome')
  install=

  cd "$pkgbase-$pkgver"
  make -C gnome-panel DESTDIR="$pkgdir" install-libexecSCRIPTS
  make -C data DESTDIR="$pkgdir" install-sessionDATA
  make -C data DESTDIR="$pkgdir" install-xsessionDATA
}

# vim:set ts=2 sw=2 et:
