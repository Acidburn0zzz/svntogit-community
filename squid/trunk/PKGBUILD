# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Mark Coolen <mark.coolen@gmail.com>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Kevin Piche <kevin@archlinux.org>

pkgname=squid
pkgver=3.2.3
pkgrel=1
pkgdesc='Full-featured Web proxy cache server'
arch=('x86_64' 'i686')
url='http://www.squid-cache.org'
depends=('openssl' 'pam' 'cron' 'perl' 'libltdl')
makedepends=('libcap')
license=('GPL')
options=('!emptydirs')
backup=('etc/squid/squid.conf'
        'etc/squid/mime.conf'
        'etc/conf.d/squid')
install=$pkgname.install
source=("http://www.squid-cache.org/Versions/v3/3.2/$pkgname-$pkgver.tar.bz2"
        'squid'
        'squid.conf.d'
        'squid.pam'
        'squid.cron'
        'squid.service')

sha256sums=('1741c3ef647f5b0960498b7bb3e44af4a4409c321afe2d141c67d2b5c85ed5a1'
            'd809b467ead9b233b54a918d92501ed3214ad92255658d23c435635eafa2a596'
            '92ca3ff6ed26f71f20947defef67ff397038d5402b74aeed739ea504af2b188d'
            '11fb388f8679fd6461e0de006810ea608a3686fffda16904b0ed71f412be499c'
            '6b05c266892b45bee0b2540c8863f272118ad98a64cdd9175b843312310b8f4f'
            'f853e55c93fdd55b536c7e611068845d04759b0f4366e821b93ae29437fb9495')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # gcc 4.6 doesn't support -fhuge-objects.
  sed '/^    HUGE_OBJECT_FLAG=/ s/"-fhuge-objects"//' -i configure

  # fix cache_dir, cache_dir size, and effective group.
  sed '/^DEFAULT_SWAP_DIR/ s@/cache@/cache/squid@' -i src/Makefile.in
  sed '/^#cache_dir/ s/100/256/
       /^NAME: cache_effective_group/ {n;n;s/none/proxy/}' -i src/cf.data.pre

  ./configure \
    --prefix=/usr \
    --datadir=/usr/share/squid \
    --sysconfdir=/etc/squid \
    --libexecdir=/usr/lib/squid \
    --localstatedir=/var \
    --with-logdir=/var/log/squid \
    --with-pidfile=/run/squid.pid \
    --enable-auth \
    --enable-auth-basic \
    --enable-auth-ntlm \
    --enable-auth-digest \
    --enable-auth-negotiate \
    --enable-removal-policies="lru,heap" \
    --enable-storeio="aufs,ufs,diskd" \
    --enable-delay-pools \
    --enable-arp-acl \
    --enable-ssl \
    --enable-snmp \
    --enable-linux-netfilter \
    --enable-ident-lookups \
    --enable-useragent-log \
    --enable-cache-digests \
    --enable-referer-log \
    --enable-arp-acl \
    --enable-htcp \
    --enable-carp \
    --enable-epoll \
    --with-filedescriptors=4096 \
    --with-large-files \
    --enable-arp-acl \
    --with-default-user=proxy \
    --enable-async-io \
    --enable-truncate
  make
}

package() {
  cd "$srcdir"

  make -C "$pkgname-$pkgver" DESTDIR="$pkgdir" install
  install -Dm755 "$srcdir/squid" "$pkgdir/etc/rc.d/squid"
  install -Dm755 "$srcdir/squid.cron" "$pkgdir/etc/cron.weekly/squid"
  install -Dm755 "$srcdir/squid.conf.d" "$pkgdir/etc/conf.d/squid"
  install -Dm644 "$srcdir/squid.pam" "$pkgdir/etc/pam.d/squid"
  install -Dm644 "$srcdir/squid.service" \
    "$pkgdir/usr/lib/systemd/system/squid.service"
}

# vim: ts=2 sw=2 et ft=sh
