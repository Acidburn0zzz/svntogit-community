# $Id$
# Maintainer:
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=proftpd
pkgver=1.3.4b
pkgrel=3
epoch=1
pkgdesc="A high-performance, scalable FTP server"
arch=('i686' 'x86_64')
url="http://www.proftpd.org/"
license=('GPL')
depends=('libmysqlclient' 'postgresql-libs')
backup=('etc/proftpd.conf' 'etc/conf.d/proftpd')
install=${pkgname}.install
source=(ftp://ftp.proftpd.org/distrib/source/${pkgname}-${pkgver}.tar.gz{,.asc}
        'proftpd' 'proftpd.logrotate' 'proftpd.conf.d' 'proftpd.service'
        'proftpd.tmpfiles')
md5sums=('0871e0b93c9c3c88ca950b6d9a04aed2'
         'e5b9bd78029a15f82994efcb7ed2e9fb'
         'c439a0a1dbc21b604d8382da87aa021b'
         'ddb09eb13131becdf0e081eef413116b'
         '71d5932b0461c318ed68c2c0c2660736'
         '5709f27ebcbe906e52963ea75e4fde64'
         '907b149a120b046f05647c73502e23c9')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  ./configure --prefix=/usr \
    --libexecdir=/usr/lib \
    --disable-pam \
    --with-modules=mod_quotatab:mod_quotatab_sql:mod_quotatab_file:mod_tls:mod_ldap:mod_sql:mod_sql_mysql:mod_sql_postgres \
    --sysconfdir=/etc \
    --localstatedir=/run/proftpd \
    --enable-ctrls \
    --enable-ipv6 \
    --with-includes=/usr/include/mysql:/usr/include/postgresql \
    --with-libraries=/usr/lib/mysql:/usr/lib/postgresql \
    --enable-nls \
    --with-systemd

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install
  sed -i 's|nogroup|nobody|g' "${pkgdir}/etc/proftpd.conf"

  install -Dm644 ../proftpd.logrotate "${pkgdir}/etc/logrotate.d/proftpd"
  install -Dm644 ../proftpd.conf.d "${pkgdir}/etc/conf.d/proftpd"
  install -Dm755 ../proftpd "${pkgdir}/etc/rc.d/proftpd"
  install -Dm755 contrib/xferstats.holger-preiss "${pkgdir}/usr/bin/ftpstats"

  install -d "${pkgdir}/usr/lib/systemd/system/"
  install -m644 "${srcdir}"/proftpd.service "${pkgdir}/usr/lib/systemd/system/"
  install -Dm644 "${srcdir}"/proftpd.tmpfiles \
    "${pkgdir}"/usr/lib/tmpfiles.d/proftpd.conf

  # /run is tmpfs
  rmdir "${pkgdir}"/run/{proftpd,}
}
