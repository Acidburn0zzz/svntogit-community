# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=python-wxpython
pkgver=4.0.1
pkgrel=1
pkgdesc="Phoenix wxWidgets GUI toolkit for Python"
arch=('x86_64')
license=('custom:wxWindows')
url="https://www.wxpython.org"
depends=('wxgtk3' 'python-setuptools' 'python-sip' 'python-six')
makedepends=('mesa' 'glu')
source=("https://github.com/wxWidgets/Phoenix/releases/download/wxPython-${pkgver}/wxPython-${pkgver}.tar.gz"
        "unbundle-sip.patch")
sha256sums=('f8f2ac1a75368b9b103259addc77f4e3dfe729c6d70aa1fd0b7e9c5b6075c710'
            '060badc5667993cd4120c8779f812d892d9a46816287b622af02756bb330cc9f')

prepare() {
    cd wxPython-${pkgver}
    patch -Np1 -i ../unbundle-sip.patch
    find -name '*.py' | xargs sed -i 's/wx.siplib/sip/'
    rm -rf sip
}

build() {
    cd wxPython-${pkgver}
    python3 setup.py build
}

package() {
    cd wxPython-${pkgver}
    python3 setup.py install --root="${pkgdir}" --optimize=1
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
    find "$pkgdir/usr/lib" -type f | xargs chmod 644
}
