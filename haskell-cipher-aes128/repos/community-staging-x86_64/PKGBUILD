# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=cipher-aes128
pkgname=haskell-cipher-aes128
pkgver=0.7.0.4
pkgrel=7
pkgdesc="AES and common modes using AES-NI when available."
url="https://github.com/TomMD/cipher-aes128"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-crypto-api' 'haskell-tagged' 'haskell-cereal')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('3a33c8f4c064ece06e16174243e90ddf2103bfba991835c0312ad97706b6754d04248e9aebb40455992f7e7f71b08672a76ab89b57d31481b9a807c4b3ce127f')

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
