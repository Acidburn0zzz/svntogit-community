# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Ben Wolsieffer <benwolsieffer@gmail.com>

pkgname=netbeans
pkgver=9.0
pkgrel=1
pkgdesc='IDE for Java, HTML5, PHP, Groovy, C and C++'
arch=('any')
url='http://netbeans.apache.org/'
license=('Apache2')
# classpath is not needed, see FS#38567 (Oracle JDK does not need atk either)
depends=('java-environment' 'ttf-font' 'jdk10-openjdk')
makedepends=('ant' 'java-environment' 'jdk8-openjdk')
optdepends=('php: for developing programs in php'
            'groovy: for developing programs in groovy'
            'lib32-glibc')
source=("https://www.apache.org/dist/incubator/netbeans/incubating-netbeans-java/incubating-9.0/incubating-${pkgname}-java-${pkgver}-source.zip"{,.asc}
       "netbeans.clusters")

sha256sums=('cc4cdaf3e1a6c80227410a6e54414e2109516d11a7150b7b9660061135bbc8db'
            'SKIP'
            'a7849ee0f21acef34a7bbd1216e8f822aba38284e3138ea04532224ad6f227c2')
validpgpkeys=("1A83C352499305B6682E3D95CF7BA0AB1CCF4647")

build() {
  # full cluster configuration is not available at this time
  #ant -quiet -Dcluster.config=full
  export JAVA_HOME=/usr/lib/jvm/java-8-openjdk/
  ant -quiet -Dnbjdk.home=/usr/lib/jvm/java-8-openjdk -Djava.home=/usr/lib/jvm/java-8-openjdk
}

check() {
  cd "${srcdir}"
  ant -quiet test -Djavac.compilerargs=-nowarn -Dbuild.compiler.deprecation=false -Dtest.includes=NoTestsJustBuild
}

package() {

  # load script
  install -Dm755 "nbbuild/${pkgname}/bin/${pkgname}" "$pkgdir/usr/bin/$pkgname"

  # Resources for platform config
  mkdir -vp "${pkgdir}/usr/lib/${pkgname}"
  cp -r "nbbuild/${pkgname}/"{java,harness,javafx,profiler,nb,ide,extide,apisupport,websvccommon,platform} "$pkgdir/usr/lib/${pkgname}"

  # copy cluster configuration
  install -Dm644 "${srcdir}/nbbuild/netbeans/etc/netbeans."* -t "${pkgdir}/usr/etc/"
  install -Dm644 "netbeans.clusters" -t "${pkgdir}/usr/etc/"

  # Desktop shortcut and icon
  install -Dm644 "nbbuild/packaging/snap/gui/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  sed -i "s/Icon=.*/Icon=\/usr\/share\/pixmaps\/${pkgname}.png/" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 "ide.branding/release/${pkgname}.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"

}

# getver: netbeans.org
# vim:set ts=2 sw=2 et:
