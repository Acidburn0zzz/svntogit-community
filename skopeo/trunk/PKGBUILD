# Maintainer: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>

pkgname=skopeo
pkgver=0.1.31
pkgrel=1
pkgdesc='A command line utility for various operations on container images and image repositories.'
arch=(x86_64)
url='https://github.com/projectatomic/skopeo'
license=(APACHE)
depends=(gpgme device-mapper ostree)
makedepends=(go-pie go-md2man btrfs-progs git)
_commit=b0b750dfa1bc598a643945c7f292204fa0f0bf09  # tags/v0.1.31
source=(git+$url#commit=$_commit
        0001-Revert-ostree-add-selinux-label-for-each-file.patch)
sha256sums=('SKIP'
            'e9ac315d13c61e18cd96e17d35e6623ed5fa6dc444c2536ce24c80aed159d8c9')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

prepare() {
  pushd $pkgname/vendor/github.com/containers/image
  patch -p1 -i "$srcdir/0001-Revert-ostree-add-selinux-label-for-each-file.patch"
  popd

  export GOPATH="$srcdir"
  mkdir -p src/github.com/projectatomic
  cp -r $pkgname src/github.com/projectatomic/skopeo
}

build() {
  cd src/github.com/projectatomic/skopeo
  go build -v -o skopeo ./cmd/skopeo
  make docs
}

package() {
  cd src/github.com/projectatomic/skopeo
  make DESTDIR="$pkgdir" install
}
