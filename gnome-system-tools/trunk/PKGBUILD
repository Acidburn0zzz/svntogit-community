# $Id$
# Maintainer : Biru Ionut <ionut@archlinux.ro>
# Contributor: Hugo Doria <hugodoria@archlinux-br.org>

pkgname=gnome-system-tools
pkgver=2.28.1
pkgrel=1
pkgdesc="Gnome System Configuration Utilities"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gst/"
license=('GPL')
depends=('wireless_tools' 'liboobs>=2.22.2' 'nautilus>=2.28' 'libgnomeui' 'libglade' 'polkit-gnome>=0.94')
makedepends=('perlxml' 'gnome-doc-utils>=0.18.0' 'pkgconfig' 'intltool>=0.40')
install=gnome-system-tools.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/2.28/${pkgname}-${pkgver}.tar.bz2)

build() {
	cd $srcdir/${pkgname}-${pkgver}

	./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
		--disable-scrollkeeper --disable-static
	make || return 1
	make DESTDIR=$pkgdir GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 GCONFTOOL=/bin/true install || return 1

	mkdir -p $pkgdir/usr/share/gconf/schemas
	gconf-merge-schema $pkgdir/usr/share/gconf/schemas/${pkgname}.schemas $pkgdir/etc/gconf/schemas/*.schemas
	rm -f $pkgdir/etc/gconf/schemas/*.schemas
}
md5sums=('1614c0727801a9cb124c773f8bd76291')
