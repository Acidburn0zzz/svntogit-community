# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Hugo Doria <hugodoria@archlinux-br.org>

pkgname=gnome-system-tools
pkgver=3.0.0
pkgrel=2
pkgdesc="Gnome System Configuration Utilities"
arch=('i686' 'x86_64')
url="http://www.gnome.org/projects/gst/"
license=('GPL')
options=('!libtool')
depends=('wireless_tools' 'liboobs' 'nautilus' 'polkit-gnome' 'dconf')
makedepends=('gnome-doc-utils' 'intltool' 'gnome-common')
install=gnome-system-tools.install
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/3.0/${pkgname}-${pkgver}.tar.bz2
        po.patch)
sha256sums=('905df26c02f00a6c2c18706ba3db7eab764e3df6576289e103504aa39a47ae0b'
            '827c3169ae5060403b3f833b0f9c1d5cefe1d80aa5ade22e2ff7908b1ce1b5ef')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    patch -Np1 -i "${srcdir}/po.patch"
    gnome-autogen.sh
    ./configure --prefix=/usr  --sysconfdir=/etc \
        --localstatedir=/var --disable-scrollkeeper --disable-static \
        --disable-services --disable-schemas-compile
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
