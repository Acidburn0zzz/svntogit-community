# Maintainer: David Runge <dave@sleepmap.de>
pkgname=polyphone
pkgver=2.0.1
pkgrel=2
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'qt5-svg' 'portaudio' 'rtmidi' 'stk')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('6eb59f5b19c2a944dd2cc2bb06391a32e786444f2e796d223adb2066c1965293f67ceb4f42b968e6ce101fed0a4cc4cb49a2011527701ac7bdede059548a6eac')

prepare() {
  cd "${pkgname}-${pkgver}/sources"
  # we need to use the vendored qcustomplot, as the current is incompatible
  # https://github.com/davy7125/polyphone/issues/56
  sed -e '/USE_LOCAL_QCUSTOMPLOT/ s/#//' -i "${pkgname}.pro"
}

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro"
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 ../logo.png \
    "${pkgdir}/usr/share/icons/128x128/apps/${pkgname}.png"
  install -vDm 644 "../deployment/debian/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "../deployment/debian/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "../deployment/debian/${pkgname}.sharedmimeinfo" \
    "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
}
