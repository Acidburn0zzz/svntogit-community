# Maintainer: David Runge <dave@sleepmap.de>
pkgname=polyphone
pkgver=2.1.2
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'flac' 'gcc-libs' 'glibc' 'hicolor-icon-theme'
'libjack.so' 'libogg' 'libqcustomplot.so' 'libvorbis.so' 'libvorbisenc.so'
'libvorbisfile.so' 'openssl' 'qt5-base' 'qt5-svg' 'portaudio' 'rtmidi' 'stk')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('241e0ca3e76788870239a9897c3a11909fd840fca0ba33c60f9a1da5b85890809403b6aaf97931e68286621eccfef5be622286dc3035e4ac4735c47a59c808fb')

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro"
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "resources/logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -vDm 644 "resources/${pkgname}.png" \
    -t "${pkgdir}/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 "contrib/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "contrib/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "contrib/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
