# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=polyphone
pkgver=2.2.0
pkgrel=1
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL3')
groups=('pro-audio')
depends=('desktop-file-utils' 'libFLAC.so' 'gcc-libs' 'glibc'
'hicolor-icon-theme' 'libjack.so' 'libogg' 'libportaudio.so'
'libqcustomplot.so' 'libstk.so' 'libvorbis.so' 'libvorbisenc.so'
'libvorbisfile.so' 'openssl' 'qt5-base' 'qt5-svg' 'rtmidi' 'zlib')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('88b4f0d951f75ed517e0942e75d543eca28c92fe8f2f99aee1d4be69f7d4fd74ee2219d7779b68b918befc8554cac5dc3f93ac9055bd8a94327e777a0725e5e3')

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro" PREFIX=/usr
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target: https://github.com/davy7125/polyphone/issues/62
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 "resources/logo.svg" \
    "${pkgdir}/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
  install -vDm 644 "resources/${pkgname}.png" \
    -t "${pkgdir}/usr/share/icons/hicolor/512x512/apps"
  install -vDm 644 "contrib/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "contrib/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "contrib/${pkgname}.xml" \
    -t "${pkgdir}/usr/share/mime/packages/"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
