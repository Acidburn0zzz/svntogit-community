# Maintainer: David Runge <dave@sleepmap.de>
pkgname=polyphone
pkgver=2.0.1
pkgrel=5
pkgdesc="A soundfont editor for quickly designing musical instruments"
arch=('x86_64')
url="https://polyphone-soundfonts.com/en/"
license=('GPL')
groups=('pro-audio')
depends=('desktop-file-utils' 'hicolor-icon-theme' 'qt5-svg' 'portaudio'
'qcustomplot' 'rtmidi' 'stk')
makedepends=('qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/davy7125/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-2.0.1-nullptr.patch::https://github.com/davy7125/polyphone/commit/a8223e1.patch"
        "${pkgname}-2.0.1-qcustomplot.patch::https://github.com/davy7125/polyphone/commit/9616c1a1d014c802288ae853b98e3eaab839b909.patch")
sha512sums=('f8f75d4b0fd8c2acb00c766180b03d76c4ea560c5f685916c4cfcdd8614e80a601246a50194330ab388352db16a8d03c472d370a6ad33308170303e2862368d2'
            'f6867703525e3def2d981ae65e48807c25f388142ba1a715b28edee0046762beaf432ec110a0e687bc01bbf4b5b56cabcfa67301b6dd87ca23340c0bb8a9ecd9'
            '65747fe274feb4ff01df3e9e52d5635acfb13c397cd752b14427e6533d401ddc1504d25483171fbb358b4cb1772b1db3fe302b3f0b59ec67ea4d9ed885b23b73')

prepare() {
  cd "${pkgname}-${pkgver}"
  # adding patches to allow system qcustomplot to be used:
  # https://github.com/davy7125/polyphone/issues/56
  patch -Np1 -i ../${pkgname}-2.0.1-nullptr.patch
  patch -Np1 -i ../${pkgname}-2.0.1-qcustomplot.patch
}

build() {
  cd "${pkgname}-${pkgver}/sources"
  qmake-qt5 "${pkgname}.pro"
  make
}

package() {
  cd "${pkgname}-${pkgver}/sources"
  # doesn't have an install target
  install -vDm 755 "bin/${pkgname}" -t "${pkgdir}/usr/bin/"
  install -vDm 644 ../logo.png \
    "${pkgdir}/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"
  install -vDm 644 "../deployment/debian/${pkgname}."*1 \
    -t "${pkgdir}/usr/share/man/man1/"
  install -vDm 644 "../deployment/debian/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 "../deployment/debian/${pkgname}.sharedmimeinfo" \
    "${pkgdir}/usr/share/mime/packages/${pkgname}.xml"
  install -vDm 644 {../README.md,changelog} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
