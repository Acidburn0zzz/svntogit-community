# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=perl-net-idn-encode
_cpanname=Net-IDN-Encode
pkgver=2.401
pkgrel=1
pkgdesc="Net::IDN::Encode provides an easy-to-use interface for encoding and decoding Internationalized Domain Names (IDNs)."
arch=('any')
url="http://search.cpan.org/~cfaerber/${_cpanname}/"
license=('GPL')
depends=('perl')
checkdepends=('perl-test-nowarnings' 'perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/C/CF/CFAERBER/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('e66bd90d40554ea17e0c2cc0ae1a741325c505dc4d970a05cd104eb81ac4be38bc8136701caf25bd9bd118145a7a97bec173f21e1fc22b34bb11e920be5ee6b6')

build() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  make test
}

package() {
  cd "$srcdir/${_cpanname}-${pkgver}"
  make install DESTDIR="$pkgdir/"
}

# vim:set ts=2 sw=2 sts=2 et:
