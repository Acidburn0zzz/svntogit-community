# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>

pkgname=toml-bombadil
pkgver=3.0.0
pkgrel=1
pkgdesc="Adotfile manager with templating"
arch=('x86_64')
url="https://github.com/oknozor/toml-bombadil"
license=('MIT')
depends=('gcc-libs')
makedepends=('rust')
options=(!lto)
source=($pkgname-$pkgver.tar.gz::https://github.com/oknozor/toml-bombadil/archive/v${pkgver}.tar.gz)
sha512sums=('7afc9d60a9931e571a0b83fba168b87546a6eedc924465eceb9004e3416286165f9ad1ad658bc44dc419e4b83a4fd6193eeba60732b48f89b1a801ca0d132ebf')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo build --release --locked
}

check() {
  cd "$srcdir/$pkgname-$pkgver"

  cargo test --locked
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  install -Dm755 target/release/toml-bombadil "$pkgdir"/usr/bin/toml-bombadil
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
