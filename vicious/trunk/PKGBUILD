# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=vicious
pkgver=2.0.4
pkgrel=2
pkgdesc='Widgets for the Awesome window manager'
arch=('any')
url='http://git.sysphere.org/vicious/about/'
license=('GPL2')
depends=('awesome')
conflicts=('vicious-git')
optdepends=(
	'hddtemp: for the HDD Temp widget type'
  'alsa-utils: for the Volume widget type'
  'wireless_tools: for the Wireless widget type'
  'curl: for widget types accessing network resources'
)
source=("http://git.sysphere.org/vicious/snapshot/$pkgname-$pkgver.tar.gz")
md5sums=('12bb4c438bcef5a3aaec76d20e697d88')

package() {
	cd $pkgname-$pkgver
	# Install the vicious library
  install -dm755 "$pkgdir/usr/share/awesome/lib/$pkgname"
  install -m644 *.lua "$pkgdir/usr/share/awesome/lib/$pkgname"
	# Install vicious official widgets
  install -dm755 "$pkgdir/usr/share/awesome/lib/$pkgname/widgets"
  install -m644 widgets/*.lua "$pkgdir/usr/share/awesome/lib/$pkgname/widgets"
	# Install user contributed widgets
  install -dm755 "$pkgdir/usr/share/awesome/lib/$pkgname/contrib"
  install -m644 contrib/*.lua "$pkgdir/usr/share/awesome/lib/$pkgname/contrib"
	# Install documentation
  install -dm755 "$pkgdir/usr/share/doc/$pkgname"
	install -m644 README CHANGES "$pkgdir/usr/share/doc/$pkgname"
}

# vim:set ts=2 sw=2 ft=sh noet:
