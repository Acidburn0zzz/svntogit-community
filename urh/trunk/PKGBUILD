# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinuc.org>
# Contributor: Johannes Pohl <johannes.pohl90@gmail.com>

pkgname=urh
pkgver=2.8.1
pkgrel=1
pkgdesc='Universal Radio Hacker: investigate wireless protocols like a boss'
arch=('x86_64')
url='https://github.com/jopohl/urh'
license=('GPL3')
depends=('python' 'python-pyqt5' 'python-numpy' 'python-psutil' 'python-pyzmq' 'cython')
makedepends=('gcc' 'python-setuptools'
             'airspy' 'hackrf' 'libuhd' 'limesuite' 'rtl-sdr') # Optional
checkdepends=('python-pytest-runner' 'python-pytest-xdist' 'xorg-server-xvfb')
optdepends=('airspy: Airspy backend'
            'cython: compiling native modules inside URH'
            'hackrf: HackRF backend'
            'libuhd: USRP backend'
            'limesuite: LimeSDR backend'
            'rtl-sdr: RTL-SDR backend'
            'gnuradio: GNURadio backend'
            'gnuradio-osmosdr: OsmoSDR backend, via GNURadio'
            'python-pyaudio: Soundcard backend')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jopohl/urh/archive/v$pkgver.tar.gz")
sha512sums=('ce1d9de195578072b80927248fa601659a3e7e6454f7c3bcffcdff8ec4c30738a55933fa31ae81ac1afdb251c1cb3155927ad63012f6bf0fcd5c2bbae6c80a7f')

prepare() {
  cd $pkgname-$pkgver

  # Issues with PyQt, see https://github.com/jopohl/urh/issues/623
  rm tests/{test_filter.py,test_plugins.py}
}

build() {
  cd $pkgname-$pkgver

  python setup.py build build_ext
  python src/urh/cythonext/build.py
}

check() {
  cd $pkgname-$pkgver

  xvfb-run pytest --boxed
}

package() {
  cd $pkgname-$pkgver

  python setup.py install --root="$pkgdir" -O1 --skip-build

  install -Dm644 ./data/urh.desktop "$pkgdir"/usr/share/applications/urh.desktop
  install -Dm 644 data/icons/appicon.png "$pkgdir"/usr/share/pixmaps/urh.png
  install -Dm 644 README.md "$pkgdir"/usr/share/doc/$pkgname/README
}

