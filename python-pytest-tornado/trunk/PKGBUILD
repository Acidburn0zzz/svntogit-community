# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-tornado
pkgver=0.7.0
pkgrel=1
pkgdesc='A py.test plugin providing fixtures and markers to simplify testing of asynchronous tornado applications.'
arch=('any')
license=('Apache')
url='https://github.com/eugeniy/pytest-tornado'
depends=('python-pytest' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/eugeniy/pytest-tornado/archive/v$pkgver.tar.gz")
sha512sums=('23d81afd08e06dd95fbae4c30ef2fc8ffc2a07ede80277847b64b193d45a742d0b655e16648aa21344c58bf4dd5c55ba2ac5b43d6339d935b89a21f662323bfb')

build() {
  cd pytest-tornado-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-tornado-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.7/site-packages:$PYTHONPATH" py.test
}

package() {
  cd pytest-tornado-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
