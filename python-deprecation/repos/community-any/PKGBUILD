# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=2.0.2
pkgrel=1
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="http://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python2-unittest2')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/briancurtin/deprecation/archive/$pkgver.tar.gz")
sha512sums=('0cbafaded86d65a0e425ba25e7256b4da0bbcd0a55784122bb55ec05c119a4f2273801e1a389b1835afddc0b88e15a14a9965fd42ef246258588fa0aa3f156c1')

prepare() {
  cp -a deprecation-$pkgver{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation-$pkgver/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation-$pkgver
  python setup.py build

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation-$pkgver
  python -m unittest discover

  cd "$srcdir"/deprecation-$pkgver-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
