# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-deprecation
pkgname=('python-deprecation' 'python2-deprecation')
pkgver=1.0
_commit=6f27aaad7de77f5bbf804da82f08d39b78a139e6
pkgrel=1
pkgdesc="A library to handle automated deprecations"
arch=('any')
license=('Apache')
url="http://deprecation.readthedocs.io"
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python2-unittest2')
source=("git+https://github.com/briancurtin/deprecation.git#commit=$_commit")
sha512sums=('SKIP')

prepare() {
  cp -a deprecation{,-py2}

  sed -i 's/unittest2/unittest/g' deprecation/tests/*.py

  export LC_CTYPE=en_US.UTF-8
}

build() {
  cd "$srcdir"/deprecation
  python setup.py build

  cd "$srcdir"/deprecation-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/deprecation
  python -m unittest discover

  cd "$srcdir"/deprecation-py2
  python2 -m unittest discover
}

package_python-deprecation() {
  depends=('python')

  cd deprecation
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-deprecation() {
  depends=('python2')

  cd deprecation-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
