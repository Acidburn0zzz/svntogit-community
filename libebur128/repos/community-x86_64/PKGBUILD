# Maintainer:
# Contributor: BoosterDEV <booster DOT devel AT gmail DOT com>

pkgname=libebur128
pkgver=1.2.3
pkgrel=1
pkgdesc="A library that implements the EBU R 128 standard for loudness normalisation."
arch=(x86_64)
url="https://github.com/jiixyj/libebur128"
license=(MIT)
depends=(glibc)
makedepends=(cmake)
source=($pkgname-$pkgver.tar.gz::"https://github.com/jiixyj/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('13776667743cf6e3df36bcf4f8c7ac828859ce02c9988514e3fdddc3efba98f7')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_STATIC_LIBS=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$pkgname-$pkgver/COPYING "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
