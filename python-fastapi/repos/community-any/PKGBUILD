# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=fastapi
pkgname=python-$_pkgname
pkgver=0.66.0
pkgrel=1
pkgdesc='FastAPI framework, high performance, easy to learn, fast to code, ready for production'
arch=('any')
url='https://github.com/tiangolo/fastapi'
license=('MIT')
depends=('python-starlette' 'python-pydantic')
optdepends=('python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
            'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
            'python-email-validator' 'python-python-multipart')
makedepends=('python-setuptools' 'python-dephell')
checkdepends=('python-pytest-runner' 'python-pytest-cov'
              'python-requests' 'python-aiofiles' 'python-jinja' 'python-itsdangerous'
              'python-yaml' 'python-graphene' 'python-ujson' 'python-orjson'
              'python-email-validator'  'python-pyjwt' 'python-sqlalchemy'
              'python-peewee' 'python-flask' 'python-passlib'
              'python-bcrypt' 'python-httpx' 'python-python-multipart'
              'python-databases' 'python-aiosqlite' 'python-jose')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('6a260221683641f7695c1e8bf91b5197ea04d3a14d3b441c69eab62532fb6bc3d8473e82f942422edef22dd57ef7b9c1107b4ad67da13d07c8eaa1df0f6462b7')

prepare() {
  cd $_pkgname-$pkgver

  dephell deps convert --from pyproject.toml --to setup.py

  # don't pin starlette dependency
  sed -i 's|starlette.*"|starlette"|' pyproject.toml
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
