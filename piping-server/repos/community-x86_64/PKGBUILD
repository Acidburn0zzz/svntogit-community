# Maintainer: Justin Kromlinger <hashworks@archlinux.org>
pkgname=piping-server
pkgver=0.8.2
pkgrel=1
pkgdesc='Infinitely transfer between every device over pure HTTP'
arch=('x86_64')
url="https://github.com/nwtgck/piping-server-rust"
license=('MIT')
depends=('gcc-libs')
makedepends=('cargo')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha256sums=('7f6a7a8068f591e2e682122f435dc90a71f724d8fd06238b48dc10638896955d')

build() {
  cd "piping-server-rust-$pkgver"
  cargo build --release --locked --target-dir=target
}

check() {
  cd "piping-server-rust-$pkgver"
  cargo test --release --locked
}

package() {
  cd "piping-server-rust-$pkgver"

  install -Dm755 "target/release/$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -Dm 644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.md "$pkgdir/usr/share/doc/$pkgname/README.md"
}
