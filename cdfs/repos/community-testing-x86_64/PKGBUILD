# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: J. Santiago Hirschfeld <jsantiagoh@yahoo.com.ar>

pkgname=cdfs
pkgver=2.6.27
pkgrel=37
pkgdesc="File system module that 'exports' all tracks and boot images on a CD as normal files."
arch=(i686 x86_64)
url="http://www.elis.UGent.be/~ronsse/cdfs/"
license=('GPL')
makedepends=('linux-headers')
install=cdfs.install
source=("http://www.elis.UGent.be/~ronsse/cdfs/download/$pkgname-$pkgver.tar.bz2"
	"cdfs-3.0.patch"
	"cdfs-3.2.patch"
	"cdfs-3.4.patch"
	"cdfs-3.8.patch")
md5sums=('ac64c014a90e3c488394832ea29605b3'
         'aba7da94a9dcbb8a93ea423cb6958fef'
         'e934407b3460257a301822ffc4ab3933'
         '9215e7bdac728bd2f889fb525e543454'
         'fbc4592fc8d47e9a7b9db22da2acb8f8')

_kernver=`pacman -Q linux | cut -d . -f 2 | cut -f 1 -d -`
_kernverfull=`cat /usr/lib/modules/extramodules-3.${_kernver}-ARCH/version`

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -p1 <$srcdir/cdfs-3.0.patch
  patch -p1 <$srcdir/cdfs-3.2.patch
  patch -p1 <$srcdir/cdfs-3.4.patch
  patch -p1 <$srcdir/cdfs-3.8.patch
  make KDIR=/usr/lib/modules/${_kernverfull}/build
  sed -i "s|extramodules-.*-ARCH|extramodules-3.${_kernver}-ARCH|" $startdir/cdfs.install
}

package() {
  depends=("linux>=3.${_kernver}" "linux<3.`expr ${_kernver} + 1`")

  cd $srcdir/$pkgname-$pkgver
  install -Dm0644 cdfs.ko $pkgdir/usr/lib/modules/extramodules-3.${_kernver}-ARCH/cdfs.ko
}
