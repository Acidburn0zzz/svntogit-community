# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=cargo-sort
pkgver=1.0.6
pkgrel=2
pkgdesc="Check if tables and items in a .toml file are lexically sorted"
arch=('x86_64')
url="https://github.com/DevinR528/cargo-sort"
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")
sha512sums=('5ab6843daefd4d5a2c947cb61a94930b78e42613afb3bd706964a8aeaf6a04bbecf45e73b8e71ceec9890343744024ab70d2cd87f673362562fd65d543a1ffb4')
b2sums=('8d6ec7ad78925a1c3659e1540c8cc3f20c938a51c2d7adfffa99db413d11c266bb2a69274222967e37e13e9b899c582b6982db29ac597146748f85e73501abe8')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # licenses
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/^####.*$/,/^<\/sub>$/p' README.md \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examp "$pkgdir/usr/share/doc/$pkgname/examples"
}
