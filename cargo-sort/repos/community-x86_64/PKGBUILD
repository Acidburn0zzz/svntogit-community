# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Dominic Meiser [git at msrd0 dot de]

pkgname=cargo-sort
pkgver=1.0.6
pkgrel=1
pkgdesc="Check if tables and items in a .toml file are lexically sorted"
arch=('x86_64')
url="https://github.com/DevinR528/cargo-sort"
license=('Apache' 'MIT')
depends=('gcc-libs')
makedepends=('rust')
# no upstream tag, and test files are not included in crate tarball
# using a pinned commit from upstream to extract test files
_commit='7f90788bab0578fbe6e879905ac34708ca11d56e'
source=(
  "$pkgname-$pkgver.tar.gz::https://static.crates.io/crates/$pkgname/$pkgname-$pkgver.crate"
  "$url/archive/$_commit.tar.gz"
)
sha512sums=('b27fb0938e598c54dacd2593ca021b01528d45637b9031ebdb2289446fdb9485818a12bd62812a1efd2bc5a7c116123ddd7852a7a2491d06f6673e4d9ae1b17d'
            '610b920e0e18a5d16919f504d79455a86f921c9e128c1bbc3aee80a7d31d04b85c9fc66efa1b41ae73319be67aede0912b36a1be0ce376baee9b90b85d2dfe8b')
b2sums=('f899b68a260bae85cad1b129e8027a1f857532555766f2e02ef590e8a683e7f2beaf279980d78fd8b175cd5c90ca5123bc8cc7d51631f6cb4899a929b16f1799'
        '18dd544385ff7747925c7e31bcbc169bab97704050206ed41fd4a7df5a8f051893283fbbbf6fe4fadc6fb04841f73073aec1783bd6bb84741d2d5ffab5e09587')
noextract=("$_commit.tar.gz")

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"

  # extract test files
  bsdtar \
    --strip-components 1 \
    --extract \
    --file "$srcdir/$_commit.tar.gz" \
    --include '*fixtures*' \
    --include '*examp*'
}

build() {
  cd "$pkgname-$pkgver"
  cargo build --release --frozen
}

check() {
  cd "$pkgname-$pkgver"
  cargo test --frozen
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "target/release/$pkgname"

  # licenses
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  sed -n '/^####.*$/,/^<\/sub>$/p' README.md \
    > "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  cp -vr examp "$pkgdir/usr/share/doc/$pkgname/examples"
}
