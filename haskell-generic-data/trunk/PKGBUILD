# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=generic-data
pkgname=haskell-generic-data
pkgver=0.8.0.0
pkgrel=1
pkgdesc="Deriving instances with GHC.Generics and related utilities"
url="https://github.com/Lysxia/generic-data"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-base-orphans' 'haskell-contravariant' 'haskell-show-combinators')
makedepends=('ghc' 'haskell-doctest' 'haskell-generic-lens' 'haskell-glob' 'haskell-one-liner'
             'haskell-quickcheck' 'haskell-tasty' 'haskell-tasty-hunit')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('d57cc213b9f07e6d97a950b7ec5e5828c1f94a4e95e795ea54fabf48eb9bf478959aa073a7f0a8740ce91c61f9a56b3500cfc5fd174f3bcde8ed2bef8ee86ea5')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
