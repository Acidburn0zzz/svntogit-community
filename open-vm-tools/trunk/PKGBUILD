# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Krzysztof Raczkowski <raczkow@gmail.com>

pkgname=open-vm-tools
pkgver=2010.11.17
_pkgsubver=327185
pkgrel=3
pkgdesc="The Open Virtual Machine Tools (open-vm-tools) are the open source implementation of VMware Tools."
arch=('i686' 'x86_64')
url="http://open-vm-tools.sourceforge.net/"
license=('LGPL')
depends=('open-vm-tools-modules' 'libdnet' 'icu' 'procps' 'glib2' 'uriparser' 'libsigc++' 'libxss')
makedepends=('chrpath' 'doxygen')
optdepends=('gtkmm' 'libnotify' 'libxtst' 'fuse' 'libsm')
options=('docs' '!libtool')
install=$pkgname.install
source=(http://switch.dl.sourceforge.net/$pkgname/$pkgname-$pkgver-${_pkgsubver}.tar.gz
	scripts-network.patch
	scripts-network-FS19541.patch
	open-vm-tools-X11Bool.patch
	open-vm-tools.conf.d
	open-vm-tools.rc.d
	tools.conf
	vmware-guestd
	xautostart.conf)
md5sums=('0129bd623af3c7307c65158a38d61c6f'
         '06f7448e274db2a911f582e276088fc9'
         'b183ec265200d68431a5e4eb1b0c8cf5'
         '8c333a979578bdc0c3134c1dd6bb7353'
         '79b0a14d86191fee70a4639da8bd7785'
         '67cb83a9e5a4dd016c1a1da2863ca36d'
         'b55d15e2c4be396aad709aeca91033d3'
         '73cc1a2665b0dd62427733d62ead8b9a'
         '75a25d83417e683957321f97a00f8465')

build() {
  cd "$srcdir/$pkgname-${pkgver}-${_pkgsubver}"

  [ $NOEXTRACT -eq 1 ] || patch -Np1 -i $srcdir/open-vm-tools-X11Bool.patch

  [ $NOEXTRACT -eq 1 ] || sed -i 's#3.2.7#3.2.8#' configure
  [ $NOEXTRACT -eq 1 ] || sed -i 's#3.2.7#3.2.8#' configure.ac

  unset LDFLAGS

  [ -f Makefile ] || ./configure --prefix=/usr --without-kernel-modules
  make
  make install DESTDIR=$pkgdir
  install -D -m 755 scripts/common/vmware-xdg-detect-de $pkgdir/usr/bin/vmware-xdg-detect-de
  chmod 07755 $pkgdir/usr/bin/vmware-user-suid-wrapper

  cd $pkgdir
  patch -Np1 -i $srcdir/scripts-network.patch etc/vmware-tools/scripts/vmware/network
  patch -Np1 -i $srcdir/scripts-network-FS19541.patch etc/vmware-tools/scripts/vmware/network

  ln -fs /usr/sbin/mount.vmhgfs $pkgdir/sbin/mount.vmhgfs

  install -D -m 755 $srcdir/open-vm-tools.rc.d $pkgdir/etc/rc.d/open-vm-tools
  install -D -m 644 $srcdir/open-vm-tools.conf.d $pkgdir/etc/conf.d/open-vm-tools
  install -D -m 644 $srcdir/tools.conf $pkgdir/etc/vmware-tools/tools.conf
  install -D -m 644 $srcdir/xautostart.conf $pkgdir/etc/vmware-tools/xautostart.conf
  install -D -m 644 $srcdir/vmware-guestd $pkgdir/etc/pam.d/vmware-guestd
  rm -rf $pkgdir/usr/etc

  cd $pkgdir && find -type f -exec sh -c "file {} | grep ELF >/dev/null && echo {} && chrpath -d {}" \;
}
