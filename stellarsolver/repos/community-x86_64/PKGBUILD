# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=stellarsolver
pkgver=2.0
pkgrel=2
pkgdesc='The cross platform Sextractor and Astrometry.net-Based internal astrometric solver'
arch=(x86_64)
url='https://github.com/rlancaste/stellarsolver'
license=(GPL3)
depends=(qt5-base gsl wcslib)
makedepends=(cmake)
source=(https://github.com/rlancaste/stellarsolver/archive/$pkgver/$pkgname-$pkgver.tar.gz
        https://github.com/rlancaste/stellarsolver/commit/01da1add.patch)
sha256sums=('6d384fffa0d4eced3bad8eec6691d2e05855760334d8a01fac18306427bbc5f0'
            '47c1aba29b7031267427ca5e8551fb8e015fafad79db60046154e978a531ab0e')

prepare() {
  patch -d $pkgname-$pkgver -p1 < 01da1add.patch # Add missing desktop file
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTER=ON
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
