# $Id$
# Maintainer:
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Darwin Bautista <djclue917@gmail.com>

pkgname=soundkonverter
pkgver=1.3.3
pkgrel=1
pkgdesc="Front-end to various audio converters"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php?content=29024"
license=('GPL')
depends=('kdemultimedia-kioslave' 'taglib')
makedepends=('automoc4' 'cmake')
optdepends=('cdrkit: cdda2wav backend'
	    'faac: faac backend'
	    'faad2: faad backend'
	    'ffmpeg: ffmpeg backend'
	    'flac: flac backend'
	    'lame: lame backend'
	    'mplayer: mplayer backend'
	    'vorbis-tools: oggenc, oggdec backends'
	    'speex: speexenc, speexdec backends'
	    'vorbisgain: vorbisgain backend'
	    )
install=$pkgname.install
source=("http://kde-apps.org/CONTENT/content-files/29024-${pkgname}-${pkgver}.tar.gz")
md5sums=('cb61ee363f78f9dfaf807780ef65095d')

build() {
  cd "${srcdir}"
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install
}
