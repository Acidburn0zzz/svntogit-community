# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-email-validator
pkgname=(python-email-validator python2-email-validator)
pkgver=1.0.0.20160123
_commit=5af0c32c728178acd45da8fec282e8ceff7810fe
pkgrel=1
pkgdesc="A robust email syntax and deliverability validation library for Python 2.x/3.x."
arch=('any')
url="https://github.com/JoshData/python-email-validator"
license=('custom:CC0')
makedepends=('git' 'python-setuptools' 'python2-setuptools' 'python-dnspython' 'python2-dnspython'
             'python-idna' 'python2-idna')
source=("git+https://github.com/JoshData/python-email-validator.git#commit=$_commit")
md5sums=('SKIP')

prepare() {
  sed -i '298s/email/line/g' python-email-validator/email_validator/__init__.py
  cp -a python-email-validator{,-py2}
}

build() {
  cd "$srcdir"/python-email-validator
  python setup.py build

  cd "$srcdir"/python-email-validator-py2
  python2 setup.py build
}

check() {
  export LC_CTYPE=en_US.UTF-8

  cd "$srcdir"/python-email-validator
  python email_validator/__init__.py --tests < test_pass.txt

  cd "$srcdir"/python-email-validator-py2
  python2 email_validator/__init__.py --tests < test_pass.txt
}

package_python-email-validator() {
  depends=('python-dnspython' 'python-idna')

  cd "$srcdir"/python-email-validator
  python setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-email-validator() {
  depends=('python2-dnspython' 'python2-idna')

  cd "$srcdir"/python-email-validator-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
  install -Dm644 "LICENSE" "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/email_validator{,2}
}

# vim:set ts=2 sw=2 et:
