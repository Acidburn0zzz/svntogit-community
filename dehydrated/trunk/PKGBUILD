# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: VVL <me@ivvl.ru>
# Contributor: Alexander Görtz <aur@nyloc.de>
# Contributor: Patrick Burroughs (Celti) <celti@celti.name>

pkgname=dehydrated
pkgver=0.6.4
pkgrel=1
pkgdesc='letsencrypt/acme client implemented as a shell-script – just add water'
arch=('any')
url=https://dehydrated.io
license=('MIT')
source=("https://github.com/lukas2511/dehydrated/archive/v$pkgver/$pkgname-v$pkgver.tar.gz"
        "$pkgname".{service,timer})
depends=('curl' 'openssl')
sha512sums=('16940d52bcc85b463d6ce1d18fbf2666ee51985fc15e7ecf28e766297151343c4250076d383d927093536346293e5d6334a083922885b29554051b0135cec7f1'
            'e3cb75d50e35b14121f605b1fc19c027e8143075eef93c2d5a59c98c0266b0e934c9c315522490e8f49e54c7a8d2f6db3bbff27d4adc6eb39f7547488103cd8c'
            '56847122a86573c06973b7eb74dd53fe3010394dff478f3a9d7ccc872f41fd4e8fdf236cd5e3ff1075210c4aaa6c9afaa79b2bd29da277c42d8075993e24a81a')

package() {
  cd $pkgname-$pkgver
  install -d "$pkgdir"/etc/dehydrated
  install -Dm755 -t "$pkgdir"/usr/bin $pkgname
  install -Dm644 -t "$pkgdir"/usr/lib/systemd/system ../$pkgname.{service,timer}
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname docs/*.md
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname/examples docs/examples/*
  install -Dm644 -t "$pkgdir"/usr/share/man/man1 docs/man/$pkgname.1
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
