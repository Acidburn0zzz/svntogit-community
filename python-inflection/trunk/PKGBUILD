# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-inflection
pkgver=0.3.1
pkgrel=1
pkgdesc="A port of Ruby on Rails inflector to Python"
url="https://github.com/jpvanhal/inflection"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jpvanhal/inflection/archive/$pkgver.tar.gz")
sha512sums=('2a69a6228f2d44a5afecbd3ef51a51b4d5f63f69ddc844f4abfd6f326cb25c518403e8c5b9f98589885777d41d8fea31bacab3a5a3ab80db411960238d2f49ad')

build() {
  cd inflection-$pkgver
  python setup.py build
}

check() {
  cd inflection-$pkgver
  python setup.py pytest
}

package() {
  cd inflection-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
