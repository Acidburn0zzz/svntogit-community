# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-inflection
pkgver=0.4.0
pkgrel=1
pkgdesc="A port of Ruby on Rails inflector to Python"
url="https://github.com/jpvanhal/inflection"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jpvanhal/inflection/archive/$pkgver.tar.gz")
sha512sums=('071b21bdc24f05f7be1e8c51c0cc422d4835b4b6e03cdd2b51717356d4d1bb714ebcd875091e331b7e9be85cfb577a8f80f999ca2bf0c7dd8496bf057ddb144c')

build() {
  cd inflection-$pkgver
  python setup.py build
}

check() {
  cd inflection-$pkgver
  python setup.py pytest
}

package() {
  cd inflection-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
