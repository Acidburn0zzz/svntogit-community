# Maintainer: Konstantin Gizdov < arch at kge dot pw >
# Contributor: Joshua Ellis < josh at jpellis dot me >
# Contributor: Stefano Campanella < stefanocampanella1729 at gmail dot com >

pkgbase=pythia8
pkgname=('pythia8' 'python-pythia8')
pkgver=8.3.01
_pkgid="${pkgbase:0:-1}${pkgver//./}"
pkgrel=1
pkgdesc="High-energy physics events generator"
arch=('x86_64')
url="http://home.thep.lu.se/Pythia/"
license=('GPL')
depends=('python' 'openmp')
makedepends=('fastjet' 'hepmc' 'lhapdf>=6.2' 'root')
source=("http://home.thep.lu.se/~torbjorn/pythia8/${_pkgid}.tgz"
        'pythia8.sh'
        'fix_ar_options.patch'
        'fix_python_lib_paths.patch')
sha256sums=('51382768eb9aafb97870dca1909516422297b64ef6a6b94659259b3e4afa7f06'
            '4e373b685960e410024b4e33e22c2dea360dfedd7962837087332f428c974ae5'
            '27b8a11d404be6cb683ec7fc5a2b3cca7f443bde751be2771d68f060a380c212'
            '50958ce04faf57da452af64ec3f1fd3ba961a3b27e0e22fee24ea046f461186b')
get_pyver () {
    python -c 'import sys; print(str(sys.version_info[0]) + "." + str(sys.version_info[1]))'
}

prepare() {
    cd "${srcdir}/${_pkgid}"
    patch -p1 -i "${srcdir}/fix_ar_options.patch"
    patch -p1 -i "${srcdir}/fix_python_lib_paths.patch"
}

build() {
    _inc=/usr/include
    _lib=/usr/lib
    _share=/usr/share/${pkgbase}

    cd "${srcdir}/${_pkgid}"
    # # no such package yet
    # --with-evtgen \
    # --with-hepmc3 \
    # --with-mg5mes \
    # --with-powheg \
    # --with-rivet \
    # --with-yoda \
    ./configure \
        --prefix=/usr \
        --prefix-include=${_inc} \
        --prefix-lib=${_lib} \
        --prefix-share=${_share} \
        --cxx-common="${CXXFLAGS} -fPIC" \
        --cxx-shared="-shared ${LDFLAGS} -ldl" \
        --with-fastjet3 \
        --with-fastjet3-include=${_inc} \
        --with-fastjet3-lib=${_lib} \
        --with-gzip \
        --with-gzip-include=${_inc} \
        --with-gzip-lib=${_lib} \
        --with-hepmc2 \
        --with-hepmc2-include=${_inc} \
        --with-hepmc2-lib=${_lib} \
        --with-lhapdf5 \
        --with-lhapdf5-include=${_inc} \
        --with-lhapdf5-lib=${_lib} \
        --with-lhapdf6 \
        --with-lhapdf6-include=${_inc} \
        --with-lhapdf6-lib=${_lib} \
        --with-openmp \
        --with-openmp-include=${_inc} \
        --with-openmp-lib=${_lib} \
        --with-python \
        --with-python-include="/usr/include/python$(get_pyver)" \
        --with-python-lib="/usr/lib/python$(get_pyver)" \
        --with-root \
        --with-root-include=/usr/include/root \
        --with-root-lib=/usr/lib/root
    make
}

package_pythia8() {
    provides=('pythia')
    conflicts=('pythia')
    replaces=('pythia')
    optdepends=('fastjet: fast jet finding in pp and e+e- collisions'
                'hepmc: storing collisions from Monte Carlo'
                'lhapdf: evaluate PDFs from discretised data files'
                'root: integrated examples with CERN ROOT data analysis framework')

    cd "${srcdir}/${_pkgid}"
    install -Dm755 bin/pythia8-config "${pkgdir}/usr/bin/pythia8-config"
    install -Dm644 lib/libpythia8.a "${pkgdir}/usr/lib/libpythia8.a"
    install -Dm755 lib/libpythia8.so "${pkgdir}/usr/lib/libpythia8.so"
    install -Dm755 lib/libpythia8lhapdf5.so "${pkgdir}/usr/lib/libpythia8lhapdf5.so"
    install -Dm755 lib/libpythia8lhapdf6.so "${pkgdir}/usr/lib/libpythia8lhapdf6.so"

    cp -r include "${pkgdir}/usr/"
    install -d "${pkgdir}/usr/share/${pkgbase}"
    cp -r share/Pythia8/* "${pkgdir}/usr/share/${pkgbase}/"
    cp -r examples "${pkgdir}/usr/share/${pkgbase}/"

    install -D "${srcdir}/pythia8.sh" "${pkgdir}/etc/profile.d/pythia8.sh"
}

package_python-pythia8() {
    pkgdesc="Python bindings for Pythia"
    depends=('pythia8')

    cd "${srcdir}/${_pkgid}"
    install -Dm755 lib/pythia8.so "${pkgdir}/usr/lib/python$(get_pyver)/site-packages/pythia8.so"
}
