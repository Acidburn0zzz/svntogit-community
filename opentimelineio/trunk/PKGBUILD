# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=opentimelineio
pkgver=0.13
pkgrel=4
pkgdesc='Open Source API and interchange format for editorial timeline information'
url='http://opentimeline.io/'
license=(Apache)
arch=(x86_64)
depends=(python-aaf2)
makedepends=(cmake python-pip pybind11 git)
source=(https://github.com/PixarAnimationStudios/OpenTimelineIO/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        git+https://github.com/Tencent/rapidjson#commit=66eb606
        opentimelineio-c++17.patch)
sha256sums=('33a63891b4656804242512e122b33ed12e35d4038fd78610ccb82b441b9506dd'
            'SKIP'
            '927675021a15d0fde97697e1b7a7312e0ee64f628cb5a09716edf972870d0cbc')

prepare() {
  mv rapidjson OpenTimelineIO-$pkgver/src/deps
  sed -e '/deps/d' -i OpenTimelineIO-$pkgver/src/CMakeLists.txt # Unbundle pybind11
  sed -e '1 i\find_package(pybind11)' -i OpenTimelineIO-$pkgver/src/py-opentimelineio/opentime-bindings/CMakeLists.txt \
                                      -i OpenTimelineIO-$pkgver/src/py-opentimelineio/opentimelineio-bindings/CMakeLists.txt

  patch -d OpenTimelineIO-$pkgver -p1 < opentimelineio-c++17.patch # Use native C++17 types
}

build() {
  cmake -B build -S OpenTimelineIO-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build

  cd OpenTimelineIO-$pkgver
  python setup.py build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  cd OpenTimelineIO-$pkgver
  DESTDIR="$pkgdir" python setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/build/opentimelineio/src/*/*/*/opentimelineio/* "$pkgdir"/usr/lib/python*/site-packages/opentimelineio
  rm -fr "$pkgdir"/build
}
