# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=qtcurve-kde4
pkgver=1.8.10
pkgrel=1
arch=('i686' 'x86_64')
groups=('qtcurve')
license=('GPL')
pkgdesc='A configurable set of widget styles for KDE and Gtk'
url='http://www.kde-look.org/content/show.php?content=40492'
depends=('kdebase-workspace')
makedepends=('cmake' 'automoc4')
source=("http://craigd.wikispaces.com/file/view/QtCurve-KDE4-${pkgver}.tar.bz2"
        'bypass-kde-config.patch')
md5sums=('b77e9a176a6324652a637edc10d051aa'
         '3f6681147cc361bd2bfd4d7477f2a8d0')

build() {
	cd "${srcdir}"/QtCurve-KDE4-${pkgver}
	# Bypass KDE prefix detection by kde4-config, see:
	# http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=541667
	patch -p1 -i "${srcdir}"/bypass-kde-config.patch

	cd "${srcdir}"
	mkdir build
	cd build
	cmake ../QtCurve-KDE4-${pkgver} \
		-DCMAKE_BUILD_TYPE=Release \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd "${srcdir}"/build
	make DESTDIR="${pkgdir}" install
}
