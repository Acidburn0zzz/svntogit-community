# $Id$
#Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
#Contributor: BlackEagle < ike DOT devolder AT gmail DOT com >
#Contributor: Dany Martineau <dany.luc.martineau@gmail.com>

pkgname=clementine
pkgver=0.6
pkgrel=5
pkgdesc="A music player and library organizer"
url="http://code.google.com/p/clementine-player/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gstreamer0.10' 'taglib' 'glew' 'liblastfm' 'libgpod' 'libmtp' 'hicolor-icon-theme')
makedepends=('cmake' 'boost')
optdepends=('gstreamer0.10-base-plugins: for more open formats'
            'gstreamer0.10-good-plugins: for use with "Good" plugin libraries'
            'gstreamer0.10-bad-plugins: for use with "Bad" plugin libraries'
            'gstreamer0.10-ugly-plugins: for use with "Ugly" plugin libraries')
source=(http://clementine-player.googlecode.com/files/${pkgname}-${pkgver}.tar.gz
        clementine-0.6-no-ubuntu-icons.patch
        parsing_non-ascii_filenames.patch)
sha256sums=('7a7393af462a3c0864279d8cb2fc59afeec718837e5a1d0818a16b8a20d7cd0c'
            'a2b5f7d268c3b69f69cc15b17e61213c5fb555f9410ad132e860e94bb363baa4'
            '77b3cd332b49692115ba2c56bfa832acd7509d2967a3920c9498911c3aa032f5')
install=clementine.install

build() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   
   # Do not install Ubuntu icons on ArchLinux :P
   patch -Np1 -i ../clementine-0.6-no-ubuntu-icons.patch 
   
   # Fix parsing non-ascii filenames (FS#22912)
   patch -Np1 -i ../parsing_non-ascii_filenames.patch

   cmake . -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release -DSTATIC_SQLITE=0
   make
}

package() {
   cd "${srcdir}/${pkgname}-${pkgver}"
   make DESTDIR="${pkgdir}" install
}
