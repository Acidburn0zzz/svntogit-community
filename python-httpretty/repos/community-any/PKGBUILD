# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-httpretty
pkgver=1.1.0
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
depends=('python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-coverage' 'python-eventlet' 'python-freezegun' 'python-httplib2'
              'python-redis' 'python-rednose' 'python-requests' 'python-sure' 'python-tornado')
source=("$pkgname-$pkgver.tar.gz::https://github.com/gabrielfalcao/HTTPretty/archive/$pkgver.tar.gz")
sha512sums=('055528188d43b4463f758d1d94f10125f62150cf327b086d28eb81abcc850a356fa5eaed03e6a4e0a98af96fc4aaf5df86f362b78e17769b094fe72eb537af2e')

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests
}

package_python-httpretty() {
  cd HTTPretty-$pkgver
  python3 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING
}
