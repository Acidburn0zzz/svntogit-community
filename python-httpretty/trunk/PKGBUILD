# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-httpretty
pkgname=("python-httpretty" "python2-httpretty")
pkgver=0.9.4
_commit=2a9e08d96feff10d5d68557f0579b9c6b8d9ebf2
pkgrel=1
pkgdesc="HTTP client mock for Python"
arch=('any')
url="https://github.com/gabrielfalcao/httpretty"
license=('MIT')
makedepends=('python-urllib3' 'python2-urllib3' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose' 'python-coverage' 'python2-coverage' 'python-mock'
              'python2-mock' 'python-sure' 'python2-sure' 'python-tornado' 'python2-tornado'
              'python-requests' 'python2-requests' 'python-httplib2' 'python2-httplib2'
              'python-nose-randomly' 'python2-nose-randomly' 'python-rednose' 'python2-rednose')
source=("$pkgbase-$_commit.tar.gz::https://github.com/gabrielfalcao/HTTPretty/archive/$_commit.tar.gz")
sha512sums=('1c0b517388cc2a0e69c00a2ddc63d19164089caf5dca9525134387c8040911f4a2460b22370601ef9a79ca4f87bf5c3a7a0a6f5991fb58c9558292d9f84a464e')

prepare() {
  mv HTTPretty-{$_commit,$pkgver}
  cp -a HTTPretty-$pkgver{,-py2}
}

check() {
  cd HTTPretty-$pkgver
  nosetests3 -s tests || warning "Tests failed"

  cd ../HTTPretty-$pkgver-py2
  nosetests2 -s tests
}

package_python-httpretty() {
  depends=('python-urllib3')

  cd HTTPretty-$pkgver
  python3 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python-httpretty/COPYING"
}

package_python2-httpretty() {
  depends=('python2-urllib3')

  cd HTTPretty-$pkgver-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/python2-httpretty/COPYING"
}
