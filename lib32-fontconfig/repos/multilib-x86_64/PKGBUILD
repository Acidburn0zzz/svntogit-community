# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=lib32-fontconfig
pkgver=2.13.93
pkgrel=1
epoch=2
pkgdesc='Library for configuring and customizing font access'
url=https://www.freedesktop.org/wiki/Software/fontconfig/
arch=(x86_64)
license=(custom)
depends=(
  fontconfig
  lib32-expat
  libfreetype.so
)
makedepends=(
  git
  gperf
  lib32-freetype2
  lib32-json-c
  python-lxml
  python-six
)
provides=(libfontconfig.so)
install=fontconfig-32.install
_tag=d06103e3e764bd43758e213414a1716858ab384c
source=(
  git+https://gitlab.freedesktop.org/fontconfig/fontconfig.git#tag=${_tag}
  fontconfig-32.hook
)
b2sums=('SKIP'
        '32d89de4c80edbe14c67852e9b8c5b38208aa97447bc618e160f0b5e75eac6597c61e43c18e1cc7efdc89b44365b09be4cdedd59bce6f400927db88993d99e5b')

prepare() {
  cd fontconfig
  NOCONFIGURE=1 ./autogen.sh
}

pkgver() {
  cd fontconfig
  git describe --tags
}

build() {
  cd fontconfig
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG=i686-pc-linux-gnu-pkg-config
  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --localstatedir=/var \
    --sysconfdir=/etc \
    --disable-static \
    --with-add-fonts=/usr/local/share/fonts \
    --with-default-fonts=/usr/share/fonts \
    --with-templatedir=/etc/fonts/conf.avail
  make
}

package() {
  make DESTDIR="${pkgdir}" install -C fontconfig
  rm -r "${pkgdir}"/{etc,usr/{include,share}}
  mv "${pkgdir}"/usr/bin/fc-cache{,-32}
  find "${pkgdir}"/usr/bin -type f -not -name '*-32' -delete
  install -Dm 644 *.hook -t "${pkgdir}"/usr/share/libalpm/hooks/
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s fontconfig "${pkgdir}"/usr/share/licenses/lib32-fontconfig
}

# vim:set sw=2 et:
