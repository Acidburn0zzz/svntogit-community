# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>

_pkgbasename=fontconfig
pkgname=lib32-$_pkgbasename
pkgver=2.12.1
pkgrel=4
pkgdesc="A library for configuring and customizing font access (32-bit)"
arch=(x86_64)
url="https://www.freedesktop.org/wiki/Software/fontconfig/"
license=('custom')
depends=('lib32-expat' 'lib32-freetype2' $_pkgbasename)
makedepends=('python2')
install=lib32-fontconfig.install
source=(https://www.freedesktop.org/software/$_pkgbasename/release/$_pkgbasename-$pkgver.tar.bz2
        lib32-fontconfig.hook
        0001-fix-test-with-freetype2-2.7.1.patch)
sha256sums=('b449a3e10c47e1d1c7a6ec6e2016cca73d3bd68fbbd4f0ae5cc6b573f7d6c7f3'
            'd97c0c5b88023da5a2acf64cf560265390a9365305c43b8e86b4f89348e727b3'
            'b4992aab4e2b4ddae01f7565486752fa877edb6cd5ed435e15938ed21dfe22c8')

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

prepare() {
  cd $_pkgbasename-$pkgver
  patch -Np1 -i ../0001-fix-test-with-freetype2-2.7.1.patch
}

build() {
  cd $_pkgbasename-$pkgver

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd $_pkgbasename-$pkgver
  make -k check
}

package() {
  cd $_pkgbasename-$pkgver
  make DESTDIR="$pkgdir" install

  rm -rf "$pkgdir"/{etc,usr/{include,share}}
  find "$pkgdir/usr/bin" -not -type d -not -name fc-cache -delete
  mv "$pkgdir"/usr/bin/fc-cache{,-32}

  install -Dm644 ../lib32-fontconfig.hook "$pkgdir/usr/share/libalpm/hooks/lib32-fontconfig.hook"

  # Install license
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
