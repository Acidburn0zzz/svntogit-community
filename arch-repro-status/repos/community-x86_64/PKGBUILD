# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>

pkgname=arch-repro-status
pkgver=0.3.0
pkgrel=1
pkgdesc="Check the reproducibility status of your Arch Linux packages"
arch=('x86_64')
url="https://gitlab.archlinux.org/archlinux/arch-repro-status"
license=('MIT')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$pkgname-$pkgver.tar.gz::$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.gz")
sha512sums=('06063cd0dc5b7e007aaf3c3a4154b18d18fd5b133eb187c5ed2936eebf92172b64a5e27187c02b12af99d3c5445ae59896f89d6667139df8bc347e0950a6112b')

build() {
  cd "$pkgname-v$pkgver"
  cargo build --release --locked
}

check() {
  cd "$pkgname-v$pkgver"
  cargo test --release --locked
}

package() {
  cd "$pkgname-v$pkgver"
  install -Dm 755 "target/release/$pkgname" -t "$pkgdir/usr/bin"
  install -Dm 644 README.md -t "$pkgdir/usr/share/doc/$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
  install -Dm 644 "man/$pkgname.1" -t "$pkgdir/usr/share/man/man1"
}
