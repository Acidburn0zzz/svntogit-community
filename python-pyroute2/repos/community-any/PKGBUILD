# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Alexander Phinikarides <alexisph _AT_ gmail _DOT_ com>

pkgbase='python-pyroute2'
_pkgbase="${pkgbase//python-/}"
pkgname=("python-${_pkgbase}")
pkgdesc="A pure Python netlink and Linux network configuration library"
pkgver=0.5.19
pkgrel=2
arch=('any')
url="https://docs.pyroute2.org/"
license=('GPL2' 'Apache')
depends=('python')
makedepends=('python' 'python-setuptools' 'python-setuptools-scm' 'python-sphinx' 'git' 'python-aafigure' 'twine')
#source=("https://files.pythonhosted.org/packages/source/${_pkgbase:0:1}/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz")
source=("${_pkgbase}-${pkgver}.tar.gz::https://github.com/svinota/${_pkgbase}/archive/${pkgver}.tar.gz"
        'fix_sphinx_css.patch')
sha512sums=('19b0acbff17415acad20a214e0622ca9a3fc51b92e603ab15663ea7e8da290fc0b2033aeca1c20200307e34173802f89998151f77d5bcfc5f87eb8b42ba591ce'
            '18be191800ccebfb31fc310b1d0185bd221ec6f528569a6418fba61cdf9ed0b7b8f309c92b50cdb15774d45d0d8d4efa7a6d855b185099b2a94c0bbefa063b86')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  patch -p1 -N -i "${srcdir}/fix_sphinx_css.patch"
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make dist
  make docs
}

check() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make test
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"
  make install root="${pkgdir}/"
  # Tests get installed into the package for some reason
  PY_MAJ_MIN="$(python -c 'import sys; print(f"{sys.version_info.major}.{sys.version_info.minor}")')"
  rm -rf "${pkgdir}/usr/lib/python${PY_MAJ_MIN}/site-packages/tests"
}

# vim:set ts=2 sw=2 et:
