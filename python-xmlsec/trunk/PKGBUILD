# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xmlsec
pkgver=1.3.4
pkgrel=1
pkgdesc="Python bindings for the XML Security Library"
url="https://github.com/mehcode/python-xmlsec"
license=('MIT')
arch=('x86_64')
depends=('python-pkgconfig' 'python-lxml' 'xmlsec')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/mehcode/python-xmlsec/archive/$pkgver.tar.gz")
sha512sums=('e57a1844f7131cd9ef745a215b3a7363e2176277ac4160ba9c6738da6e1cf9b1fd74227a9a0de9a8de99716b3c30e63410db4f47f833d3f9f21ac2849fe3f55c')

build() {
  cd python-xmlsec-$pkgver
  python setup.py build
}

check() {
  cd python-xmlsec-$pkgver
  python setup.py pytest || warning "https://github.com/mehcode/python-xmlsec/issues/84"
}

package() {
  cd python-xmlsec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
