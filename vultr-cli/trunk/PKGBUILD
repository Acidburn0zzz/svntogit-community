# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=2.5.0
pkgrel=2
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/kpcyrd/sn0int'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('3c4ed4a6a25168fe30436f8e651121d2011b6646c7e6cbec1baa1e6244df0dd2226fdc67206b73563a5ec3a2d7aa5360a9af67854940dad70b6a7161a5b7cfe3')
b2sums=('4ce1b15becc56599cad87159f667c218ba85f9d7e43295047cf303392038a4c3ad8aa7b8c8554dfbce2e7f4933385f389c8744d1ba444a94387023c6f376d716')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
