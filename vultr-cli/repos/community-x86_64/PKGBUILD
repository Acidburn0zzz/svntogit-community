# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=2.8.1
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('97adcdf740faddfca26a2c5b2c2411a1bd41fcf038a60deed94a05c14d46a8a821592b140653003956037d870665600d29c7577607d5fb7445a0695dfafc3437')
b2sums=('5d4849ac16e778249767686bcf41aaad4fa2e0512fe03c1da3e7479f6ae6c4028815120d471a52d268bdead7ecd2b49a9cb4f256b7d5ace2816e861fde9c006a')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"

  # workaround, see https://github.com/vultr/vultr-cli/issues/92#issuecomment-877722219
  export VULTR_API_KEY=x
  ./vultr-cli completion bash > "${pkgdir}/usr/share/bash-completion/completions/vultr-cli"
  ./vultr-cli completion zsh > "${pkgdir}/usr/share/zsh/site-functions/_vultr-cli"
  ./vultr-cli completion fish > "${pkgdir}/usr/share/fish/vendor_completions.d/vultr-cli.fish"
}

# vim: ts=2 sw=2 et:
