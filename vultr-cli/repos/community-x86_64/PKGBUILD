# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=vultr-cli
pkgver=2.6.0
pkgrel=1
pkgdesc='Official command line tool for Vultr services'
url='https://github.com/vultr/vultr-cli'
arch=('x86_64')
license=('Apache')
makedepends=('go')
source=(https://github.com/vultr/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a153933bad2b29435739b3f10edbc76a3f9f744116732d391e1e218f5ce73284d71924054190968974148b9fcc55e3cde3e93c0fc1bd04ab8c554af0067b73fb')
b2sums=('59d1f1e2ab3a34ac3e61ca3b10a5f70858166ed9a0424b47a26ab13bccd9bb781543519f504c20ef073eec1b9ecd081eb35c0544f764262e960448f61520d2c1')

build() {
  cd ${pkgname}-${pkgver}
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags \"${LDFLAGS}\"" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 vultr-cli -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
