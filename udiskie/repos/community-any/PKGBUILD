# $Id: PKGBUILD 73684 2012-07-14 05:00:28Z dwallace $
# Maintainer: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Byron Clark <byron@theclarkfamily.name>
pkgname=udiskie
pkgver=0.4.1
pkgrel=2
pkgdesc="Removable disk automounter using udisks"
arch=('any')
url="http://www.bitbucket.org/byronclark/udiskie"
license=('MIT')
depends=('udisks' 'dbus-python' 'pygobject' 'python-notify')
makedepends=('asciidoc')
optdepends=('consolekit: easier permissions for udisks')
options=(!emptydirs)
conflicts=('python2-udiskie')
replaces=('python2-udiskie')
source=("http://bitbucket.org/byronclark/$pkgname/downloads/$pkgname-$pkgver.tar.gz")
md5sums=('62e5d5faafa9dbf11423d3c77a87b9a0')

package() {
  cd "$srcdir/$pkgname-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1

  make -C doc
  install -m 0644 -D "doc/${pkgname}.8" "$pkgdir/usr/share/man/man8/${pkgname}.8"
  install -Dm644 "$srcdir/$pkgname-$pkgver/COPYING" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

}

# vim:set ts=2 sw=2 et:
