# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-unittest-mixins
pkgname=('python-unittest-mixins' 'python2-unittest-mixins')
pkgver=1.1.1
pkgrel=2
pkgdesc='Helpful mixins for unittest classes'
arch=('any')
license=('Apache')
url='http://github.com/nedbat/unittest-mixins'
makedepends=('python-setuptools' 'python2-setuptools' 'git')
checkdepends=('python-pytest' 'python2-pytest')
source=("git+https://github.com/nedbat/unittest-mixins#tag=v$pkgver")
md5sums=('SKIP')

prepare() {
  cp -a unittest-mixins{,-py2}
}

build() {
  cd "$srcdir"/unittest-mixins
  python setup.py build

  cd "$srcdir"/unittest-mixins-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/unittest-mixins
  py.test

  cd "$srcdir"/unittest-mixins-py2
  py.test2
}

package_python-unittest-mixins() {
  depends=('python-six')

  cd unittest-mixins
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-unittest-mixins() {
  depends=('python2-six')

  cd unittest-mixins-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
