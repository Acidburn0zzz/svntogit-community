# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Eric Belanger <belanger@astro.umontreal.ca>
# Contributor: Kritoke <kritoke@nospam.gmail.com>

pkgname=gnomesu
pkgver=0.3.1
pkgrel=7
pkgdesc="A program that allows a user to su to root in a Gnome environment."
arch=('i686' 'x86_64')
url="http://www.gtkfiles.org/app.php/GnomeSu"
license=("GPL")
depends=('libgnomeui' 'libzvt')
makedepends=('pkgconfig')
conflicts=('libgnomesu')
source=(http://downloads.sourceforge.net/sourceforge/xsu/$pkgname-$pkgver.tar.gz)
md5sums=('8ebcf248b4f8430c96d80379ec2acdd8')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's/1.4/1.10/' configure
  sed -i 's/gsu/gnomesu/' xsu.desktop.in
  sed -i 's/gnome-warning/dialog-warning/' xsu.desktop.in
  sed -i 's|gnome-warning|/usr/share/icons/gnome/16x16/status/dialog-warning|' src/xsu.c
  ./configure --prefix=/usr --mandir=/usr/share
  make CFLAGS+=-Wl,-export-dynamic
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
  install -D -m644 xsu.desktop $pkgdir/usr/share/applications/gnomesu.desktop
  rm -r $pkgdir/usr/share/gnome
  mv $pkgdir/usr/doc $pkgdir/usr/share/doc
}
