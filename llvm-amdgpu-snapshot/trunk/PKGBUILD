# $Id$
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

pkgname=llvm-amdgpu-snapshot
pkgver=20121230
pkgrel=1
pkgdesc='Low Level Virtual Machine with AMDGPU enabled to build r600g shader llvm support'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('libffi' 'python2' 'ocaml')
conflicts=('llvm')
sources=(ftp://ftp.archlinux.org/other/community/llvm-amdgpu/llvm-amdgpu-master.20121230.tar.gz)

build() {
  cd "$srcdir"/llvm-master

  sed -i 's/python$/python2/' utils/llvm-build/llvm-build

  # Apply strip option to configure
  _optimized_switch="enable"
  [[ $(check_option strip) == n ]] && _optimized_switch="disable"

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

  # Force the use of GCC instead of clang
  PYTHON=python2 CC=gcc CXX=g++ \
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/llvm \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --enable-experimental-targets=AMDGPU \
    --disable-expensive-checks \
    --with-binutils-include=/usr/include \
    --$_optimized_switch-optimized

  make REQUIRES_RTTI=1
}

package() {
  cd "$srcdir"/llvm-master

  make DESTDIR="$pkgdir" install

  # Fix permissions of static libs
  #chmod -x "$pkgdir"/usr/lib/llvm*.a

  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
