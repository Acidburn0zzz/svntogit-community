# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Sebastien Binet <binet@lblbox>

pkgname=python-numexpr
pkgver=2.7.0
pkgrel=1
pkgdesc="Fast numerical array expression evaluator for Python, NumPy, PyTables, pandas"
url="https://github.com/pydata/numexpr"
arch=('x86_64')
license=('MIT')
depends=('python-numpy')
makedepends=('python-setuptools')
source=($pkgname-$pkgver.tar.gz::https://github.com/pydata/numexpr/archive/v$pkgver.tar.gz)
sha256sums=('1923f038b90cc69635871968ed742be7775c879451c612f173c2547c823c9561')

build() {
  cd "$srcdir"/numexpr-$pkgver
  python setup.py build
}
package_python-numexpr() {
  cd "$srcdir"/numexpr-$pkgver
  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
