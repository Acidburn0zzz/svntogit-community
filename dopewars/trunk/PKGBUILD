# $Id$
# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: Thayer Williams <thayer@archlinux.org>

pkgname=dopewars
pkgver=1.5.12
pkgrel=10
pkgdesc='Drug dealing game set in New York'
url='https://dopewars.sourceforge.net/'
license=('GPL')
arch=('x86_64')
depends=('alsa-lib' 'audiofile' 'gtk2' 'ncurses' 'sdl_mixer')
backup=('srv/dopewars/dopewars.sco')
source=("https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'dopewars.service')
sha256sums=('23059dcdea96c6072b148ee21d76237ef3535e5be90b3b2d8239d150feee0c19'
            'b81fe5156d1a0f650f9005bd9c7f92cb531817c2f9adc0b48d36834aacd9101f')

build() {
  cd "$pkgname-$pkgver"

  ./configure \
    --prefix=/usr \
    --localstatedir=/srv/dopewars \
    --mandir=/usr/share/man
  make
}

package() {
  make -C "$pkgname-$pkgver" \
    DESKTOPDIR="$pkgdir/usr/share/applications" \
    DESTDIR="$pkgdir" \
    install

  # fix permissions
  chmod 0755 "$pkgdir/usr/bin/dopewars"
  chmod 0775 "$pkgdir/srv/dopewars"
  chmod 0664 "$pkgdir/srv/dopewars/dopewars.sco"
  chown root:root "$pkgdir/usr/bin/dopewars"
  chown root:root "$pkgdir/srv/dopewars"
  chown root:root "$pkgdir/srv/dopewars/dopewars.sco"

  install -Dm644 dopewars.service \
    "$pkgdir/usr/lib/systemd/system/dopewars.service"
}

# vim: ts=2 sw=2 et:
