# Maintainer: David Runge <dave@sleepmap.de>
_name=pymediainfo
pkgname=python-pymediainfo
pkgver=4.0
pkgrel=1
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
depends=('libmediainfo' 'python')
makedepends=('mediainfo' 'python-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-runner')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('988f0d8a212bc5893925bd96a5b41a1a2c8e9508c747e7aa1106f3a065ad5707a7ed9de71264d74ba6637a2b8efbdeeed3677d779167bb90f643d40f81c5a74e')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  python setup.py test
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {AUTHORS,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
