# Maintainer: David Runge <dave@sleepmap.de>
_name=pymediainfo
pkgbase=python-pymediainfo
pkgname=('python-pymediainfo' 'python2-pymediainfo')
pkgver=3.0
pkgrel=1
pkgdesc="A Python wrapper around the MediaInfo library"
arch=('any')
url="https://github.com/sbraz/pymediainfo/"
license=('MIT')
makedepends=('mediainfo' 'python-setuptools-scm' 'python2-setuptools-scm')
checkdepends=('python-pytest' 'python-pytest-runner' 'python2-pytest' 'python2-pytest-runner')
source=("${pkgname}-${pkgver}.tar.gz::https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('b6828065f8d4907e94a299e4b3d550b1aad6dfa335b5eaae73e51a9b9052a8c494960b95473b49c10bb4407d3f48db07c2457b2937464317d64997c380571bcd')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgbase}-${pkgver}"
}

build() {
  cd "${pkgbase}-${pkgver}"
  python setup.py build
  python2 setup.py build
}

check() {
  cd "${pkgbase}-${pkgver}"
  python setup.py test
  python2 setup.py test
}

package_python-pymediainfo() {
  depends=('libmediainfo' 'python')
  cd "${pkgbase}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README.rst}
}

package_python2-pymediainfo() {
  depends=('libmediainfo' 'python2')
  cd "${pkgbase}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 {AUTHORS,README.rst}
}
