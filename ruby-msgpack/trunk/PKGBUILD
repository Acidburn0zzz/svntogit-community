# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

_gemname=msgpack
pkgname="ruby-${_gemname}"
pkgver=1.2.9
pkgrel=1
pkgdesc='MessagePack implementation for Ruby'
url='https://github.com/msgpack/msgpack-ruby'
arch=('x86_64')
license=('Apache')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-yard' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=("https://github.com/msgpack/msgpack-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('49fce9247c71fd94d3451ce854b4403f7c6c3e3d343dc2c903fb0080967567c6')
sha512sums=('71b93f3ef7a0c2ba4f9f8fb69ca2881cf8e5a335cc15e51dad5d718a6ccda9bca8d20ab2cbb68451f235b3757eb07762b0cebea41a5f62ca62f2cba5abfdfff9')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"
  sed 's|git ls-files|find|' -i "${_gemname}.gemspec"
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd "${_gemname}-ruby-${pkgver}"
  rake compile
  rake build
  rake clean
}

check() {
  cd "${_gemname}-ruby-${pkgver}"
  rake spec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin"  \
    "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/"${_gemname}-${pkgver}"/{ext,tmp,lib/*/*.so} \
    extensions/*/*/"${_gemname}-${pkgver}"/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
