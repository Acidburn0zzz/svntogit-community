# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>

_gemname=msgpack
pkgname="ruby-${_gemname}"
pkgver=1.3.2
pkgrel=1
pkgdesc='MessagePack implementation for Ruby'
url='https://github.com/msgpack/msgpack-ruby'
arch=('x86_64')
license=('Apache')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-yard' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=("https://github.com/msgpack/msgpack-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz")
sha256sums=('286a8a9adeddccb5372be9f15bc692e1b792f2e8c43b3da184310a011a68a9d5')
sha512sums=('db3477384e5badfd25548bd5bbd029acd0d7afba7c14a04932a4f154f60e6b13209ad78f3c013552967c5f6b65f2fec7fa0092329620c0c0d13cedb8e0d7cf33')

prepare() {
  cd "${_gemname}-ruby-${pkgver}"
  sed 's|git ls-files|find|' -i "${_gemname}.gemspec"
  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd "${_gemname}-ruby-${pkgver}"
  rake compile
  rake build
  rake clean
}

check() {
  cd "${_gemname}-ruby-${pkgver}"
  rake spec
}

package() {
  cd "${_gemname}-ruby-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install \
    --ignore-dependencies \
    --no-user-install \
    -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin"  \
    "pkg/${_gemname}-${pkgver}.gem"

  install -Dm 644 README.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/"${_gemname}-${pkgver}"/{ext,tmp,lib/*/*.so} \
    extensions/*/*/"${_gemname}-${pkgver}"/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
