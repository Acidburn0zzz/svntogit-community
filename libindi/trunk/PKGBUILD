# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=libindi
pkgver=1.7.0
pkgrel=1
pkgdesc="A distributed control protocol designed to operate astronomical instrumentation"
url="http://www.indilib.org/index.php?title=Main_Page"
license=(GPL2)
arch=(x86_64)
depends=(libnova cfitsio libusb libjpeg gsl libtheora)
makedepends=(cmake boost)
#source=("https://github.com/indilib/indi/releases/download/v$pkgver/libindi_$pkgver.tar.gz")
source=($pkgname-$pkgver.tar.gz::"https://github.com/indilib/indi/archive/v$pkgver.tar.gz")
sha256sums=('a214219296e53d297985218d5d0342bb267817b76328d4b576c311388ac3bb05')

prepare() {
  mkdir -p build

  sed -e 's|GROUP="plugdev", MODE="0666"|TAG+="uaccess"|' -i indi-$pkgver/libindi/drivers/*/*.rules
}

build() {
  cd build
  cmake ../indi-$pkgver/libindi \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DUDEVRULES_INSTALL_DIR=/usr/lib/udev/rules.d
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
