# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: archtux <antonio.arias99999 at gmail.com>

pkgname=converseen
pkgver=0.9.6.1
pkgrel=1
pkgdesc="The batch image converter and resizer"
arch=('i686' 'x86_64')
url="http://converseen.fasterland.net/"
license=('GPL3')
depends=('imagemagick' 'qt5-base' 'libwmf' 'openexr' 'libwebp' 'openjpeg2' 'librsvg' 'ghostscript')
makedepends=('cmake' 'qt5-tools')
source=("http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
md5sums=('5e72976f887a7d0733539c32afdf7436')

build() {
  mkdir build
  cd build
  cmake ../${pkgname}-${pkgver} \
          -DCMAKE_BUILD_TYPE=Release \
          -DCMAKE_INSTALL_PREFIX=/usr 
  make
}

package() {
  cd build

  make DESTDIR="${pkgdir}" install
}
