# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=uusi
pkgver=0.2.0.0
pkgrel=2
pkgdesc="Remove all version constraints of dependencies in .cabal file"
url="https://github.com/berberman/uusi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-microlens' 'haskell-microlens-th')
makedepends=('ghc' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bd9a98856976b0364e39f2d36240551f3b04f9e881b81470ed3c60ffe0d9296728ce57e158e82eb8b88fa2624d5c8698883fee15907922e3090b27069fecbe48')

prepare() {
  cd $pkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
  sed -i 's/<3.1/<4/' $pkgname.cabal
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
}

check() {
  cd $pkgname-$pkgver
  runhaskell Setup test
}

package() {
  cd $pkgname-$pkgver

  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
