# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=uusi
pkgver=0.1.0.0
pkgrel=1
pkgdesc="Remove all version constraints of dependencies in .cabal file"
url="https://github.com/berberman/uusi"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('9155af204bca5b88a979a04f4612c69b657730ee2fe4b47de1c76c6ed5fa125bb9cc113c0751869f5dd90b42dca78f519c53d4beab0c1348bee8aaa1ff056350')

prepare() {
  cd $pkgname-$pkgver
  echo -e "import Distribution.Simple\nmain = defaultMain" > Setup.hs
}

build() {
  cd $pkgname-$pkgver

  runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
    --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
    --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
    --ghc-option=-optl-Wl\,-z\,relro\,-z\,now \
    --ghc-option='-pie'

  runhaskell Setup build $MAKEFLAGS
}

package() {
  cd $pkgname-$pkgver

  runhaskell Setup copy --destdir="$pkgdir"
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
