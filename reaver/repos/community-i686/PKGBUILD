# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Bartosz Fe≈Ñski <fenio@debian.org>

pkgname=reaver
pkgver=1.5.2
pkgrel=1
pkgdesc="Brute force attack against Wifi Protected Setup"
arch=('i686' 'x86_64')
url="http://code.google.com/p/reaver-wps/"
license=('GPL2')
depends=('libpcap' 'sqlite')
makedepends=('git')
backup=('etc/reaver/reaver.db')
source=("git+https://github.com/t6x/reaver-wps-fork-t6x.git#tag=v$pkgver")
md5sums=('SKIP')

build() {
  cd "$srcdir/reaver-wps-fork-t6x/src"
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  install -Dm755 "$srcdir/reaver-wps-fork-t6x/src/reaver" "$pkgdir/usr/bin/reaver"
  install -Dm755 "$srcdir/reaver-wps-fork-t6x/src/wash" "$pkgdir/usr/bin/wash"
  install -Dm644 "$srcdir/reaver-wps-fork-t6x/src/reaver.db" "$pkgdir/etc/reaver/reaver.db"
  install -Dm644 "$srcdir/reaver-wps-fork-t6x/docs/README" "$pkgdir/usr/share/doc/reaver/README"
  install -Dm644 "$srcdir/reaver-wps-fork-t6x/docs/reaver.1.gz" "$pkgdir/usr/share/man/man1/reaver.1.gz"
}
