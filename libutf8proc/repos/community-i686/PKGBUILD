# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=libutf8proc
pkgver=1.3.1
pkgrel=1
pkgdesc='C library for processing UTF-8 encoded Unicode strings'
arch=('x86_64' 'i686')
url='http://julialang.org/utf8proc/'
license=('MIT' 'custom')
replaces=('utf8proc')
depends=('glibc')
source=("utf8proc.tar.gz::https://github.com/JuliaLang/utf8proc/archive/v$pkgver.tar.gz")
sha256sums=('83b60fe21fd8a017b8ad469515873893c8e911a5bef336a427594d398b5688cc')

build() {
  make -C "utf8proc-$pkgver"
}

check() {
  make -C "utf8proc-$pkgver" check
}

package() {
  cd "utf8proc-$pkgver"

  make DESTDIR="$pkgdir" prefix=/usr install
  install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE.md"
}

# getver: julialang.org/utf8proc/releases/
# vim:set ts=2 sw=2 et:
