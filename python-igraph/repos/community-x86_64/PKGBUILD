# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-igraph
pkgver=0.8.3
pkgrel=3
url="https://igraph.org/python/"
license=(GPL)
arch=(x86_64)
pkgdesc="Python bindings for the igraph library"
depends=(python-texttable gmp libxml2)
# TODO: unbundle igraph when 0.9.0 is released
makedepends=(python-setuptools)
source=("https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('e1f27622eddeb2bd5fdcbadb41ef048e884790bb050f9627c086dc609d0f1236')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  cd $pkgname-$pkgver
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
