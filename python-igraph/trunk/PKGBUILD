# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-igraph
pkgver=0.7.1.post6
pkgrel=6
url="https://igraph.org/python/"
license=(GPL)
arch=(x86_64)
pkgdesc="Python bindings for the igraph library"
depends=(igraph python)
source=("https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz"
        igraph-python3.patch::"https://github.com/igraph/python-igraph/commit/8864b468.patch")
sha256sums=('a5ea42790a077eadb3d1e8f82edc47516fea4a2b26bbf52bfc148370fe0fe676'
            '2ae94434dca55c4a7afbfc292e308aea48c647e737a73fd5c3a2d3f2b2dc24dc')

prepare() {
  cd $pkgbase-$pkgver
  patch -p1 -i ../igraph-python3.patch # fixed Plot._repr_svg_() on Python 3.x
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build
}

package() {
  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
