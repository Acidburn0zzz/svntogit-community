# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Lukasz Pozarlik <lpozarlik@gmail.com>

pkgname=python-igraph
pkgver=0.9.7
pkgrel=1
url='https://igraph.org/python/'
license=(GPL)
arch=(x86_64)
pkgdesc='Python bindings for the igraph library'
depends=(python-texttable igraph)
makedepends=(python-setuptools)
source=(https://pypi.python.org/packages/source/p/$pkgname/$pkgname-$pkgver.tar.gz)
sha256sums=('832e9c6fe8a36886399f0c97fb1f7f75350df0b79e089d96c4b9ff86b2058dd3')

build() {
  cd $pkgname-$pkgver
  python setup.py build --use-pkg-config
}

check() {
  cd $pkgname-$pkgver
  PYTHONPATH="$PWD"/`ls -d build/lib.*` python -m unittest
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --skip-build --root="$pkgdir" --optimize=1 
}
