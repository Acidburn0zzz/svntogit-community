# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kipi-plugins
pkgver=5.9.0
pkgrel=5
pkgdesc="A collection of plugins extending the KDE graphics and image applications"
arch=(x86_64)
license=(GPL)
url="http://www.digikam.org/"
depends=(kio libkipi)
optdepends=('libmediawiki: MediaWiki Export plugin'
            'libkvkontakte: VKontakte.ru Exporter plugin'
            'qt5-xmlpatterns: rajce.net plugin')
makedepends=(extra-cmake-modules libksane liblqr doxygen lensfun eigen boost kdoctools marble-common opencv
             kfilemetadata akonadi-contacts qtav threadweaver kcalcore knotifyconfig libmediawiki libkvkontakte
             glu qt5-xmlpatterns)
source=("http://download.kde.org/stable/digikam/digikam-$pkgver.tar.xz")
sha256sums=('660d32eedcfd6fe1a75170a651fab068ae62f7f092ae710771a2f32a1c550d1b')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake ../digikam-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF \
    -DENABLE_KFILEMETADATASUPPORT=ON \
    -DENABLE_MEDIAPLAYER=ON \
    -DENABLE_AKONADICONTACTSUPPORT=ON \
    -DENABLE_MYSQLSUPPORT=ON \
    -DENABLE_APPSTYLES=ON
  make -C extra
}

package() {
  cd build/extra
  make DESTDIR="$pkgdir" install
  cd $srcdir/build/po
  make DESTDIR="$pkgdir" install

# Provided by digikam
  rm "$pkgdir"/usr/share/locale/*/LC_MESSAGES/digikam.mo
}
