# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-phonenumbers
pkgname=(python-phonenumbers python2-phonenumbers)
pkgver=8.9.16
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
makedepends=("python-setuptools" "python2-setuptools")
source=("$pkgbase-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('19003ede851130faeff5dad456873e5f66ee3f41d287b56df212c78b770361f2fd1342f6a9ce845129d78cf51cdd5182fa15d51d88dd4942d2c3f6d683a1775f')

prepare() {
  cp -a python-phonenumbers-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py build

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/python-phonenumbers-$pkgver
  python setup.py test

  cd "$srcdir"/python-phonenumbers-$pkgver-py2
  python2 setup.py test
}

package_python-phonenumbers() {
  depends=('python')

  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}

package_python2-phonenumbers() {
  depends=('python2')

  cd python-phonenumbers-$pkgver-py2
  python2 setup.py install -O1 --root "$pkgdir"
}
