# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.12.19
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=("python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('6b40de0c9ed4c17c4d098bbec515e6e6a5f9e5c9b7961c8cd40ef9320761d7ad0650b2d722edd58b4512cc86835e742d559196939a8825588238d18806fba2b7')

build() {
  cd python-phonenumbers-$pkgver
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}
