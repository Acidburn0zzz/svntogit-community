# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.10.11
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=("python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('8cb4cc45e8bc3c3d2b6887b5f9b4c7dc5904909744a61d7cc676f18e9886114f87d629c296ae86d72c70a0bf8f9a9b76b85f538b059fa913a203132a32a55049')

build() {
  cd python-phonenumbers-$pkgver
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}
