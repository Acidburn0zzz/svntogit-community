# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-phonenumbers
pkgver=8.12.28
pkgrel=1
pkgdesc="Python version of Google's common library for parsing, formatting, storing and validating international phone numbers"
arch=('any')
url="https://github.com/daviddrysdale/python-phonenumbers"
license=('Apache')
depends=('python')
makedepends=("python-setuptools")
source=("$pkgname-$pkgver.tar.gz::https://github.com/daviddrysdale/python-phonenumbers/archive/v$pkgver.tar.gz")
sha512sums=('cbae9ee650975a82d7feedfa91e6122205984e0a3cbb0fcdbafa92a8b735cb832dd34006e862f09cc1391dbc8f6eb8e8a9a0a831d708dd9a6bdac7c7686ca638')

build() {
  cd python-phonenumbers-$pkgver
  python setup.py build
}

check() {
  cd python-phonenumbers-$pkgver
  python setup.py test
}

package() {
  cd python-phonenumbers-$pkgver
  python setup.py install -O1 --root "$pkgdir"
}
