# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.027.00
pkgrel=3
pkgdesc="A kernel module for Realtek 8168 network cards for kernel26-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('i686' 'x86_64')
depends=('glibc' "linux-lts>=3.0" "linux-lts<3.1")
makedepends=("linux-lts-headers>=3.0" "linux-lts-headers<3.1")
source=(http://r8168.googlecode.com/files/$_pkgname-$pkgver.tar.bz2)
install=$pkgname.install

_extramodules=extramodules-3.0-lts
_kernver="$(cat /lib/modules/$_extramodules/version || true)"

build() {
	cd "$srcdir/$_pkgname-$pkgver"
	make -C /lib/modules/$_kernver/build SUBDIRS="$srcdir/$_pkgname-$pkgver/src" EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" modules
}

package() {
	cd "$srcdir/$_pkgname-$pkgver/src"
	install -D -m644 $_pkgname.ko "$pkgdir/lib/modules/$_extramodules/$_pkgname.ko"
	find "$pkgdir" -name '*.ko' -exec gzip -9 {} \;
}

md5sums=('98155ce1ad5c248eeb7a93c9954d8344')
sha256sums=('5bc3b44bd69a05e45ab13be727256a95adf18a658f726141283046b397dc22e2')
