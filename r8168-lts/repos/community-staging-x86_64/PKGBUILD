# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=r8168-lts
_pkgname=r8168
pkgver=8.047.01
pkgrel=7
pkgdesc="A kernel module for Realtek 8168 network cards for linux-lts"
url="http://www.realtek.com.tw"
license=("GPL")
arch=('x86_64')
depends=('glibc' "linux-lts>=4.19.49" "linux-lts<4.20")
makedepends=("linux-lts-headers>=4.19.49" "linux-lts-headers<4.20")
source=(https://github.com/mtorromeo/r8168/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('d298c4b52bb68b494230c219879eaf57cabe255f3475f7a792f76ad7fd83a0eb')

_extramodules=extramodules-4.19-lts

build() {
	cd "$_pkgname-$pkgver"
	KERNEL_VERSION="$(</usr/lib/modules/$_extramodules/version)"
	make -C /usr/lib/modules/$KERNEL_VERSION/build \
			SUBDIRS="$srcdir/$_pkgname-$pkgver/src" \
			EXTRA_CFLAGS="-DCONFIG_R8168_NAPI -DCONFIG_R8168_VLAN" \
			modules
}

package() {
	cd "$_pkgname-$pkgver"
	install -Dt "$pkgdir/usr/lib/modules/$_extramodules" -m644 src/*.ko
	find "$pkgdir" -name '*.ko' -exec xz {} +

	echo "blacklist r8169" | \
		install -Dm644 /dev/stdin "$pkgdir/usr/lib/modprobe.d/$pkgname.conf"
}
