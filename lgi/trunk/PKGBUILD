# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: SÃ©bastien Luttringer

pkgbase=lgi
pkgname=(lua53-lgi)
pkgver=0.9.2
pkgrel=5
pkgdesc='Lua bindings for gnome/gobject using gobject-introspection library'
url="https://github.com/pavouk/$pkgbase"
arch=(x86_64)
license=(MIT)
depends=(glibc glib2 libffi lua53 gobject-introspection-runtime)
makedepends=(gobject-introspection)
replaces=(lgi)
conflicts=(lgi lua-lgi)
_archive="$pkgbase-$pkgver"
source=("$url/archive/$pkgver/$_archive.tar.gz")
sha256sums=('cfc4105482b4730b3a40097c9d9e7e35c46df2fb255370bdeb2f45a886548c4f')

build() {
	cd "$_archive"
	CFLAGS="$CFLAGS -I/usr/include/lua5.3" make
}

check() {
	cd "$_archive"
	# TODO: fix cairo-gobject dependency
	# make check
}

package_lua53-lgi() {
	cd "$_archive"
	make \
		LUA_LIBDIR=/usr/lib/lua/5.3 \
		LUA_SHAREDIR=/usr/share/lua/5.3 \
		DESTDIR="$pkgdir/" install
	install -Dm0755 tools/dump-typelib.lua "$pkgdir/usr/bin/dump-typelib"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
	install -Dm0644 -t "$pkgdir/usr/share/doc/$pkgname/" docs/*
	install -Dm0644 -t "$pkgdir/usr/share/$pkgname/samples/" samples/*.lua
	install -Dm0644 -t "$pkgdir/usr/share/$pkgname/samples/gtk-demo/" samples/gtk-demo/*
	install -Dm0644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
