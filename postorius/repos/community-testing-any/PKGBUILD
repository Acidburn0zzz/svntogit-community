# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=postorius
pkgver=1.3.2
pkgrel=2
pkgdesc="The New Mailman Web UI"
arch=('any')
url="https://gitlab.com/mailman/postorius"
license=('GPL3')
depends=('python-cmarkgfm' 'python-django' 'python-django-mailman3'
'python-mailmanclient' 'python-readme-renderer')
makedepends=('python-setuptools')
checkdepends=('mailman3' 'python-beautifulsoup4' 'python-isort' 'python-mock'
'python-pytest' 'python-pytest-django' 'python-vcrpy')
optdepends=('mailman3: for configuring a local mailman instance'
            'uwsgi-plugin-python: for running inside uwsgi')
replaces=('python-django-postorius')
backup=("etc/uwsgi/${pkgname}.ini"
        "etc/webapps/${pkgname}/settings.py")
install="${pkgname}.install"
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}.sysusers"
        "${pkgname}.tmpfiles"
        "${pkgname}.uwsgi"
        "${pkgname}-1.3.2-settings.patch")
sha512sums=('41ff225fa574dd4bd0ac88419a10a55a71c52a64df01a1a593963fdfa62baf16a0df735364688ea24680c8e88093841892474279d40c1428948375db7eb6333b'
            'SKIP'
            '3884b818da2deb148279e50316f720c95f0aaa529777b1b3662d8ab253cda823a90682a9eb47e941ca5ba80eefcb9a2d4afacd29f0340d7f9828b36ebcee23b3'
            '0fe9517bb56c241d38e97e0141e499480760b3c6e786e78a4e6f5825fc319a4b518649a2457a5a224e49decaeb0340cbb0929ec82d51b66afe9cce7f9d270f1d'
            'c042f00d0d8dc72dadd19023152e55246c4d7a4b9d53bca3a05e7b8cb38cac5116417440d4ae82350820c7cb0cd414dcce45fa6af9be19296c4ebd0862dba896'
            '589b000789375a1669eaa165c904e27fec097802fca39b4469b61cac6b615031a2ca6ece63f32cfd78f91ef249b0b15ec43c6ce6130166e7c127ef3d717e02f2')
validpgpkeys=('541EA0448453394FF77A0ECC9D9B2BA061D0A67C') # Abhilash Raj <raj.abhilash1@gmail.com>

prepare() {
  cd "$pkgname-$pkgver"
  # patch global paths into settings.py
  patch -Np1 -i "../${pkgname}-1.3.2-settings.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  local python_stdlib_basepath="$(python -c "from sysconfig import get_path; print(get_path('stdlib'))")"
  # install to temporary location, so that tests can run
  python setup.py install --root="${PWD}/tmp_install" --optimize=1
  export PYTHONPATH="${PWD}/tmp_install/${python_stdlib_basepath/\//}/site-packages:${PYTHONPATH}"
  cd example_project/
  pytest -v "../tmp_install/${python_stdlib_basepath/\//}/site-packages/${pkgname}"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # django project
  install -vDm 644 example_project/{__init__,manage,urls,wsgi}.py \
    -t "${pkgdir}/usr/share/webapps/${pkgname}"
  # symlink locale and static dirs to state dir
  ln -svf "/var/lib//${pkgname}/locale/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  ln -svf "/var/lib/${pkgname}/static/" \
    "${pkgdir}/usr/share/webapps/${pkgname}"
  # state dir
  install -vdm 750 "${pkgdir}/var/lib/${pkgname}"
  install -vdm 755 "${pkgdir}/var/lib/${pkgname}/"{locale,static}
  # log dir
  install -vdm 750 "${pkgdir}/var/log/${pkgname}"
  # config
  install -vDm 640 example_project/settings.py \
    -t "${pkgdir}/etc/webapps/${pkgname}"
  ln -svf "/etc/webapps/${pkgname}/settings.py" \
    "${pkgdir}/usr/share/webapps/${pkgname}/settings.py"
  # uwsgi
  install -vDm 644 "../${pkgname}.uwsgi" "${pkgdir}/etc/uwsgi/${pkgname}.ini"
  # tmpfiles.d
  install -vDm 644 "../${pkgname}.tmpfiles" \
    "${pkgdir}/usr/lib/tmpfiles.d/${pkgname}.conf"
  # sysusers.d
  install -vDm 644 "../${pkgname}.sysusers" \
    "${pkgdir}/usr/lib/sysusers.d/${pkgname}.conf"
}
