# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Patrick Ulbrich <zulu99 at gmx . net>
# Contributor: rasmus <rasmus . pank at gmail . com>

pkgname=mailnag
pkgver=1.3.0+49+g1ec3bc9
pkgrel=1
pkgdesc="Extensible mail notification daemon"
arch=('any')
url="https://github.com/pulb/mailnag"
license=('GPL')
depends=('gst-plugins-base' 'gtk3' 'libnotify' 'libsecret' 'python-dbus' 'python-gobject' 'python-xdg')
makedepends=(git)
optdepends=('mailnag-goa-plugin: GNOME Online Accounts integration'
            'mailnag-gnome-shell: GNOME Shell integration')
_commit=1ec3bc945efde6dfad8d9dec183b74d9ab7758bb  # master
source=("git+https://github.com/pulb/mailnag.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  cd $pkgname
  python setup.py build
}

package() {
  cd $pkgname
  python setup.py install --root="$pkgdir" --optimize=1
}
