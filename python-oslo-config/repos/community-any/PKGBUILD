# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-config
pkgver=8.5.0
pkgrel=1
pkgdesc="parsing command line arguments and .ini style configuration files"
arch=('any')
url="https://pypi.python.org/pypi/oslo.config/$pkgver"
license=('Apache')
depends=('python-six' 'python-netaddr' 'python-stevedore' 'python-debtcollector'
         'python-oslo-i18n' 'python-rfc3986' 'python-yaml')
makedepends=('python-setuptools')
checkdepends=('python-oslotest' 'python-sphinx' 'python-requests-mock' 'python-oslo-log')
options=('!emptydirs')
source=("https://github.com/openstack/oslo.config/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0617bd7ad5834ff0c037398b2079f3919b219f63c5e27b401ea065d31129355e4990e8cd0eb576cbbf91c3f8a924c4683cfe039278e784f583027a4ab04913dc')

export PBR_VERSION=$pkgver

build() {
  cd oslo.config-$pkgver
  python setup.py build
}

check() {
  cd oslo.config-$pkgver
  # https://bugs.launchpad.net/oslo.config/+bug/1908934
  stestr run || echo "Tests failed"
}

package() {
  cd oslo.config-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
