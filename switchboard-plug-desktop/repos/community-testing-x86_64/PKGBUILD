# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-desktop
pkgver=2.8.1
pkgrel=1
pkgdesc='Switchboard Desktop Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-pantheon-shell
license=(GPL3)
groups=(pantheon-unstable)
depends=(
  gdk-pixbuf2
  glib2
  gnome-desktop
  gtk3
  libgee
  libgexiv2
  libgranite.so
  libswitchboard-2.0.so
  plank
  tumbler
)
makedepends=(
  git
  meson
  vala
)
source=(switchboard-plug-desktop::git+https://github.com/elementary/switchboard-plug-pantheon-shell.git#tag=65316fe4896a19a2727c7b7af18f227b2e0a6f65)
sha256sums=(SKIP)

pkgver() {
  cd switchboard-plug-desktop

  git describe --tags
}

prepare() {
  sed 's|/usr/share/backgrounds|/usr/share/backgrounds/elementary|' -i switchboard-plug-desktop/src/Views/Wallpaper.vala
}

build() {
  arch-meson switchboard-plug-desktop build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
