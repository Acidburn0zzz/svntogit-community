# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: hexchain <i at hexchain.org>

pkgname=range-v3
pkgver=0.3.7
pkgrel=1
pkgdesc="Experimental range library for C++11/14/17"
arch=('any')
url='https://github.com/ericniebler/range-v3'
license=('custom')
makedepends=('cmake')
source=("https://github.com/ericniebler/range-v3/archive/${pkgver}.tar.gz")
sha512sums=('326264d055eec0c59857a093cf2f664e3b5b61ff2812b731d37ef2f889fe18b0d4696cae455d979cac4c047a6935d096e699fb9651e8d6da0d3245a66443426c')

prepare() {
    cd "${srcdir}/range-v3-${pkgver}"
    rm -rf build && mkdir build
}

build() {
    cd "${srcdir}/range-v3-${pkgver}"

    cd build
    cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DRANGE_V3_TESTS=OFF \
        -DRANGE_V3_HEADER_CHECKS=OFF \
        -DRANGE_V3_EXAMPLES=OFF \
        -DRANGE_V3_PERF=OFF
    make
}

package() {
    cd "${srcdir}/range-v3-${pkgver}"
    install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    cd build
    make DESTDIR="${pkgdir}/" install
}
