# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: hexchain <i at hexchain.org>

pkgname=range-v3
pkgver=0.10.0
pkgrel=2
pkgdesc="Experimental range library for C++11/14/17"
arch=('any')
url='https://github.com/ericniebler/range-v3'
license=('custom')
makedepends=('cmake' 'ninja')
source=("$pkgname-pkgver.tar.gz::https://github.com/ericniebler/range-v3/archive/${pkgver}.tar.gz")
sha512sums=('4db089dcbf638eb30fa20dcf6785dd460c5a074a8912984a484592753cfee9f5b9a0e2c56b5bc5921d1b6120877aa2c83ac0a6fe428088ef2854033a635e9723')

build() {
    cd "${srcdir}/range-v3-${pkgver}"

    cmake . \
        -Bbuild \
        -GNinja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DRANGE_V3_TESTS=OFF \
        -DRANGE_V3_HEADER_CHECKS=OFF \
        -DRANGE_V3_EXAMPLES=OFF \
        -DRANGE_V3_PERF=OFF
    ninja -C build
}

package() {
    cd "${srcdir}/range-v3-${pkgver}"
    install -Dm644 "LICENSE.txt" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

    DESTDIR="${pkgdir}/" ninja -C build install
    mv "$pkgdir"/usr/include/module.modulemap "$pkgdir"/usr/include/range-v3.modulemap
}
