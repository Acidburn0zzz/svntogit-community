# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=pdftk
pkgver=3.3.1
pkgrel=2
pkgdesc='Command-line tool for working with PDFs'
arch=('any')
url='https://gitlab.com/pdftk-java/pdftk'
license=('GPL')
depends=('java-runtime-headless' 'sh')
makedepends=('ant' 'bcprov' 'java-commons-lang' 'strip-nondeterminism')
checkdepends=('java-hamcrest' 'junit' 'junit-system-rules' 'poppler')
optdepends=('bcprov: support for signed PDF documents'
            'java-commons-lang: burst, dump_data, dump_data_fields and update_info operation support')
source=("$url/-/archive/v$pkgver/$pkgname-v$pkgver.tar.bz2" 'pdftk.sh'
        "pdftk-3.3.1_fix-bcprov-1.70.patch::$url/-/commit/d8259eaaee8cf3c52504037fe08c180eb94e4b76.patch")
sha512sums=('4b51bb6003680a5e41a3c08a8ab49e09de56a7dd5d4808ead1adf8293549436685fa4f3901c4426f0f92555bf85d2c875a80f2858e7de7ae53e01fda4f395d18'
            '439c397472012d71c8e782a75819a7a950474c387b7050bbd5aa2f849d1dbe29423accf3f8e17d18436ba32eb4f76072df5b4f85caebd853df1e75c131631988'
            '599cf4816f5cec537e2265ad79b3ff2cce4183b1c35ec50fad3b871d78a306ab3e654009c703c4947bd4f3919df7bbcb251f1fa27afb9ebf395e5d8669324a23')

prepare() {
	cd "$pkgname-v$pkgver"
	mkdir lib
	ln -s /usr/share/java/{bcprov,commons-lang/commons-lang,hamcrest-core,junit,junit-system-rules/system-rules}.jar lib
	# No need for code coverage reports during tests
	sed -ri '/<\/?jacoco:coverage>/d' build.xml

	# Fix building with bcprov 1.70 (https://gitlab.com/pdftk-java/pdftk/-/merge_requests/26)
	patch --forward --strip=1 --input="$srcdir/pdftk-3.3.1_fix-bcprov-1.70.patch"
}

build() {
	cd "$pkgname-v$pkgver"
	ant clean jar

	# Timestamps in JAR files generated by Ant do not honour SOURCE_DATE_EPOCH
	# (https://bz.apache.org/bugzilla/show_bug.cgi?id=61269)
	strip-nondeterminism --timestamp "$SOURCE_DATE_EPOCH" build/jar/pdftk.jar
}

check() {
	cd "$pkgname-v$pkgver"
	ant test-run
}

package() {
	cd "$pkgname-v$pkgver"
	install -Dm644 build/jar/pdftk.jar -t "$pkgdir/usr/share/java/$pkgname"
	install -Dm644 pdftk.1 -t "$pkgdir/usr/share/man/man1"
	install -Dm755 "$srcdir/pdftk.sh" "$pkgdir/usr/bin/pdftk"
}
