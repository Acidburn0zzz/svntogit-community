# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=libretro-blastem
pkgver=944
pkgrel=1
pkgdesc='Sega Mega Drive core'
arch=(x86_64)
url='https://github.com/libretro/blastem-libretro'
license=(GPL3)
groups=(libretro)
depends=(libretro-core-info)
makedepends=(git)
_commit='ccc11186b008fd674263812e37361df2b89166a9'
source=(
  libretro-blastem::git+https://github.com/libretro/blastem-libretro.git#commit=${_commit}
  libretro-blastem-flags.patch
)
sha256sums=(
  SKIP
  2289a7f76626bed09f5e191643a1ffeaaa2191245573eaa336824c0e9a45c35e
)

pkgver() {
  cd libretro-blastem

  git rev-list --count HEAD
}

prepare() {
  cd libretro-blastem

  patch -Np1 -i ../libretro-blastem-flags.patch
}

build() {
  cd libretro-blastem/libretro

  make
}

package() {
  cd libretro-blastem/libretro

  install -Dm 644 blastem_libretro.so -t "${pkgdir}"/usr/lib/libretro/
}

# vim: ts=2 sw=2 et:
