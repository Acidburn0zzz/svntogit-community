# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Robert Emil Berge <robert@rebi.no>

pkgname=rosegarden
pkgver=18.12
pkgrel=1
pkgdesc="MIDI/audio sequencer and notation editor"
arch=('x86_64')
url="https://www.rosegardenmusic.com/"
license=('GPL2')
groups=('pro-audio')
depends=('desktop-file-utils' 'fftw' 'jack' 'liblrdf' 'liblo' 'lirc'
'hicolor-icon-theme' 'qt5-base')
makedepends=('cmake' 'dssi' 'imake' 'ladspa' 'qt5-tools')
optdepends=('cups: Printing support'
            'dssi-plugins: DSSI plugin support'
            'evince: Print preview support'
            'ladspa-plugins: LADSPA plugin support'
            'lilypond: Notation display support'
            'mupdf: Print preview support'
            'okular: Print preview support'
            'timidity++: Softsynth support'
            'flac: FLAC support'
            'wavpack: Lossless audio codec support')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.bz2")
sha512sums=('2adf1c2bfbd6a054d66438b766a33774997ae577668ddbe7869ed4d25ab263b04e1ff086cc5fb4d2916621138d8986a1c032bd2719fa7cc7b43f0abc073d78d8')

prepare() {
  cd "${pkgname}-${pkgver}"
  mkdir -p build
}

build() {
  cd "${pkgname}-${pkgver}/build"
  cmake .. \
        -DCMAKE_INSTALL_PREFIX=/usr
  make
}

check() {
  cd "${pkgname}-${pkgver}/build"
  make test
}

package() {
  cd "${pkgname}-${pkgver}/build"
  make DESTDIR="$pkgdir" install
  install -t "${pkgdir}/usr/share/doc/${pkgname}" \
    -vDm 644 ../{AUTHORS,README}
}

# vim:set ts=2 sw=2 et:
