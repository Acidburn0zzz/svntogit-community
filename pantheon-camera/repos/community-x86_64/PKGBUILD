# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-camera
pkgver=1.0.6
pkgrel=1
pkgdesc='The Pantheon Camera Application'
arch=(x86_64)
url=https://github.com/elementary/camera
license=(GPL3)
groups=(pantheon)
depends=(
  clutter
  clutter-gst
  clutter-gtk
  glib2
  gstreamer
  gtk3
  libcanberra
  libgranite.so
)
makedepends=(
  appstream
  git
  intltool
  meson
  vala
)
source=(pantheon-camera::git+https://github.com/elementary/camera.git#tag=6f5c984751479290bb22321a3e1773746d8f520e)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-camera

  git describe --tags
}

build() {
  arch-meson pantheon-camera build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
