# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.11
pkgrel=6
pkgdesc="Add-ons for xmonad"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('ghc=7.6.2-1' 'xmonad=0.11-5' 'sh' 'haskell-x11=1.6.1.1-2' 'haskell-x11-xft=0.3.1' 'haskell-utf8-string=0.3.7-4' 'haskell-random=1.0.1.1-4')
install='xmonad-contrib.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz xmonad-contrib-remove-getatomname.patch)

build() {
  cd $srcdir/$pkgname-$pkgver
  patch -Np1 -i $srcdir/xmonad-contrib-remove-getatomname.patch

  runhaskell Setup.lhs configure --ghc --enable-shared --enable-split-objs --prefix=/usr -fuse_xft \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  install -D LICENSE $pkgdir/usr/share/licenses/xmonad-contrib/LICENSE
}
md5sums=('05fba258ac6063e49b50786a0869bd1f'
         '1d1fab94ae24b2a8f6a5dcd9cd6d9548')
