# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.9.1
pkgrel=7
pkgdesc="Add-ons for xmonad"
arch=('i686' 'x86_64')
url="http://xmonad.org/"
license=('BSD')
depends=('xmonad=0.9.1' 'sh' 'haskell-x11-xft=0.3' 'haskell-utf8-string=0.3.6')
makedepends=('ghc=7.0.1' 'haskell-x11=1.5.0.0')
install='xmonad-contrib.install'
source=(http://hackage.haskell.org/packages/archive/$pkgname/$pkgver/$pkgname-$pkgver.tar.gz
        ghc-7-compat.patch)
md5sums=('09a6e5173878d43b9f3ddd13b3ec955b'
         '9714db28af449d37efe04ae5bd5abd94')

build() {
  cd $srcdir/$pkgname-$pkgver
  # Patches from darcs to build with GHC7 : adapted from patch
  #  Sun Jan 24 21:33:24 CET 2010  Adam Vogt <vogt.adam@gmail.com>
  #    * Use extensible-exceptions to allow base-3 or base-4
  patch -fp1 -i "${srcdir}/ghc-7-compat.patch" || true
  runhaskell Setup.lhs configure --ghc --prefix=/usr --flags="use_xft use_utf8" \
             --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd $srcdir/$pkgname-$pkgver
  install -D -m744 register.sh $pkgdir/usr/share/haskell/$pkgname/register.sh
  install -m744 unregister.sh $pkgdir/usr/share/haskell/$pkgname/unregister.sh
  runhaskell Setup.lhs copy --destdir=$pkgdir
  install -D LICENSE $pkgdir/usr/share/licenses/xmonad-contrib/LICENSE
}
