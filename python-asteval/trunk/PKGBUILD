# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-asteval
pkgver=0.9.20
pkgrel=1
pkgdesc="Minimalistic evaluator of python expression using ast module"
url="https://github.com/newville/asteval"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-numpy')
source=("https://github.com/newville/asteval/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a37d6eb5e1a952b11118fafea9c7f8bbf659a71bff2dfe6126ccc79116039e6ede94ed2b1e46ee152a3e3975c98e144960c5daedc59d82cef5dec56c51c4e258')

build() {
  cd asteval-$pkgver
  python setup.py build
}

check() {
  cd asteval-$pkgver
  python -m pytest
}

package() {
  cd asteval-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
