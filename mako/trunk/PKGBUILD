# Maintainer: Ainola <ainola@archlinux.org>
# Contributor: Drew DeVault

pkgname=mako
pkgver=1.3
pkgrel=1
license=('MIT')
pkgdesc='Lightweight notification daemon for Wayland'
makedepends=("meson" "scdoc" "wayland-protocols")
depends=(
	"gdk-pixbuf2"
	"pango"
	"systemd-libs"
	"wayland"
)
arch=("x86_64")
url='https://mako-project.org'
source=("$pkgname-$pkgver.tar.gz::https://github.com/emersion/$pkgname/archive/v$pkgver.tar.gz")
sha256sums=('a048bf81907014c8f133a7726e771d0026fa9bbbf81ee7aa6852db3ac7e7806f')


build() {
	meson --prefix /usr \
		--buildtype=plain \
		-Dzsh-completions=true \
		"$pkgname-$pkgver" build
	ninja -C build
}

package() {
	DESTDIR="$pkgdir" ninja -C build install
	install -Dm644 "$pkgname-$pkgver/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
