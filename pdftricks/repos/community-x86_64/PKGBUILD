# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=pdftricks
pkgver=0.2.5
pkgrel=1
pkgdesc="Simple, efficient application for small manipulations in PDF files using Ghostscript"
arch=(x86_64)
url="https://github.com/muriloventuroso/pdftricks"
license=(GPL3)
depends=(ghostscript granite imagemagick)
makedepends=(meson vala)
source=("$pkgname-$pkgver.tar.gz::https://github.com/muriloventuroso/$pkgname/archive/$pkgver.tar.gz"
        0001-Fix-build-with-vala-0.44.patch
        0001-Fix-message-dialog-about-ImageMagick-Policies.patch
        0001-Fix-non-ascii-error.patch
        0001-Remove-incompatible-option.patch)
sha256sums=('8b97061c82a85e17025c43948b04221cca12929c51d6c7f4f6b25bebec2aa939'
            '00f8bba01f73aba6b1903db735bfce9ad6124f92376cbd089a4499df7909e158'
            'edf96e59bf1a4a7d53d669ab5cf2727f8aa73246f70d6b342e061e18565067e2'
            '977de8d12760ca942877c7f67e99d05afe13b80b3d0b5bd532b66651fb64175e'
            '4a1643e639a30de9a5dabb61b9e3c5f1dc34277802ee32feb5bbe0ab0a2479ec')

prepare() {
  cd $pkgname-$pkgver

  # https://github.com/muriloventuroso/pdftricks/pull/31
  patch -Np1 -i ../0001-Fix-build-with-vala-0.44.patch

  # https://github.com/muriloventuroso/pdftricks/pull/32
  patch -Np1 -i ../0001-Fix-message-dialog-about-ImageMagick-Policies.patch

  # https://github.com/muriloventuroso/pdftricks/pull/33
  patch -Np1 -i ../0001-Fix-non-ascii-error.patch

  # https://github.com/muriloventuroso/pdftricks/pull/34
  patch -Np1 -i ../0001-Remove-incompatible-option.patch

  # Use standard icon name
  sed -i 's/process-completed/gtk-apply/' src/Widgets/*.vala
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

