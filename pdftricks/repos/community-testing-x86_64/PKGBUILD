# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=pdftricks
pkgver=0.2.7
pkgrel=2
pkgdesc="Simple, efficient application for small manipulations in PDF files using Ghostscript"
arch=(x86_64)
url="https://github.com/muriloventuroso/pdftricks"
license=(GPL3)
depends=(ghostscript granite imagemagick)
makedepends=(meson vala)
source=("$pkgname-$pkgver.tar.gz::https://github.com/muriloventuroso/$pkgname/archive/$pkgver.tar.gz")
sha256sums=('501fafa0405e6e96560c2db3e43b4edb875129d74b9b20363af8d2b12a13e8cd')

prepare() {
  cd $pkgname-$pkgver

  # Use standard icon name
  sed -i 's/process-completed/gtk-apply/' src/Widgets/*.vala
}

build() {
  arch-meson $pkgname-$pkgver build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}

