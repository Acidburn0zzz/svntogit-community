# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Spyros Stathopoulos <foucault.online@gmail.com>
# Contributor: richli <rich at dranek dot com>
# Contributor: rememberthemer <rememberthemer@_GMAIL_DOT_COM_>

_pkg=netCDF4
pkgbase=python-${_pkg,,}
pkgname=("python-${_pkg,,}" "python2-${_pkg,,}")
pkgver=1.4.0
pkgrel=1
pkgdesc="Python/NumPy interface to the netCDF C library"
arch=('x86_64')
url="https://unidata.github.io/${_pkg,,}-python"
license=('MIT')
makedepends=('cython' 'python-setuptools' 'cython2' 'python2-setuptools' 'python-numpy' 'python-cftime' 'python2-numpy' 'python2-cftime' 'netcdf')
source=("https://pypi.io/packages/source/n/${_pkg}/${_pkg}-${pkgver}.tar.gz"
        '802.patch')
sha256sums=('a6c0b46f410f101c09d60b8cc460aafec06732f0130c6cb2730717bcc605b388'
            '17e3442e75d75fcba5fabe387f5dc5ef1cc2e7a4de25d96a2fcec699ce628289')

prepare() {
    cd ${_pkg}-${pkgver}
    patch -p1 -i ../802.patch
    cd ..
    cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py build

    cd ../${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py build
}

check() {
    cd ${_pkg}-${pkgver}/test
    PYTHONPATH="../build/lib.linux-${CARCH}-3.6" python -B ./run_all.py

    cd ../../${_pkg}-${pkgver}-py2/test
    PYTHONPATH="../build/lib.linux-${CARCH}-2.7" python2 -B ./run_all.py
}

package_python-netcdf4() {
    depends=('python-numpy' 'python-cftime' 'netcdf')

    cd ${_pkg}-${pkgver}
    USE_NCCONFIG=1 python setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

package_python2-netcdf4() {
    depends=('python2-numpy' 'python2-cftime' 'netcdf')

    cd ${_pkg}-${pkgver}-py2
    USE_NCCONFIG=1 python2 setup.py install --prefix=/usr --root="${pkgdir}" --skip-build --optimize=2

    for binary in "${pkgdir}"/usr/bin/* ;
        do mv ${binary}{,-py2} ;
    done
    install -Dm644 docs/netCDF4/* -t "${pkgdir}"/usr/share/doc/${pkgname}/docs/netCDF4
    install -Dm644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}
