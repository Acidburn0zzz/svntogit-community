# $Id$
# Contributor: Giorgio Lando <lando at imap dot cc>
# Contributor: Sergej Pupykin
# Contributor: Thomas Bächler
# Maintainer: Thorsten Töpper <atsutane-tu@freethoughts.de>

pkgname=fcron
pkgver=3.0.6
pkgrel=7
pkgdesc="feature-rich cron implementation"
arch=(i686 x86_64)
url="http://fcron.free.fr"
license=('GPL')
depends=('pam')
makedepends=('smtp-server')
optdepends=('smtp-server: to receive mails from cron jobs')
provides=('cron')
conflicts=('dcron')
backup=(etc/fcron/fcron.conf etc/fcron/fcron.allow etc/fcron/fcron.deny \
        var/spool/fcron/systab var/spool/fcron/systab.orig)
options=('emptydirs' '!makeflags')
source=(http://fcron.free.fr/archives/$pkgname-$pkgver.src.tar.gz fcron.rc \
        systab systab.orig run-cron fcron.service)
md5sums=('69ebcb41921e2a282f41ebecb3a27053'
         'e0c3f0bdc3c98fbbe46eff19001c18f2'
         '938722c6654ef7b07f4aa10001905ba1'
         'bfb7daa22ebe22b9917e455c1ca4a382'
         '5ff0cdcb9ec99778938ac6ef26800327'
         'ab589cc6813ec32b5e96bf05c2b51c4c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  ./configure --prefix=/usr \
              --sysconfdir=/etc/fcron \
              --with-answer-all=no \
              --with-boot-install=no \
              --with-username=root \
              --with-groupname=root \
              --datarootdir=/usr/share \
	            --datadir=/usr/share \
              --with-docdir=/usr/share/doc \
              --localstatedir=/var \
	            --with-editor=/usr/bin/vi \
              --with-sendmail=/usr/sbin/sendmail
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir/" install
  install -D -m755 "$srcdir/fcron.rc" "$pkgdir/etc/rc.d/fcron"
  install -D -m644 "$srcdir/$pkgname-$pkgver/files/fcron.pam" "$pkgdir/etc/pam.d/fcron"
  
  install -D -m644 "$srcdir/$pkgname-$pkgver/files/fcrontab.pam" "$pkgdir/etc/pam.d/fcrontab"
  # Install default fcrontab so that fcron can completely replace dcron
  
  install -D -m600 "$srcdir/systab" "$pkgdir/var/spool/fcron/systab"
  # In order to preserve the systab crontab in any case it is better to have
  # it in non-binary form too
  install -D -m600 "$srcdir/systab.orig" "$pkgdir/var/spool/fcron/systab.orig"

  # Add cron.* directories
  install -d -m755 "$pkgdir/etc/cron.daily"
  install -d -m755 "$pkgdir/etc/cron.hourly"
  install -d -m755 "$pkgdir/etc/cron.monthly"
  install -d -m755 "$pkgdir/etc/cron.weekly"
  
  # Install run-cron script to make fcron run without dcron
  install -D -m755 "$srcdir/run-cron" "$pkgdir/usr/sbin/run-cron"

  # Install systemd service
  install -D -m644 "$srcdir/fcron.service" \
    "$pkgdir/usr/lib/systemd/system/fcron.service"

  # avoid conflict with filesystem>=2012.06
  rmdir "$pkgdir/var/run"
}

# vim:set ts=2 sw=2 et:
