# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: xav <xav at ethertricks dot net>

pkgname=arp-scan
pkgver=1.9.5
pkgrel=2
pkgdesc="A tool that uses ARP to discover and fingerprint IP hosts on the local network"
arch=('x86_64')
url="https://github.com/royhills/arp-scan"
license=('GPL')
depends=('libpcap' 'perl-libwww')
source=(https://github.com/royhills/$pkgname/archive/$pkgver/$pkgname-$pkgver.tar.gz
        libpcap-timeout.patch::https://github.com/royhills/arp-scan/commit/8513a181c10be83afdc46f49e9abe97212189d4a.patch)
sha256sums=('aa9498af84158a315b7e0ea6c2cddfa746660ca3987cbe7e32c0c90f5382d9d2'
            '0ad040df57810bca6f8c016e7b83fdfe05f5aae683574ad7d73929746701c911')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p1 -i "$srcdir"/libpcap-timeout.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	autoreconf --install
	./configure --prefix=/usr --mandir=/usr/share/man
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="${pkgdir}" install
}
