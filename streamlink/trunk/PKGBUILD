# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>

pkgname=streamlink
pkgver=0.13.0
pkgrel=1
pkgdesc='CLI program that launches streams from various streaming services in a custom video player (livestreamer fork)'
arch=('any')
url='https://streamlink.github.io/'
license=('BSD')
depends=("python-"{crypto,isodate,pycountry,requests,pysocks,websocket-client} 'rtmpdump')
checkdepends=("python-"{mock,pytest})
makedepends=("python-"{recommonmark,setuptools,sphinx})
optdepends=('ffmpeg: Required to play streams that are made up of separate audio and video streams, eg. YouTube 1080p+')
source=("https://github.com/${pkgname}/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz"{,.asc})
validpgpkeys=('E2B794C7C2C37162E5E2A097E3DB9E282E390FA0') # Streamlink Signing Key <streamlink@protonmail.com>
sha256sums=('4d91dbc8233e8737a9dcb629ac471373e903116b3a35c882205c95bdb14eac29'
            'SKIP')
sha512sums=('9744eb16aaae395daff6e626c435d8984a4c8c47091bd068a83d216d2e79535d72b1fd61b8befec1ce379ef85174722ffe837fabf6facb9ed2a608f32987eeff'
            'SKIP')

build() {
  cd "$pkgname-$pkgver"
  export STREAMLINK_USE_PYCOUNTRY="true"
  export STREAMLINK_USE_PYCRYPTO="true"
  python setup.py build_sphinx -b man
}

check() {
  cd "$pkgname-$pkgver"
  python setup.py test || warning "Tests failed"
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 build/sphinx/man/streamlink.1 \
    "$pkgdir/usr/share/man/man1/streamlink.1"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
