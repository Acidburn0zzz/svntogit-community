# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

# TODO: Try building with the latest ffmpeg sources

pkgname=gimp-gap
pkgver=2.6.0
pkgrel=4
pkgdesc='Plugins for editing and creating animations with The Gimp'
arch=('x86_64' 'i686')
license=('GPL')
url='about:blank'
depends=('gimp' 'xvidcore' 'bash' 'libjpeg')
makedepends=('intltool')
source=("ftp://ftp.gimp.org/pub/gimp/plug-ins/v2.6/gap/$pkgname-$pkgver.tar.bz2"
        'dummy.makefile')
sha256sums=('1483f5e17ed5f9107c1702fcac3b7a66e6834061560ccb449fc4eaca7a3c2cca'
            '28ae682fe46cea74f9face0b6820b59f23e8bd3a2ac053e38c0e7e62613a4177')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # -lm must be one of the last libraries for the compilation to succeed
  sed -i 's/-lm"/"/' configure

  # libmpeg3 is not in [extra], only libmpeg2
  # ignore the included ffmpeg library
  ./configure \
    --prefix=/usr \
    --exec-prefix=/usr \
    --sbindir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --libdir=/usr/lib \
    --includedir=/usr/include \
    --datadir=/usr/share \
    --disable-libmpeg3 \
    --disable-libavformat

  # Create a dummy makefile for the external libraries
  cat ../dummy.makefile > extern_libs/Makefile

  # Add -lm as the last library to be linked and start compiling
  make LIBS+=' -lm'
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
