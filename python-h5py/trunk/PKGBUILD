# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

_pkg=h5py
pkgbase=python-$_pkg
pkgname=(python-$_pkg python2-$_pkg)
pkgver=2.8.0
pkgrel=4
pkgdesc="General-purpose Python bindings for the HDF5 library"
url="http://www.h5py.org/"
arch=('x86_64')
license=('BSD')
makedepends=('hdf5' 'cython' 'cython2' 'python-numpy' 'python2-numpy' 'python-six' 'python2-six'
             'python-pkgconfig' 'python2-pkgconfig')
checkdepends=('python2-unittest2')
source=("https://files.pythonhosted.org/packages/source/h/$_pkg/$_pkg-$pkgver.tar.gz"{,.asc})
sha256sums=('e626c65a8587921ebc7fb8d31a49addfdd0b9a9aa96315ea484c09803337b955'
            'SKIP')
validpgpkeys=('AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7') # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>

prepare() {
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" $_pkg-$pkgver/setup_build.py

  cp -a $_pkg-$pkgver{,-py2}
}

build() {
  cd $_pkg-$pkgver
  python setup.py build

  cd ../$_pkg-$pkgver-py2
  python2 setup.py build
}

check() {
  cd $_pkg-$pkgver
  python setup.py test || warning "Tests failed"

  cd ../$_pkg-$pkgver-py2
  python2 setup.py test || warning "Tests failed"
}

package_python-h5py() {
  depends=('hdf5' 'python-numpy' 'python-six')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-h5py() {
  depends=('hdf5' 'python2-numpy' 'python2-six')
  conflicts=('hdf5-openmpi')

  cd $_pkg-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  install -D licenses/license.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
