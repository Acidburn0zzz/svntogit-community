# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Rich Li <rich@dranek.com>
# Contributor: Sebastien Binet <binet@lblbox>

_pkg=h5py
pkgbase=python-${_pkg}
pkgname=(python-${_pkg} python2-${_pkg})
pkgver=2.10.0
pkgrel=2
pkgdesc="General-purpose Python bindings for the HDF5 library"
arch=(x86_64)
url="https://www.h5py.org/"
license=(BSD)
depends=(hdf5)
makedepends=(cython cython2 python-numpy python2-numpy python-six python2-six
             python-pkgconfig python2-pkgconfig)
checkdepends=(python-pytest python2-pytest python2-unittest2)
conflicts=(hdf5-openmpi)
source=(https://files.pythonhosted.org/packages/source/h/${_pkg}/${_pkg}-${pkgver}.tar.gz)
sha256sums=('84412798925dc870ffd7107f045d7659e60f5d46d1c70c700375248bf6bf512d')
validpgpkeys=(AC47F71DB275ECD0B3DA46E857FA4540DD4EFCF7  # Thomas A Caswell (Brookhaven National Lab) <tcaswell@bnl.gov>
              96B7334D7610EE3E68AFFE589E027116943D6A8B) # Thomas A Caswell <tcaswell@bnl.gov> (new key)
# See https://github.com/h5py/h5py/issues/1299 about lack of GPG sigs for recent releases

prepare() {
  # Remove RPATH
  sed -i "s/settings\\['runtime_library_dirs'\\] = settings\\['library_dirs'\\]/pass/" ${_pkg}-${pkgver}/setup_build.py
  cp -a ${_pkg}-${pkgver}{,-py2}
}

build() {
  cd ${_pkg}-${pkgver}
  python setup.py build

  cd ../${_pkg}-${pkgver}-py2
  python2 setup.py build
}

check() {
  cd ${_pkg}-${pkgver}
  python setup.py test

  cd ../${_pkg}-${pkgver}-py2
  # https://github.com/h5py/h5py/issues/1435
  python2 setup.py test || warning "Tests failed"
}

package_python-h5py() {
  depends+=(python-numpy python-six)

  cd ${_pkg}-${pkgver}
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -Dm644 licenses/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}

package_python2-h5py() {
  depends+=(python2-numpy python2-six)

  cd ${_pkg}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -Dm644 licenses/license.txt -t "${pkgdir}"/usr/share/licenses/${pkgname}/
}
