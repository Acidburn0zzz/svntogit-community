# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=python-ipywidgets
_pipname=ipywidgets
pkgver=7.7.0
pkgrel=2
pkgdesc="IPython widgets for the Jupyter Notebook"
arch=('any')
url="https://github.com/ipython/ipywidgets"
license=('BSD')
depends=('python' 'jupyter-widgetsnbextension')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=(https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz)
md5sums=('61deb9024c3de1848812b97c2560d0cf')

build() {
  cd ipywidgets-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd ipywidgets-$pkgver
  pytest -vv
}

package() {
  cd ipywidgets-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
