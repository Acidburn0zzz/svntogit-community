# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=phonegap
pkgver=7.0.0
pkgrel=1
pkgdesc="PhoneGap command-line interface and node.js library"
arch=('i686' 'x86_64')
url="https://github.com/phonegap/phonegap-cli"
license=('Apache')
depends=('npm' 'acorn' 'semver' 'cordova' 'browserify')
makedepends=('python2')
install=${pkgname}.install
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('9bf95209a66c07f2580f68f415f4fad8549aeb9c8cc16c763954e87b4ff156685bd2db59cf68bacf0fa4d7a55f9942bc292989a6f007cc7920c9a9604148cb5c')

package() {
  PYTHON=python2 npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in acorn semver cordova npm browserify; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/syntax-error/node_modules
  for dep in acorn; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/cordova-lib/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/npm-package-arg/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/normalize-package-data/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/init-package-json/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done

  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules/semver-diff/node_modules
  for dep in semver; do
    rm -r $dep;
    npm link $dep;
  done
}

# vim:set ts=2 sw=2 et:
