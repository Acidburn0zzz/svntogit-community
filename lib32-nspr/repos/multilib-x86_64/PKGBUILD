# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

pkgname=lib32-nspr
pkgver=4.22
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64)
license=(MPL GPL)
depends=(lib32-glibc nspr)
makedepends=(zip)
source=(https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/nspr-${pkgver}.tar.gz)
sha1sums=('69bd044d4a258a9c1cb1062a182fb8a30f0ad641')
sha256sums=('c9e4b6cc24856ec93202fe13704b38b38ba219f0f2aeac93090ce2b6c696d430')

build() {
  cd nspr-$pkgver
  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug
  make
}

package() {
  cd nspr-$pkgver
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
        "$pkgdir"/usr/include/nspr/md
  rm -rf "$pkgdir"/usr/{bin,share,include}
}
