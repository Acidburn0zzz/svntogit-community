#$Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: kfgz <kfgz at interia pl>
# Contributor: Ionut Biru <ibiru at archlinux dot org>

_pkgbasename=nspr
pkgname=lib32-${_pkgbasename}
pkgver=4.14
pkgrel=1
pkgdesc="Netscape Portable Runtime (32-bit)"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSPR"
arch=(x86_64)
license=(MPL GPL)
depends=(lib32-glibc ${_pkgbasename})
makedepends=(zip gcc-multilib)
options=(!emptydirs)
source=(https://ftp.mozilla.org/pub/mozilla.org/nspr/releases/v${pkgver}/src/${_pkgbasename}-${pkgver}.tar.gz)
sha1sums=('4c9ffda940882229104090f8fc8539f6412e1ff7')
sha256sums=('64fc18826257403a9132240aa3c45193d577a84b08e96f7e7770a97c074d17d5')

build() {
  cd ${_pkgbasename}-$pkgver
  ./nspr/configure \
      --prefix=/usr \
      --libdir=/usr/lib32 \
      --includedir=/usr/include/nspr \
      --enable-optimize \
      --disable-debug
  make
}

package() {
  cd ${_pkgbasename}-$pkgver
  make DESTDIR="$pkgdir" install
  ln -s nspr.pc "$pkgdir/usr/lib32/pkgconfig/mozilla-nspr.pc"
  rm -r "$pkgdir"/usr/bin/{compile-et.pl,prerr.properties} \
        "$pkgdir"/usr/include/nspr/md
  rm -rf "$pkgdir"/usr/{bin,share,include}
}
