# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=monero-gui
pkgver=0.15.0.2
pkgrel=3
pkgdesc="Official QT GUI wallet for Monero, a private, secure, untraceable peer-to-peer currency"
license=('BSD')
arch=('x86_64')
url="https://getmonero.org/"
depends=('boost-libs' 'libunwind' 'openssl' 'readline' 'zeromq' 'pcsclite' 'hidapi' 'unbound'
    'qt5-base' 'qt5-declarative' 'qt5-quickcontrols' 'qt5-svg')
makedepends=('git' 'cmake' 'boost' 'qt5-tools')
source=(
    "${pkgname}"::"git+https://github.com/monero-project/monero-gui#tag=v${pkgver}"
    "git+https://github.com/monero-project/monero.git"
    "no-avx2.patch"
)
sha512sums=('SKIP'
            'SKIP'
            'dab89406ce77cd7e96adfe81156d262aa8457220b73dcfe86be50bf0322230319efb73517954e126ef77b8c90d68fb706d924ce432f9c6aabac3f94cd55ea553')

prepare() {
  cd "${pkgname}"
  git submodule init
  git config submodule.monero.url "$srcdir/monero"
  git submodule update
  patch --forward --strip=1 --input="${srcdir}/no-avx2.patch"
}

build() {
  cd "${pkgname}"
  QT_SELECT=5 ./build.sh
}

package() {
  cd "${pkgname}"
  install -Dm755 build/release/bin/monero-wallet-gui -t "${pkgdir}/usr/bin"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
