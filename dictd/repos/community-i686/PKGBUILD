# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: SmackleFunky <smacklefunky@optusnet.com.au>
# Contributor: Enrico Morelli <morelli@cerm.unifi.it>

pkgname=dictd
pkgver=1.11.2
pkgrel=2
pkgdesc="Online dictionary client and server"
url="http://sourceforge.net/projects/dict/"
license=("GPL")
arch=(i686 x86_64)
depends=('zlib' 'libmaa')
makedepends=('flex')
backup=(etc/dict/dictd.conf
	etc/dict/dict.conf
	etc/dict/site.info
	etc/conf.d/dictd
	etc/xinetd.d/dictd)
source=(http://downloads.sourceforge.net/project/dict/dictd/dictd-$pkgver/dictd-$pkgver.tar.gz
	dictd
	dict.conf
	dictd.conf
	dictd.confd
	site.info
	dictd.xinetd)
md5sums=('7008ec3bb0001c302ce751580f9d2ea4'
         '6542df4fc585773fc03f1a088730126b'
         'fb72f7d8c55f84b1b37bbc8d33a8e55a'
         '64cd6ffdb40fb3367224f91ac44926ac'
         '838887fee38ae4cebd652d399b1f2266'
         'b8cabf913876e544d919c7f20ff8db9e'
         'b7dbc9529df13ff895dae2b4abd080d6')

build() {
  cd $srcdir/$pkgname-$pkgver
  ./configure --enable-dictorg --prefix=/usr --sysconfdir=/etc/dict
  make || return 1
  make DESTDIR=$pkgdir install && \
  install -D -m 0755 $srcdir/dictd $pkgdir/etc/rc.d/dictd && \
  install -D -m 0644 $srcdir/dictd.xinetd $pkgdir/etc/xinetd.d/dictd && \
  install -D -m 0644 $srcdir/dictd.conf $pkgdir/etc/dict/dictd.conf && \
  install -D -m 0644 $srcdir/dict.conf $pkgdir/etc/dict/dict.conf && \
  install -D -m 0644 $srcdir/site.info $pkgdir/etc/dict/site.info && \
  install -D -m 0644 $srcdir/dictd.confd $pkgdir/etc/conf.d/dictd
}
