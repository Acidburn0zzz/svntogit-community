# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: ThecaTTony <thecattony at gmx dot com>
# Contributor: <giacomogiorgianni at gmail dot com>

pkgname=quiterss
pkgver=0.18.12
pkgrel=2
pkgdesc='Fast and light RSS/Atom feed reader written in Qt/С++'
arch=(x86_64)
url='https://quiterss.org/'
license=(GPL3)
depends=(desktop-file-utils gst-plugins-good qt5-multimedia qt5-webkit)
makedepends=(qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/QuiteRSS/$pkgname/archive/$pkgver.tar.gz
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('120de0d640e2c417d664872b8b8881c85c3e76aec9b76d235f0e12e5ee78f01f'
            '5cae0fb12127223a5a1ce0424e44cfcb03949bfeedaa7cf2334a88f06f6c9a07')

prepare() {
  cd $pkgname-$pkgver

  # Fix AppStream metadata validation
  # https://github.com/QuiteRSS/quiterss/pull/1130
  patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch
}

build() {
  cd $pkgname-$pkgver
  qmake-qt5 CONFIG+=release PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 quiterss.appdata.xml "$pkgdir/usr/share/metainfo/quiterss.appdata.xml"
  rm -r "$pkgdir/usr/share/pixmaps/"
}

