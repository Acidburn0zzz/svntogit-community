# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: willemw <willemw12@gmail.com>
# Contributor: ThecaTTony <thecattony at gmx dot com>
# Contributor: <giacomogiorgianni at gmail dot com>

pkgname=quiterss
pkgver=0.19.3
pkgrel=1
pkgdesc='Fast and light RSS/Atom feed reader written in Qt/С++'
arch=(x86_64)
url='https://quiterss.org/'
license=(GPL3)
depends=(desktop-file-utils gst-plugins-good qt5-multimedia qt5-webkit)
makedepends=(qt5-tools)
source=($pkgname-$pkgver.tar.gz::https://github.com/QuiteRSS/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('5177a15d5799536e6554022d7765eeb19f66976ab88844457cbe2df66a6d784e')

build() {
  cd $pkgname-$pkgver
  qmake-qt5 CONFIG+=release PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver
  make INSTALL_ROOT="$pkgdir" install
  install -Dm644 quiterss.appdata.xml "$pkgdir/usr/share/metainfo/quiterss.appdata.xml"
  rm -r "$pkgdir/usr/share/pixmaps/"
}

