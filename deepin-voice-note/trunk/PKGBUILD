# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.8.7
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'vlc')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('7da8c56ce01fbb1d1fbfc33b5f1af5fb7aeca9da7e5f0f601b9dab6a18b119501b0616309260ee42a042c08def39871d5f167b8a2c704ff1ae1391551a6ab7a5')

prepare() {
  cd deepin-voice-note-$pkgver
  # Upstream likes to refactor code while ignoring pull requests
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
  # Utterly broken code that treats all audio/mpeg as mp3 and they refused to fix.
  # They really need to learn what the f**k is a codec and a container and they are *not* the same.
  sed -e 's|codec.startsWith(m_defaultAudioFmt)|codec.startsWith("audio/mpeg, mpegversion=(int)1, layer=(int)3")|' \
      -i src/common/vnoteaudiomanager.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
