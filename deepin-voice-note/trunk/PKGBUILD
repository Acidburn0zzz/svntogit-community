# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.7.13
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('a441f263f7c4a2222471028d7bb679aa5904e2a167871fac74a906f914e67421f2c096b874ff1ea740ecc2298d96c666bdfa6f63b5b6bbf7137d671e621e87a3')

prepare() {
  cd deepin-voice-note-$pkgver
  # Upstream likes to refactor code while ignoring pull requests
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
  # Utterly broken code that treats all audio/mpeg as mp3 and they refused to fix.
  # They really need to learn what the f**k is a codec and a container and they are *not* the same.
  sed -e 's|codec.startsWith(m_defaultAudioFmt)|codec.startsWith("audio/mpeg, mpegversion=(int)1, layer=(int)3")|' \
      -i src/common/vnoteaudiomanager.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
