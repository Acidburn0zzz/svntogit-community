# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.7.9
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('706cf34bdfdca667aa6e381403a044ef587acf925e095aa5ec5bf59d5632487391af32c33083fd3b4a87cfbbf7cf32931a566095ade8c6812d68ced2b5756d10')

prepare() {
  cd deepin-voice-note-$pkgver
  # Upstream likes to refactor code while ignoring pull requests
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
  # Utterly broken code that treats all audio/mpeg as mp3 and they refused to fix.
  # They really need to learn what the f**k is a codec and a container and they are *not* the same.
  sed -e 's|codec.startsWith(m_defaultAudioFmt)|codec.startsWith("audio/mpeg, mpegversion=(int)1, layer=(int)3")|' \
      -i src/common/vnoteaudiomanager.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-voice-note-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
