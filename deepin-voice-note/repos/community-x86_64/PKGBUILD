# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.8.2
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration')
makedepends=('cmake' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('0340e48bf1ed73367099b60abfade97d12a5eb6b5e423b9b4ce7a50286ee78db81a8bf5237e437d4714a230bc11eae6c8375a8b2039c289a8974fa60b33d4e84')

prepare() {
  cd deepin-voice-note-$pkgver
  # Upstream likes to refactor code while ignoring pull requests
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
  # Utterly broken code that treats all audio/mpeg as mp3 and they refused to fix.
  # They really need to learn what the f**k is a codec and a container and they are *not* the same.
  sed -e 's|codec.startsWith(m_defaultAudioFmt)|codec.startsWith("audio/mpeg, mpegversion=(int)1, layer=(int)3")|' \
      -i src/common/vnoteaudiomanager.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
