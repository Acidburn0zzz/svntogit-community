# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.7.8
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
depends=('deepin-qt5integration')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('09987530260331313f4104b1c4e7d7d042dc52977e444c740c8ab221d04682363df7298db71fd64207ad64d7f4e479234e4ae2c1f030a156a8ba0dda71922dc9')

prepare() {
  cd deepin-voice-note-$pkgver
  sed -i '1i#include <QMouseEvent>' src/views/leftfolderlist.cpp
  sed -i '1i#include <QObject>' src/Controllers/foldercontroller.cpp
  sed -i '1i#include <QPainterPath>' src/views/waveform.cpp src/views/waveformreplay.cpp
  sed -i '228s|audio/mpeg|audio/mpeg, mpegversion=(int)1, layer=(int)3|' src/views/recordPage.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-voice-note-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
