# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=deepin-voice-note
pkgver=5.9.6
pkgrel=1
pkgdesc='A lightweight memo tool to make text notes and voice recordings'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-voice-note"
license=('GPL3')
replaces=('deepin-voice-recorder')
depends=('deepin-qt5integration' 'vlc')
makedepends=('cmake' 'gmock' 'gtest' 'ninja' 'qt5-tools')
groups=('deepin-extra')
source=("https://github.com/linuxdeepin/deepin-voice-note/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('bae61dc5442643e8572592c27bfeb272e35cd1398d7c73c0660e61021613b4f8fddfeb570e4a02d18049a3b6df4c720cd194e320e2b993faae172073011eb7d9')

prepare() {
  cd deepin-voice-note-$pkgver
  sed -i '1i#include <QPainterPath>' src/views/middleviewdelegate.cpp src/views/leftviewdelegate.cpp
}

build() {
  cd deepin-voice-note-$pkgver
  cmake -GNinja .
  ninja
}

check() {
  cd deepin-voice-note-$pkgver
  ninja test || :
}

package() {
  cd deepin-voice-note-$pkgver
  DESTDIR="$pkgdir" ninja install
}
