# Maintainer: Bruno Pagani <archange@archlinux.org> 

pkgname=nsjail
pkgver=2.7
pkgrel=1
pkgdesc="A light-weight process isolation tool"
arch=('x86_64')
url="http://nsjail.com"
license=('Apache')
depends=('protobuf' 'libnl')
makedepends=('git')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/google/nsjail/archive/${pkgver}.tar.gz"
        'git+https://github.com/google/kafel.git#commit=409ccb2113e1f82f497d1dcfb5ae18376ff7056c')
sha256sums=('28afa89c7dbed94528408e4f11d724622d24ea582739b254fa2a739320e87481'
            'SKIP')

prepare() {
    mv kafel ${pkgname}-${pkgver}
    # See https://github.com/google/nsjail/issues/83
    sed -i 's/-Wno-format-nonliteral/-Wno-format-nonliteral -Wno-format-truncation/' ${pkgname}-${pkgver}/Makefile
}

build() {
    cd ${pkgname}-${pkgver}
    make
}

package() {
    cd ${pkgname}-${pkgver}
    install -Dm755 nsjail -t "${pkgdir}"/usr/bin/ 
}
