# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Alasdair Haswell <ali at arhaswell dot co dot uk>

pkgname=rng-tools
pkgver=6.3.1
pkgrel=1
pkgdesc="Random number generator related utilities"
arch=('x86_64')
url="https://github.com/nhorman/rng-tools"
license=('GPL')
depends=('libgcrypt' 'curl' 'libxml2' 'sysfsutils')
backup=(etc/conf.d/rngd)
source=(https://github.com/nhorman/rng-tools/archive/v$pkgver/$pkgname-$pkgver.tar.gz
        rngd.conf
        rngd.service)
sha256sums=('9ac402ce8f0c0b3c25e6bf47979d25b17f2dbed11ae19a7c103aa151d9e6e712'
            'f4fd197dd682cdab07ddeabe8d3b73b1bc2e1cf322237be238a26b49a7dcb047'
            'd3e38ac1460e3072792def73281faf5f59dffe9ac43b00916252cc69f7c7d1b5')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	mv README{.md,}

	libtoolize --force
	aclocal
	autoheader
	automake --force-missing --add-missing
	autoconf
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure --prefix=/usr --sbindir=/usr/bin
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
	install -Dm0644 "$srcdir"/rngd.service "$pkgdir"/usr/lib/systemd/system/rngd.service
	install -Dm0644 "$srcdir"/rngd.conf "$pkgdir"/etc/conf.d/rngd
}
