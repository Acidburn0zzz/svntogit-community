# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=chell-quickcheck
pkgname=haskell-chell-quickcheck
pkgver=0.2.5.1
pkgrel=16
pkgdesc="QuickCheck support for the Chell testing library"
url="https://john-millikin.com/software/chell/"
license=('MIT')
arch=('x86_64')
depends=('ghc-libs' 'haskell-chell' 'haskell-quickcheck' 'haskell-random')
makedepends=('ghc')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz
        quickcheck-2.12.patch)
sha512sums=('4879c084731112e0e17e6d82510850c119c7e5e5cde90c03ecce9ea470f94429ddd040b9346a5aa268b0ea41c5656025cc2dacfe533d370efd7162473a1ebc6e'
            '47d6f0e7a51a186fe1a0085a1bdcb11402f063ffe48a71282f40b8f69add78c64821c79b4b5c1e0f97feb0d8154745e7dcb4572761b41aca89e399a844bbfafe')

prepare() {
    cd $_hkgname-$pkgver
    patch -p2 -i ../quickcheck-2.12.patch
}

build() {
    cd $_hkgname-$pkgver
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "license.txt" "${pkgdir}/usr/share/licenses/${pkgname}/license.txt"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/license.txt"
}
