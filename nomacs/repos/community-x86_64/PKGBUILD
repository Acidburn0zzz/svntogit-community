# $Id$
# Maintainer: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=3.8.0
pkgrel=1
pkgdesc="A Qt image viewer"
arch=(i686 x86_64)
url="https://www.nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv')
makedepends=('cmake' 'qt5-tools')
source=("$pkgname-$pkgver::https://github.com/nomacs/nomacs/archive/$pkgver.tar.gz"
        nomacs-plugins-$pkgver.tar.gz::"https://github.com/nomacs/nomacs-plugins/archive/$pkgver.tar.gz"
        nomacs-no-coverage.patch)
sha256sums=('f43ad1507f2890db2fbaa0cb110de3ac5efe754cf2e7a053eaabfed4d5e06845'
            '5a700b27990fb72ae8582e69143389d0cf9c9313f9b361348b9bb8339b3dabdc'
            '7e825b93119055cae88960cb3bd1fbc2efc43f48d996eef01ee2ae3b569d3097')

prepare() {
  mv nomacs-plugins-$pkgver $pkgname-$pkgver/ImageLounge/plugins
  cd $pkgname-$pkgver
  mkdir -p build
  patch -p1 -i ../nomacs-no-coverage.patch # Disable tests
}

build() {
  cd $pkgname-$pkgver/build
  cmake ../ImageLounge -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build
  make DESTDIR="$pkgdir" install
}
