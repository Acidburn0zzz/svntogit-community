# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: speps <speps at aur dot archlinux dot org>

pkgname=nomacs
pkgver=3.8.1
pkgrel=1
pkgdesc="A Qt image viewer"
arch=('x86_64')
url="https://nomacs.org/"
license=('GPL3')
depends=('qt5-svg' 'exiv2' 'libraw' 'opencv')
optdepends=('qt5-imageformats: support additional image formats')
makedepends=('cmake' 'qt5-tools')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-plugins-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}-plugins/archive/${pkgver}.tar.gz")
sha512sums=('ff27570bc15096d0e331ad2c871b2f504a414927b8e64166668dedbbbc41f8cd031d9fb142c89c22cff76bf79156f93decf1038e30eecfc3de3e328a325f0134'
            '86bc4db30b0815fd2efa272b14d3917f0e88fa95284f0afb55acca87b77b3db7668238fb3023cc9b21fe8c87cad72319d45d48633a646bf5b6acb80a6537b8c8')

prepare() {
  cd "${pkgname}-${pkgver}"
  # move plugins
  mv "${srcdir}/${pkgname}-plugins-${pkgver}" "ImageLounge/plugins"
}

build() {
  cd "${pkgname}-${pkgver}"
  cmake ./ImageLounge -DCMAKE_INSTALL_PREFIX=/usr \
                      -DENABLE_TRANSLATIONS=true
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
