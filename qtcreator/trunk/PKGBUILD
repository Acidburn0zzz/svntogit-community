# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname=qtcreator
pkgver=2.0.1
pkgrel=4
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/developer-tools'
license=('LGPL')
depends=('qt')
optdepends=('qt-doc: for the integrated Qt documentation'
            'gdb: for the debugger'
            'openssh-askpass: for ssh support'
	    'git: for git support'
	    'mercurial: for mercurial support')
options=('docs')
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${pkgver}-src.zip"
        'qtcreator.desktop')
md5sums=('7b670968ec80d2d413ff22605a31a77d'
         '2c3ffbd66845b37be9804f2966815a10')
build() {

  export QTCREATOR_WITH_QML=1

  cd ${srcdir}
  mkdir build
  cd build

  qmake ${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro
  make
}

package() {
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install

  install -Dm644 ${srcdir}/qtcreator.desktop \
    ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT \
    ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
