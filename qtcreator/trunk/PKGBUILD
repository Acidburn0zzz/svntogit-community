# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname="qtcreator"
pkgver=2.3.0
pkgrel=2
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/developer-tools'
license=('LGPL')
depends=('qt>=4.7.4')
makedepends=('qt-private-headers>=4.7.4' 'git')
options=('docs')
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${pkgver}-src.tar.gz"
        'qtcreator.desktop')
md5sums=('b7ee1b779f9afe9190c5c72fb207650c'
         'f1f4680b9838ff0b8f4cf11eb850d662')

build() {
  cd ${srcdir}
  [[ -d build ]] && rm -r build
  mkdir build && cd build

  qmake ${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro
  make
}

package() {
  optdepends=('qt-doc: for the integrated Qt documentation'
              'gdb: for the debugger'
              'cmake: for cmake project suppport'
              'openssh-askpass: for ssh support'
              'git: for git support'
              'mercurial: for mercurial support'
              'bzr: for bazaar support')
  
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install

  # qt already fixed this as well
  if [[ -f "$pkgdir/usr/lib/qmldesigner/libqtquickplugin.so" ]]; then
      echo "Remove this manual install as it is now unnecessary (probably qtcreator 2.3.0)"
      exit 1
  fi
  mkdir "$pkgdir/usr/lib/qmldesigner"
  cp lib/qmldesigner/*.so "$pkgdir/usr/lib/qmldesigner/"
  # remove it soon

  install -Dm644 ${srcdir}/qtcreator.desktop \
    ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT \
    ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}
