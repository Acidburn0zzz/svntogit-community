# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgname=qtcreator
pkgver=2.0.0
pkgrel=1
_qtver=4.6.3
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://trolltech.com/developer/qt-creator'
license=('LGPL')
makedepends=()
depends=('qt>=4.6.3-1')
optdepends=('qt-doc: for the integrated Qt documentation'
            'gdb: for the debugger')
options=(docs)
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${pkgver}-src.zip"
        "ftp://ftp.qt.nokia.com/qt/source/qt-everywhere-opensource-src-${_qtver}.tar.gz"
        paths.patch
        qtcreator.desktop
        qtcreator)
md5sums=('3459ed5313115fddffbe2bba5d332799'
         '5c69f16d452b0bb3d44bc3c10556c072'
         '68663f1fa9fe5bb3825b28ac02a41eed'
         '2c3ffbd66845b37be9804f2966815a10'
         '53a02595008d842ca30c828640eb8f0c')

build() {
  cd "${srcdir}/qt-everywhere-opensource-src-${_qtver}"

  touch ".qmake.cache"
  cd "${srcdir}/qt-creator-${pkgver}-src"
  # Note for maintainer: patch needs to updated!
  #patch -Np1 -i ${srcdir}/paths.patch

  export QTDIR="${srcdir}/qt-everywhere-opensource-src-${_qtver}"
  
  if [ -d ${srcdir}/build ]; then
    rm -rf ${srcdir}/build
  fi
  
  mkdir ${srcdir}/build
  cd ${srcdir}/build
  mkdir -p share/doc/qtcreator
  touch share/doc/qtcreator/qtcreator.qch

  qmake ${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro -o Makefile
  make
}

package() {
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install
  install -Dm755 ${srcdir}/qtcreator ${pkgdir}/usr/bin/qtcreator
  install -Dm644 ${srcdir}/qtcreator.desktop ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}

# vim: sw=2:ts=2 et:
