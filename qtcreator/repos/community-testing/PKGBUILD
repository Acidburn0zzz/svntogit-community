# $Id$
# Maintainer: Imanol Celaya <ornitorrincos@archlinux-es.org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor: Dan Vratil <progdan@progdansoft.com>
# Contributor: thotypous <matiasΘarchlinux-br·org>
# Contributor: delor <bartekpiech gmail com>

pkgbase=qtcreator
pkgname=('qtcreator' 'qtcreator-todo-plugin' 'qtcreator-artisticstyle-plugin')
pkgver=2.1.0
pkgrel=2
pkgdesc='Lightweight, cross-platform integrated development environment'
arch=('i686' 'x86_64')
url='http://qt.nokia.com/products/developer-tools'
license=('LGPL')
depends=('qt>=4.7.2-3')
makedepends=('git')
options=('docs')
source=("http://get.qt.nokia.com/qtcreator/qt-creator-${pkgver}-src.zip"
	"http://download.berlios.de/astyleplugin/ArtisticStylePlugin-0.6-src.zip"
	'http://gitorious.org/qt-creator-todo-plugin/qt-creator-todo-plugin/archive-tarball/master'
        'qtcreator.desktop')
md5sums=('a6e48b4347314fb2a92b77f40b4fc9a5'
	 '4dab1bb07ba5c39ae4c975a333d32ec1'
         'd774903118fade2d57cd6926567dc37e'
         '2c3ffbd66845b37be9804f2966815a10')

_gitroot_todo="git://gitorious.org/qt-creator-todo-plugin/qt-creator-todo-plugin.git"
_gitname_todo="todo-plugin"

build() {

  # Clone TODO plugin git sources
  msg "Connecting to TODO plugin GIT server...."
  
#  if [ -d $_gitname_todo ] ; then
#    cd $_gitname_todo && git pull origin
#    msg "The local files are updated"
#  else
#    git clone $_gitroot_todo $_gitname_todo
#  fi

#  cp -r $srcdir/${_gitname_todo} $srcdir/${_gitname_todo}-build

  export QTCREATOR_WITH_QML=1

  cd ${srcdir}
  if [ ! -d build ] ; then
  mkdir build
  fi
  cd build

  qmake ${srcdir}/qt-creator-${pkgver}-src/qtcreator.pro
  make
  
  # Building TODO plugin
  msg "Starting TODO plugin build...."
  cd $srcdir/qt-creator-todo-plugin-qt-creator-todo-plugin
#  cd $srcdir/${_gitname_todo}-build
  sed -i "s|QTC_BUILD_DIR = /opt/qtcreator-2.0.92|QTC_BUILD_DIR = ${srcdir}/build|g" todoplugin.pro
  sed -i "s|QTC_SOURCE_DIR = /home/vass/qt-creator-2.1.0-beta1-src|QTC_SOURCE_DIR  = $srcdir/qt-creator-${pkgver}-src/|g" todoplugin.pro
  sed -i "s|\"2.0.92\"|\"${pkgver}\"|g" todo.pluginspec
  qmake
  make
  
  msg "Starting ArtisticStyle plugin build...."
  cd $srcdir/ArtisticStylePlugin-0.6
  qmake QTC_BUILD=$srcdir/build QTC_SOURCE=$srcdir/qt-creator-${pkgver}-src -config release
  make
}

package_qtcreator() {
  optdepends=('qt-doc: for the integrated Qt documentation'
              'gdb: for the debugger'
              'openssh-askpass: for ssh support'
              'git: for git support'
              'mercurial: for mercurial support')
  
  cd ${srcdir}/build

  make INSTALL_ROOT="${pkgdir}/usr/" install

  install -Dm644 ${srcdir}/qtcreator.desktop \
    ${pkgdir}/usr/share/applications/qtcreator.desktop
  install -Dm644 ${srcdir}/qt-creator-${pkgver}-src/LGPL_EXCEPTION.TXT \
    ${pkgdir}/usr/share/licenses/qtcreator/LGPL_EXCEPTION.TXT
}

package_qtcreator-todo-plugin() {
  license=('BSD')
  depends=('qtcreator=2.1.0')
  pkgdesc='TODO plugin for QtCreator'
  url='http://gitorious.org/qt-creator-todo-plugin'
  
  cd $srcdir/qt-creator-todo-plugin-qt-creator-todo-plugin
  make INSTALL_ROOT=$pkgdir/usr/ install
  install -Dm644 LICENSE.BSD $pkgdir/usr/share/licenses/qtcreator-todo-plugin/LICENSE.BSD
}

package_qtcreator-artisticstyle-plugin() {
  license=('GPL3' 'LGPL')
  depends=('qtcreator=2.1.0')
  pkgdesc='ArtisticStyle plugin for QtCreator'
  url='http://astyleplugin.berlios.de/index.php'
  pkgrel=2
  
  cd $srcdir/ArtisticStylePlugin-0.6
  make INSTALL_ROOT=$pkgdir/usr/ install
  mkdir -p $pkgdir/usr/share/licenses/qtcreator-todo-plugin
}
