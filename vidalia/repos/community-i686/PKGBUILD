# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=vidalia
pkgver=0.2.19
pkgrel=2
pkgdesc="Controller GUI for Tor"
url="https://www.torproject.org/vidalia"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt' 'tor')
makedepends=('cmake')
install=vidalia.install
source=(https://www.torproject.org/vidalia/dist/$pkgname-$pkgver.tar.gz{,.asc}
        revert-e36785b3bc90fd33eed45e5ca6d334780d5fab30.patch)
md5sums=('caf92b44ad2932bbc1c40d6405951c39'
         '1ba45d7c91797e880d79a3fc85818dee'
         '72de3a292b8481b381f67016cb2a677c')

build() {
  [[ -f /etc/profile.d/qt4.sh ]] && source /etc/profile.d/qt4.sh

  patch "$srcdir"/$pkgname-$pkgver/src/vidalia/MainWindow.cpp "$srcdir"/revert-e36785b3bc90fd33eed45e5ca6d334780d5fab30.patch

  cd "$srcdir"
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../$pkgname-$pkgver/doc/vidalia.1 "$pkgdir"/usr/share/man/man1/vidalia.1 || return 1
}
