# $Id$
# Maintainer: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Rorschach <r0rschach@lavabit.com>

pkgname=vidalia
pkgver=0.2.15
pkgrel=1
pkgdesc="Controller GUI for Tor"
url="https://www.torproject.org/vidalia"
arch=('i686' 'x86_64')
license=('GPL')
depends=('qt' 'tor')
makedepends=('cmake')
install=vidalia.install
source=("https://www.torproject.org/vidalia/dist/$pkgname-$pkgver.tar.gz")
md5sums=(845b627eb2534aa5da29efd6c8d30978)

build() {
  [[ -f /etc/profile.d/qt4.sh ]] && source /etc/profile.d/qt4.sh

  cd "$srcdir"
  mkdir build
  cd build
  cmake -D CMAKE_INSTALL_PREFIX=/usr ../$pkgname-$pkgver
  make
}

package() {
  cd "$srcdir/build"
  make DESTDIR="$pkgdir/" install
  install -Dm644 ../$pkgname-$pkgver/doc/vidalia.1 "$pkgdir"/usr/share/man/man1/vidalia.1 || return 1
}
