# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-os-service-types
pkgname=(python-os-service-types python2-os-service-types)
pkgver=1.6.0
pkgrel=1
pkgdesc="Python library for consuming OpenStack sevice-types-authority data"
arch=('any')
url="https://pypi.python.org/pypi/os-service-types/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-requests-mock' 'python2-requests-mock')
options=('!emptydirs')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/os-service-types/archive/$pkgver.tar.gz")
sha512sums=('f8779b61258377d7f963f67c4a5d3c09076f1fd73e8ae373c0413c2c2e8e16f3ec110d780bf415109cf14c6456e19f561f6731db629d03d26a1b60e5647ea57c')

prepare() {
  echo "global-include *.json" >> os-service-types-$pkgver/MANIFEST.in

  cp -a os-service-types-$pkgver{,-py2}

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/os-service-types-$pkgver
  python setup.py build

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/os-service-types-$pkgver
  stestr run

  cd "$srcdir"/os-service-types-$pkgver-py2
  PYTHON=python2 stestr2 run
}

package_python-os-service-types() {
  depends=('python-pbr')

  cd "$srcdir"/os-service-types-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-os-service-types() {
  depends=('python2-pbr')

  cd "$srcdir"/os-service-types-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
