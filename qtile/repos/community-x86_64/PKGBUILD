# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=qtile
pkgver=0.15.1
pkgrel=1
pkgdesc="A full-featured, pure-Python tiling window manager"
arch=('x86_64')
url="http://www.qtile.org"
license=('MIT')
depends=('gdk-pixbuf2' 'pango' 'python-cffi' 'python-xcffib' 'python-cairocffi'
'python-dbus' 'python-gobject' 'python-setuptools' 'python-six')
makedepends=('jupyter_console' 'libpulse' 'python-ipykernel' 'python-iwlib'
'python-keyring' 'python-psutil' 'python-pytest' 'python-xdg')
optdepends=('alsa-utils: volume widget'
            'canto-daemon: canto widget'
            'cmus: cmus widget'
            'khal: khal_calendar widget'
            'libpulse: for pulse_volume and pulseaudio_ffi widget'
            'lm_sensors: sensors widget'
            'moc: moc widget'
            'python-iwlib: wlan widget'
            'python-keyring: imapwidget widget'
            'python-mpd2: mpd2widget widget'
            'python-psutil: graph, net and memory widget'
            'python-setproctitle: change process name to qtile'
            'python-xdg: launchbar widget')
checkdepends=('imagemagick' 'python-pytest' 'python-xvfbwrapper' 'xorg-xcalc'
'xorg-xclock' 'xorg-xeyes' 'xorg-server-xephyr' 'xorg-xrandr' 'xterm')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('57dfeeea212346c14b922303b5d2fd950292383a1da3be6bf5ca0880bedaa3adb7ffc121df617cf763fcd1208c44e34a7ba128a1f2a6e8d3042dd6906ece7e20')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

# # tests disabled for now, as the test suite won't return:
# # https://github.com/qtile/qtile/issues/1130
# # some tests are failing:
# # https://github.com/qtile/qtile/issues/1352
# check() {
#   cd "${pkgname}-${pkgver}"
#   export PYTTHONPATH="build/lib/:${PYTHONPATH}"
#   export LC_TYPE=en_US.UTF-8
#   ./scripts/ffibuild
#   pytest -vv -k 'not test_images_good \
#                  and not test_svg_scaling \
#                  and not test_pattern_rotate \
#                  and not test_pattern \
#                  and not test_from_path \
#                  and not test_init \
#                  and not test_get_cairo_surface'
# }

package() {
  cd "${pkgname}-${pkgver}"
  # install
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}" \
    --prefix=/usr
  # license
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  # config
  install -vDm 644 libqtile/resources/default_config.py \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # desktop file
  install -vDm 644 "resources/${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/xsessions/"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname}/" \
    -vDm 644 {CHANGELOG,README.rst}
}
