# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=9.0.0
pkgrel=1
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-typing-extensions')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest-asyncio' 'python-pytest-trio'
              'python-twisted')
optdepends=('python-trio' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('2384cc85d9f3ced3b2964c9c0ca453368678ee9d9da3b4ec1d9f30ddb8ca6772')
b2sums=('f55c106f8e95c1bc35aa83549b9e693b9895f1ce4b99ea21062bc7339f0432f3dad6f4d1d6d91814e627d1c626e5efe20df463a9a3aa2bb25b04d117703abcd6')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requres list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python setup.py build
}

check() {
  cd pyee-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m pytest
}

package() {
  cd pyee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
