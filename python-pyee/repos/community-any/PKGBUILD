# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Josh Holbrook <josh.holbrook@gmail.com>

pkgname=python-pyee
pkgver=9.0.1
pkgrel=1
pkgdesc="Port of node.js's EventEmitter to python"
arch=('any')
url=https://github.com/jfhbrook/pyee
license=('MIT')
depends=('python-typing-extensions')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest-asyncio' 'python-pytest-trio'
              'python-twisted')
optdepends=('python-trio' 'python-twisted')
source=("https://files.pythonhosted.org/packages/source/p/pyee/pyee-$pkgver.tar.gz")
sha256sums=('038584bf384c76787648213cd95777e96ca607dbf4bd2653aa5d0e6a8db667fe')
b2sums=('1745a31af7ee3bfc208a92154f97bd5030c0d041f944eacca82894e7fa9577930f41b5c77b00b9428cb2482ce6a3531936b9f9c08fa5e32c16d5d67b8063f8a7')

prepare() {
  cd pyee-$pkgver
  # Remove setup_requres list
  sed -i '/setup_requires/,/\],/d' setup.py
}

build() {
  cd pyee-$pkgver
  python setup.py build
}

check() {
  cd pyee-$pkgver
  python -m venv --system-site-packages test-env
  test-env/bin/python setup.py install --optimize=1 --skip-build
  test-env/bin/python -m pytest
}

package() {
  cd pyee-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}

# vim:set ts=2 sw=2 et:
