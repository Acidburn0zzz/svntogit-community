# $Id: PKGBUILD,v 1.15 2009/06/22 09:01:01 pressh Exp $
# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Ronald van Haren <ronald.archlinux.org>

pkgname=exhibit-svn
pkgver=41086
pkgrel=1
pkgdesc="Image viewer for e17"
arch=('i686' 'x86_64')
groups=('e17-extra-svn')
url="http://www.enlightenment.org"
license=('BSD')
depends=('etk-svn' 'epsilon-svn' 'efreet-svn' 'engrave-svn')
makedepends=('svn')
conflicts=('exhibit')
provides=('exhibit')
replaces=('exhibit-cvs')
options=('!libtool')
install=exhibit.install
source=('exhibit.desktop')
md5sums=('1c58afb819fe8c4d53cd0d2e174cac37')

_svntrunk="http://svn.enlightenment.org/svn/e/trunk/OLD/exhibit"
_svnmod="exhibit"

build() {
   cd $srcdir

  msg "Connecting to $_svntrunk SVN server...."
  if [ -d $_svnmod/.svn ]; then
    (cd $_svnmod && svn up -r $pkgver)
  else
    svn co $_svntrunk --config-dir ./ -r $pkgver $_svnmod
  fi

  msg "SVN checkout done or server timeout"
  msg "Starting make..."

  cp -r $_svnmod $_svnmod-build
  cd $_svnmod-build

  ./autogen.sh --prefix=/usr
  make || return 1
  make DESTDIR=$pkgdir install || return 1

# install license files
  install -Dm644 $srcdir/$_svnmod-build/COPYING \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING
  	
  install -Dm644 $srcdir/$_svnmod-build/COPYING-PLAIN \
  	$pkgdir/usr/share/licenses/$pkgname/COPYING-PLAIN
      
# install desktop file and icon
  install -Dm644 $srcdir/exhibit.desktop $pkgdir/usr/share/applications/exhibit-cvs.desktop
  install -Dm644 $srcdir/$_svnmod-build/doc/img/exhibit.png $pkgdir/usr/share/pixmaps/exhibit.png      
  
  rm -r $srcdir/$_svnmod-build
}
