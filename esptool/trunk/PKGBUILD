# Maintainer: Jelle van der Waa <jelle@vdwaa.nl>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Marcin Kornat <rarvolt+aur@gmail.com>
# Contributor: MatejSpindler <spindler.matej@gmail.com>

pkgname=esptool
pkgver=2.6
pkgrel=1
pkgdesc="A cute Python utility to communicate with the ROM bootloader in Espressif ESP8266"
arch=('any')
url="https://github.com/espressif/esptool"
license=('GPL2')
makedepends=('git' 'python-setuptools')
depends=('python-pyserial' 'python-ecdsa' 'python-pyaes')
source=("https://github.com/espressif/esptool/archive/v${pkgver}.tar.gz")
sha512sums=('dd2e1815df07a491d5b41b709aafecb642f5b642c83b11e309986ba0ad827257e6b7bb93f4decd7ee37fd10a1ee7b36c836598c0c2e1e60118bf96397c187dfd')

build() {
  cd "${pkgname}-${pkgver}"
  python3 setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python3 setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
