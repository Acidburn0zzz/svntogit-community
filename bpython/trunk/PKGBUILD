# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Anton Bazhenov <anton.bazhenov at gmail>

pkgname=('bpython' 'bpython2')
pkgver=0.10
pkgrel=1
pkgdesc='A fancy interface to the Python interpreter'
arch=('any')
url='http://bpython-interpreter.org/'
license=('MIT')
makedepends=('python-distribute' 'python2-distribute')
source=("http://bpython-interpreter.org/releases/bpython-${pkgver}.tar.gz")
md5sums=('67cc919031c71605260b6644a7edaba4')

build() {
  cp -r bpython-${pkgver} bpython2-${pkgver}

  cd bpython-${pkgver}

  python setup.py build

  cd ${srcdir}/bpython2-${pkgver}

  python2 setup.py build
}

package_bpython() {
  depends=('python-pygments')

  cd bpython-${pkgver}

  python setup.py install \
    --root=${pkgdir} \
    --optimize=1

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/bpython/LICENSE
}

package_bpython2() {
  depends=('python2-pygments')

  cd bpython2-${pkgver}

  python2 setup.py install \
    --root=${pkgdir} \
    --optimize=1

  install -Dm644 LICENSE \
    ${pkgdir}/usr/share/licenses/bpython2/LICENSE

  # conflicting files with bpython
  mv ${pkgdir}/usr/bin/bpython \
    ${pkgdir}/usr/bin/bpython2
  mv ${pkgdir}/usr/bin/bpython-gtk \
    ${pkgdir}/usr/bin/bpython2-gtk
  mv ${pkgdir}/usr/share/applications/bpython.desktop \
    ${pkgdir}/usr/share/applications/bpython2.desktop
  mv ${pkgdir}/usr/share/man/man1/bpython.1 \
    ${pkgdir}/usr/share/man/man1/bpython2.1
  mv ${pkgdir}/usr/share/man/man5/bpython-config.5 \
    ${pkgdir}/usr/share/man/man5/bpython2-config.5
}
