# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

pkgname=xmobar
pkgver=0.16
pkgrel=3
pkgdesc="A Minimalistic Text Based Status Bar"
url="http://hackage.haskell.org/package/xmobar"
license=('custom:BSD3')
arch=('i686' 'x86_64')
depends=('gmp' 'libxft' 'libxinerama' 'wireless_tools' 'libxrandr')
makedepends=('ghc=7.6.1-1' 'haskell-x11=1.6.0.2' 'haskell-x11-xft=0.3.1' 'haskell-utf8-string'
	     'haskell-stm>=2.3' 'haskell-parsec>=3.1' 'haskell-mtl>=2')
options=('strip')
source=(http://hackage.haskell.org/packages/archive/xmobar/$pkgver/xmobar-$pkgver.tar.gz
0001-Update-usage-of-catch-to-catchIOError-since-catch-ha.patch)
md5sums=('b470f8253f8d2b014f59e9d657cc7ddd'
         '149583c19ac3d0ad829f3138342bb9db')
build() {
    cd ${srcdir}/xmobar
    patch -Np1 -i $srcdir/0001-Update-usage-of-catch-to-catchIOError-since-catch-ha.patch

    runhaskell Setup configure --disable-optimization --prefix=/usr --flags="with_threaded with_utf8 with_xft with_iwlib"
    runhaskell Setup build
}

package() {
    cd ${srcdir}/xmobar
    runhaskell Setup copy --destdir=${pkgdir}
    install -D -m644 license ${pkgdir}/usr/share/licenses/$pkgname/LICENSE
}

