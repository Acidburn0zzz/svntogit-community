# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Link Dupont <link@subpop.net>

pkgname=cherokee
pkgver=1.2.100
pkgrel=1
pkgdesc="A very fast, flexible and easy to configure Web Server"
arch=('i686' 'x86_64')
url="http://www.cherokee-project.com/"
license=('GPL2')
depends=('openssl' 'pcre')
makedepends=('python2' 'gettext' 'libldap' 'pam' 'libmysqlclient'
             'ffmpeg' 'geoip')
optdepends=('python2: cherokee-admin (administrative web interface)'
            'libldap: ldap validator'
            'pam: pam validator'
            'libmysqlclient: mysql validator'
            'ffmpeg: Audio/Video streaming handler'
            'geoip: GeoIP rule module'
            'rrdtool: RRDtool based information collector')
backup=('etc/cherokee/cherokee.conf'
        'etc/logrotate.d/cherokee'
        'etc/pam.d/cherokee')
options=('!libtool')
source=(http://www.cherokee-project.com/download/1.2/$pkgver/cherokee-$pkgver.tar.gz
        cherokee.rc
        cherokee.logrotate
        fix-ctk-path-handler-match.patch)
sha1sums=('2ed95504c7f156227f9b688a901608be21cf3dff'
          '85e24ccc94e6e97858226222ac62cca79c17bc80'
          '266064ab6d3ebd206d7207e1d97b003d2021b2e4'
          '6d64371dfb23f43c29a24e37e7ae99e08a0c84cc')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Fix path matching bug in CTK apps (e.g. market)
  patch -Np1 -i "$srcdir/fix-ctk-path-handler-match.patch"

  # Use subdirectory for logs
  sed -i -r 's|(%localstatedir%/log)|\1/cherokee|' cherokee.conf.sample.pre

  # Use Python 2 in cherokee-admin
  sed -i 's/"python"/"python2"/' cherokee/main_admin.c

  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --with-wwwroot=/srv/http \
    --with-wwwuser=http \
    --with-wwwgroup=http \
    --with-python=python2 \
    --enable-os-string="Arch Linux"
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir" install

  # PAM configuration file for cherokee
  install -D -m644 pam.d_cherokee "$pkgdir/etc/pam.d/$pkgname"

  # Fix ownership of /var/lib/cherokee/graphs
  chown -R http:http "$pkgdir/var/lib/$pkgname/graphs"

  # Use Python 2
  sed -i 's/env python$/&2/' \
    "$pkgdir/usr/share/cherokee/admin/"{server,upgrade_config}.py \
    "$pkgdir/usr/bin/"{CTK-run,cherokee-{admin-launcher,tweak}}
  sed -i -r "s/['\"]python/&2/g" \
    "$pkgdir/usr/share/cherokee/admin/wizards/django.py"

  # Compile Python scripts
  python2 -m compileall "$pkgdir"
  python2 -O -m compileall "$pkgdir"

  install -d -o http -g http "$pkgdir/var/log/$pkgname"
  install -D "$srcdir/$pkgname.rc" "$pkgdir/etc/rc.d/$pkgname"
  install -Dm644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"

  # Cleanup
  rm -rf "$pkgdir/srv"
}

# vim:set ts=2 sw=2 et:
