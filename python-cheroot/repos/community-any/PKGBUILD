# Maintainer: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: wangjiezhe <wangjiezhe AT yandex DOT com>

pkgbase='python-cheroot'
_pkgbase="${pkgbase//python-/}"
pkgname=('python-cheroot' 'python2-cheroot')
pkgdesc="Highly-optimized, pure-python HTTP server"
pkgver=6.2.1
pkgrel=3
arch=('any')
url="https://github.com/cherrypy/cheroot"
license=('MIT')
makedepends=('python-six' 'python2-six' 'python-setuptools-scm'
  'python2-setuptools-scm' 'python2-backports.functools_lru_cache'
  'python2-backports.unittest_mock' 'tree')
source=("${pkgbase}-${pkgver}.tar.gz::https://pypi.python.org/packages/8b/80/d84275abc1b9cf69d707a6c16ea3fe1faa45d02f4eb59c60bb5eba8cf29e/cheroot-6.2.1.tar.gz"
        "LICENSE"
        "cheroot-fix-setup-python3.patch")
sha512sums=('4f8e97b835b4c12d72b51ce90bba52b0c5e2cac3382b34a34997fe6c58e660200ff1e163f7811810bdd5162c01e1b29e4a24d9d9e332cd636dfc38b9f9ce585d'
            '1106afed483b7258e4ae89c5d9459c3834412b31aac90169725ed62d2ab44f61f6f79e894d4c9e4d8bd99e14530ab778df2187784f0b25eaab86d312fad68944'
            '1a6da34ef7d4b7cad392fcf6e0251b28ad2267af36564acc673a560963bdfdadb2f80950fc33783f6fb1a9683ae10a32bedf7b4969cd9c9b916c6060979ce547')

prepare(){
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  # patch setup.py (don't require legacy stuff on python3)
  patch -p1 -l -i "${srcdir}/cheroot-fix-setup-python3.patch"
}

package_python-cheroot() {
  depends=('python-six')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_python2-cheroot() {
  depends=('python2-six' 'python2-backports.functools_lru_cache'
           'python2-backports.unittest_mock')

  cd "${srcdir}/${_pkgbase}-${pkgver}"
  python2 setup.py install --root="$pkgdir/" --optimize=1

  # the author has promised to include a LICENSE file in future releases:
  # https://github.com/jaraco/skeleton/issues/1
  # for now the LICENSE file has been taken from:
  # https://github.com/jaraco/skeleton/blob/skeleton/LICENSE
  install -D -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
