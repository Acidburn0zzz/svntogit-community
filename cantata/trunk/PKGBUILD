# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=2.0.0
pkgrel=4
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(i686 x86_64)
url='https://code.google.com/p/cantata/'
license=(GPL)
depends=(qt5-svg libmtp libcddb libmusicbrainz5 mpg123 vlc taglib-extras cdparanoia media-player-info 
         udisks2 hicolor-icon-theme)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support')
makedepends=(cmake qt5-tools ffmpeg)
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
        cantata-cxxflags.patch::"https://github.com/CDrummond/cantata/commit/bf353b1c.patch")
md5sums=('dc7a65145692c5b2cac4c2c8a7c767c5'
         'df65c35e954a4ddb9891b958fe78a6fe')

prepare() {
  mkdir -p build

# Fix build against ffmpeg 3.0
  sed -e '/audioconvert.h/d' -i $pkgname-$pkgver/replaygain/ffmpeginput.cpp
# Don't replace CXXFLAGS
  cd $pkgname-$pkgver
  patch -p1 -i ../cantata-cxxflags.patch
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TOUCH_SUPPORT=ON \
    -DCMAKE_POSITION_INDEPENDENT_CODE=ON
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
