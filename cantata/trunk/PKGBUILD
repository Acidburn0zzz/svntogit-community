# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=2.3.1
pkgrel=2
pkgdesc='Qt5 client for the music player daemon (MPD)'
arch=(x86_64)
url='https://github.com/CDrummond/cantata'
license=(GPL)
depends=(libmtp libmusicbrainz5 mpg123 vlc taglib-extras media-player-info libcdio-paranoia udisks2)
optdepends=('perl-uri: dynamic playlist' 'mpd: playback' 'ffmpeg: ReplayGain support' 'libebur128: ReplayGain support'
            'sshfs: remote devices support')
makedepends=(cmake qt5-tools ffmpeg libebur128)
source=("https://github.com/CDrummond/cantata/releases/download/v$pkgver/$pkgname-$pkgver.tar.bz2"
        cantata-no-samba.patch)
sha256sums=('5b1e563ec125d57296495d0f8f97eeecf0b2260aa50b35bae7cab221d7914eb4'
            '171368534670dca28b864d242df20a670343d360b2aff40f9cf73f79ee8a5b99')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../cantata-no-samba.patch # Remove Samba share mounting code due to security issues
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
