# $Id$
# Maintainer:  Federico Cinelli <cinelli@aur.archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Mcder3 <mcder3[at]gmail[dot]com>
# Contributor: MisterFred <mister.fred[at]free[dot]fr>

pkgname=cantata
pkgver=1.0.3
pkgdesc="A KDE client for the music player daemon (MPD)"
pkgrel=2
arch=('i686' 'x86_64')
url="http://kde-apps.org/content/show.php/Cantata?content=147733"
license=('GPL')
depends=('kdebase-runtime' 'taglib-extras' 'mpg123' 'libmtp' 'libcddb' 'libmusicbrainz5')
makedepends=('cmake' 'automoc4')
install="$pkgname.install"
source=("http://cantata.googlecode.com/files/$pkgname-$pkgver.tar.bz2")
md5sums=('fd942d25127a7936efaf1fe887d66189')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's/AVCODEC_MAX_AUDIO_FRAME_SIZE/192000/' replaygain/ffmpeginput.cpp
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  mkdir build

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_PHONON=ON

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  
  make DESTDIR="$pkgdir" PREFIX="/usr" install
}
