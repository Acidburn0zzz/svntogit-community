# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=9.2.0
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('npm' 'semver')
source=(https://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('d1ef77255bb312b58773b7a30a844cf2e9b663100e72a658cd52c7fe38b71dc41b11f868ec81e8697351a95f24a538d23357c23fc17d237b05ae9ad764eeeb9d')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in semver; do
    rm -r $dep;
  done

  # Non-deterministic race in npm gives 777 permissions to random directories.
  # See https://github.com/npm/npm/issues/9359 for details.
  chmod -R u=rwX,go=rX "$pkgdir"

  # npm installs package.json owned by build user
  # https://bugs.archlinux.org/task/63396
  chown -R root:root "$pkgdir"
}
