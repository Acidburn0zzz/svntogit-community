# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=npm-check-updates
pkgdesc='Find newer versions of dependencies than what your package.json or bower.json allows'
pkgver=2.11.1
pkgrel=1
arch=('any')
url='https://github.com/tjunnone/npm-check-updates'
license=('Apache')
depends=('semver' 'npm')
source=(http://registry.npmjs.org/$pkgname/-/$pkgname-$pkgver.tgz)
noextract=($pkgname-$pkgver.tgz)
sha512sums=('bb651818148ed9220c75e78f47efa490a83316dce1ccf0159275e4d219e08db4ac1963ff4e46304ebf4c86ef0e41b1451ec7aeb930cfe774ff471e567b316a28')

package() {
  npm install -g --user root --prefix "$pkgdir"/usr "$srcdir"/$pkgname-$pkgver.tgz
  rm -r "$pkgdir"/usr/etc

  # Experimental dedup
  cd "$pkgdir"/usr/lib/node_modules/$pkgname/node_modules
  for dep in npm semver; do
    rm -r $dep;
    npm link $dep;
  done

  # Fix npm derp
  find "$pkgdir/usr" -type d -exec chmod 755 '{}' +
}
