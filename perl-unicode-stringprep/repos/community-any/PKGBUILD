# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=perl-unicode-stringprep
pkgver=1.103.0
_pkgver=1.103
pkgrel=2
pkgdesc="Preparation of Internationalized Strings (RFC 3454)"
arch=('any')
url="http://search.cpan.org/dist/Unicode-Stringprep/"
license=('GPL' 'PerlArtistic')
depends=('perl')
makedepends=('perl-test-simple' 'perl-test-nowarnings')
provides=("perl-unicode-stringprep-bidi=$pkgver" "perl-unicode-stringprep-mapping=$pkgver"
	  "perl-unicode-stringprep-prohibited=$pkgver" "perl-unicode-stringprep-unassigned=$pkgver")
options=(!emptydirs)
source=("http://search.cpan.org/CPAN/authors/id/C/CF/CFAERBER/Unicode-Stringprep-${_pkgver}.tar.gz")
md5sums=('38b37d58e8444832a8bb5b3c492726cd')

build() {
  local _dir=$(find $srcdir -maxdepth 2 -type f -name 'Makefile.PL')
  cd $(dirname "$_dir")
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

package() {
  local _dir=$(find $srcdir -maxdepth 2 -type f -name 'Makefile.PL')
  cd $(dirname "$_dir")
  make install DESTDIR="${pkgdir}"
  # remove perllocal.pod and .packlist
  find ${pkgdir} -name perllocal.pod -delete
  find ${pkgdir} -name .packlist -delete
}
