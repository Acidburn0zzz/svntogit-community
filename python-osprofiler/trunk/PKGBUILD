# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-osprofiler
pkgver=3.2.2
pkgrel=1
pkgdesc="OpenStack Profiler Library"
arch=('any')
url="https://docs.openstack.org/osprofiler/latest/"
license=('Apache')
depends=('python-netaddr' 'python-oslo-concurrency' 'python-oslo-serialization' 'python-oslo-utils'
         'python-prettytable' 'python-requests' 'python-six' 'python-webob')
checkdepends=('python-ddt' 'python-docutils' 'python-elasticsearch' 'python-jaeger-client'
              'python-pymongo' 'python-redis' 'python-stestr')
source=("https://github.com/openstack/osprofiler/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('466f27986ff4387bcfed4a69b1c465fda67ccc589deec70e25df34a1a70ee346c389a112bd51f6727c2c470b953eb759f66d93e90f0a072bea19186fcbc55156')

export PBR_VERSION=$pkgver

build() {
  cd osprofiler-$pkgver
  python setup.py build
}

check() {
  cd osprofiler-$pkgver
  stestr run
}

package() {
  cd osprofiler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
