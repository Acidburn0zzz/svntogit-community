# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-osprofiler
pkgver=3.3.0
pkgrel=1
pkgdesc="OpenStack Profiler Library"
arch=('any')
url="https://docs.openstack.org/osprofiler/latest/"
license=('Apache')
depends=('python-netaddr' 'python-oslo-concurrency' 'python-oslo-serialization' 'python-oslo-utils'
         'python-prettytable' 'python-requests' 'python-six' 'python-webob')
checkdepends=('python-ddt' 'python-docutils' 'python-elasticsearch' 'python-jaeger-client'
              'python-pymongo' 'python-redis' 'python-stestr')
source=("https://github.com/openstack/osprofiler/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('68fa21397b572bbde303ebce7fd31a0d4e24074d43a3a000dc5e44e452216c54cbbc7820ec36f5e8ff560f84b4e9256dfab300a3f564437707972c3164d08f2f')

export PBR_VERSION=$pkgver

build() {
  cd osprofiler-$pkgver
  python setup.py build
}

check() {
  cd osprofiler-$pkgver
  stestr run
}

package() {
  cd osprofiler-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
