# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=wai-extra
pkgname=haskell-wai-extra
pkgver=3.0.24.3
pkgrel=1
pkgdesc="Provides some basic WAI handlers and middleware."
url="https://github.com/yesodweb/wai"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' 'haskell-aeson' 'haskell-ansi-terminal' 'haskell-base64-bytestring'
         'haskell-case-insensitive' 'haskell-cookie' 'haskell-data-default-class'
         'haskell-fast-logger' 'haskell-http-types' 'haskell-iproute' 'haskell-network'
         'haskell-old-locale' 'haskell-resourcet' 'haskell-streaming-commons'
         'haskell-unix-compat' 'haskell-vault' 'haskell-void' 'haskell-wai'
         'haskell-wai-logger' 'haskell-word8' 'haskell-zlib')
makedepends=('ghc' 'haskell-hspec' 'haskell-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('84b2a644ba2a4c0634fc452cac315da0c2ef4828354a988423ba20c5b1e5c8982c441c0ec7523f79449963485035187074c4c1f6236ffd4a03073eac98f89496')

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid \
            -f-build-example
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
