# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=5.1.0.1
pkgrel=3
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
depends=('deepin-daemon' 'deepin-qt5integration' 'gsettings-qt' 'liblightdm-qt5' 'startdde')
makedepends=('cmake' 'qt5-tools')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver.tar.gz"
        icon-size.patch)
sha512sums=('d328762fabb6ca491c618206adbfc1db651a229928eea1ca9e9b5e51d5432127725676c8e85da6837e5c82b959f5f5d403f80054425beab84a4830a84ca0fce1'
            '4e4c2a0d6ea124121fcad6377fb8ecd7a678667f1bab962cbce3a439fd5155172c897264a11b15667cc07b160de19c0b2e669af7b31bdabf3e04e134343aab39')

prepare() {
  cd dde-session-shell-$pkgver
  patch -p1 -i ../icon-size.patch
  sed -i '/darrowrectangle/d' CMakeLists.txt src/widgets/widgets.pri
  sed -i 's/include "darrowrectangle.h"/include <darrowrectangle.h>/' src/widgets/errortooltip.h
  sed -i '/#include <QFile>/a #include <QPainterPath>' src/widgets/useravatar.cpp

  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/authagent.cpp
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd dde-session-shell-$pkgver
  make DESTDIR="$pkgdir" install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
