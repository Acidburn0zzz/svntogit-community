# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-session-shell
pkgver=5.1.0.5
pkgrel=1
pkgdesc='Deepin desktop-environment - session-shell module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-session-shell"
license=('GPL3')
depends=('deepin-daemon' 'deepin-qt5integration' 'deepin-wallpapers' 'gsettings-qt' 'liblightdm-qt5'
         'startdde')
makedepends=('cmake' 'qt5-tools')
conflicts=('deepin-session-ui<5')
provides=('lightdm-deepin-greeter')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-session-shell/archive/$pkgver.tar.gz"
        icon-size.patch)
sha512sums=('550ecf5d5c1cf4e0a89abde8783383c3e4af0f60bb40f1dfe85f04cc5fd0839338bad07b3f00ff6451faf2672d291a7254077c83a56422bcd87dda3c6df64011'
            'bfd3b15a5dce361eea2f8ced63a240edbbc19096a372963dc788d45faa372f841db2b2a6f29cb87aebadc00c921d56a99b21cec951e595acf220c8fc687395b6')

prepare() {
  cd dde-session-shell-$pkgver
  patch -p1 -i ../icon-size.patch
  sed -i '/darrowrectangle/d' CMakeLists.txt src/widgets/widgets.pri
  sed -i 's/include "darrowrectangle.h"/include <darrowrectangle.h>/' src/widgets/errortooltip.h
  sed -i '/#include <QFile>/a #include <QPainterPath>' src/widgets/useravatar.cpp

  # We don't have common-auth on Arch
  sed -i 's/common-auth/system-login/' src/libdde-auth/authagent.cpp
}

build() {
  cd dde-session-shell-$pkgver
  cmake . -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd dde-session-shell-$pkgver
  make DESTDIR="$pkgdir" install

  chmod +x "$pkgdir"/usr/bin/deepin-greeter
}
