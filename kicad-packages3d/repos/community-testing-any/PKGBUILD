# Maintainer: George Rawlinson <grawlinson@archlinux.org>

pkgname=kicad-packages3d
pkgver=6.0.0
pkgrel=1
pkgdesc="KiCad 3D model libraries"
arch=('any')
url="https://www.kicad.org"
license=('custom:CC-BY-SA-4.0')
makedepends=('cmake')
conflicts=('kicad-library3d')
replaces=('kicad-library3d')
source=("https://gitlab.com/kicad/libraries/kicad-packages3D/-/archive/$pkgver/kicad-packages3D-$pkgver.tar.bz2")
sha512sums=('48f6698d8b48483e484638f780cf5a9522cda5381c3858b15b0862994dfacc271426ecbaaa5168a2fc8f6be84ac6e9b160e080ac74183d5be282de36088416af')
b2sums=('5287b9ae322f6eac5035c0dbe725a26f2d44cde259c95d8d5db950839de067075716ff343db32088861873b77f06116c2dc077461fc35681fcf4c86e5b9ba3d1')

build() {
  cmake \
    -B build \
    -S "kicad-packages3D-$pkgver" \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "kicad-packages3D-$pkgver/LICENSE.md"
}

