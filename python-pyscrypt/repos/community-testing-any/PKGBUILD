# Maintainer: David Runge <dave@sleepmap.de>

_name=pyscrypt
pkgbase=python-pyscrypt
pkgname=('python-pyscrypt' 'python2-pyscrypt')
pkgver=1.6.2
pkgrel=1
pkgdesc='A very simple, pure-Python implementation of the scrypt password-based key derivation function and scrypt file format library'
url='https://pypi.python.org/pypi/pyscrypt'
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
source=("https://pypi.io/packages/source/p/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('1e36cd333baad33577478b1947de96445adb6ba7b0e339b01b22f858dd61d86c4873d59fc1c5994562136ac9307ed0893667bc877bb6ed13dee180580bb8a260')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  python tests/run-tests-file.py
  python tests/run-tests-hash.py
  cd ../"${pkgname[1]}-${pkgver}"
  python2 tests/run-tests-file.py
  python2 tests/run-tests-hash.py
}


package_python-pyscrypt() {
  depends=('python')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname[0]}/README.md"
  install -vDm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
}

package_python2-pyscrypt() {
  depends=('python2')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm644 README.md "${pkgdir}/usr/share/doc/${pkgname[1]}/README.md"
  install -vDm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
}

# vim:set ts=2 sw=2 et:
