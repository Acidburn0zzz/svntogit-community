# Contributor: Andrea Zucchelli <zukka77@gmail.com>
# Contributor: Jonathan Liu <net147@gmail.com>
# Maintainer: Jon Nordby <jononor@gmail.com>

pkgname=lxc
pkgver=0.8.0_rc1
pkgrel=1
pkgdesc="Linux Containers"
arch=('i686' 'x86_64')
url="http://lxc.sourceforge.net/"
depends=('bash' 'perl')
license=('LGPL')
source=("http://lxc.sourceforge.net/download/lxc/$pkgname-${pkgver/_/-}.tar.gz")
md5sums=('06ceecf4dbe1be988fc903ad8dd34d29')

build() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  ./configure \
	--prefix=/usr \
	--localstatedir=/var \
	--libexecdir=/usr/bin \
	--sysconfdir=/etc \
	--disable-doc
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver/_/-}"

  make DESTDIR="$pkgdir" install
  install -d -m755 "$pkgdir/var/lib/lxc"

  cd doc
  find . -type f -name '*.1' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man1/{}" \;
  find . -type f -name '*.5' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man5/{}" \;
  find . -type f -name '*.7' -exec install -D -m644 "{}" "$pkgdir/usr/share/man/man7/{}" \;
}
