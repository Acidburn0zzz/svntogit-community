# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=switchboard-plug-online-accounts
pkgver=6.1.0
pkgrel=1
pkgdesc='Switchboard Online Accounts Plug'
arch=(x86_64)
url=https://github.com/elementary/switchboard-plug-onlineaccounts
license=(GPL2)
groups=(pantheon)
depends=(
  glib2
  glibc
  gtk3
  json-glib
  libedataserver-1.2.so
  libedataserverui-1.2.so
  libgee
  libgranite.so
  libhandy-1.so
  libswitchboard-2.0.so
  rest
  webkit2gtk
)
makedepends=(
  git
  meson
  vala
)
_tag=856a3c29e7c07d06ae8d2774536f52ef1733a1a1
source=(switchboard-plug-online-accounts::git+https://github.com/elementary/switchboard-plug-onlineaccounts.git#tag=${_tag})
sha256sums=(SKIP)

prepare() {
  cd switchboard-plug-online-accounts
  git cherry-pick -n 7923f25e9156d50c24621b0dbce0ab0e67b39e7f
  git cherry-pick -n 8973efbee9f05314d8806160e63900c9c5ec1de3
}

pkgver() {
  cd switchboard-plug-online-accounts
  git describe --tags
}

build() {
  arch-meson switchboard-plug-online-accounts build
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
