# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Francois Boulogne <fboulogne at april dot org>

pkgbase=python-joblib
pkgname=('python2-joblib' 'python-joblib')
pkgver=0.13.2
pkgrel=1
pkgdesc="Set of tools to provide lightweight pipelining in Python"
arch=('any')
url="https://joblib.readthedocs.io/"
license=('BSD')
makedepends=('python2-setuptools' 'python-setuptools')
source=("https://github.com/joblib/joblib/archive/${pkgver}.tar.gz")
sha256sums=('1a45a4d8d3728245a941946e0741cbfb7822c5a91cf5fbad3b3efbe2b27050ae')

build() {
  cd "$srcdir"
  cp -a joblib-${pkgver} joblib-py2-${pkgver}

  # "Building Python2"
  cd "$srcdir"/joblib-py2-${pkgver}
  python2 setup.py build

  # "Building Python3"
  cd "$srcdir"/joblib-${pkgver}
  python setup.py build
}

package_python2-joblib() {
  depends=('python2')
  optdepends=('python2-numpy: for array manipulation'
	      'python2-lz4: for compressed serialization')

  cd "$srcdir"/joblib-py2-${pkgver}

  python2 setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-joblib() {
  depends=('python')
  optdepends=('python-numpy: for array manipulation'
	      'python-lz4: for compressed serialization')

  cd "$srcdir"/joblib-${pkgver}

  python setup.py install --skip-build --root="$pkgdir" --optimize=1

  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

