# Maintainer: David Runge <dave@sleepmap.de>
pkgname=libmusicxml
pkgver=3.14
pkgrel=2
pkgdesc="Portable C++ library around the MusicXML format"
arch=('x86_64')
url="https://github.com/grame-cncm/libmusicxml"
license=('MPL2')
depends=('gcc-libs')
makedepends=('cmake')
provides=('libmusicxml2.so')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grame-cncm/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('67c0c40e85fded3cf891f8f9365aaed3ab2396ddb5734f27e45577cfbd1aca11c49a37e3f5cb2eea290cf6f7585a776ee0939e64037a1d7f4bdd52664d23ea0a')

prepare() {
  cd "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  # CMakeLists.txt is in build and generates Makefile in root
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        build
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # moving docs to /usr/share/doc/${pkgname}
  mv -v "${pkgdir}/usr/share/${pkgname}/doc/"* \
    "${pkgdir}/usr/share/${pkgname}/CHANGELOG.txt" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/share/${pkgname}/doc/"
}
