# Maintainer: David Runge <dave@sleepmap.de>
pkgname=libmusicxml
pkgver=3.13
pkgrel=1
pkgdesc="Portable C++ library around the MusicXML format"
arch=('x86_64')
url="https://github.com/grame-cncm/libmusicxml"
license=('MPL2')
depends=('gcc-libs')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/grame-cncm/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('c4ee0d28ccdaa9fd3faaa765367a95a45210381b4197557a90a9978cf3ecf05be474b90ade00135d3eb3a79146a6d40e0106fddd8f8647b2f2d6c50557f36ccc')

prepare() {
  cd "$pkgname-$pkgver"
  # fixing not correctly defined DOCDIR:
  # https://github.com/grame-cncm/libmusicxml/issues/21
  sed -e 's|shared/lib|share/lib|' -i build/CMakeLists.txt
}

build() {
  cd "$pkgname-$pkgver"
  # CMakeLists.txt is in build and generates Makefile in root
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        build
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 readme.md -t "${pkgdir}/usr/share/doc/${pkgname}"
# moving docs to /usr/share/doc/${pkgname}
  mv -v "${pkgdir}/usr/share/${pkgname}/doc/"* \
    "${pkgdir}/usr/share/${pkgname}/CHANGELOG.txt" \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/share/${pkgname}/doc/" 
}
