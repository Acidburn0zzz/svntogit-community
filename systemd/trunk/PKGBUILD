# $Id$
# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=systemd
pkgver=21
pkgrel=1
pkgdesc="Session and Startup manager"
arch=('i686' 'x86_64')
url="http://www.freedesktop.org/wiki/Software/systemd"
license=('GPL2')
depends=('dbus-core' 'initscripts' 'sysvinit' 'util-linux>=2.19')
makedepends=('docbook-xsl' 'libxslt' 'cryptsetup')
optdepends=('cryptsetup: required for encrypted block devices'
            'initscripts-systemd: native boot and initialization scripts'
            'systemd-arch-units: collection of native unit files for Arch daemon/init scripts')
groups=('systemd')
options=('!libtool')
backup=(etc/systemd/system.conf
        etc/tmpfiles.d/systemd.conf
        etc/tmpfiles.d/x11.conf)
install=systemd.install
source=("http://www.freedesktop.org/software/$pkgname/$pkgname-$pkgver.tar.bz2"
        "os-release")
md5sums=('dff5735b30840d2ccc4a0b55d3af9c47'
         'f9922299150b4adda7b89e10bca33033')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr \
              --with-rootdir=/ \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib \
              --localstatedir=/var \
              --disable-audit \
              --disable-tcpwrap \
              --disable-gtk

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir/os-release" "$pkgdir/etc/os-release"
  install -dm755 "$pkgdir/etc/modules-load.d" "$pkgdir/run"
  printf "d /var/run/console 755 root root\n" > "$pkgdir/etc/tmpfiles.d/console.conf"

  # install target doesn't bring in plymouth units
  install -m644 units/plymouth-* "$pkgdir/lib/systemd/system"

  # rename man pages to avoid conflicts with sysvinit
  cd "$pkgdir/usr/share/man/man8"
  for manpage in telinit halt reboot poweroff runlevel shutdown; do
    mv {,systemd.}"$manpage.8"
  done
}

