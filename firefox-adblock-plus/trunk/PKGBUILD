# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=firefox-adblock-plus
pkgver=1.3.6
_firefox_ver=4.0
pkgrel=1
pkgdesc="plugin for firefox which block ads and banners"
arch=('any')
url="http://adblockplus.org/"
license=('GPL')
depends=("firefox")
makedepends=('unzip')
_addons_file=1865
source=(https://addons.mozilla.org/downloads/latest/${_addons_file}/adblock_plus-$pkgver.xpi)
md5sums=('dc20de8e47483d3644aac014a2731c3a')

build() {
    cd $srcdir
    local emid=$(sed -n -e '/<\?em:id>\?/!d; s/.*\([\"{].*[}\"]\).*/\1/; s/\"//g; p; q' install.rdf)
    local dstdir=$pkgdir/usr/lib/firefox-$_firefox_ver/extensions/${emid}
    install -d $dstdir
    sed -i 's#<em:maxVersion>.*</em:maxVersion>#<em:maxVersion>4.*</em:maxVersion>#' install.rdf
    cp -R * $dstdir
    rm $dstdir/adblock_plus-$pkgver.xpi
}
