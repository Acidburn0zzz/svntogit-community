# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgname=croc
pkgver=9.1.6
pkgrel=1
pkgdesc='Easily and securely send things from one computer to another.'
arch=('x86_64')
url="https://github.com/schollz/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go')
source=("${url}/releases/download/v${pkgver}/${pkgname}_${pkgver}_src.tar.gz")
sha256sums=('54f030f8832d49fbeb8f3f5697a62b50a035e53858d1f58844817e2a92e49ba6')

build() {
    cd "${pkgname}-${pkgver}"
    export CGO_CPPFLAGS="${CPPFLAGS}"
    export CGO_CFLAGS="${CFLAGS}"
    export CGO_CXXFLAGS="${CXXFLAGS}"
    export CGO_LDFLAGS="${LDFLAGS}"
    export GOFLAGS="-buildmode=pie -trimpath -ldflags=-linkmode=external -mod=vendor -modcacherw"
    go build
}

check() {
    cd "${pkgname}-${pkgver}"
    go test ./...
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" "${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/lib/systemd/system/" "${pkgname}.service"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
