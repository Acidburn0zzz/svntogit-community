# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Mikuro Kagamine <mikurok@forgecrushing.com>

pkgname=croc
pkgver=6.4.1
pkgrel=1
pkgdesc='Easily and securely send things from one computer to another.'
arch=('x86_64')
url="https://github.com/schollz/${pkgname}"
license=('MIT')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("${url}/archive/v${pkgver}.tar.gz"
        "${pkgname}.service")
sha256sums=('8babb4472a71de537c055e8b35642140f44c735574e1eca8047b52e56220f18c'
            'cf074568f16c7b86870a89a0a2d8e488e3837223cca071a911085d814d6f2194')

prepare() {
    cd "${pkgname}-${pkgver}"
    go mod tidy
    go mod vendor
}

build() {
    cd "${pkgname}-${pkgver}"
    go build -mod vendor -ldflags "-extldflags ${LDFLAGS}" -gcflags "all=-trimpath=${PWD}" -asmflags "all=-trimpath=${PWD}" -o "${pkgname}"
}

check() {
    cd "${pkgname}-${pkgver}"
    go test -mod vendor ./...
}

package() {
    cd "${pkgname}-${pkgver}"
    install -Dm755 -t "${pkgdir}/usr/bin/" "${pkgname}"
    install -Dm644 -t "${pkgdir}/usr/lib/systemd/system/" "${srcdir}/${pkgname}.service"
    install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}/" LICENSE
}
