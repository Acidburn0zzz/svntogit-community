# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.7
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('x86_64')
url='https://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkit2gtk' 'libesmtp' 'libnotify'
         'gpgme' 'gtksourceview3' 'gspell' 'desktop-file-utils')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(https://pawsa.fedorapeople.org/balsa/$pkgname-$pkgver.tar.bz2)
sha256sums=('dd0616e838893bf0def5c8de5070ef48215e9cb0ae997c57e51687caaf83d879')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-gpgme=gpgme-config \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-spell-checker=gspell \
      --with-rubrica \
      --with-sqlite \
      --with-gnome \
      --with-html-widget=webkit2
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
