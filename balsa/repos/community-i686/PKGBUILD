# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.5.3
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
url='http://pawsa.fedorapeople.org/balsa/'
license=('GPL')
depends=('gmime' 'webkit2gtk' 'webkitgtk' 'libesmtp' 'libnotify'
         'gpgme' 'gtksourceview3' 'gtkspell3' 'desktop-file-utils')
makedepends=('yelp-tools' 'intltool')
changelog=$pkgname.changelog
source=(https://git.gnome.org/browse/$pkgname/snapshot/$pkgname-$pkgver.tar.xz)
sha256sums=('47c1cfa20ca8e8a58e8ca5fc6951c4c5048590031798c8b204a2eba0119ad1d2')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./autogen.sh
  ./configure --prefix=/usr \
      --sysconfdir=/etc \
      --localstatedir=/var \
      --with-ssl \
      --with-gpgme=gpgme-config \
      --with-gss \
      --with-ldap \
      --with-gtksourceview \
      --with-gtkspell \
      --with-rubrica \
      --with-sqlite \
      --without-nm \
      --without-gnome \
      --with-html-widget=webkit
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
