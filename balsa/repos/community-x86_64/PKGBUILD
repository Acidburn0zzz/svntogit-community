# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Maintainer: Brad Fanella <bradfanella@archlinux.us>
# Contributor: Roman Kyrylych <roman@archlinux.org>

pkgname=balsa
pkgver=2.4.10
pkgrel=1
pkgdesc="An e-mail client for GNOME"
arch=('i686' 'x86_64')
license=('GPL')
url='http://pawsa.fedorapeople.org/balsa/'
depends=('gmime' 'libwebkit' 'libesmtp' 'libnotify' 'gpgme' 'gtksourceview2' 'gtkspell' 'gnome-icon-theme')
makedepends=('perlxml' 'gnome-doc-utils' 'intltool' 'namcap')
install=balsa.install
source=(http://pawsa.fedorapeople.org/${pkgname}/${pkgname}-${pkgver}.tar.bz2)
md5sums=('fa2b7cb9d248912ac2e3dcc08cd6aa5b')

build() {
  	cd ${srcdir}/${pkgname}-${pkgver}

  	./configure --prefix=/usr \
  		--sysconfdir=/etc \
		--localstatedir=/var \
    		--with-ssl \
		--with-gpgme=gpgme-config \
		--with-gss \
		--with-ldap \
		--with-gtksourceview \
		--with-gtkspell \
		--with-rubrica \
		--with-sqlite \
		--without-nm \
        	--without-gnome \
		--with-html-widget=webkit
  	
	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make GTK_UPDATE_ICON_CACHE=/bin/true DESTDIR="${pkgdir}" install
}
