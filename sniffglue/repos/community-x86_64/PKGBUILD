# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=sniffglue
pkgver=0.11.0
pkgrel=1
pkgdesc='Secure multithreaded packet sniffer'
url='https://github.com/kpcyrd/sniffglue'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libseccomp')
makedepends=('cargo')
backup=('etc/sniffglue.conf')
source=(https://github.com/kpcyrd/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/kpcyrd/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz.asc
        sniffglue-sysuser.conf
        sniffglue.conf)
sha512sums=('e01061aa7a638326476cc8adce9340684d802fd9cb2373b77a688b0fa926d725c5434ce743aa97f8212a0ff26c9494f4d9087e784eea20beaa0663681a270915'
            'SKIP'
            '2595eb4856bc7a59c078255bf2aeeaa44a4f6a69b37cc8982e5d70a8ef14cb29b83b4d191ab3baabfbcf209f3effd232c10ec5baabc6ce52990969526d6fda98'
            '66e6b87e832ba8424334d24e3457a214e60e67c731471098329d0ca54c574518f7856ba780bb6bffde53bdefe2c8f789a242138ae67e2fef3e562fa083b362b1')
b2sums=('d468d0a4b81b96de4100e15f47e2c3821e7728169834af322313d7745c5493ec73a0ab300a20d60a310ebc1998b0fbb135c7695afe1f631eb54bfac8c2b75fd0'
        'SKIP'
        '69f5ba2cddca6878b776714c5015c1573df31fc4958432291baa4537349e5bdd1ab4307fd1c00bc20484213c118ecfc2ccfab88b942c0311e4d72872bf4eeeda'
        'ad68689ed08e882f734a613300c14ddfb6d84656dec8dbbed818302b0fe69907f7fc76306f49c0bac019acf4dcb46fec984e7194b16a481f7cc4636ca9aaf303')

validpgpkeys=("64B13F7117D6E07D661BBCE0FE763A64F5E54FD6")

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 target/release/${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 "${srcdir}/sniffglue-sysuser.conf" "${pkgdir}/usr/lib/sysusers.d/sniffglue.conf"
  install -Dm 644 "${srcdir}/sniffglue.conf" -t "${pkgdir}/etc"
  install -Dm 644 docs/sniffglue.1 -t "${pkgdir}/usr/share/man/man1"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
