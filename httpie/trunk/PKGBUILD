# $Id$
# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Thomas Wei√üschuh <thomas_weissschuh lavabit com>

pkgname=httpie
pkgver=0.9.9
pkgrel=3
pkgdesc="cURL for humans"
url="https://github.com/jkbr/httpie"
depends=('python-requests' 'python-pygments' 'python-setuptools')
checkdepends=('python-pytest')
conflicts=(python-httpie)
replaces=(python-httpie python2-httpie)
license=('BSD')
arch=('any')
source=($pkgname-$pkgver.tar.gz::"https://github.com/jkbrzt/httpie/archive/$pkgver.tar.gz")
sha1sums=('81fa9b74df29b4acc31a8f93ec658e5224f31a5e')

build() {
  cd $pkgname-$pkgver
  python3 setup.py build
}

package() {
  cd $pkgname-$pkgver
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/httpie/LICENSE"
  python3 setup.py install --root="$pkgdir" --optimize=1
}

check() {
  cd $pkgname-$pkgver
  # Requires: python-raven python-pbr and pytest-httpbin
  #python3 setup.py test
}
