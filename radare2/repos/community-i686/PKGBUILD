# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.0.1
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='http://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
makedepends=('pkg-config')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz)
sha512sums=('aa8a1af57ee73f48788737fad94d0174b1d529addf151d90b9e67466b103d90494dca520124822ccfc216491bf489a41d8cf40685fa52770112272695d622116')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
