# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=1.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='http://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
makedepends=('pkg-config')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/radare2/archive/${pkgver}.tar.gz
        radare2-fix-build-1.0.patch::https://github.com/radare/radare2/commit/ec4e60d2bd3a2ea2448d275d9631c7097ac8e0bb.patch)
sha512sums=('d1af8d64b81573e61bc90495eeed346e8111dea8aac5abd46f7a50894b2426ec05405e3c530e457bcb64ac631519abdefc47a6f2ddb5933af385d29c773202b8'
            'fc8ed0e72c735cd29bc82c040b9b897c3ce70e48e119d1eed10d545d217dfebe893a1bdda50a17a94aca10a0900b8e1c1b189acddc6ea72171ede75869912b12')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < "${srcdir}/radare2-fix-build-1.0.patch"
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --with-syscapstone \
    --with-syszip \
    --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
