# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=0.10.2
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='http://radare.org'
arch=('i686' 'x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'openssl' 'libzip')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz)
sha512sums=('d2b3ef641aaba1726ddd57095245fe832869ee8d02713d5a7fd80fa8c140f2f685f24580b6ffce68dbf88d9917d82e19d24cb82543072823d5ec8c81d2654352')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --with-syscapstone --with-syszip --with-openssl
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
