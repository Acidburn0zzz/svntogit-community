# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jose Valecillos <valecillosjg (at) gmail (dot) com>
# Contributor: Olivier Ramonat <olivier at ramonat dot fr>
pkgname=gource
pkgver=0.43
pkgrel=12
pkgdesc="software version control visualization"
license=(GPL3)
arch=(i686 x86_64)
url=http://code.google.com/p/gource/
depends=('ftgl' 'sdl2' 'sdl2_image' 'pcre' 'glew' 'boost-libs')
makedepends=('boost' 'glm' 'mesa')
source=(https://github.com/acaudwell/Gource/releases/download/gource-${pkgver}/gource-${pkgver}.tar.gz gource-gcc6.patch)
md5sums=('d2b601782692301f6d8ecc97dc85d3f7'
         '4cfabcea55fe8bc71ebcf0ac06d89f29')

prepare() {
  cd $pkgname-$pkgver
  # Fix build with GCC 6 (Fedora)
  patch -p0 -i ../gource-gcc6.patch
}

build() {
	cd "$srcdir/$pkgname-$pkgver"

	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"

	make DESTDIR="$pkgdir" install
}
# vim: ts=2:sw=2 et:
