# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.12.4
pkgrel=3
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=(any)
url="https://www.django-rest-framework.org/"
license=(BSD)
depends=(python-django)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-django)
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=('python-django-filter: for dynamic queryset filters'
            'python-django-guardian: for a per-object permission model'
            'python-markdown: for markdown support'
            'python-psycopg2: for PostgreSQL support'
            'python-pygments: for syntax highlighting'
            'python-yaml: for YAML support')
# tests are not complete in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz"
  "${pkgname}-3.12.4-datetime_mock.patch::https://github.com/encode/django-rest-framework/commit/a0a2c5cb370ff80a95deaa8d23f099acc4e5e0c5.patch"
)
sha512sums=('406b5354809b94e371a2c7d4b62ce66c18a1f60bd02574cba603ad4c44faa4297bdbbd6ee50de63fe21d2aae18386e05d46983a506bddd17f06ce610d5b727c9'
            'dee6e1e1b5b52d9b2c6856630e8002c64a98b16426705a565b32d9b5235913e81fdb17464a1fcd474a0e1804bf322757afe5f1f274182c0f1e5e1c8948727ddf')
b2sums=('48150e8d966ddd169a2b58d90656efa8f9f549c9d2fdee6025254fba2a6221704fe6bcb246567c8acb963f76ea56046935d3190c7c391daf4773b100a641ba02'
        '8a607bc69d9b5fc7faf73e373286538c99e18148cfb0fb2093ca25a394ecc08a17826df762b826088e3f81ff43288e9b169be4c903e41d0c4b525a7a4fdcf9d3')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not use coverage during testing
  sed -e '/addopts/d' -i setup.cfg
  # fix problems with tests using datetime mock
  patch -Np1 -i ../"${pkgname}-3.12.4-datetime_mock.patch"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
