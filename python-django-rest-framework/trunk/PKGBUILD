# Maintainer: David Runge <dvzrv@archlinux.org>

# weird name on pypi, because reasons
# _name=djangorestframework
_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.12.3
pkgrel=1
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=('any')
url="https://www.django-rest-framework.org/"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=('python-django-filter: for dynamic queryset filters'
            'python-django-guardian: for a per-object permission model'
            'python-markdown: for markdown support'
            'python-psycopg2: for PostgreSQL support'
            'python-pygments: for syntax highlighting'
            'python-yaml: for YAML support')
# tests not in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('073b9f554a98d8e50393a61f17fcaa1245b19d0cc1c1525f8a2fdf62dfe242e250285437f11f3fc3254420832e8666b199aed41f4f5bc2ce2e88af443f05e0f3')
b2sums=('536ea948c0190b9cf2b3f4d4f0d7d34f87a5d06ac436e2824b4712e143773a61636b8dc735ac8c9b852e9caf9702b24aa09e43b940c6a011eeef88d784b6ee01')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not use coverage during testing
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
