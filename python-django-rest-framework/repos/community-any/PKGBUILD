# Maintainer: David Runge <dvzrv@archlinux.org>

# weird name on pypi, because reasons
# _name=djangorestframework
_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.12.0
pkgrel=1
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=('any')
url="https://www.django-rest-framework.org/"
license=('BSD')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=('python-django-filter: for dynamic queryset filters'
            'python-django-guardian: for a per-object permission model'
            'python-markdown: for markdown support'
            'python-psycopg2: for PostgreSQL support'
            'python-pygments: for syntax highlighting'
            'python-yaml: for YAML support')
# tests not in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('d4b178b578457ba1e68bd819788bc2df0cdd0f30f427a3f6cbbe558e47488fe8517b736611f6639cc2d80cd4e44e9230e7ff71ea73c8e1603d77161c02c1e750')
b2sums=('f9c2f647ef8ea2d2e3b9216ee9ed07b3dbe77f999045b1e6288add5477cac8b91e8dfb7f002b8720e8eb98337bd06a9734802b5b28a95ab2901aaadef3ef3e6f')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # we do not use coverage during testing
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  export PYTHONHASHSEED=0
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
