# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-rest-framework
pkgname=python-django-rest-framework
pkgver=3.13.0
pkgrel=1
pkgdesc="A powerful and flexible toolkit for building Web APIs."
arch=(any)
url="https://www.django-rest-framework.org/"
license=(BSD)
depends=(python-django python-pytz)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-django)
# bootstrap without python-django-filter, as it has a checkdepends on
# python-django-rest-framework
optdepends=(
  'python-django-filter: for dynamic queryset filters'
  'python-django-guardian: for a per-object permission model'
  'python-markdown: for markdown support'
  'python-psycopg2: for PostgreSQL support'
  'python-pygments: for syntax highlighting'
  'python-yaml: for YAML support'
)
# tests are not complete in pypi sdist:
# https://github.com/encode/django-rest-framework/issues/7141
source=(
  "${pkgname}-${pkgver}.tar.gz::https://github.com/encode/${_name}/archive/${pkgver}.tar.gz"
)
sha512sums=('2368f382e6dd01490366443ad944f4a48ebbc4928ed45ff58c77a63deb251b148b9e4b13d89b0d2e27a8e257239d75f58f91a11604402683c1c36fac8ce27acc')
b2sums=('8acd6bf386ef20302c8fe52b3a5f1926c03701b32b8c409d13771d632a62e0e01bd8c2efde079e923342a08e6a3d21b3ab13a66817567fe2372f7b8e63eee61a')

prepare() {
  cd "${_name}-$pkgver"
  # we do not use coverage during testing
  sed -e '/addopts/d' -i setup.cfg
}

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
