# Maintainer: David Runge <dvzrv@archlinux.org>

_name=libtmux
pkgname=python-libtmux
pkgver=0.9.0
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('MIT')
depends=('python' 'tmux')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tmux-python/${_name}/archive/v${pkgver}.tar.gz")
# pypi sources don't include the tests: https://github.com/tmux-python/libtmux/issues/173
# source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('d47078300614b2a286eab6063c9492baa115787b917d2fb2e4188032ce580f495f0e7f4a14383407c61063a77d640092d117ba43974524f18da024dfc687c8a5')
b2sums=('fdf6f424ce8eead5f0f19cb53bfd438e9cd7b5326e57e676281f74cedac6e1bcd7cded27e4ef226d2fc3bc09bbb57ff0e63aaa7d901ab910b6a890275c7fe63d')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  # license
  install -vDm 644 LICENSE \
    -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # docs
  install -vDm 644 {CHANGES,README.rst,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
