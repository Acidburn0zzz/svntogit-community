# Maintainer: David Runge <dave@sleepmap.de>
_name=libtmux
pkgbase=python-libtmux
pkgname=('python-libtmux' 'python2-libtmux')
pkgver=0.7.8
pkgrel=1
pkgdesc="Python api for tmux"
arch=('any')
url="https://libtmux.git-pull.com/"
license=('BSD')
#makedepends=('flake8' 'python-setuptools' 'python-isort' 'python2-flake8' 'python2-setuptools' 'python2-isort' )
makedepends=('python-setuptools' 'python2-setuptools' 'tmux')
checkdepends=('python-pytest' 'python2-pytest' 'procps-ng')
source=("$pkgname-$pkgver.tar.gz::https://github.com/tony/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('54c1718211e861666f8d3ab6a9e7b09e2b174f25b20b11744c7e19fbf3eb3de7b4623a35237af0175ea56f20d1b9f86a66f4b72b89418b693851b15acedcac76')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname[0]}-${pkgver}"
  cp -av "${pkgname[0]}-${pkgver}" "${pkgname[1]}-${pkgver}"
}

build() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py build
  cd ../"${pkgname[1]}-${pkgver}"
  python2 setup.py build
}

check() {
  cd "${pkgname[0]}-${pkgver}"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test

  cd ../"${pkgname[1]}-${pkgver}"
  export PYTHONPATH=build:${PYTHONPATH}
  py.test2
}

package_python-libtmux() {
  depends=('python' 'tmux')
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[0]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[0]}" \
    -vDm644 {CHANGES,README.rst,TODO}
}

package_python2-libtmux() {
  depends=('python2' 'tmux')
  cd "${pkgname[1]}-${pkgver}"
  python2 setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  # license
  install -vDm644 LICENSE \
    "${pkgdir}/usr/share/licenses/${pkgname[1]}/LICENSE"
  # docs
  install -t "${pkgdir}/usr/share/doc/${pkgname[1]}" \
    -vDm644 {CHANGES,README.rst,TODO}
}

# vim:set ts=2 sw=2 et:
