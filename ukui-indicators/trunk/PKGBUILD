# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-indicators
pkgver=1.1.8
pkgrel=1
pkgdesc="A small applet to display information from various applications consistently in UKUI panel"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-indicators"
groups=('ukui')
depends=('python' 'ukui-panel' 'webkit2gtk')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-indicators/archive/v$pkgver.tar.gz")
sha512sums=('c7b12660b682af0b06907c68dc4daf5e6a3d082c8f76d60c76512cfeb5527e9e8ed50881d5a363013ded0470e74cddd965cd87424a916806f43cfcc2757cc940')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  cd ../$pkgname-$pkgver
  install -Dm755 diskstatusicon/diskstatusicon -t "$pkgdir"/usr/bin/
  install -Dm644 data/diskstatusicon.desktop -t "$pkgdir"/etc/xdg/autostart/
  install -Dm644 man/diskstatusicon.1 -t "$pkgdir"/usr/share/man/man1/
}
