# Maintainer: Filipe Laíns (FFY00) <lains@archlinux.org>
# Contributor: Windel Bouwman <windel at windel dot nl>
# Contributor: Jakub Klinkovský <kuba.klinkovsky@gmail.com>

_pkgname=pyqtgraph
pkgname=python-$_pkgname
pkgver=0.10.0
pkgrel=8
pkgdesc='Scientific Graphics and GUI Library for Python'
arch=('any')
url='http://www.pyqtgraph.org'
license=('MIT')
makedepends=('python-setuptools')
depends=('python' 'python-pyqt5' 'python-numpy')
optdepends=('python-opengl: 3d graphics'
            'python-pyside2: alternative backend')
source=("$_pkgname-$pkgver.tar.gz::$url/downloads/$pkgver/pyqtgraph-$pkgver.tar.gz")
sha512sums=('4c0a6315c08d65499a9042f418029b8cb1dabb567829c765c3a456dc1df4a8ba1049ccb6980bddb8ef60df504bdc015b87b13064d116cee9e277b15844b3bd7a')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

