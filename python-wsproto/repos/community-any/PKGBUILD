# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgname=python-wsproto
pkgver=0.14.0
pkgrel=2
pkgdesc="WebSockets state-machine based protocol implementation"
url="https://github.com/python-hyper/wsproto"
license=('MIT')
arch=('any')
depends=('python-h11')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.io/packages/source/w/wsproto/wsproto-$pkgver.tar.gz")
sha512sums=('91ffc51229434b8a3c1517b22a082dce0078e38e0f5e7ad016aca9b4d4f2a77c2f9901ea9227e80e61009e70d3ffebb44ed9ae4c988d54a9807b45076e81d43f')

build() {
  cd wsproto-$pkgver
  python setup.py build
}

check() {
  cd wsproto-$pkgver
  python setup.py pytest
}

package() {
  cd wsproto-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/lib/python3.7/site-packages/{,wsproto/}test
}
