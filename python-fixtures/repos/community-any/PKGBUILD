# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-fixtures
pkgname=('python-fixtures' 'python2-fixtures')
pkgver=2.0.0
pkgrel=1
pkgdesc="Fixtures, reusable state for writing clean tests and more"
arch=('any')
license=('Apache')
url="https://launchpad.net/python-fixtures"
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr'
             'python-six' 'python2-six' 'python-testtools' 'python2-testtools')
checkdepends=('python-subunit' 'python2-subunit' 'python-mock' 'python2-mock')
source=("https://pypi.python.org/packages/source/f/fixtures/fixtures-$pkgver.tar.gz")
sha512sums=('ab67b7db8b289df49c3dec5b4d864941e6d34441153f08a119e3a103b83c8a2ea273712e1e053b37580f5045c76cb5c1994ed587a94b0e9ad12d2bfc41b7d7b3')

prepare() {
  cp -a fixtures-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/fixtures-$pkgver
  python setup.py build

  cd "$srcdir"/fixtures-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/fixtures-$pkgver
  python -m testtools.run fixtures.test_suite

  cd "$srcdir"/fixtures-$pkgver-py2
  python2 -m testtools.run fixtures.test_suite
}

package_python-fixtures() {
  depends=('python-pbr' 'python-six' 'python-testtools')

  cd fixtures-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-fixtures() {
  depends=('python2-pbr' 'python2-six' 'python2-testtools')

  cd fixtures-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
