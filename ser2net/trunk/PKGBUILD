# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=ser2net
pkgver=2.8rc3
pkgrel=2
pkgdesc="A proxy that allows telnet/tcp connections to be made to serial ports"
arch=(i686 x86_64)
url="http://sourceforge.net/projects/ser2net"
license=('GPL')
depends=('glibc')
backup=(etc/ser2net.conf)
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver/rc/-rc}.tar.gz
	ser2net.service)
md5sums=('bbe95fb557781c314d321356897a50b9'
         '7f9d879325b9ed6ddc313dcbc3f83e56')

build() {
  cd $srcdir/${pkgname}-${pkgver/rc/-rc}
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/${pkgname}-${pkgver/rc/-rc}
  make DESTDIR=$pkgdir install
  install -Dm0644 ser2net.conf $pkgdir/etc/ser2net.conf
  install -Dm0644 $srcdir/ser2net.service $pkgdir/usr/lib/systemd/system/ser2net.service
}
