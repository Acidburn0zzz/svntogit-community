# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-pifpaf
pkgname=('python-pifpaf' 'python2-pifpaf')
pkgver=1.4.11
pkgrel=1
pkgdesc='Suite of tools and fixtures to manage daemons for testing'
arch=('any')
license=('Apache')
url='https://github.com/jd/pifpaf'
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python-cliff'
             'python2-cliff' 'python-six' 'python2-six' 'python-daiquiri' 'python2-daiquiri'
             'python-fixtures' 'python2-fixtures' 'python-xattr' 'python2-xattr' 'python-jinja'
             'python2-jinja')
checkdepends=('python-testrepository' 'python2-testrepository' 'python-requests' 'python2-requests'
              'python-testtools' 'python2-testtools' 'python-oslotest' 'python2-oslotest'
              'memcached' 'mongodb' 'percona-server' 'percona-server-clients' 'postgresql' 'redis'
              'ceph' 'consul' 'couchdb')
# 'elasticsearch': test fails
# 'rabbitmq': test hangs
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jd/pifpaf/archive/$pkgver.tar.gz")
sha512sums=('7d910b4f6ff831a385a41ddb141d6296bdee8ddea44b766491dd52e06d484f371195d7bab91950e46c225bb81467cda4ed37bc25692f121d4d8e3bf9fad16190')

prepare() {
  cp -a pifpaf-$pkgver{,-py2}

  export LC_CTYPE=en_US.UTF-8
  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/pifpaf-$pkgver
  python setup.py build

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pifpaf-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" python setup.py testr

  cd "$srcdir"/pifpaf-$pkgver-py2
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python2.7/site-packages" PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHON=python2 python2 setup.py testr
}

package_python-pifpaf() {
  depends=('python-pbr' 'python-cliff' 'python-six' 'python-fixtures' 'python-daiquiri'
           'python-xattr' 'python-jinja')

  cd pifpaf-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-pifpaf() {
  depends=('python2-pbr' 'python2-cliff' 'python2-six' 'python2-fixtures' 'python2-daiquiri'
           'python2-xattr' 'python2-jinja')

  cd pifpaf-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1

  mv "$pkgdir"/usr/bin/pifpaf{,2}
}

# vim:set ts=2 sw=2 et:
