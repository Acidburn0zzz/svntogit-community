# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=eom
pkgver=1.22.1
pkgrel=1
pkgdesc="An image viewing and cataloging program for MATE"
url="https://mate-desktop.org"
arch=('x86_64')
license=('GPL')
depends=('dbus-glib' 'gobject-introspection-runtime' 'exempi' 'lcms2' 'libexif' 'libjpeg-turbo' 'mate-desktop' 'libpeas')
makedepends=('gobject-introspection' 'intltool' 'itstool')
groups=('mate-extra')
conflicts=('eom-gtk3')
replaces=('eom-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('64d73069ba4db8515a6c2c90fadba87e1c5cac67dd1e102b271f62e537ee970e')

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--localstatedir=/var \
        	--with-librsvg

    	#https://bugzilla.gnome.org/show_bug.cgi?id=656231
    	sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
