# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-paint
_pkgname=Paint
pkgver=65
pkgrel=1
pkgdesc="Sugar activity to create paintings or drawings"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Paint"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
source=(https://download.sugarlabs.org/sources/honey/$_pkgname/$_pkgname-$pkgver.tar.bz2
        fix-type-error.patch
        fix-text-tool.patch
        fix-drawing-area.patch)
sha256sums=('9b53cf296fac1bfff691d2d2ec8f5db43771e41b9e3dffaf346f99f424132c67'
            'df6ff08acaf4a9f080321a26429ac1820d6afe1916aa12b600ee6b01ce61f4b1'
            'b7cc84895d1c920ccab457fed8987c19bcc5c82b8d9fd0bdaf6e60726fd23750'
            '023c0eefe5168bab0152fe7c6df391d3dccabf806b17bd12ed3539d01e8c1066')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py

  patch -Np1 -i ../fix-type-error.patch
  patch -Np1 -i ../fix-text-tool.patch
  patch -Np1 -i ../fix-drawing-area.patch

  rm -r fill
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
}
