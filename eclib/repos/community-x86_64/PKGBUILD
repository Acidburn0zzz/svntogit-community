# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=eclib
pkgver=20210318
pkgrel=1
pkgdesc="Includes mwrank (for 2-descent on elliptic curves over Q) and modular symbol code used to create the elliptic curve database"
arch=(x86_64)
url="https://github.com/JohnCremona/eclib/"
license=(GPL)
depends=(flint pari boost-libs)
makedepends=(boost)
source=(https://github.com/JohnCremona/eclib/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha256sums=('8ca611e818cc3acdd54c800be66f66219a52603516bff048d350dd1e95605484')

build() {
  cd $pkgname-$pkgver
  ./autogen.sh
  ./configure --prefix=/usr --with-flint=/usr --with-boost
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool # Fix overlinking
  make
}

package() {
  cd $pkgname-$pkgver
  make install DESTDIR="$pkgdir"
}
