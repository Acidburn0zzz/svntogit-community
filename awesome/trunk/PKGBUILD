# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.5.8
pkgrel=1
pkgdesc='Highly configurable framework window manager'
url='http://awesome.naquadah.org/'
arch=('i686' 'x86_64')
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua' 'lua-lgi'
         'pango' 'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms'
         'xcb-util-wm' 'xorg-xmessage')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'ldoc' 'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager')
provides=('notification-daemon')
backup=('etc/xdg/awesome/rc.lua')
source=(${pkgname}-${pkgver}.tar.xz::http://awesome.naquadah.org/download/awesome-${pkgver}.tar.xz)
sha512sums=('be5604821e256e36067750c341ca6f8669e159527b93654c8fc41bcd161da6d87fa0e3c5fccc51821586d163535ac78363d12d99ed81b6fb14e26da59136d5ef')

prepare() {
  cd ${pkgname}-${pkgver}
  mkdir -p build
}

build() {
  cd ${pkgname}-${pkgver}/build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua.so.5.3
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 644 awesome.desktop "${pkgdir}/usr/share/xsessions/awesome.desktop"
  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
