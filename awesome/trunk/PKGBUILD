# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=awesome
pkgver=3.5
pkgrel=1
pkgdesc='Highly configurable framework window manager'
arch=('i686' 'x86_64')
url='http://awesome.naquadah.org/'
license=('GPL2')
depends=(
  'cairo'
  'dbus'
  'gdk-pixbuf2'
  'imlib2'
  'lgi'
  'libxcursor'
  'libxdg-basedir'
  'lua'
  'pango'
  'startup-notification'
  'xcb-util-image'
  'xcb-util-keysyms'
  'xcb-util-wm'
  'xorg-xmessage'
)
makedepends=(
  'asciidoc'
  'cmake'
  'docbook-xsl'
  'doxygen'
  'gperf'
  'imagemagick'
  'xmlto'
)
optdepends=(
  'rlwrap: readline support for awesome-client'
  'feh: image background setter for awsetbg'
  'dex: autostart your desktop files'
  'vicious: widgets for the Awesome window manager'
)
provides=('notification-daemon')
source=("$url/download/$pkgname-$pkgver.tar.xz"
        "$pkgname.desktop")
md5sums=('ce998f19c80bd98f8d119a91f53526df'
         '0fdbeec43d211c6750041d7e37611a6a')

build() {
  cd $pkgname-$pkgver
  make CMAKE_ARGS=' -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DCMAKE_BUILD_TYPE=RELEASE'
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  # install desktop file so you can start awesome from your login manager
  install -Dm644 ../awesome.desktop "$pkgdir/usr/share/xsessions/awesome.desktop"
}

# vim:set ts=2 sw=2 ft=sh et:
