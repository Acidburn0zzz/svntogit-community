# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Jonathan Steel <jsteel at archlinux.org>

pkgname=cmatrix
pkgver=1.2
pkgrel=1
pkgdesc="A curses-based scrolling 'Matrix'-like screen"
arch=('x86_64')
url="http://www.asty.org/cmatrix/"
license=('GPL3')
depends=('ncurses')
makedepends=('fontconfig')
optdepends=('fontconfig: custom font'
            'kbd: cmatrix-tty custom font'
            'xterm: cmatrix-tty custom font')
# "http://www.asty.org/$pkgname/dist/$pkgname-$pkgver.tar.gz"
source=("cmatrix-$pkgver.tgz::https://github.com/abishekvashok/cmatrix/archive/$pkgver.tar.gz"
        "cmatrix-tty")
md5sums=('f74707c4237dc2f33a7fce9dea8395ca'
         '6bb2431c23350cb2082e14b6ba761760')

prepare() {
  cd "$pkgname-$pkgver"
  sed -i 's|/usr/lib/kbd|/usr/share/kbd|' configure{.ac,} config.h.in cmatrix.spec.in Makefile.{in,am}
  sed -i 's|/usr/lib/X11/fonts/misc|/usr/share/fonts/misc|' configure{.ac,} config.h.in Makefile.{in,am}
  sed -i 's| /usr/| $(DESTDIR)/usr/|' Makefile.{in,am}
}

build() {
  cd "$pkgname-$pkgver"

  CPPFLAGS="-D_FORTIFY_SOURCE=0" ./configure \
    --prefix=/usr --mandir=/usr/share/man

  make
}

package() {
  cd "$pkgname-$pkgver"

  install -d "$pkgdir/usr/share/fonts/misc/"
  install -d "$pkgdir/usr/share/kbd/consolefonts/"
  make DESTDIR="$pkgdir" install
  
  install -Dm755 "$srcdir/cmatrix-tty" "$pkgdir/usr/bin/cmatrix-tty"
  rm -f "$pkgdir/usr/share/fonts/misc/fonts.dir"

  for i in AUTHORS NEWS COPYING README ChangeLog INSTALL; do
    install -Dm644 $i "$pkgdir/usr/share/doc/$pkgname/$i"
  done
}
