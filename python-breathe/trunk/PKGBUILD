# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgname=python-breathe
pkgver=4.32.0
pkgrel=1
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output"
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
depends=('python' 'python-sphinx' 'python-docutils')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/michaeljones/breathe/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "${pkgname}-${pkgver}.tar.gz.sig"::"https://github.com/michaeljones/breathe/releases/download/v${pkgver}/breathe-${pkgver}.tar.gz.sig")
sha512sums=('d62d721fa2afa81d501405044d39bd4c0753d4aef82c39c543c2c6d58f5e0e3f97c92b9fb2772e07fb877b85107a6493f34a0099bd71c416843a01a10b4bbb73'
            'SKIP')
validpgpkeys=('8AED58021FEACDD5F27BA0E6A72F627716EA9D96') # Melvin Vermeeren <vermeeren@vermwa.re>

build() {
  cd "breathe-${pkgver}"
  python setup.py build
}

check() {
  cd "breathe-${pkgver}"
  PYTHONPATH="$PWD" make test
}

package_python-breathe() {
  cd "breathe-${pkgver}"
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1
  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
