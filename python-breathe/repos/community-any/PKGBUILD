# Maintainer : Daniel Bermond <dbermondarchlinux-org>
# Maintainer : Santiago Torres-Arias <santiago@archlinux.org>
# Contributor: Simon Boulay <simon.boulay@alkeona.net>

pkgbase=python-breathe
pkgname='python-breathe'
pkgver=4.13.0
pkgrel=2
pkgdesc="An extension to reStructuredText and Sphinx to be able to read and render Doxygen xml output."
arch=('any')
url="https://breathe.readthedocs.org/en/latest/"
license=('BSD')
makedepends=('python' 'git' 'python-setuptools')
BUILDENV=('!check')
source=("git+https://github.com/michaeljones/breathe#tag=v${pkgver}?signed")
checkdepends=('python-nose' 'python-sphinx')
sha256sums=('SKIP')
validpgpkeys=('79DC0FAF18C31807E82E1A6B9EF1DC92B5F301BD') # Melvin Vermeeren <mail@mel.vin>

# FIXME: tests are disabled for now, as sphinx 1.8.1+ break them.
# See upstream bug: https://github.com/michaeljones/breathe/issues/394
check() {
  cd breathe

  PYTHONPATH=$PWD make test
}

build() {
  cd breathe
  python setup.py build
}

package_python-breathe() {
  depends=('python' 'python-setuptools' 'python-sphinx' 'python-docutils' 'python-six')

  cd "breathe"
  python setup.py install --root="${pkgdir}" --skip-build --optimize=1

  install -D -m644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
