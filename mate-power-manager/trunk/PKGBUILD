# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-power-manager
pkgver=1.18.0
pkgrel=2
pkgdesc="Power management tool for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus-glib' 'libcanberra' 'libgnome-keyring' 'libnotify' 'upower')
makedepends=('intltool' 'itstool' 'mate-panel' 'python')
optdepends=('mate-panel: Set brightness and inhibit power management from the panel')
source=("http://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        mate-power-manager-dbus-abort.patch::"https://github.com/mate-desktop/mate-power-manager/commit/52a94894.patch")
groups=('mate-extra')
conflicts=('mate-power-manager-gtk3')
replaces=('mate-power-manager-gtk3')
sha1sums=('1eafa1c74e15f9347b2d9d354de8e14a35c1edcd'
          '8493d2514b800942c5f86391df31b481ad8d4366')

prepare() {
  cd $pkgname-$pkgver
  patch -p1 -i ../mate-power-manager-dbus-abort.patch
}

build() {
    cd ${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --enable-applets \
        --disable-strict
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
