# Contributor: Mikko Seppälä <t-r-a-y@mbnet.fi>
# Contributor: Pierre Schmitz <pierre@archlinux.de>

pkgname=lib32-atk
pkgver=2.30.0
pkgrel=1
pkgdesc="The interface definitions of accessibility infrastructure (32-bit)"
url="https://git.gnome.org/browse/atk"
arch=(x86_64)
license=(LGPL)
depends=(lib32-glib2 atk)
makedepends=(gobject-introspection git gtk-doc meson)
_commit=90ed6bd964b8a2b93c7cf3ea018483e437913785 # tags/ATK_2_30_0^0
source=("git+https://git.gnome.org/browse/atk#commit=$_commit")
sha512sums=('SKIP')

pkgver() {
  cd atk
  git describe --tags | sed 's/^ATK_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd atk
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH=/usr/lib32/pkgconfig

  arch-meson atk build -D enable_docs=false --libdir=/usr/lib32
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -r "${pkgdir}"/usr/{include,share}
}
