# $Id$
# Contributor: Pierre Schmitz <pierre@archlinux.de>
# Maintainer: Mikko Seppälä <t-r-a-y@mbnet.fi>

_pkgname=atk
pkgname=lib32-$_pkgname
pkgver=2.22+1+gd57f97d
pkgrel=1
pkgdesc="A library providing a set of interfaces for accessibility (32-bit)"
arch=('x86_64')
license=('LGPL')
depends=('lib32-glib2' $_pkgbasename)
makedepends=('gcc-multilib' 'git' 'gtk-doc')
_commit=d57f97d831b02071e3a17d8376e5d27c52a523de
source=("git://git.gnome.org/atk#commit=$_commit")
url='https://developer.gnome.org/atk/'
sha256sums=(SKIP)

pkgver() {
  cd $_pkgname
  git describe --tags | sed -e 's/ATK_//' -e 's/_/\./g' -e 's/-/+/g'
}

prepare() {
  cd $_pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd $_pkgname
  ./configure --prefix=/usr --libdir=/usr/lib32 \
    --disable-introspection
  make
}

package() {
  cd $_pkgname
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share}
}
