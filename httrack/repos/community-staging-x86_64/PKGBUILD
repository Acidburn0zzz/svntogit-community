# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcos Heredia <chelqo@gmail.com>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: speps <speps at aur dot archlinux dot org>
# Contributor: Roman Kyrylych <roman@archlinux.org>
# Contributor: orelien <aurelien.foret@wanadoo.fr>
# Contributor: Muflone <webreg@vbsimple.net>

pkgname=httrack
pkgver=3.48.22
pkgrel=3
pkgdesc="An easy-to-use offline browser utility"
arch=('i686' 'x86_64')
url="http://www.httrack.com/"
license=('GPL3')
depends=('bash' 'xdg-utils' 'hicolor-icon-theme' 'openssl')
source=(http://mirror.httrack.com/historical/$pkgname-$pkgver.tar.gz{,.asc} 514c308ec71074228e7a32356646a7ea18576377.patch)
validpgpkeys=('BB71C7E6CB1AD8FAF53FE42A60C3AA7180598EFB') # Xavier Roche
sha256sums=('b2831ad7b48e933959f83a9de8a72bcaa0f8eb87e9453ad85debd50d33a9c48f'
            'SKIP'
            '8c5129ed281a6468238d09f83c2fe5fe8a6e77bc7998d119eb40b8034ff95307')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver
  patch -Np1 -i $srcdir/514c308ec71074228e7a32356646a7ea18576377.patch
  autoconf -i

}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  ./configure --prefix=/usr --enable-static=no
  make
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
