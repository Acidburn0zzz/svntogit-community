# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp1.12
pkgver=1.12.1
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit (legacy 1.12 branch)'
url='http://pupnp.sourceforge.net/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig')
provides=('libupnp-1.12.so' 'libixml-1.12.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-build.patch")
sha512sums=('e4741f9245378529a15b8be7acec5af52d802ce84b82f110dab44c30da65fc2514d51d3bee6311643eb28146ea90bb3c97f44f800edb4f3ccccc1681f3e70f80'
            'c6af9af97e3ef54e45ecec5b4aac5a3819d5a73c36970ec19ba4cec4d3139782ae767ac02d49da3087c8976f6d1fc045e1bdba765c9a41be43c2e4292c0bcbd6')
b2sums=('e44847dda5a7aeabb22a5b76cdc48d1dc69e70f7ecac85824199bf8d6e9b802cb717f2959f70690cc00babf1b554a0cb031995f80c28246b9eec670ebe7aaa90'
        'c51cbf157aeb84b301e93f2757140a2878f13d1a55dea6390ef5bfc02c8e2ed178cf77c720ac73d180e134188d522a94b45e25b4113eca9974fe4a13c6f9f72f')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # rename the shared libraries and include directories, so that they do not
  # conflict with libupnp
  patch -Np1 -i "../${pkgname}-build.patch"
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
