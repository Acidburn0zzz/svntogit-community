# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp1.12
pkgver=1.12.1
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit (legacy 1.12 branch)'
url='http://pupnp.sourceforge.net/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('pkgconfig')
provides=('libupnp-1.12.so' 'libixml-1.12.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz"
        "${pkgname}-build.patch")
sha512sums=('e4741f9245378529a15b8be7acec5af52d802ce84b82f110dab44c30da65fc2514d51d3bee6311643eb28146ea90bb3c97f44f800edb4f3ccccc1681f3e70f80'
            'd7efab75d68d6ac09648e8aff57723460efefd36872875c5c71fbe0af22a86eea8eb650783e2fc4a09230cad7aca06fd8a0b3bc573d6608fae9e6d136519621b')
b2sums=('e44847dda5a7aeabb22a5b76cdc48d1dc69e70f7ecac85824199bf8d6e9b802cb717f2959f70690cc00babf1b554a0cb031995f80c28246b9eec670ebe7aaa90'
        'bd0459fe11175cfe8626b8ff34394716f2d6925d95a45bf445cc6356cc5bdaedff4f8046660f04e91bf062d4684832f6d03ecb020d92b4209bfe8f8204bf4f59')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
  cd ${pkgname}-${pkgver}
  # rename the shared libraries and include directories, so that they do not
  # conflict with libupnp
  patch -Np1 -i "../${pkgname}-build.patch"
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -k check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 {ChangeLog,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
