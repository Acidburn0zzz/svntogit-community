# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Frank Tetzel <frank.tetzel@mailbox.tu-dresden.de>
# Contributor: Hugo Doria <hugo@archlinux.org>
# Contributor: Aurelien Foret <orelien@chez.com>

pkgname=zsnes
pkgver=1.51
pkgrel=11
pkgdesc="Super Nintendo emulator"
url="http://www.zsnes.com/"
arch=('i686' 'x86_64')
license=('GPL')
options=(!buildflags)

if [[ $CARCH == "x86_64" ]]; then
	depends=('gcc-libs-multilib' 'lib32-sdl' 'lib32-libpng>=1.5.0' 'lib32-libgl' 'lib32-ncurses' 'lib32-mesa')
	makedepends=('nasm' 'gcc-multilib')
else
	depends=('gcc-libs' 'sdl' 'libpng>=1.5.0' 'libgl' 'ncurses' 'mesa')
	makedepends=('nasm' 'gcc')
fi

source=(http://downloads.sourceforge.net/${pkgname}/zsnes151src.tar.bz2
	zsnes.patch
	zsnes-1.51-libpng15.patch
	zsnes.desktop
	zsnes_icon.png)
md5sums=('7071186bf80632ae88a153239498d8c9'
         'aa250af3ef8dbf8a44e095d4cd132db2'
         'ea82b4dbf31c87dbcc6c67d02ddfb299'
         'b100b2aa342c551167d7c5dcdcde9dd4'
         'b5260c02adfc3b7ff725041214b00bfd')

build() {
  cd ${srcdir}/${pkgname}_1_51/
  patch -p1 -i ../zsnes.patch
  patch -p0 -i ../zsnes-1.51-libpng15.patch
  
  cd ${srcdir}/${pkgname}_1_51/src
if [ $CARCH == "x86_64" ]; then
  export CC="gcc -m32"
  export CXX="g++ -m32"
fi
  ./autogen.sh --prefix=/usr \
    x_libraries=/usr/lib \
    force_arch=i686 \
    --enable-release
  
  make
  make DESTDIR=${pkgdir} install
  
  # adding --mandir= to autoconf line fails
  install -dm755 ${pkgdir}/usr/share

  install -Dm644 ${srcdir}/zsnes.desktop ${pkgdir}/usr/share/applications/zsnes.desktop
  install -Dm644 ${srcdir}/zsnes_icon.png ${pkgdir}/usr/share/pixmaps/zsnes_icon.png

  mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}
