# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-path.py
pkgver=11.5.1
pkgrel=2
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
depends=('python2-importlib-metadata' 'python2-backports.os')
makedepends=('python2-setuptools-scm')
checkdepends=('python2-pytest-runner' 'python2-pytest-flake8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('ed2b6adb5f6e3184858318d3a94afc744c0f635bc04f2c00d01780bae9c86c3907e0cd64d23242f6df8cab7e0703698c6d39ed4d037f71bf5e3565dfc47f27d6')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path.py-$pkgver
  python2 setup.py build
}

check() {
  cd path.py-$pkgver
  python2 setup.py pytest
}

package() {
  cd path.py-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
