# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python2-path.py
pkgver=11.5.2
pkgrel=1
pkgdesc='A module wrapper for os.path'
arch=('any')
license=('MIT')
url='https://github.com/jaraco/path.py'
depends=('python2-importlib-metadata' 'python2-backports.os')
makedepends=('python2-setuptools-scm')
checkdepends=('python2-pytest-runner' 'python2-pytest-flake8')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jaraco/path.py/archive/$pkgver.tar.gz")
sha512sums=('f1beb0b2cb6426cdd5b620550e10e22169d2dbeaa658b352a0df0abbc3572347034836dd01dd4fa446cc5ebac2fe39c722da1880687128cf5cfaeccf3fdda1a1')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd path.py-$pkgver
  python2 setup.py build
}

check() {
  cd path.py-$pkgver
  python2 setup.py pytest
}

package() {
  cd path.py-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}
