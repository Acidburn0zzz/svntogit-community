# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgname=python2-basemap
pkgver=1.0
pkgrel=1
pkgdesc="Toolkit for plotting data on map projections"
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/basemap/doc/html/"
license=('custom')
depends=('python2-matplotlib' 'geos' 'shapelib' 'python2-numpy') 
makedepends=('python2-distribute')
optdepends=('python-imaging: Python Imaging Library is only needed for bluemarble and warpimage methods.')
provides=('python-matplotlib-basemap')
replaces=('python-matplotlib-basemap')
conflicts=('python-matplotlib-basemap')
source=("http://downloads.sourceforge.net/project/matplotlib/matplotlib-toolkits/basemap-${pkgver}/basemap-${pkgver}.tar.gz")
sha1sums=('c1e2a9e76fdc6582dd951b86b4479cc439e8b372')

build() {
   cd "${srcdir}/basemap-${pkgver}"
 
   # Remove the bundled librarie
   rm -r geos-?.?.?
   sed -i "s/lib64/lib/g" setup.py
  
   python2 setup.py config
   python2 setup.py build
}

package() {
   cd "${srcdir}/basemap-${pkgver}"

   python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr 

   # conflict with files from python-matplotlib
   rm "${pkgdir}"/usr/lib/python2.7/site-packages/mpl_toolkits/__init__.{py,pyc,pyo}

   # License
   install -Dm644 LICENSE_data "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 LICENSE_pyshapelib "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 LICENSE_proj4 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 README "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
