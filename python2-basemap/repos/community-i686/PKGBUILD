# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Julien Morin <jmorin@cp.dias.ie>

pkgname=python2-basemap
pkgver=1.0.4
pkgrel=1
pkgdesc="Toolkit for plotting data on map projections"
arch=('i686' 'x86_64')
url="http://matplotlib.sourceforge.net/basemap/doc/html/"
license=('custom')
depends=('python2-matplotlib' 'geos' 'shapelib' 'python2-numpy' 'python2-httplib2')
optdepends=('python-imaging: Python Imaging Library is only needed for bluemarble and warpimage methods.')
source=("http://downloads.sourceforge.net/project/matplotlib/matplotlib-toolkits/basemap-${pkgver}/basemap-${pkgver}.tar.gz")
sha1sums=('b6d312129d2ae7fb612490516cf87daaf18cb033')

build() {
   cd "${srcdir}/basemap-${pkgver}"

   # Remove the bundled librarie
   rm -r geos-*

   sed -i "s/lib64/lib/g" setup.py

   python2 setup.py config
   python2 setup.py build
}

package() {
   cd "${srcdir}/basemap-${pkgver}"

   _pybasever=2.7

   python2 setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr

   # Conflict with files from python2-matplotlib
   rm "${pkgdir}"/usr/lib/python${_pybasever}/site-packages/mpl_toolkits/__init__.{py,pyc,pyo}

   # License
   install -Dm644 LICENSE_data  "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 LICENSE_pyshp "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 LICENSE_proj4 "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
   install -Dm644 README        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
