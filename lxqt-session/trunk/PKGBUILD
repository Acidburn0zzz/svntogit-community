# $Id$
# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=lxqt-session
pkgver=0.11.0
pkgrel=1
pkgdesc="The LXQt session"
arch=("i686" "x86_64")
groups=("lxqt")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("liblxqt" "lxqt-common" "xdg-user-dirs")
optdepends=(
	"breeze-icons: Default icon theme"
	"oxygen-icons: Alternative icon theme"
	"sddm: The recommended display manager"
)
makedepends=("cmake" "qt5-tools" "git")
source=(
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/$pkgname/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha256sums=(
	"c03e8229ba53a80f4f8807299460ea052ae0916ea991dd1f48274280ef0d8b57"
	"SKIP"
)
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)


build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
