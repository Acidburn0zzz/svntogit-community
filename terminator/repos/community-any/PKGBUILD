# $Id$
# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=1.90
pkgrel=2
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('any')
url='https://gnometerminator.blogspot.com/p/introduction.html'
license=('GPL2')
depends=('libkeybinder3' 'libnotify' 'python2-cairo' 'python2-dbus' 'python2-psutil' 'python2-gobject' 'vte3' 'xdg-utils')
makedepends=('intltool')
source=("https://launchpad.net/terminator/gtk3/${pkgver:0:3}/+download/${pkgname}-${pkgver}.tar.gz"
        "https://launchpad.net/terminator/gtk3/${pkgver:0:3}/+download/${pkgname}-${pkgver}.tar.gz.asc")
md5sums=('7159b1e63e44184ddabdca023d41f763'
         'SKIP')
sha512sums=('bd4a7b3b204bc497462fa4c957442d02ade3f161e5f79314f5c698aa5b186dcfac1efac2dedd3668568967ce7c8c742aa8baa6c78fd3c70e394b345f17cc0cd6'
            'SKIP')
validpgpkeys=('FE67C001C72200A71DE9EFA5DB69E1780B95EF32')

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # python2 fix
    for file in terminatorlib/{,plugins/}*.py; do
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

    # fix run without gconf
    sed -i 's/except ImportError:/except ValueError:/' terminatorlib/config.py
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    python2 setup.py build
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    python2 setup.py install --skip-build --root="${pkgdir}"

    rm -f "${pkgdir}/usr/share/icons/hicolor/icon-theme.cache"
}
