# $Id$
# Maintainer: Guillaume ALAUX <guillaume@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: fancris3 <fancris3 at gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=terminator
pkgver=0.96
pkgrel=4
pkgdesc='Terminal emulator that supports tabs and grids'
arch=('any')
url='http://www.tenshu.net/p/terminator.html'
license=('GPL2')
depends=('vte' 'pygtk' 'xdg-utils' 'python2-notify' 'python2-keybinder2')
makedepends=('desktop-file-utils' 'gettext' 'intltool')
install=terminator.install
source=("https://launchpad.net/terminator/trunk/${pkgver}/+download/${pkgname}_${pkgver}.tar.gz")
sha256sums=('d708c783c36233fcafbd0139a91462478ae40f5cf696ef4acfcaf5891a843201')

build() {
    cd ${pkgname}-${pkgver}

    # python2 fix
    for file in terminatorlib/{,plugins/}*.py; do
        sed -i 's_#!/usr/bin/python_#!/usr/bin/python2_' $file
        sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
    done

    python2 setup.py build
}

package() {
    cd ${pkgname}-${pkgver}

    python2 setup.py install --skip-build --root=${pkgdir}

    rm -f ${pkgdir}/usr/share/applications/${pkgname}.desktop
    rm -f ${pkgdir}/usr/share/icons/hicolor/icon-theme.cache
    install -D -m644 ${srcdir}/${pkgname}-${pkgver}/data/${pkgname}.desktop.in ${pkgdir}/usr/share/applications/${pkgname}.desktop
    sed -i 's|_||' ${pkgdir}/usr/share/applications/${pkgname}.desktop
}
