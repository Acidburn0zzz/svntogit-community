# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=tpm2-totp
pkgver=0.2.1
pkgrel=1
pkgdesc='Attest the trustworthiness of a device against a human using time-based one-time passwords'
arch=('x86_64')
url='https://github.com/tpm2-software/tpm2-totp'
license=('BSD')
depends=('qrencode' 'tpm2-tss')
checkdepends=('ibm-sw-tpm2' 'iproute2' 'oath-toolkit' 'tpm2-tools')
optdepends=('initramfs: dracut and mkinitcpio hooks to display the TOTP during boot')
source=("$url/releases/download/v$pkgver/$pkgname-$pkgver.tar.gz"{,.asc})
sha512sums=('2bda34e89b76319a40206e08c0208b0f139309c0b0e40e7168b5e7562096750df504dfcae3a39e6f82fdccbf7a79ed0802d115fbd7560dc3f5b52d81fe8fb59c'
            'SKIP')
validpgpkeys=('D6B4D8BAC7E0CC97DCD4AC7272E88B53F7A95D84'  # Andreas Fuchs <andreas.fuchs@sit.fraunhofer.de>
              'FE2E6249201CA54A4FB90D066E80CA1446879D04') # Jonas Witschel <diabonas@gmx.de>

build() {
	cd "$pkgname-$pkgver"
	./configure --prefix=/usr \
	            --libexecdir=/usr/lib \
	            --with-dracutmodulesdir=/usr/lib/dracut/modules.d \
	            --with-mkinitcpiodir=/usr/lib/initcpio \
	            $( ((CHECKFUNC)) && echo --enable-integration)
	make
}

check() {
	cd "$pkgname-$pkgver"
	make check
}

package() {
	cd "$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
