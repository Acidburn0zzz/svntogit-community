# $Id$
# Maintainer: Jonathan Conder <jonno dot conder at gmail dot com>
# Contributor: Arkham <arkham at archlinux dot us>
# Contributor: Nathan Jones <nathanj@insightbb.com>
# Contributor: Javier "Phrodo_00" Aravena <phrodo.00 at gmail dot com>
# Contributor: angvp <angvp at archlinux dot us>
# Contributor: Allan <mcrae_allan at hotmail dot com>
# Contributor: w0rm <w0rmtux at gmail dot com>
# Contributor: vEX <vex at niechift dot com>
# Contributor: Asher256 <achrafcherti at gmail dot com>

pkgbase=desmume
pkgname=('desmume-core' 'desmume-cli' 'desmume-gtk' 'desmume-glade')
pkgver=0.9.6
pkgrel=2
pkgdesc="Nintendo DS emulator"
arch=('i686' 'x86_64')
url="http://desmume.org/"
license=('GPL')
makedepends=('agg' 'gtkglext' 'intltool' 'libgl' 'libglade' 'mesa')
source=("http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver-1.tar.gz")
sha256sums=('9dabdb7a5e89aca5b8427fe12a712b6ead52a39de47c7f136db69200b6930742')

build() {
  cd "$srcdir/$pkgbase-$pkgver"
  ./configure --prefix=/usr --enable-wifi
  make
}

package_desmume-core() {
  conflicts=('desmume')

  cd "$srcdir/$pkgbase-$pkgver"
  make -C po DESTDIR="$pkgdir/" install
}

package_desmume-cli() {
  depends=("desmume-core=$pkgver" 'agg' 'glib2' 'mesa')

  cd "$srcdir/$pkgbase-$pkgver"
  make -C src/cli DESTDIR="$pkgdir/" install
}

package_desmume-gtk() {
  depends=("desmume-core=$pkgver" 'agg' 'gtk2')
  conflicts=('desmume-glade')
  replaces=('desmume')

  cd "$srcdir/$pkgbase-$pkgver"
  make -C src/gtk DESTDIR="$pkgdir/" install
}

package_desmume-glade() {
  depends=("desmume-core=$pkgver" 'agg' 'gtkglext' 'libglade')
  conflicts=('desmume-gtk')

  cd "$srcdir/$pkgbase-$pkgver"
  make -C src/gtk-glade DESTDIR="$pkgdir/" install
}
