# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.10
pkgrel=1
pkgdesc="LUA embedding for pd"
arch=('x86_64')
url="https://github.com/agraef/pd-lua"
license=('GPL2')
groups=('pd-externals' 'pro-audio')
depends=('gcc-libs' 'glibc' 'lua' 'pd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agraef/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('06f805d32f1fb38384e38b4efe7e0e2f7f3aec120ca2c972f05614d9915c2e1efbccda80df29bcda0bd49c29fcb1dfa99424a32a55ae50e32a23f3dda921d47f')
b2sums=('e5ca818ee06dd1fe2caae7903ab032b3effd36bae921ea02b6cd108c75dffcf5d08490903c8c2211e2886518e8f3eeceea96dcf6bc24e45331b2086fc692e373')

prepare() {
  cd "$pkgname-$pkgver"
  # not installing license and README (to wrong location)
  sed -e '/COPYING /d' \
      -e '/README /d' \
      -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make LDFLAGS="${LDFLAGS}" CFLAGS="${CFLAGS}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" prefix='/usr' install
  # docs
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"* \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"
}
