# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=pd-lua
pkgver=0.9
pkgrel=3
pkgdesc="LUA embedding for pd"
arch=('x86_64')
url="https://github.com/agraef/pd-lua"
license=('GPL2')
groups=('pd-externals' 'pro-audio')
depends=('gcc-libs' 'glibc' 'lua53' 'pd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agraef/${pkgname}/archive/${pkgver}.tar.gz"
        "${pkgname}-0.9-lua53.patch")
sha512sums=('edcb4a9e65ca4a5816e0ad326946af74771edf163925ce5e0f71371ffff3cae8386c4d2b696c9f815859bb38105af7d6118425cdf797c89498962d58ff0f7233'
            'b48030d03386334c8efa112e11aca661cad15000d8dc5d9c150aaef35f4e489eb6f0eaa4cf73fc5e123135be6d86288db4c19f61267678d7147dc5ad946dea8b')
b2sums=('e836f11ca67dcc9ab2633fe599c8c094563f5284c54744584fdd24005c82669a424e3278e3595a79207228d2856c8663bbd2cd4bbc29493b5319c22df58c0384'
        '0940a21fc9968cde07d1d2e9efc25077733706788e8ab5182afbd4d9b38da55c6f200a7ac0ad4f73fc99a80b2488b6b706f1da9ab0ca7dcd711b3b3a09abf9d3')

prepare() {
  cd "$pkgname-$pkgver"
  # installing external to correct location
  # not installing license and README (to wrong location)
  sed -e 's/-externals/\/extra/' \
      -e '/COPYING /d' \
      -e '/README /d' \
      -i Makefile
  # building against lua53 due to issues with lua >= 5.4
  # https://github.com/agraef/pd-lua/issues/7
  patch -Np1 -i "../${pkgname}-0.9-lua53.patch"
}

build() {
  cd "$pkgname-$pkgver"
  make LDFLAGS="${LDFLAGS}" CFLAGS="${CFLAGS}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" prefix='/usr' install
  # docs
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"* \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"
}
