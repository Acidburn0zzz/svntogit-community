# Maintainer: David Runge <dave@sleepmap.de>
pkgname=pd-lua
pkgver=0.9
pkgrel=2
pkgdesc="LUA embedding for pd"
arch=('x86_64')
url="https://github.com/agraef/pd-lua"
license=('GPL2')
groups=('pd-externals' 'pro-audio')
depends=('lua' 'pd')
source=("$pkgname-$pkgver.tar.gz::https://github.com/agraef/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('edcb4a9e65ca4a5816e0ad326946af74771edf163925ce5e0f71371ffff3cae8386c4d2b696c9f815859bb38105af7d6118425cdf797c89498962d58ff0f7233')

prepare() {
  cd "$pkgname-$pkgver"
  # installing external to correct location
  # not installing license and README (to wrong location)
  sed -e 's/-externals/\/extra/' \
      -e '/COPYING /d' \
      -e '/README /d' \
      -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make LDFLAGS="${LDFLAGS}" CFLAGS="${CFLAGS}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" prefix='/usr' install
  # docs
  install -vDm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  mv -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"* \
    "${pkgdir}/usr/share/doc/${pkgname}"
  rmdir -v "${pkgdir}/usr/lib/pd/extra/pdlua/doc/"
}
