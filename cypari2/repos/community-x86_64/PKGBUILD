# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgbase=cypari2
pkgname=(python-cypari2 python2-cypari2)
pkgver=2.0.2
pkgrel=1
pkgdesc="Cython bindings for PARI"
arch=(x86_64)
url="https://www.sagemath.org"
license=(LGPL)
makedepends=(cython2 cython python2-cysignals python-cysignals)
source=($pkgname-$pkgver.tar.gz::"https://github.com/defeo/cypari2/archive/$pkgver.tar.gz")
sha256sums=('77216af4e91c80e041487ae9b6e21f74752ac697cf9a2b24ddeb2205bffaf793')

prepare() {
  cp -r $pkgbase-$pkgver{,-py2}
}

build() {
  cd $pkgbase-$pkgver
  python setup.py build

  cd "$srcdir"/$pkgbase-$pkgver-py2
  python2 setup.py build
}

package_python-cypari2() {
  depends=(python-cysignals)

  cd $pkgbase-$pkgver
  python setup.py install --root="$pkgdir"
}

package_python2-cypari2() {
  depends=(python2-cysignals)

  cd $pkgbase-$pkgver-py2   
  python2 setup.py install --root="$pkgdir"
}
