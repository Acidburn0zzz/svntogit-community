# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-lingua-en-inflect
pkgver=1.903
pkgrel=1
pkgdesc='Convert singular to plural. Select "a" or "an"'
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Lingua-EN-Inflect"
depends=('perl')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/D/DC/DCONWAY/Lingua-EN-Inflect-$pkgver.tar.gz")
sha512sums=('2f629e1dd9eddba123423c95531907a7c0a7eb0bc99ee425ea2c3cdb4059af0e78625c45392eb65780c8b06da8eec790e242d51912c87a15bcf5d466d68549ce')

build() {
  cd Lingua-EN-Inflect-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Lingua-EN-Inflect-$pkgver
  make test
}

package() {
  cd Lingua-EN-Inflect-$pkgver
  make DESTDIR="$pkgdir" install
}
