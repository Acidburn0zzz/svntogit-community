# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: Jan-Erik Rediger <badboy at archlinux dot us>

pkgname=kcov
pkgver=37
pkgrel=1
pkgdesc="Code coverage tester for compiled programs, Python scripts and shell scripts"
arch=('x86_64')
url="http://simonkagstrom.github.com/kcov/index.html"
license=('GPL2')
depends=('elfutils' 'curl')
makedepends=('cmake' 'python')
source=("https://github.com/SimonKagstrom/kcov/archive/v${pkgver}.tar.gz")
sha512sums=('86ce75d13bbed4aac880483693843a3fb7a335e4198f8c6e3c76560061ee0e6bb2de5a9ead081100eb73a13576197951236d964d86c901b07d205bdf9df7061c')

prepare() {
  cd $pkgname-$pkgver

  rm -rf build
  mkdir -p build
}

build() {
  cd $pkgname-$pkgver/build

  cmake .. -DCMAKE_INSTALL_PREFIX=/usr
  make
}

package() {
  cd $pkgname-$pkgver/build

  make DESTDIR="$pkgdir/" install
  make clean
}

# vim:set ts=2 sw=2 et:
