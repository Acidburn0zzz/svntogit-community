# $Id$
# Maintainer: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Jelle van der Waa <jelle@vdwaa.nl>

pkgbase=python-pymongo
pkgname=('python-pymongo' 'python2-pymongo')
pkgver=2.5
pkgrel=1
pkgdesc='Python driver for MongoDB'
arch=('x86_64' 'i686')
license=('APACHE')
url='http://pypi.python.org/pypi/pymongo/'
makedepends=('python-distribute' 'python2-distribute')
source=("http://pypi.python.org/packages/source/p/pymongo/pymongo-$pkgver.tar.gz")
sha256sums=('79aff1c77ec6be6158a4d1a11002400ee0eb8f74dc6f4f67527f59bfac78c222')

build() {
  cd "$srcdir"

  cp -a "pymongo-$pkgver" "pymongo2-$pkgver"
}

package_python2-pymongo() {
  depends=('python2')

  cd "$srcdir/pymongo2-$pkgver"
  python2 setup.py install --root="$pkgdir" --optimize=1
}

package_python-pymongo() {
  depends=('python')

  cd "$srcdir/pymongo-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
