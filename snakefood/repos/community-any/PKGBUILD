# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=snakefood
pkgver=1.4
pkgrel=1
pkgdesc="Generate dependency graphs from Python code"
arch=('any')
url="http://furius.ca/snakefood/"
license=('GPL')
depends=('python2')
optdepends=('graphviz: generate dependency graphs')
source=("https://pypi.python.org/packages/source/s/$pkgname/$pkgname-$pkgver.tar.gz")
md5sums=('56c88667a33d8909b0aabf2ab6903bdf')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' lib/python/snakefood/checker.py
}

build() {
  cd $pkgname-$pkgver
  python2 setup.py build
}

package() {
  cd $pkgname-$pkgver
  python2 setup.py install --root="$pkgdir" --optimize=1
}
