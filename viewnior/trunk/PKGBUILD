# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
# Maintainer: M Rawash <mrawash@gmail.com>

pkgname=viewnior
pkgver=1.7
pkgrel=1
pkgdesc="A simple, fast and elegant image viewer program"
arch=('x86_64')
url="https://siyanpanayotov.com/project/viewnior"
license=('GPL3')
depends=('gtk2' 'exiv2')
makedepends=('meson')
source=(https://github.com/hellosiyan/Viewnior/archive/viewnior-$pkgver.tar.gz)
md5sums=('b09587081077b50f9cc51bb4b0223ffe')

build() {
  cd Viewnior-viewnior-$pkgver
  arch-meson build
  ninja -C build
}

check() {
  cd Viewnior-viewnior-$pkgver
  meson test -C build
}

package() {
  cd Viewnior-viewnior-$pkgver
  DESTDIR="$pkgdir" meson install -C build
}
