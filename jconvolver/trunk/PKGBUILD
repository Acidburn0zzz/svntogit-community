# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=jconvolver
pkgver=1.0.0
pkgrel=1
pkgdesc="A real-time convolution engine"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2')
groups=('pro-audio')
depends=('clthreads' 'jack' 'libsndfile' 'zita-convolver')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-${pkgver}.tar.bz2"
        "https://kokkinizita.linuxaudio.org/linuxaudio/downloads/${pkgname}-reverbs.tar.bz2")
sha512sums=('a46d0ccd054d6c3a3cc90d95b2a1e2b93969db333e565e9a16dfb7469210b2f215412590da440dbe0ef807cdecd6c3fd0c20bfd9f692190946174a81d0a5ca68'
            'aa79dab5cb5326e6eddbf99a3cfae97cf5052db9d75c74346bf70f1b210db5576e31cff70c3d6dc8ff88b8eff74e4163a770fd537db978c2b955855f8b2dfc7b')
validpgpkeys=()

prepare() {
  cd "$pkgname-$pkgver"
  # removing optimizations
  sed -e '/march=native/d' -i source/Makefile-linux
}

build() {
  cd "$pkgname-$pkgver/source"
  make LIBDIR=lib PREFIX=/usr
}

package() {
  cd "$pkgname-$pkgver/source"
  make DESTDIR="$pkgdir/" PREFIX=/usr install
  # docs
  install -vDm 644 ../{AUTHORS,README,README.CONFIG} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
  # configs and examples
  cd ../config-files
  for folder in $(ls |grep -vE "(.conf|.wav)"); do
    install -vDm 644 "$folder/"* \
      -t "${pkgdir}/usr/share/doc/${pkgname}/examples/${folder}/"
  done
  install -vDm 644 *.{wav,conf} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/"
  cd "${srcdir}/reverbs"
  for folder in $(ls |grep -vE "(.conf|.wav)"); do
    install -vDm 644 "$folder/"* \
      -t "${pkgdir}/usr/share/doc/${pkgname}/examples/${folder}/"
  done
  install -vDm 644 *.wav \
    -t "${pkgdir}/usr/share/doc/${pkgname}/examples/demo-reverbs/"
}
