# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgbase=python-oslo-utils
pkgname=(python-oslo-utils python2-oslo-utils)
pkgver=3.36.2
pkgrel=1
pkgdesc="Oslo Utility library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.utils/$pkgver"
license=('Apache')
makedepends=('python-setuptools' 'python2-setuptools' 'python-pbr' 'python2-pbr' 'python2-funcsigs'
             'python-iso8601' 'python2-iso8601' 'python-oslo-i18n' 'python2-oslo-i18n'
             'python2-monotonic' 'python-pytz' 'python2-pytz' 'python-netaddr' 'python2-netaddr'
             'python-netifaces' 'python2-netifaces' 'python-debtcollector' 'python2-debtcollector')
checkdepends=('python-oslotest' 'python2-oslotest' 'python-ddt' 'python2-ddt')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/openstack/oslo.utils/archive/$pkgver.tar.gz")
sha512sums=('3b82912f5b04671b430c92867203cef8bef038777cfbcf68d3136d97093e63641a308fe80f628d8d940c3d0618330bd875ef4eaf2845a053637b3d7d16616580')

prepare() {
  cp -a oslo.utils-$pkgver{,-py2}

  # Use python 3's standard monotonic function
  sed -i '/monotonic/d' oslo.utils-$pkgver/requirements.txt
  sed -i 's/from monotonic import monotonic/from time import monotonic/' oslo.utils-$pkgver/oslo_utils/timeutils.py

  export PBR_VERSION=$pkgver
}

build() {
  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py build

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py testr || warning "Tests failed"

  cd "$srcdir"/oslo.utils-$pkgver-py2
  PYTHON=python2 python2 setup.py testr || warning "Tests failed"
}

package_python-oslo-utils() {
  depends=('python-six' 'python-pbr' 'python-iso8601' 'python-oslo-i18n' 'python-pytz'
           'python-netaddr' 'python-netifaces' 'python-debtcollector')

  cd "$srcdir"/oslo.utils-$pkgver
  python setup.py install --root="$pkgdir/" --optimize=1
}

package_python2-oslo-utils() {
  depends=('python2-six' 'python2-pbr' 'python2-iso8601' 'python2-oslo-i18n' 'python2-monotonic'
           'python2-pytz' 'python2-netaddr' 'python2-netifaces' 'python2-debtcollector'
           'python2-funcsigs')

  cd "$srcdir"/oslo.utils-$pkgver-py2
  python2 setup.py install --root="$pkgdir/" --optimize=1
}

# vim:set ts=2 sw=2 et:
