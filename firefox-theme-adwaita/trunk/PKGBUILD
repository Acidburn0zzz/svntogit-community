# $Id$
# Maintainer: Muflone http://www.muflone.com/contacts/english/
# Contributor: Diego Principe <cdprincipe@at@gmail@dot@com>

pkgname=firefox-theme-gnome
_pkgname=firefox-gnome
pkgver=45.1
pkgrel=1
pkgdesc="Adwaita theme for Firefox (Matches the default GNOME Shell theme)"
url="https://addons.mozilla.org/en-US/firefox/addon/adwaita/"
arch=('any')
license=('MPL')
depends=("firefox")
makedepends=('python2')
provides=('firefox-theme-adwaita')
replaces=('firefox-theme-adwaita')
conflicts=('firefox-theme-adwaita')
source=("https://addons.mozilla.org/firefox/downloads/file/410922/gnome_3-${pkgver}-fx-linux.xpi")
sha256sums=('d230a3cc20739696281d7bb5aa5bb395afce782d53a97ab5270278602e4cb274')

package() {
  install -m 755 -d "${pkgdir}/usr/lib/firefox/browser/extensions"
  _emid=$(sed -n '/.*<em:id>\(.*\)<\/em:id>.*/{s//\1/p;q}' install.rdf)
  install -m 644 "gnome_3-${pkgver}-fx-linux.xpi" "${pkgdir}/usr/lib/firefox/browser/extensions/${_emid}.xpi"
}

