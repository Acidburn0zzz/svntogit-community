# $Id$
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Mirco Tischler <mt-ml at gmx dot de>

pkgname=fwupd
pkgver=1.0.5
pkgrel=2
pkgdesc='A simple daemon to allow session software to update firmware'
arch=('x86_64')
url='https://github.com/hughsie/fwupd'
license=('GPL2')
depends=('appstream-glib' 'fwupdate' 'colord' 'python')
makedepends=('meson' 'valgrind' 'gtk-doc' 'help2man' 'python-pillow' 'gobject-introspection'
             'python-cairo' 'ttf-dejavu' 'adobe-source-han-sans-cn-fonts' 'python-gobject')
checkdepends=('umockdev')
source=(${pkgname}-${pkgver}.tar.gz::"${url}/archive/${pkgver}.tar.gz"
        'capsule-changes.patch'
        'config-esp-path.patch')
sha256sums=('236289aab278fc6611e0a425f7a186895d6755cfdbfd9f182c7bd4bb7fee0324'
            '5361c6cfc2b7d0cb9a161b7082b780ba30b468347b6489dd32a83e7a376716ac'
            '541b7d31eee9fc9386ad451bf92bb17f400216183bd76c554e7628c7006e90ba')

prepare() {
    cd ${pkgname}-${pkgver}
    patch -p1 -i ../capsule-changes.patch
    patch -p1 -i ../config-esp-path.patch
}

build() {
    cd ${pkgname}-${pkgver}
    arch-meson -D b_lto=false ../build
    ninja -v -C ../build
}

check() {
    cd build
    meson test
}

package() {
    DESTDIR="${pkgdir}" ninja -C build install
    # Fixup mode to match polkit
    install -d -o root -g 102 -m 750 "${pkgdir}/usr/share/polkit-1/rules.d"
    # Remove the tests
    rm -r ${pkgdir}/usr/share/installed-tests/
}
