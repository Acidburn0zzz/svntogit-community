# Maintainer: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=2.0.3
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('i686' 'x86_64')
license=('GPL')
url="https://github.com/linuxmint/cinnamon-screensaver"
backup=(etc/pam.d/cinnamon-screensaver)
depends=('cinnamon' 'cinnamon-desktop')
makedepends=('intltool' 'gnome-common')
options=(!emptydirs)
source=($pkgname-$pkgver.tar.gz::https://github.com/linuxmint/cinnamon-screensaver/archive/$pkgver.tar.gz
       cinnamon-screensaver.pam
       fix-dbus-compatibility.patch
       lock_screen_on_suspend.patch)
sha256sums=('00c866e98994afc8939d1d347de631cebaa6ec34ad59646a7ccd223bd757cc7d'
            'b6ea9e2eb586d94bcabb617a8f1c2958111df87afdbb51f645882bccdc15cbda'
            'eebd1d056c21d2d3f60fb954677b9214fd1d575ed1c2bbb56326cedf41a39eb6'
            '516c479558576c6c5a2509abfcbf4fdafb5953d252e7a4ab972f9db6137daca8')

prepare() {
  cd $pkgname-$pkgver

  # Fix compatibility with gnome-screensaver's D-Bus interface and various fixes
  patch -Np1 -i ${srcdir}/fix-dbus-compatibility.patch

  # Lock screen on suspend with systemd
  patch -Np1 -i ${srcdir}/lock_screen_on_suspend.patch
}

build() {
  cd $pkgname-$pkgver

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
               --libexecdir=/usr/lib/cinnamon-screensaver \
               --localstatedir=/var \
               --with-mit-ext --with-systemd
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../cinnamon-screensaver.pam "$pkgdir/etc/pam.d/cinnamon-screensaver"

  # Autostart only in Cinnamon
  install -d "$pkgdir/etc/xdg/autostart/"
  cp "$pkgdir/usr/share/applications/cinnamon-screensaver.desktop" \
    "$pkgdir/etc/xdg/autostart/cinnamon2d-screensaver.desktop"
  cp "$pkgdir/usr/share/applications/cinnamon-screensaver.desktop" \
    "$pkgdir/etc/xdg/autostart/cinnamon-screensaver.desktop"
  sed -i 's/^OnlyShowIn=.*/OnlyShowIn=GNOME;/' \
    "$pkgdir"/etc/xdg/autostart/cinnamon{,2d}-screensaver.desktop
  echo 'AutostartCondition=GNOME3 if-session cinnamon' \
    >> "$pkgdir/etc/xdg/autostart/cinnamon-screensaver.desktop"
  echo 'AutostartCondition=GNOME3 if-session cinnamon2d' \
    >> "$pkgdir/etc/xdg/autostart/cinnamon2d-screensaver.desktop"
}
