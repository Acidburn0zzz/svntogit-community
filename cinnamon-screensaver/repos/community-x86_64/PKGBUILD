# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alexandre Filgueira <alexfilgueira@cinnarch.com>
# Based on gnome-screensaver package:
# Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Jan de Groot <jgc@archlinux.org>

pkgname=cinnamon-screensaver
pkgver=5.0.5
pkgrel=1
pkgdesc="Screensaver designed to integrate well with the Cinnamon desktop."
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('accountsservice' 'cinnamon-desktop' 'libgnomekbd'
         'python-cairo' 'python-gobject' 'python-setproctitle' 'python-xapp' 'xapp')
optdepends=('cinnamon-translations: i18n')
makedepends=('gobject-introspection' 'meson' 'samurai')
backup=('etc/pam.d/cinnamon-screensaver')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha512sums=('18cf174c372f2082b3984fab2fc6e18a41f0f508230fdf91e54b8d0d3c7cf1de1009327716eacf33a7975e0983882abb41944b1470b54f1bd77b7ced8570bea1')
b2sums=('5bfe290547fe1bb8cd67cd99781b6ab59e7c146fd9775e4b73544a697e3f56793a998dcd55124df2df568877a3abcb1938ab9a695325562f286e6c03b44f8789')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib/cinnamon-screensaver \
          --buildtype=plain \
          ..
    samu
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
}
