# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-kwin
pkgver=0.0.7
pkgrel=2
pkgdesc='KWin configures on DDE'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-kwin"
license=('GPL3')
depends=('deepin-qt5integration' 'kwin')
makedepends=('expac' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-kwin/archive/$pkgver.tar.gz")
sha512sums=('6e333558e4434e346aae3cf1678af9fb44510d94f92e50e81766c418de4cd7a344023a7057a4d1a0a591180c858fc7c526d7590a1c284b4e3f89d087412fb8e7')

build() {
  cd dde-kwin-$pkgver
  qmake-qt5 PREFIX=/usr KWIN_VERSION=$(expac %v kwin | cut -d - -f 1)
  make
}

package() {
  cd dde-kwin-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  chmod +x "$pkgdir"/usr/bin/kwin_no_scale
}
