# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=arm-elf-binutils
pkgver=2.23.2
pkgrel=1
_xprefix=/usr
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(i686 x86_64)
license=(GPL)
options=(!libtool)
url="http://sources.redhat.com/binutils"
depends=('glibc' 'zlib')
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.gz)
md5sums=('cda9dcc08c86ff2fd3f27e4adb250f6f')

prepare() {
  cd $srcdir/binutils-${pkgver}
  sed -i 's|-Wall|-Wall -O2|g' libiberty/configure
  sed -i 's|$CC -E|$CC -E -O2|g' libiberty/configure
#  sed -i '1,1i#include <stdlib.h>' include/hashtab.h
#  sed -i '1,1i#include <limits.h>' libiberty/fibheap.c
#  sed -i '1,1i#include <sys/stat.h>' libiberty/getpwd.c
#  sed -i '1,1i#include <fcntl.h>' libiberty/pex-unix.c
#  sed -i '1,1i#include <unistd.h>' libiberty/pex-{common.h,unix.c}
}

build() {
  cd $srcdir/binutils-${pkgver}

  [ $NOEXTRACT -eq 1 ] || ./configure --prefix=${_xprefix} \
	--program-prefix=arm-elf- \
	--enable-shared \
	--disable-multilib \
	--with-lib-path=${_xprefix}/lib/binutils/arm-elf \
	--disable-nls \
	--target=arm-elf \
	--host=$CHOST \
	--build=$CHOST \
	--with-sysroot=/usr/$CHOST/arm-elf

  make configure-host
  make tooldir=$pkgdir/${_xprefix}
}

package() {
  cd $srcdir/binutils-${pkgver}
  make prefix=$pkgdir/${_xprefix} tooldir=$pkgdir/${_xprefix} install

  mkdir -p $pkgdir/${_xprefix}/lib/binutils/arm-elf
  cp -v include/libiberty.h $pkgdir/${_xprefix}/lib/binutils/arm-elf

  rm -f $pkgdir/${_xprefix}/man/man1/{dlltool,nlmconv,windres}*

  rm -f $pkgdir/usr/bin/ar
  rm -f $pkgdir/usr/bin/as
  rm -f $pkgdir/usr/bin/ld
  rm -f $pkgdir/usr/bin/nm
  rm -f $pkgdir/usr/bin/objdump
  rm -f $pkgdir/usr/bin/ranlib
  rm -f $pkgdir/usr/bin/strip
  rm -f $pkgdir/usr/bin/objcopy
  rm -f $pkgdir/usr/lib/libiberty.a
  rm -rf $pkgdir/usr/share
}
