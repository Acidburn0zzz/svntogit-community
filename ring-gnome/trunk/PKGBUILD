# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>

pkgname=ring-gnome
pkgver=20180414.2.2c51f89
pkgrel=2
epoch=3
pkgdesc="Ring is a free and universal communication platform which preserves the users' privacy and freedoms (GNOME client)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=("ring")
depends=("libringclient" "ring-daemon"
         "gtk3" "dconf" "clutter" "clutter-gtk" "webkit2gtk" "qrencode"
         "qt5-base" "gtk-update-icon-cache" "desktop-file-utils"
         "evolution-data-server" "libnotify" "libcanberra" "libnm")
makedepends=('git' 'cmake')
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-client-gnome#commit=1e2dd354a324c37561500440524864d778f3b614"
        "0001-settings-check-if-g_get_user_special_dir-fails.patch")
sha256sums=('SKIP'
            'f6e36863d5d4e7a6d7e5a29abb46a674186f2842dadea28ab1fd57a267b0bc5b')

prepare() {
  cd "${srcdir}/ring-client-gnome"
  patch -p1 < "${srcdir}/0001-settings-check-if-g_get_user_special_dir-fails.patch"
}

build() {
  cd "ring-client-gnome"

  msg2 'Building...'
  mkdir -p build
  cd build
  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=Release
  make
}

package() {
  cd "ring-client-gnome/build"

  msg2 'Installing...'
  make DESTDIR="$pkgdir" install

  msg2 'Cleaning up pkgdir...'
  find "$pkgdir" -type d -name .git -exec rm -r '{}' +
  find "$pkgdir" -type f -name .gitignore -exec rm -r '{}' +
}

# vim:set ts=2 sw=2 et:
