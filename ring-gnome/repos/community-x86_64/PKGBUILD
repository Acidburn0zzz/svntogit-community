# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ring-gnome
pkgver=20190103
pkgrel=1
epoch=3
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (GNOME client)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=('ring')
depends=('libringclient' 'ring-daemon'
         'gtk3' 'dconf' 'clutter' 'clutter-gtk' 'webkit2gtk' 'qrencode'
         'qt5-base' 'gtk-update-icon-cache' 'desktop-file-utils'
         'evolution-data-server' 'libnotify' 'libcanberra' 'libnm')
makedepends=('git' 'cmake')
_commit=32e606106920a21da416ef365fb654b5df721098
source=("git+https://gerrit-ring.savoirfairelinux.com/ring-client-gnome#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ring-client-gnome \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
