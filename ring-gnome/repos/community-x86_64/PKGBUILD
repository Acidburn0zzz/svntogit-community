# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=ring-gnome
pkgver=20190315
pkgrel=1
epoch=3
pkgdesc="Free and universal communication platform which preserves the usersâ€™ privacy and freedoms (GNOME client)"
arch=("x86_64")
url="https://ring.cx"
license=('GPL3')
groups=('ring')
depends=('libringclient' 'ring-daemon'
         'gtk3' 'dconf' 'clutter' 'clutter-gtk' 'webkit2gtk' 'qrencode'
         'qt5-base' 'gtk-update-icon-cache' 'desktop-file-utils'
         'libnotify' 'libcanberra' 'libnm' 'libappindicator-gtk3')
makedepends=('git' 'cmake')
_commit=2e7ba6094795f0e88e339a598e8d6899df594146
source=("git+https://git.ring.cx/savoirfairelinux/ring-client-gnome.git#commit=${_commit}")
sha256sums=('SKIP')

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../ring-client-gnome \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_BUILD_TYPE=None
  make
}

package() {
  cd build
  make DESTDIR="${pkgdir}" install
}
