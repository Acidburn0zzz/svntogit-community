# Maintainer: Florian Pritz <bluewind@xinu.at>
pkgname=grafana-zabbix
pkgver=4.1.0
pkgrel=1
pkgdesc="Zabbix plugin for Grafana dashboard"
arch=('any')
url="https://github.com/alexanderzobnin/grafana-zabbix"
license=('APACHE')
depends=('grafana')
makedepends=(yarn python2 libfaketime) # node-sass depends on node-gyp 3.8.0 which is still python2 https://github.com/sass/node-sass/issues/2877
source=("$pkgname-$pkgver.tar.gz::https://github.com/alexanderzobnin/grafana-zabbix/archive/v$pkgver.tar.gz")
sha256sums=('6101a738c572690804d5b9371374f09ff1b9435e31db797b792e29aa84d2029c')

build() {
	cd "$pkgname-$pkgver"
	yarn install --pure-lockfile
	yarn upgrade node-sass
	yarn build
}

check() {
	cd "$pkgname-$pkgver"
	# Force UTC timezone so that tests pass, even after a DST change
	TZ=UTC yarn test
}

package() {
	cd "$pkgname-$pkgver"
	install -dm755 "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	cp -r . "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app"
	rm -rf "$pkgdir/var/lib/grafana/plugins/alexanderzobnin-zabbix-app/node_modules"
}
