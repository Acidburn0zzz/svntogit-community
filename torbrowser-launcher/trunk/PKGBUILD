# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=torbrowser-launcher
_tor_key=2021-06-12
pkgver=0.3.4
pkgrel=1
pkgdesc="Securely and easily download, verify, install, and launch Tor Browser in Linux"
url="https://github.com/micahflee/torbrowser-launcher"
depends=('python' 'python-pyqt5' 'python-requests' 'python-pysocks' 'python-gpgme' 'python-packaging'
         'gnupg' 'tor' 'hicolor-icon-theme' 'dbus-glib')
makedepends=('python-setuptools' 'python-distro')
optdepends=('apparmor: support for apparmor profiles')
arch=('any')
license=('MIT')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/micahflee/torbrowser-launcher/archive/v${pkgver}.tar.gz"
        # fix for "force english version" on non-english system
        "https://sources.debian.org/data/main/t/torbrowser-launcher/0.3.3-3/debian/patches/05-Fix-use-case-that-enforce-install-of-EN-version-on-n.patch"
        # Fix pt_BR po file 
        "https://github.com/micahflee/torbrowser-launcher/pull/579/commits/4baa4362ac15b5ef4dc71722f842739afc1c450c.patch")
sha512sums=('1dfde9f10200ff3b8a5d74eb3aa1faad118016b47501389a1879934b742d89287b591a98e1a515953a78b8cbdfe3e867fc052a0f5aea4e4c1ac4ccbe60c3c487'
            'd2a29a77d1873656a46672f0e1bd3bb9d3d5d479b6b5974c4318077b0ec0b4d68e36dc257f8ce0b2c72723e16a723795256ceb99e1375f47977d15e0d75aacfb'
            'd78b5604f773edc094185c48da5d2a1b1086b542e4b2c899e8a0d787a703b838d7a57d48d58959d5f683b64220a316c91a82d674628a3ee9ff445600431e0c3e')
b2sums=('64aaeef134075edbaaca9163e22bc6b3f595b44f873a0db686a309434017b581900762523055ce7210b0504e7f5a4be141f4544acfb2eaeb17d95ac1b2d06fc1'
        '7a0c56f8b6bd9e7d596bf2dbe78879918e355160785ff4dc879a52608934d6423334abc7fbe387c272bede186218a21232a5fe54018d9bb95f6af4730881fbf1'
        'f5d473979fb750d438ea6945403439af86c1d18bc97b55fa486ac8de1a3cc96cedd881cb76a3e80c0d5963410c4cd2776ddd91786cf77852378cfa0efc7b8e5a')

prepare() {
  cd "${pkgname}-${pkgver}"
  patch --forward --strip=1 --input="${srcdir}/4baa4362ac15b5ef4dc71722f842739afc1c450c.patch"
  patch --forward --strip=1 --input="${srcdir}/05-Fix-use-case-that-enforce-install-of-EN-version-on-n.patch"
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
