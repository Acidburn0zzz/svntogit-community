# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=tcp-wrappers
pkgver=7.6.24
pkgrel=1
pkgdesc="Wietse Venema's network logger"
arch=('i686' 'x86_64')
url='ftp://ftp.porcupine.org/pub/security/index.html'
license=('BSD')
depends=('glibc')
options=('!makeflags')
source=("ftp://ftp.porcupine.org/pub/security/tcp_wrappers_${pkgver%.*}.tar.gz"
        "http://http.debian.net/debian/pool/main/t/tcp-wrappers/tcp-wrappers_${pkgver%.*}.q-${pkgver##*.}.debian.tar.xz"
        'tcp-wrappers-headers.patch'
        'tcp-wrappers-redhat-bug11881.patch')
sha256sums=('9543d7adedf78a6de0b221ccbbd1952e08b5138717f4ade814039bb489a4315d'
            '672ad0d3f70579815cd8f15c29054c49f6fd9902863be42cc2452a9ecbe8eacd'
            'caf0719fdac5c0c8874420af63bddf3b970c40d464f0553a9822a53d372854d8'
            '2e527e54c1ea208de10a206c667f751e54651eb77c973271d213e3459d690403')

prepare() {
  cd tcp_wrappers_${pkgver%.*}

  for patch in $(cat ../debian/patches/series); do
    patch -Np1 -i ../debian/patches/${patch}
  done
  patch -Np1 -i ../tcp-wrappers-headers.patch
  patch -Np1 -i ../tcp-wrappers-redhat-bug11881.patch
}

build() {
  cd tcp_wrappers_${pkgver%.*}

  make \
    COPTS="$CFLAGS" \
    LDOPTS="$LDFLAGS" \
    REAL_DAEMON_DIR='/usr/bin' \
    linux
}

package() {
  cd tcp_wrappers_${pkgver%.*}

  install -Dm 755 safe_finger tcpd tcpdchk tcpdmatch try-from -t "${pkgdir}"/usr/bin/
  install -Dm 644 tcpd.h -t "${pkgdir}"/usr/include/
  install -Dm 644 shared/libwrap.so.0.${pkgver%.*} -t "${pkgdir}"/usr/lib/
  ln -s libwrap.so.0.${pkgver%.*} "${pkgdir}"/usr/lib/libwrap.so.0
  ln -s libwrap.so.0 "${pkgdir}"/usr/lib/libwrap.so
  install -Dm 644 *.3 -t "${pkgdir}"/usr/share/man/man3/
  install -Dm 644 *.5 -t "${pkgdir}"/usr/share/man/man5/
  install -Dm 644 *.8 -t "${pkgdir}"/usr/share/man/man8/
  install -Dm 644 README -t "${pkgdir}"/usr/share/doc/tcp-wrappers/
  install -Dm 644 DISCLAIMER -t "${pkgdir}"/usr/share/licenses/tcp-wrappers/
}

# vim: ts=2 sw=2 et:
