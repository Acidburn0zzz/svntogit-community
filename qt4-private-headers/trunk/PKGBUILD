# $Id$
# Maintainer:
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt4-private-headers
pkgver=4.8.4
pkgrel=1
pkgdesc="Qt4 private headers for development"
arch=('any')
license=('GPL3' 'LGPL')
depends=("qt4=${pkgver}")
conflicts=('qt-private-headers')
replaces=('qt-private-headers')
_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/qt/source/${_pkgfqn}.tar.gz")
md5sums=('89c5ecba180cae74c66260ac732dc5cb')

package() {
    pkgdesc="Qt private headers for development"
    depends=("qt=${pkgver}")

    install -d "${pkgdir}"/usr/include/{QtCore,QtDeclarative,QtGui,QtScript}
    install -d "${pkgdir}"/usr/src/{corelib,declarative,gui,script}
    
    for i in QtCore QtDeclarative QtGui QtScript; do
      cp -r ${_pkgfqn}/include/${i}/private/ \
        "${pkgdir}"/usr/include/${i}/
    done

    for i in corelib declarative gui script; do
      cp -r ${_pkgfqn}/src/${i} "${pkgdir}"/usr/src/
    done
}
