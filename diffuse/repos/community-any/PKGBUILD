# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: zhuqin <zhuqin83@gmail.com>

pkgname=diffuse
pkgver=0.5.0alpha7+4+g813d6e7
pkgrel=1
pkgdesc="A graphical tool for merging and comparing text files"
arch=('any')
url="http://diffuse.sourceforge.net/"
license=('GPL')
depends=('gtk3' 'python-gobject')
makedepends=('git')
_commit=813d6e7bbac0dbcbf464951ba6b070562737ed8d  # master
source=("git+https://github.com/MightyCreak/diffuse.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-alpha\./alpha/;s/-/+/g'
}

package() {
  cd "$srcdir/$pkgname"
  python install.py --prefix=/usr --destdir="$pkgdir"
}
