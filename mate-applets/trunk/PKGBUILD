# Maintainer: Brad Fanella <cesura@archlinux.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.22.2
pkgrel=3
pkgdesc="Applets for MATE panel"
arch=('x86_64')
url="https://mate-desktop.org"
license=('GPL')
depends=('cpupower' 'gtksourceview3' 'libgtop' 'libnotify' 'mate-panel' 'polkit' 'upower' 'wireless_tools')
makedepends=('intltool' 'itstool' 'gucharmap' 'yelp-tools')
optdepends=('fortune-mod: for displaying fortune cookies in the Wanda the Fish applet' 'gucharmap: character picker applet')
groups=('mate-extra')
conflicts=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
replaces=('mate-applets-gtk3' 'mate-netspeed' 'mate-netspeed-gtk3')
source=("https://pub.mate-desktop.org/releases/${pkgver%.*}/${pkgname}-${pkgver}.tar.xz"
        '0001-make-the-dbus-directory-configurable.patch')
sha256sums=('0c47d69fdecda40231d021cb72594c479e01a4d4b7f49c9550731a9fe7cc62b0'
            '86c62a2a4df68582e758c7b9ead62d55dc673bbbda841d9bd6f324fb0d53685b')

prepare() {
    	cd "${pkgname}-${pkgver}"

	# D-Bus policy in /usr/share/dbus-1/system.d instead of /etc/dbus-1/system.d
	# https://www.archlinux.org/todo/d-bus-policy-in-usrsharedbus-1systemd-instead-of-etcdbus-1systemd/
    	patch -Np1 < ../0001-make-the-dbus-directory-configurable.patch

    	autoreconf -fi
}

build() {
    	cd "${pkgname}-${pkgver}"
    	./configure \
        	--prefix=/usr \
        	--sysconfdir=/etc \
        	--libexecdir=/usr/lib/${pkgname} \
        	--with-dbus-sys="/usr/share/dbus-1/system.d" \
        	--enable-polkit \
        	--enable-ipv6
    	make
}

package() {
    	cd "${pkgname}-${pkgver}"
    	make DESTDIR="${pkgdir}" install
}
