# Maintainer: Brett Cornwall <ainola@archlinux.org>

pkgname=urlscan
pkgver=0.9.3
pkgrel=2
pkgdesc="Mutt and terminal url selector"
url="https://github.com/firecat53/urlscan"
arch=('any')
license=('GPL2')
depends=('python-urwid')
makedepends=('python-setuptools')
optdepends=(
    'xclip: Clipboard support for X11 environments'
)
source=("$pkgname-$pkgver.tar.gz::https://github.com/firecat53/urlscan/archive/$pkgver.tar.gz")
sha256sums=('d3eaef486df70da85614b51a383fa5d570b6253c147349da29df04cd36ed7955')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
