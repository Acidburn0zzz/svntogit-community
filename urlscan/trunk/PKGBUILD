# Maintainer: Ainola <ainola@archlinux.org>

pkgname=urlscan
pkgver=0.9.1
pkgrel=2
pkgdesc="Mutt and terminal url selector"
url="https://github.com/firecat53/urlscan"
arch=('any')
license=('GPL')
depends=('python-urwid')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/firecat53/urlscan/archive/$pkgver.tar.gz")
sha256sums=('6d5c2537e1f05a3419d5dcac341c3d692d5c25a8549ab82a63b112af91ff332c')

build() {
    cd "$pkgname-$pkgver"
    python setup.py build
}

check() {
    cd "$pkgname-$pkgver"
    python setup.py test
}

package() {
    cd "$pkgname-$pkgver"
    python setup.py install --root="$pkgdir/" --optimize=1 --skip-build
}
