# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Felipe Machado aka arch_audio <machado.felipe@gmail.com>

pkgname=rubberband
pkgver=2.0.1
pkgrel=1
pkgdesc="Time-stretching and pitch-shifting audio library and utility"
arch=(x86_64)
url="https://www.breakfastquay.com/rubberband/"
license=(GPL2)
depends=(gcc-libs glibc)
makedepends=(fftw java-environment ladspa meson libsamplerate libsndfile
vamp-plugin-sdk)
optdepends=(
  'ladspa-host: for LADSPA plugin'
  'vamp-host: for VAMP plugin'
  'vamp-plugin-sdk: for VAMP plugin'
)
provides=(librubberband.so librubberband-jni.so)
source=("$pkgname-$pkgver.tar.gz::https://github.com/breakfastquay/rubberband/archive/v$pkgver.tar.gz")
sha512sums=('e51c2845036af6799b94026275f669f598293f0b721f4ac66bcf855b55d6ceee62251d4df8338fce5dee0e8f87c60f4dc9a20b9149da9968ec979a6855b4dd28')
b2sums=('1d6077e3a91d6002ebdaa4841d60b11180e5fb44c870cb0a14a1398ab093bd002e47003321f24c11910ca7f9277e45f86b36d23e3b524d4b2bec814332076e0f')

build() {
  local java_major=$(java --version 2>/dev/null |grep 'openjdk'| cut -d ' ' -f2| cut -d '.' -f1)
  export JAVA_HOME="/usr/lib/jvm/java-${java_major}-openjdk"

  cd "$pkgname-$pkgver"
  arch-meson -Dfft=fftw \
            -Dresampler=libsamplerate \
            -Dextra_include_dirs="/usr/lib/jvm/java-${java_major}-openjdk/include,/usr/lib/jvm/java-${java_major}-openjdk/include/linux" \
            build
  ninja -C build
}

package() {
  depends+=(libfftw3.so libsamplerate.so libsndfile.so)

  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {CHANGELOG,README.md} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
