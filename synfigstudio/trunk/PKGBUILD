# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Franco Iacomella <yaco@gnu.org>

pkgname=synfigstudio
pkgver=0.63.00
pkgrel=2
pkgdesc="Professional vector animation program (GUI)"
arch=(i686 x86_64)
url="http://synfig.org"
license=('GPL2')
depends=('gtkmm' 'synfig')
makedepends=('openexr' 'imagemagick' 'xorg-fonts-100dpi' 'xorg-fonts-75dpi'
	     'xorg-fonts-misc' 'xorg-fonts-type1')
install=synfigstudio.install
source=(http://arch.p5n.pp.ru/~sergej/dl/synfig-$pkgver.tar.gz)
md5sums=('ea3633e9522ae89489519af351ba2443')

build() {
  cd $srcdir/synfig-fb5ba9c/synfig-studio
  [ -f configure ] || { libtoolize --ltdl --copy --force && autoreconf --install --force; }
  [ -f Makefile ] || ./configure --prefix=/usr --sysconfdir=/etc --with-libavcodec --with-libdv
  sed -i 's#Gtk::IconSize::IconSize#Gtk::IconSize#' src/gui/dialogs/dialog_color.cpp
  make
}

package() {
  cd $srcdir/synfig-fb5ba9c/synfig-studio
  make DESTDIR=$pkgdir install
  rm -f $pkgdir/usr/share/pixmaps/$pkgname/*.mng
  install -m644 images/*.png -t $pkgdir/usr/share/pixmaps/$pkgname/
  rm -f $pkgdir/usr/share/mime/XMLnamespaces
  rm -f $pkgdir/usr/share/mime/aliases
  rm -f $pkgdir/usr/share/mime/generic-icons
  rm -f $pkgdir/usr/share/mime/globs
  rm -f $pkgdir/usr/share/mime/globs2
  rm -f $pkgdir/usr/share/mime/icons
  rm -f $pkgdir/usr/share/mime/magic
  rm -f $pkgdir/usr/share/mime/mime.cache
  rm -f $pkgdir/usr/share/mime/subclasses
  rm -f $pkgdir/usr/share/mime/treemagic
  rm -f $pkgdir/usr/share/mime/types
}
