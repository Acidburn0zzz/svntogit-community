# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Sebastian Voecking <voeck@web.de>

pkgname=root
pkgver=5.28.00a
pkgrel=1
pkgdesc='C++ data analysis framework and interpreter from CERN.'
arch=('i686' 'x86_64')
url='http://root.cern.ch'
license=('LGPL2.1')
# TODO: add gcc-fortran to optdepends after figuring out what to describe it for
depends=('avahi' 'desktop-file-utils' 'ftgl' 'giflib' 'glew' 'graphviz' 'gsl' 'libldap' 'libmysqlclient'
         'libxft' 'postgresql-libs' 'python2' 'unixodbc' 'shared-mime-info' 'xmlrpc-c' 'xorg-fonts-75dpi' 'mesa' 'gcc-fortran')
makedepends=('fftw')
install='root.install'
# force needed because newer (minor) versions include an extra letter
options=('force')
source=("ftp://root.cern.ch/root/root_v${pkgver}.source.tar.gz"
        'root.sh'
        'rootd'
        'root.desktop'
        'root.xml')
md5sums=('bef76dbbba63ca83575b7b6b04669631'
         '0e883ad44f99da9bc7c23bc102800b62'
         'efd06bfa230cc2194b38e0c8939e72af'
         'ac61b17395d75a2705fefa2ef841a6bf'
         'e2cf69b204192b5889ceb5b4dedc66f7')

build() {
  cd ${pkgname}

  if [ ${CARCH} == 'i686' ]; then
    TARGET=linux;
  else
    TARGET=linuxx8664gcc;
  fi

  # python2 switch
  find . -type f -exec sed -i 's/python -c/python2 -c/g' {} \;
  find . -type f -exec sed -i 's/python -O/python2 -O/g' {} \;
  sed -i 's/python 2/python2 2/g' configure

  ./configure \
    ${TARGET} \
    --prefix=/usr \
    --disable-builtin-glew \
    --disable-builtin-freetype \
    --disable-builtin-ftgl \
    --disable-builtin-pcre \
    --disable-builtin-zlib \
    --enable-gdml \
    --enable-gsl-shared \
    --enable-minuit2 \
    --enable-soversion \
    --enable-roofit \
    --enable-python \
    --with-python-incdir=/usr/include/python2.7 \
    --with-python-libdir=/usr/lib \
    --disable-krb5
    # disable krb5 because of compile error

    # move from aur
    #--disable-builtin-afterimage \

    #--with-python-incdir=/usr/include/python2.7/Python.h \
    #--with-python-libdir=/usr/lib/libpython2.7.so

  make
}

package() {
  cd ${pkgname}

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/root.sh ${pkgdir}/etc/profile.d/root.sh
  install -D ${srcdir}/rootd ${pkgdir}/etc/rc.d/rootd
  install -D -m644 ${srcdir}/root.desktop ${pkgdir}/usr/share/applications/root.desktop
  install -D -m644 ${srcdir}/root.xml ${pkgdir}/usr/share/mime/packages/root.xml

  rm -rf ${pkgdir}/etc/root/daemons
}
