# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: eolianoe <eolianoe At GoogleMAIL DoT com>

_name=click-log
pkgname=python-click-log
pkgver=0.3.2
pkgrel=4
pkgdesc="Logging integration for Click"
arch=("any")
url="https://pypi.python.org/pypi/click-log"
license=("MIT")
depends=("python-click")
makedepends=("python-setuptools")
checkdepends=("python-pytest" "python-click")
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('8ae5c43d739127367bbcdcf32bfd4b34b2ea1c377948df3b8588e4bfe2b0f5e7a61d1ccc6cb32c77045b15251222866daffa5a7bfb7a907487548784ba345351')

build() {
  cd "${_name}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_name}-${pkgver}"
  # Needed for the tests, especially when building in clean chroot
  [[ "${LANG}" = C ]] && export LANG=en_US.UTF8
  export PYTHONPATH=build/lib:$PYTHONPATH
  py.test

}

package() {
  cd "${_name}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 "LICENSE" -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
