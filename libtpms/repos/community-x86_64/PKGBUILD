# Maintainer: Jonas Witschel <diabonas@archlinux.org>
pkgname=libtpms
pkgver=0.8.5
_tag=ac4b9363504b8259e85bad09e967ad5aa87c5e22 # git rev-parse "v$pkgver"
pkgrel=2
pkgdesc='Library providing a software emulation of a Trusted Platform Module (TPM 1.2 and TPM 2.0)'
arch=('x86_64')
url='https://github.com/stefanberger/libtpms'
license=('BSD')
depends=('openssl')
makedepends=('git')
source=("git+$url.git?signed#tag=$_tag")
sha512sums=('SKIP')
validpgpkeys=('B818B9CADF9089C2D5CEC66B75AD65802A0B4211') # Stefan Berger <stefanb@linux.vnet.ibm.com>

pkgver() {
	cd "$pkgname"
	git describe | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd "$pkgname"

	# Fix for tpm2-tools test suite (https://github.com/stefanberger/libtpms/pull/260)
	git cherry-pick --no-commit 8ae3d744bc0cf9dc8a0078fe052f5bd70e41c818

	autoreconf --install --force
}

build() {
	cd "$pkgname"
	./configure --prefix=/usr --with-openssl --with-tpm2
	make
}

check() {
	cd "$pkgname"
	make check
}

package() {
	cd "$pkgname"
	make DESTDIR="$pkgdir" install
	install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
