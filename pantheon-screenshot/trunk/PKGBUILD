# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=pantheon-screenshot
pkgver=1.7.0
pkgrel=1
pkgdesc='The Pantheon Screenshot Tool'
arch=(x86_64)
url=https://github.com/elementary/screenshot
license=(GPL3)
groups=(pantheon)
depends=(
  gdk-pixbuf2
  glib2
  gtk3
  libcanberra
  libgranite.so
)
makedepends=(
  git
  intltool
  meson
  vala
)
source=(pantheon-screenshot::git+https://github.com/elementary/screenshot.git#tag=678e754e003f22bf331298e5f1e83ac54354988f)
sha256sums=(SKIP)

pkgver() {
  cd pantheon-screenshot

  git describe --tags
}

build() {
  arch-meson pantheon-screenshot build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
}

# vim: ts=2 sw=2 et:
