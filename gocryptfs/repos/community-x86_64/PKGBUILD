# Maintainer: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Peter Reschenhofer <peter.reschenhofer@gmail.com>

_pkgauthor=rfjakob
pkgname=gocryptfs
pkgver=2.2.0
pkgrel=1
pkgdesc='Encrypted overlay filesystem written in Go.'
arch=('x86_64')
url="https://github.com/${_pkgauthor}/${pkgname}"
license=('MIT')
depends=('gcc-libs' 'openssl' 'fuse')
makedepends=('go' 'pandoc' 'man-db')
source=(
    "${pkgname}-${pkgver}.tar.gz::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz"
    "${pkgname}-${pkgver}.tar.gz.asc::https://github.com/${_pkgauthor}/${pkgname}/releases/download/v${pkgver}/${pkgname}_v${pkgver}_src-deps.tar.gz.asc"
)
sha256sums=('555e8fba2bc5ed87ad52fc9b7a5525ed0fd01b0a34dfd23cb3ecfae6de75edcd'
            'SKIP')
validpgpkeys=('FFF3E01444FED7C316A3545A895F5BC123A02740')

build() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"

    export CGO_LDFLAGS="$LDFLAGS"
    export GOFLAGS="-buildmode=pie -trimpath -mod=vendor"
    make build
}

package() {
    cd "${srcdir}/${pkgname}_v${pkgver}_src-deps"
    make DESTDIR="${pkgdir}" install
}
