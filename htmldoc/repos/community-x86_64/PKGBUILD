# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: James An <james@jamesan.ca>
# Contributor: Mariusz Libera <mariusz.libera@gmail.com>
# Contributor: mortdeus <mortdeus@gocos2d.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Simon Rutishauser <simon.rutishauser@gmx.ch>

pkgname=htmldoc
pkgver=1.9.9
pkgrel=1
pkgdesc='HTML Conversion Software'
arch=('x86_64')
url='https://www.msweet.org/htmldoc/'
license=('GPL2')
depends=('libxpm' 'gnutls' 'fltk' 'libjpeg-turbo' 'libpng' 'zlib'
         'shared-mime-info')
source=("https://github.com/michaelrsweet/htmldoc/releases/download/v${pkgver}/htmldoc-${pkgver}-source.tar.gz"{,.sig})
sha256sums=('47ef6ba866913d71efdface28169cffd5211b714c26a7f8c2394c277e56bf294'
            'SKIP')
validpgpkeys=('845464660B686AAB36540B6F999559A027815955') # Michael R Sweet

build() {
    cd "${pkgname}-${pkgver}"
    ./configure \
        --prefix='/usr' \
        --enable-ssl \
        --enable-gnutls \
        --enable-cdsassl \
        --disable-localjpeg \
        --disable-localzlib \
        --disable-localpng \
        --with-gui
    make
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="$pkgdir" install
}
