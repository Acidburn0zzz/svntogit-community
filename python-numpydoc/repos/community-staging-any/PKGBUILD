# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=0.9.1
pkgrel=3
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('30be62f1d874553d0f05e1f96cf5f22d1c48ed869945cfeffd4a446072446a52f29a4b510bcc6e64c5d36184f0748a47269f96abc3c561179979199fb4f4e46d')

build() {
  cd numpydoc-$pkgver
  python setup.py build
}

check() {
  cd numpydoc-$pkgver
  python setup.py pytest
}

package() {
  cd numpydoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
