# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-numpydoc
pkgver=0.8.0
pkgrel=1
pkgdesc="Numpy's Sphinx extensions"
url="https://github.com/numpy/numpydoc"
license=('Apache')
arch=('any')
depends=('python-sphinx' 'python-jinja')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/numpy/numpydoc/archive/v$pkgver.tar.gz")
sha512sums=('c85d44b91bea70f41678c4b324c5af82444322b619fb6d86ed1aa943e29dfd87ec93e1b73160ebd0eb94bcf12d645710ec6b65f6d81be14a185710998fe6921e')

build() {
  cd numpydoc-$pkgver
  python setup.py build
}

check() {
  cd numpydoc-$pkgver
  python setup.py nosetests
}

package() {
  cd numpydoc-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
