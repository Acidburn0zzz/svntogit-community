# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-crispy-forms
pkgname=python-django-crispy-forms
pkgver=1.11.2
pkgrel=1
pkgdesc="The best way to have DRY Django forms"
arch=('any')
url="https://github.com/django-crispy-forms/django-crispy-forms"
license=('MIT')
depends=('python-django')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('f7c7cfbeadea80a302a49d029aa39d2d396bc941c6d1b5c6310015c514ea5a466eee3064502e05092b710cc230472181faa0c9b6b137981de3288a9dd18f02dc')
b2sums=('657c86ea7b92e9636aac235ab432be6a0143ba7ae74afa55ddc33de907107ff08d4e4ee204f46d641ab243d02f4f363a3b7d38624f22861e254e9b26b25eb731')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  DJANGO_SETTINGS_MODULE=crispy_forms.tests.test_settings pytest -v crispy_forms/tests
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {CONTRIBUTORS.txt,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
