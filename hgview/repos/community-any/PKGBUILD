# Maintainer: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: TDY <tdy@gmx.com>

pkgname=hgview
pkgver=1.10.2
pkgrel=4
pkgdesc="A fast Mercurial log navigator"
arch=('any')
url="https://www.logilab.org/project/hgview/"
license=('GPL')
depends=('mercurial>=1.6' 'python2-urwid' 'python2-pyinotify' 'python2-pygments')
makedepends=('asciidoc' 'xmlto')
source=(http://download.logilab.org/pub/$pkgname/$pkgname-$pkgver.tar.gz
        hg47.patch)
sha1sums=('ac176dca4ef3d89e4259f878a04f708af43a64ea'
          '7be1754ff56ed386b43684446c1d6be72751b9db')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -p1 < ../hg47.patch

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root="$pkgdir"/ --optimize=1

  # fix man page placement
  install -d "$pkgdir"/usr/share
  mv "$pkgdir"/usr/man "$pkgdir"/usr/share/man

  # fix FS#59623
  rm "$pkgdir"/usr/lib/python2.7/site-packages/hgext3rd/__init__.*
}

