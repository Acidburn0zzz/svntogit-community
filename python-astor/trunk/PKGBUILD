# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-astor
pkgver=0.7.1
pkgrel=2
pkgdesc="Read/rewrite/write Python ASTs"
arch=('any')
license=('BSD')
url="http://astor.rtfd.org/"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose')
source=("$pkgname-$pkgver.tar.gz::https://github.com/berkerpeksag/astor/archive/$pkgver.tar.gz"
        string-newline.patch)
sha512sums=('02764e5751e4c4b0ffa83da262b87e0a6bf027461529a99d3ca01a415db0896754f2b3f278e8a28f9bce4972ee7a75eec4eec5ac47d1064e6d6656a007b38a64'
            'ad977606c1b0374dccece65a9743e875e250e86d923c987eee7babb09f1209a874a7b447f253e1666364da32e309579d261ac70cf32b3d909c9fe78501b19cb8')

prepare() {
  patch -d astor-$pkgver -p1 -i ../string-newline.patch
}

build() {
  cd astor-$pkgver
  python setup.py build
}

check() {
  cd astor-$pkgver
  nosetests3 || warning "https://github.com/berkerpeksag/astor/issues/124"
}

package() {
  cd astor-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
