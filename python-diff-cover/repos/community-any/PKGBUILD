# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=6.4.3
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=(any)
url="https://github.com/Bachmann1234/diff_cover"
license=(Apache)
depends=(python-chardet python-jinja python-pluggy python-pygments)
makedepends=(python-setuptools)
checkdepends=(python-pytest python-pytest-mock python-tomli)
optdepends=('python-tomli: for TOML support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('c7a4078ecdd018eea3b6c92975ea24408860e756bd2ef636f0694ff50207bc3aa16aad956dc55640e2da3d80db807fb1c222ecc1bc0d6ee49048b18e40a0830d'
            'SKIP')
b2sums=('a83ef046f791f545d7a06d9a15fe8aa551f6020d4b76300a2c0af44a43dd287d4e423173cc417894f8aa36ab90baa18039d3b3aa4be90c2b61cfc482539a6dd1'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir='test_dir'

  cd "$pkgname-$pkgver"
  # install to temporary location, as importlib is used
  python setup.py install --optimize=1 --root="${_test_dir}"
  PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}" \
  # ignore integration and code style checks
  pytest -v --ignore 'tests/test_integration.py' --ignore 'tests/test_violations_reporter.py' --ignore 'tests/test_clover_violations_reporter.py'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
