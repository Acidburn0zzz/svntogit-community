# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=6.4.1
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-chardet' 'python-jinja' 'python-jinja_pluralize'
'python-pluggy' 'python-pygments')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-pytest-mock' 'python-tomli')
optdepends=('python-tomli: for TOML support')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('ba5848d2c2f206675e13740477fe676f71f372be9fa7fbc9c610176ee9fa079e20b32cfb439e4f2d1f842015bd1c5fa8de4c6f01bcfb457d5220d8b7087712b3'
            'SKIP')
b2sums=('7be9a138b338b8c4f2bce60b708e7c2275859dbe019d9d0db71c2052ac76c0af04180b29042c0d8ef77fe2c7899fb3a9db50dedd48c4dd08375109c975207efa'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  local _site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  local _test_dir='test_dir'

  cd "$pkgname-$pkgver"
  # install to temporary location, as importlib is used
  python setup.py install --optimize=1 --root="${_test_dir}"
  PYTHONPATH="${_test_dir}/${_site_packages}:${PYTHONPATH}" \
  # ignore integration and code style checks
  pytest -v --ignore 'tests/test_integration.py' --ignore 'tests/test_violations_reporter.py' --ignore 'tests/test_clover_violations_reporter.py'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
