# Maintainer: David Runge <dvzrv@archlinux.org>

_name=diff_cover
pkgname=python-diff-cover
pkgver=4.1.0
pkgrel=1
pkgdesc="Automatically find diff lines that need test coverage"
arch=('any')
url="https://github.com/Bachmann1234/diff_cover"
license=('Apache')
depends=('python-jinja' 'python-jinja_pluralize' 'python-pluggy'
'python-pygments' 'python-setuptools')
checkdepends=('flake8' 'python-pytest' 'python-mock' 'python-pytest-cov'
'python-pycodestyle' 'python-pyflakes' 'python-pylint' 'python-pydocstyle')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz"{,.asc})
sha512sums=('9b554bc784c8711965cf39361df72ac3fee755709050850317dc2663bbb524bfa62d82145ec30dfe64cf2668037d70992b167af1c219f61c287313403c760859'
            'SKIP')
b2sums=('831d4dadfec0c527addd4625a4728b86e5026d0b3f162d8c367c22094ee2bf738b2692487c04791153bc86d49e99bc9f1fd4b4fac3314ceb1ff4842994fb5f73'
        'SKIP')
validpgpkeys=('54CAEABCAC2956D407348256972401BDE60128CB') # Matt Bachmann <matt.bachmann@lola.com>

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable test with missing files:
  # https://github.com/Bachmann1234/diff_cover/issues/166
  pytest -v -k 'not test_load_snippets_markdown'
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,NOTICE,README.rst} -t "${pkgdir}/usr/share/doc/${pkgname}"
}
