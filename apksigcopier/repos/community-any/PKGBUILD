# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Marcus Hoffmann <bubu@bubu1.eu>

pkgname=apksigcopier
pkgver=1.0.0
pkgrel=2
pkgdesc='copy/extract/patch apk signatures & compare apks'
url='https://github.com/obfusk/apksigcopier'
arch=('any')
license=('GPL3')
depends=('python' 'python-click')
makedepends=('python-setuptools' 'pandoc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/obfusk/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('b2d7865b8b28e94fb3acf7b9e4b7f508023283ef21b94dd4ca2d8138bde050d8c78fd85fc691a0c0b09fc12be2f7d07c16557999786acbdc853ed6b4e2ace4b9')
b2sums=('be7fd3a2421f4ed383da534f745dc3c2a0b341645c0714062b3e92c7ba20561b9f33b5fd069e80c71cb8a6b348959eca61946c574c1fbedafcce4fa14557f5c2')

build() {
  cd ${pkgname}-${pkgver}
  export PYTHONHASHSEED=0
  python setup.py build
  make
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1
  install -Dm 644 apksigcopier.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim:set ts=2 sw=2 et:
