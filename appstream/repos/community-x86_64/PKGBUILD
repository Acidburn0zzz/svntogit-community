# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: Tim Jester-Pfadt <t.jp<at>gmx.de>

pkgbase=appstream
pkgname=(appstream appstream-qt)
pkgver=0.10.1
pkgrel=2
pkgdesc="Provides a standard for creating app stores across distributions"
arch=(i686 x86_64)
url="http://distributions.freedesktop.org/wiki/AppStream"
license=(GPL)
depends=(xapian-core polkit libyaml libxml2 protobuf libstemmer)
makedepends=(cmake xmlto intltool gobject-introspection python2 docbook-xsl qt5-base)
source=("http://www.freedesktop.org/software/appstream/releases/AppStream-$pkgver.tar.xz" update-appstream-index.hook
        debian-screenshots.patch::"https://github.com/ximion/appstream/commit/4e405180.patch")
sha256sums=('b4cef33655856aa072ac48d5877443ac4844f7dee189749122881e3a6db8cd70'
            'eb4fcec37d2cbe42ddf13778c21554aba0dcbc789781571a568ea35526496f79'
            '77ea412bb95480609efbdc6137c06df61aefc7d8f8dab8867f9e207b63cf431b')

prepare() {
  mkdir -p build

# download screenshots from debian.net
  cd AppStream-$pkgver
  patch -p1 -i ../debian-screenshots.patch
}

build() {
  cd build

  cmake ../AppStream-$pkgver \
	-DQT=ON \
	-DCMAKE_INSTALL_LIBDIR=lib \
	-DCMAKE_BUILD_TYPE=Release
  make
}

package_appstream() {
  cd build

  make DESTDIR="$pkgdir" install
  
# provided by -qt subpackage
  rm -r "$pkgdir"/usr/{include/AppstreamQt,lib/cmake,lib/libAppstreamQt.*}

  install -Dm644 "$srcdir"/update-appstream-index.hook "$pkgdir"/usr/share/libalpm/hooks/update-appstream-index.hook
}

package_appstream-qt() {
  pkgdesc='Qt5 interface for AppStream'
  depends=(appstream qt5-base)

  cd build/qt
  make DESTDIR="$pkgdir" install
}
