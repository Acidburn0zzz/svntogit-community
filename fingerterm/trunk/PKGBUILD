# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.3.14
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('x86_64')
url="https://git.merproject.org/mer-core/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications' 'qt5-tools')
source=("$pkgname-$pkgver.tar.bz2::https://git.merproject.org/mer-core/fingerterm/repository/archive.tar.bz2?ref=$pkgver")
sha512sums=('0088312ce0d39a12668e591de11eda2420caf6fe5a6f83636457b52d22173e73bd4329c37c7bb04325dd7ed26b24b1de5743e53befab214b0bd16f4a6d625e6f')

prepare() {
  rm fingerterm-$pkgver.tar.bz2
  mv fingerterm-* fingerterm-$pkgver
}

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}
