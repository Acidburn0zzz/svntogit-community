# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=fingerterm
pkgver=1.3.5
pkgrel=1
pkgdesc="A terminal emulator with a custom virtual keyboard"
arch=('i686' 'x86_64')
url="https://git.merproject.org/mer-core/fingerterm"
license=('LGPL')
depends=('qt5-feedback')
makedepends=('nemo-qml-plugin-notifications')
source=("$pkgname-$pkgver.tar.bz2::https://git.merproject.org/mer-core/fingerterm/repository/archive.tar.bz2?ref=$pkgver")
sha256sums=('48cdd5954cee6522f3fd28f52394d1d94044d6d5c3dde9f73d3b5b487bd72a4e')

prepare() {
  rm fingerterm-$pkgver.tar.bz2
  mv fingerterm-* fingerterm-$pkgver
}

build() {
  cd fingerterm-$pkgver

  qmake-qt5 CONFIG+=enable-feedback CONFIG+=enable-nemonotifications
  make
}

package() {
  cd fingerterm-$pkgver

  make INSTALL_ROOT="${pkgdir}" install
}
