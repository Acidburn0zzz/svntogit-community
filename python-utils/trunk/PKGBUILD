# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=3.0.0
pkgrel=1
arch=(any)
url="https://github.com/WoLpH/python-utils"
license=(BSD)
depends=(python-six)
makedepends=(python-setuptools)
checkdepends=(python-pytest-runner)
source=("https://pypi.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('b03f97b66b71af8eb023ecfa1b671f2d566d139464f63cd512b129a5830651172843285fe7ef028047ca03f345221f5208eac8e1647361f173f1c7b3aed866a6'
            'SKIP')
b2sums=('640ebc1b3e59a30be5a5c40291d121919b9f90a71476acf66741cb4b8af691c50c3826d9e8e6b30464f2be8e5ba0ac4683f79962c8833073b8a43a108ead281d'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v -c /dev/null
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
