# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=2.5.5
pkgrel=1
url="https://github.com/WoLpH/python-utils"
license=('BSD')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc})
sha512sums=('448ff86a7f05b764f260398496fc722c2bde335aec6402c686e8fc063faa8999088f5a6b7a960b415fb9a3b4d35bc9f26aa74fe4dfe02c61926c6cfd31a3876c'
            'SKIP')
b2sums=('44874a8d8089f18ff79eccf94f0b436c234dfc7d691e5b9e8e1d41e4cd3cc1005bfca44822f0b3558496ff80dc12443dcd2f29ece6c65956e17ace09c1c5663e'
        'SKIP')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  cd "${pkgname}-${pkgver}"
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
