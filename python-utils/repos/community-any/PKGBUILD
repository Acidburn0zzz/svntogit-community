# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Maxim Andersson <thesilentboatman@gmail.com>
# Contributor: NicoHood <archlinux {cat} nicohood {dog} de>

pkgdesc="A module with some convenient utilities not included with the standard Python install"
pkgname=python-utils
pkgver=2.5.0
pkgrel=1
url="https://github.com/WoLpH/python-utils"
license=('BSD')
arch=('any')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("https://pypi.org/packages/source/p/${pkgname}/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "${pkgname}-2.5.0-installation.patch::https://github.com/dvzrv/python-utils/commit/632a90dfbf9195cb67ea9e8c7248b31ecf88af6d.patch"
)
sha512sums=('d43aa1ddb20120d467c6d260f0dd495867596da2dcf9df8c83912bf79b76c51789060c27c5eb716bcb0198cc32343a349c19002ca3f1e509bb630fdb9df4dc29'
            'SKIP'
            'bdd46cd478d25862b528a1a1808faa5b5016ec6ce40bddcda58bb021d808da54a832acb4aa80105abed8fd3103a61ed1c9b8c86b767b197eecdce052a73fe9fb')
b2sums=('fc357546754e4b3f06090f4a9e88e4352315fd692a360b592806e1c1eaea4042d483936fb0b810945d98d0c5bb07dbc08286bcff153aa14d4744adb05f70c1b0'
        'SKIP'
        'f4e935d8db37b146e28b669e6bae7a12114f1362f6a1c4195ad52996bd8776ad5e768908185b47101cd1257ebf6a48f2954eed6c6fc8f0bc9fa086fd7d2211f5')
validpgpkeys=('149325FD15904E9C4EB89E95E81444E9CE1F695D') # Rick van Hattem <wolph@wol.ph>

prepare() {
  cd "${pkgname}-${pkgver}"
  # prevent tests from being installed globally
  patch -Np1 -i "../${pkgname}-2.5.0-installation.patch"
  # don't care about custom pytest flags
  rm -v pytest.ini
}

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="${PWD}/build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install --skip-build \
                          --optimize=1 \
                          --root="${pkgdir}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
