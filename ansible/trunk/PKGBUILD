# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
pkgname=ansible
pkgver=5.4.0
pkgrel=3
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-core')
provides=('python-ansible_collections')
optdepends=('python-argcomplete: shell completions'
            'python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'python-proxmoxer: proxmos modules'
            'python-openstacksdk: OpenStack module'
            'python-pynetbox: NetBox module'
            'python-ldap: LDAP modules'
            'python-keyring: Keyring module'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz"
    fix-pacman-upgrade.patch::https://github.com/ansible-collections/community.general/pull/4275.patch)
sha512sums=('7065c7d16a3bb13db27a3ec99c33c48e9d4abb4bec8267751e3d8df89da7d0948a8415b50b8fabd05f6a97fba661714f08f8473ae145fcb323dd83f6c7349a6d'
            'fddad753e42426fa4f134e7cc62c7c8174b92c6d3caac1f5222cec45558b64ddf056266045aa0a2f6233768ae4387e27cc90746ed39e52357cddb23666169e12')

prepare() {
  cd ansible-${pkgver}/ansible_collections/community/general
  patch -Np1 -i "${srcdir}"/fix-pacman-upgrade.patch
  cp -v plugins/modules/{packaging/os/,}pacman.py
}
build() {
  cd ansible-${pkgver}
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}
