# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=ansible
pkgver=3.1.0
pkgrel=1
pkgdesc='Official assortment of Ansible collections'
arch=('any')
url='https://pypi.org/project/ansible/'
license=('GPL3')
depends=('python' 'ansible-base')
provides=('python-ansible_collections')
optdepends=('python-pyopenssl: openssl modules'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-ldap: ldap support'
            'python-proxmoxer: proxmos modules'
            'python-openstacksdk: OpenStack module'
            'acme-tiny: openssl_certificate module')
makedepends=('python-setuptools')
source=("https://pypi.python.org/packages/source/a/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('ec8b453e2d1bda902b5e46ae75be84d52f08b2c394333ef95b6c162781cc291ade5b21548c68e55e1af642d43e62092ca0db42f51bdd0f79b2d8d221c88543b8')

build() {
  cd ansible-${pkgver}
  python setup.py build
}

package() {
  cd ansible-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/${pkgname}/COPYING
}
