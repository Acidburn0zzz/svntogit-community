# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Chris <seitz.christoph@gmail.com>
# Contributor: m0ikz <ndelatorre@moikz.com.ar>
# Contributor: atweiden <archbaum@gmail.com>

pkgname=(ansible python-ansible)
pkgver=2.5.2
pkgrel=2
pkgdesc='Radically simple IT automation platform'
arch=('any')
url='https://www.ansible.com'
license=('GPL3')
depends=('python' 'python-yaml' 'python-paramiko' 'python-jinja' 'python-crypto')
optdepends=('sshpass: for ssh connections with password'
            'python-passlib: crypt values for vars_prompt'
            'python-pyopenssl: openssl modules'
            'python-netaddr: for the ipaddr filter'
            'python-systemd: log to journal'
            'python-pywinrm: connect to Windows machines'
            'python-dnspython: for dig lookup'
            'python-ovirt-engine-sdk: ovirt support'
            'python-boto3: aws_s3 module'
            'python-jmespath: json_query support'
            'acme-tiny: openssl_certificate module')
makedepends=('asciidoc' 'fakeroot' 'python-setuptools')
backup=('etc/ansible/ansible.cfg')
source=("https://releases.ansible.com/ansible/ansible-${pkgver}.tar.gz")
sha512sums=('25568665b96ee9d7ca0888dadd90de178a9cc1da7dba07acb682ca4455ef91622344958ec2000eafbb4cfda87aec325be77d3d5b395aa4bbc2d180fad1a90a34')

prepare() {
  cp -r ${pkgname}-${pkgver} "${srcdir}"/${pkgname}-${pkgver}-python
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  python setup.py build

  cd "${srcdir}"/${pkgname}-${pkgver}-python
  python setup.py build
}

package_ansible() {
  provides=('python-ansible')

  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}"
  install -Dm644 COPYING "${pkgdir}"/usr/share/doc/ansible/COPYING

  install -d "${pkgdir}"/usr/share/ansible/doc
  cp -dpr --no-preserve=ownership ./examples "${pkgdir}"/usr/share/ansible/doc/
  install -Dm644 examples/ansible.cfg "${pkgdir}"/etc/ansible/ansible.cfg

  install -d "${pkgdir}"/usr/share/man/man1
  cp -dpr --no-preserve=ownership docs/man/man1/*.1 "${pkgdir}"/usr/share/man/man1
}

package_python-ansible() {
  pkgdesc='python bindings for ansible'
  depends=('python-paramiko' 'python-jinja' 'python-yaml' 'python-crypto')

  cd "${srcdir}"/ansible-${pkgver}-python
  python setup.py install --root=${pkgdir} --optimize=1
  rm "${pkgdir}"/usr/bin/ansible*
}
