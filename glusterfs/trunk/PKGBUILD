# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributors:
#   Andrei Antoukh - niwi@niwi.be - http://www.niwi.be
#   henning mueller <henning@orgizm.net>

pkgname=glusterfs
pkgver=3.3.1
_basever=`echo $pkgver | cut -f1-2 -d.`
pkgrel=5
pkgdesc='Is a cluster file-system capable of scaling to several peta-bytes.'
arch=(i686 x86_64)
url='http://www.gluster.org/'
license=(GPL2 LGPL3)
depends=(fuse python2 libxml2)
makedepends=(flex bison)
options=(!libtool)
source=(http://download.gluster.org/pub/gluster/glusterfs/$_basever/$pkgver/glusterfs-$pkgver.tar.gz
	glusterd.service)
sha256sums=('19aaefe3a0f9059ae831b44c92944503cf439da82da23467101ac13da8b77519'
            'e9aa54fcff422e0266e0f89d4bda73e0c77458f7c5443b8327db1e7b4242e6f7')
sha256sums=('19aaefe3a0f9059ae831b44c92944503cf439da82da23467101ac13da8b77519'
            '18e6c3bcca396a4524d866977d3fa86135377d7bf5d28a130e697ae2e85f043b')

build() {
  cd $srcdir/$pkgname-$pkgver

  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --with-mountutildir=/usr/bin \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --mandir=/usr/share/man \
    --libexecdir=/usr/lib/$pkgname \
    PYTHON=python2

  make

  sed -i s:env\ python:env\ python2: \
    xlators/features/marker/utils/syncdaemon/gsyncd.py
  sed -i s:/usr/bin/python:/usr/bin/python2: \
    contrib/ipaddr-py/ipaddr.py
}

package() {
  cd $srcdir/$pkgname-$pkgver

  make DESTDIR=$pkgdir install

  install -D -m 644 \
    $srcdir/$pkgname-$pkgver/{README,INSTALL,COPYING*} \
    $pkgdir/usr/share/doc/glusterfs/

  install -D -m 644 \
    $srcdir/glusterd.service \
    $pkgdir/usr/lib/systemd/system/glusterd.service

  cp -rf \
    $srcdir/$pkgname-$pkgver/doc/* \
    $pkgdir/usr/share/doc/glusterfs/
}

# vim:set ts=2 sw=2 et:
