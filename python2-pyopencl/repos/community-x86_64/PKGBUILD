# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>

pkgname=python2-pyopencl
pkgver=2011.2
pkgrel=1
pkgdesc="A complete, object-oriented language binding of OpenCL to Python"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
depends=('python2' 'python2-numpy' 'python2-mako' 'python2-pytools' 'libcl' 'opencl-headers' 'mesa')
makedepends=('ctags' 'python2-distribute')
source=("http://pypi.python.org/packages/source/p/pyopencl/pyopencl-${pkgver}.tar.gz"
        'LICENSE.txt')
sha1sums=('6db28dcb896c9fef7c1c361f69ebfb96050d2977'
          '2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7')

build() {
   cd "${srcdir}/pyopencl-${pkgver}"
   python2 ./configure.py --cl-enable-gl

   python2 setup.py build
}

package() {
   cd "${srcdir}/pyopencl-${pkgver}"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   install -D -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
