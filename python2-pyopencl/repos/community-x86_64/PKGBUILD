# $Id$
# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
pkgname=python2-pyopencl
pkgver=0.92
pkgrel=2
pkgdesc="A complete, object-oriented language binding of OpenCL to Python"
arch=('i686' 'x86_64')
url="http://mathema.tician.de/software/pyopencl"
license=('custom')
depends=('python2' 'boost' 'python2-numpy' 'libcl' 'opencl-headers')
makedepends=('ctags' 'python2-distribute')
provides=('pyopencl=${pkgver}')
replaces=('pyopencl')
conflicts=('pyopencl')
source=("http://pypi.python.org/packages/source/p/pyopencl/pyopencl-${pkgver}.tar.gz"
        'LICENSE.txt')
sha1sums=('5f683638328bc452e8ae5dceaf49c6059735fbca'
          '2e6966b3d9b15603ce2c3ff79eeadd63c5d066b7')

build() {
   cd "${srcdir}/pyopencl-${pkgver}"
   python2 ./configure.py --boost-inc-dir=/usr/include \
                          --boost-lib-dir=/usr/lib \
                          --boost-python-libname=boost_python \
                          --cl-inc-dir=/usr/include \
		                    --cl-lib-dir=/usr/lib \
		                    --cl-libname=OpenCL
   python2 setup.py build
}

package() {
   cd "${srcdir}/pyopencl-${pkgver}"
   python2 setup.py install --prefix=/usr --root="${pkgdir}" --optimize=1 --skip-build

   install -D -m644 ../LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
