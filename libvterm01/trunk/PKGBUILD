# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Florian Walch <florian+aur@fwalch.com>

pkgname=libvterm01
pkgver=0.1.4
pkgrel=1
pkgdesc='Abstract library implementation of a VT220/xterm/ECMA-48 terminal emulator'
arch=('x86_64')
url='https://www.leonerd.org.uk/code/libvterm/'
license=('MIT')
depends=('glibc')
provides=('libvterm')
conflicts=('libvterm')
source=("https://www.leonerd.org.uk/code/libvterm/libvterm-${pkgver}.tar.gz")
sha256sums=('bc70349e95559c667672fc8c55b9527d9db9ada0fb80a3beda533418d782d3dd')

build() {
  cd "libvterm-${pkgver}"
  make PREFIX=/usr
}

package() {
  cd "libvterm-${pkgver}"
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
