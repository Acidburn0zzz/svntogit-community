# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Thayne McCombs <astrothayne@gmail.com>

pkgname=dart-sass
pkgver=1.49.10
pkgrel=1
pkgdesc="Sass makes CSS fun again"
arch=('x86_64')
url='http://sass-lang.com/'
license=('MIT')
depends=('glibc')
makedepends=('git' 'dart')
options=('!strip')
provides=('sass')
conflicts=('ruby-sass')
_commit='6652001f19710f96947ad02341d67e522a1739ac'
source=("$pkgname::git+https://github.com/sass/dart-sass.git#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags
}

prepare() {
  cd "$pkgname"

  # fix version solving for a nonexistent version
  sed \
    -i pubspec.yaml \
    -e 's/cli_pkg: ^2.1.0/cli_pkg: ^2.0.0/'

  # download dependencies
  pub get
}

build() {
  cd "$pkgname"
  dart compile exe -Dversion=$pkgver -o sass bin/sass.dart
}

package() {
  cd "$pkgname"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" sass

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
