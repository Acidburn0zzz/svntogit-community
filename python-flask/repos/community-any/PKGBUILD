# Maintainer: Sven-Hendrik Haase <svenstaro@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Tarmo Heiskanen <turskii@gmail.com>

pkgname=python-flask
pkgver=2.1.0
pkgrel=1
pkgdesc='Micro webdevelopment framework for Python'
url='http://flask.pocoo.org/'
arch=('any')
license=('custom:BSD')
depends=('python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
makedepends=('python-setuptools'  'python-werkzeug' 'python-jinja' 'python-itsdangerous' 'python-click')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pallets/flask/archive/${pkgver}.tar.gz")
sha512sums=('aac87f68d9123ef8ea8dccbadecc91d6996dc4343fff65b01c8847d176911b4c21b14e4d62788ff734b62987fa35a983ba94d01a5dc8b375209f2875da83c430')

build() {
  cd "flask-$pkgver"
  python setup.py build
}

check() {
  cd "flask-$pkgver"
  python setup.py test
}

package() {
  cd "flask-$pkgver"

  python setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.rst "$pkgdir/usr/share/licenses/$pkgname/LICENSE.rst"
}

# vim:set ts=2 sw=2 et:
