# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>

pkgname=python-oslo-context
pkgver=3.0.1
pkgrel=1
pkgdesc="OpenStack Context library"
arch=('any')
url="https://pypi.python.org/pypi/oslo.context/$pkgver"
license=('Apache')
depends=('python-pbr' 'python-debtcollector')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-oslotest')
options=('!emptydirs')
source=("$pkgname-$pkgver.tar.gz::https://github.com/openstack/oslo.context/archive/$pkgver.tar.gz")
sha512sums=('07261e12e66468954f5b3f94f1899f6a691851a9541e5c457ea3d5922b37fd285d5531c7bb0d0d5a955dd65e368f7d71c96570cd12fd6ef841d08c9f2a40c156')

export PBR_VERSION=$pkgver

build() {
  cd oslo.context-$pkgver
  python setup.py build
}

check() {
  cd oslo.context-$pkgver
  stestr run
}

package() {
  cd oslo.context-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
