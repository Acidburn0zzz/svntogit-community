# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>
# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=openimagedenoise
_pkgname=oidn
pkgver=1.2.0
pkgrel=2
pkgdesc='Intel(R) Open Image Denoise library'
arch=('x86_64')
url='https://openimagedenoise.github.io'
license=('Apache')
depends=('intel-tbb' 'python')
makedepends=('git' 'cmake' 'ninja' 'ispc')
source=("git+https://github.com/OpenImageDenoise/oidn#tag=v$pkgver"
        'git+https://github.com/OpenImageDenoise/oidn-weights'
        "oidn-mkl-dnn::git+https://github.com/OpenImageDenoise/mkl-dnn")
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
    cd $_pkgname

    git submodule init
    git config submodule.weights.url "$srcdir"/oidn-weights
    git config submodule.mkl-dnn.url "$srcdir"/oidn-mkl-dnn
    git submodule update
}

build() {
    cd $_pkgname

    cmake \
        -B build \
        -G Ninja \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    ninja -C build
}

package() {
    cd $_pkgname
    DESTDIR="$pkgdir" ninja -C build install
    rm "$pkgdir/usr/bin/tests"
}

