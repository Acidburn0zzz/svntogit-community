# Maintainer: Maxime Gauduin <alucryd@gmail.com>

pkgname=lib32-orc
pkgver=0.4.26+2+g249c709
pkgrel=1
pkgdesc='The Oild Runtime Compiler'
url="https://cgit.freedesktop.org/gstreamer/orc"
arch=('x86_64')
license=('custom')
url='https://code.entropywave.com/projects/orc/'
depends=(lib32-glibc orc)
makedepends=(valgrind-multilib git)
_commit=249c709e90458ecef7a068df553ea41a83eedf0f  # master
source=("git+https://anongit.freedesktop.org/git/gstreamer/orc#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd orc
  git describe --tags | sed 's/^orc-//;s/-/+/g'
}

prepare() {
  cd orc
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd orc

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  ./configure \
    --prefix='/usr' \
    --libdir='/usr/lib32' \
    --disable-static \
    --enable-backend='mmx,sse,altivec'
  make
}

package() {
  cd orc

  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}

  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s orc "${pkgdir}"/usr/share/licenses/lib32-orc
}

# vim: ts=2 sw=2 et:
