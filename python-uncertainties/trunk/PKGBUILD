# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-uncertainties
pkgver=3.1.1
pkgrel=2
pkgdesc="Transparent calculations with uncertainties on the quantities involved (aka error propagation); fast calculation of derivatives."
arch=('any')
license=('BSD')
url="https://github.com/lebigot/uncertainties"
depends=('python')
optdepends=('python-numpy: additional support for NumPy arrays and matrices')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-numpy')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lebigot/uncertainties/archive/$pkgver.tar.gz")
sha512sums=('201780d52cac513a04804b4d0b986cba6fa4280d0bb882c73c7afc4a88e3b669a7d94bce2700cfec91222e60a2c0c539ec27b2b794c9443f8ef1292321a340ed')

build() {
  cd uncertainties-$pkgver
  python setup.py build
}

check() {
  cd uncertainties-$pkgver
  python setup.py nosetests
}

package() {
  cd uncertainties-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
