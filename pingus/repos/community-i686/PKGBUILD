# $Id$
# Maintainer: Eric Belanger <eric@archlinux.org>

pkgname=pingus
pkgver=0.7.4
pkgrel=1
pkgdesc="A Lemmings clone, i.e. a level-based puzzle game."
arch=('i686' 'x86_64')
url="http://pingus.seul.org"
license=('GPL')
depends=('gcc-libs' 'sdl_image' 'sdl_mixer' 'libgl' 'boost-libs>=1.47')
makedepends=('scons' 'boost>=1.47' 'mesa')
source=(http://pingus.seul.org/files/${pkgname}-${pkgver}.tar.bz2 pingus.desktop)
md5sums=('82742bc9906cad98338cfd3ea700b396'
         'b94efaa6e2de959de7fdb50cfc7dbdea')
sha1sums=('e61e5ffefcecebed83286c25b3626f70ed75d2a5'
          'f35649f3b0b2bfcb01ce75085cf719dcaa609a54')

build() {
  cd ${pkgname}-${pkgver}

  scons prefix=/usr
}

package() {
  cd ${pkgname}-${pkgver}

  # fix launcher
  sed -i 's/echo/echo -e/g' ./Makefile

  make install DESTDIR="${pkgdir}" PREFIX="/usr"

  install -D -m644 "${srcdir}/pingus.desktop" "${pkgdir}/usr/share/applications/pingus.desktop"
}
