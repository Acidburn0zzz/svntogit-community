# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.6.1
pkgrel=2
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/iikira/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go-pie' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("$pkgname-$pkgver.tar.gz::https://github.com/iikira/BaiduPCS-Go/archive/v$pkgver.tar.gz")
sha512sums=('796e204ab940417b7a1de04bdbd3723febb3c913bb76db362ddf3af7faf05ef71307f90dc4b0b6a0bcf6c41c4cf4b364a4128ca7cbd391f127d948f3deb8429a')

prepare() {
  cd BaiduPCS-Go-$pkgver

  mkdir -p .gopath/src/github.com/iikira
  ln -sf "$PWD" .gopath/src/github.com/iikira/BaiduPCS-Go
  export GOPATH="$PWD/.gopath:/usr/share/gocode"

  go get github.com/iikira/args github.com/olekukonko/tablewriter github.com/peterh/liner \
         github.com/urfave/cli
}

build() {
  cd BaiduPCS-Go-$pkgver
  go build -ldflags "-extldflags ${LDFLAGS}" -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
