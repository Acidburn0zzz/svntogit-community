# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=baidupcs-go
pkgver=3.7.9
pkgrel=1
pkgdesc="Terminal utility for Baidu Network Disk"
arch=('x86_64')
url="https://github.com/qjfoidnh/BaiduPCS-Go"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
conflicts=("baidupcs")
provides=("baidupcs")
source=("https://github.com/qjfoidnh/BaiduPCS-Go/archive/v$pkgver/$pkgname-$pkgver.tar.gz"
        $pkgname-go.sum-fix.patch::https://github.com/qjfoidnh/BaiduPCS-Go/commit/d5c53c92454cf63d52e9549b80f50c2b36749b4b.patch)
sha512sums=('c0dd69b87891bd829f367da0f3d584086969b6ce576f4b39f61c09859d657b078c8b3aa2b84707cf66a51902fad2cc64440f52bd692f59f4843778ea41c2a539'
            '7897d8c6495854f77d2fc50eb3051c60799152f95e9b137e383ebe4eac09d0774f72134f80fa416d80c892cf41a896280171095ee649824d1b96dfbceb94d09d')

prepare() {
  cd BaiduPCS-Go-$pkgver
  patch -p1 -i ../$pkgname-go.sum-fix.patch
}

build() {
  cd BaiduPCS-Go-$pkgver
  export GOFLAGS="-buildmode=pie -trimpath"
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  go build -o baidupcs-go
}

package() {
  cd BaiduPCS-Go-$pkgver
  install -Dm755 baidupcs-go "$pkgdir"/usr/bin/baidupcs
}
