# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=hotdoc
pkgname=hotdoc
pkgver=0.13.2
pkgrel=1
pkgdesc="the tastiest API documentation system"
arch=('x86_64')
url="https://github.com/${_pkgname}/${_pkgname}"
license=('LGPL')
_py_deps=('appdirs' 'cchardet' 'dbus-deviation' 'lxml' 'networkx' 'pkgconfig'
          'schema' 'toposort' 'wheezy-template' 'yaml')
depends=("${_py_deps[@]/#/python-}" 'json-glib')
makedepends=('python-setuptools' 'cmake')
optdepends=('clang: for the C extension'
            'llvm: for the C extension')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz")
sha256sums=('243bc53fa8ed321b00066a673ab518e0e63c6d34bdd78eab56c68ecceb967916')
b2sums=('cfe4d1bbb9ea912af9efadfd332dd29c731bd2c3db471e5fd1eb1b659af3810bcdec7b6a9be73a03b3c7fe65558f0a352eadbe2573dcec6c11579c75b33f28dd')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    # remove crucial file that isn't in git and crucially prints '42' when imported
    # (just kidding, the sdist accidentally disted it)
    rm hotdoc/foo.py
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${pkgname}-${pkgver}/build/lib.linux-$CARCH-3*

    python -m unittest
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}
