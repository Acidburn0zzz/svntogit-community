# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: cth451 <cth451@gmail.com>

pkgname=materia-gtk-theme
pkgver=20191017
pkgrel=1
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments"
arch=(any)
url="https://github.com/nana-4/materia-theme"
license=(GPL)
depends=(gtk3 gnome-themes-extra)
optdepends=('gtk-engine-murrine: for gtk2 theme')
provides=(materia-theme)
conflicts=(materia-theme)
replaces=(materia-theme)
source=(https://github.com/nana-4/materia-theme/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('ab60f2b79f4b22cefa432d00bae8456e27073d2ba22297da0ea92ac09c994d71')

package() {
    cd ${pkgname/gtk-/}-${pkgver}
    install -d "${pkgdir}"/usr/share/themes
    ./install.sh -d "${pkgdir}"/usr/share/themes
}
