# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: cth451 <cth451@gmail.com>

pkgname=materia-gtk-theme
pkgver=20180928
pkgrel=2
pkgdesc="A Material Design theme for GNOME/GTK+ based desktop environments"
arch=('any')
url="https://github.com/nana-4/materia-theme"
license=('GPL')
depends=('gtk3' 'gnome-themes-extra')
optdepends=('gtk-engine-murrine: for gtk2 theme')
provides=('materia-theme')
conflicts=('materia-theme')
replaces=('materia-theme')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/nana-4/materia-theme/archive/v${pkgver}.tar.gz"
        rebase-1.patch::"https://github.com/nana-4/materia-theme/commit/f4eea5b4ad5028a9593c72a50ad564244c38f2d5.patch"
        rebase-2.patch::"https://github.com/nana-4/materia-theme/commit/b8a96df4ca358faed94957fca93d686dd85c6f68.patch"
        nautilus.patch::"https://github.com/nana-4/materia-theme/commit/ea65ff3f36bca605581beadd430afc8cb9dde3b5.patch")
sha256sums=('aa1cdaa0f822da816802c75664ee2fa1391ffd08a17d74a894c97be2ef53d987'
            'dae7f38ad2eb8bdf2dd5254ab0d57035d347e148376bf58d894260398fc42163'
            'e0469ef6e4257b605561183fe63d8a825cc6879f39dac8c87aa54f4cf2397b45'
            '4caedfcbb1191ad0d04a06d99bd20abd872c7cca22305c63741330aac8a67074')

prepare() {
    cd ${pkgname/gtk-/}-${pkgver}
    # Required for nautilus patch to apply
    patch -p1 -i ../rebase-1.patch
    patch -p1 -i ../rebase-2.patch
    # Nautilus 3.30.1 headerbar
    patch -p1 -i ../nautilus.patch
    chmod 755 install.sh
}

package() {
    cd ${pkgname/gtk-/}-${pkgver}
    install -d "${pkgdir}"/usr/share/themes
    ./install.sh -d "${pkgdir}"/usr/share/themes
}
