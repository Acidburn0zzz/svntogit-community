# Contributor: Martin Wimpress <code@flexion.org>

_ver=1.14
pkgbase=mate-polkit
pkgname=(${pkgbase} ${pkgbase}-gtk3)
pkgver=${_ver}.0
pkgrel=1
pkgdesc="PolicyKit integration for the MATE desktop"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('gtk2' 'gtk3')
makedepends=('accountsservice' 'gobject-introspection' 'intltool')
#makedepends=( 'mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('da2b4cf7b905a24fa22dd14e8991a71814ad7777')

prepare() {
    cd "${srcdir}"
    mv "${pkgname}-${pkgver}" "${pkgbase}-gtk2"
    cp -a "${pkgbase}-gtk2" "${pkgbase}-gtk3"
}

build() {
    cd "${srcdir}/${pkgbase}-gtk2"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=2.0 \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make

    cd "${srcdir}/${pkgbase}-gtk3"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --enable-introspection

    #https://bugzilla.gnome.org/show_bug.cgi?id=656231
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make
}

package_mate-polkit() {
    pkgdesc+=' (GTK2 version)'
    groups=('mate')
    conflicts=("${pkgbase}-gtk3")
    depends=('gtk2' 'polkit')

    cd "${srcdir}/${pkgbase}-gtk2"
    make DESTDIR="${pkgdir}" install
}

package_mate-polkit-gtk3() {
    pkgdesc+=' (GTK3 version)'
    groups=('mate-gtk3')
    conflicts=("${pkgbase}")
    depends=('gtk3')

    cd "${srcdir}/${pkgbase}-gtk3"
    make DESTDIR="${pkgdir}" install
}
