# Maintainer: Christian Hesse <arch@eworm.de>
# Contributor:  TDY <tdy@gmx.com>
# Contributor: Marc St-Laurent <mstlaurent@canada.com>

pkgname=xfmpc
_tag='58f79c46a632f321b8ac8c3d7fdcf6c8e0369dd0' # git rev-parse ${pkgver}
pkgver=0.3.0
pkgrel=3
pkgdesc="A graphical GTK+ MPD client focusing on low footprint"
arch=('x86_64')
url="https://goodies.xfce.org/projects/applications/xfmpc/"
license=('GPL')
depends=('libxfce4ui' 'libmpd')
makedepends=('git' 'xfce4-dev-tools' 'vala')
source=("git+https://gitlab.xfce.org/apps/${pkgname}.git#tag=${_tag}"
        '0001-add-parenthesis-around-expression.patch'
        '0002-use-Gtk-MenuButton-for-context-button.patch')
sha256sums=('SKIP'
            'cbb647270655347477ca34c6abd19ca92baa3cf5a7118c48c676303870ad8683'
            '6a7a9914c72f84da45710f1faf98cd06c0127f8559a26695427411e0f3b90a58')

prepare() {
	cd "${srcdir}/${pkgname}"

	git cherry-pick -n \
		cf6986a4fbc34203f8ca52ccb9c11dd2eb7c8def

	patch -Np1 < ../0001-add-parenthesis-around-expression.patch
	patch -Np1 < ../0002-use-Gtk-MenuButton-for-context-button.patch
}

build() {
	cd "${srcdir}/${pkgname}"

	./autogen.sh \
		--prefix=/usr
	make
}

package() {
	cd "${srcdir}/${pkgname}"

	make DESTDIR="${pkgdir}" install
}

