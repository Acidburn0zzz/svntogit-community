# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=cargo-fuzz
pkgver=0.6.0
pkgrel=1
pkgdesc="Command line helpers for fuzzing rust"
url="https://github.com/rust-fuzz/cargo-fuzz"
depends=('cargo')
arch=('x86_64')
license=('MIT' 'APACHE')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/rust-fuzz/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('b647e8d68a93eaa099dc1bf5e5e5e57ee8bd0e03f023c1664e6535e2938acfa5b884bc938cccd22d1071021170104fa48177ff6eab86d11cd0d1ecf1835d6aa0')
b2sums=('a9a778545ea7d9707fb24534684366e71ddcd08fde07626d8f7b3b67d4189eed09abc47c7a7eeaae3919904c6c534005a8d3e6f77b5de592c72a35937eb628da')

build() {
  cd "${pkgname}-${pkgver}"
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  # this is broken on linux-hardened
  #RUSTC_BOOTSTRAP=1 cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 "target/release/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
