# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.13
_commit="03c698e926d4421ba43d905b6d9fd7388b97352f"
pkgrel=6
pkgdesc="An advanced and well-established feature-rich text mode web browser."
arch=("i686" "x86_64")
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua>=5.1.1' 'libidn' 'js'
	 'gc' 'tre' 'smbclient')
source=("http://arch.p5n.pp.ru/~sergej/dl/elinks-$pkgver-${_commit}.tar.gz")
md5sums=('85916038633d0eb4ed98c3f82ac0b405')

build() {
  cd "$srcdir/elinks"
  [ -x configure ] || sh autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --enable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --with-zlib
  make
}

package() {
  cd "$srcdir/elinks"
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/share/locale/locale.alias"
  install -D -m644 "contrib/debian/$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
}
