# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>
# Contributor: damir <damir@archlinux.org>
# Contributor: Ben <contrasutra@myrealbox.com>

pkgname=elinks
pkgver=0.13
_commit="fe83703714fd5e16401dcd152d9bdcec6036424d"
pkgrel=5
pkgdesc="An advanced and well-established feature-rich text mode web browser."
arch=("i686" "x86_64")
url="http://elinks.or.cz"
license=('GPL')
depends=('bzip2' 'expat>=2.0' 'gpm>=1.20.4' 'openssl' 'lua>=5.1.1' 'libidn' 'spidermonkey>=1.7.0'
	 'gc' 'tre' 'smbclient')
source=()
md5sums=()

__gitroot="http://elinks.cz/elinks.git"
__gitname="elinks"

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."
 
  if [ -d $__gitname ] ; then
    cd $__gitname && git pull origin
    msg "The local files are updated."
  else
    git clone $__gitroot
  fi
 
  msg "GIT checkout done or server timeout"
  msg "Starting make..."
 
  rm -rf "$srcdir/$__gitname-build"
  git clone "$srcdir/$__gitname" "$srcdir/$__gitname-build"
  cd "$srcdir/$__gitname-build"
  git checkout "$_commit"

  [ -x configure ] || sh autogen.sh
  ./configure --prefix=/usr --mandir=/usr/share/man \
              --sysconfdir=/etc \
              --enable-smb --without-x --enable-cgi \
              --enable-leds --enable-256-colors --enable-html-highlight \
              --with-zlib
  make
}

package() {
  cd "$srcdir/$__gitname-build"
  make DESTDIR="$pkgdir" install
  rm -f "$pkgdir/usr/share/locale/locale.alias"

  install -D -m644 "$srcdir/$__gitname-build/contrib/debian/$pkgname.desktop" \
  "$pkgdir/usr/share/applications/$pkgname.desktop"
}
