# Maintainer: David Runge <dvzrv@archlinux.org>

_name=BChoppr
pkgname=bchoppr
pkgver=1.10.2
pkgrel=1
pkgdesc="An audio stream chopping LV2 plugin"
arch=('x86_64')
url="https://github.com/sjaehn/BChoppr"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio')
depends=('cairo' 'gcc-libs' 'glibc' 'libx11' 'lv2-host')
makedepends=('lv2')
checkdepends=('lv2lint')
source=("$pkgname-$pkgver.tar.gz::https://github.com/sjaehn/${_name}/archive/${pkgver}.tar.gz")
sha512sums=('84c877cf367ff879ca4841c5da25de38d842b07c47d4db76d63a8b2f7b624a1f545210eb2c08090bca92968cb6d1e0771b0ebcbc4f48b0766fc3afb6c84a6ffc')
b2sums=('0f41a0461332c7ea204f25826b0fbf7e9d81bacc0bc8676026a185ab08abf51eebdd0a26dd88c3ed97df9f7be187dfa60bd129e339a7c4e34361cd2bcab4edf1')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  make PREFIX='/usr'
}

check() {
  cd "$pkgname-$pkgver"
  lv2lint -Mpack -I "${PWD}/${_name}.lv2/" "https://www.jahnichen.de/plugins/lv2/${_name}"
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" PREFIX='/usr' install
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  # remove useless license file
  find "${pkgdir}/usr/lib/" -type f -iname "*LICENSE*" -delete
}
