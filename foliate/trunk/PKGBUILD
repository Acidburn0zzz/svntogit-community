# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=foliate
pkgver=1.5.3
pkgrel=1
pkgdesc="A simple and modern GTK eBook reader"
arch=("i686" "x86_64")
url="https://johnfactotum.github.io/foliate/"
license=("GPL3")
depends=("gjs"  "webkit2gtk")
makedepends=("meson" "ninja" "gettext")
optdepends=(
	"hyphen: Auto-hyphenation support"
	"dictd: Offline dictionary support"
	"festival: Text-to-speech support; also voice package needed"
	"espeak-ng: Text-to-speech support; also voice package needed"
)
source=("https://github.com/johnfactotum/foliate/archive/$pkgver.tar.gz")
sha256sums=("95f45c0511493a3c732f9a199ddc2b5851a220d74d93e6d14976f29f243ee2d7")


build() {
	cd "$srcdir/$pkgname-$pkgver"
	meson build --prefix=/usr
	ninja -C build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
	ln -sv com.github.johnfactotum.Foliate "$pkgdir/usr/bin/foliate"
}
