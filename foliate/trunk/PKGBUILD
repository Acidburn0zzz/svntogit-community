# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=foliate
pkgver=2.3.0
pkgrel=1
pkgdesc="A simple and modern GTK eBook reader"
arch=("i686" "x86_64")
url="https://johnfactotum.github.io/foliate/"
license=("GPL3")
depends=("gjs"  "webkit2gtk")
makedepends=("meson" "ninja" "gettext")
optdepends=(
	"hyphen: Auto-hyphenation support"
	"dictd: Offline dictionary support"
	"festival: Text-to-speech support; also voice package needed"
	"espeak-ng: Text-to-speech support; also voice package needed"
)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/johnfactotum/foliate/archive/$pkgver.tar.gz")
sha256sums=('96f0eb3426300293242331a8a67fedf92b7cec46774512d19c691f42e06ce187')


build() {
	cd "$srcdir/$pkgname-$pkgver"
	meson build --prefix=/usr
	ninja -C build
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	DESTDIR="$pkgdir" ninja -C build install
	ln -sv com.github.johnfactotum.Foliate "$pkgdir/usr/bin/foliate"
}
