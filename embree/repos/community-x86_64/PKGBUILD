# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgname=embree
pkgver=3.3.0
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'freeglut' 'libxmu' 'openexr')
provides=('embree-isa')
replaces=('embree-isa')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/embree/embree/archive/v${pkgver}.tar.gz")
sha256sums=('b7afee01034544d80cce4f81eb3ead03c527728186ff96b4662a242252e224f6')

build() {
    cd ${pkgname}-${pkgver}
    cmake . \
      -DCMAKE_INSTALL_PREFIX=/usr \
      -DCMAKE_INSTALL_LIBDIR=lib \
      -DCMAKE_BUILD_TYPE=Release \
      -DEMBREE_TUTORIALS=OFF \
      -DEMBREE_MAX_ISA="AVX512SKX"
    # Embree detects actual ISA at runtime
    make
}

package() {
    cd ${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}

