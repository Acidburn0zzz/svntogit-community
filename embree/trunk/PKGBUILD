# $Id$
# Maintainer: Lukas Jirkovsky <l.jirkovsky@gmail.com>
pkgname=embree
pkgver=2.10.0
pkgrel=1
pkgdesc="A collection of high-performance ray tracing kernels"
arch=('x86_64')
url="https://embree.github.io/"
license=('Apache')
depends=('intel-tbb')
makedepends=('cmake' 'ispc' 'openexr')
source=("$pkgname-${pkgver}.tar.gz::http://github.com/embree/embree/archive/v${pkgver}.tar.gz"
        "rtcore_ray_forceinline.diff")
md5sums=('a130a5a36d0ad2c93f1ecfef7dea208f'
         '2ad75c38bdf4b035d01ba4c6cec53ec2')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"
  # see https://github.com/embree/embree/issues/88
  patch -Np1 < "$srcdir/rtcore_ray_forceinline.diff"
}

build() {
  cd "$srcdir/$pkgname-$pkgver"

  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DENABLE_TUTORIALS=OFF \
    -DTBB_ROOT=/usr
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

