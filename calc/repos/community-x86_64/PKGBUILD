# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=calc
pkgver=2.12.5.6
pkgrel=1
pkgdesc="console calculator"
arch=(i686 x86_64)
url="http://www.isthe.com/chongo/tech/comp/calc/"
license=('GPL')
depends=('readline')
makedepends=('chrpath')
#source=(http://switch.dl.sf.net/sourceforge/calc/calc-$pkgver.tar.bz2)
source=(http://www.isthe.com/chongo/src/calc/calc-$pkgver.tar.bz2)
sha256sums=('76090203a96d35dd10103112a7ad612f4d9b0526b758f2f49ac5bef02ba7cb39')

build() {
  cd "$srcdir"/calc-$pkgver
  make USE_READLINE="-DUSE_READLINE" READLINE_LIB="-lreadline" READLINE_EXTRAS="-lhistory -lncurses" -j1
}

package() {
  cd "$srcdir"/calc-$pkgver
  make T="$pkgdir" install
  mv "$pkgdir"/usr/bin/cscript "$pkgdir"/usr/share/calc/
  chrpath -d "$pkgdir"/usr/bin/calc
}
