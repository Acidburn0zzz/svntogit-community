# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=safecopy
pkgname=haskell-safecopy
pkgver=0.9.3.2
pkgrel=4
pkgdesc="Binary serialization with version control."
url="http://acid-state.seize.it/safecopy"
license=('custom:PublicDomain')
arch=('i686' 'x86_64')
depends=('ghc-libs' 'haskell-cereal' 'haskell-old-time' 'haskell-text' 'haskell-vector')
makedepends=('ghc')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('d4dca927b5e6ab78e4548db550c416314507d11e4403a41dc1643e52319a2d397bc1fe8ea6c78b8d2fdaa3bca05603222380dc1a4ad1d956aeacc98919a35943')

prepare() {
    sed -e 's/time < 1.7,/time,/' -e 's/template-haskell < 2.12,/template-haskell,/' -i $_hkgname-$pkgver/$_hkgname.cabal
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"

    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    runhaskell Setup copy --destdir="${pkgdir}"

    # Remove static libs
    find "$pkgdir"/usr/lib -name "*.a" -delete
}
