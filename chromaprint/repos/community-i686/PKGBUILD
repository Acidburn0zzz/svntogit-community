# $Id$
# Contributor: Wieland Hoffmann <the_mineo@web.de>
# Maintainer: Gaetan Bisson <bisson@archlinux.org>

pkgname=chromaprint
pkgver=0.6
pkgrel=3
pkgdesc='Library that implements a custom algorithm for extracting fingerprints from any audio source'
url='http://acoustid.org/chromaprint/'
arch=('i686' 'x86_64')
license=('LGPL')
depends=('ffmpeg')
makedepends=('cmake')
source=("https://github.com/downloads/lalinsky/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        'ffmpeg.patch')
sha1sums=('e8bcc1d0d8dfec86aa648b87ba3f69b6d589eae0'
          '56057f824a29194fc6041bd73e0d07412fcc065b')

build() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	patch -p1 -i ../ffmpeg.patch
	cmake -DCMAKE_INSTALL_PREFIX=/usr -DBUILD_EXAMPLES=ON .
	make
}

package() {
	cd "${srcdir}/${pkgname}-${pkgver}"
	make DESTDIR="${pkgdir}" install
}
