# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: John Gerritse <john.gerritse@gmail.com>

pkgname=atop
pkgver=2.0_1
pkgrel=1
pkgdesc="A system and process level monitor."
arch=(i686 x86_64)
url="http://www.atoptool.nl/"
license=('GPL')
depends=('ncurses' 'sh' 'zlib')
source=(http://www.atoptool.nl/download/atop-${pkgver/_/-}.tar.gz)
md5sums=('6867ea2211edaf8140b94130470d646b')

build() {
  cd $srcdir/$pkgname-${pkgver/_/-}
  sed -i 's#root ##' atop.cron
  make
}

package() {
  cd $srcdir/$pkgname-${pkgver/_/-}
  make DESTDIR=$pkgdir INIPATH=/etc/rc.d install
  rm -f $pkgdir/etc/cron.d/atop
  install -dm0755 $pkgdir/etc/cron.daily
  mv $pkgdir/etc/atop/atop.daily $pkgdir/etc/cron.daily/atop
  sed -i 's|/etc/atop/atop.daily|/etc/cron.daily/atop|g' $pkgdir/etc/rc.d/atop
  install -Dm0644 atop.service $pkgdir/usr/lib/systemd/system/atop.service
}
