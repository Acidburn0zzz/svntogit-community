# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-ddt
pkgver=1.3.0
pkgrel=1
pkgdesc="Data-Driven/Decorated Tests"
arch=('any')
license=('MIT')
url="https://github.com/txels/ddt"
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-yaml')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txels/ddt/archive/$pkgver.tar.gz")
sha512sums=('8738765f37132e3cb87737ad2e73a760e1021b7defcd1e7b024b29331ebd2e44d2fff0b3758de4c5cb27cc1c20020dc2d56e6da5dec1155ba44665e6b1585506')

build() {
  cd ddt-$pkgver
  python setup.py build
}

check() {
  cd ddt-$pkgver
  nosetests3
}

package() {
  cd ddt-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.md
}
