# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=tasty-golden
pkgname=haskell-tasty-golden
pkgver=2.3.2.1
pkgrel=1
pkgdesc="Golden tests support for tasty"
url="https://github.com/feuerbach/tasty-golden"
license=("MIT")
arch=('x86_64')
depends=('ghc-libs' "haskell-tasty" "haskell-optparse-applicative" "haskell-temporary"
         "haskell-tagged" "haskell-async")
makedepends=('ghc' 'haskell-tasty-hunit')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
sha512sums=('3aac63c5efbe6600840417a26b11b6374119cf39e3aafab9132f82e4632f075c7a8bf37e925f435878846a753a81582a1e42badd4f7d7710a5b68bcf6b6c7157')

prepare() {
    cd $_hkgname-$pkgver
    sed -i 's/temporary-rc/temporary/' ${_hkgname}.cabal
}

build() {
    cd $_hkgname-$pkgver

    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver

    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
