# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-ldap3
pkgname=(python-ldap3 python2-ldap3)
pkgver=2.5.2
pkgrel=1
pkgdesc="A strictly RFC 4510 conforming LDAP V3 pure Python client library"
url="https://github.com/cannatag/ldap3"
license=('LGPL')
arch=('any')
makedepends=('python-pyasn1' 'python2-pyasn1' 'python-setuptools' 'python2-setuptools')
checkdepends=('python-nose' 'python2-nose')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/cannatag/ldap3/archive/v$pkgver.tar.gz")
sha512sums=('212b79cc24c55639ce9fab1b3340bb10c4f00b6f08886d5d3120f0ef3af3a138f02c1e63ee55ed22a877d8ecd8fb45447b8bf60371de00520cfc38df7713c20d')

prepare() {
  cp -a ldap3-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/ldap3-$pkgver
  python setup.py build

  cd "$srcdir"/ldap3-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/ldap3-$pkgver
  nosetests3 -s test || warning "Tests require a running LDAP server"

  cd "$srcdir"/ldap3-$pkgver-py2
  nosetests2 -s test || warning "Tests require a running LDAP server"
}

package_python-ldap3() {
  depends=('python-pyasn1')

  cd ldap3-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-ldap3() {
  depends=('python2-pyasn1')

  cd ldap3-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
