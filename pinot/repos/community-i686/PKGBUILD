# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Alexander Fehr <pizzapunk gmail com>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=pinot
pkgver=1.01
pkgrel=1
pkgdesc='Personal search and metasearch tool'
arch=('i686' 'x86_64')
url='http://code.google.com/p/pinot-search/'
license=('GPL')
depends=('gtkmm3' 'xapian-core' 'libtextcat' 'sqlite' 'libxml++' 'curl' 'gmime' 
         'dbus-glib' 'shared-mime-info' 'libexif' 'taglib' 'hicolor-icon-theme' 
         'cairo' 'exiv2' 'boost-libs')
makedepends=('boost' 'desktop-file-utils')
optdepends=('unzip: ZIP files extraction'
            'poppler: PDF to text conversion'
            'catdvi: DVI to text conversion'
            'djvulibre: DjVu text extraction'
            'unrtf: RTF to HTML conversion'
            'antiword: MS Word to text conversion'
            'catdoc: XLS and PPT to text conversion')
options=('!emptydirs')
install=$pkgname.install
changelog=$pkgname.changelog
source=(http://pinot-search.googlecode.com/files/$pkgname-$pkgver.tar.gz)
sha256sums=('a2a82009db05ca7abb4ffbd8baafa8fb820a7e961fd3cdcb21af46bb05eb57d4')

build() {
  cd "${srcdir}/$pkgname-$pkgver"
  sed -i 's|/usr/share/libtextcat/|/usr/share/libtextcat/LM/|' textcat_conf.txt
  sed -i 's|/LM/russian.lm|/LM/russian-windows1251.lm|' textcat_conf.txt
  sed -i 's|/usr/share/libtextcat/|/usr/share/libtextcat/LM/|' textcat3_conf.txt
  sed -i 's|/usr/share/libtextcat/|/usr/share/libtextcat/LM/|' textcat31_conf.txt
  sed -i 's|/usr/share/libtextcat/|/usr/share/libtextcat/LM/|' textcat32_conf.txt
  sed -i -e "s|.*russian$|/usr/share/libtextcat/LM/russian-iso8859_5.lm   russian-iso8859_5\n\
/usr/share/libtextcat/LM/russian-koi8_r.lm      russian-koi8_r\n\
/usr/share/libtextcat/LM/russian-windows1251.lm russian-windows1251|" textcat3_conf.txt

  LIBS="-lboost_system" ./configure --prefix=/usr --sysconfdir=/etc --libexecdir=/usr/lib --enable-gtkmm3=yes
  make
}

package() {
  cd "${srcdir}/$pkgname-$pkgver"

  make DESTDIR="${pkgdir}" install
}
