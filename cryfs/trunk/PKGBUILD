# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andy Weidenbaum <archbaum@gmail.com>
# Contributor: kaylyn
# Contributor: Giuseppe Cal√† <jiveaxe@gmail.com>

pkgname=cryfs
pkgver=0.11.1
pkgrel=2
pkgdesc='Cryptographic filesystem for the cloud'
arch=(x86_64)
depends=(fuse2 curl spdlog fmt boost-libs)
makedepends=(cmake python boost range-v3)
url='https://www.cryfs.org'
license=(LGPL3)
source=(https://github.com/cryfs/cryfs/releases/download/$pkgver/cryfs-$pkgver.tar.xz{,.asc}
        fix-openmp.patch)
sha256sums=('55f139b07b9737851cc0d6e26c425a7debc2fabd2a62aa43ba56e5a33ca93ece'
            'SKIP'
            '9db0aeef631c21a4d10a4235a16573e2ef81808fb7645ecf2b77615e6978b0b1')
validpgpkeys=(5D5EC7BC6F1443EC2AF7177A9E6C996C991D25E1) # CryFS Team <messmer@cryfs.org>

prepare() {
  sed -e '/Release/d' -i doc/CMakeLists.txt # Install man page also with dafault build type
  patch -p1 < fix-openmp.patch # Fix openMP support https://github.com/cryfs/cryfs/issues/414
}

build() {
  cmake -B build -S . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_TESTING=OFF \
    -DCRYFS_UPDATE_CHECKS=OFF \
    -DDEPENDENCY_CONFIG="cmake-utils/DependenciesFromLocalSystem.cmake"
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -Dm 644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}
