# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-news
_name=news
pkgver=16.2.1
pkgrel=1
pkgdesc="An RSS/Atom feed aggregator"
arch=('any')
url="https://github.com/nextcloud/news"
license=('AGPL3')
makedepends=('nextcloud' 'ripgrep' 'yq')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/${pkgver}/${_name}.tar.gz")
sha512sums=('a3e7df5bb1a1ee4172bead0c69960b4fb87705b0ab2181f5486c281c48e746a2942d4fa1b53b5b422f149d551d9506b22814e4cb96d990ac34f16e0fe04a1122')
b2sums=('e8f9c63c954d8de5396780d5d0e5aa404a4cc6aa3882275afe85f0217368987d877a5c1f54bc511b97c006d360fb483e548cd726b8299811719708dd52ee20c7')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
