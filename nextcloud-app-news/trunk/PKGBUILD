# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Jonas Heinrich <onny@project-insanity.org>

pkgname=nextcloud-app-news
_name=news
pkgver=15.4.3
pkgrel=1
pkgdesc="An RSS/Atom feed aggregator"
arch=('any')
url="https://github.com/nextcloud/news"
license=('AGPL3')
depends=('nextcloud>21' 'nextcloud<22')
makedepends=('ripgrep' 'yq')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/${_name}/releases/download/${pkgver}/${_name}.tar.gz")
sha512sums=('40545903f23012417db981c0400fe9909cbbd3f7e595e891da85992e2eb97898f847369789827f88d49cddf3917d8285513556f5021339a699c423f50aa9f13b')
b2sums=('fd1b662f2393d40644a53997764b803563c081ef2fe2c8fb37d0762073141cf700f888b1d169b5b959b7ff1f992c3a1914fa812c0c522818f37276a5fd8cc1dc')

check() {
  local _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}

package() {
  install -vdm 755 "${pkgdir}/usr/share/webapps/nextcloud/apps"
  cp -av "${_name}" "${pkgdir}/usr/share/webapps/nextcloud/apps/"
}
