# Maintainer: David Runge <dave@sleepmap.de>
# Contributor: Joakim Hernberg <jhernberg@alchemy.lu>
# Contributor: Ninez <triplesquarednine@gmail.com>

pkgname=python-linux-procfs
pkgver=0.6.1
pkgrel=3
pkgdesc="Linux /proc abstraction classes for python"
arch=('any')
url="https://git.kernel.org/pub/scm/libs/python/python-linux-procfs/python-linux-procfs.git/"
license=('GPL2')
depends=('python-six')
makedepends=('python-setuptools')
source=("https://git.kernel.org/pub/scm/libs/python/${pkgname}/${pkgname}.git/snapshot/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d4dbfcbd76013c8f3008019156aa48d6d1ad082b51ee4f8b3d60f32ba9fb6f9d1f840819f725a6bcf525ce5b3742c3e8af32b2ad06bcf9af7329315685868b50')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  export PYTHONPATH="build:${PYTHONPATH}"
  python bitmasklist_test.py
}

package() {
  cd "${pkgname[0]}-${pkgver}"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}/"
}
