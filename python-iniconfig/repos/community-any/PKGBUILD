# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-iniconfig
pkgname=(python-iniconfig python2-iniconfig)
pkgver=1.1.0
pkgrel=1
pkgdesc="brain-dead simple config-ini parsing"
url="https://github.com/RonnyPfannschmidt/iniconfig"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-setuptools-scm'
             'python2-setuptools-scm')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/RonnyPfannschmidt/iniconfig/archive/v$pkgver.tar.gz")
sha512sums=('f5fcea660910ae03e45ada19c7c3b03f240b1cb1cadbf1a9e509ef8536c3d1158d4a9728cce375c8fe4b311eacfa126251d7c6e5f501f24487892e5286f5f380')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

prepare() {
  cp -a iniconfig-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/iniconfig-$pkgver
  python setup.py build

  cd "$srcdir"/iniconfig-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/iniconfig-$pkgver
  python setup.py pytest

  cd "$srcdir"/iniconfig-$pkgver-py2
  python2 setup.py pytest
}

package_python-iniconfig() {
  depends=('python')

  cd iniconfig-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-iniconfig() {
  depends=('python2')

  cd iniconfig-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
