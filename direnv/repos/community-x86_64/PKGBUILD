# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: zimbatm <zimbatm@zimbatm.com>
# Contributor: ShadowKyogre <shadowkyogre.public+aur@gmail.com>
# Contributor: rmorgans <rick.morgans@gmail.com>

pkgname=direnv
pkgver=2.30.0
pkgrel=1
pkgdesc='A shell extension that manages your environment'
arch=('x86_64')
url="https://direnv.net"
license=('MIT')
depends=('glibc')
makedepends=('go')
checkdepends=('zsh' 'fish' 'bash' 'tcsh' 'elvish')
options=('!lto')
source=("$pkgname-$pkgver.tar.gz::https://github.com/direnv/direnv/archive/v$pkgver.tar.gz")
sha512sums=('b6687556e0611a061a003b5411971c876c4f29ba5c7d299b69352334a7ebcfa9810d5140d1a776c814ec195834712ca167a560c81a12f6c8fd0f42ed34483013')
b2sums=('5006234ccbc90571d43ef385511b38fe154ce85c0aadeef9ae33821c35bcbc64aa0ef13885053cdcde89a0c145d4b545302e095b04db1ad55ba577d88e159f4c')

prepare() {
  cd "$pkgname-$pkgver"

  # download dependencies
  go mod download
}
build() {
  cd "$pkgname-$pkgver"

  # set Go flags
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"

  go build -v \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags "-linkmode external -extldflags ${LDFLAGS}" \
    .
}

check() {
  cd "$pkgname-$pkgver"

  # run go tests
  go test -v ./...

  # run shell-specific tests
  bash ./test/direnv-test.bash
  elvish ./test/direnv-test.elv
  fish ./test/direnv-test.fish
  tcsh -e ./test/direnv-test.tcsh
  zsh ./test/direnv-test.zsh

  # run stdlib tests
  ./test/stdlib.bash
}

package() {
  cd "$pkgname-$pkgver"

  # binary
  install -vDm755 -t "$pkgdir/usr/bin" "$pkgname"

  # documentation
  install -vDm644 -t "$pkgdir/usr/share/man/man1" man/*.1
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname" README.md
  install -vDm644 -t "$pkgdir/usr/share/doc/$pkgname/docs" docs/*

  # license
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" LICENSE
}
