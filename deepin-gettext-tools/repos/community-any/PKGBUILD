# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gettext-tools
pkgver=1.0.4
pkgrel=3
pkgdesc='Deepin Gettext Tools'
arch=('any')
url="https://github.com/linuxdeepin/deepin-gettext-tools"
license=('GPL3')
depends=('gettext' 'python2' 'perl-xml-libxml' 'perl-xml-libxml-prettyprint')
makedepends=('git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-gettext-tools/archive/$pkgver.tar.gz")
sha256sums=('6458cb9a504207d3905421c7b0ef52127b636de824cf67d1d2ff500d8dbb6fdb')

prepare() {
  cd deepin-gettext-tools-$pkgver

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='

  sed -e 's/sudo cp/cp/' -i src/generate_mo.py
  sed -e 's/qmake/qmake-qt5/' -e '/lupdate/d' -i Makefile
}

package() {
  cd deepin-gettext-tools-$pkgver
  make DESTDIR="$pkgdir" install
}
