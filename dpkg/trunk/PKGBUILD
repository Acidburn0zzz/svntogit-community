# Maintainer: Anatol Pomozov
# Contributor: Alad Wenter <alad (at) archlinux.info>
# Contributor: Jochen Schalanda <jochen+aur (at) schalanda.name>
# Contributor: C. Dominik BÃ³di <dominik.bodi@gmx.de>
# Contributor: Pierre Carrier <pierre@spotify.com>
# Contributor: Thomas Dziedzic <gostrc (at) gmail>
# Contributor: Chris Giles <Chris.G.27 (at) Gmail.com>
# Contributor: seblu <seblu+arch (at) seblu.net>
# Contributor: squiddo <squiddo (at) intheocean.net>
# Contributor: dront78 <dront78 (at) gmail.com>
# Contributor: hugelgupf <ckoch (at) cs.nmt.edu>

pkgname=dpkg
pkgver=1.20.0
pkgrel=1
pkgdesc='The Debian Package Manager tools'
arch=('x86_64')
url="https://tracker.debian.org/pkg/dpkg"
license=('GPL')
options=('emptydirs')
depends=('xz' 'zlib' 'bzip2' 'perl')
makedepends=('perl-io-string' 'perl-timedate' 'git')
checkdepends=('perl-io-string' 'perl-test-pod')
install=dpkg.install
source=(https://salsa.debian.org/dpkg-team/dpkg/-/archive/$pkgver/dpkg-$pkgver.tar.bz2)
sha256sums=('036b402c49b2ef0c6e2c43ef0aaa3f13c3f16ce1de615fba0ef1f28400fe7035')

prepare() {
    cd dpkg-$pkgver
    echo "$pkgver" > .dist-version
    ./autogen
}

build() {
    cd dpkg-$pkgver
    ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --libexecdir=/usr/lib \
        --disable-start-stop-daemon
    make
}

check() {
    cd dpkg-$pkgver
    # make check
}

package() {
    cd dpkg-$pkgver
    make DESTDIR="$pkgdir" install

    install -d "$pkgdir/var/dpkg"/updates/
    touch "$pkgdir/var/lib/dpkg"/{status,available}
}
