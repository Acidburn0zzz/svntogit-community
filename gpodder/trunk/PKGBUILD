# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Magnus Brevik <janmbrevik@gmail.com>

pkgname=gpodder
pkgver=3.10.5
pkgrel=2
pkgdesc='Podcast client written in Python using GTK+'
arch=(any)
url='https://gpodder.github.io/'
license=(GPL3)
depends=(gtk3 python-cairo python-dbus python-gobject python-mygpoclient python-podcastparser)
makedepends=(intltool)
source=($pkgname-$pkgver.tar.gz::http://github.com/gpodder/$pkgname/archive/$pkgver.tar.gz
        org.gpodder.gpodder.appdata.xml)
sha256sums=('9b65956d8a55917b19ea09f631b2cdf600a3b07f00bbb38a88c1651ba71d6e8a'
            'bf1ee775db693cfbbf5a63f5d4840c28e271256be684da85578e6837844dcece')

build() {
  cd $pkgname-$pkgver
  make messages
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 ../org.gpodder.gpodder.appdata.xml "$pkgdir/usr/share/metainfo/org.gpodder.gpodder.appdata.xml"
}
