# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan Magnus Brevik <janmbrevik@gmail.com>

pkgname=gpodder
pkgver=3.10.3
pkgrel=3
pkgdesc='Podcast client written in Python using GTK+'
arch=(any)
url='https://gpodder.github.io/'
license=(GPL3)
depends=(gtk3 python-cairo python-dbus python-gobject python-mygpoclient python-podcastparser)
makedepends=(intltool)
source=($pkgname-$pkgver.tar.gz::http://github.com/gpodder/$pkgname/archive/$pkgver.tar.gz
        python37.patch)
sha256sums=('a53b23c605b3a92701aed3e318f0695ae2e4de1ba64692337e0a8f77253ebbb1'
            'ccd6c3c781e585b44e037c4ba7712477edd631b7982d93f546e2dbb081c7c8aa')

prepare() {
  cd $pkgname-$pkgver
  # Fix for Python 3.7
  patch -Np1 -i ../python37.patch
}

build() {
  cd $pkgname-$pkgver
  make messages
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
