# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=1.3.6.1
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('dtkwidget' 'dtkwm' 'procps-ng' 'nethogs' 'libcap')
makedepends=('qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('544b754012dc435c0ea2d02d92f0cf65a091a40e762928eb4f1576034dd3156a18fe91af31a33fbe2d1bef350d1f90dd3d609fac2b677544f368bac7c2120641')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
