# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=5.6.7
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-qt5integration' 'dtkwm' 'procps-ng' 'libcap')
makedepends=('deepin-gettext-tools' 'qt5-tools')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz"
        qt-5.14.patch
        deepin-system-monitor-qt5.15.patch)
sha512sums=('ad290bc9cd4e69119b6671b8a80a44c6453f5113f5eba90d8cb67790ebaa0b0f277fc3b23f0cae794c4a09b145444a588c8de9c149aeeffcb1927253f91149cf'
            'c87ac75fdb2b9c70d8fdaf90a742e3090090521ce139903b1b0c4130ccaaa9c17c90ea511a42621c740d3b93aef7f91898686c832cb6e6c6434e59f010063a37'
            '45f9938885c6e29abf61174db3bc5654646baa7a8ec067ee4e048511eb62ae79996976c02d9275e8fdf1a7321dd0219b58ae54d8d370c5acf8346dd77959c608')

prepare() {
  cd deepin-system-monitor-$pkgver
  patch -p1 -i ../qt-5.14.patch
  patch -p1 -i ../deepin-system-monitor-qt5.15.patch # Fix build with Qt 5.15
  sed -i '/<QPixmap>/a #include <QUrl>' src/process/stats_collector.cpp

# Workaround build failure with GCC 10
  sed -e 's|print_err|print_err_system|g' -i src/process/system_stat.cpp
  sed -e 's|print_err|print_err_process|g' -i src/process/process_stat.cpp
  sed -e 's|print_err|print_err_desktop|g' -i src/process/desktop_entry_stat.cpp
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
