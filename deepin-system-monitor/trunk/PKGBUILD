# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-system-monitor
pkgver=0.0.5
pkgrel=1
pkgdesc='A more user-friendly system monitor'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-system-monitor"
license=('GPL3')
depends=('deepin-tool-kit' 'procps-ng' 'nethogs' 'libcap')
groups=('deepin')
install=deepin-system-monitor.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-system-monitor/archive/$pkgver.tar.gz")
sha512sums=('42ce2fdfc7a935790057f1645513dcdfa34308891fb1754b3c1aa77ccae4cf52c44dfa630d5e060b591603a6c1039bb0be9378190a330f61c8442af84713c992')

prepare() {
  cd deepin-system-monitor-$pkgver
  rm -r nethogs
  sed -i 's|error("Build nethogs static library failed.")||' deepin-system-monitor.pro
}

build() {
  cd deepin-system-monitor-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-system-monitor-$pkgver
  install -Dm755 deepin-system-monitor "$pkgdir"/usr/bin/deepin-system-monitor
}
