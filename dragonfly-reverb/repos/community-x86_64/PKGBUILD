# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=dragonfly-reverb
_name=DragonflyReverb
pkgver=3.2.4
pkgrel=1
pkgdesc="A concert hall reverb and a room reverb"
arch=('x86_64')
url="https://michaelwillis.github.io/dragonfly-reverb/"
license=('GPL3')
groups=('lv2-plugins' 'pro-audio' 'vst-plugins')
depends=('gcc-libs' 'glibc' 'libglvnd' 'libx11')
makedepends=('freeverb3' 'gendesk' 'jack' 'lv2')
checkdepends=('lv2lint')
optdepends=('jack: for standalone applications'
            'lv2-host: for LV2 plugins'
            'vst-host: for VST plugins')
source=("$pkgname-$pkgver.tar.gz::https://github.com/michaelwillis/${pkgname}/releases/download/${pkgver}/${_name}-Source-v${pkgver}.tar.gz")
sha512sums=('2ef32257171d7d019314bc0446c58d535939026c55325a236bfcfe8715eb1ddc497843b0b50f283604f59d2722846c9af4add15200f5aad7a2bc57026fd20bb7')
b2sums=('e36b9d2a285bcc523545df5797d5be3c1de51c11ccf56334a4f34b9bcf8691858d632c1d0784c5f1d13dba217da5ec2ffd29771abefe88c449a9ac5be8681d83')
_names=('DragonflyEarlyReflections' 'DragonflyHallReverb' 'DragonflyPlateReverb' 'DragonflyRoomReverb')

prepare() {
  declare -A _description=(
    ["DragonflyEarlyReflections"]="A free stereo-to-stereo early reflections reverb effect"
    ["DragonflyHallReverb"]="A free stereo-to-stereo concert hall reverb effect"
    ["DragonflyPlateReverb"]="A free stereo-to-stereo plate reverb effect"
    ["DragonflyRoomReverb"]="A free stereo-to-stereo room reverb effect"
  )
  declare -A _generic=(
    ["DragonflyEarlyReflections"]="Early Reflections Reverb"
    ["DragonflyHallReverb"]="Concert Hall Reverb"
    ["DragonflyPlateReverb"]="Plate Reverb"
    ["DragonflyRoomReverb"]="Room Reverb"
  )
  mv -v "DragonflyReverb-Source-v${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"

  for name in "${_names[@]}"; do
    gendesk -n \
            --pkgname "io.github.michaelwillis.${name}" \
            --name "${name}" \
            --exec "${name}" \
            --pkgdesc "${comments[$name]}" \
            --genericname "${generic[$name]}" \
            --categories "AudioVideo;Audio"
  done

  # fix broken use of PKG_CONFIG in Makefiles:
  # https://github.com/michaelwillis/dragonfly-reverb/issues/98
  sed -e 's/$PKG_CONFIG/$(PKG_CONFIG)/g' \
    -i plugins/dragonfly-{{hall,plate,room}-reverb,early-reflections}/Makefile
}

build() {
  cd "$pkgname-$pkgver"
  export SYSTEM_FREEVERB3="true"
  make
}

check() {
  local _name=""
  declare -A _urls=(
    ["DragonflyHallReverb"]='https://github.com/michaelwillis/dragonfly-reverb'
    ["DragonflyEarlyReflections"]='urn:dragonfly:early'
    ["DragonflyPlateReverb"]='urn:dragonfly:plate'
    ["DragonflyRoomReverb"]='urn:dragonfly:room'
  )

  cd "$pkgname-$pkgver"
  for _name in "${_names[@]}"; do
    lv2lint -Mpack -s "lv2_generate_ttl" -I "${PWD}/bin/${_name}.lv2/" "${_urls[$_name]}" \
            || echo "Known to fail: https://github.com/michaelwillis/dragonfly-reverb/issues/65"
  done
}

package() {
  depends+=('libfreeverb3.so')
  cd "$pkgname-$pkgver"
  for name in "${_names[@]}"; do
    install -vDm 755 "bin/${name}" -t "${pkgdir}/usr/bin"
    install -vDm 755 "bin/${name}-vst.so" -t "${pkgdir}/usr/lib/vst"
    install -vDm 755 "bin/${name}.lv2/"*.so \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
    install -vDm 644 "bin/${name}.lv2/"*.ttl \
      -t "${pkgdir}/usr/lib/lv2/${name}.lv2/"
  done
  install -vDm 644 *".desktop" -t "${pkgdir}/usr/share/applications/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
