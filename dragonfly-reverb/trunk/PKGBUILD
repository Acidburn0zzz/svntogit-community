# Maintainer: David Runge <dave@sleepmap.de>
pkgname=dragonfly-reverb
_name=DragonflyReverb
pkgver=1.1.3
pkgrel=1
pkgdesc="A free hall-style reverb based on freeverb3 algorithms"
arch=('x86_64')
url="https://github.com/michaelwillis/dragonfly-reverb"
license=('GPL3')
groups=('pro-audio' 'lv2-plugins' 'vst-plugins')
depends=('jack' 'libglvnd')
makedepends=('gendesk' 'liblo' 'lv2')
source=("$pkgname-$pkgver.tar.gz::https://github.com/michaelwillis/${pkgname}/releases/download/${pkgver}/${_name}-Source-v${pkgver}.tgz")
sha512sums=('1e93c1f067499153b4015715448f1d14effaeab06b0a0fb4d009fe5531730355c0830c65e46ae9694ee219da043c1476e0f73fccd4369072f170d9327209d55e')

prepare() {
  mv -v "DragonflyReverb-Source-v${pkgver}" "$pkgname-$pkgver"
  cd "$pkgname-$pkgver"
  # XDG desktop file
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name DragonflyReverb \
          --exec DragonflyReverb \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  export SYSTEM_LIBSAMPLERATE="true"
  make
}

package() {
  cd "$pkgname-$pkgver"
  install -vDm 755 "bin/${_name}" -t "${pkgdir}/usr/bin"
  install -vDm 644 "${pkgname}.desktop" \
    -t "${pkgdir}/usr/share/applications/"
  install -vDm 755 "bin/${_name}-vst.so" -t "${pkgdir}/usr/lib/vst"
  install -vDm 755 "bin/${_name}.lv2/"*.so \
    -t "${pkgdir}/usr/lib/lv2/${_name}.lv2/"
  install -vDm 644 "bin/${_name}.lv2/"*.ttl \
    -t "${pkgdir}/usr/lib/lv2/${_name}.lv2/"
  install -vDm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}
