# Maintainer:
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: tobias <tobias@archlinux.org>
# Contributor: Manolis Tzanidakis <manolis@archlinux.org>
# Contributor: Simon Lackerbauer <calypso "at" strpg.org>

pkgname=phpmyadmin
pkgver=5.0.1
pkgrel=1
pkgdesc='PHP and hence web-based tool to administrate MySQL over the WWW'
arch=('any')
url='https://www.phpmyadmin.net'
license=('GPL')
depends=('mysql-clients' 'php')
backup=(etc/webapps/phpmyadmin/config.inc.php)
source=(https://files.phpmyadmin.net/phpMyAdmin/$pkgver/phpMyAdmin-$pkgver-all-languages.tar.xz)
sha512sums=('576323a5e63438fe9e57e6165697c5e03e5291346f3786c84d5d6c4cd6851713cb10b2eed4b0c7e9ab6445dad9b06cbded6c03711fcc2942e999f2b5e7ab446b')

package() {
  _instdir="$pkgdir"/usr/share/webapps/phpMyAdmin
  mkdir -p "$_instdir" "$pkgdir"/etc/webapps/phpmyadmin
  cd "$_instdir"

  cp -ra "$srcdir"/phpMyAdmin-$pkgver-all-languages/* .

  ln -s /etc/webapps/phpmyadmin/config.inc.php "$_instdir"/config.inc.php
  cp "$_instdir"/config.sample.inc.php "$pkgdir"/etc/webapps/phpmyadmin/config.inc.php
}
