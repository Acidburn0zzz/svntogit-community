# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jens Adam (byte/jra) <j_adam@web.de>

pkgname=lazarus
pkgver=0.9.27
pkgrel=2
pkgdesc='Delphi-like IDE for FreePascal'
url='http://www.lazarus.freepascal.org/'
license=('GPL2' 'MPL' 'custom:LGPL')
arch=('i686' 'x86_64')
depends=('fpc' 'fpc-src' 'gdk-pixbuf')
options=('!emptydirs' '!makeflags')
source=(http://snapshots.lazarus.shikami.org/lazarus/downloads/lazarus-$pkgver-19010-20090317-src.tar.bz2)
md5sums=('f974f74c9ca54a919910ca3749699b13')

build() {
  cd $srcdir/${pkgname}
  # don't 'make examples' or you'll end up with a 100MB pkg
  make FPC=/usr/bin/fpc clean all || return 1

  # skip the 'make install' mess completely and do everything manually
  mkdir -p $pkgdir/usr/lib/lazarus $pkgdir/usr/bin
  rm -r debian
  cp -R . $pkgdir/usr/lib/lazarus || return 1

  ln -s /usr/lib/lazarus/lazarus $pkgdir/usr/bin/lazarus && \
  ln -s /usr/lib/lazarus/startlazarus $pkgdir/usr/bin/startlazarus && \
  ln -s /usr/lib/lazarus/lazbuild $pkgdir/usr/bin/lazbuild || return 1

#  install -Dm644 docs/lazbuild.1 $pkgdir/usr/share/man/man1/lazbuild.1

  # make 'desktop-file-validate' happy and fix missing .png icon
  sed -e 's|\(Categories\).*|\1=IDE;Development;|' \
      -e 's|\.png|\.xpm|' -i install/lazarus.desktop && \
  install -Dm644 install/lazarus.desktop $pkgdir/usr/share/applications/lazarus.desktop && \
  install -Dm644 images/ide_icon48x48.png $pkgdir/usr/share/pixmaps/lazarus.png && \
  rm -r $pkgdir/usr/lib/lazarus/install || return 1

  # license files: /usr/lib/lazarus/COPYING*
  install -D -m644 COPYING.modifiedLGPL.txt $startdir/pkg/usr/share/licenses/$pkgname/COPYING.modifiedLGPL
}
