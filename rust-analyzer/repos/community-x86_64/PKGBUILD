# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200615
_pkgver=2020-06-15
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('59b528a72a40cd83f2c53cce328868ed321f8a80935d7475e7c4a6be54e3adfc744ccc6dd125077be92a626b6797e53b13ec423de6f26418511b301d7469d6bb')

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

# https://github.com/rust-analyzer/rust-analyzer/issues/4218#issuecomment-625612149
# check() {
#   cd $pkgname-$_pkgver
#   cargo test --release --locked
# }

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
