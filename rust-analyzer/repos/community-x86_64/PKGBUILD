# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
pkgver=20200511
_pkgver=2020-05-11
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
sha512sums=('67247bd531f4accfb15d65398b226f054bb5e435ce1a7aff54525cfd4322dc74dabc13a8c2246be372424f76ad5f61997872db864f87152330ad0ccc43dba746')

pkgver() {
  echo ${_pkgver//-}
}

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

# https://github.com/rust-analyzer/rust-analyzer/issues/4218#issuecomment-625612149
# check() {
#   cd $pkgname-$_pkgver
#   cargo test --release --locked
# }

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}
