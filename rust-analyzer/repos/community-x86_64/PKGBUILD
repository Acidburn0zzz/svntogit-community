# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sergey A. <murlakatamenka@disroot.org>

pkgname=rust-analyzer
_pkgver=2020-12-21
pkgver=${_pkgver//-}
pkgrel=1
pkgdesc='Experimental Rust compiler front-end for IDEs'
arch=('x86_64')
url=https://github.com/rust-analyzer/rust-analyzer
license=('Apache' 'MIT')
depends=('gcc-libs' 'rust')
source=("$url/archive/$_pkgver/$pkgname-$_pkgver.tar.gz")
b2sums=('7dd8c7604ad085ccd75dc95250ecd76921764e944964891103bc1b1896cb7a0bbf8f12a083dbe4b2b7c7354a497b801c84c4e544238f0bea6b81fa30e9600d30')

build() {
  cd $pkgname-$_pkgver
  cargo build --release --locked
}

package() {
  cd $pkgname-$_pkgver
  install -Dt "$pkgdir"/usr/bin target/release/rust-analyzer
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
