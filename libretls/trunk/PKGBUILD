# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lillian rose winter <hi@neko.vg>
# Contributor: Evan McCarthy <evan@mccarthy.mn>

pkgname=libretls
pkgver=3.3.4
pkgrel=1
pkgdesc="libtls for OpenSSL"
arch=('x86_64')
url="https://git.causal.agency/${pkgname}"
license=('ISC' 'BSD' 'MIT')
depends=('openssl')
source=("https://causal.agency/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('24c73fe67679b6046eeb2e9ed8d0158325dcb246a474878dfa407e62bcc0916d')
b2sums=('08f85c420952599375ab3905238bb3fac474b2b97b6359517201dc85e5bf2d5dc9825c0ca186560435cf2a6ea5a6733e39d772116a892904ed0cc6f08e0f3f3f')

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    ./configure --prefix=/usr
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    make DESTDIR="${pkgdir}" install
    sed -n '1,/*\//p' tls.c | install -Dm644 /dev/stdin "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
