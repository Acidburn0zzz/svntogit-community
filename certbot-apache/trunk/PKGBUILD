# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Gordian Edenhofer <gordian.edenhofer[at]yahoo[dot]de>

pkgname=certbot-apache
pkgver=1.1.0
pkgrel=1
pkgdesc="Apache plugin for Letâ€™s Encrypt client"
arch=('any')
license=('Apache')
url="https://pypi.python.org/pypi/$pkgname"
replaces=("letsencrypt-apache")
depends=('certbot' 'python-acme' 'python-augeas' 'python-mock' 'python-setuptools'
         'python-zope-component' 'python-zope-interface')
checkdepends=('python-pytest')
source=("https://pypi.io/packages/source/c/$pkgname/$pkgname-$pkgver.tar.gz"{,.asc})
validpgpkeys=('148C30F6F7E429337A72D992B00B9CC82D7ADF2C'
              '1B41B758F9A78C7F444C20FA3E5B4152E3ADB1FD'
              'A2CFB51FA275A7286234E7B24D17C995CD9775F2')
sha512sums=('b38b4e1bd5b0abdd00bbf8e1ca802361e1ab808adbea2c306043b6ac32221d67d8e26b139d1ff0dde42e9401fde759089348f0976abcda6ab4bc26e3471ffd4f'
            'SKIP')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

check() {
  # https://github.com/certbot/certbot/issues/5510
  cd $pkgname-$pkgver
  python setup.py test
}

package() {
  cd $pkgname-$pkgver
  python setup.py install -O1 --root="$pkgdir"
}
