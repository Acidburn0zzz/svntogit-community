# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: mortzu

pkgname=ipv6calc
pkgver=0.99.2
pkgrel=1
pkgdesc='Small utility to manipulate, convert and calculate (more than) IPv6 addresses'
url='https://www.deepspace6.net/projects/ipv6calc.html'
license=('GPL2')
arch=('i686' 'x86_64')
# See https://www.deepspace6.net/projects/ipv6calc.html#idm140634407144416
makedepends=('geoip')
optdepends=('geoip: for country GeoIP support'
            'geoip-database-extra: for city-level GeoIP support')
source=("https://github.com/pbiering/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('f2eeec1b8d8626756f2cb9c461e9d1db20affccf582d43ded439bdb2d12646ef')

prepare() {
  cd "${pkgname}-${pkgver}"

  autoconf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix="/usr" \
    --libdir="/usr/lib" \
    --datarootdir="/usr/share" \
    --enable-shared \
    --enable-bundled-md5 \
    --enable-geoip \
    --with-geoip-dynamic=yes
  make
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
