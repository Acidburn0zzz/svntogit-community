# Maintainer: Baptiste Jonglez <baptiste--aur at jonglez dot org>
# Contributor: mortzu

pkgname=ipv6calc
pkgver=0.99.2
pkgrel=2
pkgdesc='Small utility to manipulate, convert and calculate (more than) IPv6 addresses'
url='https://www.deepspace6.net/projects/ipv6calc.html'
license=('GPL2')
arch=('i686' 'x86_64')
# See https://www.deepspace6.net/projects/ipv6calc.html#idm140634407144416
makedepends=('geoip')
optdepends=('geoip: for country GeoIP support'
            'geoip-database-extra: for city-level GeoIP support')
checkdepends=('perl-digest-sha1' 'perl-uri' 'perl-html-parser')
source=("ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "ftp://ftp.bieringer.de/pub/linux/IPv6/ipv6calc/CODE-GPG-KEY-bieringer.de-2013")
sha256sums=('f2eeec1b8d8626756f2cb9c461e9d1db20affccf582d43ded439bdb2d12646ef'
            'SKIP'
            '5499fe16d94c686d0c220b55592e129dca79741b96fa5b5cf2b4e872956e4801')
validpgpkeys=('AAB38CB797C750C778C608C6DDEB141DF7380F61') # Peter Bieringer (Code Signing Key 2013) <code@bieringer.de>

prepare() {
  cd "${pkgname}-${pkgver}"

  autoconf
}

build() {
  cd "${pkgname}-${pkgver}"

  ./configure \
    --prefix="/usr" \
    --libdir="/usr/lib" \
    --datarootdir="/usr/share" \
    --enable-shared \
    --enable-bundled-md5 \
    --enable-geoip \
    --with-geoip-dynamic=yes
  make
}

check() {
  cd "${pkgname}-${pkgver}"

  make test-minimal
}

package() {
  cd "${pkgname}-${pkgver}"

  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
