# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Bermond <danielbermond@gmail.com>

pkgname=libmfx
pkgver=1.25
pkgrel=3
pkgdesc='Intel Media SDK dispatcher library'
arch=(x86_64)
url=https://github.com/lu-zero/mfx_dispatch
license=(BSD)
depends=(
  libva.so
  libva-drm.so
  libva-x11.so
)
makedepends=(git)
provides=(libmfx.so)
source=(git+https://github.com/lu-zero/mfx_dispatch.git#tag=${pkgver})
sha256sums=(SKIP)

prepare() {
  cd mfx_dispatch

  sed "s/1\.23/${pkgver}/" -i configure.ac
  autoreconf -fiv
}

build() {
  cd mfx_dispatch

  ./configure \
    --prefix=/usr \
    --enable-shared \
    --with-libva_drm=yes \
    --with-libva_x11=yes
  make
}

package() {
  make DESTDIR="${pkgdir}" -C mfx_dispatch install
  install -Dm 644 mfx_dispatch/LICENSE -t "${pkgdir}"/usr/share/licenses/libmfx
}

# vim: ts=2 sw=2 et:
