# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paperwork
pkgver=1.2.4
pkgrel=3
pkgdesc="Personal document manager for GNOME to manage scanned documents and PDFs"
arch=(any)
url="https://openpaper.work/"
license=(GPL3)
depends=(gtk3 libnotify poppler-glib python-cairo python-dateutil python-gobject
         python-levenshtein python-natsort python-pillowfight python-pycountry python-pyenchant
         python-pyinsane python-pyocr python-setuptools python-simplebayes python-termcolor
         python-whoosh python-xdg)
makedepends=(git)
_commit=36c00eaac636c6fa6db58f2f86a01e1c1abf2398  # tags/1.2.4^0
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/paperwork.git#commit=$_commit"
        0001-Filter-out-boxes-that-start-at-0-0.patch
        0001-Filter-out-too-large-boxes-on-selection.patch
        0001-Fix-importing-PNG-files-with-transparency.patch
        0001-util-find_language-New-versions-of-pycountry-do-not-.patch)
sha256sums=('SKIP'
            '8fb8b760fba68c393b33bf2f2b52c6e7edf46d8958863c8f02c4f988842d84e3'
            '1e11b2c18181b9ab36f51f25ff2e489759ae12245de41a4dcf377def400afe99'
            'aa867fb97da9a53c0e2beb758c26881dc2a16265e9b2deaa83e6fe1443ec8260'
            '00f501fb42abe44ec34982fbfd0f71cb4a589e0cc4bb983e0bfa50156fc30d3c')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/781
  patch -Np1 -i ../0001-Filter-out-boxes-that-start-at-0-0.patch

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/782
  patch -Np1 -i ../0001-Filter-out-too-large-boxes-on-selection.patch

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/783
  patch -Np1 -i ../0001-Fix-importing-PNG-files-with-transparency.patch

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/issues/812
  patch -Np1 -i ../0001-util-find_language-New-versions-of-pycountry-do-not-.patch
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname/paperwork-backend
  python3 setup.py install --root="$pkgdir" --optimize=1

  cd ../paperwork-gtk
  python3 setup.py install --root="$pkgdir" --optimize=1

  cd "$pkgdir"/usr/lib/python3.7/site-packages/paperwork/frontend/
  PYTHONPATH=`echo "$pkgdir"/usr/lib/python*/site-packages/` python3 -c 'import shell; shell.install_system(icon_basedir="../../../../../share/icons", data_basedir="../../../../../share")'
}
