# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=paperwork
pkgver=1.2.4
pkgrel=5
pkgdesc="Personal document manager for GNOME to manage scanned documents and PDFs"
arch=(any)
url="https://openpaper.work/"
license=(GPL3)
depends=(gtk3 libnotify poppler-glib python-cairo python-dateutil python-gobject
         python-levenshtein python-natsort python-pillowfight python-pycountry python-pyenchant
         python-pyinsane python-pyocr python-setuptools python-simplebayes python-termcolor
         python-whoosh python-xdg)
makedepends=(git)
_commit=36c00eaac636c6fa6db58f2f86a01e1c1abf2398  # tags/1.2.4^0
source=("git+https://gitlab.gnome.org/World/OpenPaperwork/paperwork.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgname

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/781
  git cherry-pick -n d1da8cd20554aa1d3c766855c251a5a643d2abb4

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/782
  git cherry-pick -n 318d9ef80a7dc21da7ad45fa46c11c8fb19ec8fb

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/783
  git cherry-pick -n 2ffb468a41d28eceda3afe869dd8c5af70203bf4

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/issues/812
  git cherry-pick -n ad4555f9904805e4fd56a30ca6529536edb0f919

  # https://gitlab.gnome.org/World/OpenPaperwork/paperwork/merge_requests/785
  sed -i 's/datadir, "appdata"/datadir, "metainfo"/' paperwork-gtk/src/paperwork/frontend/shell.py
  sed -i 's|https://raw.githubusercontent.com/openpaperwork/paperwork-screenshots/master/1.2.1/|https://gitlab.gnome.org/World/OpenPaperwork/paperwork-screenshots/raw/master/1.2.1/|' paperwork-gtk/data/work.openpaper.Paperwork.appdata.xml
}

build() {
  cd $pkgname
  make
}

package() {
  cd $pkgname/paperwork-backend
  python3 setup.py install --root="$pkgdir" --optimize=1

  cd ../paperwork-gtk
  python3 setup.py install --root="$pkgdir" --optimize=1

  cd "$pkgdir"/usr/lib/python3.7/site-packages/paperwork/frontend/
  PYTHONPATH=`echo "$pkgdir"/usr/lib/python*/site-packages/` python3 -c 'import shell; shell.install_system(icon_basedir="../../../../../share/icons", data_basedir="../../../../../share")'
}
