# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=tlsh
pkgname=('tlsh' 'python-tlsh' 'python2-tlsh')
pkgver=3.4.4
pkgrel=1
pkgdesc='Fuzzy matching library that generates a hash value which can be used for similarity comparisons'
url='https://github.com/trendmicro/tlsh'
arch=('i686' 'x86_64')
license=('Apache')
makedepends=('cmake' 'python' 'python2' 'gcc-libs')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/trendmicro/${pkgname}/archive/v${pkgver}.tar.gz
        fix-python3.patch)
sha512sums=('c4897cf70cfef555638b14e2a77e550fd52dc3c23ca7dd98077d37478d944282b93c0019593c6e13a40df298a439fbdf20d0aa893aa1a78de0ea7b39ef76b572'
            'aeafea636656fc974d0486acd37d210ffab14cd868c0e621170b5abc45ba68702826cea39cec41e2c8fe286b6ac0b85b3499c92e065abda21ce215a4c0a69bc4')

prepare() {
  cd ${pkgbase}-${pkgver}
  patch -p1 < "${srcdir}/fix-python3.patch"
  cp -ra py_ext{,-py2}
  cp Testing/python_test.sh Testing/python2_test.sh
  sed 's|py_ext|py_ext-py2|g' -i Testing/python2_test.sh
}

build() {
  cd ${pkgbase}-${pkgver}
  mkdir -p build
  (cd build
    cmake ..
    make
  )
  (cd py_ext
    python setup.py build
  )
  (cd py_ext-py2
    python2 setup.py build
  )
}

check() {
  cd ${pkgbase}-${pkgver}
  (cd build
    make test
  )
  Testing/python_test.sh
  Testing/python2_test.sh python2
}

package_tlsh() {
  depends=('gcc-libs')

  cd ${pkgbase}-${pkgver}
  install -Dm 755 bin/tlsh_unittest "${pkgdir}/usr/bin/tlsh_unittest"
  install -Dm 644 README.md "${pkgdir}/usr/share/doc/${pkgname}/README.md"
}

package_python-tlsh() {
  depends=('python')

  cd ${pkgbase}-${pkgver}/py_ext
  python setup.py install -O1 --root="${pkgdir}" --skip-build
}

package_python2-tlsh() {
  depends=('python2')

  cd ${pkgbase}-${pkgver}/py_ext-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
}

# vim: ts=2 sw=2 et:
