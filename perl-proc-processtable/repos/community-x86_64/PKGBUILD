# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-proc-processtable
pkgver=0.58
pkgrel=1
pkgdesc="Provides a consistent interface to process table information."
arch=('x86_64')
url="https://search.cpan.org/dist/Proc-ProcessTable/"
license=('PerlArtistic')
depends=('perl')
provides=('perl-proc-killall' 'perl-proc-killfam' 'perl-proc-processtable-process')
options=(!emptydirs)
source=("https://search.cpan.org/CPAN/authors/id/J/JW/JWB/Proc-ProcessTable-$pkgver.tar.gz")
sha512sums=('19df0aa61d54bf75539056d5b8ecbf3e47a6ebc6c6c0bf679a4cceae9e4ee1da8b9fdcbbc82a4fd952dc5875e01f039e4d4560afbe8faf9b7393be32b0bf5c09')

build() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"

  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"
  make test
}

package() {
  cd "$srcdir/Proc-ProcessTable-$pkgver"

  make install DESTDIR="${pkgdir}"
  find "${pkgdir}" -name perllocal.pod -delete
  find "${pkgdir}" -name .packlist -delete
}
