# Contributor: Heeru Kiyura <M8R-p9i5nh@mailinator.com>
# Maintainer: Stefan Husmann <stefan-husmann@t-online.de>

_commit=b0882d615876ec7c1554e50b652bed79209da4d6
pkgname=conkeror-git
pkgver=1.0pre
pkgrel=3
pkgdesc="A highly programmable web browser based on Mozilla XULRunner."
arch=('i686' 'x86_64')
url="http://conkeror.mozdev.org/"
license=('MPL' 'GPL' 'LGPL')
depends=('xulrunner' 'desktop-file-utils')
makedepends=('git')
provides=('conkeror')
source=(conkeror_gimpfile.xpm conkeror.sh)
md5sums=('b592582a5b923db1707615564a95737f'
         '11c6c76a2639254754b34a542f1965a1')
install=conkeror-git.install

__gitroot="git://repo.or.cz/conkeror.git"
__gitname="conkeror"

build() {
  cd "$srcdir"

 ## Git checkout
  if [ -d "$srcdir/${__gitname}" ] ; then
    msg "Git checkout:  Updating existing tree"
    cd ${__gitname} && git checkout ${_commit}
    msg "Git checkout:  Tree has been updated"
  else
    msg "Git checkout:  Retrieving sources"
    git clone ${__gitroot} && cd ${__gitname} && git checkout ${_commit} 
  fi
  msg "Checkout completed"

 ## Build
  [ -d ${__gitname}-build ] || rm -rf "$srcdir/${__gitname}-build"
  git clone "$srcdir/${__gitname}" "$srcdir/${__gitname}-build"
  cd "$srcdir/${__gitname}-build"
  make
}

package() { 
  cd "$srcdir/${__gitname}-build"
  install -d "$pkgdir"/usr/share/{conkeror,man/man1}
  cp -a "$srcdir"/${__gitname}-build/* "$pkgdir/usr/share/conkeror"
  install -Dm644 "$pkgdir/usr/share/conkeror/contrib/man/conkeror.1" \
    "$pkgdir/usr/share/man/man1/conkeror.1"
  rm -r "$pkgdir/usr/share/conkeror/contrib/man"
  install -Dm644 "$srcdir/${__gitname}-build/debian/conkeror.desktop" \
    "$pkgdir/usr/share/applications/conkeror.desktop"
  rm -r "$pkgdir/usr/share/conkeror/debian"
  install -Dm644 "$srcdir/conkeror_gimpfile.xpm" "$pkgdir/usr/share/pixmaps/conkeror_gimpfile.xpm"
  install -Dm755 "$srcdir/conkeror.sh" "$pkgdir/usr/bin/conkeror"
} 
