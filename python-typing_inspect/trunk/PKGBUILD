# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

_pkgname=typing_inspect
pkgname=python-$_pkgname
pkgver=0.5.0
pkgrel=1
pkgdesc='Runtime inspection utilities for Python typing module'
arch=('any')
url='https://github.com/ilevkivskyi/typing_inspect'
license=('MIT')
depends=('python-mypy_extensions' 'python-typing_extensions')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('246a793f0f6d13273d4cb882bf92aabdb9c4bfed02c30d25783e595164023a996124dcc28df00837e9224f18c47180d69b60c702bea38e2d7f17ebe230d24f08')

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  python setup.py pytest
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm 644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
