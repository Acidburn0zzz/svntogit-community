# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Tesx <tesfabpel@gmail.com>

pkgname=goxel
pkgver=0.10.7
pkgrel=2
pkgdesc='3D program that lets you create voxel volumes'
arch=(x86_64)
url='https://goxel.xyz/'
license=(GPL3)
depends=(glfw gtk3)
makedepends=(scons)
source=("$pkgname-$pkgver.tar.gz::https://github.com/guillaumechereau/$pkgname/archive/v$pkgver.tar.gz")
b2sums=('88371b85335d2ef60a13817d3d2c958fdcd35ae46efca0c97d44e97db238e34cee52f8867cad801fb6a2b3fe4e657175410ee1319f9015cbecc22e2026ff206c')

prepare() {
  cd $pkgname-$pkgver
  sed -i 's|scons mode=release|scons mode=release werror=n|
          s|/share/applications/goxel.desktop|/share/applications/io.github.guillaumechereau.Goxel.desktop|
          s|install -Dm744 goxel|install -Dm755 goxel|' Makefile
  sed -i 's|\${SNAP}/icon.png|goxel|' snap/gui/goxel.desktop
}

build() {
  make -C $pkgname-$pkgver
}

package() {
  make -C $pkgname-$pkgver DESTDIR="$pkgdir" PREFIX=/usr install
}
