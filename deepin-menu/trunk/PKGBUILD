# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-menu
pkgver=3.4.4
pkgrel=1
pkgdesc="Deepin menu service for building beautiful menus"
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-menu"
license=('GPL3')
depends=('deepin-qt5integration')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-menu/archive/$pkgver.tar.gz")
sha512sums=('4ff2ba21591a48b3b3d317e0fa1aeb0f12675888890d972dda9bc00e9e58be4781d00f46638b2922fde5f62fd6359a774ac66703b007b303e2f0090610623e9a')

build() {
  cd deepin-menu-$pkgver

  qmake-qt5 DEFINES+=QT_NO_DEBUG_OUTPUT
  make
}

package() {
  cd deepin-menu-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
