# $Id$
# Maintainer: David Runge <dave@sleepmap.de>
pkgname=yass
pkgver=0.0.2
pkgrel=2
pkgdesc="Yet Another Scrolling Score"
arch=('x86_64')
url="https://kokkinizita.linuxaudio.org/linuxaudio/"
license=('GPL2')
groups=('pro-audio')
depends=('clthreads' 'clxclient' 'jack')
makedepends=('gendesk')
source=("https://kokkinizita.linuxaudio.org/linuxaudio/downloads/$pkgname-$pkgver.tar.bz2")
sha512sums=('bd15d0f28c8877879f2cd75a18185234f254b8c7c00e78d6b8de84ed8c98ec014098aa0560d5f1efdaab8ad8638973d1e92230273d836903d553e56fa80e9a57')
validpgpkeys=()

prepare() {
  mv -v ${pkgname} ${pkgname}-${pkgver}
  cd "$pkgname-$pkgver"
  # XDG desktop files
  gendesk -n \
          --pkgname ${pkgname} \
          --pkgdesc "${pkgdesc}" \
          --name YASS \
          --categories "AudioVideo;Audio"
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  # Makefile is too broken to do things properly
  install -vDm 755 ${pkgname} -t "${pkgdir}/usr/bin/"
  # XDG desktop integration
  install -vDm 644 "${pkgname}.desktop" -t "${pkgdir}/usr/share/applications/"
  # docs
  install -vDm 644 {AUTHORS,README} -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
