# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-control-center
pkgver=2.0.5
pkgrel=1
pkgdesc="Utilities to configure the UKUI desktop"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-control-center"
groups=('ukui')
depends=('kconfig' 'kconfigwidgets' 'ki18n' 'kscreen' 'kwidgetsaddons' 'peony' 'ukui-interface'
         'ukui-panel' 'ukui-media' 'ukui-menus' 'ukui-power-manager' 'ukui-screensaver'
         'ukui-settings-daemon' 'upower')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-control-center/archive/v$pkgver.tar.gz")
sha512sums=('d1e5e85b35f5ca3f0c315adda3a8ac98e1426a0d72da0057958d04aae22bafd5d33a7be8c44964c264817f319bd65375b5c23122f402911006cec119c09cc50d')

prepare() {
  cd ukui-control-center-$pkgver
  # QSysInfo::productVersion() returns "unknown" on Arch
  #TODO: sed -i 's/productVersion/prettyProductName/' plugins/messages-task/about/about.cpp
}

build() {
  cd ukui-control-center-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-control-center-$pkgver
  make INSTALL_ROOT="$pkgdir" install

  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.service -t "$pkgdir"/usr/share/dbus-1/system-services/
  install -Dm644 registeredQDbus/conf/com.control.center.qt.systemdbus.conf -t "$pkgdir"/usr/share/dbus-1/system.d/
}
