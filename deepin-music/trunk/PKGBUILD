# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=3.0.12
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-tool-kit' 'ffmpeg' 'taglib' 'libcue' 'qt5-svg' 'gst-plugins-bad')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz")
sha512sums=('71b1dadbbc56d2b00674aae1bbcb2adb80a42ad4d49d7d76a63344ae912526221bb413c6ddd1b4fd0dd010a4d9b6b1b5b6c94699ea181d6cb2baab7a5b1f5e16')

prepare() {
  sed -i 's/-0-2//g' deepin-music-$pkgver/music-player/music-player.pro
}

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
