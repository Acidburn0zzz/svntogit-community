# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=3.1.10
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'taglib' 'libcue' 'qt5-svg' 'gst-plugins-bad')
makedepends=('python' 'qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz")
sha512sums=('da39db34cba7001e3da6cc4d52db5b6b78e497340cfe4a953b46b1d69fb7c7807876c1a498f393d56258e3a5f8082a1906459c233701056822d771baf4dfa289')

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
