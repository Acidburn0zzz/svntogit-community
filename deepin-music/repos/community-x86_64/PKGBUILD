# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=6.0.1.18
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('x86_64')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('deepin-qt5integration' 'ffmpeg' 'kcodecs' 'libcue' 'qt5-svg' 'taglib' 'vlc')
makedepends=('qt5-tools')
groups=('deepin-extra')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/deepin-music/archive/$pkgver.tar.gz"
        "qt-5.15.patch")
sha512sums=('e3f87f0404d86df732fc65b7448bf8b7eaf386a7d1177a3c4caf944da9f41a7ec2966e7055942186378d40f86580c99b63777886873a6208e6a51c7e8ab22e50'
            '8e9d74a78c010431cf5428561ffb59006658281da9f0856c5dfdd4d854e40f4b03d5799f8b0cce81c08c393edf2c225505e76e3ad010c0a3834edeb3c208e853')

prepare() {
  cd deepin-music-$pkgver
  patch -Np1 -i ../qt-5.15.patch
}

build() {
  cd deepin-music-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd deepin-music-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
