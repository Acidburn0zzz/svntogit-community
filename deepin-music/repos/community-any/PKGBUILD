# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-music
pkgver=2.3.2
pkgrel=1
pkgdesc='Awesome music player with brilliant and tweakful UI Deepin-UI based.'
arch=('any')
url="https://github.com/linuxdeepin/deepin-music"
license=('GPL3')
depends=('gstreamer0.10-python' 'gstreamer0.10-bad-plugins' 'gstreamer0.10-good-plugins'
         'gstreamer0.10-ugly-plugins' 'mutagen' 'python2-chardet' 'python2-pyquery' 'python2-dbus'
         'sonata' 'cddb-py' 'python2-pycurl' 'python2-keybinder2' 'python2-deepin-ui')
makedepends=('deepin-gettext-tools' 'git')
groups=('deepin-extra')
provides=('deepin-music-player')
replaces=('deepin-music-player')
conflicts=('deepin-music-player')
source=("git+https://github.com/linuxdeepin/deepin-music.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd deepin-music

  # fix python version
  find -iname "*.py" | xargs sed -i 's=\(^#! */usr/bin.*\)python *$=\1python2='
}

build() {
  cd deepin-music
  deepin-generate-mo tools/locale_config.ini
}

package() {
  cd deepin-music
  make DESTDIR="${pkgdir}" PREFIX="/usr" install
}
