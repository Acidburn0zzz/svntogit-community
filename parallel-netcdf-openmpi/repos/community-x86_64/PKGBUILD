# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

_pkg=parallel-netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=1.8.1
pkgrel=1
pkgdesc="A Parallel I/O Library for NetCDF File Access"
arch=('x86_64') # Tests fail for 'i686'
url="https://trac.mcs.anl.gov/projects/parallel-netcdf"
license=('custom')
depends=('openmpi')
makedepends=('gcc-fortran')
provides=('parallel-netcdf')
conflicts=('parallel-netcdf')
replaces=('parallel-netcdf')
options=('!makeflags')
source=("http://cucis.ece.northwestern.edu/projects/PnetCDF/Release/${_pkg}-${pkgver}.tar.gz")
sha1sums=('2a8179158731f3fa24c9b183b4b1442e8853d229')

build() {
    cd ${_pkg}-${pkgver}
    ./configure --prefix=/usr
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make check
    make ptest
}

package() {
    cd ${_pkg}-${pkgver}
    make prefix="${pkgdir}/usr" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}
}
