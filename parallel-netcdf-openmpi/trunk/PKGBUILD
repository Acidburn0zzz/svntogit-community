# Maintainer: Bruno Pagani (a.k.a. ArchangeGabriel) <archange@archlinux.org>

_pkg=parallel-netcdf
_mpi=openmpi
pkgname=${_pkg}-${_mpi}
pkgver=1.9.0
pkgrel=2
pkgdesc="A Parallel I/O Library for NetCDF File Access"
arch=('x86_64')
url="https://trac.mcs.anl.gov/projects/parallel-netcdf"
license=('custom')
depends=('openmpi')
makedepends=('gcc-fortran')
checkdepends=('inetutils')
provides=('parallel-netcdf')
conflicts=('parallel-netcdf')
replaces=('parallel-netcdf')
options=('!makeflags' 'staticlibs')
source=("http://cucis.ece.northwestern.edu/projects/PnetCDF/Release/${_pkg}-${pkgver}.tar.gz"
        'shared.patch')
sha1sums=('0978ae5cdaba925e73ced2b550fc02a132b2c5ed'
          'dae94c293b5be9fa31e73be95e4d56c1f598ffbf')

prepare() {
    cd ${_pkg}-${pkgver}
    patch -p0 -i ../shared.patch
}

build() {
    cd ${_pkg}-${pkgver}

    export CPPFLAGS="${CPPFLAGS} -fPIC"
    aclocal
    ./configure --prefix=/usr --enable-shared
    make
}

check() {
    cd ${_pkg}-${pkgver}
    make check
    make ptest
}

package() {
    cd ${_pkg}-${pkgver}
    make DESTDIR="${pkgdir}" install

    install -Dm644 COPYRIGHT -t "${pkgdir}"/usr/share/licenses/${_pkg}
}
