# Maintainer: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perl-package-stash
pkgver=0.34
pkgrel=1
pkgdesc="Routines for manipulating stashes"
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.8.1' 'perl-dist-checkconflicts>=0.02'
         'perl-package-deprecationmanager' 'perl-scalar-list-utils'
         'perl-package-stash-xs>=0.24')
makedepends=('perl-extutils-makemaker>=6.30')
checkdepends=('perl-test-fatal' 'perl-test-requires' 'perl-test-simple' 'perl-module-implementation')
url="http://search.cpan.org/dist/Package-Stash"
source=("http://search.cpan.org/CPAN/authors/id/D/DO/DOY/Package-Stash-${pkgver}.tar.gz")
md5sums=('d85874dc8abada51b8d7870d8728e3b7')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"     \
      PERL_MB_OPT="--installdirs vendor" \
      MODULEBUILDRC=/dev/null

    cd Package-Stash-${pkgver}
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd Package-Stash-${pkgver}
    make test
  )
}

package() {
    cd Package-Stash-${pkgver}
    make DESTDIR="$pkgdir" install
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
