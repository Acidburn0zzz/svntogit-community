# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: JJDaNiMoTh <jjdanimoth.aur@gmail.com>
# Contributor: Jesus Lazaro Plaza <jesuslazaro84@gmail.com>

pkgname=mandvd
pkgver=2.6
pkgrel=1
pkgdesc="A program to simply create DVD Video"
arch=('i686' 'x86_64')
url="http://www.kde-apps.org/content/show.php/ManDVD?content=83906"
license=('GPL')
depends=('dvd+rw-tools>=5.21.4' 'dvd-slideshow>=0.7.5' 'mjpegtools>=1.8.0' 'mplayer' 'qt3' 'xine-ui>=0.99.4')
changelog=$pkgname.changelog
source=(http://www.grommit.se/$pkgname/${pkgname}-${pkgver}-1.fc12.tar.gz)
md5sums=('7b10baa97b9c74a054f432b45940f3aa')

build() {
  cd ${srcdir}/$pkgname-$pkgver

  source /etc/profile.d/qt3.sh
  ${QTDIR}/bin/qmake
  sed -i "s|O2|O1|" ./Makefile
  make
}

package() {
  cd ${srcdir}/$pkgname-$pkgver

  install -D -m755 $pkgname ${pkgdir}/usr/bin/$pkgname

# Creating menu item
  install -D -m644 mandvdico.png ${pkgdir}/usr/share/pixmaps/$pkgname.png
  install -D -m644 $pkgname.desktop ${pkgdir}/usr/share/applications/$pkgname.desktop
}
