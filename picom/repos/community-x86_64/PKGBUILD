# Maintainer: Alexander F. Rødseth <xyproto@archlinux.org>
# Contributor: WorMzy Tykashi <wormzy.tykashi@gmail.com>
# Contributor: OK100 <ok100@lavabit.com>
# Contributor: Valère Monseur <valere.monseur@ymail.com>

pkgname=picom
pkgver=7.4
pkgrel=2
pkgdesc='X compositor that may fix tearing issues'
arch=(x86_64)
url='https://github.com/yshui/picom'
license=(MIT)
replaces=(compton)
provides=(compton)
conflicts=(compton)
makedepends=(asciidoc git mesa meson ninja setconf uthash)
depends=(hicolor-icon-theme libconfig libdbus libev libgl libxdg-basedir pcre
         pixman xcb-util-image xcb-util-renderutil)
backup=(etc/xdg/picom.conf)
optdepends=('dbus:          For controlling picom via D-Bus'
            'xorg-xwininfo: For picom-trans'
            'xorg-xprop:    For picom-trans'
            'python:        For running picom-convgen.py')
# > git tag v7.4, this is after the rename
source=("git+$url#commit=9a88d971d59ed8c33f9d8734cd0039d9fd6030a5"
        picom.conf)
sha256sums=('SKIP'
            '78b697fcc5f1c377b682cf2196683766b601a12ef61321291f1c7b875679c481')

prepare() {
  cd $pkgname
  setconf $pkgname.desktop Exec picom
  setconf $pkgname.desktop Comment 'Compositor for X11'
  setconf $pkgname.desktop TryExec /usr/bin/picom
}

build() {
  mkdir -p build
  arch-meson build $pkgname -D build_docs=true -D b_ndebug=true
  ninja -v -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 picom.conf "$pkgdir/etc/xdg/picom.conf"
  cd $pkgname
  install -Dm644 picom.sample.conf "$pkgdir/etc/xdg/picom.conf.example"
  install -Dm644 LICENSES/MIT "$pkgdir/usr/share/licenses/$pkgname/MIT"
  install -Dm644 LICENSES/MPL-2.0 "$pkgdir/usr/share/licenses/$pkgname/MPL-2.0"
}

# getver: github.com/yshui/picom/releases
# vim: ts=2 sw=2 et
