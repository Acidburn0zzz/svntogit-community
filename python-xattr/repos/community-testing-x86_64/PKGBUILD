# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-xattr
pkgver=0.9.8
pkgrel=3
pkgdesc='Python wrapper for extended filesystem attributes'
arch=('x86_64')
license=('MIT')
url='https://github.com/xattr/xattr'
depends=('python-cffi')
makedepends=('python-setuptools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/xattr/xattr/archive/v$pkgver.tar.gz")
sha512sums=('e983c22023a9ea0e7d53b890a6f2a09ace12e559a4852b33767bfed8554f1e077c259b1b9003ba15772d2b7d433dcb19c95a816c5de9a505fa656607135f08bc')

build() {
  cd xattr-$pkgver
  python setup.py build
}

check() { (
  # Build env doesn't have permission for those xattrs
  cd xattr-$pkgver
  python setup.py test
  ) || echo "Tests failed"
}

package() {
  cd xattr-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 et:
