# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
pkgname=csoundqt
pkgver=0.9.8
pkgrel=1
pkgdesc="A frontend for Csound"
arch=('x86_64')
url="https://csoundqt.github.io/"
license=('CCPL' 'FDL' 'GPL' 'LGPL2.1')
groups=('pro-audio')
depends=('csound-doc' 'gcc-libs' 'glibc' 'hicolor-icon-theme'  'qt5-base'
'qt5-declarative' 'qt5-webengine')
makedepends=('csound' 'rtmidi')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/v${pkgver}.tar.gz")
sha512sums=('253ed416d8bd6d6fe0b7b70c5b6e1cc3d80e9963e018e949c0085e6a85cc120c53472190b7aaaec4509132586c0254b512314277f27ef09fb7296599dd33b87c')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake-qt5 qcs.pro \
    SHARE_DIR="${pkgdir}/usr/share" \
    INSTALL_DIR="${pkgdir}/usr" \
    CONFIG+=rtmidi \
    CONFIG+=html_webengine \
    CONFIG+=record_support
  make
}

package() {
  depends+=('libcsnd6.so' 'libcsound64.so' 'librtmidi.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
