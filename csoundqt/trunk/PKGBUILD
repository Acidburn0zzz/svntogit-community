# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ainola
# Contributor: rtfreedman

_name=CsoundQt
pkgname=csoundqt
pkgver=0.9.7
pkgrel=1
pkgdesc="A frontend for Csound"
arch=('x86_64')
url="https://csoundqt.github.io/"
license=('CCPL' 'FDL' 'GPL' 'LGPL2.1')
groups=('pro-audio')
depends=('csound-doc' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'libcsnd6.so'
'libcsound64.so' 'librtmidi.so' 'qt5-base' 'qt5-declarative')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${pkgname}/${pkgname}/archive/${pkgver}.tar.gz")
sha512sums=('ebfb1a7b18c35794b5b4ae9fb73d53f2e87127b4c9f55824a37e5fe7133c7e68cc4b1446fb5a21bd9d213d503df06930320f345a31dd9ad175cf32215c6fd757')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
}

build() {
  cd "${pkgname}-${pkgver}"
  qmake-qt5 qcs.pro \
    SHARE_DIR="${pkgdir}/usr/share" \
    INSTALL_DIR="${pkgdir}/usr" \
    CONFIG+=rtmidi
  make
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
