# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-ppix-quotelike
pkgver=0.016
pkgrel=1
pkgdesc="Parse Perl string literals and string-literal-like things"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/PPIx-QuoteLike"
depends=('perl-ppi')
makedepends=('perl-ppix-regexp' 'perl-readonly')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/W/WY/WYANT/PPIx-QuoteLike-$pkgver.tar.gz")
sha512sums=('68933307f77cb0926f61249f1d80397443d02ccbf900051cdb7d091b7fe7923fe3705ed39975428e251cad58eeef2b3614c34863618541dea37e5aa98a73e1ba')

build() {
  cd PPIx-QuoteLike-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd PPIx-QuoteLike-$pkgver
  make test
}

package() {
  cd PPIx-QuoteLike-$pkgver
  make DESTDIR="$pkgdir" install
}
