# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-flasgger
pkgname=(python-flasgger python2-flasgger)
pkgver=0.9.2
pkgrel=2
pkgdesc="Extract swagger specs from your flask project"
url="https://github.com/rochacbruno/flasgger"
license=('PerlArtistic')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-flask' 'python2-flask' 'python-yaml'
             'python2-yaml' 'python-jsonschema' 'python2-jsonschema' 'python-mistune'
             'python2-mistune' 'python-six' 'python2-six')
checkdepends=('python-pytest-runner' 'python-flex' 'python-marshmallow' 'python-decorator'
              'python-apispec-webframeworks' 'python-flask-restful' 'python-flask-jwt')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/rochacbruno/flasgger/archive/$pkgver.tar.gz"
        https://github.com/rochacbruno/flasgger/pull/263.patch
        https://github.com/rochacbruno/flasgger/pull/298.patch)
sha512sums=('9f36bb008b7cfbc3bd54eb679fd0eba98390aa2fea27099a12f2dff20050acaf2479fd29a85fc0ba25ce8834aa1c3fbab4610e0e1c12b9ff96d5c48ab97370f9'
            'e516f285f911b820dc056b4b947837d0be0f17d4ca30ba89b0babb818107378a9780bdb71544e88709a8681ec388daaac871ab9ef457b930894c764c5e2571b5'
            'cec01edde05ebe036aa514c48fcaaa253304623cf14d0b2c2e0ea05db4916730ccd3692ccaf33916127f67619ed3c57e440ff35fd43df29e14a2398dec60edca')

prepare() {
  patch -d flasgger-$pkgver -p1 -i ../263.patch
  patch -d flasgger-$pkgver -p1 -i ../298.patch
  cp -a flasgger-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/flasgger-$pkgver
  python setup.py build

  cd "$srcdir"/flasgger-$pkgver-py2
  python2 setup.py build
}

check() {
  cd flasgger-$pkgver
  PYTHONPATH="$PWD/etc/flasgger_package" python setup.py pytest || warning "https://github.com/rochacbruno/flasgger/issues/285"
}

package_python-flasgger() {
  depends=('python-flask' 'python-yaml' 'python-jsonschema' 'python-mistune' 'python-six')

  cd flasgger-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-flasgger() {
  depends=('python2-flask' 'python2-yaml' 'python2-jsonschema' 'python2-mistune' 'python2-six')

  cd flasgger-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
