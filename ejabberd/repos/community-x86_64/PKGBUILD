# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

#
# cd deps ; ls -1 | while read g; do (test -d $g && cd $g && sh ../t.sh); done >qwe
#
# U=`grep 'url =' .git/config | cut -f2 -d= | tr -d ' '`
# C=`git rev-parse HEAD`
# N=`basename $(pwd)`
# echo -e "\t\"$N::$U#commit=$C\""
#

pkgname=ejabberd
pkgver=16.01
pkgrel=3
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"cache_tab::git://github.com/processone/cache_tab#commit=26caea06c72c2117ca54d04beedb5b49a45af1a8"
	"elixir::git://github.com/elixir-lang/elixir#commit=f2a9c7016633ca63541a8160c63a53eb7edbccdb"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/p1_sip#commit=ff3cc9fa2f9ea96f47e6b5b60a04da90889a5626"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=64864ba7fcf40988361340e48680b49a2c2938cf"
	"hamcrest::git://github.com/hyperthunk/hamcrest-erlang#commit=908a24fda4a46776a5135db60ca071e3d783f9f6"
	"jiffy::git://github.com/davisp/jiffy#commit=e43ea64ae0d227af5dd003132234cdf4494d781d"
	"lager::git://github.com/basho/lager#commit=599dda6786a81200e00b3d9b4b36951e052727a3"
	"meck::git://github.com/eproxus/meck#commit=dde759050eff19a1a80fd854d7375174b191665d"
	"oauth2::git://github.com/kivra/oauth2#commit=8d129fbf8866930b4ffa6dd84e65bd2b32b9acb8"
	"p1_iconv::git://github.com/processone/eiconv#commit=9751f86baa5a60ed1420490793e7514a0757462a"
	"p1_mysql::git://github.com/processone/mysql#commit=064948ad3c77e582d85cbc09ccd11016ae97de0e"
	"p1_pam::git://github.com/processone/epam#commit=f0d6588f4733c4d8068af44cf51c966af8bf514a"
	"p1_pgsql::git://github.com/processone/pgsql#commit=248b6903cad82c748dc7f5be75e014dd8d47a3d1"
	"p1_stringprep::git://github.com/processone/stringprep#commit=7315a75360547cd6458b144a65ede64b44c6193d"
	"p1_stun::git://github.com/processone/stun#commit=ab418dfb11bd9b37d6f7501420f32384729fecd7"
	"p1_tls::git://github.com/processone/tls#commit=f19e1f701e0a3980ffc70b3917c4aa85e68d8520"
	"p1_utils::git://github.com/processone/p1_utils#commit=e6779f1ee7a0841da08a063930d7ea51c2be4203"
	"p1_xml::git://github.com/processone/xml#commit=1190214326c70101db26809b61c2d30133c940b1"
	"p1_yaml::git://github.com/processone/p1_yaml#commit=753f99c39200705bb7ccb6f38f7f10aeb45c0ea4"
	"p1_zlib::git://github.com/processone/zlib#commit=e1f928e61553cf85638eaac7d024c8f68ce0ff36"
	"protobuffs::git://github.com/basho/erlang_protobuffs.git#commit=a1eeee77aef639a33cc5a2dd7abed7e4f4b83f9b"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"riakc::git://github.com/basho/riak-erlang-client#commit=527722d12d0433b837cdb92a60900c2cb5df8942"
	"riak_pb::git://github.com/basho/riak_pb#commit=eacf4404625de69f7b3908cebd8efe416d9598b0"
	"sqlite3::git://github.com/alexeyr/erlang-sqlite3#commit=cbc3505f7a131254265d3ef56191b2581b8cc172"
	"xmlrpc::git://github.com/rds13/xmlrpc#commit=9cd92b219ad97869d9da19ee4ea25ba1a40aea98"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf")
md5sums=('ef7bd609e06aea11a7abb6a178016b7e'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'f97c8a96160f30e0aecc9526c12e6606'
         '527d7e8616332320007f2c1616bc8270'
         'a3ebb00982d290284c1176c176a59b48')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in elixir eredis esip goldrush jiffy lager meck oauth2 cache_tab \
	p1_iconv p1_mysql p1_pam p1_pgsql p1_stringprep p1_stun p1_tls p1_utils \
	p1_xml p1_yaml p1_zlib protobuffs rebar_elixir_plugin riakc riak_pb \
	sqlite3 xmlrpc hamcrest; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv $srcdir/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/p1_pam/priv/bin/epam $pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 $srcdir/ejabberd.service $pkgdir/usr/lib/systemd/system/ejabberd.service
  install -Dm644 $srcdir/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf

  # workaround
  ln -s mod_configure.beam $pkgdir/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f $pkgdir/usr/bin/{elixir,iex,mix}

  # permissions
  chown -R 17:17 $pkgdir/var/{lib,log}/ejabberd
  chown root:17 \
    $pkgdir/etc/ejabberd/ejabberd.yml \
    $pkgdir/etc/ejabberd/ejabberdctl.cfg \
    $pkgdir/etc/ejabberd \
    $pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/epam
}

# vim:set ts=2 sw=2 et:
