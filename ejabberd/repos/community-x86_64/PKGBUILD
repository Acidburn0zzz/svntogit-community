# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff 'codemac' Mickey <jeff@archlinux.org>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

#
# cd deps ; ls -1 | while read g; do (test -d $g && cd $g && sh ../t.sh); done >qwe
#
# U=`grep 'url =' .git/config | cut -f2 -d= | tr -d ' '`
# C=`git rev-parse HEAD`
# N=`basename $(pwd)`
# echo -e "\t\"$N::$U#commit=$C\""
#

pkgname=ejabberd
pkgver=16.12
pkgrel=1
pkgdesc="Jabber server written in Erlang"
arch=('x86_64' 'i686')
url="http://www.ejabberd.im/"
license=("GPL")
depends=('expat' 'openssl' 'zlib' 'erlang-nox' 'pam' 'iproute2' 'erlang-unixodbc'
	 'libyaml' 'sqlite')
makedepends=('git' 'rebar' 'hevea' 'texlive-bin' 'texlive-core' 'texlive-latexextra')
backup=('etc/ejabberd/ejabberd.yml'
	'etc/ejabberd/ejabberdctl.cfg'
	'etc/logrotate.d/ejabberd')
install=$pkgname.install
options=(emptydirs)
source=("$pkgname-$pkgver.tar.gz::https://github.com/processone/ejabberd/archive/$pkgver.tar.gz"
	"cache_tab::git://github.com/processone/cache_tab#commit=898f74ed9f5ce9f2897fddd60e8e0d222bf6a144"
	"elixir::git://github.com/elixir-lang/elixir#commit=3dbfb92860fb24f83c92e1f1b67f48554fb211d6"
	"eredis::git://github.com/wooga/eredis#commit=cbc013f516e464706493c01662e5e9dd82d1db01"
	"esip::git://github.com/processone/esip#commit=3f9d75d69baf74c606279a3d989040f08e882f2f"
	"ezlib::git://github.com/processone/ezlib#commit=85617df345589c0b6eca5d4100eb04ac4bffe3d5"
	"fast_tls::git://github.com/processone/fast_tls#commit=cc294715537f84098e85d8c3bdc79aabc3497f15"
	"fast_xml::git://github.com/processone/fast_xml#commit=79515c8ea84496502c625307f90c74dfb58b3361"
	"fast_yaml::git://github.com/processone/fast_yaml#commit=c042649a7d356535f4b7e15f752b2a34da3ed001"
	"goldrush::git://github.com/DeadZen/goldrush.git#commit=212299233c7e7eb63a97be2777e1c05ebaa58dbe"
	"hamcrest::git://github.com/basho/hamcrest-erlang.git#commit=98bc7aa19ea081478c816824aa05fc5a48acae66"
	"iconv::git://github.com/processone/iconv#commit=b439b4befff48ae655730fd1f9c4d4138b29430d"
	"jiffy::git://github.com/davisp/jiffy#commit=1febce3ca86c5ca5d5a3618ed3d5f125bb99e4c5"
	"lager::git://github.com/basho/lager#commit=8187757388c9adc915379caaab36a2f2ca26e944"
	"luerl::git://github.com/rvirding/luerl#commit=f7b2cc0ab6fa4245ebeda0169fc994aff0628bf9"
	"meck::git://github.com/eproxus/meck#commit=70d6a33ce7407029dc59e22a5b3c1c61c1348b23"
	"moka::git://github.com/processone/moka.git#commit=3eed3a6dd7dedb70a6cd18f86c7561a18626eb3b"
	"p1_mysql::git://github.com/processone/p1_mysql#commit=45ac9ffc243c0822c6f58ca2da04ef55351a0e42"
	"p1_oauth2::git://github.com/processone/p1_oauth2#commit=34f9b20fd68134a4646130bdcf1abf320f815a00"
	"p1_pam::git://github.com/processone/epam#commit=f0d6588f4733c4d8068af44cf51c966af8bf514a"
	"p1_pgsql::git://github.com/processone/p1_pgsql#commit=6c47b99b9988ac5252f730bf8f268ed8c80f70b0"
	"p1_utils::git://github.com/processone/p1_utils#commit=fe9064cc2f3acdd1b93e6015321687208b66fc62"
	"protobuffs::git://github.com/basho/erlang_protobuffs.git#commit=0d22220a8e56adab129836d6ff582a9bca103a0c"
	"rebar_elixir_plugin::git://github.com/processone/rebar_elixir_plugin#commit=10614dfef5d10b7071f7181858149259e50159f6"
	"riakc::git://github.com/basho/riak-erlang-client#commit=261df630afe4a998e099a8ab209a2dc7419f85de"
	"riak_pb::git://github.com/basho/riak_pb#commit=9ca2395f9d6464b82bab466bbdca410cb85a0ac7"
	"samerlib::git://github.com/processone/samerlib#commit=fbbba035b1548ac4e681df00d61bf609645333a0"
	"sqlite3::git://github.com/processone/erlang-sqlite3#commit=6ee3570060fbbf793a0bc873831e8eabb6c39851"
	"stringprep::git://github.com/processone/stringprep#commit=7f082d7f53710837151058734009958efc07db65"
	"stun::git://github.com/processone/stun#commit=422c1475d7dbc5c07b68720286d1cfab0f398c56"
	"xmpp::git://github.com/processone/xmpp#commit=c010141487632db1666bcbf64f90f42c0fde50aa"
	"$pkgname.logrotate"
	"ejabberd.service"
	"sysuser.conf")
sha256sums=('a7eeb9fe49ef141daab1be01838a7612dff9194a28c3dfc922cc691bb8c9b532'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '31780cac78736d285e46f445f8c8463a70f2aeb2683280c259129db11832ddd2'
            '94582b1df76f6d86abfcc78bf4c4ac79276761c75483dd40a45b7ff2b19dc8a8'
            '0599a3d20153d620e9ac4fdb5bd7370854136235382a2fed4110b1950cf07d20')

prepare() {
  cd "$srcdir/$pkgname-$pkgver"

  mkdir -p "$srcdir/$pkgname-$pkgver/deps"
  for i in cache_tab elixir eredis esip ezlib fast_tls fast_xml fast_yaml \
    goldrush hamcrest iconv jiffy lager luerl meck moka p1_mysql p1_oauth2 \
    p1_pam p1_pgsql p1_utils protobuffs rebar_elixir_plugin riakc riak_pb \
    samerlib sqlite3 stringprep stun xmpp; do
    rm -rf "$srcdir/$pkgname-$pkgver/deps/$i"
    mv "$srcdir"/$i "$srcdir/$pkgname-$pkgver/deps"
  done

  sed -i "s|git describe --tags 2>/dev/null|echo $pkgver|" configure.ac
  sed -i 's#R15|R16|17|18#R15|R16|17|18|19#' deps/riakc/rebar.config
}

build() {
  cd "$srcdir/$pkgname-$pkgver"
  [ -x configure ] || ./autogen.sh
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --localstatedir=/var \
    --enable-all
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install

  install -Dm04750 deps/p1_pam/priv/bin/epam "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
  install -d "$pkgdir/var/lib/$pkgname"
  install -D -m0644 "$srcdir/$pkgname.logrotate" "$pkgdir/etc/logrotate.d/$pkgname"
  chmod ug+r "$pkgdir/etc/$pkgname/"*
  chmod a+rx "$pkgdir/usr/bin/ejabberdctl" "$pkgdir/usr/lib/ejabberd-$pkgver/priv/bin/captcha.sh"
  rm -rf "$pkgdir/var/lock"
  install -Dm0644 "$srcdir"/ejabberd.service "$pkgdir"/usr/lib/systemd/system/ejabberd.service
  install -Dm644 "$srcdir"/sysuser.conf "$pkgdir"/usr/lib/sysusers.d/ejabberd.conf
  install -Dm644 tools/ejabberdctl.bc "$pkgdir"/usr/share/bash-completion/completions/ejabberdctl

  # workaround
  ln -s mod_configure.beam "$pkgdir"/usr/lib/ejabberd-$pkgver/ebin/configure.beam
  rm -f "$pkgdir"/usr/bin/{elixir,iex,mix}

  # permissions
  chown -R 17:17 "$pkgdir"/var/{lib,log}/ejabberd
  chown root:17 \
    "$pkgdir"/etc/ejabberd/ejabberd.yml \
    "$pkgdir"/etc/ejabberd/ejabberdctl.cfg \
    "$pkgdir"/etc/ejabberd \
    "$pkgdir"/usr/lib/ejabberd-$pkgver/priv/bin/epam
}

# vim:set ts=2 sw=2 et:
