# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sebastian Faltoni <sebastian.faltoni@gmail.com>

pkgname=dbmail
pkgver=3.0.2
pkgrel=10
pkgdesc="Fast and scalable sql based mail services"
arch=('i686' 'x86_64')
depends=('gmime24' 'libzdb' 'mhash' 'libevent')
makedepends=('asciidoc' 'xmlto' 'docbook-xsl' 'docbook-xml' 'postgresql-libs>=8.4.1'
	     'sqlite' 'libmysqlclient' 'libldap>=2.4.18' 'libsieve')
optdepends=('libldap: for LDAP authentication'
	    'libsieve: for dbmail-sieve')
url="http://www.dbmail.org"
license=('GPL')
options=('!libtool' 'zipman')
backup=(etc/xinetd.d/dbmail-imapd
	etc/xinetd.d/dbmail-pop3d
	etc/xinetd.d/dbmail-lmtpd
	etc/xinetd.d/dbmail-timsieved)
source=(http://www.dbmail.org/download/3.0/dbmail-${pkgver/_/-}.tar.gz
	dbmail.tmpfiles
	dbmail-imapd.service
	dbmail-lmtpd.service
	dbmail-pop3d.service
	dbmail-timsieved.service
	dbmail-imapd.xinetd
	dbmail-lmtpd.xinetd
	dbmail-pop3d.xinetd
	dbmail-timsieved.xinetd)
md5sums=('eb32235abffdf967253ee9d004e0e4a9'
         'c4b5793c5422b62a675d4c66ff7e9300'
         '84efa46eaac66057c4eb131d9bc27fa8'
         '19560277f6a56d1f3f2fdb02315dcf0f'
         '89a0f793737eaf36291409f8c840891e'
         'dd1b5b2c542f55d9d934a58a36d0513d'
         'aa5cf851d1bfeffac69a2c8a9ad15c92'
         '069cd4285c4b2ec95dfdcebc2cfee387'
         '1ac466541ad5bc0064250ad37ba217a4'
         '63dc2dcc13efda3cb2ecc9366a368005')

build() {
  cd $srcdir/dbmail-${pkgver/_/-}/

  [ -f Makefile ] || ./configure --prefix=/usr --with-ldap --with-sieve
  make
}

package() {
  cd $srcdir/dbmail-${pkgver/_/-}/
  make DESTDIR=$pkgdir install
  (cd man && make && make install DESTDIR=$pkgdir)

  mkdir $pkgdir/etc
  install -Dm644 dbmail.conf $pkgdir/etc/dbmail.conf.sample
  mkdir $pkgdir/usr/share/dbmail
  cp -r sql/* $pkgdir/usr/share/dbmail/
  cp dbmail.schema $pkgdir/usr/share/dbmail/

  for i in dbmail-imapd dbmail-lmtpd dbmail-pop3d dbmail-timsieved; do
    install -Dm0644 $srcdir/$i.service $pkgdir/usr/lib/systemd/system/$i.service
    install -Dm0644 $srcdir/$i.xinetd $pkgdir/etc/xinetd.d/$i
  done

  install -Dm0644 $srcdir/dbmail.tmpfiles $pkgdir/usr/lib/tmpfiles.d/dbmail.conf
}
