# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: arkhan <arkhan@disroot.org>

pkgname=ueberzug
pkgver=18.1.7
pkgrel=1
pkgdesc='Command line util which allows to display images in combination with X11'
arch=('x86_64')
url=https://github.com/seebye/ueberzug
license=('GPL3')
depends=('libxext' 'python-attrs' 'python-docopt' 'python-pillow'
         'python-psutil' 'python-setuptools' 'python-xlib')
provides=("python-$pkgname")
replaces=("python-$pkgname")
source=("https://files.pythonhosted.org/packages/source/${pkgname::1}/$pkgname/$pkgname-$pkgver.tar.gz")
sha256sums=('ef0d6ac5815446ede654a38da550d2c44abd0fc05c901b2232935a65bcbca875')
b2sums=('24ecab9503d0eaf04ad7626fb28bfdc2503bc42d72fde687d120e1a64c55a2e798958f14c9b19dd08f0c24dde9acc4aa9b232d40ca4e1d3c750264c6c38aedc1')

build() {
  cd $pkgname-$pkgver
  python setup.py build
}

package() {
  cd $pkgname-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
