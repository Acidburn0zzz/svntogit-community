# $Id$
# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>

pkgname=perlbrew
pkgver=0.63
pkgrel=1
pkgdesc='Manage perl installations in your $HOME'
arch=('any')
url="https://metacpan.org/release/App-perlbrew"
license=('MIT')
depends=('perl' 'perl-capture-tiny' 'perl-cpan-perl-releases' 'perl-devel-patchperl'
         'perl-file-path-tiny' 'perl-local-lib')
makedepends=('perl-extutils-makemaker')
checkdepends=('perl-test-exception' 'perl-test-output' 'perl-test-spec' 
              'perl-path-class' 'perl-io-all')
provides=("perl-app-perlbrew=$pkgver")
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/G/GU/GUGOD/App-perlbrew-$pkgver.tar.gz")
sha1sums=('5bb15b0cc4fdc617d5c9db52094c74c174d858f5')

build() {
  cd App-perlbrew-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor 
  make
}

check() {
  cd App-perlbrew-$pkgver
  make test
}

package() {
  cd App-perlbrew-$pkgver
  make install DESTDIR="$pkgdir"
  install -D -m 644 LICENSE "$pkgdir"/usr/share/licenses/perlbrew/LICENSE
}
