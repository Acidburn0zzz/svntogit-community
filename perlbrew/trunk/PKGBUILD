# Packager: Justin Davis <jrcd83@gmail.com>
# $Id$

pkgname=perlbrew
pkgver=0.40
pkgrel=1
pkgdesc="Manage perl installations in your \$HOME"
arch=(any)
license=(custom:MIT)
options=(!emptydirs)
depends=('perl>=5.8.0' 'perl-capture-tiny>=0.13'
         'perl-cpan-perl-releases>=0.40' 'perl-devel-patchperl>=0.62'
         'perl-file-path-tiny>=0.1' 'perl-local-lib>=1.008')
checkdepends=('perl-test-exception' 'perl-test-output' 'perl-test-spec'
              'perl-io-all' 'perl-path-class')
provides=("perl-app-perlbrew=${pkgver}")
url=https://metacpan.org/release/App-perlbrew
source=("http://search.cpan.org/CPAN/authors/id/G/GU/GUGOD/App-perlbrew-${pkgver}.tar.gz")
md5sums=(7a3002c7715666298e2447dbb92f8903)
sha512sums=(c66a9e800e27815a74d2c9505629ce2f4c39a983026d20e8f4e975c6897bf49ccef0fd9b39fafe9f98ef761fcc93f2fd502f6c9045e3216dff4867a2167d686f)
_distdir="${srcdir}/App-perlbrew-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
    mkdir -p "$pkgdir/usr/share/licenses/perlbrew"
    cp LICENSE "$pkgdir/usr/share/licenses/perlbrew/LICENSE"
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
