# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-apispec
pkgname=(python-apispec python2-apispec)
pkgver=2.0.2
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-bottle' 'python-marshmallow'
              'python-flask' 'python-prance' 'python-yaml' 'python-mock'
              'python-openapi-spec-validator')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('c2b13ea631109fba224544932a2dd6bbc0552903686d176e693149095c8326e113569c7cfdab661a3a36f7eaf72b9f453714774ffb02a55155bccedfbef23a66')

prepare() {
  cp -a apispec-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/apispec-$pkgver
  python setup.py build

  cd "$srcdir"/apispec-$pkgver-py2
  python2 setup.py build
}

check() {
  cd apispec-$pkgver
  python setup.py pytest
}

package_python-apispec() {
  depends=('python')
  optdepends=('python-yaml: for yaml support'
              'python-prance: for validation'
              'python-openapi-spec-validator: for validation')

  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apispec() {
  depends=('python2')
  optdepends=('python2-yaml: for yaml support')

  cd apispec-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
