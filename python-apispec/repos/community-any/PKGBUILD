# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-apispec
pkgname=(python-apispec python2-apispec)
pkgver=0.37.0
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools' 'python-yaml' 'python2-yaml')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-tornado' 'python2-tornado'
              'python-bottle' 'python2-bottle' 'python-marshmallow' 'python2-marshmallow'
              'python-flask' 'python2-flask' 'python-mock' 'python2-mock' 'check_api')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('438e49e537c757368eec1e0c7776e402f6b176a6f2653804c0b349318fb85a4fad8029f927455c1d9380be166b91331ab384ace041617c6773c9e36f46c14f4d')

prepare() {
  sed -i 's/<2/<3/' apispec-$pkgver/setup.py
  cp -a apispec-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/apispec-$pkgver
  python setup.py build

  cd "$srcdir"/apispec-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/apispec-$pkgver
  python setup.py pytest

  cd "$srcdir"/apispec-$pkgver-py2
  python2 setup.py pytest
}

package_python-apispec() {
  depends=('python-yaml')

  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apispec() {
  depends=('python2-yaml')

  cd apispec-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
