# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apispec
pkgver=4.2.0
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
depends=('python')
optdepends=('python-yaml: for yaml support'
            'python-prance: for validation'
            'python-openapi-spec-validator: for validation')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-bottle' 'python-marshmallow'
              'python-flask' 'python-prance' 'python-yaml' 'python-mock'
              'python-openapi-spec-validator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('51187a80f8ad260bd0a89d4a882d2517f56237a6cfb0596cf29999e3fc42af0700c88037ba205a4e7f51587abf8b1db5dbae7ee5d98ae55fc9fe2314f69fa656')

build() {
  cd apispec-$pkgver
  python setup.py build
}

check() {
  cd apispec-$pkgver
  python setup.py pytest
}

package() {
  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
