# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-apispec
pkgver=4.4.1
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
depends=('python')
optdepends=('python-yaml: for yaml support'
            'python-prance: for validation'
            'python-openapi-spec-validator: for validation')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-tornado' 'python-bottle' 'python-marshmallow'
              'python-flask' 'python-prance' 'python-yaml' 'python-mock'
              'python-openapi-spec-validator')
source=("$pkgname-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('fea0787598ca93711eaa231675f97623eb3d6eb224e11c40e66857b95f48724618bf7a9bfda476b56c1c96fe70bb61675966b1f3fb073fe3f7e4da91e78d3d90')

build() {
  cd apispec-$pkgver
  python setup.py build
}

check() {
  cd apispec-$pkgver
  python setup.py pytest
}

package() {
  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
