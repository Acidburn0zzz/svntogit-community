# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-apispec
pkgname=(python-apispec python2-apispec)
pkgver=1.1.0
pkgrel=1
pkgdesc="A pluggable API specification generator. Currently supports the OpenAPI Specification."
url="https://github.com/marshmallow-code/apispec"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-tornado' 'python2-tornado'
              'python-bottle' 'python2-bottle' 'python-marshmallow' 'python2-marshmallow'
              'python-flask' 'python2-flask' 'python-prance' 'python2-prance' 'python-yaml'
              'python2-yaml' 'python-mock' 'python2-mock' 'python-openapi-spec-validator'
              'python2-openapi-spec-validator')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/marshmallow-code/apispec/archive/$pkgver.tar.gz")
sha512sums=('2d4c36947864840a32b654066d9a384a692437681fc2aa72d963872f2b6ac0e41d58ec2b99e7eeb292e1942740650f6c513cc4178c7aa916977ed386b11fc885')

prepare() {
  cp -a apispec-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/apispec-$pkgver
  python setup.py build

  cd "$srcdir"/apispec-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/apispec-$pkgver
  python setup.py pytest

  cd "$srcdir"/apispec-$pkgver-py2
  python2 setup.py pytest
}

package_python-apispec() {
  depends=('python')
  optdepends=('python-yaml: for yaml support'
              'python-prance: for validation'
              'python-openapi-spec-validator: for validation')

  cd apispec-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-apispec() {
  depends=('python2')
  optdepends=('python2-yaml: for yaml support'
              'python2-prance: for validation'
              'python2-openapi-spec-validator: for validation')

  cd apispec-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
