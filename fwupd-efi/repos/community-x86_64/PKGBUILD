# Maintainer: Filipe La√≠ns <lains@archlinux.org>

pkgname=fwupd-efi
pkgver=1.0
pkgrel=1
pkgdesc='EFI Application used by uefi-capsule plugin in fwupd'
arch=('x86_64')
url='https://github.com/fwupd/fwupd'
license=('LGPL')
makedepends=('meson' 'gnu-efi')
source=("https://people.freedesktop.org/~hughsient/releases/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('add34f160e90eddd60911eb132fb7504c4b5727153f9623160d421be6ca11099'
            'SKIP')
validpgpkeys=('163EB50119225DB3DF8F49EA17ACBA8DFA970E17') # Richard Hughes <richard@hughsie.com>

build() {
  mkdir $pkgname-$pkgver/build
  cd $pkgname-$pkgver/build

  arch-meson .. \
      -D efi_sbat_distro_id='arch' \
      -D efi_sbat_distro_summary='Arch Linux' \
      -D efi_sbat_distro_pkgname=$pkgname \
      -D efi_sbat_distro_version=$pkgver \
      -D efi_sbat_distro_url="https://archlinux.org/packages/community/x86_64/$pkgname/"
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" meson install
}
