# Maintainer: David Runge <dvzrv@archlinux.org>
pkgname=qxgedit
pkgver=0.6.1
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=('x86_64')
url="https://qxgedit.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'gcc-libs' 'glibc' 'hicolor-icon-theme' 'qt5-base')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('10e6d768b5d209c1927a7ba2ac6a5e4423a79a43dc0d30f9b248a13580af843ac49f2d5906318ba5b01eda50f3655a480eef0233f4ce6479fbc047d84ae7efde')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
