# Maintainer: David Runge <dave@sleepmap.de>
pkgname=qxgedit
pkgver=0.5.4
pkgrel=1
pkgdesc="GUI for editing MIDI System Exclusive files for XG devices (eg. Yamaha DB50XG)"
arch=('x86_64')
url="https://qxgedit.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('alsa-lib' 'hicolor-icon-theme' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('2d3267a3b40a600b32665c280318db4bcd112d90de631c970ca3f1178e7a94ad179b738c75f9e43c3374cf6b805fce8e8a87dfc505a4834ab635445b16cdf2f8')

prepare() {
  cd "$pkgname-$pkgver"
  autoreconf -vfi
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
