# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=age
_pkgver=1.0.0-rc.2
pkgver=${_pkgver//-/}
pkgrel=1
pkgdesc='A simple, modern and secure file encryption tool'
arch=('x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD')
depends=('glibc')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
sha512sums=('488255ccf268a45f304d3fdcacbf32e214835516900b61e0554fe586bf61161e3bba2b1ee88e732490cdf82118a922c372133c888753d51f4d7afcb80421c088')

build() {
    cd "$pkgname-$_pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-linkmode external -extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$_pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$_pkgver"
    install -Dm755 -t "$pkgdir/usr/bin/" age age-keygen
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
