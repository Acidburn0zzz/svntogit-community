# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Tharre <tharre3@gmail.com>
# Contributor: Caleb Maclennan <caleb@alerque.com>

pkgname=age
_pkgver=1.0.0-rc.1
pkgver=${_pkgver//-/}
pkgrel=2
pkgdesc='A simple, modern and secure file encryption tool'
arch=('x86_64')
url="https://github.com/FiloSottile/$pkgname"
license=('BSD')
makedepends=('go')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$_pkgver.tar.gz")
sha512sums=('7efcd2f84fba3d6da8cda0b8229fedab692db8731de94d0b4c60bfaee68f91bb9bed3c78d894916dafceb414a90773d9258457b39ec4d373a80b6e08e06fc87f')

build() {
    cd "$pkgname-$_pkgver"
    go build \
      -trimpath \
      -buildmode=pie \
      -mod=readonly \
      -modcacherw \
      -ldflags "-extldflags \"$LDFLAGS\"" \
      -o . ./...
}

check() {
    cd "$pkgname-$_pkgver"
    go test ./...
}

package() {
    cd "$pkgname-$_pkgver"
    install -Dm755 -t "$pkgdir/usr/bin/" age age-keygen
    install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
}
