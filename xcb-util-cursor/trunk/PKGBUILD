# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu at seblu dot net>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

pkgname=xcb-util-cursor
pkgver=0.1.0
pkgrel=2
pkgdesc='XCB cursor library'
arch=('i686' 'x86_64')
url='http://cgit.freedesktop.org/xcb/util-cursor'
license=('custom:MIT')
options=('!libtool')
depends=('libxcb' 'xcb-util-renderutil' 'xcb-util-image')
makedepends=('git' 'xorg-util-macros' 'gperf')
# we use git bease snapshot tarball lack of the submodule m4 stuff
source=("$pkgname::git+http://anongit.freedesktop.org/git/xcb/util-cursor.git#tag=$pkgver")
md5sums=('SKIP')

build() {
  cd $pkgname
  git submodule update --init
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
