# Maintainer: Bruno Pagani <archange@archlinux.org>

pkgbase=materia-kde
pkgname=(materia-kde kvantum-theme-materia)
pkgver=20201113
pkgrel=2
pkgdesc="Materia theme for KDE Plasma 5"
arch=(any)
url="https://github.com/PapirusDevelopmentTeam/materia-kde"
license=(GPL3)
options=(!strip)
source=(${url}/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz
        ${pkgbase}-fix-transparency1.patch::${url}/commit/a007aef771cd58574d7924bb209567a8256dc799.patch
        ${pkgbase}-fix-transparency2.patch::${url}/commit/e546a51e4245deb8d2c3ebe4f8ba1b4f0034a70a.patch)
sha256sums=('6d8a5f8706f4b2a888e6af8a49852cecfda8a6a29a46133bd24978d365e34778'
            '28e6d1abd7244ed34ec1ae496426d146beb5b3a8af0a9bea23c01bf07ccd6999'
            '7a16f71465163f169e3ed5b8bc0909f6ed5b050e4187307d41ca921ce4fc1188')

prepare() {
    cd ${pkgbase}-${pkgver}
    patch -Np1 -i ../${pkgbase}-fix-transparency1.patch
    patch -Np1 -i ../${pkgbase}-fix-transparency2.patch
}

package_materia-kde() {
    optdepends=('materia-gtk-theme: Matching GTK theme'
                'kvantum-theme-materia: Materia theme for Kvantum Qt style (recommended)')

    cd ${pkgbase}-${pkgver}
    install -d "${pkgdir}"/usr/share

    cp -r plasma "${pkgdir}"/usr/share
    cp -r aurorae "${pkgdir}"/usr/share
    cp -r color-schemes "${pkgdir}"/usr/share
    cp -r konsole "${pkgdir}"/usr/share
    cp -r yakuake "${pkgdir}"/usr/share
}

package_kvantum-theme-materia() {
    pkgdesc="Materia theme for KDE Plasma 5"
    depends=(kvantum-qt5)

    cd ${pkgbase}-${pkgver}
    install -d "${pkgdir}"/usr/share

    cp -r Kvantum "${pkgdir}"/usr/share
}
