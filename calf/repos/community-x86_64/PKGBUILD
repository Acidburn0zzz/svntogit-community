# Maintainer: Ray Rashif <schiv@archlinux.org>

pkgname=calf
pkgver=0.0.18.6
pkgrel=1
pkgdesc="LV2/DSSI/LADSPA plug-ins suite and standalone JACK host"
arch=('i686' 'x86_64')
url="http://calf.sf.net/"
license=('LGPL')
depends=('libglade' 'lash')
makedepends=('lv2core' 'ladspa' 'dssi')
source=("http://downloads.sf.net/$pkgname/$pkgname-$pkgver.tar.gz"
        'include_fix.patch')
md5sums=('780854561dad1cb873041d7a0b98cbcd'
         '5aab4d057fb3646c43e6dfcad4c7128f')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/include_fix.patch"

  ./configure	--prefix=/usr \
              --enable-experimental
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  rm "$pkgdir/usr/share/icons/hicolor/icon-theme.cache"
}

# vim:set ts=2 sw=2 et:
