# Maintainer: NicoHood <archlinux {cat} nicohood {dog} de>
# PGP ID: 97312D5EB9D7AE7D0BD4307351DAE9B7C1AE9161
# Contributor: Morgenstern <charles [at] charlesbwise [dot] com>

pkgname=cherrytree
pkgver=0.99.38
pkgrel=1
pkgdesc="Hierarchical note-taking application"
arch=('x86_64')
url="https://www.giuspen.com/cherrytree/"
license=('GPL3')
depends=('fmt' 'gspell' 'gtksourceviewmm' 'libxml++2.6' 'uchardet')

makedepends=('cmake' 'python' 'spdlog')
source=("${pkgname}-${pkgver}.tar.xz::https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz"
        "${pkgname}-${pkgver}.tar.xz.asc::https://www.giuspen.com/software/${pkgname}_${pkgver}.tar.xz.asc"
         cherrytree-fmt8.patch::https://github.com/giuspen/cherrytree/commit/674dc6d6.patch)
sha512sums=('794e42a88c171fcc4b15129e3a12802d1516e257ef7d37734956b074e1ec4a086c2665a81d9ad266670c723fc40460861e66b19bc54bd8a94ce4cfc7c0944185'
            'SKIP'
            '3fed2caabcc0de7f64bdb354968cf7006093143507c5df8ecf5c7a386135129c6029104d24a5d006e247efd010914cd755af4b38e8e26fb637b140740d7e6902')
validpgpkeys=('C7BF38CE0BD442C2369AA984049128A20CE0648D') # Giuseppe Penone <giuspen [at] gmail [dot] com>

prepare() {
  patch -d ${pkgname}_${pkgver} -p1 < cherrytree-fmt8.patch # Fix build with fmt 8
}
 
build() {
  cmake \
	-B "${pkgname}_${pkgver}/build" \
	-S "${pkgname}_${pkgver}" \
  -DINSTALL_GTEST:BOOL='OFF' \
	-DAUTO_RUN_TESTING:BOOL='OFF' \
	-Wno-dev
  make -C "${pkgname}_${pkgver}/build"
}

# NOTE: In order to run tests that depend on the X server in a clean chroot,
# you must allow the chroot access your X server:
# xhost +local:
# https://wiki.archlinux.org/index.php/chroot#Run_graphical_applications_from_chroot
check() {
  export DISPLAY=:0
  cd "${pkgname}_${pkgver}"
  ./"${_pkgname}/build/run_tests_with_x_1"
  ./"${_pkgname}/build/run_tests_with_x_2"
}

package() {
  make -C "${pkgname}_${pkgver}/build" DESTDIR="${pkgdir}" install
}
