# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ogre
pkgver=1.12.6
pkgrel=1
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('freeimage' 'freetype2' 'libxaw' 'libxrandr' 'openexr' 'pugixml'
         'nvidia-cg-toolkit' 'zziplib' 'sdl2' 'glu' 'tinyxml')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'python' 'swig' 'systemd' 'git' 'mono')
install=ogre.install
replaces=('ogre-docs')
source=("https://github.com/OGRECave/ogre/archive/v${pkgver}.tar.gz"
        "git+https://github.com/ocornut/imgui.git#commit=688cf868ea83db6c2958dd6bf7a20d471d00940b")
optdepends=('cppunit: unit testing'
            'python: python bindings')
sha512sums=('2d62f86a94b5b066abc53ecfb7d98381fd684c180c630b99b58cf486069d2407c6c9e2c35f2baf315ba9bd887d6f507c8cc6663a50c97c4ee63f454e33e0450d'
            'SKIP')

prepare() {
  mkdir ogre-${pkgver}/build

  cp -r "${srcdir}"/imgui ogre-${pkgver}/Components/Overlay/src/
}

build() {
  cd ogre-${pkgver}/build

  cmake .. \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_BUILD_DEPENDENCIES=FALSE \
    -DCMAKE_BUILD_TYPE=RelWithDebInfo \
    -DOGRE_BUILD_PLUGIN_FREEIMAGE=TRUE \
    -DOGRE_BUILD_PLUGIN_EXRCODEC=TRUE

  make
  make OgreDoc
}

package() {

  cd ogre-${pkgver}/build

  make DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/usr/bin/SampleBrowser "${pkgdir}"/usr/bin/OgreSampleBrowser
  install -Dm644 ../LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
