# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=ogre
pkgver=13.0.1
pkgrel=2
pkgdesc='Scene-oriented, flexible 3D engine written in C++'
arch=('x86_64')
url='http://www.ogre3d.org'
license=('custom:MIT')
depends=('freeimage' 'freetype2' 'libxaw' 'libxrandr' 'sdl2' 'glu' 'pugixml' 'zlib')
makedepends=('cmake' 'doxygen' 'graphviz' 'ttf-dejavu' 'mesa' 'python' 'ninja' 'cppunit')
install=ogre.install
source=("$pkgname-$pkgver.tar.gz::https://github.com/OGRECave/ogre/archive/v${pkgver}.tar.gz"
        https://patch-diff.githubusercontent.com/raw/OGRECave/ogre/pull/2221.patch)
optdepends=('python: python bindings')
sha512sums=('72d8621ab9a9f9ae8561237fdeb23c9887984f2aed1ee72e62199fa7f26c259c75bca88c7f0f3472c0715b6e8295b7d76a96d434100413f7ece6f2db218f6278'
            '0f1251e43b21cf70339cf636bdc3928ad0f47080e05a532fbe6d431213515c8bb24ab48151955455f1820076eca452ccf90bf5ae9720e92ff678133dd97d976d')

prepare() {
  cd ogre-${pkgver}

  patch -Np1 -i "$srcdir"/2221.patch
}

build() {
  cd ogre-${pkgver}

  cmake \
    -Bbuild \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DOGRE_BUILD_TESTS=ON \
    -DOGRE_BUILD_DEPENDENCIES=OFF

  ninja -C build
  ninja -C build OgreDoc
}

check() {
  cd ogre-${pkgver}

  ninja -C build test
}

package() {
  cd ogre-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install

  mkdir -p "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/SampleBrowser "${pkgdir}"/opt/ogre/samples
  mv "${pkgdir}"/usr/bin/Test_* "${pkgdir}"/opt/ogre/samples
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}

# vim:set ts=2 sw=2 et:
