# $Id$
# Maintainer: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=0.200
pkgrel=1
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://en.znc.in/wiki/index.php/ZNC'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('c-ares' 'gcc-libs' 'openssl')
makedepends=('swig' 'tcl' 'python' 'perl' 'cyrus-sasl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
source=("http://znc.in/releases/znc-${pkgver}.tar.gz")
md5sums=('da5b690bc31b007474a77aae70c9c049')

build() {
  cd "znc-$pkgver"

  ./configure --prefix=/usr \
    --enable-sasl \
    --enable-tcl \
    --enable-python \
    --enable-perl \
    --enable-extra

  make
}

package() {
  cd "znc-$pkgver"

  make DESTDIR="$pkgdir" install
}
