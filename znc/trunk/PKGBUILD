# $Id$
# Maintainer: SÃ©bastien Luttringer <seblu@aur.archlinux.org>
# Contributor: Kaiting Chen <kaitocracy@gmail.com>
# Contributor: mickael9 <mickael9 at gmail dot com>

pkgname=znc
pkgver=0.206
pkgrel=3
pkgdesc='IRC bouncer with modules & scripts support'
url='http://en.znc.in/wiki/index.php/ZNC'
license=('GPL2')
arch=('i686' 'x86_64')
depends=('c-ares' 'libsasl')
#makedepends=('swig' 'tcl' 'python' 'perl')
makedepends=('tcl' 'python' 'perl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')
source=("http://znc.in/releases/$pkgname-$pkgver.tar.gz"
        "http://people.znc.in/~darthgandalf/znc/modperl/modperl-znc-0.204.tar.bz2"
        "http://people.znc.in/~darthgandalf/znc/modpython/modpython-znc-0.204.tar.bz2")
md5sums=('b7d3f21da81abaeb553066b0e10beb53'
         'a9c72d556d9a4ef100520433f17d71ae'
         '19bf033c5cb9243171c215043c594f07')

build() {
  # swig 2.0.6 is bugged, see http://bugs.debian.org/cgi-bin/bugreport.cgi?bug=672035
  # use an alternate ways of building znc explain here:
  # - http://wiki.znc.in/Modperl
  # - http://wiki.znc.in/Modpython
  mv -v modperl/* $pkgname-$pkgver/modules/modperl
  mv -v modpython/* $pkgname-$pkgver/modules/modpython
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
    --enable-sasl \
    --enable-python \
    --enable-perl \
    --enable-tcl \
    --disable-swig \
    --enable-extra
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
