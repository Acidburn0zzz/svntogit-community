# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Stefano Facchini <stefano.facchini@gmail.com>

pkgname=libosinfo
pkgver=1.4.0
pkgrel=2
pkgdesc="GObject based library API for managing information about operating systems, hypervisors and the (virtual) hardware devices they can support"
arch=('x86_64')
url="https://libosinfo.org/"
license=('GPL' 'LGPL')
depends=('glib2' 'libxslt' 'osinfo-db')
makedepends=('check' 'gobject-introspection' 'libsoup' 'vala' 'gtk-doc')
source=(https://releases.pagure.org/libosinfo/$pkgname-$pkgver.tar.gz{,.asc}
        0001-media-Fix-usage-of-application-id.patch
        0002-loader-Properly-load-the-arch-value-for-images.patch)
validpgpkeys=('DAF3A6FDB26B62912D0E8E3FBE86EBB415104FDF'  # Daniel P. Berrange
              '09B9C8FF223EF113AFA06A39EE926C2BDACC177B') # Fabiano Fidêncio <fidencio@redhat.com>
sha256sums=('a29e61a82b1a5727b145403914812273dfa033a0c07a15500078726bb3b84165'
            'SKIP'
            '439e2cf620560cb116d0c08201282c0e152b46b2ab1fa4abcd7e717a2d45dec7'
            '80c7583882a935658b9f9409cf5beeb36f46c873bb9041bcc858aca4e2fa7741')

prepare() {
  cd $pkgname-$pkgver
  sed -i '/export LC_ALL = C/d' maint.mk

  # Upsteam fixes
  patch -Np1 -i ../0001-media-Fix-usage-of-application-id.patch
  patch -Np1 -i ../0002-loader-Properly-load-the-arch-value-for-images.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --with-usb-ids-path=/usr/share/hwdata/usb.ids \
              --with-pci-ids-path=/usr/share/hwdata/pci.ids \
              --enable-gtk-doc
  make
}

check() {
  cd $pkgname-$pkgver
  make check || :
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
