# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=nextcloud-app-bookmarks
_name=bookmarks
epoch=1
pkgver=4.4.0
pkgrel=1
pkgdesc="Bookmarks app for nextcloud"
arch=('any')
url="https://github.com/nextcloud/bookmarks"
license=('AGPL3')
depends=('nextcloud')
makedepends=('ripgrep' 'yq')
groups=('nextcloud-apps')
options=('!strip')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nextcloud/bookmarks/releases/download/v$pkgver/bookmarks-$pkgver.tar.gz")
sha256sums=('e5b476cd9439fb2feae94016a1e3482d13387ab422edae6f51bb74f5f33345bf')
b2sums=('c6bc1ae6c2dc1b3921b65f7f30491b08b82adc2c54f8e0d6b5a84465e1b0d3d321b086d3d7b79ec72975ba4ac0e68b8a7b5113ece679c95794bc2e94b9b523d3')

_get_nextcloud_versions() {
  _app_min_major_version="$(xq '.info.dependencies.nextcloud["@min-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version="$(xq '.info.dependencies.nextcloud["@max-version"]' "${_name}/appinfo/info.xml"| sed 's/"//g')"
  _app_max_major_version=$(expr ${_app_max_major_version} + 1)
}

check() {
  local _app_min_major_version
  local _app_max_major_version
  _get_nextcloud_versions

  local _nextcloud_major_version="$(rg "OC_Version = " /usr/share/webapps/nextcloud/version.php |cut -d'(' -f2| cut -d ',' -f1)"
  if [[ "$(vercmp "${_nextcloud_major_version}" "${_app_min_major_version}")" -lt 0 ]] || [[ "$(vercmp "${_nextcloud_major_version}" "${_app_max_major_version}")" -gt 0 ]] ; then
    printf "%s requires nextcloud >= %s/ nextcloud <= %s, but nextcloud %s is provided.\n" "$pkgname" "${_app_min_major_version}" "${_app_max_major_version}" "${_nextcloud_major_version}"
    exit 1
  fi
}
sha256sums=('0a93cb724e2ccc24a719b232d6e2eb886c85205753a8d158162d4ef9931b5011')
b2sums=('76108db409738a0794ec13294652653c634f420fe0712b9f27d897c1159a842815f258a42729de041a0d140c2cd04b082c37fc583d8415507b5988b60056d306')

package() {
  _get_nextcloud_versions

  depends=("nextcloud>=${_app_min_major_version}" "nextcloud<${_app_max_major_version}")

  install -d "$pkgdir"/usr/share/webapps/nextcloud/apps
  cp -a "$srcdir"/bookmarks "$pkgdir"/usr/share/webapps/nextcloud/apps/bookmarks
}
