# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Simon Sapin <simon dot sapin at exyr dot org>
# Contributor: Kyle Keen <keenerd@gmail.com>

pkgbase=python-h11
pkgname=(python-h11 python2-h11)
pkgver=0.8.0
pkgrel=1
pkgdesc="A pure-Python, bring-your-own-I/O implementation of HTTP/1.1"
url="https://github.com/njsmith/h11"
license=('MIT')
arch=('any')
makedepends=('python-setuptools' 'python2-setuptools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner')
source=("https://pypi.io/packages/source/h/h11/h11-$pkgver.tar.gz")
sha512sums=('edd440dd74c03ce4fb7fd9e6b88e7c9550eb7c50b70676b506a8dafaf1469d9830de01300536128f0e7c61ae09d5256d15875dd5e6a9b99b89470bf70ee343b8')

prepare() {
  cp -a h11-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/h11-$pkgver
  python setup.py build

  cd "$srcdir"/h11-$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/h11-$pkgver
  python setup.py pytest

  cd "$srcdir"/h11-$pkgver-py2
  python2 setup.py pytest
}

package_python-h11() {
  depends=('python')

  cd h11-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}

package_python2-h11() {
  depends=('python2')

  cd h11-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.txt
}
