# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>

_name=lychee
pkgname=lychee-link-checker
pkgver=0.6.0
pkgrel=2
pkgdesc='Fast, async, resource-friendly link checker written in Rust'
arch=('x86_64')
url=https://github.com/lycheeverse/lychee
license=('Apache' 'MIT')
replaces=('lychee-rs')
depends=('gcc-libs' 'openssl')
makedepends=('rust')
source=("$url/archive/v$pkgver/$_name-$pkgver.tar.gz")
b2sums=('41b7e1b7c0dccae2144b3ac7f57c2ac3eda00f93d063ae6fef48a12893d5d7df342b5b1ecee4ddd62e2f393efdee6aceac7de7e4af363ac2eaf49e718d8e3701')

build() {
  cd $_name-$pkgver
  cargo build --release --locked
}

check() {
  cd $_name-$pkgver
  cargo test --release --locked
}

package() {
  cd $_name-$pkgver
  install -Dt "$pkgdir"/usr/bin target/release/$_name
  install -Dm644 -t "$pkgdir"/usr/share/doc/$pkgname README.md
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE-MIT
}

# vim:set ts=2 sw=2 et:
