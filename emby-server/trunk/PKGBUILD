# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server
pkgver=3.4.0.0
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=('any')
url='http://emby.media'
license=('custom')
depends=('dotnet-runtime' 'ffmpeg' 'skia-sharp58' 'sqlite')
install='emby-server.install'
source=("https://github.com/MediaBrowser/Emby/releases/download/${pkgver}/embyserver-netcore-${pkgver}.zip"
        'emby-server'
        'emby-migrate-database'
        'emby-server.conf'
        'emby-server.service'
        'emby-server.sysusers'
        'emby-server.tmpfiles'
        'license.docx')
backup=('etc/conf.d/emby-server')
sha256sums=('8951a2e84f83d2feadb3d8e615687befb15f1f53eb908c91cabf65dd2d826015'
            'f0afca728cf695ae80fffe944ea2700e154293247b30ed592f632c2e58dd00f1'
            'b25bf83a0ab371aff3b13b82f7af71b51bfe6d7e51eb8a8a3dd8f0774ffce6a5'
            '015869b52601a9cdbd303aed9dffe11ef19959da4a070c775ece8416e699979f'
            '8a91ea49a1699c820c4a180710072cba1d6d5c10e45df97477ff6a898f4e1d70'
            'f7fa33949757ffc587ecf82496dc35ebc8c8e5c98b882b31dc40a24263d3921a'
            'a13a844a1bdc679356698e3fd7f3946d07bdfe021ed857d944ead011ad338056'
            'a6d7ea65dcb06392479a85e1a10a7aeb872d803da6f784f6935fcd4ee63008c6')

prepare() {
  rm -rf system/{electron,runtimes}
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve='ownership' system "${pkgdir}"/usr/lib/emby-server
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 755 emby-migrate-database -t "${pkgdir}"/usr/bin/
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.sysusers "${pkgdir}"/usr/lib/sysusers.d/emby-server.conf
  install -Dm 644 emby-server.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/emby-server.conf
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

# vim: ts=2 sw=2 et:
