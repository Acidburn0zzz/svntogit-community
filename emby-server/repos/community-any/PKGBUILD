# $Id$
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Daniel Seymour <dannyseeless@gmail.com>

pkgname=emby-server
pkgver=3.4.1.0
pkgrel=1
pkgdesc='Bring together your videos, music, photos, and live television'
arch=('any')
url='http://emby.media'
license=('custom')
depends=('dotnet-runtime' 'ffmpeg' 'skia-sharp58' 'sqlite')
install='emby-server.install'
source=("https://github.com/MediaBrowser/Emby/releases/download/${pkgver}/embyserver-netcore-${pkgver}.zip"
        'emby-server'
        'emby-migrate-database'
        'emby-server.conf'
        'emby-server.service'
        'emby-server.sysusers'
        'emby-server.tmpfiles'
        'license.docx')
backup=('etc/conf.d/emby-server')
sha256sums=('8de28fbc9138f2c1c597f3cf028b221846d549b9ac68e7bc98c95e6c92ec752d'
            'fcab0c95fb94179ca52d63b354b2ca44b8e35885469cb2b3d006b5fbc5622490'
            'b25bf83a0ab371aff3b13b82f7af71b51bfe6d7e51eb8a8a3dd8f0774ffce6a5'
            '015869b52601a9cdbd303aed9dffe11ef19959da4a070c775ece8416e699979f'
            'efdaabd3002fee13a6831e90fd700dea4d7c6c954ede00da6f28c1eada33ad8c'
            'f7fa33949757ffc587ecf82496dc35ebc8c8e5c98b882b31dc40a24263d3921a'
            'a13a844a1bdc679356698e3fd7f3946d07bdfe021ed857d944ead011ad338056'
            'a6d7ea65dcb06392479a85e1a10a7aeb872d803da6f784f6935fcd4ee63008c6')

prepare() {
  rm -rf system/{electron,runtimes}
}

package() {
  install -dm 755 "${pkgdir}"/usr/lib
  cp -dr --no-preserve='ownership' system "${pkgdir}"/usr/lib/emby-server
  install -Dm 755 emby-server -t "${pkgdir}"/usr/bin/
  install -Dm 755 emby-migrate-database -t "${pkgdir}"/usr/bin/
  install -Dm 644 emby-server.service -t "${pkgdir}"/usr/lib/systemd/system/
  install -Dm 644 emby-server.sysusers "${pkgdir}"/usr/lib/sysusers.d/emby-server.conf
  install -Dm 644 emby-server.tmpfiles "${pkgdir}"/usr/lib/tmpfiles.d/emby-server.conf
  install -Dm 644 emby-server.conf "${pkgdir}"/etc/conf.d/emby-server
  install -Dm 644 license.docx -t "${pkgdir}"/usr/share/licenses/emby-server/
}

# vim: ts=2 sw=2 et:
