# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>

pkgname=kbfs
pkgver=2.0.0
pkgrel=1
pkgdesc="The Keybase filesystem"
arch=('x86_64')
url="https://keybase.io/docs/kbfs"
license=('BSD')
depends=('fuse' 'keybase')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/keybase/kbfs/archive/v$pkgver.tar.gz")
sha512sums=('ee43f890723dcd0f2ec95981334a55fe42e27d1224f0058c8fd78c337b7097c6c5af0d798bcee37fe381e9ef1ad188fce5ce6a23ecfd9293d61378932635582b')

prepare() {
  cd kbfs-$pkgver

  export GOPATH="$srcdir/.gopath"
  mkdir -p "$GOPATH"/src/github.com/keybase
  ln -sf "$PWD" "$GOPATH"/src/github.com/keybase/kbfs
}

build() {
  cd kbfs-$pkgver
  export GOPATH="$srcdir/.gopath"
  go build -a -tags production -o bin/kbfsfuse github.com/keybase/kbfs/kbfsfuse
  go build -a -tags production -o bin/git-remote-keybase github.com/keybase/kbfs/kbfsgit/git-remote-keybase
}

package() {
  cd kbfs-$pkgver/

  install -Dm755 -t "${pkgdir}"/usr/bin/ bin/*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 packaging/linux/systemd/kbfs.service "$pkgdir"/usr/lib/systemd/user/kbfs.service
}
