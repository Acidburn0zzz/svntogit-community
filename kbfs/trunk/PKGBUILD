# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Michael Hansen <zrax0111 gmail com>

pkgname=kbfs
pkgver=1.0.48
pkgrel=1
pkgdesc="The Keybase filesystem"
arch=('x86_64')
url="https://keybase.io/docs/kbfs"
license=('BSD')
depends=('fuse' 'keybase')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/keybase/kbfs/archive/v$pkgver.tar.gz")
sha512sums=('89b391223e2c7014e64c09e5303c7b89bea2508331b5d53facf8802de2442c4e53a4d79f65c085a2b3234762b334d3d36e903867ef841140d44207926d82991c')


prepare() {
  cd kbfs-$pkgver

  export GOPATH="$srcdir/.gopath"
  mkdir -p "$GOPATH"/src/github.com/keybase
  ln -sf "$PWD" "$GOPATH"/src/github.com/keybase/kbfs
}

build() {
  cd kbfs-$pkgver
  export GOPATH="$srcdir/.gopath"
  go build -a -tags production -o bin/kbfsfuse github.com/keybase/kbfs/kbfsfuse
  go build -a -tags production -o bin/git-remote-keybase github.com/keybase/kbfs/kbfsgit/git-remote-keybase
}

package() {
  cd kbfs-$pkgver/

  install -Dm755 -t "${pkgdir}"/usr/bin/ bin/*
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
  install -Dm644 packaging/linux/systemd/kbfs.service "$pkgdir"/usr/lib/systemd/user/kbfs.service
}
