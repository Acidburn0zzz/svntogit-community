# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kwpolska <kwpolska@kwpolska.tk>

pkgname=python-pudb
pkgver=2022.1
pkgrel=1
pkgdesc="A full-screen, console-based Python debugger"
url="https://pypi.python.org/pypi/pudb"
arch=('any')
license=('MIT')
depends=('python-urwid' 'python-pygments' 'python-jedi' 'python-packaging')
checkdepends=('python-pytest' 'python-pytest-mock')
source=("$pkgname-$pkgver.tar.gz::https://github.com/inducer/pudb/archive/v$pkgver.tar.gz")
sha512sums=('934164fc5e29bf5bb292a28af5d2a3ac4c92803007ae0efe12ac67113d0ade64cd6b6999befbd3468be6ac2a91bc65302824211d3fe1787811f5a6950053c7f5')

build() {
  cd pudb-$pkgver
  python setup.py build
}

check() {
  cd pudb-$pkgver
  python -m pytest --tb=native -rxs test/
}

package() {
  cd pudb-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim: set ft=sh ts=2 sw=2 et:
