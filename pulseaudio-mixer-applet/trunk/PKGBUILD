# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Samed Beyribey <ras0ir AT eventualis DOT org>

pkgname=pulseaudio-mixer-applet
pkgver=0.2.2
pkgrel=3
pkgdesc="GNOME panel applet to control PulseAudio devices and streams"
arch=(i686 x86_64)
url="https://launchpad.net/pama"
license=(GPL3)
depends=(gnome-media gnome-panel pulseaudio hicolor-icon-theme)
makedepends=(intltool)
groups=(pulseaudio-gnome)
install=pulseaudio-mixer-applet.install
source=(http://launchpad.net/pama/${pkgver%.*}/$pkgver/+download/pulseaudio-mixer-applet-$pkgver.tar.gz)
md5sums=('b7df43c999ad8583f779e678cfe768ae')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure --prefix=/usr
  make || return 1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install

  # Make the gnome-media microphone icons globally available
  # Should probably be done in gnome-media
  for _size in 16x16 22x22 24x24 32x32 scalable; do
    mkdir -p "$pkgdir/usr/share/icons/hicolor/$_size/status"

    if [[ _size == scalable ]]; then
      _ext=svg
    else
      _ext=png
    fi

    for _icon in high low medium muted; do
      ln -s /usr/share/gnome-media/icons/hicolor/$_size/status/audio-input-microphone-$_icon.$_ext \
        "$pkgdir/usr/share/icons/hicolor/$_size/status/"
    done
  done
}
