# $Id$
# Maintainer: Vesa Kaihlavirta <vegai@iki.fi>

pkgname=quassel
pkgver=0.7.1
pkgrel=1
pkgdesc="QT4 irc client with a separated core"
arch=('i686' 'x86_64')
url="http://quassel-irc.org/"
license=('GPL')
depends=('kdelibs' 'oxygen-icons' 'hicolor-icon-theme')
makedepends=('cmake' 'automoc4')
install=$pkgname.install
backup=(etc/conf.d/quassel.conf)
source=(http://quassel-irc.org/pub/$pkgname-$pkgver.tar.bz2
        rc.$pkgname
        $pkgname.conf)
sha256sums=('914a9cd21bfe4be1f78b2f6c96e54b52db7ed3c0adc4dce6bfabec38a30f7ecd'
            '845e621448a51ec181554e9327bfcc3a3de1501ae8f70410f0b9b8991ac171b0'
            'f3031ea8217e01ba42cea14606169e3e27affa5918968ffd5a03c21ae92fe2b8')

build() {
  [[ ! -d build ]] && mkdir build
  cd build

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr/ \
    -DWITH_KDE=1 \
    -DCMAKE_BUILD_TYPE="Release" \
    ../${pkgname}-${pkgver}/ \
    -Wno-dev

  make
}

package() {
  cd build

  make DESTDIR=${pkgdir} install

  install -D ${srcdir}/rc.$pkgname \
    ${pkgdir}/etc/rc.d/$pkgname
  install -D -m644 ${srcdir}/$pkgname.conf \
    ${pkgdir}/etc/conf.d/$pkgname
}
