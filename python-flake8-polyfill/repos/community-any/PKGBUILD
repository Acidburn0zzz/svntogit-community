# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Christopher Arndt <aur -at- chrisarndt -dot- de>

_module=flake8-polyfill
pkgname=python-flake8-polyfill
pkgver=1.0.2
pkgrel=1
pkgdesc="Polyfill package for Flake8 plugins"
url="https://gitlab.com/pycqa/flake8-polyfill"
depends=('flake8')
checkdepends=('python-pytest' 'python-mock' 'python-pycodestyle' 'python-pep8')
makedepends=('python-setuptools')
license=('MIT')
arch=('any')
source=(https://gitlab.com/pycqa/flake8-polyfill/-/archive/${pkgver}/flake8-polyfill-${pkgver}.tar.gz)
md5sums=('6b2cd316fc75929ce41a5b07244fd310')

build() {
  cd "${_module}-${pkgver}"
  python setup.py build
}

check() {
  cd "${_module}-${pkgver}"
  PYTHONPATH=$PWD/src/ pytest tests
}

package() {
  cd "${_module}-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build

  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
