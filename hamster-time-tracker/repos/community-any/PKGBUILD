# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Georg Vogetseder <georg.vogetseder@gmail.com>

pkgname=hamster-time-tracker
pkgver=3.0.1
pkgrel=1
pkgdesc="Time tracking application that helps you to keep track on how much time you have spent during the day on activities you choose to track"
arch=('any')
url="https://github.com/projecthamster/hamster"
license=('GPL')
depends=('gtk3' 'python-dbus' 'python-cairo' 'python-gobject' 'python-xdg')
makedepends=('intltool' 'itstool')
source=("$pkgname-$pkgver.tar.gz::https://github.com/projecthamster/hamster/archive/v$pkgver.tar.gz"
        0001-Use-itstool-instead-of-xml2po.patch)
sha256sums=('f3386ce57ffcc67ccdcf1791baaaf7ada8467bee573802f34f796a126e752ca2'
            '3a4b81e2f79468180212b35cdf7b1aa130ed2aa89e03617832bbd808a469c23d')

prepare() {
  cd hamster-$pkgver
  # Don't use deprecated xml2po
  # https://github.com/projecthamster/hamster/issues/583
  patch -Np1 -i ../0001-Use-itstool-instead-of-xml2po.patch
}

build() {
  cd hamster-$pkgver
  python ./waf configure --prefix=/usr --libexecdir=/usr/lib  --skip-icon-cache-update
  python ./waf build
}

package() {
  cd hamster-$pkgver
  python ./waf install --destdir="$pkgdir"
  rm "$pkgdir/usr/share/glib-2.0/schemas/gschemas.compiled"
}
