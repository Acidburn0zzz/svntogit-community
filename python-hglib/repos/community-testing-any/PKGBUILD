# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Erik Johnson <palehose at gmail dot com>
# Contributor: Jelle van der Waa <jelle@archlinux.org>

pkgbase=python-hglib
pkgname=('python2-hglib' 'python-hglib')
pkgver=2.3
pkgrel=1
arch=('any')
url="https://pypi.python.org/pypi/python-hglib"
license=('MIT')
makedepends=('python2' 'mercurial' 'python')
checkdepends=('python-nose' 'python2-nose')
source=(https://pypi.python.org/packages/80/9c/1618281fc1ef0df4436b1435de6276452fefb46b111b3b00d3e20fcf5e17/${pkgbase}-${pkgver}.tar.gz{,.asc})
validpgpkeys=('FAD61395F642FC2B33C84B9A205781AC682A2D72') # Matthew Mackall <mpm@selenic.com>
sha256sums=('3d2e2eb2ecf73789635363784fdc6f0c0935256fe0263dfd6ed005a055c418df'
            'SKIP')

prepare() {
  cp -rf "${pkgbase}-${pkgver}" "${pkgbase}-${pkgver}-2"
}

check() {
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python test.py

  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 test.py
}

package_python2-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python2' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}-2"
  python2 setup.py install --root=${pkgdir} --optimize=1
}

package_python-hglib() {
  pkgdesc="A library with a fast, convenient interface to Mercurial. It uses Mercurial's command server for communication with hg."
  depends=('python' 'mercurial')
  cd "${srcdir}/${pkgbase}-${pkgver}"
  python setup.py install --root=${pkgdir} --optimize=1
}
