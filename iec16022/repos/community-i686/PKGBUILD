# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=iec16022
pkgver=0.2.7
pkgrel=1
pkgdesc="Produce 2D barcodes often also referenced as DataMatrix"
arch=('i686' 'x86_64')
url="http://datenfreihafen.org/projects/iec16022.html"
license=('GPL')
depends=('popt' 'zlib')
source=($pkgname-$pkgver.tar.gz::https://github.com/rdoeffinger/$pkgname/archive/v$pkgver.tar.gz)
sha256sums=('34f00da5f6c83dd14b2803e5ed0f5310310e452023fe989c59fc5489b78cd33a')

prepare() {
  cd $pkgname-$pkgver
  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

check() {
  cd $pkgname-$pkgver
  make check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
