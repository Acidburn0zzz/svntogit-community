# Maintainer: Kyle Keen <keenerd@gmail.com>

pkgname=jupyter-nbclient
pkgver=0.5.13
pkgrel=2
pkgdesc="A tool for running Jupyter Notebooks in different execution contexts."
arch=('any')
url="https://github.com/jupyter/nbclient"
license=('BSD')
depends=('python-nest-asyncio' 'python-traitlets' 'python-async_generator' 'python-jupyter_client' 'jupyter-nbformat')
makedepends=('python-build' 'python-installer' 'python-setuptools' 'python-wheel')
checkdepends=('python-pytest-asyncio' 'python-xmltodict' 'jupyter-nbconvert')
source=("https://github.com/jupyter/nbclient/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('3caa3a63a2f6d7e9f4ffd61eb924a3ba353224719525677eabee8c97ae3341a1')

build() {
  cd nbclient-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd nbclient-$pkgver
  pytest -v
}

package() {
  cd nbclient-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm644 LICENSE -t "$pkgdir/usr/share/licenses/$pkgname"
}
