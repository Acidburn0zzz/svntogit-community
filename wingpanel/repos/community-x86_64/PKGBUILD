# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=wingpanel
pkgver=2.2.6
pkgrel=1
pkgdesc='The Pantheon Top Panel'
arch=(x86_64)
url=https://github.com/elementary/wingpanel
license=(GPL3)
groups=(pantheon)
depends=(
  glib2
  gtk3
  libgala.so
  libgee
  libgranite.so
  mutter
)
makedepends=(
  git
  gala
  meson
  vala
)
provides=(libwingpanel-2.0.so)
source=(git+https://github.com/elementary/wingpanel.git#tag=09462f558186fc279576d3a6a5a0eeac9da6835b)
sha256sums=(SKIP)

pkgver() {
  cd wingpanel

  git describe --tags
}

prepare() {
  cd wingpanel

  git cherry-pick -n 23a73e76c19e02aecf4e39352723f8763c7145f3
}

build() {
  arch-meson wingpanel build
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
