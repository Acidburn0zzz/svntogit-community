# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-kiwisolver
pkgver=1.3.0
pkgrel=1
pkgdesc="A fast implementation of the Cassowary constraint solver"
url="https://github.com/nucleic/kiwi"
license=('PerlArtistic')
arch=('x86_64')
depends=('python')
makedepends=('python-cppy' 'python-pip')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/nucleic/kiwi/archive/$pkgver.tar.gz")
sha512sums=('64bb475f86e6f409318c5030cd2874bf060c26bf6c1ea62bfb96c1ae27f7326fcd6fc575cbdad7351808d18ae9b9f6664eb312019d432cd6a4c0b8af4fa317b6')

build() {
  cd kiwi-$pkgver
  python setup.py build
}

check() {
  cd kiwi-$pkgver
  python setup.py pytest
}

package() {
  cd kiwi-$pkgver/
  python3 setup.py install --root="$pkgdir" --optimize=1
}
