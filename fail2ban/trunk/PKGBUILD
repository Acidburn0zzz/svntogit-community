# $Id$
# Maintainer:  Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Geoffroy Carrier <geoffroy.carrier@koon.fr>
# Contributor: michalzxc
# Contributor: nbags <neilbags@gmail.com>

pkgname=fail2ban
pkgver=0.8.7.1
pkgrel=2
pkgdesc="Bans IPs after too many failed authentification attempts against common daemons"
url="http://www.fail2ban.org/"
license=("GPL")
arch=('any')
depends=('python2' 'iptables')
backup=('etc/fail2ban/fail2ban.conf' 'etc/fail2ban/jail.conf')
source=(https://github.com/downloads/$pkgname/$pkgname/${pkgname}_${pkgver}.orig.tar.gz
		rc.d service)
md5sums=('39ae20deafbd0441ad385204d532e423'
         '70caa58e130a13a505d63a35b9aecfb6'
         '3114503de43111d594d398ea3f364e9e')

package() {
  cd $srcdir/$pkgname-$pkgver
  python2 setup.py install --root "$pkgdir"

  install -Dm755 $srcdir/rc.d $pkgdir/etc/rc.d/$pkgname
  install -Dm644 $srcdir/service $pkgdir/usr/lib/systemd/system/$pkgname.service 

  # avoid conflict with filesystem>=2012.06
  rm -r "$pkgdir/var/run"
}
