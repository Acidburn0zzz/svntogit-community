# Maintainer: Dave Reisner <d@falconindy.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=yajl
pkgver=2.0.0
pkgrel=2
pkgdesc='Yet Another JSON Library.'
arch=('i686' 'x86_64')
url='http://lloyd.github.com/yajl/'
license=('ISC')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/lloyd/$pkgname/tarball/$pkgver"
        "handle-null-case.patch")
md5sums=('ff3bb8531d1f3c48441a57220919c8e7'
         'dd6ad7386f6d19f1d68357d8a2d48f3e')

build() {
  dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$dirname"

  patch -Np1 < "$srcdir/handle-null-case.patch"

  cmake -DCMAKE_INSTALL_PREFIX=/usr .

  make
}

package() {
  dirname=$(tar tf "$srcdir/$pkgname-$pkgver.tar.gz" | sed 1q);
  cd "$dirname"

  make DESTDIR="$pkgdir" install

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
