# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>
# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=yajl
pkgver=1.0.11
pkgrel=3
pkgdesc='Yet Another JSON Library.'
arch=('i686' 'x86_64')
url='http://lloyd.github.com/yajl/'
license=('BSD')
makedepends=('cmake')
source=(${pkgname}-${pkgver}.tar.gz::"https://github.com/lloyd/${pkgname}/tarball/${pkgver}")
md5sums=('5b60f4d59b3b1fb42d7808d08460fb12')

build() {
  cd lloyd-${pkgname}-f4baae0

  cmake \
    -DCMAKE_INSTALL_PREFIX=/usr \
    .

  make
}

package() {
  cd lloyd-${pkgname}-f4baae0

  make DESTDIR=${pkgdir} install

  install -D -m644 COPYING \
    ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
