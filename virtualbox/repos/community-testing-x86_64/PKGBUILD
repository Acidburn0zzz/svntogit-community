# $Id:
# Maintainer: Dave Reisner <d@falconindy.com>

pkgbase=virtualbox
pkgname=('virtualbox' 'virtualbox-additions' 'virtualbox-modules')
pkgver=4.0.0
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.virtualbox.org"
license=('GPL')
makedepends=('bin86' 'dev86' 'iasl' 'libpulse' 'libxslt' 'libxml2' 'alsa-lib'
             'sdl' 'makeself' 'libvncserver' 'libx11' 'zlib' 'libpng' 'openssl'
            'kernel26-headers>=2.6.35' 'libidl2' 'libxmu' 'mesa' 'python2' 'qt')
optdepends=()
source=("http://download.virtualbox.org/virtualbox/$pkgver/VirtualBox-$pkgver.tar.bz2"
        '60-virtualbox.rules' '60-vboxguest.rules' 'LocalConfig.kmk' 'vboxdrv.sh')
md5sums=('0d8c3d2cb4924140b602bfc115c5801f'
         '641bd2d65f0183ef5897d8dd0322f7bc'
         'ed1341881437455d9735875ddf455fbe'
         '5ed1a069f04aa5279d9fdbb446af7431'
         '6c88f8a711dfafafe5a7022eda08c623')

build() {
  cd "$srcdir/VirtualBox-${pkgver}_OSE"

  cp "$srcdir/LocalConfig.kmk" .

  # we probably want to build with both of these. I haven't tried very hard
  # to resolve the build errors these generate.
  ./configure --disable-java --disable-docs
  source ./env.sh
  kmk all

  make -C "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"
  make -C "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src/vboxvideo"
}

package_virtualbox() {
  pkgdesc="Powerful x86 virtualization for enterprise as well as home use"
  depends=('libxml2' 'libxcursor' 'libidl2' 'libxinerama' 'sdl' 'libxmu' 'curl' 'libvncserver' 'kernel26-headers' 'gcc' 'make')
  optdepends=('libxt: for shared clipboard'
              'python2: for python support'
              'qt: for VirtualBox GUI')
  replaces=('virtualbox-ose')
  conflicts=('virtualbox-ose')
  install=virtualbox.install

  source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

  cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin"
  mkdir -p "$pkgdir"/usr/{bin,lib/virtualbox/components,share/virtualbox/nls}

  #Binaries and Wrapper with Launchers
  install -m 0755 VBox.sh "$pkgdir/usr/bin/VBox"
  ln -sf VBox "$pkgdir/usr/bin/VBoxHeadless"
  ln -sf VBox "$pkgdir/usr/bin/VBoxManage"
  ln -sf VBox "$pkgdir/usr/bin/VBoxSDL"
  ln -sf VBox "$pkgdir/usr/bin/VirtualBox"

  install -m 0755 VBoxTunctl "$pkgdir/usr/bin"

  #components
  install -m 0755 components/* -t "$pkgdir/usr/lib/virtualbox/components"

  #lib
  install -m 0755 *.so "$pkgdir/usr/lib/virtualbox"
  install -m 0644 *.gc *.r0  VBoxEFI*.fd "$pkgdir/usr/lib/virtualbox"

  #setuid root binaries
  install -m 4755 VBoxHeadless VBoxSDL VBoxNetDHCP VBoxNetAdpCtl VirtualBox VBoxBFE \
      -t "$pkgdir/usr/lib/virtualbox"

  #other binaries
  install -m 0755 VBoxManage VBoxSVC VBoxXPCOMIPCD VBoxSysInfo.sh xpidl \
                  VBoxTestOGL EfiThunk -t "$pkgdir/usr/lib/virtualbox"

  #language
  install -m 0755 nls/*.qm -t "$pkgdir/usr/share/virtualbox/nls"

  #icon
  install -D -m 0644 VBox.png "$pkgdir/usr/share/pixmaps/VBox.png"

  #desktop
  install -D -m 0644 virtualbox.desktop "$pkgdir/usr/share/applications/virtualbox.desktop"

  #install configuration
  mkdir -p "$pkgdir/etc/vbox"
  echo 'INSTALL_DIR=/usr/lib/virtualbox' > "$pkgdir/etc/vbox/vbox.cfg"

  #udev and licence
  install -D -m 0644 "$srcdir/VirtualBox-${pkgver}_OSE/COPYING" \
      "$pkgdir/usr/share/licenses/virtualbox-ose/LICENSE"
  install -D -m 0644 "$srcdir/60-virtualbox.rules" \
      "$pkgdir/lib/udev/rules.d/60-virtualbox.rules"
}

package_virtualbox-additions(){
    pkgdesc="Guest Additions for VirtualBox (Only for guest systems)"
    depends=("virtualbox-ose-additions-modules=$pkgver" 'gcc-libs' 'libxmu')

    source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

    cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions"

    install -d "$pkgdir/usr/bin"
    install -d "$pkgdir/sbin"
    install -m755 VBoxClient VBoxControl VBoxService "$pkgdir/usr/bin"
    install -m755 mount.vboxsf "$pkgdir/sbin"

    install -m644 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/50-vboxmouse.conf \
        "$pkgdir"/etc/X11/xorg.conf.d/50-vboxmouse.conf
    install -m644 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/linux/installer/70-xorg-vboxmouse.rules \
        "$pkgdir"/lib/udev/rules.d/70-xorg-vboxmouse.rules
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/98vboxadd-xclient \
        "$pkgdir"/usr/bin/VBoxClient-all
    install -m755 -D "$srcdir"/VirtualBox-${pkgver}_OSE/src/VBox/Additions/x11/Installer/vboxclient.desktop \
        "$pkgdir"/etc/xdg/autostart/vboxclient.desktop
    install -D vboxmouse_drv_19.so \
        "$pkgdir/usr/lib/xorg/modules/input/vboxmouse.so"
    install -D vboxvideo_drv_19.so \
        "$pkgdir/usr/lib/xorg/modules/drivers/vboxvideo.so"
    install -d "$pkgdir/usr/lib/xorg/modules/dri"
    install -m755 VBoxOGL*.so "$pkgdir/usr/lib"
    ln -s /usr/lib/VBoxOGL.so "$pkgdir/usr/lib/xorg/modules/dri/vboxvideo_dri.so"
    install -m755 -D pam_vbox.so "$pkgdir/lib/security/pam_vbox.so"

}

package_virtualbox-modules(){
  _kernver=2.6.36-ARCH
  pkgdesc="Kernel modules used in guest archlinux systems"
  license=('GPL')
  install=virtualbox-ose-additions-modules.install
  depends=('kernel26>=2.6.36' 'kernel26<2.6.37')

  source "$srcdir/VirtualBox-${pkgver}_OSE/env.sh"

  cd "$srcdir/VirtualBox-${pkgver}_OSE/out/linux.$BUILD_PLATFORM_ARCH/release/bin/additions/src"

  cd vboxguest
  install -D -m644 vboxguest.ko \
      "$pkgdir/lib/modules/$_kernver/misc/vboxguest.ko"

  cd ../vboxsf
  install -D -m644 vboxsf.ko \
      "$pkgdir/lib/modules/$_kernver/misc/vboxsf.ko"

  cd ../vboxvideo
  install -D -m644 vboxvideo.ko \
      "$pkgdir/lib/modules/$_kernver/misc/vboxvideo.ko"

  install -D -m 0644 "$srcdir/60-vboxguest.rules" \
      "$pkgdir/lib/udev/rules.d/60-vboxguest.rules"

  sed -i -e "s/KERNEL_VERSION='.*'/KERNEL_VERSION='${_kernver}'/" "$startdir/virtualbox-ose-additions-modules.install"
}

# vim:set ts=2 sw=2 et:
