# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-arpeggio
pkgver=1.10.0
pkgrel=1
pkgdesc="Packrat parser interpreter"
url="https://github.com/textX/Arpeggio"
license=('MIT')
arch=('any')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://github.com/textX/Arpeggio/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('368c478114ddedbf40c94b4e656945063df5661deacc9c75780ddca18198a9e3e504718fc6c9a1e84c11c5c8f2b454584d1cfc5a14ea2b783e97b8e317047023')

build() {
  cd Arpeggio-$pkgver
  python setup.py build
}

check() {
  cd Arpeggio-$pkgver
  python -m pytest
}

package() {
  cd Arpeggio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
