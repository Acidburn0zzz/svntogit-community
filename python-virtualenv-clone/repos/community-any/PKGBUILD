# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-virtualenv-clone' 'python2-virtualenv-clone')
pkgver=0.2.4
pkgrel=1
pkgdesc="A script for cloning a non-relocatable virtualenv."
arch=('any')
url="https://github.com/edwardgeorge/virtualenv-clone"
license=('GPL')
makedepends=('python-distribute' 'python2-distribute')
options=('!emptydirs')
source=(http://pypi.python.org/packages/source/v/virtualenv-clone/virtualenv-clone-$pkgver.tar.gz)
sha256sums=('6797e96bc2a1ab41995bcabdfe08ac1b02c55c719c62036ccf53a0ed5430b935')

package_python-virtualenv-clone() {
  depends=('python')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python setup.py install --root="$pkgdir" -O1

  # virtualenv-clone is provided by the python2-virtualenv-clone package
  rm "$pkgdir/usr/bin/virtualenv-clone"
}

package_python2-virtualenv-clone() {
  depends=('python2-distribute')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
