# $Id$
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com

pkgname=('python-virtualenv-clone' 'python2-virtualenv-clone')
pkgver=0.2.3
pkgrel=2
pkgdesc="A script for cloning a non-relocatable virtualenv."
arch=('any')
url="https://github.com/edwardgeorge/virtualenv-clone"
license=('GPL')
makedepends=('python-distribute' 'python2-distribute')
options=('!emptydirs')
source=(http://pypi.python.org/packages/source/v/virtualenv-clone/virtualenv-clone-$pkgver.tar.gz        binary-file-handling-fixes.patch)
sha256sums=('c03c7b719484f3901980fe0c04493315230292cc12a40ae6c9a65be77bbe8334'
            '1002bbd6e678b43b0ac160ca1b3958f6c55d9eea7faed86f0b0ddac0af67d1da')

build() {
  cd "$srcdir/virtualenv-clone-$pkgver"

  # https://github.com/edwardgeorge/virtualenv-clone/issues/5
  patch -Np1 -i "$srcdir/binary-file-handling-fixes.patch"
}

package_python-virtualenv-clone() {
  depends=('python')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python setup.py install --root="$pkgdir" -O1

  # virtualenv-clone is provided by the python2-virtualenv-clone package
  rm "$pkgdir/usr/bin/virtualenv-clone"
}

package_python2-virtualenv-clone() {
  depends=('python2-distribute')

  cd "$srcdir/virtualenv-clone-$pkgver"
  python2 setup.py install --root="$pkgdir" -O1
}

# vim:set ts=2 sw=2 et:
