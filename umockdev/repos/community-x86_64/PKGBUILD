# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Marcin (CTRL) Wieczorek <marcin@marcin.co>
# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=umockdev
pkgver=0.11.2
pkgrel=1
pkgdesc="Record/mock hardware devices for bug reports and regression tests"
arch=('x86_64')
url="https://github.com/martinpitt/umockdev/"
license=('LGPL')
depends=('systemd' 'libgudev')
makedepends=('gobject-introspection' 'gtk-doc' 'vala')
source=($pkgname-$pkgver.tar.gz::https://github.com/martinpitt/$pkgname/archive/$pkgver.tar.gz)
sha256sums=('76f89a62399e2b55d270d8216765845c845a18f257f633f6dc55d2ed290cdba5')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  ./autogen.sh --prefix=/usr --enable-gtk-doc --enable-introspection
  make
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}

  make DESTDIR="${pkgdir}" install
}
