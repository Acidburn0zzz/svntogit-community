# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=3.0
pkgrel=1
pkgdesc='Lightweight and fast web browser'
arch=('x86_64' 'i686')
url='http://www.netsurf-browser.org/'
license=('GPL')
depends=('libmng' 'curl' 'libglade' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'libcss>=0.1.2' 'hubbub>=0.1.2' 'libwebp>=0.2.0' 'gstreamer0.10')
makedepends=('re2c' 'lcms' 'gendesk')
provides=('netsurf')
conflicts=('netsurf')
install="$pkgname.install"
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://download.$pkgname-browser.org/$pkgname/releases/source/$pkgname-$pkgver-src.tar.gz")
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            '7c6a48d3cc3e9a3e3a51b532ddf60f7697e97bf8b61a6d3b2ced1a2e89fbccc6')

_exec=('netsurf %U')
_genericname=('Web Browser')
_comment=('Lightweight web browser')
_mimetype=('text/html;application/xhtml+xml;x-scheme-handler/http;x-scheme-handler/https;x-scheme-handler/geo')

prepare() {
  cd "$srcdir"

  gendesk
}

build() {
  cd "$pkgname-$pkgver"

  make \
    PREFIX=/usr \
    TARGET=gtk \
    NETSURF_USE_WEBP=YES \
    NETSURF_USE_VIDEO=YES
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make install PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
