# $Id$
# Maintainer: Alexander Rødseth <rodseth@gmail.com>
# Contributor: Paulo Matias <matiasΘarchlinux-br·org>
# Contributor: Georgij Kondratjev <smpuj@bk.ru>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=netsurf
pkgver=2.8
pkgrel=4
pkgdesc="Lightweight and fast web browser"
arch=('x86_64' 'i686')
url="http://www.netsurf-browser.org/"
license=('GPL')
depends=('libmng' 'curl' 'libglade' 'librsvg' 'desktop-file-utils' 'libnsbmp' 'libnsgif' 'hubbub' 'libcss' 'hubbub' 'libwebp')
makedepends=('re2c' 'lcms')
provides=('netsurf')
conflicts=('netsurf')
install=netsurf.install
source=("netsurf.png::http://ubuntu.allmyapps.com/data/n/e/netsurf-netsurf-web-browser/icon_48x48_netsurf.png"
        "http://www.netsurf-browser.org/downloads/releases/$pkgname-$pkgver-src.tar.gz"
        "$pkgname.desktop")
sha256sums=('f0dbcc5d80bf03d706aa8b28a322aa7f169a40813848c2d1505691f6e2c7ef00'
            '09f3aa38f26248a410481aaf125811d6af4c8535725bec950e75af37ccd0aad2'
            '5adf656bcd8d247ceb2808d8079cdc59155b8e4cf8569e050bb2730ad9783347')

build() {
  cd "$srcdir/$pkgname"

  sed 's:libpng:libpng15:' -i gtk/Makefile.target
  sed 's:libpng:libpng15:' -i monkey/Makefile.target
  make PREFIX=/usr TARGET=gtk NETSURF_USE_WEBP=YES
}

package() {
  cd "$srcdir/$pkgname"

  make install PREFIX=/usr DESTDIR="$pkgdir"
  install -Dm644 "../$pkgname.png" \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "../$pkgname.desktop" \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 COPYING \
    "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

# vim:set ts=2 sw=2 et:
