# Maintainer: Jerome Leclanche <jerome@leclan.ch>

pkgname=libsysstat
pkgver=0.4.2
pkgrel=1
pkgdesc="Library to query system statistics (net, resource usage, ...)"
arch=("x86_64")
url="http://lxqt.org"
license=("LGPL2.1")
depends=("qt5-base")
makedepends=("lxqt-build-tools")
source=(
	"https://github.com/lxde/libsysstat/releases/download/$pkgver/$pkgname-$pkgver.tar.xz"
	"https://github.com/lxde/libsysstat/releases/download/$pkgver/$pkgname-$pkgver.tar.xz.asc"
)
sha512sums=('9059b9d20edbb7ecd89e722b48c6603b8042030ffad06d3eac167bb518691c2733d314649dc4c42a839d4812c7b65dcd043a13a6bd5b56678da112a8065717df'
            'SKIP')
validpgpkeys=(
	"169704C6FB490C6892C7F23C37E0AF1FDA48F373"  # Jerome Leclanche <jerome@leclan.ch>
	"7C733BA5F585AAD669E4D23A42C9C8D3AF5EA5E3"  # Alf Gaida <agaida@siduction.org>
)

build() {
	mkdir -p build
	cd build
	cmake "$srcdir/$pkgname-$pkgver" \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBDIR=/usr/lib
	make
}

package() {
	cd build
	make DESTDIR="$pkgdir" install
}
