# Maintainer: Felix Yan <felixonmars@archlinux.org>

_hkgname=hi-file-parser
pkgname=haskell-hi-file-parser
pkgver=0.1.1.0
pkgrel=5
pkgdesc="Parser for GHC's hi files"
url="https://github.com/commercialhaskell/stack"
license=('BSD')
arch=('x86_64')
depends=('ghc-libs' 'haskell-rio' 'haskell-vector')
makedepends=('ghc' 'haskell-hspec')
source=(https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz)
sha512sums=('10a9f82f1f88df854f2acb58549c964538a9349f3294472c1a5af1cf85eb94caa648f1e79f9e3a0f3847e7e457ae5df70209c978a78f025d89b24bd216f572d9')

build() {
    cd $_hkgname-$pkgver    
    
    runhaskell Setup configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
        --prefix=/usr --docdir=/usr/share/doc/$pkgname --enable-tests \
        --dynlibdir=/usr/lib --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build $MAKEFLAGS
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

check() {
    cd $_hkgname-$pkgver
    runhaskell Setup test
}

package() {
    cd $_hkgname-$pkgver
    
    install -D -m744 register.sh "$pkgdir"/usr/share/haskell/register/$pkgname.sh
    install -D -m744 unregister.sh "$pkgdir"/usr/share/haskell/unregister/$pkgname.sh
    runhaskell Setup copy --destdir="$pkgdir"
    install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
    rm -f "$pkgdir"/usr/share/doc/$pkgname/LICENSE
}
