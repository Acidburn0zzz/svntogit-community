# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=dephell_discover
pkgname=python-dephell-discover
pkgver=0.2.8
pkgrel=4
pkgdesc="Find project modules and data files (packages and package_data for setup.py)"
arch=('any')
url="https://github.com/dephell/${_pkgname}"
license=('MIT')
depends=('python-attrs')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "https://github.com/dephell/dephell_discover/pull/9/commits/af7fc75c378f52f4c7f423c1ff835a85da844379.patch")
sha256sums=('70347d511074c19a3facb16cd87f98c7077b76f3dfb6f4fc49e03392a81cb97d'
            '59b30f0accbc0be9d242b385af5b7213253b140cea5402a496c519008bf3b64d')
b2sums=('3e739975095e4358ab8931a24fd7949b6b25faaeaeaf94f3a89f9b81d1eb6d0368c391aa440e9d498d5449cd0155a0524a5e7ca62cfa71239bdc83a60264afda'
        'c221bb631bc2669922264396e0cff46aef15a982f52ed8e6444fa994194ff59fab9e1927bffd4c8636e8c51ac2bda6057e64f216ead79df0c3293ae8f75f658e')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # Fix metadata parsing, in python 3.8 ast.Constant is used for all constants
    # https://github.com/dephell/dephell_discover/issues/8
    patch -p1 -i ../af7fc75c378f52f4c7f423c1ff835a85da844379.patch
    # pycache slipped into release tarballs
    find . -name \*.pyc -delete
}

build(){
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python -m pytest
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
