# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Moritz Lipp <mlq@pwmt.org>

_pipname=Flask-AutoIndex
pkgbase=python-flask-autoindex
pkgname=(python2-flask-autoindex python-flask-autoindex)
pkgver=0.6
pkgrel=4
pkgdesc="Flask-AutoIndex generates an index page for your Flask application automatically"
arch=(any)
url="http://packages.python.org/Flask-AutoIndex/"
license=(BSD)
makedepends=(python2-setuptools python-setuptools)
source=("https://pypi.io/packages/source/${_pipname:0:1}/$_pipname/$_pipname-$pkgver.tar.gz"
        autoindex-flask-1.0.patch::"https://github.com/sublee/flask-autoindex/commit/bb842705.patch")
sha256sums=('1a3b1fd9258769ddcbdd3e7abf161811576457b6dcc4e994970e671b560561a5'
            '4c110833e611b794b8b32bac4df9e11efa10ff8634965f9c9d56abaf71754a2e')

prepare() {
  cd $_pipname-$pkgver
    patch -p1 -i ../autoindex-flask-1.0.patch # Fix imports removed in flask 1.0
  cd ..
  cp -R $_pipname-$pkgver python2-$_pipname-$pkgver
}

package_python2-flask-autoindex() {
  depends=(python2-flask-silk python2-future)
  cd python2-$_pipname-$pkgver
  python2 setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname

# Fix conflict with python-flask-autoindex
  mv "$pkgdir"/usr/bin/fai{,2}
}

package_python-flask-autoindex() {
  depends=(python-flask-silk python-future)
  cd $_pipname-$pkgver 
  python setup.py install --root="$pkgdir/" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
