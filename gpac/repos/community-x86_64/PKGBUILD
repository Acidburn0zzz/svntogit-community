# $Id$
# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=gpac
pkgver=3824
pkgrel=2
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
license=('LGPL')
depends=('ffmpeg' 'libjpeg' 'libpng' 'mesa')
makedepends=('jack' 'a52dec' 'freetype2' 'libxv' 'faad2' 'libmad')
optdepends=('jack: for jack support' 'a52dec: for A52 support'
            'faad2: for AAC support' 'libmad: for mp3 support')
options=('!makeflags')
source=(ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.xz)
sha1sums=('bf7039c2585d539475babc7996851627efd7ec59')

# source PKGBUILD && mksource
mksource() {
  [[ -x /usr/bin/svn ]] || (echo "svn not found. Install subversion." && return 1)
  _svnver=$pkgver
  _svntrunk="https://gpac.svn.sourceforge.net/svnroot/gpac/trunk/gpac"
  _svnmod="$pkgname-$pkgver"
  mkdir ${pkgname}-$pkgver
  pushd ${pkgname}-$pkgver
  svn co $_svntrunk --config-dir ./ -r $_svnver $_svnmod
  echo "#define GPAC_SVN_REVISION	\"$_svnver\"" > ${pkgname}-$pkgver/include/gpac/version.h
  find . -depth -type d -name .svn -exec rm -rf {} \;
  tar -cJf ../${pkgname}-$pkgver.tar.xz ${pkgname}-$pkgver/*
  popd
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man --X11-path=/usr --use-js=no
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install install-lib
}
