# Maintainer: Brad Fanella <bradfanella@archlinux.us
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: niQo
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=gpac
pkgver=0.4.5
pkgrel=8
pkgdesc="A multimedia framework based on the MPEG-4 Systems standard"
arch=('i686' 'x86_64')
url="http://gpac.sourceforge.net"
depends=('libxml2' 'wxgtk' 'alsa-lib' 'sdl' 'libmad'
         'faad2' 'xvidcore' 'ffmpeg' 'freeglut')
# 'js'
license=('LGPL')
options=('!makeflags') # Multiple build jobs aren't handled correctly
source=(http://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.gz
        libpng14-infopp-null.patch openjpeg14.patch)
md5sums=('755e8c438a48ebdb13525dd491f5b0d1'
         '14d6483c9eb84434aea68596f243e0ca'
         '1605fc11bd49b9c8220820bb38912021')

build() {
	cd ${srcdir}/${pkgname}
	chmod +x configure
	sed -i 's|--warn-common||' configure
	sed -i 's#osmozilla##g' applications/Makefile
	sed -i 's#"$(prefix)#"$(DESTDIR)$(prefix)#' applications/osmo4_wx/Makefile
	sed -i 's#ldconfig || true##g' Makefile
	
	#FS#14506
	sed -i 's#lib64#lib#g' configure
	patch -Np1 -i ${srcdir}/libpng14-infopp-null.patch
	patch -Np1 -i ${srcdir}/openjpeg14.patch
	
	# Was getting "symbol lookup error: /usr/lib/gpac/gm_x11_out.so: undefined
	# symbol: XvQueryExtension" with our LDFLAGS :\
	export LDFLAGS=${LDFLAGS/,--as-needed/}
	
	./configure --prefix=/usr --mandir=/usr/share/man --use-js=no
	make
}

package() {
	cd ${srcdir}/${pkgname}
	make DESTDIR=${pkgdir} install install-lib
}
