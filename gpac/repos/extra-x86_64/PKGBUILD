# Maintainer: Eric BÃ©langer <eric@archlinux.org>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>

pkgname=gpac
pkgver=1.0.0
pkgrel=1
epoch=1
pkgdesc='A multimedia framework based on the MPEG-4 Systems standard'
arch=(x86_64)
url=https://gpac.wp.imt.fr/
license=(LGPL)
depends=(
  glibc
  openssl
  xz
  zlib
)
makedepends=(git)
provides=(libgpac.so)
_tag=a99c032b7afbc2e0a55d0259fd09b4139f8a7402
source=(git+https://github.com/gpac/gpac.git#tag=${_tag})
sha256sums=(SKIP)

pkgver() {
  cd gpac

  git describe --tags | sed 's/^v//'
}

prepare() {
  cd gpac

  git cherry-pick -n 4710737bed502534499754afd730601c7ffb3d7b
  git cherry-pick -n 1bfe33a9f4f985065efbff00ff700cca9e380be9
  git cherry-pick -n 45633fabccf331405f5c09aa44db86d6962dfd0f
}

build() {
  cd gpac

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --disable-oss-audio \
    --disable-dvb4linux \
    --disable-qjs \
    --enable-pic \
    --use-js=no
  make
}

package() {
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install
  make DESTDIR="${pkgdir}" STRIP=true -C gpac install-lib
}

# vim: ts=2 sw=2 et:
