# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
# Contributor: gardar <aur@gardar.net>
pkgname=python-pynetbox
pkgver=6.1.2
pkgrel=1
pkgdesc="NetBox API client library"
arch=('any')
url="https://github.com/digitalocean/pynetbox"
license=('Apache')
depends=('python' 'python-requests' 'python-six')
makedepends=('python-setuptools')
source=("https://github.com/netbox-community/pynetbox/archive/refs/tags/v6.1.2.tar.gz")
sha256sums=('80743328f51f28827825fb2c9cb1993175da37c88988f02075f6f50e16333e0c')

build() {
	cd "${srcdir}/pynetbox-${pkgver}"
	python setup.py build
}

package() {
	cd "$srcdir/pynetbox-$pkgver"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

