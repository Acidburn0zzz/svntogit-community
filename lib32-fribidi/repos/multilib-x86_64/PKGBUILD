# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

pkgname=lib32-fribidi
pkgver=1.0.7
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32-bit)"
arch=('x86_64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
depends=('lib32-glibc' 'fribidi')
makedepends=('git' 'meson')
_commit=aac3845708969b9c4bc2874edc4950123595b1b4  # tags/v1.0.7
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd fribidi
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}

prepare() {
  cd fribidi
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson fribidi build -D docs=false --libdir=/usr/lib32
  ninja -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -rf "$pkgdir"/usr/{include,share,bin}
}
