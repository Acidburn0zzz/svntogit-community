# $Id$
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Nezmer <Nezmer@gmail.com>

_pkgbasename=fribidi
pkgname=lib32-$_pkgbasename
pkgver=1.0.2
pkgrel=1
pkgdesc="A Free Implementation of the Unicode Bidirectional Algorithm (32-bit)"
arch=('x86_64')
license=('LGPL')
url="https://github.com/fribidi/fribidi/"
depends=('lib32-glibc' $_pkgbasename)
makedepends=('git' 'meson')
_commit=f2c9d50722cb60d0cdec3b1bafba9029770e86b4  # tags/v1.0.2^0
source=("git+https://github.com/fribidi/fribidi#commit=$_commit")
md5sums=('SKIP')

pkgver() {
  cd $_pkgbasename
  git describe --tags | sed -e 's/-/+/g' -e 's/^v//'
}

prepare() {
  cd $_pkgbasename
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  arch-meson $_pkgbasename build -D docs=false --libdir=/usr/lib32
  ninja -C build
}

check() {
  cd build
  meson test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  rm -rf "$pkgdir"/usr/{include,share,bin}
}
