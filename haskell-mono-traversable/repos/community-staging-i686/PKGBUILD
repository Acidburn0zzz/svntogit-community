# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Arch Haskell Team <arch-haskell@haskell.org>

_hkgname=mono-traversable
pkgname=haskell-mono-traversable
pkgver=0.10.0.1
pkgrel=5
pkgdesc="Type classes for mapping, folding, and traversing monomorphic containers"
url="https://github.com/snoyberg/mono-traversable"
license=("MIT")
arch=('i686' 'x86_64')
depends=("ghc=7.10.3" "haskell-comonad" "haskell-dlist" "haskell-dlist-instances"
         "haskell-hashable" "haskell-semigroupoids" "haskell-semigroups"
         "haskell-split" "haskell-text" "haskell-unordered-containers"
         "haskell-vector" "haskell-vector-algorithms" "haskell-vector-instances")
options=('staticlibs')
source=("http://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
install="${pkgname}.install"
sha256sums=('2e25c24ed3cf644cd4818cfb6d4e122cffcac2a375f0edb544b6814f871af45d')

prepare() {
    # I'm afraid this is not a proper workaround...
    # https://github.com/snoyberg/mono-traversable/issues/86
    sed -i '/Coproduct/d' ${_hkgname}-${pkgver}/src/Data/MonoTraversable.hs
}

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/${pkgname}/register.sh"
    install    -m744 unregister.sh "${pkgdir}/usr/share/haskell/${pkgname}/unregister.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
