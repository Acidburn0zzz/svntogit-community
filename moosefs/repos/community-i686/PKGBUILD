# $Id: PKGBUILD 133367 2015-05-13 12:31:10Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>

pkgname=moosefs
pkgver=2.0.83
pkgrel=1
pkgdesc='High performing and fault tolerant distributed file system'
url='http://moosefs.com/'
replaces=('mfs-master' 'mfs-chunkserver' 'mfs-client')
conflicts=('mfs-master' 'mfs-chunkserver' 'mfs-client')
arch=('x86_64' 'i686')
license=('GPL2')
depends=('fuse' 'python2' 'bash' 'xfsprogs')
install="$pkgname.install"
source=("http://ppa.moosefs.com/src/moosefs-${pkgver}-1.tar.gz")
sha256sums=('1d15878396e7e26979cea22ccd8f6c136e132afd378c74519ec1cc865f90198f')

build() {
  cd "moosefs-${pkgver%_*}"

  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \
    --sysconfdir=/etc \
    --localstatedir=/var/lib \
    --with-default-user=mfs \
    --with-default-group=mfs \
    --with-systemdsystemunitdir=/usr/lib/systemd/system \
    --sbindir=/usr/bin
  make
}

package() {
  make DESTDIR="$pkgdir" -C "moosefs-${pkgver%_*}" install
}

# getver: moosefs.com/download/sources.html
# vim:set ts=2 sw=2 et:
