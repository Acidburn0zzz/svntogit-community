# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: codl <codl@aquageek.net>
# Contributor: Megamixman <Megamixman@gmail.com>

pkgname=python2-virtkey
_pkgname=python-virtkey
pkgver=0.60.0
pkgrel=3
pkgdesc="Python extension for emulating keypresses and getting layout information from the X server"
arch=('i686' 'x86_64')
url="https://launchpad.net/python-virtkey"
license=('LGPL')
depends=('python2' 'libxtst' 'gtk2')
replaces=('python-virtkey')
provides=('python-virtkey')
source=(http://launchpad.net/$_pkgname/${pkgver%.*}/$pkgver/+download/$_pkgname-$pkgver.tar.gz)
md5sums=('f4f6776f379bc635a29eac83a14fdde5')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py build
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
