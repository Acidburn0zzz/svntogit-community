# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-text
pkgver=0.3.1
pkgrel=1
pkgdesc='Go text processing support'
arch=('any')
url="https://github.com/golang/text"
license=('BSD')
depends=('go-pie' 'golang-golang-x-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/golang/text/archive/v$pkgver.tar.gz")
sha512sums=('a2cdd676e0d85ce14cd7561e7530cc1d052b4a4bbd850a78cb2bfae84d33f4140d00059bdd70a1791702ba9f9392494c506d2179e7dcebb5938ba980964c50a2')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$srcdir"/build/src/golang.org/x/text
  cd "$srcdir"/build/src/golang.org/x/text
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$pkgdir"/usr/share/gocode/src/golang.org/x/text

  install -Dm644 text-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
