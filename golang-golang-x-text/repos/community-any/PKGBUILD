# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-golang-x-text
pkgver=0.3.2
pkgrel=1
pkgdesc='Go text processing support'
arch=('any')
url="https://github.com/golang/text"
license=('BSD')
depends=('go-pie' 'golang-golang-x-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/golang/text/archive/v$pkgver.tar.gz")
sha512sums=('48156313c3ae4c01ea3a8b790ae829f50f2a53ea9ad7c14509749405da5cb7c98e6e1a2e9f3619e01393935d452b5b5e568b29719cd1f286570272d59508da58')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$srcdir"/build/src/golang.org/x/text
  cd "$srcdir"/build/src/golang.org/x/text
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/golang.org/x
  cp -a "$srcdir"/text-$pkgver "$pkgdir"/usr/share/gocode/src/golang.org/x/text

  install -Dm644 text-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
