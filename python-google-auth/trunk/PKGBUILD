# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-google-auth
pkgname=(python-google-auth python2-google-auth)
pkgver=1.5.0
pkgrel=2
pkgdesc="Google Authentication Library"
url="https://github.com/GoogleCloudPlatform/google-auth-library-python"
license=('Apache')
arch=('any')
makedepends=('ca-certificates' 'python-setuptools' 'python2-setuptools' 'python-pyasn1-modules'
             'python2-pyasn1-modules' 'python-rsa' 'python2-rsa' 'python-six' 'python2-six'
             'python-cachetools' 'python2-cachetools')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-requests' 'python2-requests' 'python-flask' 'python2-flask'
              'python-oauth2client' 'python2-oauth2client' 'python-pytest-localserver'
              'python2-pytest-localserver')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/GoogleCloudPlatform/google-auth-library-python/archive/v$pkgver.tar.gz"
        https://github.com/felixonmars/google-auth-library-python/commit/974f7202f56fb2b18f16f820b53286ca15ac016e.patch)
sha512sums=('3924332a5ffea4974ac65f46aa40912c2ce9306113c52001e208765080c16580d98e8953d0d81a239c8a906584bd7702de04c92fd7c8946803bb68223b43f31b'
            '6c6f66260b79c9c2c2658663b14ea1eccd0675825083f68773bf42ea6edc3b3a3c18e0d4944a516d46baa9119072ed1802dc67418351f15913a0580fb9992211')

prepare() {
  (cd google-auth-library-python-$pkgver; patch -p1 -i ../974f7202f56fb2b18f16f820b53286ca15ac016e.patch)
  # Use system cert
  sed -i -e 's|certifi.where()|"/etc/ssl/certs/ca-certificates.crt"|' \
         -e 's|if certifi is not None:|if True:|' \
         google-auth-library-python-$pkgver/google/auth/transport/urllib3.py
  cp -a google-auth-library-python-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py build

  cd "$srcdir"/google-auth-library-python-$pkgver-py2
  python2 setup.py build
}

check() {
  # Skip the without_certifi tests because we always have the system ca bundle

  cd "$srcdir"/google-auth-library-python-$pkgver
  python setup.py pytest --addopts "tests -k 'not test__make_default_http_without_certfi'"

  cd "$srcdir"/google-auth-library-python-$pkgver-py2
  python2 setup.py pytest --addopts "tests -k 'not test__make_default_http_without_certfi'"
}

package_python-google-auth() {
  depends=('ca-certificates' 'python-pyasn1-modules' 'python-rsa' 'python-six' 'python-cachetools')

  cd google-auth-library-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

package_python2-google-auth() {
  depends=('ca-certificates' 'python2-pyasn1-modules' 'python2-rsa' 'python2-six' 'python2-cachetools')

  cd google-auth-library-python-$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
}
