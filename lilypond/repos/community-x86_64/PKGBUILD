# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Evgeniy Alekseev <arcanis at archlinux dot org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: William Rea <sillywilly@gmail.com>
# Contributor: Robert Emil Berge <filoktetes@linuxophic.org>

pkgname=lilypond
pkgver=2.20.0
pkgrel=4
pkgdesc="Music engraving program, devoted to producing the highest-quality sheet music possible"
arch=('x86_64')
url="https://lilypond.org"
license=('FDL1.3' 'GPL3' 'custom:OFL')
groups=('pro-audio')
depends=('gcc-libs' 'ghostscript' 'glibc' 'guile1.8' 'ttf-dejavu')
# TODO: package extractpdfmark
makedepends=('dblatex' 'fontconfig' 'fontforge' 'freetype2' 'glib2'
'gnu-free-fonts' 'gsfonts' 'imagemagick' 'mftrace' 'netpbm' 'pango' 'python2'
'rsync' 't1utils' 'texi2html' 'texinfo' 'texlive-core' 'tex-gyre-fonts'
'texlive-langcyrillic' 'ttf-bitstream-vera' 'ttf-liberation'
'ttf-linux-libertine' 'zip')
optdepends=('python2: for lilypond-book and other scripts')
options=('emptydirs')
source=("https://lilypond.org/downloads/sources/v${pkgver%.*}/$pkgname-$pkgver.tar.gz"
        "${pkgname}-2.20.0-CVE-2020-17353.patch")
sha512sums=('8c5749576362b8c8acaed9eed50f22fdbf986bbe1733219921e366166d9cb829ffb280bfec936647248ddc48b3441af67a4e9d4023e003fdc7522d913f83928a'
            '99663585ceed5493cc25e34c85f68328254d55822d66767f8384d058218835d24179b938547d303f84b33dae328b2b9734748a1c58186a7f279695d76f5ac2b7')
b2sums=('1bf4aa1db189b6a2c4be9b9f35a0ac913533640cc2ca6327492909cf71218bba7a31ca3c5a84a94746e361e2f985fe1b73e4ad6fbea13927e465f7b7f14bd16a'
        '6a5b7ab61da2a7e96aa54c411784fc7d698afdc3cfded9bfd3e50639c083aa400edf58f5c041a360a36ac418f00c851ca45a56aa2d008baa56d5422c15a42f37')

prepare() {
  cd "$pkgname-$pkgver"
  # fix CVE-2020-17353: FS#67680
  patch -Np1 -i "../${pkgname}-2.20.0-CVE-2020-17353.patch"
  # Remove version constraint on texi2html
  sed -e 's|1.82, 1.82|1.82|g' -i configure.ac
  autoconf --force --verbose
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libfontconfig.so' 'libfreetype.so' 'libglib-2.0.so'
  'libgobject-2.0.so' 'libpangoft2-1.0.so' 'libpango-1.0.so')
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" vimdir="/usr/share/vim/vimfiles" install
  install -vDm 644 LICENSE.OFL -t "${pkgdir}/usr/share/licenses/${pkgname}/"
  install -vDm 644 {AUTHORS,NEWS,README}.txt \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
