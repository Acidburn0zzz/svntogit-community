# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.12.3
pkgrel=1
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('x86_64')
url="https://wiki.gnome.org/Apps/Almanah_Diary"
license=('GPL')
depends=('evolution' 'libcryptui' 'gtkspell3' 'gtksourceview3')
makedepends=('git' 'meson')
_commit=d653556ca3955a1b281031e77ec45a5fbdb7c25c  # tags/0.12.3^0
source=("git+https://gitlab.gnome.org/GNOME/almanah.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^V_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
}

build() {
  arch-meson $pkgname build
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  DESTDIR="$pkgdir" meson install -C build
}
