# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=almanah
pkgver=0.11.1
pkgrel=9
pkgdesc="Small GTK+ application to allow you to keep a diary of your life"
arch=('x86_64')
url="https://wiki.gnome.org/AlmanahDiary"
license=('GPL')
depends=('evolution' 'libcryptui')
makedepends=('intltool' 'python')
source=(https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        0001-Fix-AppStream-metadata-validation.patch)
sha256sums=('00a66b31763fecea7d45a6127494ec997df5d35c8002867be8432f84a7f70ebc'
            'e2601efa189a12dd639ed070ff3fea41454b0e845716ab8a4c84f0adbd2845c5')

prepare() {
  cd $pkgname-$pkgver

  # Fix AppStream metadata validation
  patch -Np1 -i ../0001-Fix-AppStream-metadata-validation.patch
  sed -i 's|${datadir}/appdata|${datadir}/metainfo|' configure
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
