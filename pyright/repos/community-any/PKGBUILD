# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Kaizhao Zhang <zhangkaizhao@gmail.com>

pkgname=pyright
pkgver=1.1.148
pkgrel=1
pkgdesc='Type checker for the Python language'
arch=('any')
url=https://github.com/microsoft/pyright
license=('MIT')
depends=('nodejs>=12')
makedepends=('npm')
source=("$url/archive/$pkgver/$pkgname-$pkgver.tar.gz")
b2sums=('38d55d14819a1d137f05ddc35ffcf4743df7dae7dea991c3b342ebecb0a27ffddca3a3f43510a826a68ed2fadaafa4efc394e6f112d5c651eb4fd585b16a2004')

build() {
  cd $pkgname-$pkgver
  npm ci
  cd packages/$pkgname
  npm run build
}

package() {
  install -d "$pkgdir"/usr/bin
  ln -s ../lib/node_modules/$pkgname/index.js "$pkgdir"/usr/bin/$pkgname
  ln -s ../lib/node_modules/$pkgname/langserver.index.js \
    "$pkgdir"/usr/bin/$pkgname-langserver

  cd $pkgname-$pkgver
  install -d "$pkgdir"/usr/share/doc/$pkgname
  cp -r {CONTRIBUTING,README}.md docs "$pkgdir"/usr/share/doc/$pkgname
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE.txt

  cd packages/$pkgname
  install -d "$pkgdir"/usr/lib/node_modules/$pkgname
  cp -r dist {,langserver.}index.js "$pkgdir"/usr/lib/node_modules/$pkgname
}

# vim:set ts=2 sw=2 et:
