# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-gir-generator
pkgver=1.1.0
pkgrel=2
pkgdesc='Generate static golang bindings for GObject'
arch=('x86_64')
url="https://github.com/linuxdeepin/go-gir-generator"
license=('GPL3')
depends=('gobject-introspection' 'libgudev')
makedepends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-gir-generator/archive/$pkgver.tar.gz"
        SettingsBackendLike.patch launch_uris_as_manager_with_fds.patch)
sha512sums=('7fc7a491f9078cdbde46d2354975d7c742b887bb973f469602023786abf7f9ddb3f512d3dfc2f26db1ddf65804b9094623cecfc862111a34a64d1150a55a9a8c'
            'b36b10c01afb33f7bb054a928d4c5e0a017fac35e38992fe441bfcb9224b3aebd55e77a2f740a7b439221694f103a0edd102bd9bc0ef72a45512514886a90ea8'
            '1923f3282ded1032e0fb0d5bab4d7e7e4d51ef9ce54240f0842f767844655910d2795a5bec75e812e43d97d0a61ea8ef4fbcbdb63f43cc61448ab31fcfc3bce3')

prepare() {
  export GOPATH="$srcdir/go-gir-generator/vender:$srcdir/go-gir-generator"

  cd go-gir-generator-$pkgver
  # Our gobject-introspection is too new
  # https://cr.deepin.io/#/c/16880/
  patch -p1 -i ../SettingsBackendLike.patch
  patch -p1 -i ../launch_uris_as_manager_with_fds.patch
}

build() {
  cd go-gir-generator-$pkgver
  make
}

package() {
  cd go-gir-generator-$pkgver
  make DESTDIR="$pkgdir" install
}
