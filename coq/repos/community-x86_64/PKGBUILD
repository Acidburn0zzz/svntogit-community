# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.3
pkgrel=4
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
depends=('gtk2')
makedepends=('ocaml' 'lablgtk2' 'camlp5-transitional' 'netpbm' 'hevea')
# patches fix FS#21943 - [coq] incompatible with campl5-6 https://bugs.archlinux.org/task/21943
source=("http://coq.inria.fr/distrib/V${pkgver}/files/${pkgname}-${pkgver}.tar.gz"
        'coq-8.3-camlp5-6-compat.patch'
        'coq-8.3-make-3.82-compat.patch')
md5sums=('14b80e6b7075adf1516ab931202be8df'
         'a9320c8c9031f095857aea0484976a9d'
         'fd0403d4552bcfe84981308fd2886e0d')

build() {
  cd ${pkgname}-${pkgver}

  patch -Np1 -i ${srcdir}/coq-8.3-camlp5-6-compat.patch
  patch -Np1 -i ${srcdir}/coq-8.3-make-3.82-compat.patch

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -opt

  make world

  make doc-html
}

package() {
  cd ${pkgname}-${pkgver}

  make COQINSTALLPREFIX=${pkgdir} install

  make COQINSTALLPREFIX=${pkgdir} install-doc-html
}
