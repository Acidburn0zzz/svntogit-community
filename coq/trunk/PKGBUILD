# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.3pl4
pkgrel=1
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('gtk2' 'lablgtk2' 'ocaml')
makedepends=('camlp5-transitional' 'netpbm' 'hevea')
source=("http://coq.inria.fr/distrib/V${pkgver}/files/coq-${pkgver}.tar.gz")
md5sums=('88e2ce021b09eca207e3119d5202a695')

build() {
  cd coq-${pkgver}

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -opt \
    -with-doc yes

  make world

  make doc-html
}

package() {
  cd coq-${pkgver}

  make COQINSTALLPREFIX=${pkgdir} install

  make COQINSTALLPREFIX=${pkgdir} install-doc-html
}
