# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: George Giorgidze <giorgidze@gmail.com>

pkgname=coq
pkgver=8.3pl3
pkgrel=3
pkgdesc='Formal proof management system.'
arch=('i686' 'x86_64')
url='http://coq.inria.fr/'
license=('GPL')
options=('!emptydirs')
depends=('gtk2' 'lablgtk2' 'ocaml')
makedepends=('camlp5-transitional' 'netpbm' 'hevea')
source=("http://coq.inria.fr/distrib/V${pkgver}/files/coq-${pkgver}.tar.gz"
        'camlp5-latest-fix.diff')
md5sums=('37e9a52110a025128667c03fed75f9c2'
         '76c55f67f86a3f4b782bbba02ba8893f')

build() {
  cd coq-${pkgver}

  # fix compilation bug, remove next bump
  # https://coq.inria.fr/bugs/show_bug.cgi?id=2728
  patch -Np0 -i ${srcdir}/camlp5-latest-fix.diff

  ./configure \
    -prefix '/usr' \
    -mandir '/usr/share/man' \
    -opt \
    -with-doc yes

  make world

  make doc-html
}

package() {
  cd coq-${pkgver}

  make COQINSTALLPREFIX=${pkgdir} install

  make COQINSTALLPREFIX=${pkgdir} install-doc-html
}
