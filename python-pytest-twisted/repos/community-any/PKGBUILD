# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-twisted
pkgver=1.13.3
pkgrel=1
pkgdesc='A twisted plugin for py.test'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-twisted'
depends=('python-pytest' 'python-greenlet' 'python-decorator' 'python-twisted')
makedepends=('python-setuptools')
source=("https://github.com/pytest-dev/pytest-twisted/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('60400bc5598c129cfeb25c9fc9bded5e5b6a79079364b2445a276a07119229d94c59ed9c07e34d68f66a067ca68d85fbb423e012b2bac090d2d676948311284b')

build() {
  cd pytest-twisted-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-twisted-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/${site_packages}:$PYTHONPATH" py.test
}

package() {
  cd pytest-twisted-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
