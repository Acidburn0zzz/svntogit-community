# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-twisted
pkgver=1.13
pkgrel=1
pkgdesc='A twisted plugin for py.test'
arch=('any')
license=('BSD')
url='https://github.com/pytest-dev/pytest-twisted'
depends=('python-pytest' 'python-greenlet' 'python-decorator' 'python-twisted')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-twisted/archive/v$pkgver.tar.gz")
sha512sums=('5f99ce39f54d69fdae434c07fc6c0697f7b0006ae73b7d366f1357674bec10420ffa1059ff1bb3b2cad7a04e207d264fd70003505f0a08ec2b6bb4038d4bc397')

build() {
  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd "$srcdir"/pytest-twisted-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  local site_packages=$(python -c "import site; print(site.getsitepackages()[0])")
  PYTHONPATH="$PWD/tmp_install/${site_packages}:$PYTHONPATH" py.test
}

package() {
  cd pytest-twisted-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
