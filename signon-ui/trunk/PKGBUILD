# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.11
pkgrel=1
pkgdesc="UI component that responsible for handling the user interactions which can happen during the login process of an online account"
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-signon-ui"
license=('GPL')
depends=('libaccounts-qt' 'qtwebkit' 'libproxy')
makedepends=('signon')
source=(https://launchpad.net/online-accounts-signon-ui/trunk/$pkgver/+download/$pkgname-$pkgver.tar.bz2)
md5sums=('b0359c61523f7b0df241bd3c21dbb2b2')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Do not install tests
  echo 'INSTALLS =' >>tests/unit/unit.pro

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
