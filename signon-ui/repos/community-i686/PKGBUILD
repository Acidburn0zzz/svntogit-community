# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=signon-ui
pkgver=0.12
pkgrel=1
pkgdesc="UI component that responsible for handling the user interactions which can happen during the login process of an online account"
arch=('i686' 'x86_64')
url="https://launchpad.net/online-accounts-signon-ui"
license=('GPL')
depends=('libaccounts-qt' 'qtwebkit' 'libproxy')
makedepends=('signon' 'libnotify')
source=(https://launchpad.net/online-accounts-signon-ui/13.04/$pkgver/+download/$pkgname-$pkgver.tar.bz2
        Makefile)
md5sums=('dca78c25d2d5303a1479403fec334e27'
         'c73ecd27c76a9060862512b7678ae4f1')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Do not install tests
  echo 'INSTALLS =' >>tests/unit/unit.pro

  # Fix build
  cp "$srcdir/Makefile" po/Makefile

  qmake PREFIX=/usr LIBDIR=/usr/lib
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make INSTALL_ROOT="$pkgdir" install
}
