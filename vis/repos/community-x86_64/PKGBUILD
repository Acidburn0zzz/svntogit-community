# Maintainer: Christian Hesse <mail@eworm.de>

pkgname=vis
_tag=0.5
_commit='30c6ceb2447be5b8b73958271972ba98457dc0b7'
pkgver=0.5.r167.g30c6ceb
pkgrel=1
pkgdesc='modern, legacy free, simple yet efficient vim-like editor'
arch=('x86_64')
url='https://github.com/martanne/vis#vis-a-vim-like-text-editor'
depends=('acl' 'bash' 'ncurses' 'libtermkey' 'lua' 'lua-lpeg' 'tre' 'file')
makedepends=('git')
checkdepends=('vim')
license=('custom:ISC')
backup=('etc/vis/visrc.lua')
source=('git://github.com/martanne/vis.git?commit=${_commit}'
        'git://github.com/martanne/vis-test.git')
sha256sums=('SKIP'
            'SKIP')

prepare() {
	cd vis/

	git config --file=.gitmodules submodule.test.url ../vis-test/
	git update-index --assume-unchanged .gitmodules
	git submodule init
	git submodule update

	# revert: test/vis: use C.UTF-8 locale
	git -C test revert -n 92ee4fc43fd750246bbc1529082c0c0c8d9f233e
}

pkgver() {
	cd vis/

	printf '%s.r%s.g%s' "${_tag}" "$(git rev-list --count v${_tag}..)" "$(git rev-parse --short HEAD)"
}

build() {
	cd vis/

	./configure \
		--prefix=/usr

	make
}

check() {
	cd vis/

	make -C test/
}

package() {
	cd vis/

	make DESTDIR="${pkgdir}" install

	install -D -m0644 lua/visrc.lua "${pkgdir}"/etc/vis/visrc.lua

	install -D -m0644 'LICENSE' "${pkgdir}/usr/share/licenses/vis/LICENSE"
}

