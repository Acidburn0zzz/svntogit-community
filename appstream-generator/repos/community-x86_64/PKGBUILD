# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.6.6
pkgrel=2
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb)
makedepends=(dmd meson ninja mustache-d curl gir-to-d gobject-introspection)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        https://github.com/ximion/appstream-generator/commit/3afc47a9.patch
        https://github.com/ximion/appstream-generator/commit/412460dc.patch)
sha256sums=('585b87e425d1891a09ce5dcf698d7c86c2b4a401d510ae0e345df8fff855927b'
            '45f22f647e2070a8582edd6baa07978369fb1462b539c9c6603f1a039f98f630'
            'a16ae880d6afaf587142033513df9b92465cb09ad4acbb15e858b7153275cfe6')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../3afc47a9.patch # Fix build with DMD 2.076
  patch -p1 -i ../412460dc.patch # Fix exceptions when writing archive
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload_js=false --prefix=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
