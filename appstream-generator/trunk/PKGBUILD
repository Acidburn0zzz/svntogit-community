# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.6.3
pkgrel=2
pkgdesc="A fast AppStream metadata generator"
arch=(i686 x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb liblphobos)
makedepends=(ldc meson ninja mustache-d curl)
source=("https://github.com/ximion/appstream-generator/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        appstream-generator-ldc-1.2.patch::"https://github.com/ximion/appstream-generator/commit/07e46bae.patch"
        appstream-generator-buildpath.patch)
sha256sums=('927ac57571b4e033801635ef9446de1125c9fda00daa39000b0c2392465e34bb'
            '296706113ad8055daa317709079ba39d5a27a832561b077c210e4263db62e133'
            '28ecd93c873956493829b0afa3f81d47239428ef765359f1871d1acb3ab015f9')

prepare() {
  mkdir -p build

# Fix build with LDC 1.2
  cd $pkgname-$pkgver
  patch -p1 -i ../appstream-generator-ldc-1.2.patch
  patch -p1 -i ../appstream-generator-buildpath.patch
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload_js=false --prefix=/usr
  ninja -j1
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
