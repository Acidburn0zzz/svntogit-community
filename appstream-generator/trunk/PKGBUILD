# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.8.2
pkgrel=6
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg optipng glibd)
makedepends=(ldc meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        appstream-generator-segfault.patch::"https://github.com/ximion/appstream-generator/commit/c4cfd008.patch")
sha256sums=('41a6563dde303adcd60f28fbd89f14eb2a4083cec8f1021c22409c769088fdc2'
            'b80864eb21a238d74be157a76913627c98f2b934ffc3d96615486d2a13a2f9e7')

prepare() {
  patch -d $pkgname-$pkgver -p1 -i ../appstream-generator-segfault.patch  
}

build() {
  meson build $pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja install -C build
}
