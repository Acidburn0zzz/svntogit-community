# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.7.3
pkgrel=1
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb optipng d-stdx-allocator)
makedepends=(dmd meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl d-containers)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        asgen-dmd-2.081.patch::https://patch-diff.githubusercontent.com/raw/ximion/appstream-generator/pull/63.patch)
sha256sums=('85c90a989f60fd33b23c540b670e69541da58fb76ece7337dda51560f220b5a9'
            '0676e0f36aa80cc6f1d672788419f660a0458dffab9f4c44c55ff329363952af')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  patch -p1 -i ../asgen-dmd-2.081.patch # Fix build with phobos 2.081
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
