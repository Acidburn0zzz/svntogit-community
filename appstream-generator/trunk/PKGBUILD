# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.7.4
pkgrel=8
pkgdesc="A fast AppStream metadata generator"
arch=(x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream libarchive librsvg lmdb optipng d-stdx-allocator glibd d-containers)
makedepends=(ldc meson ninja mustache-d curl gir-to-d gobject-introspection docbook-xsl)
source=($pkgname-$pkgver.tar.gz::"https://github.com/ximion/appstream-generator/archive/v$pkgver.tar.gz"
        0001-Load-components-for-Arch-Linux.patch)
sha256sums=('b214273b9b9525170c8c6fe4e8dca8334aab47ad7cc93fa049c16464a5d902e5'
            '8619c8dce6a10538e607231b53f212b26048b09b0d3ea7a5fa618e5e01eecfed')

prepare() {
  mkdir -p build

  cd $pkgname-$pkgver
  # Load components for Arch Linux
  # https://github.com/ximion/appstream-generator/pull/69
  patch -Np1 -i ../0001-Load-components-for-Arch-Linux.patch
}

build() {
  cd build
  meson ../$pkgname-$pkgver -Ddownload-js=false --prefix=/usr
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}
