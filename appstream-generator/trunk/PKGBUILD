# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=appstream-generator
pkgver=0.5.0
pkgrel=1
pkgdesc="A fast AppStream metadata generator"
arch=(i686 x86_64)
url="https://github.com/ximion/appstream-generator"
license=(GPL3)
depends=(appstream curl libarchive librsvg lmdb)
makedepends=(gdc dub)
source=("https://github.com/ximion/appstream-generator/releases/download/v$pkgver/$pkgname-$pkgver.tar.xz"
        "https://github.com/ximion/d-bindings/archive/3a31df3da5977cfabb7cd076e02ab83b525bbf34.zip"
        asgen-fix-i686-build.patch::"https://github.com/ximion/appstream-generator/commit/3c0bdb95.patch"
        asgen-fix-i686-build-2.patch::"https://github.com/ximion/appstream-generator/commit/608550b4.patch")
sha256sums=('446b33752a65e4b2206e69848a4ad8c062cf4d5ff4af908253754651368eff39'
            '786997e7c9693657c51591875a94f039976e005f7c12ed33f777de73393a3072'
            '8bf027d99afe1d19fee62508394a873ebe0d96e635c1ed9666edc4e42cd88487'
            'cb99014886504ca6d08eae660bc0afcb89545d1512b8c75853e2eb95bd64aafd')

prepare() {
  cd $pkgname-$pkgver
# Fix i686 build
  patch -p1 -i ../asgen-fix-i686-build.patch
  patch -p1 -i ../asgen-fix-i686-build-2.patch
}

build() {
  cd $pkgname-$pkgver
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
