# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-cloudflare
pkgver=2.8.14
pkgrel=1
pkgdesc='Python wrapper for the Cloudflare v4 API'
arch=('any')
license=('MIT')
url='https://github.com/cloudflare/python-cloudflare'
depends=('python-requests' 'python-future' 'python-yaml' 'python-jsonlines' 'python-beautifulsoup4')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=("$pkgname-$pkgver.tar.gz::https://github.com/cloudflare/python-cloudflare/archive/$pkgver.tar.gz")
sha512sums=('a5be936bed97023cae88db8c2012194632d6ea4ec3b0f3d39eca9e188092078e228bbf95f9f3c40e33310fefe60ad5035c449a406b215d9a8ff5366a49e6b46a')

build() {
  cd python-cloudflare-$pkgver
  python setup.py build
}

check() {
  cd python-cloudflare-$pkgver
  PYTHONPATH="$PWD/build/lib" pytest tests/test1.py
}

package() {
  cd python-cloudflare-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  rm -r "$pkgdir"/usr/lib/python3.9/site-packages/examples
}

# vim:set ts=2 sw=2 et:
