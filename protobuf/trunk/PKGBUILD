# Maintainer: Thomas S Hatch <thatch45@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=protobuf
pkgname=('protobuf' 'protobuf-python')
pkgver=2.4.0a
pkgrel=1
pkgdesc="A way of encoding structured data in an efficient yet extensible format"
arch=('i686' 'x86_64')
url="http://code.google.com/p/protobuf/"
license=('APACHE')
depends=('gcc-libs' 'zlib')
options=(!libtool)
source=(http://$pkgname.googlecode.com/files/$pkgname-$pkgver.tar.bz2)
md5sums=('61df3f63ec284fc6f57a68c67e4918c6')

build() {
	cd $srcdir/$pkgname-$pkgver
	./autogen.sh
	./configure --prefix=/usr --disable-static
}

package_protobuf() {
	cd $srcdir/$pkgname-$pkgver
	make DESTDIR=$pkgdir install
}

package_protobuf-python() {
	pkgdesc="protobuf python API"
	arch=('any')
	depends=("python2" "protobuf=${pkgver}")
	makedepends=('setuptools')

	cd $srcdir/protobuf-$pkgver/python
	python2 setup.py install --prefix=/usr --root $pkgdir
}
