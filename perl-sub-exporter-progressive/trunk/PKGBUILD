# $Id$
# Maintainer: Jonathan Steel <jsteel@aur.archlinux.org>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname='perl-sub-exporter-progressive'
_pkgname='Sub-Exporter-Progressive'
pkgver='0.001010'
pkgrel='1'
pkgdesc='Only use Sub::Exporter if you need it'
arch=('any')
url='http://search.cpan.org/dist/Sub-Exporter-Progressive'
license=('PerlArtistic' 'GPL')
options=('!emptydirs')
source=(http://search.cpan.org/CPAN/authors/id/F/FR/FREW/$_pkgname-$pkgver.tar.gz)
md5sums=('0350d9e12549112ae1f618cbff3ecbd9')

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor"                       \
      PERL_MB_OPT="--installdirs vendor"                     \
      MODULEBUILDRC=/dev/null

    cd "$srcdir"/$_pkgname-$pkgver

    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver

  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    make test
  )
}

package() {
  cd "$srcdir"/$_pkgname-$pkgver

  make DESTDIR="$pkgdir"/ install

  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}
