# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=giac
_pkgver=1.7.0-7
pkgver=${_pkgver//-/.}
pkgrel=1
pkgdesc='A free computer algebra system'
arch=(x86_64)
url='http://www-fourier.ujf-grenoble.fr/~parisse/giac.html'
license=(GPL3)
depends=(fltk mpfi gsl pari ntl lapack libpng curl glpk libao gmp-ecm libsamplerate)
makedepends=(python libjpeg texlive-core texlive-science hevea nauty)
optdepends=('perl: for pgiac')
replaces=(libgiac xcas)
provides=(libgiac xcas)
source=(http://www-fourier.ujf-grenoble.fr/~parisse/debian/dists/stable/main/source/${pkgname}_$_pkgver.tar.gz
        giac-test-pari-2.11.patch)
sha256sums=('101d446ffee4b677af8a3cd6f66a650e3fa60cc603937bb76642265a5bf7cd34'
            'c5f091986f0a0807fc1ae5a3b7454132816e9bc3ed64411f0a7bdf90a8c8e624')

prepare() {
  cd $pkgname-${pkgver%.*}
  patch -p1 < ../giac-test-pari-2.11.patch # Fix tests with pari 2.11
}

build() {
  cd $pkgname-${pkgver%.*}
  CXXFLAGS+=" -std=c++14" # workaround build with GCC 11
  ./configure --prefix=/usr
  make
}

check() {
  cd $pkgname-${pkgver%.*}

  make check
}

package() {
  cd $pkgbase-${pkgver%.*}
  make DESTDIR="$pkgdir" install
}
