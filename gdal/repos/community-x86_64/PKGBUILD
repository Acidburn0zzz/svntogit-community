# $Id$
# Maintainer: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gdal
pkgver=1.8.0
pkgrel=1
pkgdesc="A translator library for raster geospatial data formats"
arch=('i686' 'x86_64')
url="http://www.gdal.org/"
license=('custom')
depends=('curl' 'geos' 'giflib' 'hdf5' 'libgeotiff' 'libjpeg' 'libpng' 'libtiff' 'netcdf'  'python2' 'python2-numpy' 'cfitsio')
makedepends=('mysql' 'sqlite3' 'postgresql')
optdepends=('mysql: mysql database support' 
            'sqlite3: sqlite3 database support'
            'postgresql: postgresql database support')
options=('!libtool' '!makeflags')
changelog=$pkgname.changelog
source=(http://download.osgeo.org/${pkgname}/${pkgname}-${pkgver}.tar.gz \
        gdal-1.5.1-python-install.patch) 
sha256sums=('515f0316cd17281a09a147914b47b28bbc4855cdb86d41d12cb8716d1721e435'
            '55a0a961b2d1caddf80f18b6763a96690b0b6443fbd5a0c89e29503ded3bcea6')

build() {
  export CFLAGS="$CFLAGS -fno-strict-aliasing"

  cd ${srcdir}/$pkgname-$pkgver
  patch -Np0 -i ${srcdir}/gdal-1.5.1-python-install.patch

# python2 fixes
  sed -i 's_python python1.5_python2 python python1.5_' configure
  for file in swig/python/{,osgeo/,samples/,scripts/}*.py; do
      sed -i 's_#!/usr/bin/env python_#!/usr/bin/env python2_' $file
  done

  ./configure --prefix=/usr --with-netcdf --with-libtiff --with-sqlite3 \
              --with-geotiff --with-mysql --with-python --without-libtool --with-curl

# workaround for bug #13646
  sed -i 's/PY_HAVE_SETUPTOOLS=1/PY_HAVE_SETUPTOOLS=/g' ./GDALmake.opt
  sed -i 's/EXE_DEP_LIBS/KILL_EXE_DEP_LIBS/' apps/GNUmakefile
  install -d ${pkgdir}/usr/lib/python2.7/site-packages/

  make
}

package () {
  cd ${srcdir}/$pkgname-$pkgver

  make DESTDIR=${pkgdir} install

# install license
  install -D -m644 LICENSE.TXT ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE

#FS15477 clean up junks
  rm -f ${pkgdir}/usr/bin/*.dox
}
