# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Cl√©ment DEMOULINS <clement@archivel.fr>
pkgname=texstudio
pkgver=2.12.0
pkgrel=2
pkgdesc="Integrated writing environment for creating LaTeX documents"
arch=('i686' 'x86_64')
url="http://texstudio.sourceforge.net/"
license=('GPL')
depends=('poppler-qt5' 'qt5-svg' 'qt5-script' 'libxkbcommon-x11')
makedepends=('qt5-tools' 'mercurial')
optdepends=('evince: pdf reader'
            'okular: alternate pdf reader')
replaces=('texmakerx')
source=("hg+http://hg.code.sf.net/p/texstudio/hg#tag=${pkgver}"
        "fs52529.patch")
sha512sums=('SKIP'
            '562f49ddab6e39ac0a05dd67a2eb14e6ce47af44cfbc7b850edb6e4189b8c2ddebb6c4c118b4e8faeb0b49c3180d8cc24f5eb45b421f6acac996fd332a58f8d7')

prepare() {
    cd hg
    patch -Np1 < "$srcdir"/fs52529.patch
}

build() {
    cd hg
    qmake-qt5 CONFIG-="debug" texstudio.pro
    make
}

package() {
    cd hg
    make INSTALL_ROOT="$pkgdir" install
}
