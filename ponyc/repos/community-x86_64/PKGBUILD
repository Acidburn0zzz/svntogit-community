# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ponyc
pkgver=0.32.0
pkgrel=1
pkgdesc='An actor model, capabilities, high performance programming language'
url='https://ponylang.org/'
arch=('x86_64')
license=('BSD')
depends=('llvm7-libs')
makedepends=('llvm7')
checkdepends=('pcre2')
options=('!strip')
optdepends=('pcre2: Needed for the regex package')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ponylang/ponyc/archive/$pkgver.tar.gz")
sha512sums=('16fb61d952017e9c40dd2683fd7ca78b2da1cabbf813f16374b8ab080e28c8c0cd8fd284b9eacd8e05e828dbf1ab647d4d1970be5c7aafd8d2a7e8162e768bf0')

prepare() {
  cd ponyc-$pkgver
  sed -i 's/-Werror //' Makefile
}

build() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0

  mkdir docs
  build/release/ponyc packages/stdlib -rexpr -g -o docs
}

check() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0 test
}

package() {
  cd ponyc-$pkgver
  make config=release prefix=/usr arch=x86-64 verbose= default_pic=true default_ssl=openssl_1.1.0 ponydir=/usr/lib/pony/"$pkgver" DESTDIR="$pkgdir" install

  # Ponyc is designed to read its standard packages from the same directory.
  install -dm755 "$pkgdir"/usr/bin
  ln -sf /usr/lib/pony/"$pkgver"/bin/ponyc "$pkgdir"/usr/bin

  # Let's include the examples as documentation is scarce.
  install -dm755 "$pkgdir"/usr/share/doc/pony
  cp -dr --no-preserve=ownership examples "$pkgdir"/usr/share/doc/pony

  cp -dr docs/* "$pkgdir"/usr/share/doc/pony

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/LICENSE
}
