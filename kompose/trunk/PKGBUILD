# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=kompose
pkgver=1.21.0
pkgrel=1
pkgdesc="Docker compose to Kubernetes transformation tool"
arch=('x86_64')
url="https://github.com/kubernetes/kompose"
license=('Apache')
makedepends=('go' 'git' 'jq') # git binary is needed for test scripts
depends=('glibc')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/kubernetes/kompose/archive/v${pkgver}.tar.gz")
sha512sums=('96a6a6335d1846c945410b9a7c2e580f736ac2f9730a68f59cdce8f699a7e83910ba21116de8a9640d550c03c7156d684457421597d0d9bcd63067e6cbb03e81')

prepare() {
  export GOPATH="${srcdir}"
  mkdir -p src/github.com/kubernetes "${GOPATH}/bin"
  mv "${pkgname}-${pkgver}" "src/github.com/kubernetes/${pkgname}"
  export PACKAGE_ROOT="${GOPATH}/src/github.com/kubernetes/${pkgname}"
}

build() {
  cd "${PACKAGE_ROOT}"
  export CGO_ENABLED=0
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"
  export GOLDFLAGS="-linkmode=external"
  go build -v .
  chmod +x ./kompose
}

# tests are failing right now due to a bug in script/cmd/cmd_test.go:28
#check() {
#  cd "${PACKAGE_ROOT}"
#  go test -v ./...
#}

package() {
  cd "${PACKAGE_ROOT}"
  install -Dsm755 ./kompose "${pkgdir}/usr/bin/kompose"
}
