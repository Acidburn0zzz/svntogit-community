# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=1.5.2
pkgrel=1
pkgdesc='Alternate menu for Xfce4'
arch=('x86_64' 'i686')
url="http://gottcode.org/xfce4-whiskermenu-plugin/"
groups=('xfce4-goodies')
license=('GPL2')
depends=('xfce4-panel')
makedepends=('cmake')
install="$pkgname.install"
source=("http://gottcode.org/$pkgname/$pkgname-$pkgver.tar.bz2")
source=("http://mirror.netcologne.de/xfce/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2")
sha256sums=('307cbfa532a28c5c753409aba0330c2da010047b8fc2d669e06f12bf17712e38')

build() {
  cd "$pkgname-$pkgver"

  cmake \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib
  make
}

package() {
  make -C "$pkgname-$pkgver" install DESTDIR="$pkgdir"
}

# vim:set ts=2 sw=2 et:
