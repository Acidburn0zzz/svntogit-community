# $Id: PKGBUILD 123401 2014-12-03 12:28:05Z arodseth $
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Graeme Gott <graeme@gottcode.org>

pkgname=xfce4-whiskermenu-plugin
pkgver=1.6.0
pkgrel=1
pkgdesc='Alternate menu for Xfce4'
arch=('x86_64' 'i686')
url='http://gottcode.org/xfce4-whiskermenu-plugin/'
groups=('xfce4-goodies')
license=('GPL2')
depends=('xfce4-panel')
makedepends=('cmake' 'ninja')
#source=("http://gottcode.org/$pkgname/$pkgname-$pkgver.tar.bz2")
source=("http://mirror.netcologne.de/xfce/src/panel-plugins/xfce4-whiskermenu-plugin/${pkgver%.*}/xfce4-whiskermenu-plugin-$pkgver.tar.bz2")
sha256sums=('7318c5e00a92671b426f546f0f7d9dba618b8ad447f9f266dc079560c8d602b2')

build() {
  mkdir -p build
  cd build
  cmake "$srcdir/$pkgname-$pkgver" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -GNinja
  ninja
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

# vim:set ts=2 sw=2 et:
