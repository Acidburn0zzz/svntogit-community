# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=lv2lint
pkgver=0.16.0
pkgrel=1
pkgdesc="Check whether a given LV2 plugin is up to the specification"
arch=(x86_64)
url="https://open-music-kontrollers.ch/lv2/lv2lint/"
license=(Artistic2.0)
depends=(glibc libelf)
makedepends=(curl lilv lv2 meson)
options=(debug)
source=(https://git.open-music-kontrollers.ch/lv2/$pkgname/snapshot/$pkgname-$pkgver.tar.xz{,.asc})
sha512sums=('987c4ef075d05fe26412877e3a0fabd4f83f6b1ea88e876da906b8b4239c8c55a57754a951b79ef9d0d835d882189696fd2bd858fede3e0c37b2d912dd8f6e2c'
            'SKIP')
b2sums=('dbe9dd6809273b27460769c5a7ebb77b6d5ec93a5ced7f5c3a3f6edb56daac975647eaab92cd466df3f700ae502f4e2585044942b20e6c2f1dd440cd6f397d36'
        'SKIP')
validpgpkeys=('5AE26CEBBF49705C19FB273A30258F99258CB76A') # Hanspeter Portner <dev@open-music-kontrollers.ch>

build() {
  arch-meson -Donline-tests=enabled -Delf-tests=enabled build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  depends+=(libcurl.so liblilv-0.so)

  DESTDIR="$pkgdir" ninja -C build install
  install -vDm 644 $pkgname-$pkgver/{ChangeLog,README.md,TODO} -t "$pkgdir/usr/share/doc/$pkgname/"
}
