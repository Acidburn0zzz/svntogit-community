# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=10.1.0
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('f8059ed79854488ed7b5f289bfa77d1337521bea5080833900b9186d33f9c26fba81c28cf65639eeddadcdb5e23b8a61edc840479dc8f56fcef04fc26c8fbc8d')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # --locked won't work until https://github.com/XAMPPRocky/tokei/issues/360 is fixed.
    cargo build --release --features all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
