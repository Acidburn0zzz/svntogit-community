# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=11.0.0
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('630eff2fb645c6e8c3b66014dd5787b0c0f7934604c77b093cf0d6070200d553336522db1bd15707a5551a0372266a94593d4756995ad848d0c856a4b49b35e9')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cargo build --release --locked --features all
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cargo test --release --locked --features all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
