# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=11.2.1
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/XAMPPRocky/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/XAMPPRocky/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('32782dca38f5a66d2b6c1c19789a3ebe50061065a2762cabd364931321a0e7cf2cb41d66a3767b68b3a774d6777717ba1d8cd630a0b77f8579b20899b90b5159')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo build --release --locked --features all
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  cargo test --release --locked --features all
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

  install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
  install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
