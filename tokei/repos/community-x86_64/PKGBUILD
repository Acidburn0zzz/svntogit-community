# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>
pkgname=tokei
pkgver=11.1.0
pkgrel=1
pkgdesc='A blazingly fast CLOC (Count Lines Of Code) program'
arch=('x86_64')
url="https://github.com/Aaronepower/tokei"
license=('MIT' 'Apache')
depends=('gcc-libs')
makedepends=('rust' 'cargo')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Aaronepower/tokei/archive/v${pkgver}.tar.gz")
sha512sums=('29c0c44aa1d870d21c3810e4dbd876d07c6565bad5e4d92fe4856f9bf2ae5f5dd16d27e3a626efb3292985dc79f23ec6d21dd85cf1e60ea2ac6c001c684085e4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cargo build --release --locked --features all
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    cargo test --release --locked --features all
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -Dm755 target/release/tokei "${pkgdir}/usr/bin/tokei"

    install -Dm644 LICENCE-MIT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-MIT"
    install -Dm644 LICENCE-APACHE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE-APACHE"
}
