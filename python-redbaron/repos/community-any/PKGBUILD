# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-redbaron
pkgver=0.7.1
pkgrel=1
pkgdesc="Abstraction on top of baron, a FST for python to make writing refactoring code a realistic task"
url="https://github.com/PyCQA/redbaron"
license=('LGPL')
arch=('any')
depends=('python-baron')
makedepends=('python-setuptools' 'python-pypandoc')
checkdepends=('python-pytest-runner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/PyCQA/redbaron/archive/$pkgver.tar.gz")
sha512sums=('1de89c9127eb9ae3dc0c9ad972941018fad6907e4df3b8c4676be2c6d7809e7b2e81ae657f44f92e66ed81a2f70528665f13a1fad054af1781cf48affb0856f5')

build() {
  cd redbaron-$pkgver
  python setup.py build
}

check() {
  cd redbaron-$pkgver
  python setup.py pytest --addopts tests
}

package() {
  cd redbaron-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
