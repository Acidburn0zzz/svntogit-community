# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>
# Contributor: Tim Meusel <tim (at) bastelfreak (dot) de>

pkgname=puppet
pkgver=6.16.0
pkgrel=1
pkgdesc="Server automation framework and application"
arch=('any')
url="https://puppet.com/products/open-source-projects"
license=('Apache')
depends=('ruby' 'facter' 'hiera' 'ruby-semantic_puppet' 'ruby-augeas'
'ruby-shadow' 'ruby-concurrent' 'ruby-multi_json' 'ruby-puppet-resource_api'
'ruby-httpclient' 'ruby-deep_merge' 'ruby-sync')
makedepends=('git' 'ruby-rspec')
checkdepends=('ruby-rake' 'ruby-rspec' 'ruby-yard')
backup=('etc/puppetlabs/puppet/auth.conf'
        'etc/puppetlabs/puppet/puppet.conf'
        'etc/puppetlabs/puppet/hiera.yaml')
install="$pkgname.install"

_augeas_commit=416e8fe9df9d467289e2923c083367b04e7d8bd1
_cron_commit=4aa7bdba01493007b0d485b7ed78aee5ff347dca
_host_commit=34dca65fe54ba08bd764691e2619371c213df70b
_mount_commit=1e56e2588058551360269472823bf3426c5e1900
_selinux_commit=cfa0d78156c507f1e95a4fbd91bd5a7b0c2a2ddc
_sshkeys_commit=d28c73de96c5207409e1a1ed858399a643cca84f
_yumrepo_commit=34a1559b2b9e5455efe39fae8c96ad558977771d
_zfs_commit=bd48971776fec1b01ae4ee401f13b06bd946107d
_zone_commit=7880999cdcf15b9a3594b78a06e47399c78baba1

source=("https://github.com/puppetlabs/puppet/archive/${pkgver}/puppet-${pkgver}.tar.gz"
        "augeas_core::git+https://github.com/puppetlabs/puppetlabs-augeas_core#commit=${_augeas_commit}"
        "cron_core::git+https://github.com/puppetlabs/puppetlabs-cron_core#commit=${_cron_commit}"
        "host_core::git+https://github.com/puppetlabs/puppetlabs-host_core#commit=${_host_commit}"
        "mount_core::git+https://github.com/puppetlabs/puppetlabs-mount_core#commit=${_mount_commit}"
        "selinux_core::git+https://github.com/puppetlabs/puppetlabs-selinux_core#commit=${_selinux_commit}"
        "sshkeys_core::git+https://github.com/puppetlabs/puppetlabs-sshkeys_core#commit=${_sshkeys_commit}"
        "yumrepo_core::git+https://github.com/puppetlabs/puppetlabs-yumrepo_core#commit=${_yumrepo_commit}"
        "zfs_core::git+https://github.com/puppetlabs/puppetlabs-zfs_core#commit=${_zfs_commit}"
        "zone_core::git+https://github.com/puppetlabs/puppetlabs-zone_core#commit=${_zone_commit}"
        "ruby27_warnings.patch::https://github.com/puppetlabs/puppet/commit/6af09225b3b962547a2564a9d34ccd6832e60558.patch"
        "ruby27_warnings2.patch::https://github.com/puppetlabs/puppet/commit/88229422dd1c2deae7763711025035f70dc00127.patch"
        "ruby27_warnings3.patch::https://github.com/puppetlabs/puppet/commit/6826af3e103347629e21e0536f3d941b0dfe06c7.patch"
        "ruby27_warnings4.patch::https://github.com/puppetlabs/puppet/commit/65ab6ea40e4285f54d1c5f10260c020337dac0df.patch"
        )
sha512sums=('42f0ea7ac1c04d63d036b318e6e411320ea054e4a2ecc3ae435d267baa75e9c196c0c7dd1f85f09fd713243ffb79bed64ce2cc8c2f553f4001a75a3172096e09'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            '266326100b663eb44416b4a4597fd4d00829952d146ddbf444145b25d877a3099642fc20c21e45f98acb585d73b971fe3aca7cb57512bdf8071fb17be8c94d68'
            '739899a1eeef0c4dafe0f899469c8e76eb92d6899184d49c3882596244e9b7155aaae4e17ba0f94d5eedeb6e5bc5493118aad7ffd6ab799b067978cc8c3b88eb'
            'e99b1bb8d03eba8ac84554f657599fa46a96c7a2997cad6062e163b5cbe0d2cd365badd8455ffad9c5add76eeda5508623b23c0e5482d9a64958ab6dfc03a2ab'
            '756d5236180c3045b747fcd4ca8207673b232341a7d6c6d10a4d47d6d22d2422e4b27e5a9ab01ea0b39ea5b3a804123a4bd738cb5b38b97e35168c8c195aa934')
conflicts=('puppet5')
provides=('puppet')

prepare() {
  cd "${pkgname}-${pkgver}"

  # FS#45044: fix file location
  sed -i 's_/opt/puppetlabs/puppet_/usr_' ext/systemd/puppet.service

  patch -p1 < ../ruby27_warnings.patch
  patch -p1 < ../ruby27_warnings2.patch
  patch -p1 < ../ruby27_warnings3.patch
  patch -p1 < ../ruby27_warnings4.patch
}

check() {
  cd "${pkgname}-${pkgver}"
  # This breaks right now due to missing dependencies
  #rake --tasks              # show all tasks
  #rake commits             # requires a git repo
  #rake warnings            # requires a git repo
  #rake spec                # run tests single threaded
  #rake parallel:spec[8]
}

package() {
  cd $pkgname-$pkgver

  # generate manpages | this breaks right now..
  #rake gen_manpages

  ruby install.rb --destdir="$pkgdir" --sitelibdir="$( ruby -e \
    'puts RbConfig::CONFIG["vendorlibdir"]' )"

  install -d "$pkgdir"/usr/lib/{sysusers.d,tmpfiles.d}

  echo 'u puppet 52 "Puppet" /var/lib/puppet' > \
    "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  echo "d /run/puppetlabs 0755 $pkgname $pkgname -" > \
    "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  # systemd
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/systemd/$pkgname.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgname.service

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Match permissions that puppet sets when running
  install -dm775 "$pkgdir"/var/lib/puppet
  chmod 750 "$pkgdir"/var/log/puppetlabs/puppet

  rm -r "$pkgdir"/var/run

  cd ..
  install --directory "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  mv *_core "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  # Search for unwanted directories and files and delete them
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type d -name .git -or -name spec -exec rm -rf {} +
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -name '.*' -exec rm -rf {} +
}
