# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Contributor: Jonathan Steel <jsteel at archlinux.org>
# Contributor: Greg Sutcliffe <puppet-aur (at) emeraldreverie (dot) org>
# Contributor: Hyacinthe Cartiaux <hyacinthe (dot) cartiaux (at) free (dot) fr>
# Contributor: Thomas S Hatch <thatch45 (at) Gmail (dot) com>
# Contributor: Xavion <Xavion (dot) 0 (at) Gmail (dot) com>
# Contributor: Miah Johnson <miah (at) chia-pet dot org>
# Contributor: Dave Simons <miouhpi (at) Gmail (dot) com>
# Contributor: Niels Abspoel <aboe76 (at) Gmail (dot) com>
# Contributor: Tim Meusel <tim (at) bastelfreak (dot) de>

pkgname=puppet
pkgver=6.12.0
pkgrel=1
pkgdesc="Server automation framework and application"
arch=('any')
url="https://puppet.com/products/open-source-projects"
license=('Apache')
depends=('ruby' 'facter' 'hiera' 'ruby-semantic_puppet' 'ruby-augeas'
'ruby-shadow' 'ruby-concurrent' 'ruby-multi_json' 'ruby-puppet-resource_api'
'ruby-httpclient' 'ruby-deep_merge' 'ruby-sync')
makedepends=('git' 'ruby-rspec')
checkdepends=('ruby-rake' 'ruby-rspec' 'ruby-yard')
backup=('etc/puppetlabs/puppet/auth.conf'
        'etc/puppetlabs/puppet/puppet.conf'
        'etc/puppetlabs/puppet/hiera.yaml')
install="$pkgname.install"

_augeas_commit=416e8fe9df9d467289e2923c083367b04e7d8bd1
_cron_commit=4aa7bdba01493007b0d485b7ed78aee5ff347dca
_host_commit=34dca65fe54ba08bd764691e2619371c213df70b
_mount_commit=1e56e2588058551360269472823bf3426c5e1900
_selinux_commit=cfa0d78156c507f1e95a4fbd91bd5a7b0c2a2ddc
_sshkeys_commit=9f710d8e502b5bf2ca3d213d2bddea13f7a4b7b8
_yumrepo_commit=31c1736ae59155ed288179e81d368accb067ed0d
_zfs_commit=86fb96ad889f4570c2db7936048f4d1177331f10
_zone_commit=7880999cdcf15b9a3594b78a06e47399c78baba1

source=("https://github.com/puppetlabs/puppet/archive/6.12.0/puppet-6.12.0.tar.gz"
        "augeas_core::git+https://github.com/puppetlabs/puppetlabs-augeas_core#commit=${_augeas_commit}"
        "cron_core::git+https://github.com/puppetlabs/puppetlabs-cron_core#commit=${_cron_commit}"
        "host_core::git+https://github.com/puppetlabs/puppetlabs-host_core#commit=${_host_commit}"
        "mount_core::git+https://github.com/puppetlabs/puppetlabs-mount_core#commit=${_mount_commit}"
        "selinux_core::git+https://github.com/puppetlabs/puppetlabs-selinux_core#commit=${_selinux_commit}"
        "sshkeys_core::git+https://github.com/puppetlabs/puppetlabs-sshkeys_core#commit=${_sshkeys_commit}"
        "yumrepo_core::git+https://github.com/puppetlabs/puppetlabs-yumrepo_core#commit=${_yumrepo_commit}"
        "zfs_core::git+https://github.com/puppetlabs/puppetlabs-zfs_core#commit=${_zfs_commit}"
        "zone_core::git+https://github.com/puppetlabs/puppetlabs-zone_core#commit=${_zone_commit}"
        )
sha512sums=('642692153fc81b0db8d7ca76bed932a9008ee2c6917bf73b68e875a34d811518a25cec1fa71c5bda27995b2d6b02ed98e1fcbf83e9ef8cd5267a881d002684c7'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
conflicts=('puppet5')
provides=('puppet')

prepare() {
  cd "${pkgname}-${pkgver}"

  # FS#45044: fix file location
  sed -i 's_/opt/puppetlabs/puppet_/usr_' ext/systemd/puppet.service
}

check() {
  cd "${pkgname}-${pkgver}"
  # This breaks right now due to missing dependencies
  #rake --tasks              # show all tasks
  #rake commits             # requires a git repo
  #rake warnings            # requires a git repo
  #rake spec                # run tests single threaded
  #rake parallel:spec[8]
}

package() {
  cd $pkgname-$pkgver

  # generate manpages | this breaks right now..
  #rake gen_manpages

  ruby install.rb --destdir="$pkgdir" --sitelibdir="$( ruby -e \
    'puts RbConfig::CONFIG["vendorlibdir"]' )"

  install -d "$pkgdir"/usr/lib/{sysusers.d,tmpfiles.d}

  echo 'u puppet 52 "Puppet" /var/lib/puppet' > \
    "$pkgdir"/usr/lib/sysusers.d/$pkgname.conf
  echo "d /run/puppetlabs 0755 $pkgname $pkgname -" > \
    "$pkgdir"/usr/lib/tmpfiles.d/$pkgname.conf

  # systemd
  install -Dm644 "$srcdir"/$pkgname-$pkgver/ext/systemd/$pkgname.service \
    "$pkgdir"/usr/lib/systemd/system/$pkgname.service

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  # Match permissions that puppet sets when running
  install -dm775 "$pkgdir"/var/lib/puppet
  chmod 750 "$pkgdir"/var/log/puppetlabs/puppet

  rm -r "$pkgdir"/var/run

  cd ..
  install --directory "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  mv *_core "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/"
  # Search for unwanted .git directories and delete them
  find "${pkgdir}/opt/puppetlabs/puppet/vendor_modules/" -type d -name .git -exec rm -rf {} +
}
