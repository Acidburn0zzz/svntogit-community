# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-softlayer
pkgver=5.8.4
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz")
sha512sums=('fd8f2bbc64777541a8b5efc4f20b5dc8f55a6b48f822a24b97030c2dea1fd3bd37767c6c28678cc8a21285d8bf5d50113fed0ca4fafa000181c30089b1a152a6')

prepare() {
  cd softlayer-python-$pkgver
  sed -i 's/==/>=/' setup.py
}

build() {
  cd softlayer-python-$pkgver
  python setup.py build
}

check() {
  cd softlayer-python-$pkgver
  python setup.py pytest --addopts '-k "not test_edit_perms_on_bad"'
}

package() {
  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
