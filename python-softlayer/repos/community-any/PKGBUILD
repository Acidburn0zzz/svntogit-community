# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-softlayer
pkgver=5.5.1
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='https://github.com/softlayer/softlayer-python'
depends=('python-six' 'python-ptable' 'python-click' 'python-requests' 'python-prompt_toolkit'
         'python-pygments' 'python-urllib3')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-mock' 'python-testtools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/softlayer/softlayer-python/archive/v$pkgver.tar.gz"
        py37.patch)
sha512sums=('63ba7de5e0cb2fb9d055f9b25caf33011c4c12efb2a7fa69eca911f43bc58874d13a6fa0b9188d9eb208dde01cecf5123bb989848adfa0154a6c41d52775a897'
            '1174e16cc71eb314e9ce06ce52cad5449c963e6bf5c71e199cf8893ada4d2c7d0d2a01e8025a21e133980100dd513f9a955481400b62f80f13122e94ebd315f5')

prepare() { 
  cd softlayer-python-$pkgver
  patch -p1 -i ../py37.patch
}

build() {
  cd softlayer-python-$pkgver
  python setup.py build
}

check() {
  cd softlayer-python-$pkgver
  python setup.py pytest
}

package() {
  cd softlayer-python-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

# vim:set ts=2 sw=2 et:
