# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgbase=python-softlayer
pkgname=('python-softlayer' 'python2-softlayer')
pkgver=5.2.10
pkgrel=1
pkgdesc="A library for SoftLayer's API"
arch=('any')
license=('MIT')
url='http://github.com/softlayer/softlayer-python'
makedepends=('python-setuptools' 'python2-setuptools' 'python-prettytable' 'python2-prettytable'
             'python-click' 'python2-click' 'python-requests' 'python2-requests'
             'python-prompt_toolkit' 'python2-prompt_toolkit' 'python-pygments' 'python2-pygments')
checkdepends=('python-pytest-runner' 'python2-pytest-runner' 'python-mock' 'python2-mock'
              'python-testtools' 'python2-testtools')
source=("$pkgbase-$pkgver.tar.gz::http://github.com/softlayer/softlayer-python/archive/v.$pkgver.tar.gz")
sha512sums=('0486f151202a969d8e75663554780a6568507e032b124c2e7d4830acfcef542c21d41f57bf8158f7aa0b69635fce3b5901e9ae66344c8c85ca9a88ba79499003')

prepare() {
  cp -a softlayer-python-v.$pkgver{,-py2}
}

build() {
  cd "$srcdir"/softlayer-python-v.$pkgver
  python setup.py build

  cd "$srcdir"/softlayer-python-v.$pkgver-py2
  python2 setup.py build
}

check() {
  cd "$srcdir"/softlayer-python-v.$pkgver
  python setup.py pytest

  cd "$srcdir"/softlayer-python-v.$pkgver-py2
  python2 setup.py pytest
}

package_python-softlayer() {
  depends=('python-setuptools' 'python-prettytable' 'python-click' 'python-requests'
           'python-prompt_toolkit' 'python-pygments')

  cd softlayer-python-v.$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-softlayer() {
  depends=('python2-setuptools' 'python2-prettytable' 'python2-click' 'python2-requests'
           'python2-prompt_toolkit' 'python2-pygments')

  cd softlayer-python-v.$pkgver-py2
  python2 setup.py install --root="$pkgdir" --optimize=1
  install -D -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE

  mv "$pkgdir"/usr/bin/sl{,2}
  mv "$pkgdir"/usr/bin/slcli{,2}
}

# vim:set ts=2 sw=2 et:
