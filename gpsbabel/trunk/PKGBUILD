# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.5.4
pkgrel=1
pkgdesc="Reads, writes, and manipulates GPS waypoints in a variety of formats"
arch=('i686' 'x86_64')
url="http://www.gpsbabel.org"
license=('GPL')
depends=('libusb-compat' 'expat' 'qt5-base' 'qt5-webengine')
makedepends=('fop' 'libxml2' 'libxslt' 'gtk2' 'lcms2' 'qt5-webkit' 'java-environment' 'docbook-xsl')
optdepends=('qt5-webkit')
options=('docs')
source=("https://arch.p5n.pp.ru/~sergej/dl/gpsbabel-$pkgver.tar.gz"
	"build-fix.patch")
sha256sums=('59e29b83a16af16556fffc9315d3d3626d11fc7f2f50d6ec1f99dfc64412989e'
            '537752bd5d097d836d4d5a77b4f7a5ea68d408d6a648511e4a98865d2742fb46')

prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  patch -p1 <../build-fix.patch
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  ./configure --prefix=/usr --with-doc=babelweb
  make
  make doc
  (cd gui && qmake-qt5 && make)
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe-bin "$pkgdir"/usr/bin/gpsbabelfe-bin
  install -Dm644 gui/images/appicon.png "$pkgdir"/usr/share/pixmaps/gpsbabel.png
  install -Dm644 gui/gpsbabel.desktop "$pkgdir"/usr/share/applications/gpsbabel.desktop

  # Header install
  mkdir -p "$pkgdir"/usr/include/gpsbabel
  cp *.h "$pkgdir"/usr/include/gpsbabel

  # Doc install
  mkdir -p "$pkgdir"/usr/share/doc/$pkgname
  cp -r babelweb/* "$pkgdir"/usr/share/doc/$pkgname/
  cp README* "$pkgdir"/usr/share/doc/$pkgname/
}
