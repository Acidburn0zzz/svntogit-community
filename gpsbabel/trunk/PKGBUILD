# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: dibblethewrecker dibblethewrecker.at.jiwe.dot.org
# Contributor: William Rea <sillywilly@gmail.com>

pkgname=gpsbabel
pkgver=1.7.0
pkgrel=1
pkgdesc="Read, write and manipulate GPS waypoints in a variety of formats"
arch=(x86_64)
url="https://www.gpsbabel.org/"
license=(GPL)
depends=(hicolor-icon-theme libusb qt5-base) # qt5-webengine
makedepends=(libicns qt5-tools)
source=(https://github.com/gpsbabel/$pkgname/archive/${pkgname}_${pkgver//./_}.tar.gz
        gpsbabel.appdata.xml
        desktop-file.patch
        drop-map.patch)
sha256sums=('30b186631fb43db576b8177385ed5c31a5a15c02a6bc07bae1e0d7af9058a797'
            '5eb52ab0c4bdde61c747856e849f9bce9ca660e942c15f730a4330a38fd6a39a'
            'e66a786dc5592388f651f2bcabb4205809a42e0a937bb034cb469bd2cd50d1a1'
            'd18fd718973cc9efc6bee95ec7741b7d63a0fd46fd9e2d281f687fd465b7c19e')

prepare() {
  icns2png -x $pkgname-${pkgname}_${pkgver//./_}/gui/images/appicon.icns

  cd $pkgname-${pkgname}_${pkgver//./_}

  # Remove map due to heavy WebEngine dependency
  patch -Np1 -i ../drop-map.patch

  # Fix path
  sed -i 's|langPath_ = QApplication::applicationDirPath();|langPath_ = "/usr/share/gpsbabel";|' gui/mainwindow.cc
  sed -i 's|baseFile =  QApplication::applicationDirPath() + "/gmapbase.html";|baseFile = "/usr/share/gpsbabel/gmapbase.html";|' gui/map.cc

}

build() {
  cd $pkgname-${pkgname}_${pkgver//./_}
  ./configure --prefix=/usr
  make

  # GUI
  cd gui
  qmake
  lrelease *.ts
  make
}

package() {
  cd $pkgname-${pkgname}_${pkgver//./_}
  make DESTDIR="$pkgdir" install

  # GUI
  install -Dm755 gui/objects/gpsbabelfe "$pkgdir"/usr/bin/gpsbabelfe
  install -Dm644 gui/gpsbabel.desktop "$pkgdir"/usr/share/applications/gpsbabel.desktop
  install -Dm644 ../$pkgname.appdata.xml "$pkgdir/usr/share/metainfo/$pkgname.appdata.xml"
  install -Dm644 gui/gmapbase.html "$pkgdir"/usr/share/gpsbabel/gmapbase.html
  install -dm755 "$pkgdir"/usr/share/gpsbabel/translations/
  install -Dm644 gui/*.qm "$pkgdir"/usr/share/gpsbabel/translations/

  for res in 16 32 128 256 512; do
    install -Dm644 ../appicon_${res}x${res}x32.png \
      "$pkgdir/usr/share/icons/hicolor/${res}x${res}/apps/$pkgname.png"
  done
}
