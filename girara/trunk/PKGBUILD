# $Id$
# Maintainer: Johannes LÃ¶thberg <johannes@kyriasis.com>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: mlq <mlq@pwmt.org>

pkgname=girara
pkgver=0.2.6
pkgrel=1

pkgdesc="User interface library focused on simplicity and minimalism"
url="http://pwmt.org/projects/girara"
arch=('i686' 'x86_64')
license=('custom')

provides=('girara-gtk3' 'girara-common')
replaces=('girara-gtk3' 'girara-common')
conflicts=('girara-gtk3' 'girara-common')

depends=('gtk3')

source=(http://pwmt.org/projects/girara/download/girara-$pkgver.tar.gz
        fix-font-handling-gtk-3.21.patch)
sha1sums=('674e4294fe091fe93a1c186d95b3263b30cd8a5e'
          '609f469dc6ad4570959dea028fd36f8ba8562231')

prepare() {
  cd girara-$pkgver
  patch -p1 <"$srcdir"/fix-font-handling-gtk-3.21.patch
}

build() {
  cd girara-$pkgver
  touch colors.mk
  make GIRARA_GTK_VERSION=3
}

package() {
  cd girara-$pkgver
  make GIRARA_GTK_VERSION=3 DESTDIR="$pkgdir/" install
  install -D -m664 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
