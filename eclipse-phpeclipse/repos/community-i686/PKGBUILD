# $Id: PKGBUILD,v 1.4 2008/11/18 23:32:15 bardo Exp $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Frank Oosterhuis <frank@scriptzone.nl>

pkgname=eclipse-phpeclipse
_reldate=200810130444
pkgver=1.2.1
pkgrel=1
pkgdesc="PHP - Support for the Eclipse IDE Framework"
arch=('i686' 'x86_64')
url="http://www.phpeclipse.de/"
license=('CPL')
depends=('eclipse')
makedepends=('unzip')
source=(http://downloads.sourceforge.net/phpeclipse/updateSite-${pkgver}.${_reldate}PRD.zip)
md5sums=('21480c1a2f8c8cce333e75311658ac1f')

build() {
  cd ${srcdir}/updateSite

  # Features
  for feature in features/*.jar ; do
    install -dm755 ${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse/${feature/.jar}
    unzip -qqo -d ${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse/${feature/.jar} \
      ${feature} || return 1
  done
  
  # Plugins
  for file in $( find plugins -type f ) ; do
    install -Dm644 ${file} ${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/${file}
  done
}

# vim:set ts=2 sw=2 et:
