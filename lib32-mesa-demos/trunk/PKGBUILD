# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=mesa-demos
pkgname=lib32-$_pkgbasename
pkgver=8.0.1
pkgrel=2
arch=(x86_64)
pkgdesc='Mesa demos and tools (32-bit)'
url="http://mesa3d.sourceforge.net"
license=('custom')
depends=('lib32-glew>=1.5.6' mesa-demos)
makedepends=(gcc-multilib)
source=(ftp://ftp.freedesktop.org/pub/mesa/demos/${pkgver}/mesa-demos-${pkgver}.tar.bz2)
md5sums=('320c2a4b6edc6faba35d9cb1e2a30bf4')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd "${srcdir}/mesa-demos-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  cd "${srcdir}/mesa-demos-${pkgver}"
  make DESTDIR="${pkgdir}" install

  for i in "$pkgdir/usr/bin/"*; do
    mv "$i" "$i"32
  done

  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}
