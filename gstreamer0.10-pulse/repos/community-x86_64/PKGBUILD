# $Id$
# Maintainer: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: Thar <mailbox.tec@gmail.com>

pkgname=gstreamer0.10-pulse
pkgver=0.10.21
pkgrel=1
pkgdesc="A GStreamer 0.10 plugin for the PulseAudio sound server"
arch=('i686' 'x86_64')
license=('LGPL')
makedepends=('pkgconfig')
url="http://gstreamer.freedesktop.org/"
depends=('gstreamer0.10-base>=0.10.28' 'pulseaudio>=0.9.8')
options=(!libtool !emptydirs)
source=(${url}/src/gst-plugins-good/gst-plugins-good-${pkgver}.tar.bz2)
sha256sums=('24445a1cf56302ab4ad5f56055d5c3d81c704b8f9f6875fe78a25d4f72716d22')

build() {
  cd "${srcdir}/gst-plugins-good-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --enable-experimental \
    --with-package-name="GStreamer Good Plugins (Archlinux)" \
    --with-package-origin="http://www.archlinux.org/" || return 1

  cd ext/pulse
  make
}

package() {
  cd "${srcdir}/gst-plugins-good-${pkgver}/ext/pulse"
  make DESTDIR="${pkgdir}" install
}
