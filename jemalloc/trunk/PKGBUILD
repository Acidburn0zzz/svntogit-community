# $Id$
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>
# Contributor: Kovivchak Evgen <oneonfire@gmail.com>

pkgname=jemalloc
pkgver=3.4.0
pkgrel=1
pkgdesc="General-purpose scalable concurrent malloc implementation"
arch=('i686' 'x86_64')
license=('BSD')
url="http://www.canonware.com/jemalloc/"
depends=('glibc')
makedepends=('autoconf' 'make')
source=(http://www.canonware.com/download/jemalloc/$pkgname-$pkgver.tar.bz2)

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure --prefix=/usr
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR="$pkgdir" install
	mv "$pkgdir"/usr/bin/{,jemalloc-}pprof
	chmod 644 "$pkgdir"/usr/lib/*.a
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

sha256sums=('f60802c4c2c60bc24598fd052494aea96f4426f3d4decd832eadeed520ff81c0')
