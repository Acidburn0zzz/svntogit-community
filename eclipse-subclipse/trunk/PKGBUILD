# $Id: PKGBUILD,v 1.8 2009/04/14 11:09:00 bardo Exp $
# Maintainer: Corrado Primier <bardo@aur.archlinux.org>
# Contributor: G_Syme <demichan(at)mail(dot)upb(dot)de>

pkgname=eclipse-subclipse
pkgver=1.6.2
pkgrel=1
_urlver=45405
pkgdesc="Subversion integration for the Eclipse platform"
arch=('i686' 'x86_64')
url="http://subclipse.tigris.org/"
license=('EPL')
depends=('eclipse' 'subversion')
source=(http://subclipse.tigris.org/files/documents/906/${_urlver}/site-${pkgver}.zip)
md5sums=('021120e8d7d8dff4f1d53a79da5aa5bf')


build() {
  _dest=${pkgdir}/usr/share/eclipse/dropins/${pkgname/eclipse-}/eclipse

  cd ${srcdir}

  # Features
  find features -type f -name *.jar | while read feature ; do
    install -dm755 ${_dest}/${feature/.jar}
    cd ${_dest}/${feature/.jar}
    jar xf ${srcdir}/${feature} || return 1
  done

  # Plugins
  find plugins -type f -exec install -Dm644 {} ${_dest}/{} \;
}

# vim:set ts=2 sw=2 et:
