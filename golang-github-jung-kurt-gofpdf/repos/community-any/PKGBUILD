# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-jung-kurt-gofpdf
pkgver=2.2.0
pkgrel=1
pkgdesc='A PDF document generator with high level support for text, drawing and images'
arch=('any')
url="https://github.com/jung-kurt/gofpdf"
license=('MIT')
depends=('go-pie')
source=("$pkgname-$pkgver.tar.gz::https://github.com/jung-kurt/gofpdf/archive/v$pkgver.tar.gz")
sha512sums=('e4d3b5081373f9ca4b32fb547ece9547ce060a415372217c5e47fd0ee30b7c2849aae4b788c80aa35a23cdc7b83c3d7e72f4bfa1f1f2d6dd70fc42d6329336a7')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  cd "$srcdir"/build/src/github.com/jung-kurt/gofpdf
  go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt
  cp -a "$srcdir"/gofpdf-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/jung-kurt/gofpdf

  install -Dm644 gofpdf-$pkgver/LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
