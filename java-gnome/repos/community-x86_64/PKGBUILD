# $Id$
# Maintainer : Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
pkgname=java-gnome
pkgver=4.0.17
pkgrel=1
pkgdesc="Java bindings to the GNOME platform (including gtk, glib and glade)"
arch=('i686' 'x86_64')
url="http://java-gnome.sourceforge.net"
license=('GPL')
depends=('gtksourceview2' 'libnotify' 'libunique' 'gtkspell' 'libglade' 'java-runtime')
makedepends=('java-environment' 'junit' 'python2')
options=(!libtool)
source=(http://ftp.gnome.org/pub/gnome/sources/${pkgname}/4.0/${pkgname}-${pkgver}.tar.bz2)
sha256sums=('350cd6c4d75995c309785794b8e216bf5c09568e52975f26ff0be7d79bb2a422')

build() {
  cd ${srcdir}/${pkgname}-${pkgver}
  #python2
  sed -i "s|env python|env python2|" build/faster
  sed -i "s|env python|env python2|" src/util/demux/demux.py

  # adapt to the new build-system
  unset CLASSPATH
  if [ -d "/usr/lib/jvm/java-6-openjdk/" ]; then
    ./configure jdk=/usr/lib/jvm/java-6-openjdk/ prefix=/usr
  else
    ./configure
  fi
  make
  DISPLAY= make -j1 doc
  make -j1 DESTDIR=${pkgdir} install

  install -d ${pkgdir}/usr/share/gtk-doc/html/java-gnome
  cp -R ${srcdir}/java-gnome-${pkgver}/doc/api/* ${pkgdir}/usr/share/gtk-doc/html/java-gnome
}
