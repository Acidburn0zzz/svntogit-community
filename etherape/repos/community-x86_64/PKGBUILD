# $Id$
# Maintainer: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=etherape
pkgver=0.9.14
pkgrel=2
pkgdesc='A graphical network monitor for various OSI layers and protocols'
arch=('i686' 'x86_64')
url="http://etherape.sourceforge.net/"
license=('GPL2')
depends=('libgnomecanvas' 'libpcap' 'popt')
makedepends=('rarian' 'gnome-doc-utils')
source=(http://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        drop-libgnomeui.patch)
sha1sums=('d28ad8be9c571c204272391316ff261bdb4f1ec3'
          'SKIP')

prepare() {
  cd ${srcdir}/${pkgname}-${pkgver}

  # Remove libgnomeui dependency
  patch -Np1 -i ../drop-libgnomeui.patch

  autoreconf -fi
}

build() {
  cd ${srcdir}/${pkgname}-${pkgver}

  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${srcdir}/${pkgname}-${pkgver}

  make DESTDIR=${pkgdir} install
}
