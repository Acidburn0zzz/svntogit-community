# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# old Maintainer: Andrei "Garoth" Thorp <my-username at gmail dot com>
# Contributor: Alexandr Nevskiy <kepkin@gmail.com>

pkgname=drivel
pkgver=3.0.3
pkgrel=3
pkgdesc="GTK client for working with online journals (blogs)."
url="http://drivel.sourceforge.net/"
arch=('i686' 'x86_64')
license=("GPL")
depends=('curl' 'gnome-vfs' 'gtksourceview2' 'gtkspell' 'libsoup' 'desktop-file-utils')
makedepends=('intltool>=0.21' 'gnome-doc-utils>=0.3.2')
install=drivel.install
source=(http://downloads.sourceforge.net/drivel/$pkgname-$pkgver.tar.gz)
md5sums=('295a583d6ed652ab591070245283fb5b')

build() {
  cd $srcdir/$pkgname-$pkgver
  sed -i 's/ -Werror//' configure
  LDFLAGS=-lm ./configure --prefix=/usr --sysconfdir=/etc \
      --disable-desktop-update --disable-schemas-install \
      --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make DESTDIR=$pkgdir install
}
