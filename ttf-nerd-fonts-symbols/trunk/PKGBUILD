# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Asger Hautop Drewsen <asgerdrewsen@gmail.com>

pkgname=('ttf-nerd-fonts-symbols' 'ttf-nerd-fonts-symbols-mono')
pkgbase=ttf-nerd-fonts-symbols
pkgver=2.1.0+35+gae56e33e
pkgrel=1
pkgdesc="High number of extra glyphs from popular 'iconic fonts'"
arch=('x86_64')
url=https://github.com/ryanoasis/nerd-fonts
license=('MIT')
install=$pkgbase.install
source=("Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/ae56e33e9331b5317bcb71148ea7a1f59ebe1ae5/src/glyphs/Symbols-1000-em%20Nerd%20Font%20Complete.ttf"
        "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/ae56e33e9331b5317bcb71148ea7a1f59ebe1ae5/src/glyphs/Symbols-2048-em%20Nerd%20Font%20Complete.ttf"
        "10-nerd-font-symbols-2.1.0.conf::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/2.1.0/10-nerd-font-symbols.conf"
        "Nerd_Fonts-LICENSE-2.1.0::https://raw.githubusercontent.com/ryanoasis/nerd-fonts/2.1.0/LICENSE")
sha512sums=('0bc713770e85e6b93e6b0ea14dca2f3ac6892996842de88dbd9fa8aa39a0fe8814b11a34c91478b1f9a3272f8656f0ebfd0188a9154f3c78f65beb6fea32b316'
            'cb1f7a7d2129c9f5ffc5f2573daae4e3e5b39221a9e1fbc80ab94d17e8328aef6e06bcfde9086bd30d39148ca5e2d93bc6c1bf692e370bfe55fa48e263d486fd'
            '4b35087c7e0a8a880a160d3abc84f983371275c091c74bfd44d01e51317e452dd42cbbd0519ba483b94978de95732ad7736c898c8fdcec451bb1096216c62e14'
            '95712651fb58ad5f5ee1b796b1891e5cb6c62532699c08d3cd9255df46c10336faa170b4ec5a920be44d2cba68ab7726907c800e4a9b0060483437569cfe6fd3')

package_ttf-nerd-fonts-symbols() {
  pkgdesc+=' (2048-em)'
  conflicts=('ttf-nerd-fonts-symbols-mono')
  install -Dm644 "Symbols-2048-em_Nerd_Font_Complete-$pkgver.ttf" \
    "$pkgdir"/usr/share/fonts/TTF/'Symbols-2048-em Nerd Font Complete.ttf'
  install -Dm644 10-nerd-font-symbols-2.1.0.conf "$pkgdir"/etc/fonts/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-LICENSE-2.1.0 "$pkgdir"/usr/share/licenses/$pkgbase/LICENSE
}

package_ttf-nerd-fonts-symbols-mono() {
  pkgdesc+=' (1000-em)'
  conflicts=('ttf-nerd-fonts-symbols')
  install -Dm644 "Symbols-1000-em_Nerd_Font_Complete-$pkgver.ttf" \
    "$pkgdir"/usr/share/fonts/TTF/'Symbols-1000-em Nerd Font Complete.ttf'
  install -Dm644 10-nerd-font-symbols-2.1.0.conf "$pkgdir"/etc/fonts/conf.avail/10-nerd-font-symbols.conf
  install -Dm644 Nerd_Fonts-LICENSE-2.1.0 "$pkgdir"/usr/share/licenses/$pkgbase-mono/LICENSE
}

# vim:set ts=2 sw=2 et:
