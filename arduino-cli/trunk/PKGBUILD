# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
_pkgver=0.3.0-alpha.preview
pkgver=${_pkgver//-/.}
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/arduino/arduino-cli/archive/$_pkgver.tar.gz")
sha512sums=('61a6978db659e5429545e0f4a11a0451732959725ba57e08eff2243b56c490759bc74d11e1ff38e2a3cae8c608fd56210b072d6cceaf6c6098b4496fb5ca3a62')

build() {
  cd $pkgname-$_pkgver

  export GOPATH="$srcdir"/gopath
  export GOBIN="$GOPATH"/bin
  mkdir -p $GOBIN

  go get
  go install
}

package() {
  install -Dm 755 gopath/bin/arduino-cli-$_pkgver "$pkgdir"/usr/bin/arduino-cli
}
