# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=0.6.0
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('2aabc31e9105bbb740d3ab8996f75ba49c5f7b871f5805580bf640811549da0f2cd9d49e377971aff581db03425a3df0f76c70e16d41f3ef9d1c7522d52edfec')

prepare(){
  export GOPATH="$srcdir"/gopath

  mkdir -p "$GOPATH"/src/github.com/arduino
  ln -rTsf "$pkgname-$pkgver" "$GOPATH"/src/github.com/arduino/arduino-cli
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  export GO111MODULE=on

  go mod vendor
  go mod download
}

build(){
  export GOPATH="$srcdir"/gopath
  cd "$GOPATH"/src/github.com/arduino/arduino-cli

  go build \
  	-gcflags "all=-trimpath=$GOPATH" \
  	-asmflags "all=-trimpath=$GOPATH" \
  	-ldflags "-extldflags $LDFLAGS" \
  	-v .
}

package() {
  install -Dm 755 $pkgname-$pkgver/$pkgname "$pkgdir"/usr/bin/$pkgname
}

