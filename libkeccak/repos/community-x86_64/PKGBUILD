# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=libkeccak
pkgver=1.2.1
pkgrel=1
pkgdesc='Keccak-family hashing library'
arch=('x86_64')
url='https://github.com/maandree/libkeccak'
license=('custom:ISC')
depends=('glibc')
checkdepends=('valgrind')
source=("https://github.com/maandree/libkeccak/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('f49971aff0d43487b8df5288247281b7783f8ca3369d2367998fab94f90533e8ec68371f8239324c55635c6fca6b1faea4b7a709ed9c9dbd4aa21f75424359fa')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' optimised.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr
}

check() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk check
}

package() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr DESTDIR="${pkgdir}" install
}
