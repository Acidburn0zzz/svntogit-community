# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Mattias Andr√©e <`base64 -d`(bWFhbmRyZWUK)@kth.se>

pkgname=libkeccak
pkgver=1.3.1
pkgrel=1
pkgdesc='Keccak-family hashing library'
arch=('x86_64')
url='https://github.com/maandree/libkeccak'
license=('custom:ISC')
depends=('glibc')
checkdepends=('valgrind')
source=("https://github.com/maandree/libkeccak/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('cbd8466a8be32db633aadcc1cdfd7c09eedd0606d274d0f5e43b0055a5f87672c1ba27052fd03423779b84648379f0095310d8367cef2ed7ba70d49a66e03249')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's#FLAGS\s\+=#FLAGS +=#g' optimised.mk
}

build() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr
}

check() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk check
}

package() {
  cd ${pkgname}-${pkgver}
  make CONFIGFILE=optimised.mk PREFIX=/usr DESTDIR="${pkgdir}" install
}
