# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=rclone
pkgver=1.49.5
pkgrel=1
pkgdesc="Sync files to and from Google Drive, S3, Swift, Cloudfiles, Dropbox and Google Cloud Storage"
arch=('x86_64')
url="https://rclone.org/"
license=('MIT')
depends=('glibc')
makedepends=('python2' 'pandoc' 'go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/rclone/rclone/archive/v$pkgver.tar.gz")
sha512sums=('1fe1338eaf69ca0ca9e8705af0e4b9856aa97345b6eb1c2d604d40090da0f37a26550bed4c86c14a0c873d92428a96a0012b48c2d44984fe6dea00b89d946d0d')

prepare() {
  cd rclone-$pkgver

  sed -i "1s/python$/&2/" bin/make_manual.py bin/make_backend_docs.py

  mkdir -p .gopath/src/github.com/rclone
  ln -sf "$PWD" .gopath/src/github.com/rclone/rclone
  export GOPATH="$PWD/.gopath"

  go get github.com/rclone/rclone
}

build() {
  cd rclone-$pkgver/.gopath/src/github.com/rclone/rclone

  PATH="$GOPATH/bin:$PATH" make TAG=v$pkgver rclone rclone.1 MANUAL.html MANUAL.txt

  ./rclone genautocomplete bash rclone.bash_completion
  ./rclone genautocomplete zsh rclone.zsh_completion
}

check() {
  cd rclone-$pkgver/.gopath/src/github.com/rclone/rclone
  make TAG=v$pkgver test || warning "Tests failed"
}

package() {
  cd rclone-$pkgver/.gopath/src/github.com/rclone/rclone
  make TAG=v$pkgver DESTDIR="$pkgdir" install

  install -Dm644 rclone.bash_completion "$pkgdir"/usr/share/bash-completion/completions/rclone
  install -Dm644 rclone.zsh_completion "$pkgdir"/usr/share/zsh/site-functions/_rclone

  install -Dm644 COPYING "$pkgdir"/usr/share/licenses/$pkgname/COPYING

  install -Dm644 rclone.1 "$pkgdir"/usr/share/man/man1/rclone.1
  install -d "$pkgdir"/usr/share/doc/$pkgname
  install -t "$pkgdir"/usr/share/doc/$pkgname -m644 MANUAL.html MANUAL.txt
}
