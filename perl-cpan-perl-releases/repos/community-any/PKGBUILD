# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=perl-cpan-perl-releases
pkgver=0.58
pkgrel=1
pkgdesc='Mapping Perl releases on CPAN to the location of the tarballs'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl>=5.6.0')
url=https://metacpan.org/release/CPAN-Perl-Releases
source=("http://search.cpan.org/CPAN/authors/id/B/BI/BINGOS/CPAN-Perl-Releases-$pkgver.tar.gz")
md5sums=(ca52a720b61cc9cf0a6d08a7e69153a4)
sha512sums=(706caf0d205ab310eb8cc234dede608e6182ecf122890ed37598b67ea3ded04b9aa5c92b7b829da49cf68f9b1660e900141f3910fcf9aca4727d1e9dace2e1d3)
_dir="$srcdir/CPAN-Perl-Releases-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
