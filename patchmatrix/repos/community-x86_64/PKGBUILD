# Maintainer: David Runge <dave@sleepmap.de>
pkgname=patchmatrix
pkgver=0.14.0
pkgrel=1
pkgdesc="A JACK patchbay in flow matrix style"
arch=('x86_64')
url="https://git.open-music-kontrollers.ch/lad/patchmatrix/about/"
license=('Artistic2.0')
groups=('lv2-plugins' 'pro-audio')
depends=('hicolor-icon-theme' 'jack' 'libgl')
makedepends=('lv2' 'meson' 'sord')
source=("https://git.open-music-kontrollers.ch/lad/${pkgname}/snapshot/${pkgname}-${pkgver}.tar.xz")
sha512sums=('135cfd9342700f31a5f2e0c3c1d8615d520cb714bed0d14c0b42899d9cb6ab6c83ff93a4453b6ebf84c1c6bca713f53df4ae2c9cce5e174d5711e5c01a515a6e')

build() {
  cd "$pkgname-$pkgver"
  arch-meson --prefix=/usr \
             --libdir=lib \
             --buildtype=release \
             build
  ninja -C build
}

# check fails due to currently broken lv2_validate in lv2
# check() {
#   cd "$pkgname-$pkgver"
#   meson test -C build
# }

package() {
  cd "$pkgname-$pkgver"
  DESTDIR="${pkgdir}" meson install -C build
  # docs
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}
