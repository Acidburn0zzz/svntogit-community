# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: SanskritFritz (gmail)
# Contributor: Tobias T. <OldShatterhand at gmx-topmail dot de>

pkgname=bibletime
pkgver=3.0.1
pkgrel=1
pkgdesc='Bible study tool'
arch=('x86_64')
url='http://www.bibletime.info/'
license=('GPL2')
depends=('sword' 'clucene' 'qt5-webengine' 'qt5-svg')
makedepends=('cmake' 'docbook-xsl' 'expac' 'fop' 'ninja' 'po4a' 'qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/bibletime/bibletime/archive/v$pkgver.tar.gz")
sha512sums=('9c614e04066fcf0d440f8c78d05ac19402ebf308dd8924d121c720ed82167f2143d2b44bf9da4d38e78ea10cb934cb8a145e3b7363a8ac46744090b770a2cf51')

prepare() {
  mkdir -p build
}

build() {
  cd build

  cmake -GNinja ../bibletime-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBT_DOCBOOK_XSL_HTML_CHUNK_XSL=/usr/share/xml/docbook/xsl-stylesheets-$(expac %v docbook-xsl | cut -d - -f 1)/html/chunk.xsl \
    -DBT_DOCBOOK_XSL_PDF_DOCBOOK_XSL=/usr/share/xml/docbook/xsl-stylesheets-$(expac %v docbook-xsl | cut -d - -f 1)/fo/docbook.xsl
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# vim:set ts=2 sw=2 et:
