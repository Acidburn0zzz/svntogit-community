# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josh VanderLinden <arch@cloudlery.com>
# Contributor: Nicolas Storme <nicolas@atelier-web.com>

pkgname=goaccess
pkgver=1.1
pkgrel=1
pkgdesc="An open source real-time web log analyzer and interactive viewer"
arch=('i686' 'x86_64')
url='http://goaccess.io'
license=('GPL')
depends=('ncurses' 'geoip' 'glib2')
backup=('etc/goaccess.conf')
source=("http://tar.goaccess.io/$pkgname-$pkgver.tar.gz")
sha512sums=('d0d2cb37921ed510d3ee6d8ce65809d50092e9ba1e0234d86798f0ef71c90d88f506b667c1c02207ed33bb8aebdcea18af17bb3c8de1932d6bb3d61922ef6e4a')

build() {
  cd $pkgname-$pkgver
  ./configure --enable-geoip --enable-utf8
  make prefix=/usr sysconfdir=/etc
}

package() {
  cd $pkgname-$pkgver
  make prefix="$pkgdir/usr" sysconfdir="$pkgdir/etc" install
}

