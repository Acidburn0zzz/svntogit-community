# Maintainer: Eli Schwartz <eschwartz@archlinux.org>

_pkgname=lupa
pkgname=python-lupa
pkgver=1.8
pkgrel=2
pkgdesc="Python wrapper around Lua and LuaJIT"
arch=('x86_64')
url="https://github.com/scoder/lupa"
license=('MIT')
depends=('lua52' 'python')
makedepends=('python-setuptools')
source=("https://files.pythonhosted.org/packages/source/${_pkgname:0:1}/${_pkgname}/${_pkgname}-${pkgver}.tar.gz"
        "lua-pkg-config-name.diff")
sha256sums=('f55781c9ab8fab77aa438481067a72da0b04849e2790a4e80d3c9bc77d7f8e53'
            'ac8e3daba8465cb63e43cef6662c8dfad6d1d012c18e4faa5a5270c603df4382')
b2sums=('182e32610b129a8e6683491be6203d138dc8019fd03e33d7d882ba49d743556abf69b28d7f794bda525906e5f27b628ff776e96c5aff69f19ac9171bde9c3b16'
        'cd0b0cd208545cea9ebba1b55df9a2fc7ddd60e7c0060b0275494884029e35036425ffd416bd768e4ab407f1701b821f357f9328c1135c7e31fe41b90347d661')

prepare() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    # --no-bundle is not removed from argv if lua is properly detected, so the
    # only working safety net is to rm the bundled sources
    rm -rf third-party
    # detect lua52.pc
    patch -p1 -i ../lua-pkg-config-name.diff
}

build() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py build --no-luajit
}

check() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py test
}

package() {
    cd "${srcdir}"/${_pkgname}-${pkgver}

    python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
    install -Dm644 LICENSE.txt "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE.txt
}
