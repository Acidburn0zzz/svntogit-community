# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: William Rea <sillywilly@gmail.com>
# Maintainer: Angel 'angvp' Velasquez <angvp[at]archlinux.com.ve>

pkgname=obby
pkgver=0.4.8
pkgrel=3
pkgdesc="A library which provides synced document buffers"
url="http://gobby.0x539.de"
license=('GPL')
arch=('i686' 'x86_64')
depends=('net6' 'avahi' 'libsigc++')
validpgpkeys=('17107DB19A2842FFB6997654ED1A3933B2CFCDD8')
source=(http://releases.0x539.de/obby/obby-$pkgver.tar.gz{,.sig})
sha256sums=('7d1d7ba6e800cea1b1a3c47d1189f959ef49a647ebe77170351e2dfd27b89b67'
            'SKIP')

build() {
  cd "$srcdir"/obby-$pkgver

  # libsigc++ requires C++11
  CXXFLAGS+=' -std=c++11'

  ./configure --prefix=/usr --with-zeroconf --enable-ipv6
  make
}

package() {
  cd "$srcdir"/obby-$pkgver
  make DESTDIR="$pkgdir" install
}
