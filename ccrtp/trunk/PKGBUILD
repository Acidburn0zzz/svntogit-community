# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Jeff Mickey <jeff@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>
# Contributor: Alexander RÃ¸dseth <rodseth@gmail.com>

pkgname=ccrtp
pkgver=1.8.0
pkgrel=2
pkgdesc="GNU ccRTP"
arch=('x86_64' 'i686')
url="http://www.gnu.org/software/ccrtp/"
license=('GPL' 'custom')
depends=('commoncpp2' 'zlib' 'libxml2' 'libgcrypt')
options=('!libtool')
install=$pkgname.install
source=("ftp://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"
        'gcc46.patch')
sha256sums=('365feddd276c78104600204ae6db4e76c66036df1e5b905e855239daac6a2473'
            '4ac9385228dc8b8893e0868d5a06477e4009e9fb2dc191460406da58fc6a6fb3')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  patch -Np1 -i "$srcdir/gcc46.patch"
  # arch64 fix from Mandriva
  [ "$CARCH" == "x86_64" ] && export CXXFLAGS="$CXXFLAGS -fpermissive"
  export LDFLAGS="$LDFLAGS `pkg-config --libs libccgnu2`"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING.addendum "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  #rm -rf "$pkgdir/usr/share/info/dir"
}

# vim:set ts=2 sw=2 et:
