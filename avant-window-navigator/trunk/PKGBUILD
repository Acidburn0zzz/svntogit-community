# $Id$
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=avant-window-navigator
pkgver=0.4.0
pkgrel=8
pkgdesc="Fully customisable dock-like window navigator for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL')
depends=('libwnck' 'gconf' 'xproto' 'dbus-glib' 'libglade' 'libxdamage' \
	 'libxcomposite' 'libxrender' 'python2' 'hicolor-icon-theme' \
	 'pyxdg' 'python-feedparser' 'xdg-utils' 'libdesktop-agnostic' \
	 'libgtop' 'bzr' 'dbus-python')
makedepends=('intltool' 'gnome-doc-utils' 'vala')
install=${pkgname}.install
options=('!libtool')
source=(https://edge.launchpad.net/awn/0.4/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz
	awn-applet-simple.patch)
md5sums=('03654b45dd95cbb83fa7e112bd00523c'
	 'abd1200504e6a4a8961a3fe0a4a99363')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	# FS#19084
	patch -p0 < ${srcdir}/awn-applet-simple.patch
	
	./configure --prefix=/usr --sysconfdir=/usr/share
	sed -i 's|${prefix}/etc|${prefix}/share|' Makefile

	make
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install
	str=' (self.client, defs.THEME, defs.TOOLTIP_OUTLINE_COLOR, "theme_tooltip_outline_color", False),'
	sed -i "s|$str|#$str|" ${pkgdir}/usr/bin/awn-settings

        sed -i "s|#!/usr/bin/python$|#!/usr/bin/python2|" \
          $pkgdir/usr/bin/awn-settings \
          $pkgdir/usr/share/avant-window-navigator/awn-settings/awnSettings{,Helper}.py

        gconf-merge-schema "$pkgdir/usr/share/gconf/awn.schemas" "$pkgdir"/usr/share/gconf/schemas/*.schemas
        rm "$pkgdir"/usr/share/gconf/schemas/*.schemas
        mv "$pkgdir"/usr/share/gconf/{,schemas/}awn.schemas
}
