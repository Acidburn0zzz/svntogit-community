# $Id$
# Contributor: Jaroslav Lichtblau <dragonlord@aur.archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>
# Maintainer: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=avant-window-navigator
pkgver=0.3.2.1
pkgrel=2
pkgdesc="Fully customisable dock-like window navigator for GNOME"
arch=('i686' 'x86_64')
url="https://launchpad.net/awn"
license=('GPL')
depends=('libwnck>=2.30' 'gnome-desktop>=2.30' 'libgnome>=2.30' 'gnome-vfs' \
	 'gconf>=2.0' 'xproto' 'dbus-glib' 'libglade>=2.0' 'libxdamage' \
	 'libxcomposite' 'libxrender' 'python>=2.5' 'gnome-python-desktop' \
	 'hicolor-icon-theme' 'gnome-python-extras' 'pyxdg' 'libgnomeui' \
	 'python-feedparser' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
install=${pkgname}.install
options=('!libtool')
source=(https://edge.launchpad.net/awn/0.2/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('e1632e4756706e17ebc1958aef002cd0')

build() {
	cd ${srcdir}/${pkgname}-${pkgver}

	./configure --prefix=/usr --sysconfdir=/usr/share --disable-schemas-install || return 1
	sed -i 's|${prefix}/etc|${prefix}/share|' Makefile || return 1

	make || return 1
}

package() {
	cd ${srcdir}/${pkgname}-${pkgver}

	make GCONF_DISABLE_MAKEFILE_SCHEMA_INSTALL=1 DESTDIR=${pkgdir} install || return 1
}
