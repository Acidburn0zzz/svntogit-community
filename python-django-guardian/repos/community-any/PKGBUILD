# Maintainer: David Runge <dvzrv@archlinux.org>

_name=django-guardian
pkgname=python-django-guardian
pkgver=2.2.0
pkgrel=1
pkgdesc="Per object permissions for Django"
arch=('any')
url="https://github.com/django-guardian/django-guardian"
license=('BSD' 'MIT')
depends=('python-django' 'python-django-environ')
makedepends=('python-setuptools')
checkdepends=('python-mock' 'python-pytest' 'python-pytest-django')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('155a937ebe975053ac64e258d0e2e956e93751541f5ea993e7e9a46f074eeec034100fed404067327add28565d3e9ce7ad734e9a6ee7a5ecf3dfe4e6b3a61e80')

prepare() {
  mv -v "${_name}-${pkgver}" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  python setup.py build
}

check() {
  cd "$pkgname-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  pytest -v
}

package() {
  cd "$pkgname-$pkgver"
  python setup.py install --skip-build \
    --optimize=1 \
    --prefix=/usr \
    --root="${pkgdir}"
  install -vDm 644 {AUTHORS,CHANGES,README.rst} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
