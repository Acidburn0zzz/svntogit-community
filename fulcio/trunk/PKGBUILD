# Maintainer: Santiago Torres-Arias <santiago@archlinux.org>
# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=fulcio
pkgver=0.1.0
pkgrel=1
pkgdesc="free Root-CA for code signing certs - issuing certificates based on an OIDC email address."
arch=('x86_64')
url="https://github.com/sigstore/fulcio"
license=('Apache')
depends=('glibc')
makedepends=('go' 'git')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/sigstore/fulcio/archive/refs/tags/${pkgver}.tar.gz")
sha512sums=('fef26255c3b18e91d4502f0e9ae0152fadabcc8508c1f9399faee0492d054664d16e3dfed3dc795c442e85f9a5b01d95573727162c5222d077f1d41866d70b7b')
b2sums=('c41641723c65ea24937ceb4183506ef0db11432e8dc65c17d4b18b823fd835977339de657e403ade84ca46f8e5b557aaa98676ad14fdad1757ce5500ee46c193')

build(){
  cd "${pkgname}-${pkgver}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  mkdir build
  go build -o build/ ./...
}

check() {
  cd "${pkgname}-${pkgver}"
  make test
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm755 build/fulcio "${pkgdir}/usr/bin/${pkgname}"
}
