# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=mailutils
pkgver=3.1.1
pkgrel=1
pkgdesc="MUA command line tool (mailx)"
arch=(i686 x86_64)
url="http://www.gnu.org/software/mailutils/"
license=('GPL')
depends=('libldap' 'pam' 'gnutls' 'gsasl' 'guile')
makedepends=('emacs')
options=(!emptydirs)
validpgpkeys=('325F650C4C2B6AD58807327A3602B07F55D0C732')
source=(https://ftp.gnu.org/gnu/mailutils/mailutils-$pkgver.tar.bz2{,.sig})
sha256sums=('f5db06c9f72f2cc484339a834b5d217f62bc0e56bfb279f33a71ab749ea7feb6'
            'SKIP')

build() {
  cd "$srcdir/mailutils-$pkgver"
  ./configure \
	--prefix=/usr \
	--sbindir=/usr/bin \
	--libexecdir=/usr/lib/$pkgname \
	--with-gdbm \
	--with-gnutls \
	--disable-python
  make CFLAGS="$CFLAGS -O2" || make CFLAGS="$CFLAGS -O2" LDFLAGS="-L`pwd`/lib -lmutcpwrap"
}

package() {
  cd "$srcdir/mailutils-$pkgver"
  make -j1 DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/usr/share/info/dir
  mv "$pkgdir"/usr/bin/mail "$pkgdir"/usr/bin/gnu-mail
  mv "$pkgdir"/usr/share/man/man1/mail.1 "$pkgdir"/usr/share/man/man1/gnu-mail.1
  chown -R root:root "$pkgdir"
}
