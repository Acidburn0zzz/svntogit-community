# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
pkgver=1.4.0
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Edu4rdSHL/findomain'
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=("https://github.com/Edu4rdSHL/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('96acff4ef4b0ad7be62f894f8b446987aa9afb101be4e9031381be0ad986180494010a7545bc5034853a8e5a4f271bd00fdc28e7935f5a05bf8ee2de82044cb5')
b2sums=('3db649b58bcf344cb23d4b0d4a559fb7dff882f42a916dcd0abdebd1474649c07d9998048d3197c10ed91440dc6fb36d9c9586711fac99f461fd616005c6fc94')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
