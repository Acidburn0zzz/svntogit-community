# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
_pkgname=Findomain
pkgver=4.2.1
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Edu4rdSHL/findomain'
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=("https://github.com/${_pkgname}/${_pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
b2sums=('cca6c95ca5d0d93977425f979033cf1942c36ed8266a9f8181cd79c3143ef5b3c32d5bf2bbe7d77bb9a01f4247f523045275a01e603f436412c1486a6f8fecf0')

build() {
  cd ${_pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${_pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${_pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
