# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Eduard Tolosa <edu4rdshl@protonmail.com>

pkgname=findomain
pkgver=1.5.0
pkgrel=1
pkgdesc='The fastest and cross-platform subdomain enumerator, do not waste your time'
url='https://github.com/Edu4rdSHL/findomain'
arch=('x86_64')
license=('GPL3')
depends=('gcc-libs')
makedepends=('cargo')
optdepends=('postgresql: for subdomain monitoring')
source=("https://github.com/Edu4rdSHL/${pkgname}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('d29e5e71170e6c3edcbc29204f69cee600cc37f22d2f5a667094d31c8009a236bbbbfb0000a68cb8c2a4043043c0338b4d8cfb8539034895836a2913a2b593b5')
b2sums=('b7985939b37ce5876b4423f06274b84ab3e2c1482391ba52ea11cd698d587e05262b53fe6d6c81e68d73ce61397060e31bdd54fce87531bb30d147dd1bf01ba1')

build() {
  cd ${pkgname}-${pkgver}
  cargo build --release --locked
}

check() {
  cd ${pkgname}-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}-${pkgver}

  install -Dm 755 "target/release/${pkgname}" -t "${pkgdir}/usr/bin"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.1" -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
