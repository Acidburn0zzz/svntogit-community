# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.0.10
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gtk2' 'gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-tool-kit' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory')
makedepends=('git' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('53ee02546ff27eab2e3504472d9d9ce5edc4eaf4266fa5ce53fb5f7ef4ef4050ca4b6a53f8be272b951857286399dc4003972e027d19bdd24f668ae890c3fbac')

build(){
  cd dde-launcher-$pkgver
  qmake-qt5 PREFIX=/usr WITHOUT_UNINSTALL_APP=1
  make
}

package() {
  cd dde-launcher-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
