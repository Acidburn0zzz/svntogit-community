# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=5.3.0.23
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'deepin-qt5integration' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory' 'xdg-user-dirs')
makedepends=('cmake' 'ninja' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('a443264607c3f4fa15bc4e6f00f608cf3d87bd940d7cb9f5d27f63b36055ecce265e6558a49fe2ca231d9f9ec56637ff36ae45c83c0335901784ed9192f55294')

prepare() {
  cd dde-launcher-$pkgver
  sed -i 's|/usr/share/backgrounds/default_background.jpg|/usr/share/backgrounds/deepin/desktop.jpg|' src/boxframe/*.cpp

  sed -i '/include <QPainter>/a #include <QPainterPath>' src/widgets/miniframenavigation.cpp src/widgets/avatar.cpp src/widgets/miniframebutton.cpp
}

build() {
  cd dde-launcher-$pkgver
  cmake . -GNinja -DCMAKE_INSTALL_PREFIX=/usr -DWITHOUT_UNINSTALL_APP=
  ninja
}

package() {
  cd dde-launcher-$pkgver
  DESTDIR="$pkgdir" ninja install
}
