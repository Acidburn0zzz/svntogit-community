# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Josip Ponjavic <josipponjavic at gmail dot com>
# Contributor: Xu Fasheng <fasheng.xu[AT]gmail.com>

pkgname=deepin-launcher
pkgver=4.2.1
pkgrel=1
pkgdesc='Deepin desktop-environment - Launcher module'
arch=('i686' 'x86_64')
url="https://github.com/linuxdeepin/dde-launcher"
license=('GPL3')
depends=('gsettings-qt' 'qt5-svg' 'qt5-x11extras' 'startdde' 'dtkwidget' 'deepin-menu'
         'deepin-daemon' 'deepin-qt-dbus-factory')
makedepends=('git' 'qt5-tools')
conflicts=('dde-launcher')
replaces=('dde-launcher')
groups=('deepin')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/dde-launcher/archive/$pkgver.tar.gz")
sha512sums=('db91b0abcb5876a84a34bc141e888cb0e6ab2f863a1ec8c8591a4069617d2813eac4f429a915316f1e5087d64c2229ab22b8803fadc2cf6aa5ad836bf04a9862')

build(){
  cd dde-launcher-$pkgver
  qmake-qt5 PREFIX=/usr WITHOUT_UNINSTALL_APP=1
  make
}

package() {
  cd dde-launcher-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
