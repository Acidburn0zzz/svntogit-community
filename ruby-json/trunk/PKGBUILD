# Maintainer: David Runge <dvzrv@archlinux.org>

_name=json
pkgname=ruby-json
pkgver=2.6.0
pkgrel=1
pkgdesc="JSON implementation for Ruby"
arch=('x86_64')
url="https://github.com/flori/json"
license=('BSD')
depends=('ruby')
makedepends=('ragel' 'ruby-rake' 'ruby-rdoc')
checkdepends=('git' 'ruby-bundler' 'ruby-test-unit')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/flori/${_name}/archive/v${pkgver}.tar.gz")
sha512sums=('37bd455fc9b63962ec47858ffd5ac840196033e78efc85722363f4f7c1113f1323afccb4593e0cf022ebac70a59b1959ad59086c7de2c7a862319335c8411fd6')
b2sums=('20d6818faf7efb7f1b04d958c3315eb70ebe44ef93adb18973da722f113ff3295c3faaa79cab95f6bc740a0c318d02dbcf9c80d8ca7672f48671ef73ae62042e')

prepare() {
  mv -v "${_name}-${pkgver}" "${pkgname}-${pkgver}"
  cd "${pkgname}-${pkgver}"
  # we don't do version pinning
  sed -r 's|~>|>=|g' -i *.gemspec Gemfile Rakefile
}

build() {
  cd "${pkgname}-${pkgver}"
  gem build "${_name}.gemspec"
}

check(){
  cd "${pkgname}-${pkgver}"
  rake test
}

package() {
  cd "${pkgname}-${pkgver}"
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies \
              --no-user-install \
              -i "${pkgdir}/${_gemdir}" \
              -n "${pkgdir}/usr/bin" \
              ${_name}-${pkgver}.gem
  install -vDm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  # removing all unneeded stuff
  cd "${pkgdir}/${_gemdir}"
  rm -rvf cache \
    "doc/${_name}-${pkgver}/ri/ext/" \
    extensions/*/*/${_name}-${pkgver}/{mkmf.log,gem_make.out} \
    gems/${_name}-${pkgver}/{ext,lib/*/*.so,*.gemspec}
}
