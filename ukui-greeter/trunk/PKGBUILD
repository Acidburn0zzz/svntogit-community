# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ukui-greeter
pkgver=1.1.10
pkgrel=1
pkgdesc="Lightdm greeter for UKUI"
arch=('x86_64')
license=('GPL')
url="https://github.com/ukui/ukui-greeter"
groups=('ukui')
depends=('imlib2' 'liblightdm-qt5' 'libxrandr' 'libxtst' 'qt5-svg' 'qt5-x11extras')
makedepends=('qt5-tools')
source=("$pkgname-$pkgver.tar.gz::https://github.com/ukui/ukui-greeter/archive/v$pkgver.tar.gz"
        ukui-greeter-1.patch::https://github.com/ukui/ukui-greeter/pull/1.patch)
sha512sums=('37434e758dac8301ff50454b49ea092f33acf1aa992a6a84f406432ea09a4b832c5333fc9952cc3801999b02aa04e3515efeb62df280b6b5897b56c9a7de655c'
            '7d75be763f242a88b572af4792d101a8261601bc9956e18ce2cffbcd38f4ca43d6f1fd3993e56df61f1cd6240cb9794292532b821c308e79b1974afbb1fc9025')

prepare() {
  cd ukui-greeter-$pkgver
  patch -p1 -i ../ukui-greeter-1.patch || :

  sed -i 's|target.path = /usr/sbin/|target.path = /usr/bin/|' ukui-greeter/ukui-greeter.pro
}

build() {
  cd ukui-greeter-$pkgver
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd ukui-greeter-$pkgver
  make INSTALL_ROOT="$pkgdir" install
}
