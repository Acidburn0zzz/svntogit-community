# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20200101
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txthinking/brook/archive/v$pkgver.tar.gz")
sha512sums=('1c2c7db12a7a459abf12ddad27b8bbfffd4c12b7409e5d51c97d70bf3de64bbac0f9c774c31125c856845914b7748327e2b1c67e4764b96f05fa7ef095ddbd48')

prepare() {
  cd brook-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/txthinking
  ln -sf "$PWD" .gopath/src/github.com/txthinking/brook
  export GOPATH="$PWD/.gopath"

  go get github.com/mdp/qrterminal github.com/patrickmn/go-cache github.com/txthinking/ant \
         github.com/txthinking/socks5 golang.org/x/net/proxy github.com/gorilla/mux \
         github.com/urfave/cli github.com/txthinking/gotun2socks github.com/gorilla/websocket \
         github.com/urfave/negroni
}

build() {
  cd brook-$pkgver/cli/brook
  go build -ldflags "-extldflags ${LDFLAGS}" -o brook
}

package() {
  cd brook-$pkgver/cli/brook
  install -Dm755 brook "$pkgdir"/usr/bin/brook
}
