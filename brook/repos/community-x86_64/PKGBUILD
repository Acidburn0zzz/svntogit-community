# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20200102
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txthinking/brook/archive/v$pkgver.tar.gz")
sha512sums=('dd1c35ffdc6a5c6171459c356ab6c84f83720828c1910e12498828ef838a92f89f95cc21ae7328d954e4c724aa1263aeee9426d849c29e4a85b29c258414c5ba')

prepare() {
  cd brook-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/txthinking
  ln -sf "$PWD" .gopath/src/github.com/txthinking/brook
  export GOPATH="$PWD/.gopath"

  go get github.com/mdp/qrterminal github.com/patrickmn/go-cache github.com/txthinking/ant \
         github.com/txthinking/socks5 golang.org/x/net/proxy github.com/gorilla/mux \
         github.com/urfave/cli github.com/txthinking/gotun2socks github.com/gorilla/websocket \
         github.com/urfave/negroni
}

build() {
  cd brook-$pkgver/cli/brook
  go build -ldflags "-extldflags ${LDFLAGS}" -o brook
}

package() {
  cd brook-$pkgver/cli/brook
  install -Dm755 brook "$pkgdir"/usr/bin/brook
}
