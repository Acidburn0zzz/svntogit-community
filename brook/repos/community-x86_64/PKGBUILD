# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Kasei Wang <cnsdwpc at gmail.com>

pkgname=brook
pkgver=20171113
pkgrel=1
pkgdesc="A cross-platform proxy/vpn software"
arch=('x86_64')
url="https://github.com/txthinking/brook"
license=('GPL')
depends=('glibc')
makedepends=('go-pie' 'git')
source=("$pkgname-$pkgver.tar.gz::https://github.com/txthinking/brook/archive/v$pkgver.tar.gz")
sha512sums=('b0492bfdc241aa62533d7be90ad5777b77bab705df2d5a9fbad018dce6b093a7dfb0181144ed98beb9550068531ab2e5b23efd2e941d5b30755edab1156fd186')

prepare() {
  cd brook-$pkgver

  mkdir build
  mkdir -p .gopath/src/github.com/txthinking
  ln -sf "$PWD" .gopath/src/github.com/txthinking/brook
  export GOPATH="$PWD/.gopath"

  go get github.com/mdp/qrterminal github.com/patrickmn/go-cache github.com/txthinking/ant \
         github.com/txthinking/pac/blackwhite github.com/txthinking/socks5 golang.org/x/net/proxy
}

build() {
  cd brook-$pkgver
  go build -o build/brook
}

package() {
  cd brook-$pkgver
  install -Dm755 build/brook "$pkgdir"/usr/bin/brook
}
