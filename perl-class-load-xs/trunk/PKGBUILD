# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: 3ED <krzysztof1987 at gmail dot com>

pkgname=perl-class-load-xs
_pkgname=Class-Load-XS
pkgver=0.09
pkgrel=4
pkgdesc="XS implementation of parts of Class::Load"
arch=('i686' 'x86_64')
license=('PerlArtistic2')
url="https://metacpan.org/release/Class-Load-XS"
depends=('perl-class-load')
checkdepends=('perl-module-implementation' 'perl-test-fatal' 'perl-test-requires'
              'perl-test-without-module')
options=('!emptydirs')
source=(https://cpan.metacpan.org/authors/id/E/ET/ETHER/Class-Load-XS-${pkgver}.tar.gz)
sha512sums=('9b24637cf7b035ba369f05e42c0bd5a8590e001f2395b46df078635a50bb3df58ad57850be0010d092ae2530d9a545bbcc5f0055f6ae7f166c93905c022d469d')

build() {
  cd Class-Load-XS-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd Class-Load-XS-$pkgver
  make test
}

package() {
  cd Class-Load-XS-$pkgver
  make DESTDIR="$pkgdir" install
}
