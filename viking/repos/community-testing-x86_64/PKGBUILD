# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Nick Ã˜stergaard <oe.nick@gmail.com>
# Contributor: Jonny Gerold <jonny@fsk141.com>

pkgname=viking
pkgver=1.6.2
pkgrel=7
pkgdesc="GTK+2 application to manage GPS data"
arch=('x86_64')
url="https://sourceforge.net/projects/viking/"
license=('GPL2')
depends=('curl' 'file' 'gpsd' 'gtk2' 'libgexiv2' 'mapnik')
makedepends=('boost' 'gnome-doc-utils' 'intltool')
optdepends=('gpsbabel: import and export GPS tracks from various formats')
options=('!emptydirs')
changelog=$pkgname.changelog
source=(https://downloads.sourceforge.net/$pkgname/$pkgname-$pkgver.tar.bz2
        remove-mapquest.patch
        viking-1.6.2-gpsd-3.18-fix.patch)
sha256sums=('dfe223b1cd3a1fed3616a70e732321db9013c89df3fd6e1ad0f733a1bb067826'
            '07d78e7b86a494a3bb3e0389e4a8b42fc2f916aaa66e5b06fc9146ebfa932dc5'
            '78cc5cd6ea8ea38c6fc6be589b7828b30295ea2925f16fa89f2ae1dd15a49c88')

prepare() {
  cd "${srcdir}"/$pkgname-$pkgver

# Remove discontinued mapquest service
  patch -Np1 -i "${srcdir}"/remove-mapquest.patch

# gpsd 3.18 build fix, with the help of
# https://github.com/viking-gps/viking/commit/d570cfcfc643b701dd71e8b718784e02c953dace
  patch -Np0 -i "${srcdir}"/viking-1.6.2-gpsd-3.18-fix.patch
}

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  export CXXFLAGS="$CXXFLAGS -I/usr/include/mapnik"
  ./configure --prefix=/usr --enable-expedia --enable-geocaches
  make
}

package(){
  cd "${srcdir}"/$pkgname-$pkgver

  make DESTDIR="${pkgdir}" install
}
