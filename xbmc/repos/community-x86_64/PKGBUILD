# Contributor: [vEX] <niechift.dot.vex.at.gmail.dot.com>
# Contributor: Zeqadious <zeqadious.at.gmail.dot.com>

pkgname=xbmc
pkgver=9.11_rc1
pkgrel=1
pkgdesc="XBMC Media Center"
arch=('i686' 'x86_64')
url="http://xbmc.org"
license=('GPL' 'custom')
depends=('alsa-lib' 'curl' 'enca' 'faac' 'freetype2' 'fribidi' 'gawk' 'glew'
         'hal' 'jasper' 'libgl' 'libjpeg>=6b-5' 'libmad' 'libmysqlclient'
         'libxinerama' 'libxrandr' 'lzo2' 'sdl_image' 'sdl_mixer' 'sqlite3'
         'tre' 'unzip' 'x-server' 'libcdio' 'wavpack' 'libmpeg2' 'a52dec'
         'smbclient' 'faad2' 'libsamplerate' 'libmms')
makedepends=('autoconf' 'automake' 'boost' 'cmake' 'gcc' 'gperf'
	     'libtool>=2.2.6a-1' 'make' 'nasm' 'patch' 'pkgconfig' 'zip'
	     'libvdpau')
optdepends=('libcdio: optical disc support'
            'lirc: remote controller support'
            'pmount: mount removable devices as normal user'
            'smbclient: access windows shares'
            'unrar: access compressed files without unpacking them')
install=("${pkgname}.install")
source=(http://downloads.sourceforge.net/project/xbmc/XBMC%20Source%20Code/pre-release/xbmc-${pkgver/_/-}.tar.gz)
md5sums=('dc781afe285cc550dccae3be6e05c052')

build() {
    cd "${srcdir}/xbmc-${pkgver/_/-}"
    _xbmcprefix=/opt/xbmc

    if [ $NOEXTRACT -ne 1 ]; then
#    if true; then
    ./bootstrap
    ./configure --prefix=${_xbmcprefix} \
                --enable-vdpau \
                --disable-pulse \
                --disable-avahi \
                --enable-external-liba52 \
                --enable-external-libdts \
                --enable-external-libmpeg2 \
                --enable-external-libogg \
                --enable-external-libwavpack \
                --disable-external-libass \
                --disable-external-ffmpeg \
                --disable-external-python \
                --disable-debug || return 1
    fi

    make || return 1
    make prefix=${pkgdir}${_xbmcprefix} install || return 1

    # Fix the shell script
    sed -i '3iexport SDL_AUDIODRIVER=alsa' ${pkgdir}${_xbmcprefix}/bin/xbmc

    # Menu item
    install -Dm644 ${srcdir}/xbmc-${pkgver/_/-}/tools/Linux/xbmc.desktop \
                   ${pkgdir}/usr/share/applications/xbmc.desktop || return 1
    install -Dm644 ${srcdir}/xbmc-${pkgver/_/-}/tools/Linux/xbmc.png \
                   ${pkgdir}/usr/share/pixmaps/xbmc.png || return 1

    # XBMCTex
#    install -Dm755 ${srcdir}/xbmc-${pkgver/_/-}/tools/XBMCTex/XBMCTex \
#                   ${pkgdir}${_xbmcprefix}/share/xbmc/tools/xbmctex || return 1

    # License(s)
    install -dm755 ${pkgdir}/usr/share/licenses/${pkgname}
    for licensef in LICENSE.GPL README.linux copying.txt; do
        mv ${pkgdir}${_xbmcprefix}/share/xbmc/${licensef} \
           ${pkgdir}/usr/share/licenses/${pkgname} || return 1
    done

    # profile.d
    mkdir -p $pkgdir/etc/profile.d/ && \
    echo "export PATH=\$PATH:${_xbmcprefix}/bin" >$pkgdir/etc/profile.d/xbmc.sh && \
    chmod 0755 $pkgdir/etc/profile.d/xbmc.sh || return 1

    # fix .desktop
    sed -i 's#Exec=xbmc#Exec=/opt/xbmc/bin/xbmc#' $pkgdir/opt/xbmc/share/xsessions/XBMC.desktop $pkgdir/usr/share/applications/xbmc.desktop
}
