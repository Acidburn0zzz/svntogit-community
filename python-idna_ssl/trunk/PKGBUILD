# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgbase=python-idna_ssl
pkgname=('python-idna_ssl' 'python2-idna_ssl')
pkgver=1.0.0
pkgrel=1
pkgdesc="Patch ssl.match_hostname for Unicode(idna_ssl) domains support"
arch=('any')
license=('BSD')
url="https://github.com/aio-libs/idna_ssl"
makedepends=('python-setuptools' 'python0-setuptools')
source=("https://pypi.io/packages/source/i/idna_ssl/idna_ssl-$pkgver.tar.gz")
sha512sums=('cb5dbfab44c4d11521c67f9d29391f184d1267a3da6dc89f4ed12c60a7a909d5c7474c3ea2bddd0af7063f4f620e87a8dd586bb07e8b961b30b1dd7c969704c2')

prepare() {
   cp -a idna_ssl-$pkgver{,-py2}
}

build() {
   cd "$srcdir"/idna_ssl-$pkgver
   python setup.py build

   cd "$srcdir"/idna_ssl-$pkgver-py2
   python2 setup.py build
}

check() {
   cd "$srcdir"/idna_ssl-$pkgver
   python setup.py test

   cd "$srcdir"/idna_ssl-$pkgver-py2
   python2 setup.py test
}

package_python-idna_ssl() {
   depends=('python')

   cd idna_ssl-$pkgver
   python setup.py install --root="$pkgdir" --optimize=1 --skip-build
   install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}

package_python2-idna_ssl() {
   depends=('python2')

   cd idna_ssl-$pkgver-py2
   python2 setup.py install --root="$pkgdir" --optimize=1 --skip-build
   install -Dm644 LICENSE.rst "$pkgdir"/usr/share/licenses/$pkgname/LICENSE.rst
}
