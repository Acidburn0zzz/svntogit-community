# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>

pkgname=iwd
pkgver=0.16
pkgrel=2
pkgdesc='Internet Wireless Daemon'
arch=('x86_64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('glibc')
source=("https://www.kernel.org/pub/linux/network/wireless/iwd-${pkgver}.tar"{.xz,.sign}
        'iwd.tmpfiles')
sha512sums=('33b10516fb203eb92651e18ebff8448874452fad9425470bf4306b4d75a512d386561ed4fcd251c54175ac6953e29f6574175e4e9b18c88e55009f670f29ddb1'
            'SKIP'
            '2d6cec32def7e1faabdba0bce327fb02a4dcf25b23c812be0dc8ed062d95731046b7934fd3296a1007a9e5bc685ae2d8398b77aea63a2d0c5740c98f92facf81')
validpgpkeys=('E932D120BC2AEC444E558F0106CA9F5D1DCF2659')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
	./configure --prefix=/usr \
	--sysconfdir=/etc \
	--libexecdir=/usr/lib/iwd \
	--enable-wired \
	--localstatedir=/var
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make install DESTDIR="${pkgdir}"
}

# vim: set ts=2 sw=2 ft=sh noet:
