# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>
# Contributor: Guillaume Horel <guillaume.horel@gmail.com>

pkgname=python-moto
_pkgname=moto
pkgver=1.3.8
pkgrel=1
pkgdesc='Moto is a library to mock out the boto library.'
arch=(any)
url='https://github.com/spulec/moto'
license=(Apache)
depends=(python-aws-xray-sdk python-boto python-boto3
         python-cryptography python-dateutil python-docker python-jinja
         python-jsondiff python-jose python-mock python-pyaml
         python-pytz python-requests python-responses python-xmltodict
         python-werkzeug python-cfn-lint)
checkdepends=(python-flask python-freezegun python-nose python-sure tk)
makedepends=(python-setuptools)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/spulec/moto/archive/$pkgver.tar.gz")
sha256sums=('4463fd4af94501285b3e45b6ba341a9fea17847c3656dc63d2591da4b4ec12b8')

prepare() {
  cd $_pkgname-$pkgver

  # Remove upper bounds of dependencies
  sed --in-place=.orig -r 's#<[0-9.]+,?##;s#==([0-9.]+)#>=\1#' setup.py
}

build() {
  cd $_pkgname-$pkgver

  python setup.py build
}

check() {
  cd $_pkgname-$pkgver

  TZ=UTC nosetests -sv ./tests/ --exclude='test_iot.*' --exclude='test_lambda.*'
}

package() {
  cd $_pkgname-$pkgver

  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
