# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: sqlwwx <wwx_2012@live.com>
# Contributors: Eduardo San Martin Morote http://posva.net/

pkgname=catimg
pkgver=2.6.0
pkgrel=2
pkgdesc="Print images in a terminal with 256 colors support"
arch=('x86_64')
url="https://github.com/posva/catimg"
license=('MIT')
depends=('glibc')
makedepends=('cmake')
source=("$pkgname-$pkgver.tar.gz::https://github.com/posva/catimg/archive/$pkgver.tar.gz")
sha512sums=('a3c0ed92a797dc6e213e202cf36d4da32f4baba0ba4dc60875f4df152e3df10ec2276723e10826ce13ac1ddb4f32f75e0d6851767003032ecaa73fe0851e6559')

build() {
	cd $pkgname-$pkgver
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_BUILD_TYPE=Release .
	make
}

package() {
	cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
