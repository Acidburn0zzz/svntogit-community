# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgbase=python-ujson
pkgname=('python-ujson' 'python2-ujson')
_pkgname=ultrajson
pkgver=1.35
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/esnme/ultrajson"
arch=('i686' 'x86_64')
license=('BSD')
makedepends=('python-setuptools' 'python2-setuptools')
source=($pkgbase-$pkgver.tar.gz::https://github.com/esnme/ultrajson/archive/v$pkgver.tar.gz)
sha256sums=('1e7761583065873bed8466a3692fa5539d4f15bebc7af1c8fcc63d322a46804f')

prepare() {
  cp -ra "$srcdir"/$_pkgname-$pkgver{,-py2}
  2to3 -w "$srcdir"/$_pkgname-$pkgver-py2/tests/tests.py
}

build() {
  cd "$srcdir"/$_pkgname-$pkgver
  mkdir ../buildpy3
  python setup.py build -b ../buildpy3

  cd "$srcdir"/$_pkgname-$pkgver-py2
  mkdir ../buildpy2
  python2 setup.py build -b ../buildpy2
}

check() {
  cd "$srcdir"/$_pkgname-$pkgver
  # TODO: tests are currently broken
  #local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  #PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
    #python tests/tests.py

  cd "$srcdir"/$_pkgname-$pkgver-py2
  # TODO: tests are currently broken
  #local PYTHONVERSION="$(python2 -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  #PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" \
    #python2 tests/tests.py
}

package_python-ujson() {
  depends=('python')

  cd "$srcdir"/$_pkgname-$pkgver
  rm -rf build; ln -s ../buildpy3 build
  python setup.py install --skip-build -O1 --root="$pkgdir"
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.rst "$pkgdir/usr/share/doc/$pkgname/README"
}

package_python2-ujson() {
  depends=('python2')

  cd "$srcdir"/$_pkgname-$pkgver-py2
  rm -rf build; ln -s ../buildpy2 build
  python2 setup.py install --skip-build -O1 --root="$pkgdir"
  install -Dm 644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm 644 README.rst "$pkgdir/usr/share/doc/$pkgname/README"
}

# vim: ts=2 sw=2 et:
