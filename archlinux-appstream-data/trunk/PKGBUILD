# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=archlinux-appstream-data
pkgver=20170425
pkgrel=1
pkgdesc="Arch Linux application database for AppStream-based software centers"
arch=(any)
url="http://www.archlinux.org"
license=(GPL)
depends=()
makedepends=()
source=(https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}.xml.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-64x64.tar.gz
 https://sources.archlinux.org/other/community/archlinux-appstream-data/$pkgver/{core,extra,community}-icons-128x128.tar.gz)
noextract=({core,extra,community}.xml.gz {core,extra,community}-icons-{64x64,128x128}.tar.gz)
md5sums=('4e6b4a4e03d5ed6f4914939c86ef69c9'
         '8646fbdadf57ed93e4bf48a8c7b2fdda'
         '72478d27d42ed15d9f774af3e9c67062'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '84bb8cf842f3246f19e978e3ebce51af'
         '959515dc18838d9f4fbd99d1dec984ea'
         '31f6566d35ccd604be46ed5b1f813cdf'
         '50bfaff5bab40420c53236930784d65a'
         'd842c646fe684cd083b1b4b3920b26fa')

package() {
  mkdir -p "$pkgdir"/usr/share/app-info/{icons/archlinux-arch-{core,extra,community}/{64x64,128x128},xmls}
  for _repo in core extra community; do
   tar -xzf $_repo-icons-64x64.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/64x64
   tar -xzf $_repo-icons-128x128.tar.gz -C "$pkgdir"/usr/share/app-info/icons/archlinux-arch-$_repo/128x128
  done
  cp *.xml.gz "$pkgdir"/usr/share/app-info/xmls/
}
