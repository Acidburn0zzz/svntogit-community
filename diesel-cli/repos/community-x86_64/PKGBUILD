# Maintainer: kpcyrd <git@rxv.cc>

pkgname=diesel-cli
pkgver=1.4.3
pkgrel=2
pkgdesc="CLI for the Diesel crate"
arch=('x86_64')
url="http://diesel.rs/"
license=('MIT' 'Apache')
replaces=('diesel_cli')
depends=('sqlite' 'postgresql-libs' 'libmariadbclient')
makedepends=('cargo')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/diesel-rs/diesel/archive/v${pkgver}.tar.gz")
sha512sums=('afc348b362b3cfea88b161f17897ef0d7ca06c9e9624e09a5210428a95f351fe774a9bd98a1009b1d334abecd1800d9a266b9ddbab42ff77fbfba1e66ad501fc')
b2sums=('3357d9b0ade8142e6d0529aa128a38ddd255c1fdf7fb8567f7391122ba8157c40ce90dc94ae939997160dcdbe72c41fc9aa5b38f55ebddd4caba4ee76770be23')

build() {
  cd "diesel-${pkgver}/diesel_cli"
  # --locked is broken
  cargo build --release
}

check() {
  cd "diesel-${pkgver}/diesel_cli"
  # tests require a debug build to be present
  cargo test --no-default-features --features sqlite
}

package() {
  cd "diesel-${pkgver}"
  install -Dm755 "target/release/diesel" "${pkgdir}/usr/bin/diesel"

  install -d "${pkgdir}/usr/share/bash-completion/completions" \
             "${pkgdir}/usr/share/zsh/site-functions" \
             "${pkgdir}/usr/share/fish/vendor_completions.d"
  "${pkgdir}/usr/bin/diesel" completions bash > "${pkgdir}/usr/share/bash-completion/completions/diesel"
  "${pkgdir}/usr/bin/diesel" completions zsh > "${pkgdir}/usr/share/zsh/site-functions/_diesel"
  "${pkgdir}/usr/bin/diesel" completions fish > "${pkgdir}/usr/share/fish/vendor_completions.d/diesel.fish"

  install -Dm644 LICENSE-MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
