# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsboat
pkgver=2.22
pkgrel=1
pkgdesc="An RSS/Atom feed reader for text terminals"
arch=('x86_64')
url="https://newsboat.org/"
license=('MIT')
depends=('curl' 'hicolor-icon-theme' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('asciidoctor' 'rust' 'swig')
optdepends=('perl: for pinboard.pl'
            'python: for exportOPMLWithTags.py'
            'ruby: for feedgrabber.rb, heise.rb, and slashdot.rb')
replaces=('newsbeuter')
options=('!makeflags')
changelog=$pkgname.changelog
source=("https://newsboat.org/releases/$pkgver/$pkgname-$pkgver.tar.xz"{,.asc}
        'https://github.com/newsboat/newsboat/commit/d3485aab30a55dcf597408a45a17f17b2cb0d652.patch')
sha256sums=('5286f815d9a00b4752a5572d99bbd9bc512b69c06931453faa415968881cd790'
            'SKIP'
            '2ec829531655779213d525065b5315821da099acd7261e97c790c5daa0023e65')
validpgpkeys=('B8B1756A0DDBF0760CE67CCF4ED6CD61932B9EBE') # Newsboat project <newsboat@googlegroups.com>

prepare() {
  cd $pkgname-$pkgver

  # Fix https://github.com/newsboat/newsboat/issues/1372
  patch --forward --strip=1 --input=../d3485aab30a55dcf597408a45a17f17b2cb0d652.patch
}

build() {
  cd $pkgname-$pkgver

  make prefix=/usr
  make doc
}

package() {
  cd $pkgname-$pkgver

  make prefix=/usr DESTDIR="$pkgdir" install
  install -Dm644 -t "$pkgdir"/usr/share/licenses/$pkgname LICENSE
}
