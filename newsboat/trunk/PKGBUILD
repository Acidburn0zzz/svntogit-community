# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsboat
pkgver=2.10
pkgrel=1
pkgdesc="An RSS/Atom feed reader for text terminals"
arch=('i686' 'x86_64')
url="https://newsboat.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext')
replaces=('newsbeuter')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://newsboat.org/releases/$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('B8B1756A0DDBF0760CE67CCF4ED6CD61932B9EBE') #Newsboat project <newsboat@googlegroups.com>
sha256sums=('11a4bbb95467a4dd5b10d737e4dff005a0d175f98afd0aed65c88314ae28a65b'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
