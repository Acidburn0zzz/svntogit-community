# $Id$
# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sven Pfleiderer <pfleidi@roothausen.de>

pkgname=newsboat
pkgver=2.11.1
pkgrel=2
pkgdesc="An RSS/Atom feed reader for text terminals"
arch=('x86_64')
url="https://newsboat.org/"
license=('custom: MIT')
depends=('curl' 'json-c' 'libxml2' 'sqlite' 'stfl')
makedepends=('swig' 'gettext' 'asciidoc')
replaces=('newsbeuter')
changelog=$pkgname.changelog
options=('!emptydirs')
source=(https://newsboat.org/releases/$pkgver/$pkgname-$pkgver.tar.xz{,.asc})
validpgpkeys=('B8B1756A0DDBF0760CE67CCF4ED6CD61932B9EBE') #Newsboat project <newsboat@googlegroups.com>
sha512sums=('265b83828eb3da6e61b14b6d16012baf3de147b777fa09a8cad0e76d4cf54677389bf89abf10f94a644def57f41694869a6ea07465e7cc97ec12df74d939bb5a'
            'SKIP')

build() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr
  make doc
}

package() {
  cd "${srcdir}"/$pkgname-$pkgver

  make prefix=/usr DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/$pkgname/LICENSE
}
