# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thomas Dziedzic <gostrc@gmail.com>

_hkgname=hasql
pkgname=haskell-hasql
pkgver=0.19.15.2
pkgrel=13
pkgdesc="An efficient PostgreSQL driver and a flexible mapping API"
url="https://github.com/nikita-volkov/hasql"
license=('MIT')
arch=('i686' 'x86_64')
depends=('ghc=8.0.1' 'haskell-aeson' 'haskell-attoparsec' 'haskell-base-prelude'
         'haskell-bytestring-tree-builder' 'haskell-contravariant' 'haskell-contravariant-extras'
         'haskell-data-default-class' 'haskell-dlist' 'haskell-either' 'haskell-hashable'
         'haskell-hashtables' 'haskell-loch-th' 'haskell-mtl' 'haskell-placeholders'
         'haskell-postgresql-binary' 'haskell-postgresql-libpq' 'haskell-profunctors'
         'haskell-scientific' 'haskell-semigroups' 'haskell-text' 'haskell-uuid' 'haskell-vector')
source=("https://hackage.haskell.org/packages/archive/${_hkgname}/${pkgver}/${_hkgname}-${pkgver}.tar.gz")
md5sums=('c82ea1c8858ebeae9c4f283910310ec2')

build() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    runhaskell Setup configure -O --enable-library-profiling --enable-shared \
        --prefix=/usr --docdir="/usr/share/doc/${pkgname}" \
        --libsubdir=\$compiler/site-local/\$pkgid
    runhaskell Setup build
    runhaskell Setup haddock --hoogle --html
    runhaskell Setup register --gen-script
    runhaskell Setup unregister --gen-script
    sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
    sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
    cd "${srcdir}/${_hkgname}-${pkgver}"
    
    install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
    install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
    install -d -m755 "${pkgdir}/usr/share/doc/ghc/html/libraries"
    ln -s "/usr/share/doc/${pkgname}/html" "${pkgdir}/usr/share/doc/ghc/html/libraries/${_hkgname}"
    runhaskell Setup copy --destdir="${pkgdir}"
    install -D -m644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    rm -f "${pkgdir}/usr/share/doc/${pkgname}/LICENSE"
}
