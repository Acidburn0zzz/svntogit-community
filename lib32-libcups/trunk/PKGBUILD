# $Id$
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_pkgbasename=libcups
pkgname=lib32-$_pkgbasename
pkgver=1.5.0
pkgrel=1
pkgdesc="The CUPS Printing System - client libraries (32-bit)"
arch=('x86_64')
license=('GPL')
url="http://www.cups.org/"
depends=(lib32-krb5 lib32-libtiff lib32-libpng $_pkgbasename)
makedepends=(gcc-multilib)
source=(ftp://ftp.easysw.com/pub/cups/${pkgver}/cups-${pkgver}-source.tar.bz2)
md5sums=('e54ed09ede2340fc3014913333520fe4')

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${srcdir}/cups-${pkgver}
  
  aclocal -I config-scripts
  autoconf -I config-scripts

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
     --disable-ldap --enable-raw-printing --disable-gssapi --disable-dbus \
     --enable-ssl=no --disable-gnutls --enable-threads \
     --with-optim="$CFLAGS" --libdir=/usr/lib32
  make
}

package() {
  cd ${srcdir}/cups-${pkgver}
  make BUILDROOT=${pkgdir} install-libs
}

