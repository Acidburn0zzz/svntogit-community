# $Id$
# Maintainer: Dave Reisner <dreisner@archlinux.org>

pkgname=pkgfile
pkgver=6
pkgrel=1
pkgdesc="a pacman .files metadata explorer"
arch=('i686' 'x86_64')
url="http://github.com/falconindy/pkgfile"
license=('MIT')
depends=('libarchive' 'curl' 'pcre' 'pacman')
conflicts=('pkgtools<24-2')
makedepends=('perl')
source=("https://github.com/downloads/falconindy/$pkgname/$pkgname-$pkgver.tar.xz"{,.sig})
install=pkgfile.install
md5sums=('75139dd8441d60ee7682d900b610e189'
         '75cca8e3c0198fa98e32b54fd5e936e6')

build() {
  cd "$pkgname-$pkgver"
  ./configure
  make

  # generate a license file
  sed '/\*\//q' src/pkgfile.c >LICENSE
}

package() {
  cd "$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim: ft=sh syn=sh
