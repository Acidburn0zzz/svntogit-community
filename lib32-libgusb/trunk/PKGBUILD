# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: jtts <jussaar@mbnet.fi>
# Contributor: josephgbr <rafael.f.f1@gmail.com>

pkgname=lib32-libgusb
pkgver=0.3.2
pkgrel=3
pkgdesc='GObject wrapper for libusb1'
url=https://github.com/hughsie/libgusb
arch=(x86_64)
license=(LGPL2.1)
depends=(
  lib32-glib2
  lib32-libusb
  libgusb
)
makedepends=(
  git
  gobject-introspection
  gtk-doc
  meson
  vala
)
source=(git+https://github.com/hughsie/libgusb#tag=bda40aca1f11f841524ad6ed16c352329b8a8d41)
sha256sums=(SKIP)

prepare() {
  cd libgusb

  # https://github.com/hughsie/libgusb/issues/24
  git cherry-pick -n 3813921b7248a5f8a340f3395ff0ddf304bcf64e
}

pkgver() {
  cd libgusb

  git describe --tags
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG='i686-pc-linux-gnu-pkg-config'

  arch-meson libgusb build \
    --libdir='/usr/lib32'
  ninja -C build
}

package() {
  DESTDIR="${pkgdir}" ninja -C build install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
