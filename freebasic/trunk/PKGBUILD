# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>
# Contributor: rabyte <gmail.com: rabyte>

pkgname=freebasic
pkgver=1.07.0
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('x86_64')
url="https://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
makedepends=('libxpm' 'gpm' 'libffi' 'libxrandr' 'mesa')
source=("https://downloads.sourceforge.net/fbc/FreeBASIC-$pkgver-source.tar.xz")
source_x86_64=("https://downloads.sourceforge.net/fbc/FreeBASIC-${pkgver}-linux-x86_64.tar.xz")
md5sums=('3466dfe28aa6d3ccc9088778b8a8e52e')
md5sums_x86_64=('bb37f543ff18d78970849d17a5a08d08')

prepare() {
  [[ "$CARCH" = "x86_64" ]] && _arch=x86_64

  mkdir -p bootstrap
  cd "${srcdir}/FreeBASIC-${pkgver}-linux-$_arch"
  ./install.sh -i "${srcdir}/bootstrap"
  # Hack!
  ln -s /usr/lib/libncursesw.so "${srcdir}/bootstrap/lib/libtinfo.so.5"
}

build() {
  export PATH="${srcdir}/bootstrap/bin:$PATH"
  export LD_LIBRARY_PATH="${srcdir}/bootstrap/lib"

  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make CFLAGS="$CFLAGS $(pkg-config --cflags libffi)"
}

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make install prefix="${pkgdir}/usr"
}
