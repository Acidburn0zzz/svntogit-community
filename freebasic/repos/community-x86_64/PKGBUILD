# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jesse Jaara <gmail.com: jesse.jaara>
# Contributor: rabyte <gmail.com: rabyte>

pkgname=freebasic
pkgver=1.09.0
_bootstrapver=1.07.2
pkgrel=1
pkgdesc="A free BASIC compiler"
arch=('x86_64')
url="https://www.freebasic.net/"
license=('GPL2' 'LGPL')
depends=('ncurses')
makedepends=('freebasic' 'libxpm' 'gpm' 'libffi' 'libxrandr' 'mesa')
source=("https://downloads.sourceforge.net/fbc/FreeBASIC-$pkgver-source.tar.xz")
#source_x86_64=("https://downloads.sourceforge.net/fbc/FreeBASIC-${_bootstrapver}-linux-x86_64.tar.xz")
md5sums=('8c3c3c456bb7657114f20b1ee5a98d55')
sha512sums=('3a572e3366181fd800a029d6e031bef8d5d16de481a286191f3f0d647e57cff54dc65f7e1904abeb23d4aaffb5a401e22725556c59bd40c59ab23d026eeecc27')
#md5sums_x86_64=('e327951b74992d18d2414cc08301c7e7')
#sha512sums_x86_64=('4da46df1763590e3dda739dc2f5c7d1f0e2779227c655210ac0335ec87e64be889b855fff7e82df087ade898825b5074239f33867aba7337112f950dbe76fc3c')

bootstrap() {
  [[ "$CARCH" = "x86_64" ]] && _arch=x86_64

  mkdir -p bootstrap
  cd "${srcdir}/FreeBASIC-${_bootstrapver}-linux-$_arch"
  ./install.sh -i "${srcdir}/bootstrap"
  # Hack!
  ln -s /usr/lib/libncursesw.so "${srcdir}/bootstrap/lib/libtinfo.so.5"
}

prepare() {
  #bootstrap
  :
}

build() {
  export PATH="${srcdir}/bootstrap/bin:$PATH"
  export LD_LIBRARY_PATH="${srcdir}/bootstrap/lib"

  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make CFLAGS="$CFLAGS $(pkg-config --cflags libffi)"
}

package() {
  cd "${srcdir}/FreeBASIC-${pkgver}-source"
  make install prefix="${pkgdir}/usr"
}
