# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=nlohmann-json
pkgver=3.7.3
# upstream doesn't use annotated tags:
_commit=e7b3b40b5a95bc74b9a7f662830a27c49ffc01b4
pkgrel=3
pkgdesc="JSON for Modern C++"
arch=('any')
url="https://github.com/nlohmann/json"
license=('MIT')
makedepends=('cmake' 'git' 'meson')
source=("${pkgname}::git+https://github.com/nlohmann/json#commit=${_commit}?signed")
md5sums=('SKIP')
validpgpkeys=('797167AE41C0A6D9232E48457F3CEA63AE251B69') # Niels Lohmann <mail@nlohmann.me>

prepare() {
  mv -v "$pkgname" "$pkgname-$pkgver"
}

build() {
  cd "$pkgname-$pkgver"
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DBUILD_TESTING=ON \
        -DCMAKE_INSTALL_LIBDIR=/usr/lib \
        -DJSON_MultipleHeaders=ON \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd "$pkgname-$pkgver"
  make -k check
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="${pkgdir}" install -C build
  install -vDm 644 {CODE_OF_CONDUCT,README}.md \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -vDm 644 LICENSE.MIT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
