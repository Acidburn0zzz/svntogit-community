# $Id$
# Maintainer: Sebastien Luttringer <seblu+arch@seblu.net>

pkgbase=bird
pkgname=('bird' 'bird6')
pkgver=1.3.5
pkgrel=1
arch=('i686' 'x86_64')
url='http://bird.network.cz/'
license=('GPL2')
depends=('readline' 'ncurses')
source=("ftp://bird.network.cz/pub/bird/${pkgname}-${pkgver}.tar.gz"
        'bird.rc'
        'bird.conf'
        'bird.service'
        'bird6.service')
md5sums=('9efc2b1c05fa6298a8df60f5147ad5c1'
         '1a4b98756d64f52333a83eff2e95d233'
         '25241a4d7ef639506e0080431b2c1690'
         'e82a0519dca0b289685fda44787f5911'
         'c32e7f6cf1be6ed6f9aa98feea25176d')

build() {
  # different directory to build seprate version of bird
  cp -a ${pkgbase}-${pkgver} ${pkgbase}6-${pkgver}

  # build classic bird
  cd ${pkgbase}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc  --localstatedir=/var
  make

  # build ipv6 bird
  cd "$srcdir/${pkgbase}6-${pkgver}"
  ./configure --prefix=/usr --sysconfdir=/etc  --localstatedir=/var --enable-ipv6
  make
}

package_bird () {
  pkgdesc='RIP, OSPFv2 and BGP daemon'
  backup=('etc/bird.conf')

  # iniscripts files
  install -D -m 755 bird.rc "${pkgdir}/etc/rc.d/bird"
  install -D -m 644 bird.conf "${pkgdir}/etc/conf.d/bird"
  # systemd file
  install -D -m 644 bird.service "${pkgdir}/lib/systemd/system/bird.service"

  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" sysconfdir="${pkgdir}/etc" localstatedir="${pkgdir}/var" install
  make docdir="${pkgdir}/usr/share/doc/${pkgname}" install-docs

  # no /var inside pkg
  rm -r "$pkgdir/var"
}

package_bird6 () {
  pkgdesc='RIP, OSPFv3, RADV and BGP daemon (ipv6 version)'
  backup=('etc/bird6.conf')

  # iniscripts files
  install -D -m 755 bird.rc "${pkgdir}/etc/rc.d/bird6"
  install -D -m 644 bird.conf "${pkgdir}/etc/conf.d/bird6"
  sed -ri 's/bird/bird6/' "${pkgdir}/etc/rc.d/bird6"
  # systemd config file
  install -D -m 644 bird6.service "${pkgdir}/lib/systemd/system/bird6.service"

  cd ${pkgname}-${pkgver}
  make prefix="${pkgdir}/usr" sysconfdir="${pkgdir}/etc" localstatedir="${pkgdir}/var" install
  make docdir="${pkgdir}/usr/share/doc/${pkgname}" install-docs

  # no /var inside pkg
  rm -r "$pkgdir/var"
}

# vim:set ts=2 sw=2 ft=sh et:
