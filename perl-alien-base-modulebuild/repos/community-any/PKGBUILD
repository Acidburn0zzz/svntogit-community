# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=perl-alien-base-modulebuild
pkgver=1.10
pkgrel=1
pkgdesc="A Module::Build subclass for building Alien:: modules and their libraries"
arch=('any')
license=('PerlArtistic')
url="https://metacpan.org/release/Alien-Base-ModuleBuild"
depends=('perl-alien-build' 'perl-archive-extract' 'perl-capture-tiny' 'perl-file-chdir'
         'perl-html-parser' 'perl-module-build' 'perl-path-tiny' 'perl-shell-config-generate'
         'perl-shell-guess' 'perl-sort-versions' 'perl-uri')
makedepends=('perl-test2-suite')
checkdepends=('perl-acme-alien-dontpanic' 'perl-inline' 'perl-inline-c' 'perl-inline-cpp')
options=('!emptydirs')
source=("https://cpan.metacpan.org/authors/id/P/PL/PLICEASE/Alien-Base-ModuleBuild-$pkgver.tar.gz")
sha512sums=('d9cf0af884020b0797f756494668e05224de8ef9fc9712248670227d7175e94811f3efff101d15ebdbcc32568f742abee8c038458015bf2673c70d0e66efca57')

build() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build test
}

package() {
  cd Alien-Base-ModuleBuild-$pkgver
  perl Build install destdir="$pkgdir"
}
