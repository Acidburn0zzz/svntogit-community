# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=4.7.0
pkgrel=1
pkgdesc='A fancy and practical functional tools'
arch=('x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-pytest-runner' 'python-transaction' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('c71e96d2a8be2de28ccd1a5c771819372f34bc9329f2e31581414d292f7811716f08428212ac1e920fc075d4d3d252cdbd38df975054260f221873daeae8377e')

build() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py build
}

check() {
  cd "$srcdir"/BTrees-$pkgver
  python setup.py pytest
}

package() {
  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
