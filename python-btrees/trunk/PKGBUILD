# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-btrees
pkgver=4.9.1
pkgrel=1
pkgdesc='A fancy and practical functional tools'
arch=('x86_64')
license=('ZPL')
url='https://github.com/zopefoundation/btrees'
depends=('python-persistent' 'python-zope-interface')
makedepends=('python-setuptools')
checkdepends=('python-transaction' 'python-zope-testrunner')
source=("$pkgname-$pkgver.tar.gz::https://github.com/zopefoundation/btrees/archive/$pkgver.tar.gz")
sha512sums=('2fee23277c4d1548912be9ef498ba1b1e3afd064652d8f4a8ef344f1df473f264e4edbbdf691b517e103897a30ccc6eeead1f3cecb30cf99e11ae0afb1654f6e')

build() {
  cd BTrees-$pkgver
  python setup.py build
}

check() {
  cd BTrees-$pkgver
  PYTHONPATH="$PWD/build/lib.linux-$CARCH-3.9" python -m zope.testrunner --test-path=src
}

package() {
  cd BTrees-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}

# vim:set ts=2 sw=2 et:
