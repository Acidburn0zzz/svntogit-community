# Maintainer: David Runge <dvzrv@archlinux.org>

_name=svglib
pkgname=python-svglib
pkgver=1.2.0
pkgrel=1
pkgdesc="Read SVG files and convert them to other formats"
arch=(any)
url="https://github.com/deeplook/svglib"
license=(LGPL3)
depends=(python-lxml python-reportlab python-tinycss2 python-cssselect2)
makedepends=(python-setuptools)
checkdepends=(python-pillow python-pytest)
source=("https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz")
sha512sums=('09fef882c04dc5c4bb6401f554ba568e2f12c430c5f066dbad72f26aed0144c0d56c107883afa987fdbeaec77d8ac4e6206d2d27cbc4237dbd0309e56af5b35f')
b2sums=('49a090c9999deeb725ea81b92d397de1e55e11733a14fa1e4baefa42cd91c624511456f22c87c4cc955c6784a6cb43aca2c5595cd01d7b4707c43dcf0a0ede63')

build() {
  cd "${_name}-$pkgver"
  python setup.py build
}

check() {
  cd "${_name}-$pkgver"
  export PYTHONPATH="build:${PYTHONPATH}"
  # disable tests that download stuff and that are broken
  pytest -v -k "not TestWikipediaSymbols and not TestWikipediaFlags and not test_png_in_svg and not test_external_svg_in_svg"
}

package() {
  cd "${_name}-$pkgver"
  python setup.py install --optimize=1 --root="${pkgdir}"
  install -vDm 644 {CHANGELOG,CONTRIBUTORS,README}.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}
