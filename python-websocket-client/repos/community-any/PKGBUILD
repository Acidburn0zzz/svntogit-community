# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Thore BÃ¶decker <foxxx0@archlinux.org>
# Contributor: Matmas <matmas@matmas.net>
# Contributor: Thomas Gubler <thomasgubler@gmail.com>
# Contributor: Daniel YC Lin <dlin.tw at gmail>
# Contributor: Florian Bruhin <archlinux.org@the-compiler.org>

_pkgbase='websocket-client'
pkgname=python-$_pkgbase
pkgver=1.2.3
pkgrel=1
pkgdesc="WebSocket client library for Python"
arch=('any')
url="https://github.com/$_pkgbase/$_pkgbase"
license=('LGPL')
depends=('python-six')
makedepends=('python-setuptools')
checkdepends=('python-pysocks')
source=("https://pypi.io/packages/source/w/$_pkgbase/$_pkgbase-$pkgver.tar.gz")
sha512sums=('5e5e00d43b7b4da5e2cb0d00d1e743e420791856e51e2b3d28d78219edcd7a2eace834ae4d7ee2faf89ab27c2b65f063e508f0e470a47ff9d88c36748b3dd113')

prepare() {
  # Not sure why the conditions are not working :/
  sed -i "s/'backports.ssl_match_hostname'//" $_pkgbase-$pkgver/setup.py
}

build() {
  cd $_pkgbase-$pkgver
  python setup.py build
}

# check() {
#   cd $_pkgbase-$pkgver
#   python setup.py test
# }

package() {
  cd $_pkgbase-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname/
}

# vim:set ts=2 sw=2 ft=sh et:
