# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=golang-github-linuxdeepin-go-x11-client
pkgver=0.3.0
pkgrel=1
pkgdesc='X11 protocol go language binding'
arch=('any')
url="https://github.com/linuxdeepin/go-x11-client"
license=('GPL')
depends=('go-pie' 'golang-deepin-lib' 'golang-github-stretchr-testify' 'golang-golang-x-text'
         'golang-gopkg-check.v1')
checkdepends=('xorg-server-xvfb')
source=("$pkgname-$pkgver.tar.gz::https://github.com/linuxdeepin/go-x11-client/archive/$pkgver.tar.gz")
sha512sums=('35adbcbf9e152aefd16a9daf596f034046986a4cab2eb42712951e700d269fb0f9bc59218b38f9f1f4dce9a9628ff2a7bb29e516dba76d40bb29da061e3a0191')

check() {
  export GOPATH="$srcdir/build:/usr/share/gocode"
  mkdir -p "$srcdir"/build/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-x11-client-$pkgver "$srcdir"/build/src/github.com/linuxdeepin/go-x11-client
  cd "$srcdir"/build/src/github.com/linuxdeepin/go-x11-client
  xvfb-run go test -v $(go list ./...)
}

package() {
  mkdir -p "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin
  cp -a "$srcdir"/go-x11-client-$pkgver "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-x11-client

  rm -r "$pkgdir"/usr/share/gocode/src/github.com/linuxdeepin/go-x11-client/debian
}
