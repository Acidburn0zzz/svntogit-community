# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Martin C. Doege <mdoege at compuserve dot com>
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>

pkgname=qsampler
pkgver=0.6.2
pkgrel=1
pkgdesc="A LinuxSampler Qt GUI Interface"
arch=('x86_64')
url="https://qsampler.sourceforge.io/"
license=('GPL2')
groups=('pro-audio')
depends=('gcc-libs' 'glibc' 'hicolor-icon-theme' 'linuxsampler' 'qt5-base')
makedepends=('libgig' 'liblscp' 'qt5-tools')
source=("https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('a2760c27627adf17b9e4a624f1e768b1fdf1b45781becd5cb11b65fee11bc109b4ad5167803ebbb50f4ffd9c220fa748c8527e9ea101071c5319dd4f8fa0ee01')

prepare() {
  cd "${pkgname}-${pkgver}"
  autoreconf -vfi
}

build() {
  cd "${pkgname}-${pkgver}"
  ./configure --prefix=/usr
  make
}

package() {
  depends+=('libgig.so' 'liblscp.so')
  cd "${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -vDm 644 {AUTHORS,ChangeLog,README,TODO} \
    -t "${pkgdir}/usr/share/doc/${pkgname}/"
}
# vim:set ts=2 sw=2 et:
