# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=0.4.0
pkgrel=1
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/so-fancy/diff-so-fancy"
license=('MIT')
install="${pkgname}.install"
depends=('git')
checkdepends=('bash-bats')
source=("git+https://github.com/so-fancy/diff-so-fancy.git#tag=v$pkgver"
        "git+https://github.com/ztombol/bats-core.git"
        "git+https://github.com/ztombol/bats-assert.git"
        system-diff_highlight.patch)
sha384sums=('SKIP'
            'SKIP'
            'SKIP'
            '9ec860b54a91f4852d9426e7e471de918132c29e846772cc3d65b33ff5be78b95875ba994f96882e0ce9f5c6dccac998')

prepare() {
  cd "${srcdir}/${pkgname}"
  git config submodule."test/test_helper/bats-core".url "${srcdir}"/bats-core
  git config submodule."test/test_helper/bats-assert".url "${srcdir}"/bats-assert
  git submodule update --init

  patch -p1 -i ../system-diff_highlight.patch
}

check() {
  cd "${srcdir}/${pkgname}"
  bats test/*.bats
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 'diff-so-fancy' "${pkgdir}/usr/bin/diff-so-fancy"
}
