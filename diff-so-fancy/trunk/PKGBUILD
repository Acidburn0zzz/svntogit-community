# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: icasdri <icasdri at gmail dot com>

pkgname=diff-so-fancy
pkgver=0.3.0
pkgrel=1
pkgdesc="Good-looking diffs with diff-highlight and more"
arch=('any')
url="https://github.com/stevemao/diff-so-fancy"
license=('MIT')
install="${pkgname}.install"
depends=('bash')
makedepends=('git')
checkdepends=('bash-bats')
source=("git+https://github.com/so-fancy/diff-so-fancy.git#tag=v$pkgver"
        "git+https://github.com/ztombol/bats-core.git"
        "git+https://github.com/ztombol/bats-assert.git")
sha384sums=('SKIP'
            'SKIP'
            'SKIP')

prepare() {
  cd "${srcdir}/${pkgname}"
  git config submodule."test/test_helper/bats-core".url "${srcdir}"/bats-core
  git config submodule."test/test_helper/bats-assert".url "${srcdir}"/bats-assert
  git submodule update --init
}

check() {
  cd "${srcdir}/${pkgname}"
  bats test/*.bats
}

package() {
  cd "${srcdir}/${pkgname}"
  install -Dm755 'diff-so-fancy' "${pkgdir}/usr/bin/diff-so-fancy"
  install -Dm755 'third_party/diff-highlight/diff-highlight' "${pkgdir}/usr/bin/diff-highlight"
}
