# Maintainer: Chih-Hsuan Yen <yan12125@archlinux.org>

pkgname=python-flatbuffers
pkgver=1.10.0
pkgrel=1
pkgdesc='Memory Efficient Serialization Library'
arch=(any)
url='https://google.github.io/flatbuffers/'
license=(Apache)
makedepends=(python-setuptools)
depends=(python)
checkdepends=(bash flatbuffers)
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/google/flatbuffers/archive/v$pkgver.tar.gz")
sha256sums=('3714e3db8c51e43028e10ad7adffb9a36fc4aa5b1a363c2d0c4303dd1be59a7c')

prepare() {
  cd flatbuffers-$pkgver
  sed -i 's#${test_dir}/../flatc#flatc#' tests/PythonTest.sh
}

build() {
  cd flatbuffers-$pkgver/python
  VERSION=$pkgver python setup.py build
}

check() {
  cd flatbuffers-$pkgver
  ./tests/PythonTest.sh
}

package() {
  cd flatbuffers-$pkgver/python
  VERSION=$pkgver python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}
