
pkgname=celt051
pkgver=0.5.1.3
pkgrel=1
pkgdesc="Low-latency audio communication codec - SPICE version"
arch=(i686 x86_64)
url="http://www.celt-codec.org"
license=('BSD')
depends=('libogg')
makedepends=('rpmextract')
source=(ftp://ftp.redhat.com/pub/redhat/linux/enterprise/5Client/en/os/SRPMS/$pkgname-$pkgver-0.el5.src.rpm)

prepare() {
  cd $srcdir
  rpmextract.sh ${pkgname}-$pkgver-0.el5.src.rpm
  tar xzf celt-$pkgver.tar.gz
  sed -i \
    -e 's|^hardcode_libdir_flag_spec=.*|hardcode_libdir_flag_spec=""|g' \
    -e 's|^runpath_var=LD_RUN_PATH|runpath_var=DIE_RPATH_DIE|g' celt-$pkgver/libtool
}

build() {
  cd "$srcdir/celt-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/celt-$pkgver"
  make DESTDIR="$pkgdir" install
}
