# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Maintainer: hydro <hydro@freenet.de>

pkgname=mediainfo-gui
pkgver=18.03.1
pkgrel=1
pkgdesc='GUI for mediainfo'
arch=('x86_64')
url='http://mediaarea.net'
license=('GPL')
depends=('libmediainfo' 'wxgtk3')
makedepends=('imagemagick')
source=("https://mediaarea.net/download/source/mediainfo/${pkgver}/mediainfo_${pkgver}.tar.bz2")
sha256sums=('c74c265461ae2072296546afa3efd1e331cfa7a5744716d700f8bde873fb712e')

build() {
  cd MediaInfo/Project/GNU/GUI

  ./autogen.sh
  ./configure \
    --prefix='/usr' \
    --with-wx-config='/usr/bin/wx-config-gtk3'
  make
}

package() {
  cd MediaInfo/Project/GNU/GUI

  make DESTDIR="${pkgdir}" install

  _iconsize=`identify "${srcdir}"/MediaInfo/Source/Resource/Image/MediaInfo.png | grep -oP ' \d+x\d+ ' | tr -d ' '`
  install -Dm 644 "${srcdir}"/MediaInfo/Source/Resource/Image/MediaInfo.png "${pkgdir}"/usr/share/icons/hicolor/${_iconsize}/apps/mediainfo.png
  install -m 644 "${srcdir}"/MediaInfo/Source/Resource/Image/MediaInfo.{svg,ico,xpm} "${pkgdir}"/usr/share/icons/
  install -Dm 644 "${srcdir}"/MediaInfo/Source/Resource/Image/MediaInfo.png "${pkgdir}"/usr/share/pixmaps/mediainfo-gui.png
  install -Dm 644 "${srcdir}"/MediaInfo/Project/GNU/GUI/mediainfo-gui.desktop "${pkgdir}"/usr/share/applications/mediainfo-gui.desktop
}

# vim: ts=2 sw=2 et:
