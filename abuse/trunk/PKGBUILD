# Maintainer: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: jlvsimoes <jlvsimoes@oninet.pt>
# Contributor: kevin <kevin@archlinux.org>

pkgname=abuse
pkgver=0.9.0
pkgrel=1
pkgdesc='Side-scroller action game that pits you against ruthless alien killers'
arch=(x86_64)
url='http://abuse.zoy.org/'
license=(custom GPL)
depends=(libglvnd sdl2_mixer)
makedepends=(cmake gendesk)
source=(https://github.com/Xenoveritas/abuse/archive/v$pkgver/$pkgname-$pkgver.tar.gz)
b2sums=('3896f5b986120e0d0385daf194e7ac158a77bea07aa5b508b9beb8ee8f9423b375a0daebd8e66124719b80a57cabb101d0258176d16d63cab030c8332ca2dfcc')

prepare() {
  gendesk -f -n --pkgname=$pkgname --pkgdesc="$pkgdesc"
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_PREFIX=/usr
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  # Desktop shortcut and icon
  install -Dm644 $pkgname.desktop \
    "$pkgdir/usr/share/applications/$pkgname.desktop"
  install -Dm644 $pkgname-$pkgver/doc/$pkgname.png \
    "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # License (for the original source code)
  install -d "$pkgdir/usr/share/licenses/abuse"
  sed -n '1,/^$/p' $pkgname-$pkgver/src/view.cpp > "$pkgdir/usr/share/licenses/abuse/original"
}
