# Maintainer: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=cpanminus
pkgver=1.5002
pkgrel=1
pkgdesc="Get, unpack, build and install modules from CPAN."
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl-extutils-install>=1.46' 'perl-extutils-makemaker>=6.31'
         'perl-module-build>=0.36')
conflicts=('perl-app-cpanminus')
provides=("perl-app-cpanminus=${pkgver}")
url=https://metacpan.org/release/App-cpanminus
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/App-cpanminus-${pkgver}.tar.gz")
md5sums=(8382fd84fceaf151f9b05bd10724815e)
sha512sums=(908c86f852934baa2395f85a83ef46231ad0a0e266a5fff1d42a6a2b30a6037b29ad2a7cc90ec7ff2418067ddff271d2b5dd9e0c88f5721fbb28c763330a0a67)
_distdir="${srcdir}/App-cpanminus-${pkgver}"

build() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
  )
}

check() {
  ( export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
  )
}

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
