# Maintainer: Justin Davis (juster) <jrcd83@gmail.com>
# $Id$

pkgname=cpanminus
pkgver=1.5013
pkgrel=1
pkgdesc='Get, unpack, build and install modules from CPAN.'
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=(perl)
provides=("perl-app-cpanminus=$pkgver")
url=https://metacpan.org/release/App-cpanminus
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/App-cpanminus-$pkgver.tar.gz")
md5sums=(156e69fbf3734814c0bd2e44fa706849)
sha512sums=(bcb9fb541d5cf4e79b3dc311e55407f6d392c697d5ffc211dcaad7368b595fe361d2a818679cc0039e20cdc756e5e2da87384e3470582e7def28cad49fa11190)
_dir="$srcdir/App-cpanminus-$pkgver"

build()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL=--skipdeps
  unset PERL5LIB PERL_MM_OPT
  /usr/bin/perl Makefile.PL
  make
)

check()
(
  cd "$_dir"
  export PERL_MM_USE_DEFAULT=1
  unset PERL5LIB
  make test
)

package()
(
  cd "$_dir"
  make install INSTALLDIRS=vendor DESTDIR="$pkgdir"
  find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
)

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
