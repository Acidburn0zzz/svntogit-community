# $Id$
# Maintainer: 

pkgname=cpanminus
pkgver=1.6936
pkgrel=1
pkgdesc='Get, unpack, build and install modules from CPAN'
arch=('any')
url="https://metacpan.org/release/App-cpanminus"
license=('PerlArtistic' 'GPL')
depends=('perl')
options=('!emptydirs')
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/App-cpanminus-$pkgver.tar.gz")
md5sums=('cc3495f1797eb762c480c560419f6a1f')

build() {
  cd App-cpanminus-$pkgver
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd App-cpanminus-$pkgver
  make test
}

package() {
  cd App-cpanminus-$pkgver
  make DESTDIR="$pkgdir" install
}
