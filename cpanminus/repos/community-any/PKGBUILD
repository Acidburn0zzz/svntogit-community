# Packager: Justin "juster" Davis <jrcd83@gmail.com>
# $Id$

pkgname=cpanminus
pkgver=1.5007
pkgrel=1
pkgdesc="Get, unpack, build and install modules from CPAN."
arch=(any)
license=(PerlArtistic GPL)
options=(!emptydirs)
depends=('perl')
provides=("perl-app-cpanminus=${pkgver}")
url=https://metacpan.org/release/App-cpanminus
source=("http://search.cpan.org/CPAN/authors/id/M/MI/MIYAGAWA/App-cpanminus-${pkgver}.tar.gz")
md5sums=(5cc199013efa1e3e8cacf20ccb322736)
sha512sums=(349fbf0210c0832b5f24be9b259e39879e96c0f6bf88e78ec8ba9715a97efd698697593a9195ea990f5fd37e15abeb68996fe6ea3f95de19dc69773f41980dd6)
_distdir="${srcdir}/App-cpanminus-${pkgver}"

build()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""                 \
      PERL_AUTOINSTALL=--skipdeps                            \
      PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'"     \
      PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
      MODULEBUILDRC=/dev/null

    cd "$_distdir"
    /usr/bin/perl Makefile.PL
    make
)

check()
(
    export PERL_MM_USE_DEFAULT=1 PERL5LIB=""
    cd "$_distdir"
    make test
)

package() {
    cd "$_distdir"
    make DESTDIR="$pkgdir" install
    find "$pkgdir" -name .packlist -o -name perllocal.pod -delete
}

# Local Variables:
# mode: shell-script
# sh-basic-offset: 2
# End:
# vim:set ts=2 sw=2 et:
