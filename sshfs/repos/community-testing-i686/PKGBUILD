# $Id$
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=sshfs
pkgver=3.1.0
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=(x86_64 i686)
url="http://github.com/libfuse/sshfs"
license=(GPL)
depends=(fuse3 glib2 openssh)
makedepends=(meson)
source=(https://github.com/libfuse/sshfs/releases/download/sshfs-$pkgver/sshfs-$pkgver.tar.gz{,.asc})
sha256sums=('c4f439ef0b8122e126f3ab9c0c84cc1665a58777e0f10806fa68f241bc6d3874'
            'SKIP')
validpgpkeys=('ED31791B2C5C1613AF388B8AD113FCAC3C4E599F') # Nikolaus Rath <Nikolaus@rath.org>

build() {
  cd $pkgname-$pkgver

  [[ $CARCH == 'i686' ]] && export CFLAGS+=" -D_FILE_OFFSET_BITS=64"
  rm -rf build
  mkdir build
  cd build
  meson --prefix=/usr ..
  ninja
}

package() {
  cd $pkgname-$pkgver/build

  DESTDIR="$pkgdir" ninja install
}
