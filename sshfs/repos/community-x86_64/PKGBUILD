# $Id$
# Maintainer: Giovanni Scafora <giovanni@archlinux.org>

pkgname=sshfs
pkgver=2.6
_tag=sshfs_${pkgver/./_}
pkgrel=1
pkgdesc="FUSE client based on the SSH File Transfer Protocol"
arch=('i686' 'x86_64')
url="http://github.com/libfuse/sshfs"
license=('GPL')
depends=('fuse' 'glib2' 'openssh')
makedepends=('pkg-config')
source=(https://github.com/libfuse/sshfs/archive/${_tag}.zip)
md5sums=('b5bf2b1205ae235905f8a20e5dbc2d5f')

build() {
  cd sshfs-$_tag

  autoreconf -i
  ./configure --prefix=/usr
  make
}

package() {
  cd sshfs-${_tag}

  make DESTDIR="${pkgdir}" install
}
