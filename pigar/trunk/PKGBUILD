# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=pigar
pkgver=0.9.1
pkgrel=2
pkgdesc="A fantastic tool to generate requirements for your Python project, and more than that"
arch=('any')
url="https://github.com/Damnever/pigar"
license=('BSD')
depends=('python-setuptools' 'python-colorama' 'python-requests')
source=("$pkgname-$pkgver.tar.gz::https://github.com/Damnever/pigar/archive/v$pkgver.tar.gz")
sha512sums=('d45f8a896d95259030cc079b6466b39bbc747339672cd852ffd72eb32059dca0054b9bc68415b3cb52dfad59521ce8ffdc7b83963556401bb5fb1fb283d6d677')

build() {
  cd pigar-$pkgver
  python setup.py build
}

check() {
  cd pigar-$pkgver
  python -m unittest discover pigar/tests/ -t . -v
}

package() {
  cd pigar-$pkgver
  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
