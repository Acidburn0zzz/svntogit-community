# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Thomas Mudrunka <harvie@@email..cz>
# Maintainer: Thomas Mudrunka <harvie@@email..cz>

pkgname=mcelog
pkgver=1.0pre3
pkgrel=5
pkgdesc="Print machine check event log from x86-64 kernel"
url="http://freshmeat.net/projects/mcelog/"
license=("GPL")
arch=('i686' 'x86_64')
options=('docs')
backup=('etc/mcelog/mcelog.conf')
#source=("ftp://ftp.kernel.org/pub/linux/utils/cpu/mce/${pkgname}-${pkgver}.tar.gz"
source=("ftp://ftp.archlinux.org/other/community/${pkgname}/${pkgname}-${pkgver}.tar.gz"
	"${pkgname}.rc"
	"mcelog.service")
md5sums=('b42f2214de6f4feb992556149edc67fa'
         'f039a77d7bde3518d3d695b144816e8a'
         'a8859012781f017624bbfe61c9d700bb')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p $pkgdir/usr/share/doc/mcelog
  make install prefix=$pkgdir/usr etcprefix=$pkgdir DOCDIR=$pkgdir/usr/share/doc/mcelog
  install -Dm0755 mcelog.cron "${pkgdir}/usr/sbin/mcelog.cron"
  install -Dm0755 "${srcdir}/mcelog.rc" "${pkgdir}/etc/rc.d/mcelog"
  install -Dm0644 mcelog.logrotate "${pkgdir}/etc/logrotate.d/mcelog.logrotate"
  install -Dm0644 README "${pkgdir}/usr/share/doc/mcelog/README"
  install -Dm0644 $srcdir/$pkgname.service $pkgdir/usr/lib/systemd/system/$pkgname.service
}
